*          DATA SET SPBUYUPL   AT LEVEL 044 AS OF 11/19/19                      
*CATALP SPBUYUPL                                                                
         TITLE 'SPBUYUPL - SPOTPAK BUY UPLOAD OVERLAY'                          
SPBUYUPL CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 WORKL,SPBUYUPL,R9,R7,RR=R8,CLEAR=YES                             
         ST    R8,RELO                                                          
         LR    R8,RC                                                            
         USING WORKD,R8                                                         
         L     RC,0(R1)                                                         
         USING SPBUYWKD,RC                                                      
         L     RA,VBUYSAVE                                                      
         USING BUYSAVE,RA                                                       
         L     R3,VBUYTWA                                                       
         USING T211FFD,R3                                                       
*                                                                               
         CLI   UPMODE,UPMINIT                                                   
         BE    INIT                                                             
         CLI   UPMODE,UPMNEXT                                                   
         BE    NEXT                                                             
         CLI   UPMODE,UPMNSTA                                                   
         BE    NEXT8                                                            
         CLI   UPMODE,UPMERR                                                    
         BE    ERR                                                              
         B     EXIT                                                             
*                                                                               
EQXIT    CR    RB,RB                                                            
         J     EXIT                                                             
*                                                                               
NEQXIT   EQU   *                                                                
NEXIT    LTR   RB,RB                                                            
*                                                                               
EXIT     XIT1  ,                                                                
         EJECT                                                                  
***********************************************************************         
* INITIALIZE                                                          *         
***********************************************************************         
         SPACE 1                                                                
INIT     XC    DMCB(24),DMCB       LOAD THE BASE SCREEN                         
         MVC   DMCB+4(4),=X'D90211FF'                                           
         GOTO1 VCALLOV,DMCB,64(R3)                                              
         CLI   4(R1),X'FF'                                                      
         BNE   *+6                                                              
         DC    H'0'                                                             
         BAS   RE,CLEAR            CLEAR ALL UNPROTECTED FIELDS                 
*                                                                               
         MVI   MISCFLG1,0          CLEAR MISCELLANEOUS FLAG 1                   
         MVI   UPCURTWA,3          READ TWA3 INTO TIA                           
         BAS   RE,READTWA                                                       
         L     R4,VTIA             R4=A(TIA)                                    
         ST    R4,UPACUROB         SET A(CURRENT OBJECT)                        
         XC    UPANXTBY,UPANXTBY                                                
         XC    UPSVSTA,UPSVSTA                                                  
         CLC   0(2,R4),=X'FFFF'                                                 
         BNE   *+6                                                              
         DC    H'0'                                                             
         SR    R5,R5                                                            
         ICM   R5,3,0(R4)                                                       
         BNZ   *+6                                                              
         DC    H'0'                                                             
         CLC   10(4,R4),=C'HDR*'   FIRST OBJECT MUST BE HDR                     
         BE    *+6                                                              
         DC    H'0'                                                             
         OI    UPSW,UPHDR                                                       
         USING SHDRD,R4            FILL IN HEADER FIELDS                        
*** SBY2                                                                        
         CLC   SHDRUTYP,=C'BY2'    IS THIS A '20 DEMO' UPLOAD                   
         BNE   *+8                                                              
         OI    MISCFLG1,MF1SBY2     - YUP IT IS, TURN ON THE FLAG               
*** SBY2                                     MHC  01/17/03                      
         LA    R2,BUYMDH           MEDIA                                        
         LA    R5,SHDRMED                                                       
         LA    R1,L'SHDRMED                                                     
         BAS   RE,SETIN                                                         
         LA    R2,BUYCLH           CLIENT                                       
         LA    R5,SHDRCLT                                                       
         LA    R1,L'SHDRCLT                                                     
         BAS   RE,SETIN                                                         
         LA    R2,BUYPRH           PRODUCT                                      
         LA    R5,SHDRPRD                                                       
         LA    R1,L'SHDRPRD                                                     
         BAS   RE,SETIN                                                         
         LA    R2,BUYESH           ESTIMATE                                     
         LA    R5,SHDREST                                                       
         LA    R1,L'SHDREST                                                     
         BAS   RE,SETIN                                                         
*****                                                                           
         LA    R2,BUYOPH           PURPOSE CODE                                 
*** SBY2                                                                        
         TM    MISCFLG1,MF1SBY2    ARE WE DOING SBY2 (20 DEMOS)?                
         BO    INITBY2A             - YUP, WE ARE                               
         LA    R5,SHDRPURP                                                      
         LA    R1,L'SHDRPURP                                                    
         B     INITSET1                                                         
*                                                                               
INITBY2A DS    0H                  THIS IS SBY2 (20 DEMOS)                      
         LA    R5,SHD2PURP                                                      
         LA    R1,L'SHD2PURP                                                    
*** SBY2                                 MHC  01/17/03                          
*                                                                               
INITSET1 BAS   RE,SETIN                                                         
*****                                                                           
*                                                                               
         LA    R2,BUYBUH           BUYER                                        
*                                                                               
         TM    MISCFLG1,MF1SBY2    ARE WE DOING SBY2 (20 DEMOS)?                
         BO    INITBY2B             - YUP, WE ARE                               
         LA    R5,SHDRBID          (PUT '=' IN FRONT OF FIELD)                  
         LA    R1,L'SHDRBID-1                                                   
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   WORK+1(0),SHDRBID                                                
         MVI   WORK,C'='                                                        
         LA    R5,WORK                                                          
         LA    R1,L'SHDRBID+1                                                   
         B     INITSET2                                                         
*                                                                               
INITBY2B DS    0H                  THIS IS SBY2 (20 DEMOS)                      
         LA    R5,SHD2BID          (PUT '=' IN FRONT OF FIELD)                  
         LA    R1,L'SHD2BID-1                                                   
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   WORK+1(0),SHD2BID                                                
         MVI   WORK,C'='                                                        
         LA    R5,WORK                                                          
         LA    R1,L'SHD2BID+1                                                   
*                                                                               
INITSET2 BAS   RE,SETIN                                                         
*                                                                               
*** SBY2                                                                        
         TM    MISCFLG1,MF1SBY2    ARE WE DOING SBY2 (20 DEMOS)?                
         BO    INITPRE1             - YUP, WE ARE                               
         CLI   SHDRNOSP,C'Y'       C'Y' - NOELEMS NOT AN ERROR                  
         B     *+8                                                              
INITPRE1 CLI   SHD2NOSP,C'Y'       NOTICE  SHD2  NOT  SHDR  LIKE ABOVE          
*** SBY2                              STORAGE CHANGED  01/17/03                 
         BNE   *+8                                                              
         OI    UPSW,UPNOERR                                                     
         MVC   UPSTART(12),SPACES    CLEAR START/END                            
         CLC   SHDRSTPD(12),SPACES   TEST DATES ARE GIVEN                       
         BNH   INIT1                                                            
*&&DO                                                                           
*=============================================================*                 
* PATCH FOR Y2K JWT BUG                                       *                 
* SINCE DATE CAN NEVER END IN A SPACE                         *                 
*=============================================================*                 
         SPACE 1                                                                
* CONVERT DATES TO Y2K FORMAT                                                   
         LA    R1,SHDRSTPD                                                      
         CLI   5(R1),C' '                                                       
         BNE   JWTX                                                             
                                                                                
         LA    R1,SHDRSTPD+4                                                    
*                                                                               
JWT2     MVC   1(1,R1),0(R1)                                                    
         BCTR  R1,0                                                             
         BCT   R0,JWT2                                                          
         MVI   1(R1),C'0'                                                       
*                                                                               
JWTX     DS    0H                                                               
*&&                                                                             
*=============================================================*                 
* MAKE SURE VALID NUMERIC PASSED AS DATE                      *                 
*=============================================================*                 
         LA    R1,SHDRSTPD                                                      
         LHI   R0,12               TEST SHDRSTPD & SHDRENDP                     
INITA    CLI   0(R1),C'0'                                                       
         BL    INITB                                                            
         CLI   0(R1),C'9'                                                       
         BH    INITB                                                            
         AHI   R1,1                                                             
         BCT   R0,INITA                                                         
         B     INITC                                                            
*                                                                               
INITB    MVI   ERRCD,PERERR                                                     
         B     ERR                                                              
*                                                                               
INITC    GOTO1 VDATCON,DMCB,SHDRSTPD,(3,DUB)                                    
         GOTO1 VDATCON,DMCB,(3,DUB),SHDRSTPD                                    
         GOTO1 VDATCON,DMCB,SHDRENPD,(3,DUB)                                    
         GOTO1 VDATCON,DMCB,(3,DUB),SHDRENPD                                    
         MVC   UPSTART(12),SHDRSTPD  SAVE START/END                             
*                                                                               
         CLC   UPSTART,UPEND       YES-CHECK START NOT AFTER END                
         BH    ESTART                                                           
         GOTO1 VGETDAY,DMCB,UPSTART,FULL   GET DAY OF WEEK OF START             
         CLC   FULL(3),SPACES                                                   
         BE    ESTART                                                           
         MVC   PERSTDAY,0(R1)                                                   
******   MVC   UPSTDAY,0(R1)                                                    
         GOTO1 (RF),(R1),UPEND,FULL   VALIDATE PERIOD END                       
         CLC   FULL(3),SPACES                                                   
         BE    EEND                                                             
*                                                                               
INIT1    MVC   UPPRD2,SHDRPRD2     SAVE PIGGYBACK                               
         CLC   SHDRPRD2,SPACES     IF PIGGYBACK GIVEN,                          
         BNH   *+14                                                             
         CLC   SHDRPRD,=C'POL'     PRD=POL IS INVALID                           
         BE    EPRD2                                                            
*                                                                               
         CLC   SHDRDEMO(7),SPACES  VALIDATE DEMOS                               
         BNH   EDEMMIS                                                          
*********                                                                       
         XC    KEY,KEY             READ AGENCY RECORD TO GET PROFILE            
         MVI   KEY,6                 WE NEED THIS FOR CANADA                    
         MVC   KEY+1(2),AGYALPHA                                                
         GOTO1 HIGH                                                             
         CLC   KEY(13),KEYSAVE                                                  
         BE    *+6                                                              
         DC    H'0'                                                             
         L     R6,AREC2                                                         
         USING AGYHDR,R6                                                        
         ST    R6,AREC                                                          
         GOTO1 GETREC                                                           
         MVC   SVAPROF,AGYPROF                                                  
         MVC   SVAFLAG1,AGYFLAG1   SAVE FLAG BITS                               
         MVC   SVAFLAG2,AGYFLAG2                                                
         DROP  R6                                                               
*********                                                                       
         TM    MISCFLG1,MF1SBY2    ARE WE DOING SBY2 (20 DEMOS)?                
         BO    INIT1A               - YUP, WE ARE                               
         LA    R0,14                                                            
         LA    R1,SHDRDEMO                                                      
         LA    RE,WORK3+8          BUILD DUMMY TWA FIELD CONTAINING             
         XC    WORK3,WORK3         DEMOS FOR DEMOVAL                            
         B     INIT2                                                            
*                                                                               
INIT1A   DS    0H                  THIS IS FOR SBY2 (20 DEMOS)                  
         LA    R0,20                                                            
         LA    R1,SHD2DEMO                                                      
         LA    RE,WORK3+8          BUILD DUMMY TWA FIELD CONTAINING             
         XC    WORK3,WORK3         DEMOS FOR DEMOVAL                            
*                                                                               
INIT2    CLC   0(7,R1),SPACES                                                   
         BNH   INIT4                                                            
*                                                                               
         CLI   0(R1),C'X'          NON-TRAD DEMO?                               
         JE    INITX                                                            
         CLI   1(R1),C'X'                                                       
         JE    INITX               THEN DON'T CALL DEMOVAL --> DEATH            
*                                                                               
         MVC   0(7,RE),0(R1)                                                    
         LA    RE,6(RE)                                                         
         CLI   0(RE),C' '                                                       
         BH    *+8                                                              
         BCT   RE,*-8                                                           
         MVI   1(RE),C','                                                       
         LA    RE,2(RE)                                                         
         LA    R1,7(R1)                                                         
         BCT   R0,INIT2                                                         
*                                                                               
INIT4    BCTR  RE,0                                                             
         MVI   0(RE),C' '                                                       
         LA    RF,WORK3+8                                                       
         SR    RE,RF                                                            
         STC   RE,WORK3+5                                                       
         LA    RE,8(RE)                                                         
         STC   RE,WORK3                                                         
         XC    DBLOCK,DBLOCK                                                    
         MVC   DBFILE,=C'TP '                                                   
         MVI   DBSELMED,C'T'       ASSUMES NOT CANADA FIRST                     
         MVI   DBSELSRC,C'N'                                                    
         CLI   SVAPROF+7,C'C'      IS AGENCY CANADA?                            
         BNE   INIT5                                                            
         MVI   DBSELMED,C'C'       YES, IT IS                                   
*                                                                               
INIT5    MVC   DBCOMFCS,VCOMFACS                                                
*   LOCATION OF UPDEMS CHANGED, NEED THE FOLLOWING CODE FOR ADDRESS             
         LHI   R5,UPDEMS-BUYSAVE                                                
         AR    R5,RA                                                            
         XC    0(L'UPDEMS,R5),0(R5)   CLEAR UPDEMS                              
*                                        MHC  04/09/03                          
*        XC    UPDEMS,UPDEMS                                                    
*                                                                               
         TM    MISCFLG1,MF1SBY2    ARE WE DOING SBY2 (20 DEMOS)?                
         BO    INIT5A               - YUP, WE ARE                               
         GOTO1 VDEMOVAL,DMCB,(1,WORK3),(14,0(R5)),(C'S',DBLOCK),0               
         B     INIT5B                                                           
*                                                                               
INIT5A   DS    0H                  THIS IS FOR SBY2 (20 DEMOS)                  
         GOTO1 VDEMOVAL,DMCB,(1,WORK3),(20,0(R5)),(C'S',DBLOCK),0               
*                                                                               
INIT5B   SR    RE,RE                                                            
         ICM   RE,1,4(R1)                                                       
         BNZ   INIT6                                                            
         ZIC   R5,0(R1)            R5=FIELD NUMBER IN ERROR                     
         B     EDEM                                                             
*                                                                               
INIT6    LHI   R5,UPDEMS-BUYSAVE                                                
         AR    R5,RA                                                            
         LR    R1,R5                                                            
*NIT6    LA    R1,UPDEMS           REPLACE END-OF-LIST WITH 3X'00'              
         CLI   0(R1),X'FF'                                                      
         BE    *+12                                                             
         LA    R1,3(R1)                                                         
         BCT   RE,*-12                                                          
         XC    0(3,R1),0(R1)                                                    
*                                                                               
INITX    B     EQXIT                                                            
         EJECT                                                                  
***********************************************************************         
* GET NEXT OBJECT                                                     *         
***********************************************************************         
         SPACE 1                                                                
NEXT     DS    0H                                                               
         L     R4,UPACUROB                                                      
         CLC   10(4,R4),=C'HDR*'   TEST CURRENT OBJECT IS THE HDR               
         BNE   *+12                                                             
         BAS   RE,SETDATES         YES-SET THE REQUEST DATES                    
         B     NEXT0                                                            
*&&DO                                                                           
         CLC   10(4,R4),=C'OPT*'   TEST CURRENT OBJECT IS OPTIONS               
         BNE   NEXTA10                                                          
         LA    R2,BUYOPH           BUY OPTIONS FIELD                            
         MVI   4(R2),0             CLEAR INPUT INDICATORS                       
         OI    6(R2),X'80'         TRANSMIT                                     
*                                                                               
         XC    BUYOP,BUYOP         PUT OPTION INTO THE FIELD                    
         LA    R2,BUYOPH                                                        
         USING SOPTD,R4                                                         
         LA    R5,SOPTOPTN                                                      
         LA    R1,L'SOPTOPTN                                                    
         BAS   RE,SETIN                                                         
*                                                                               
         ZIC   RE,5(R2)            SLAP IN THE   =VALUE   TO THE FIELD          
         LA    R2,8(RE,R2)                                                      
         MVI   0(R2),C'='                                                       
         LA    R2,1(R2)                                                         
         MVC   0(L'SOPTVALU,R2),SOPTVALU                                        
         LA    RE,L'SOPTVALU-1(R2)                                              
NEXTA02  CLI   0(RE),C' '                                                       
         BH    NEXTA05                                                          
         BCT   RE,NEXTA02                                                       
NEXTA05  LA    R2,BUYOPH                                                        
         SR    RE,R2                                                            
         STC   RE,5(R2)            CORRECT LENGTH OF OPTION INPUT               
         B     NEXT0                                                            
         DROP  R4                                                               
*&&                                                                             
NEXTA10  CLC   10(4,R4),=C'BUY*'   TEST CURRENT OBJECT IS A BUY                 
         BNE   NEXTA90                                                          
         USING SBUYD,R4                                                         
*                                                                               
         OC    SBUYERNO,SBUYERNO   YES-IF ERROR, SKIP TO NEXT BUY               
         BZ    *+12                                                             
         L     R4,UPANXTBY                                                      
         B     NEXT1                                                            
         MVC   UPELSEC,BUELSEC     SAVE SOME VALUES BEFORE IT'S                 
         MVC   UPELSEC2,BUELSEC2   TOO LATE                                     
         MVC   UPDAY1IN,BUDAY1IN                                                
         MVC   UPDAYXIN,BUDAYXIN                                                
         CLI   SBUYACTN,C'A'         TEST ADD                                   
         BNE   NEXT0                                                            
****                                                                            
         OC    BUYKBUY(2),BUYKBUY  WE SHOULD NOT BE ADDING BUYLINE 0            
         BNE   *+6                                                              
         DC    H'0'                GONNA SOLVE THIS ANNOYING BUG                
****                                                                            
         SR    R0,R0                                                            
         ICM   R0,3,BUYKBUY        GET 2-BYTE LINE NUMBER                       
         STCM  R0,3,UPBYLINE                                                    
         STCM  R0,3,SBUYLIN2                                                    
         CHI   R0,255                                                           
         BH    *+8                                                              
         STC   R0,SBUYLINE         RETURN 1-BYTE VRSN IF POSSIBLE               
         B     NEXT0                                                            
*                                                                               
NEXTA90  CLC   10(4,R4),=C'DEL*'   TEST CURRENT OBJECT IS A DELETE              
         BNE   NEXT0                                                            
         TM    UPSW,UPRCL          YES-TEST JUST RECALLED THE BUY               
         BZ    NEXT0                                                            
         NI    UPSW,255-UPRCL      YES-NOW DELETE IT                            
         OI    UPSW,UPCHA          CHANGE SWITCH                                
         LA    R2,BUYINP1H                                                      
         MVI   4(R2),0                                                          
         OI    6(R2),X'80'                                                      
         XC    BUYINP1,BUYINP1                                                  
         MVC   8(3,R2),=C'DEL'                                                  
         MVI   5(R2),3                                                          
         B     NEXTX                                                            
*                                                                               
NEXT0    SR    R1,R1                                                            
         ICM   R1,3,0(R4)          ADVANCE TO NEXT OBJECT                       
         BNZ   *+6                                                              
         DC    H'0'                                                             
         LA    RE,2(R1,R4)                                                      
         ST    RE,ANEXTOBJ                                                      
         CLC   10(4,R4),=C'EBY*'   TEST CURRENT OBJECT IS END-OF-BUY            
         BE    NEXT4               YES-WRAP UP FOR THIS BUY                     
         LR    R4,RE                                                            
*                                                                               
NEXT1    CLC   0(2,R4),=X'FFFF'    TEST END OF TWA                              
         BNE   NEXT2                                                            
         BAS   RE,WRTTWA           YES-WRITE CURRENT TWA                        
         ZIC   R1,UPCURTWA                                                      
NEXT1A   LA    R1,1(R1)                                                         
         CHI   R1,5                DON'T TOUCH TEMPSTR RECORD #5                
         BE    NEXT1A              BUMP IT AS CTMAD00/PT10 DOES                 
         STC   R1,UPCURTWA                                                      
         BAS   RE,READTWA          GET NEXT TEMPSTR PAGE                        
         L     R4,VTIA                                                          
         ST    R4,UPANXTBY         NEED THIS TO PROCESS NEXT PAGE               
*                                                                               
NEXT2    ST    R4,UPACUROB                                                      
         SR    R1,R1                                                            
         ICM   R1,3,0(R4)                                                       
         BZ    NEXT99              EXIT IF END OF FILE                          
         CLC   10(4,R4),=C'EOE*'   OR END OF ESTIMATE                           
         BE    NEXT99                                                           
         LA    R1,2(R1,R4)         R1=A(NEXT OBJECT)                            
         ST    R1,ANEXTOBJ                                                      
         TM    UPSW,UPHDR          TEST LAST OBJECT WAS HDR*                    
         BZ    NEXT4                                                            
         NI    UPSW,255-UPHDR      YES-                                         
         B     NEXT6                                                            
*                                                                               
NEXT4    CLC   10(4,R4),=C'EBY*'   TEST END OF BUY OBJECT                       
         BNE   NEXT5                                                            
         CLI   UPBUYSW,0           TEST ANY OBJECTS LEFT TO PROCESS             
         BNE   NEXT20              YES-GO DO THEM                               
*                                                                               
NEXT4A   BAS   RE,PUT              WRITE THE BUY TO THE FILE                    
         L     R4,UPANXTBY         SKIP TO NEXT BUY                             
         NI    MISCFLG1,X'FF'-MF1ORBOK   RESET OFF ORB FLAG                     
         B     NEXT1                                                            
*                                                                               
NEXT5    LA    R4,0(R4)            CHECK THAT WE'VE NOT REACHED                 
         C     R4,UPANXTBY         NEXT BUY YET                                 
         BL    NEXT20              PROCESS OPTIONAL OBJECTS                     
         BE    NEXT6                                                            
         DC    H'0'                                                             
*                                                                               
NEXT6    MVC   UPANXTBY,ANEXTOBJ   STORE A(NEXT BUY OBJECT)                     
         CLC   10(4,R4),=C'DEL*'   TEST DELETE OBJECT                           
         BNE   NEXT7                                                            
         USING SDELD,R4                                                         
*                                                                               
         SR    R0,R0                                                            
         ICM   R0,3,SDELLIN2       USE 2-BYTE LINENUM IF THERE                  
         BNZ   *+8                                                              
         IC    R0,SDELLIN          ELSE USE 1-BYTE LINENUM                      
         STCM  R0,3,UPBYLINE                                                    
*                                                                               
         CLC   SDELSTA,UPSVSTA     TEST STATION CHANGE                          
         BE    NEXT9                                                            
         MVC   UPSVSTA,SDELSTA     YES-VALIDATE IT                              
         B     NEXT98                                                           
*                                                                               
NEXT7    LA    R4,2(R4)                                                         
         ST    R4,UPACUROB                                                      
         CLC   10(4,R4),=C'BUY*'   FIRST RECORD IN BUY OBJECT MUST              
         BE    *+6                 MUST BE BUY*                                 
         DC    H'0'                                                             
         USING SBUYD,R4                                                         
         ST    R4,UPACURBY         SAVE A(BUY OBJECT)                           
*                                                                               
         SR    R0,R0                                                            
         ICM   R0,3,SBUYLIN2                                                    
         BNZ   *+8                                                              
         IC    R0,SBUYLINE                                                      
         STCM  R0,3,UPBYLINE       SAVE BUYLINE NUMBER                          
*                                                                               
         MVI   UPCOM,C'0'          NO COMMENTS YET                              
*        NI    UPSW,X'FF'-UPORB    NEW BUY, ORBIT SCREEN NOT LOADED YET         
         BAS   RE,SETBYSW          SET BUY SWITCH                               
         TM    UPBUYSW,UPBROT+UPBSKD  TEST ANY SKD* OR ROT* OBJECTS             
         BZ    ESKD                NO-MUST HAVE A SCHEDULE                      
         CLC   SBUYSTA,UPSVSTA     TEST STATION CHANGE                          
         BE    NEXT9                                                            
         MVC   UPSVSTA,SBUYSTA     YES-VALIDATE IT                              
         B     NEXT98                                                           
*                                  ENTRY FOR NEW STATION ---------              
*                                  STATION HAS JUST BEEN VALIDATED              
NEXT8    MVI   UPMODE,UPMNEXT      SET MODE BACK TO 'NEXT'                      
         L     R4,UPACUROB                                                      
         CLI   ERRAREA,0           TEST STATION ERROR                           
         BE    NEXT10                                                           
         OI    UPSW,UPBADSTA       YES-INDICATE INVALID STATION                 
         MVI   UPSTAERR,53         SET STATION NFF (NOT FOUND)                  
         CLI   ERRCD,0             TEST DATAMGR ERROR                           
         BNE   NEXT9                                                            
         ICM   RE,8,DMCB+8         YES-COMPUTE ACTUAL ERROR NUMBER              
         SRL   RE,1                                                             
         LA    RF,8                                                             
         SLL   RE,1                                                             
         LTR   RE,RE                                                            
         BM    *+8                                                              
         BCT   RF,*-10                                                          
         LA    RE,58                                                            
         SR    RE,RF                                                            
         STC   RE,UPSTAERR                                                      
*                                                                               
NEXT9    TM    UPSW,UPBADSTA       TEST BAD STATION                             
         BZ    NEXT10                                                           
         CLC   10(4,R4),=C'BUY*'   YES-                                         
         BNE   NEXT9A                                                           
         USING SBUYD,R4                                                         
         MVI   SBUYERNO,0          MARK BUY OBJECT WITH INVALID STATION         
         MVC   SBUYERNO+1(1),UPSTAERR                                           
         MVI   SBUYERF,SBUYSTAQ                                                 
         B     NEXT9B                                                           
*                                                                               
NEXT9A   CLC   10(4,R4),=C'DEL*'                                                
         BE    *+6                                                              
         DC    H'0'                                                             
         USING SDELD,R4                                                         
         MVI   SDELERNO,0          MARK DEL OBJECT WITH INVALID STATION         
         MVC   SDELERNO+1(1),UPSTAERR                                           
         MVI   SDELERF,SDELSTAQ                                                 
*                                                                               
NEXT9B   MVI   ERRAREA,0                                                        
         MVI   ERRCD,0                                                          
         L     R4,UPANXTBY         ADVANCE TO NEXT BUY                          
         B     NEXT1                                                            
*                                                                               
NEXT10   XC    PRVDSPLN,PRVDSPLN   CLEAR TRACE OF PREVIOUS LINE                 
         XC    SVLIN,SVLIN                                                      
         CLC   10(4,R4),=C'BUY*'   TEST BUY OBJECT                              
         BNE   NEXT30              NO-IT'S A DELETE - RECALL FIRST              
         USING SBUYD,R4            YES-                                         
         MVC   UPUID,SBUYUID       SAVE UNIQUE BUY ID                           
*                                                                               
         LHI   R5,UPBY2UID-BUYSAVE                                              
         AR    R5,RA                                                            
         MVC   0(L'UPBY2UID,R5),SBY2UID   STORES SBY2 ONLY UID (CL12)           
*                                                                               
         MVI   UPELSEC,0           CLEAR SOME BUY VALUES                        
         MVI   UPELSEC2,0                                                       
         MVI   UPDAY1IN,0                                                       
         MVI   UPDAYXIN,0                                                       
         LA    R2,BUYINP1H         BUY INPUT LINE                               
         MVI   4(R2),0             CLEAR INPUT INDICATORS                       
         MVI   5(R2),0             LENGTH 0                                     
         OI    6(R2),X'80'         TRANSMIT                                     
         XC    BUYINP1,BUYINP1                                                  
         LA    R2,BUYINP1                                                       
*                                                                               
         NI    UPSW,255-UPCHA                                                   
         MVC   UPACTN,SBUYACTN     SAVE THE ACTION                              
         CLI   SBUYACTN,C'A'       ACTIONS ARE ADD AND CHANGE                   
         BE    NEXT12                                                           
         CLI   SBUYACTN,C'C'                                                    
         BE    NEXT11                                                           
         CLI   SBUYACTN,C'D'       DELETE WOULD ONLY SHOW IF SPECIAL            
         BE    NEXT9B              NEXT BUY OBJECT                              
         DC    H'0'                                                             
*                                                                               
NEXT11   LA    RE,REC              CLEAR BUY RECORD AREA                        
         LA    RF,4000                                                          
         XCEFL ,                                                                
         OI    UPSW,UPCHA          CHANGE INDICATOR                             
*                                                                               
NEXT12   MVC   0(2,R2),=C'B,'      ADD NEW BUY (CHANGE WON'T REALLY)            
         LA    R2,2(R2)                                                         
*                                                                               
         BAS   RE,BUYPER           FORMAT BUY PERIOD                            
         BAS   RE,BUYDAY           FORMAT DAYS ROTATION                         
         MVI   0(R2),C','          NO NUMBER PER WEEK ENTRY                     
         LA    R2,1(R2)                                                         
         BAS   RE,BUYTIM           FORMAT START/END TIMES                       
         MVC   0(1,R2),SBUYDPT     DAYPART                                      
         MVI   1(R2),C','                                                       
         LA    R2,2(R2)                                                         
         BAS   RE,BUYLEN           FORMAT SPOT LENGTH                           
         BAS   RE,BUYPRG           FORMAT PROGRAM NAME                          
***************                                                                 
         CLI   SVCPROF+9,C'0'      TEST ADJACENCY CODE REQ'D                    
         BE    NEXT14              NO - SKIP COMPLETELY                         
         LA    RE,0                1 CHARACTER ADJ CODES                        
         CLI   SADJCODE,C'0'                                                    
         BL    *+8                                                              
         LA    RE,1                2 CHARACTER NUMERIC CODES                    
         EX    RE,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R2),SADJCODE    MOVE ADJ CODE IF ITS THERE                   
*                                                                               
         CLI   0(R2),C' '                                                       
         BH    *+8                                                              
         MVI   0(R2),C'U'                                                       
*                                                                               
         CLI   SADJCODE,C'0'                                                    
         BL    *+8                                                              
         LA    R2,1(R2)            2 CHARACTER NUMERIC ADJ CODES                
         MVI   1(R2),C','                                                       
         LA    R2,2(R2)                                                         
*                                                                               
NEXT14   BAS   RE,BUYCST           FORMAT COST                                  
*                                                                               
         XC    WORK3,WORK3                                                      
         LR    R0,R2                                                            
         LA    R2,WORK3            ALL DEMO VALUES GO INTO WORK3                
*                                                                               
         BAS   RE,BUYDEM           FORMAT DEMOS                                 
         BAS   RE,BUYPROD          ADDING, M=XXX HAS TO BE AFTER DEMOS          
*                                                                               
         LA    RE,WORK3                                                         
         SR    R2,RE               R2 = # BYTES FOR DEMOS (AND MASPRD)          
         BP    NEXT14X                                                          
         LR    R2,R0                                                            
         CLI   0(R2),C','                                                       
         BE    *+8                                                              
         BCT   R2,*-8              BACKUP TO GET RID OF COMMA                   
         MVI   0(R2),0                                                          
         B     NEXT17                                                           
*                                                                               
NEXT14X  LA    RF,BUYINP1+L'BUYINP1                                             
         SR    RF,R0               RF = BYTES WE HAVE LEFT IN BUYINP1           
         CR    RF,R2                                                            
         BNL   NEXT16              WE HAVE ROOM FOR DEMOS AND MASPRD            
***************                                                                 
* WE DON'T HAVE ENOUGH ROOM ON THE FIRST LINE FOR THE DEMOS AND MASPRD          
***************                                                                 
NEXT15   CLI   SBUYACTN,C'C'       ARE WE CHANGING?                             
         BNE   NEXT15X             NO, WE'RE ADDING, WORK3 IS PERFECT           
         LR    R2,R0               RESTORE WHERE WE WERE ON FIRST LINE          
         SHI   R2,1                GET RID OF THE COMMA                         
         MVI   0(R2),0                                                          
         BAS   RE,BUYPROD          YES, PUT ",M=XXX" AFTER THE COST             
*                                                                               
         XC    WORK3,WORK3                                                      
         LR    R0,R2                                                            
         LA    R2,WORK3            ALL DEMO VALUES GO INTO WORK3                
*                                                                               
         MVC   WORK3(6),=C'C,DEM=' YES, ERROR FROM BUY03:BR5 IF WE              
         AHI   R2,6                  JUST HAVE 0.4/1.0/... (0 & DEC PT)         
*                                                                               
         BAS   RE,BUYDEM           FORMAT DEMOS & NO PROD ON 2ND LINE           
*                                                                               
         LA    RE,WORK3                                                         
         SR    R2,RE               R2 = # BYTES FOR DEMOS (AND MASPRD)          
         BP    NEXT15X                                                          
         LR    R2,R0                                                            
         CLI   0(R2),C','                                                       
         BE    *+8                                                              
         BCT   R2,*-8              BACKUP TO GET RID OF COMMA                   
         MVI   0(R2),0                                                          
         B     NEXT17                                                           
*                                                                               
NEXT15X  LA    R1,BUYINP1          WE'RE DONE WITH 1ST INPUT LINE               
         SR    R0,R1                                                            
         STC   R0,BUYINP1H+5       BUT WE NEED TO SET L'INPUT FIELD             
         LR    RF,R2               RF = L'(DEMOS AND MASPRD)                    
         BCTR  RF,0                                                             
         LA    R2,BUYINP2          R2 = A(WE LEFT OFF IN 1ST INP LINE)          
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R2),WORK3                                                    
         LA    R2,1(RF,R2)                                                      
*                                                                               
         LA    R1,BUYINP2          YES                                          
         SR    R2,R1                                                            
         STC   R2,BUYINP2H+5       SET L'INPUT FIELD                            
         LA    R2,BUYINP3H                                                      
         B     NEXT18                                                           
*                                                                               
NEXT16   LTR   RF,R2               RF = L'(DEMOS AND MASPRD)                    
         BZ    NEXT17                                                           
         BCTR  RF,0                                                             
         LR    R2,R0               R2 = A(WE LEFT OFF IN 1ST INP LINE)          
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R2),WORK3                                                    
         LA    R2,1(RF,R2)                                                      
*                                                                               
NEXT17   LA    R1,BUYINP1          NO, WE'RE STILL ON 1ST INPUT LINE            
         SR    R2,R1                                                            
         STC   R2,BUYINP1H+5       SET L'INPUT FIELD                            
         LA    R2,BUYINP2H                                                      
***********************************                                             
NEXT18   CLC   SBUYCOS2,SPACES                                                  
         BNH   NEXTX                                                            
* ADD COS2 AS A CHANGE ACTION                                                   
         MVI   4(R2),0                                                          
         MVI   5(R2),17                 SET INPUT LENGTH                        
         OI    6(R2),X'80'                                                      
         XC    8(L'BUYINP2,R2),8(R2)    CLEAR THE INPUT LINE                    
         MVC   8(7,R2),=C'C,COS2='      FORMAT INPUT LINE                       
         MVC   15(7,R2),SBUYCOS2                                                
         MVI   22(R2),C'.'              TWO IMPLIED DECIMALS                    
         MVC   23(2,R2),SBUYCOS2+7                                              
         B     NEXTX                                                            
*                                  OPTIONAL OBJECTS ----                        
NEXT20   OI    UPSW,UPCHA          CHANGE INDICATOR                             
***  IMPORTANT TO HAVE THE SKD PROCESSED BEFORE THE COM BECAUSE COM             
***  RUINS THE POINTER FOR UPACUROB                                             
         TM    UPBUYSW,UPBSKD      TEST SPOT SCHEDULE TO DO                     
         BZ    NEXT21                                                           
         NI    UPBUYSW,255-UPBSKD                                               
         BAS   RE,SKD              YES-ADD SCHEDULES MANUALLY                   
         BE    NEXTX               IF ANY, EXECUTE PERIOD CHANGE LOGIC          
*                                                                               
NEXT21   TM    UPBUYSW,UPBCOM      TEST COMMENTS TO DO                          
         BZ    NEXT22                                                           
         BAS   RE,COM              YES-FORMAT COMMENT ADD LINE(S)               
         BE    NEXTX               IF ANY, EXECUTE COMMENT ADD LOGIC            
*                                                                               
NEXT22   TM    UPBUYSW,UPBROT      TEST SPOT ROTATION TO DO                     
         BZ    NEXT24                                                           
         NI    UPBUYSW,255-UPBROT                                               
         BAS   RE,ROT              YES-ADD ROTATION MANULALLY                   
         BE    NEXTX               IF ANY, EXECUTE PERIOD CHANGE LOGIC          
*                                                                               
NEXT24   TM    UPBUYSW,UPBORB      TEST ORBIT TO DO                             
         BZ    NEXT26                                                           
         TM    MISCFLG1,MF1ORBOK   HAVE WE ADDED ORBS YET                       
         BO    NEXT24G              - YES WE ALREADY HAVE FOR THIS LINE         
         BAS   RE,ORB              YES-ADD ORBIT                                
         BE    NEXTX                                                            
*                                                                               
NEXT24G  CR    R0,RE               PUT THE CONDITION BACK TO !=                 
*                                                                               
NEXT26   B     NEXT4A              GO TO NEXT BUY                               
*                                                                               
NEXT30   DS    0H                  DELETE A BUYLINE ----                        
         USING SDELD,R4                                                         
         LA    R2,BUYINP1H         RECALL IT FIRST                              
         MVI   4(R2),0                                                          
         MVI   5(R2),3                                                          
         OI    6(R2),X'80'                                                      
         XC    BUYINP1,BUYINP1                                                  
         LA    R2,BUYINP1                                                       
         SR    RE,RE                                                            
         ICM   RE,3,SDELLIN2       GET 2-BYTE LINE IF THERE                     
         BNZ   *+8                                                              
         IC    RE,SDELLIN          ELSE GET 1-BYTE INSTEAD                      
         CVD   RE,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  0(3,R2),DUB                                                      
         OI    UPSW,UPRCL          RECALL                                       
         NI    UPSW,255-UPCHA      NOT A CHANGE                                 
         B     NEXTX                                                            
*                                                                               
NEXT98   NI    UPSW,255-UPBADSTA   STATION CHANGE -                             
         LA    R2,BUYSTH           MOVE TO TWA0                                 
         XC    BUYST,BUYST                                                      
         LA    R5,UPSVSTA                                                       
         LA    R1,L'UPSVSTA                                                     
         BAS   RE,SETIN                                                         
         NI    4(R2),255-X'20'     TURN PREV VALIDATED BIT OFF                  
         MVI   UPMODE,UPMNSTA      RETURN WITH MODE SET TO NEW STATION          
         B     NEXTX                                                            
*                                                                               
NEXT99   BAS   RE,END              DATA END                                     
*                                                                               
NEXTX    B     EXIT                                                             
         EJECT                                                                  
***********************************************************************         
* SET REQUEST DATES                                                   *         
***********************************************************************         
         SPACE 1                                                                
         USING SHDRD,R4                                                         
SETDATES ST    RE,SVRE                                                          
         CLC   UPSTART,SPACES      TEST REQUEST DATES GIVEN                     
         BNH   SETD2                                                            
         CLC   UPSTART,SVSTART     YES-TEST START/END WITHIN EST DATES          
         BL    ESTART                                                           
         CLC   UPEND,SVEND                                                      
         BH    EEND                                                             
         B     SETD4                                                            
*                                                                               
SETD2    MVC   UPSTART,SVSTART     NO-USE ESTIMATE DATES                        
         MVC   UPEND,SVEND                                                      
*                                                                               
SETD4    L     R5,AREC2            GET REQUEST WEEKS                            
         XC    WORK,WORK                                                        
         MVC   WORK+8(1),SVEOWSDY  POSSIBLY OUT-OF-WEEK ROTATORS                
         MVC   WORK+16(4),VGETBROD                                              
         MVC   WORK+20(4),VADDAY                                                
         MVC   WORK+24(4),VGETDAY                                               
         MVC   WORK+28(4),VDATCON                                               
         GOTO1 VMOBILE,DMCB,(15,UPSTART),(4,(R5)),WORK+16,WORK                  
         SR    RF,RF                                                            
         ICM   RF,1,0(R1)          N'WEEKS MUST BE 1-14                         
         BZ    SETD9                                                            
         CH    RF,=H'14'                                                        
         BH    SETD9                                                            
         LA    R1,UPDATES          SAVE START DATES ONLY                        
         MVC   0(2,R1),0(R5)                                                    
         LA    R1,2(R1)                                                         
         LA    R5,4(R5)                                                         
         BCT   RF,*-14                                                          
         XC    0(2,R1),0(R1)                                                    
         CLC   SHDRSTPD,SPACES     TEST DATES IN HDR                            
         BH    SETDX                                                            
         GOTO1 VGETDAY,DMCB,UPSTART,FULL   NO-GET DAY OF WEEK OF START          
         CLC   FULL(3),SPACES                                                   
         BNE   *+6                                                              
         DC    H'0'                                                             
*****    MVC   UPSTDAY,0(R1)                                                    
         B     SETDX                                                            
*                                                                               
SETD9    MVI   SHDRERNO+1,INVDATE      N'WEEKS GREATER THAN 14                  
         CLC   SHDRSTPD,SPACES                                                  
         BNH   *+12                                                             
         MVI   SHDRERF,SHDRENPQ                                                 
         B     EEXIT                                                            
         MVI   SHDRERF,SHDRESTQ                                                 
         B     EEXIT                                                            
*                                                                               
SETDX    L     RE,SVRE                                                          
         BR    RE                                                               
         EJECT                                                                  
***********************************************************************         
* SET BUY SWITCH                                                      *         
***********************************************************************         
         SPACE 1                                                                
SETBYSW  ST    RE,SVRE                                                          
         MVI   UPBUYSW,0                                                        
         MVI   UPBUYSW2,0                                                       
         LR    RE,R4                                                            
         SR    R1,R1                                                            
*                                                                               
SETBYSW1 ICM   R1,3,0(RE)          ADVANCE TO NEXT OBJECT                       
         LA    RE,2(R1,RE)                                                      
         C     RE,UPANXTBY         TEST REACHED NEXT BUY                        
         BNL   SETBYSWX            YES                                          
         CLC   10(4,RE),=C'EBY*'   TEST REACHED END-OF-BUY                      
         BE    SETBYSWX            YES                                          
         CLC   10(4,RE),=C'COM*'   TEST COMMENT OBJECT                          
         BNE   *+12                                                             
         OI    UPBUYSW,UPBCOM                                                   
         B     SETBYSW1                                                         
         CLC   10(4,RE),=C'ORB*'   TEST ORBIT OBJECT                            
         BNE   *+12                                                             
         OI    UPBUYSW,UPBORB                                                   
         B     SETBYSW1                                                         
         CLC   10(4,RE),=C'SKD*'   TEST SCHEDULE OBJECT                         
         BNE   SETBYSW2                                                         
         TM    UPBUYSW,UPBROT      CANNOT MIX SCHEDULES WITH ROTATIONS          
         BO    *+12                                                             
         OI    UPBUYSW,UPBSKD                                                   
         B     SETBYSW2                                                         
         USING SSKDD,RE                                                         
         MVI   SSKDERNO,0                                                       
         MVI   SSKDERNO+1,UPLDSKD                                               
         MVI   SSKDERF,0                                                        
         B     SETBYSW1                                                         
         DROP  RE                                                               
*                                                                               
SETBYSW2 CLC   10(4,RE),=C'ROT*'   TEST ROTATION OBJECT                         
         BNE   SETBYSW1                                                         
         CLI   SVPRD,X'FF'         YES-VALID ONLY FOR POL                       
         BE    SETBYSW4                                                         
         USING SROTD,RE                                                         
         MVI   SROTERNO,0                                                       
         MVI   SROTERNO+1,INVERR                                                
         MVI   SROTERF,0                                                        
         B     SETBYSW1                                                         
*                                                                               
SETBYSW4 TM    UPBUYSW,UPBSKD      CANNOT MIX ROTATIONS WITH SCHEDULES          
         BO    *+12                                                             
         OI    UPBUYSW,UPBROT                                                   
         B     SETBYSW1                                                         
         MVI   SROTERNO,0                                                       
         MVI   SROTERNO+1,UPLDSKD                                               
         MVI   SROTERF,0                                                        
         B     SETBYSW1                                                         
         DROP  RE                                                               
*                                                                               
SETBYSWX L     RE,SVRE                                                          
         BR    RE                                                               
         EJECT                                                                  
***********************************************************************         
* HANDLE ERRORS                                                       *         
***********************************************************************         
         SPACE 1                                                                
ERR      L     R4,UPACUROB                                                      
         CLC   10(4,R4),=C'HDR*'   HEADER ERROR                                 
         BNE   ERR2                                                             
         USING SHDRD,R4                                                         
         MVI   SHDRERNO,0                                                       
         MVC   SHDRERNO+1(1),ERRCD                                              
         CLI   ERRCD,NEWERRS                                                    
         BNE   *+10                                                             
         MVC   SHDRERNO,NERRCD                                                  
         MVC   SHDRERF,UPNDX                                                    
         BAS   RE,END              TERMINATE THE UPLOAD                         
         B     ERRX                                                             
*                                                                               
ERR2     CLC   10(4,R4),=C'BUY*'   BUY ERROR                                    
         BNE   ERR4                                                             
         USING SBUYD,R4                                                         
         MVI   SBUYERNO,0                                                       
         MVC   SBUYERNO+1(1),ERRCD                                              
         CLI   ERRCD,NEWERRS                                                    
         BNE   *+10                                                             
         MVC   SBUYERNO,NERRCD                                                  
         MVC   SBUYERF,UPNDX                                                    
         CLI   UPNDX,SBUYP1SQ      TEST PRD 1 TIME SHARE FIELD                  
         BNE   ERRX                                                             
         MVI   SBUYERNO+1,SLNERR   YES-CHANGE ERROR TO SLN ERROR                
         B     ERRX                                                             
*                                                                               
ERR4     CLC   10(4,R4),=C'DEL*'   DELETE ERROR                                 
         BNE   ERR6                                                             
         USING SDELD,R4                                                         
         MVI   SDELERNO,0                                                       
         MVC   SDELERNO+1(1),ERRCD                                              
         CLI   ERRCD,NEWERRS                                                    
         BNE   *+10                                                             
         MVC   SDELERNO,NERRCD                                                  
         MVC   SDELERF,UPNDX                                                    
         NI    UPSW,X'FF'-UPRCL    CANNOT ASSUME JUST RECALLED LINE             
         B     ERRX                   ESPECIALLY WHEN IT DOESN'T EXIST          
*                                                                               
ERR6     TM    UPBUYSW2,UPBORADD   ORBIT ERROR                                  
         BZ    ERR10                                                            
         SR    RF,RF                                                            
         ICM   RF,1,UPORBNDX       RF=ORBIT NUMBER                              
         BZ    ERR10                                                            
         L     R4,UPACURBY                                                      
         SR    RE,RE                                                            
*                                                                               
ERR8     ICM   RE,3,0(R4)                                                       
         LA    R4,2(RE,R4)                                                      
         CLC   10(4,R4),=C'EBY*'                                                
         BE    ERR10                                                            
         C     R4,UPANXTBY                                                      
         BNL   ERR10                                                            
         CLC   10(4,R4),=C'ORB*'                                                
         BNE   ERR8                                                             
         BCT   RF,ERR8                                                          
         USING SORBD,R4                                                         
         MVI   SORBERNO,0                                                       
         MVC   SORBERNO+1(1),ERRCD                                              
         CLI   ERRCD,NEWERRS                                                    
         BNE   *+10                                                             
         MVC   SORBERNO,NERRCD                                                  
         MVC   SORBERF,UPNDX                                                    
         B     ERRX                                                             
*                                                                               
ERR10    L     R4,UPACURBY         ANOTHER ERROR -                              
         USING SBUYD,R4            PUT ERROR TO BUY OBJECT WITHOUT              
         MVI   SBUYERNO,0          FIELD NUMBER                                 
         MVC   SBUYERNO+1(1),ERRCD                                              
         CLI   ERRCD,NEWERRS                                                    
         BNE   *+10                                                             
         MVC   SBUYERNO,NERRCD                                                  
         MVI   SBUYERF,0                                                        
*                                                                               
ERRX     MVI   ERRAREA,0           CLEAR BUY PROGRAM'S ERROR AREAS              
         MVI   ERRCD,0                                                          
         B     NEQXIT                                                           
         EJECT                                                                  
***********************************************************************         
* FORMAT BUY PERIOD                                                   *         
***********************************************************************         
         SPACE 1                                                                
BUYPER   ST    RE,SVRE                                                          
         USING SBUYD,R4                                                         
         CLC   SBUYROT,=CL7'NNNNNNN'                                            
         BE    EROT                                                             
         CLI   SBUYRDAY,C'7'                                                    
         BH    EROTST                                                           
*                                                                               
         GOTO1 VGETDAY,DMCB,UPSTART,FULL   GET DAY OF WEEK OF START             
         MVC   PERSTDAY,0(R1)                                                   
         SR    RF,RF                                                            
         LA    R1,SBUYROT                                                       
*                                                                               
         IC    RF,SVEOWSDY                                                      
         MVC   BYTE,SVEOWSDY                                                    
         CLI   SVEOWSDY,1          MONDAY START IN ROTATOR?                     
         BH    *+8                 NO                                           
         MVI   BYTE,1              YES                                          
*                                                                               
         CLC   PERSTDAY,BYTE       START DAY IS SAME DAY AS ROTATOR?            
         BE    BUYPER05            YES                                          
*                                                                               
         BAS   RE,GETSKD           ANY SKD* RECORD?                             
         BNE   BUYPER05            NO                                           
*                                                                               
         L     RE,ASKEDLIN         CHECK IF WE'RE GOING TO BUY IN THE           
         USING SSKDD,RE              NON-STANDARD START WEEK                    
         CLC   SSKDCNTR,=C'00'                                                  
         BNH   BUYPER00                                                         
         ZIC   RF,PERSTDAY                                                      
         CLI   PERSTDAY,1                                                       
         BNH   BUYPER20                                                         
         B     BUYPER10                'Y' IN THAT WEEK                         
         DROP  RE                                                               
*                                                                               
BUYPER00 ZIC   RE,SVEOWSDY         TRY THE DATE OF THE 2ND WEEK                 
         CLI   SVEOWSDY,1                                                       
         BH    *+8                                                              
         LA    RE,1                                                             
         ZIC   R0,PERSTDAY                                                      
         CR    R0,RE                                                            
         BNH   *+8                                                              
         AH    RE,=H'7'                                                         
         SR    RE,R0                                                            
         MVC   DUB(6),UPSTART                                                   
         LTR   RE,RE                                                            
         BZ    BUYPER02                                                         
         ST    RE,DMCB+8                                                        
         GOTO1 VADDAY,DMCB,UPSTART,DUB                                          
BUYPER02 LA    R1,SBUYROT                                                       
         B     BUYPER21                                                         
*                                                                               
BUYPER05 CLI   SVEOWSDY,1          ANY OUT OF WEEK ROTATOR?                     
         BNH   BUYPER20                                                         
         IC    RF,SVEOWSDY         IE: SVEOWSDY=3, NNYNNNN POINT TO THE         
         B     BUYPER10                'Y' IN THAT WEEK                         
*                                                                               
BUYPER10 BCTR  RF,0                   'Y' IN THAT WEEK                          
         LA    R1,1(R1)                                                         
         BCT   RF,*-4                                                           
*                                                                               
BUYPER20 MVC   DUB(6),UPSTART      DETERMINE BUY PERIOD START                   
BUYPER21 LA    R0,L'SBUYROT                                                     
         LA    RF,SBUYROT+L'SBUYROT                                             
         LA    R5,0                GET 1ST DAY FROM ROTATION                    
*                                                                               
BUYPER30 CLI   0(R1),C'Y'                                                       
         BE    BUYPER40                                                         
         LA    R5,1(R5)                                                         
         LA    R1,1(R1)                                                         
         CR    R1,RF               IF WE START ON A WEDNESDAY                   
         BL    *+8                                                              
         LA    R1,SBUYROT          THEN WE HAVE TO CHECK THE MONDAY             
         BCT   R0,BUYPER30                                                      
         B     EROT                ROTATION ERROR                               
*                                                                               
BUYPER40 STC   R5,UPSTDAY                                                       
         LTR   R5,R5                                                            
         BZ    BUYPER50                                                         
         ST    R5,DMCB+8                                                        
         GOTO1 VADDAY,DMCB,DUB,DUB                                              
*                                                                               
BUYPER50 GOTO1 VDATCON,DMCB,DUB,(5,(R2))                                        
*                                                                               
         BAS   RE,GETSKD                                                        
         BNE   BUYPER90                                                         
*                                                                               
         L     RE,ASKEDLIN                                                      
         USING SSKDD,RE                                                         
         CLC   SSKDSDT,SPACES      ANY SKD* START DATE?                         
         BNH   BUYPER90            NONE                                         
         MVC   SKEDSTDT,SSKDSDT    SAVE SKD* START DATE                         
         DROP  RE                                                               
*                                                                               
         LA    RF,SKEDSTDT         MAKE DATE IS ALL DIGITS                      
         LA    R1,6                FORMAT IS YYMMDD                             
BUYPER55 CLI   0(RF),C'0'          PROBLEM IS =C'051   ' IS > ZEROS             
         BL    ESKDSDT                                                          
         LA    RF,1(RF)                                                         
         BCT   R1,BUYPER55                                                      
*                                                                               
         MVC   DUB,SKEDSTDT                                                     
         LA    R1,SBUYROT                                                       
         CLI   SVEOWSDY,1          ANY OUT OF WEEK ROTATOR?                     
         BNH   BUYPER62                                                         
*                                                                               
         ZIC   RF,SVEOWSDY            POINT TO THE CORRECT DAY OF WEEK          
BUYPER60 BCTR  RF,0                                                             
         LA    R1,1(R1)                                                         
         BCT   RF,*-4                                                           
*                                                                               
BUYPER62 LA    R0,L'SBUYROT                                                     
         LA    RF,SBUYROT+L'SBUYROT                                             
         LA    R5,0                GET 1ST DAY FROM ROTATION                    
*                                                                               
BUYPER64 CLI   0(R1),C'Y'                                                       
         BE    BUYPER66                                                         
         LA    R5,1(R5)                                                         
         LA    R1,1(R1)                                                         
         CR    R1,RF               IF WE START ON A WEDNESDAY                   
         BL    *+8                                                              
         LA    R1,SBUYROT          THEN WE HAVE TO CHECK THE MONDAY             
         BCT   R0,BUYPER64                                                      
         B     EROT                ROTATION ERROR                               
*                                                                               
BUYPER66 LTR   R5,R5                                                            
         BZ    BUYPER70                                                         
         ST    R5,DMCB+8                                                        
         GOTO1 VADDAY,DMCB,SKEDSTDT,DUB                                         
*                                                                               
BUYPER70 GOTO1 VDATCON,DMCB,DUB,(5,(R2))                                        
*                                                                               
BUYPER90 MVC   8(4,R2),=C'-1W,'    MAKE BUY PERIOD 1 WEEK                       
         LA    R2,12(R2)                                                        
*                                                                               
BUYPERX  L     RE,SVRE                                                          
         BR    RE                                                               
         EJECT                                                                  
***********************************************************************         
* FORMAT DAYS ROTATION                                                *         
***********************************************************************         
         SPACE 1                                                                
BUYDAY   ST    RE,SVRE                                                          
         CLI   SBUYRDAY,C'1'                                                    
         BE    BUYDAY1                                                          
         CLI   SVEOWSDY,0          POSSIBLY OUT-OF-WEEK ROTATORS                
         BE    EROTST                                                           
         ZIC   R1,SBUYRDAY                                                      
         SLL   R1,28                                                            
         SRL   R1,28                                                            
         STC   R1,BYTE                                                          
         CLC   BYTE,SVEOWSDY       MUST MATCH START DAY OF EST                  
         BNE   EROTST                                                           
*                                                                               
BUYDAY1  SR    RF,RF                                                            
         BCTR  RF,0                                                             
         SR    RE,RE                                                            
         LA    R1,SBUYROT                                                       
         LA    R0,7                                                             
*                                                                               
BUYDAY2  CLI   0(R1),C'Y'                                                       
         BNE   *+12                                                             
         SLDL  RE,1                                                             
         B     *+8                                                              
         SLL   RE,1                                                             
         LA    R1,1(R1)                                                         
         BCT   R0,BUYDAY2                                                       
         STCM  RE,1,BYTE                                                        
         SR    R5,R5                                                            
         CLI   SBUYRDAY,C'1'                                                    
         BNH   BUYDAY4                                                          
         CLI   SBUYRDAY,C'7'                                                    
         BH    EROTST                                                           
         ZIC   R5,SBUYRDAY                                                      
         SLL   R5,28                                                            
         SRL   R5,24                                                            
*                                                                               
BUYDAY4  DS    0H                                                               
         GOTO1 VDAYUNPK,DMCB,((R5),BYTE),(R2)                                   
         MVC   UPDAYS,0(R2)                                                     
         LA    R2,7(R2)                                                         
         LA    R0,8                                                             
         CLI   0(R2),C' '                                                       
         BH    *+14                                                             
         BCTR  R2,0                                                             
         BCT   R0,*-10                                                          
         B     EROT                                                             
         MVI   1(R2),C','                                                       
         LA    R2,2(R2)                                                         
*                                                                               
BUYDAYX  L     RE,SVRE                                                          
         BR    RE                                                               
         EJECT                                                                  
***********************************************************************         
* FORMAT START/END TIMES                                              *         
***********************************************************************         
         SPACE 1                                                                
BUYTIM   ST    RE,SVRE                                                          
         XC    DUB,DUB             START/END TIMES                              
         MVZ   DUB(4),SBUYSTIM                                                  
         CLC   DUB(4),ZEROS                                                     
         BNE   ETIMST                                                           
         MVZ   DUB(4),SBUYETIM                                                  
         CLC   DUB(4),ZEROS                                                     
         BNE   ETIMEND                                                          
         PACK  DUB,SBUYSTIM                                                     
         CVB   R1,DUB                                                           
         CH    R1,=H'2400'         TIMES ARE 0001 - 2400                        
         BH    ETIMST                                                           
         STCM  R1,3,FULL                                                        
*                                                                               
         PACK  DUB,SBUYETIM                                                     
         CVB   R1,DUB                                                           
         CH    R1,=H'2400'         2400 MAX                                     
         BH    ETIMEND                                                          
         STCM  R1,3,FULL+2                                                      
***      CLC   FULL(2),FULL+2      CHECK START NOT LATER THAN END               
***      BH    ETIMST                                                           
         GOTO1 VUNTIME,DMCB,FULL,(R2)                                           
         LA    R2,10(R2)                                                        
         LA    R0,11                                                            
         CLI   0(R2),C' '                                                       
         BH    *+14                                                             
         BCTR  R2,0                                                             
         BCT   R0,*-10                                                          
         B     ETIMST                                                           
         MVI   1(R2),C','                                                       
         LA    R2,2(R2)                                                         
*                                                                               
BUYTIMX  L     RE,SVRE                                                          
         BR    RE                                                               
         EJECT                                                                  
***********************************************************************         
* FORMAT SPOT LENGTH                                                  *         
* RETURN: R6=TOTAL SPOT LENGTH                                        *         
***********************************************************************         
         SPACE 1                                                                
BUYLEN   ST    RE,SVRE                                                          
         XC    FULL,FULL           SPOT LENGTH                                  
         MVZ   FULL(3),SBUYSLEN                                                 
         CLC   FULL(3),ZEROS                                                    
         BNE   ELEN                                                             
         PACK  DUB,SBUYSLEN                                                     
         MVC   0(3,R2),SBUYSLEN    SPOT LENGTH                                  
         CLI   SBUYLUNT,C'S'                                                    
         BE    BUYLEN2                                                          
         CLI   SBUYLUNT,C'M'       TEST UNIT IS MINUTES                         
         BNE   ELENUNT                                                          
         CP    DUB,=P'2'           MAX 2 MINUTES                                
         BH    ELEN                                                             
         MP    DUB,=P'60'                                                       
         UNPK  0(3,R2),DUB                                                      
*                                                                               
BUYLEN2  CLI   0(R2),C'0'                                                       
         BNE   *+12                                                             
         MVC   0(2,R2),1(R2)                                                    
         BCTR  R2,0                                                             
         MVI   3(R2),C','                                                       
         LA    R2,4(R2)                                                         
         CVB   R1,DUB                                                           
         STC   R1,TOTLEN           SAVE TOTAL SPOT LENGTH                       
         STC   R1,UPELSEC                                                       
*                                                                               
BUYLENX  L     RE,SVRE                                                          
         BR    RE                                                               
         EJECT                                                                  
***********************************************************************         
* FORMAT PROGRAM NAME                                                 *         
***********************************************************************         
         SPACE 1                                                                
BUYPRG   ST    RE,SVRE                                                          
         LA    R1,SBUYPROG+L'SBUYPROG-1   PROGRAM NAME                          
         LA    RE,L'SBUYPROG                                                    
         CLI   0(R1),C' '                                                       
         BH    *+10                                                             
         BCTR  R1,0                                                             
         BCT   RE,*-10                                                          
         BCTR  RE,0                                                             
         LTR   RE,RE                                                            
         BM    BUYPRG10                                                         
         CH    RE,=H'16'           MAXIMUM OF 17                                
         BNH   *+8                                                              
         LA    RE,16                                                            
         EX    RE,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R2),SBUYPROG                                                 
         LA    R2,1(RE,R2)                                                      
*                                                                               
BUYPRG10 MVI   0(R2),C','                                                       
         LA    R2,1(R2)                                                         
*                                                                               
BUYPRGX  L     RE,SVRE                                                          
         BR    RE                                                               
         EJECT                                                                  
***********************************************************************         
* FORMAT COST                                                         *         
***********************************************************************         
         SPACE 1                                                                
BUYCST   ST    RE,SVRE                                                          
         CLI   SBUYCQLF,C' '       TEST FOR COST QUALIFIER                      
         BNE   *+14                                                             
         MVC   0(1,R2),SBUYCQLF    YES-PRECEDE COST WITH IT                     
         LA    R2,1(R2)                                                         
         XC    WORK,WORK                                                        
         MVZ   WORK(L'SBUYCOST),SBUYCOST                                        
         CLC   WORK(L'SBUYCOST),ZEROS                                           
         BNE   ECOST                                                            
         LA    R1,SBUYCOST                                                      
         LA    RE,L'SBUYCOST-2                                                  
         CLI   0(R1),C'0'                                                       
         BH    BUYCST2                                                          
         LA    R1,1(R1)                                                         
         BCT   RE,*-12                                                          
         MVI   0(R2),C'0'                                                       
         LA    R2,1(R2)                                                         
         B     BUYCST4                                                          
*                                                                               
BUYCST2  BCTR  RE,0                                                             
         EX    RE,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R2),0(R1)                                                    
         LA    R2,1(RE,R2)                                                      
*                                                                               
BUYCST4  CLC   SBUYCOST+L'SBUYCOST-2(2),=C'00'                                  
         BE    *+18                                                             
         MVI   0(R2),C'.'                                                       
         MVC   1(2,R2),SBUYCOST+(L'SBUYCOST-2)                                  
         LA    R2,3(R2)                                                         
         MVI   0(R2),C','                                                       
         LA    R2,1(R2)                                                         
*                                                                               
BUYCSTX  L     RE,SVRE                                                          
         BR    RE                                                               
         EJECT                                                                  
***********************************************************************         
* FORMAT DEMOS VALUES                                                 *         
*                                                                     *         
* ON ENTRY:    (R2)                WHERE TO START PUTTING DEMO VALUES *         
***********************************************************************         
BUYDEM   NTR1                                                                   
         XR    R3,R3               THIS WILL LET US KNOW IF PRECISION           
         LHI   RE,UPDEMS-BUYSAVE   NEW WAY OF CALLING UPDEMS                    
         AR    RE,RA                                                            
*        LA    RE,UPDEMS           OLD WAY OF CALLING UPDEMS                    
         SR    RF,RF                                                            
*UYDEM0  LA    R0,UPDEMS+L'UPDEMS                                               
*        CR    RE,R0                                                            
*        BNL   BUYDEM0A                                                         
BUYDEM0  OC    0(3,RE),0(RE)       CHECK IS ONLY GOOD W/IN UPDEMS               
         BZ    BUYDEM1                                                          
         LA    RE,3(RE)                                                         
         LA    RF,1(RF)            RF=MAX N'DEMOS                               
         B     BUYDEM0                                                          
*                                                                               
*UYDEM0A LA    RE,UPMDEMS          WE MIGHT HAVE MORE DEMOS HERE                
*UYDEM0G OC    0(3,RE),0(RE)                                                    
*        BZ    BUYDEM1                                                          
*        LA    RE,3(RE)                                                         
*        LA    RF,1(RF)            RF=MAX N'DEMOS                               
*        B     BUYDEM0G                                                         
*                                                                               
BUYDEM1  L     R1,UPACUROB         LOOK FOR DEMO OBJECT                         
         SR    RE,RE                                                            
*                                                                               
BUYDEM2  ICM   RE,3,0(R1)                                                       
         LA    R1,2(RE,R1)                                                      
         C     R1,UPANXTBY                                                      
         BNL   BUYDEMX                                                          
         CLC   10(4,R1),=C'DEM*'                                                
         BNE   BUYDEM2                                                          
         ST    R1,ADEMOBJ             FOUND-                                    
         ICM   RE,3,0(R1)          IF THERE IS DEMO PRECISION                   
*                                                                               
         CHI   RF,14               MORE THAN 14 DEMOS?                          
         BH    BUYDEM3              - YUP, 20 DEMOS                             
         LA    R1,SDEMDEM-SDEMD(R1)   LOOP THROUGH DEMO VALUES                  
         LA    R5,1                                                             
         LA    R0,14                                                            
         B     BUYDEM3A                                                         
*                                                                               
BUYDEM3  DS    0H                  THIS IS SBY2 (20 DEMOS)                      
         LA    R1,SDM2DEM-SDEMD(R1)   LOOP THROUGH DEMO VALUES                  
         LA    R5,1                                                             
         LA    R0,20                                                            
*                                                                               
BUYDEM3A CHI   RE,SDM3LENQ         CAN WE HAVE DEMO PRECISION?                  
         BL    BUYDEM3C            NO, ONLY IF THIS LENGTH OR GREATER           
*                                  DO WE HAVE ANY PRECISION?                    
         CLC   SDM2DEMP-SDEMDEM(L'SDM2DEMP,R1),SPACES                           
         BNH   BUYDEM3C            NO, NO PRECISION FOR THIS DEM REC            
         LA    R3,SDM2DEMP-SDEMDEM(R1)  YES, R3 WILL TELL US                    
*                                                                               
BUYDEM3C CR    RF,R0                                                            
         BNL   BUYDEM4                                                          
         LR    R0,RF                                                            
*                                                                               
BUYDEM4  CLC   0(6,R1),SPACES      TEST ANY VALUE SET                           
         BH    BUYDEM4A                                                         
         MVC   0(2,R2),=C'0/'      NO-ASSUME VALUE OF 0                         
         LA    R2,2(R2)                                                         
         B     BUYDEM12                                                         
*                                                                               
BUYDEM4A XC    DUB,DUB                                                          
         MVZ   DUB(6),0(R1)                                                     
         CLC   DUB(6),ZEROS        TEST VALID NUMERIC                           
         BNE   EDEMVAL                                                          
*                                                                               
         LA    RE,5                FORMAT DEMO VALUE                            
         LTR   R3,R3               ANY DEMO PRECISION??                         
         BZ    BUYDEM5             NONE                                         
         CLI   0(R3),C'1'          MORE THAN 1 DECIMAL PRECISION?               
         BNH   BUYDEM5             NO, SAME AS OLD WAY                          
         LA    RE,4                YES, LEAVE SOME ROOM                         
*                                                                               
BUYDEM5  LR    RF,R1                                                            
         CLI   0(RF),C'0'                                                       
         BH    BUYDEM6                                                          
         LA    RF,1(RF)                                                         
         BCT   RE,*-12                                                          
         MVI   0(R2),C'0'                                                       
         LA    R2,1(R2)                                                         
         B     BUYDEM8                                                          
*                                                                               
BUYDEM6  BCTR  RE,0                                                             
         EX    RE,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R2),0(RF)                                                    
         LA    R2,1(RE,R2)                                                      
*                                                                               
BUYDEM8  LTR   R3,R3               ANY DECIMAL PRECISION?                       
         BZ    BUYDEM8A            NONE                                         
         CLI   0(R3),C'1'                                                       
         BH    BUYDEM8B                                                         
BUYDEM8A CLI   5(R1),C'0'          TEST DECIMAL PLACE NEEDED                    
         BE    BUYDEM10                                                         
         MVI   0(R2),C'.'          YES                                          
         MVC   1(1,R2),5(R1)                                                    
         LA    R2,2(R2)                                                         
         B     BUYDEM10                                                         
*                                                                               
BUYDEM8B CLC   4(2,R1),=C'00'      TEST DECIMAL PLACE NEEDED                    
         BE    BUYDEM10                                                         
         MVI   0(R2),C'.'          YES                                          
         MVC   1(2,R2),4(R1)                                                    
         LA    R2,3(R2)                                                         
*                                                                               
BUYDEM10 MVI   0(R2),C'/'                                                       
         LA    R2,1(R2)                                                         
*                                                                               
BUYDEM12 LA    R1,6(R1)            NEXT DEMO VALUE                              
         LA    R5,1(R5)                                                         
         LTR   R3,R3               ANY DEMO PRECISION?                          
         BZ    *+8                                                              
         LA    R3,1(R3)            YES, BUMP FOR NEXT DEMO ENTRY                
         BCT   R0,BUYDEM4                                                       
*                                                                               
BUYDEM16 BCTR  R2,0                REMOVE FINAL /                               
         MVI   0(R2),C' '                                                       
*                                                                               
BUYDEMX  DS    0H                                                               
EXITREG2 XIT1  REGS=(R2)           PASS BACK R2 SO WE KNOW HOW LONG             
         EJECT                                                                  
***********************************************************************         
* FORMAT MASTER PRODUCT(S) AND PIGGBACKS IF NECESSARY                 *         
***********************************************************************         
BUYPROD  NTR1                                                                   
         CLI   SVPOLPRD,0          TEST BRAND POL BY BRAND                      
         BNE   BUYPROD3            YUP                                          
         CLI   SVPRD,X'FF'         TEST HEADER PRODUCT NOT POL                  
         BE    BUYPROD2                                                         
         CLC   SBUYMAS(6),SPACES   YES-IF MASTER PRODUCTS ARE SET, THEY         
         BNH   BUYPROD4                MUST MATCH HEADER PRODUCTS               
         CLC   SBUYMAS,QPRD                                                     
         BNE   EMAS                                                             
         CLC   SBUYMAS2,UPPRD2                                                  
         BNE   EMAS2                                                            
         B     BUYPROD4                                                         
*                                                                               
BUYPROD2 CLI   SVCPROF,C'0'        HDR PRODUCT IS POL - TEST TRUE POL           
         BE    BUYPROD4            YES                                          
         CLC   SBUYMAS,SPACES      TEST MASTER PRODUCT SUPPLIED                 
         BH    BUYPROD4            YUP                                          
         CLC   QPRD,=C'POL'        TEST UPLOADING UNDER BRAND CODE              
         BE    EMASMIS             NO                                           
BUYPROD3 MVC   SBUYMAS,QPRD        YES - USE UPLOAD PRODUCT                     
*                                                                               
BUYPROD4 CLC   UPPRD2,SPACES       TEST PIGGYBACK                               
         BH    *+14                                                             
         CLC   SBUYMAS2,SPACES                                                  
         BNH   BUYPROD6                                                         
         XC    FULL,FULL           YES-THERE MUST BE A PRODUCT 1 LENGTH         
         MVZ   FULL(3),SBUYP1SH        SHARE HERE                               
         CLC   FULL(3),ZEROS                                                    
         BNE   EPRD1SHR                                                         
         PACK  DUB,SBUYP1SH                                                     
         CLI   SBUYLUNT,C'M'                                                    
         BNE   *+10                                                             
         MP    DUB,=P'60'                                                       
         CVB   R5,DUB                                                           
         LTR   R5,R5               TEST NON-ZERO                                
         BZ    EPRD1SHR                                                         
         ZIC   R6,TOTLEN                                                        
         SR    R6,R5               TEST LOWER THAN TOTAL LENGTH                 
         BNP   EPRD1SHR            (R5/R6=SPOT LENGTHS)                         
*                                                                               
         CLI   SVCPROF,C'0'        TEST BRAND BUYING                            
         BNE   BUYPROD6                                                         
         CLI   SVPRD,X'FF'                                                      
         BE    BUYPROD6                                                         
         MVI   0(R2),C','          YES-FORMAT PARTNER PRODUCT                   
         MVC   1(3,R2),=C'PA='                                                  
         MVC   4(3,R2),UPPRD2                                                   
         LA    R2,6(R2)                                                         
         CLI   0(R2),C' '                                                       
         BNH   *+8                                                              
         LA    R2,1(R2)                                                         
         MVI   0(R2),C'-'                                                       
         ZIC   RF,SVEST            WITH ESTIMATE                                
         CVD   RF,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  1(3,R2),DUB                                                      
         MVI   4(R2),C'-'          AND PARTNER LENGTH                           
         CVD   R6,DUB              R6=PARTNER LENGTH                            
         OI    DUB+7,X'0F'                                                      
         UNPK  5(3,R2),DUB                                                      
         LA    R2,8(R2)                                                         
         B     BUYPRODX                                                         
*                                                                               
BUYPROD6 MVC   WORK(3),SBUYMAS     SET MASPRD                                   
         CLC   SBUYMAS,SPACES                                                   
         BH    *+10                                                             
         MVC   WORK(3),QPRD                                                     
         MVC   WORK+3(3),SBUYMAS2  AND SECOND MASPRD                            
         CLC   SBUYMAS2,SPACES                                                  
         BH    *+10                                                             
         MVC   WORK+3(3),UPPRD2                                                 
         CLC   WORK(3),=C'POL'     MASPRD=POL IS INVALID                        
         BE    BUYPRODX                                                         
         CLI   SVPOLPRD,0          TEST BRAND POL BY BRAND                      
         BNE   BUYPRODX                                                         
         CLI   SVPRD,X'FF'         TEST POL BUYING                              
         BE    BUYPROD8                                                         
         CLI   SVCPROF,C'0'                                                     
         BE    BUYPRODX                                                         
         CLC   WORK+3(3),SPACES    OR PIGGYBACKS                                
         BNH   BUYPRODX                                                         
*                                                                               
BUYPROD8 MVI   0(R2),C','          YES-FORMAT MASPRD                            
         MVC   1(2,R2),=C'M='                                                   
         MVC   3(3,R2),WORK                                                     
         LA    R2,5(R2)                                                         
         CLI   0(R2),C' '                                                       
         BNH   *+8                                                              
         LA    R2,1(R2)                                                         
         CLC   WORK+3(3),SPACES    TEST PIGGYBACK                               
         BNH   BUYPRODX                                                         
         MVI   0(R2),C'-'          FORMAT IS M=AAA-BBB/LLL-LLL                  
         MVC   1(3,R2),WORK+3                                                   
         LA    R2,3(R2)                                                         
         CLI   0(R2),C' '                                                       
         BNH   *+8                                                              
         LA    R2,1(R2)                                                         
         MVI   0(R2),C'/'                                                       
         CVD   R5,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  1(3,R2),DUB                                                      
         MVI   4(R2),C'-'                                                       
         CVD   R6,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  5(3,R2),DUB                                                      
         LA    R2,8(R2)                                                         
*                                                                               
BUYPRODX B     EXITREG2            PASS BACK R2 SO WE KNOW HOW LONG             
         EJECT                                                                  
***********************************************************************         
* ADD COMMENTS                                                        *         
* RETURN CONDITION CODE - EQ = COMMENTS TO ADD                        *         
*                         NE = NO COMMENTS TO ADD                     *         
***********************************************************************         
         SPACE 1                                                                
COM      ST    RE,SVRE                                                          
         LA    R6,4                MAX 4 COMMENT INPUT LINES                    
         LA    R2,BUYINP1H                                                      
         TM    UPBUYSW,UPBCOMNX    TEST ALREADY ADDED 4 COMMENTS                
         BO    COM6                YES-R4=A(NEXT OBJECT AFTER 4TH COM*)         
         L     R4,UPACURBY         FIND THE COMMENT OBJECTS                     
         B     COM4                                                             
*                                                                               
         USING SCOMD,R4                                                         
COM2     MVI   4(R2),0                                                          
         OI    6(R2),X'80'                                                      
         XC    8(L'BUYINP1,R2),8(R2)    CLEAR THE INPUT LINE                    
         LA    RF,L'SCOMDATA                                                    
         LA    R1,L'BUYINP1-8                                                   
         CR    RF,R1                                                            
         BNH   *+6                                                              
         LR    RF,R1                                                            
         LA    R1,SCOMDATA-1(RF)   DETERMINE LENGTH OF COMMENT                  
         CLI   0(R1),C' '                                                       
         BH    *+14                                                             
         BCTR  R1,0                                                             
         BCT   RF,*-10                                                          
         B     COM4                                                             
         MVC   8(6,R2),=C'C,COM='  FORMAT COMMENT INPUT LINE                    
         ZIC   RE,UPCOM                                                         
         LA    RE,1(RE)                                                         
         STC   RE,UPCOM                                                         
         STC   RE,14(R2)                                                        
         MVI   15(R2),C'-'                                                      
         BCTR  RF,0                                                             
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   16(0,R2),SCOMDATA                                                
         LA    RF,9(RF)                                                         
         STC   RF,5(R2)                                                         
         LA    R2,BUYINP2H-BUYINP1H(R2)                                         
         BCT   R6,COM4             NEXT COMMENT                                 
         OI    UPBUYSW,UPBCOMNX    4 DONE - RETURN TO ADD THEM                  
         B     COMY                                                             
*                                                                               
COM4     SR    R1,R1               ADVANCE TO NEXT OBJECT                       
         ICM   R1,3,0(R4)                                                       
         LA    R4,2(R1,R4)                                                      
         ST    R4,UPACUROB                                                      
*                                                                               
COM6     CLC   10(4,R4),=C'EBY*'   TEST END OF BUY                              
         BE    COM8                                                             
         C     R4,UPANXTBY                                                      
         BNL   COM8                                                             
         CLC   10(4,R4),=C'COM*'                                                
         BE    COM2                                                             
         B     COM4                                                             
*                                                                               
COM8     NI    UPBUYSW,255-UPBCOM  PROCESSED ALL COMMENTS                       
         NI    UPBUYSW,255-UPBCOMNX                                             
         CH    R6,=H'4'            TEST ANY COMMENTS THIS SCREEN                
         BNE   COMY                YES-RETURN TO ADD THE COMMANTS               
*                                                                               
COMN     ICM   RE,15,SVRE                                                       
         BR    RE                                                               
*                                                                               
COMY     L     RE,SVRE                                                          
         CR    RE,RE                                                            
         BR    RE                                                               
         EJECT                                                                  
***********************************************************************         
* ADD BUY SCHEDULE                                                    *         
***********************************************************************         
         SPACE 1                                                                
SKD      ST    RE,SVRE                                                          
*                                                                               
         ZIC   R0,UPDAY1IN         SPEC SAID DATE ALIGNED WITH MONDAY           
         L     R1,AREC             LOOK IN BUY FOR BDSEDAY                      
         USING BUYREC,R1                                                        
         ZIC   RF,BDSEDAY          GET START DAY FROM THE BUY                   
         DROP  R1                                                               
         SRL   RF,4                SO IT WILL ALIGN CORRECTLY                   
         STC   RF,UPDAY1IN                                                      
         BAS   RE,BLDSKD           BUILD SKELETON SCHEDULE TABLE                
         STC   R0,UPDAY1IN         RESTORE ORIGINAL                             
*                                                                               
         MVC   PERSTART,=X'FFFF'                                                
         XC    PEREND,PEREND                                                    
*                                                                               
         L     R4,UPACURBY         FIND THE SKD OBJECTS                         
         MVI   ANYSPOTS,C'N'                                                    
         XC    AFRSTSKD,AFRSTSKD                                                
*                                                                               
SKD2     SR    R1,R1               ADVANCE TO NEXT OBJECT                       
         ICM   R1,3,0(R4)                                                       
         LA    R4,2(R1,R4)                                                      
         ST    R4,UPACUROB                                                      
         CLC   10(4,R4),=C'EBY*'   END-OF-BUY                                   
         BE    SKD14                                                            
         C     R4,UPANXTBY         END-OF-BUY                                   
         BNL   SKD14                                                            
         CLC   10(4,R4),=C'SKD*'                                                
         BNE   SKD2                                                             
         USING SSKDD,R4                                                         
         LA    R5,UPDATES                                                       
         LA    R6,SKDTAB                                                        
         CLC   SSKDSDT,SPACES      TEST START DATE                              
         BNH   SKD6                                                             
         XC    DUB,DUB             YES-VALIDATE IT                              
         MVZ   DUB(6),SSKDSDT                                                   
         CLC   DUB(6),ZEROS                                                     
         BNE   ESKDSDT                                                          
         CLC   SSKDSDT,ZEROS       IF ZEROS, IGNORE                             
         BE    SKD6                                                             
         GOTO1 VDATCON,DMCB,SSKDSDT,(2,HALF)                                    
*                                                                               
SKD4     OC    0(2,R5),0(R5)       DATE MUST BE IN ONE OF OUR TABLES            
         BZ    ESKDSDT                                                          
         CLC   0(2,R6),=X'FFFF'    TEST INVALID WEEK FOR THIS ROTATION          
         BE    ESKDSDT                                                          
         CLC   HALF,0(R5)                                                       
         BE    SKD6                                                             
         CLC   HALF,0(R6)                                                       
         BE    SKD6                                                             
         LA    R5,2(R5)                                                         
         CLC   0(2,R5),0(R6)                                                    
         BNH   SKD4                                                             
         LA    R6,SKDTABL(R6)                                                   
         B     SKD4                                                             
*                                                                               
SKD6     MVI   COSTOVSW,C'N'                                                    
         CLC   SSKDCOST,SPACES     TEST FOR COST OVERRIDE                       
         BNH   SKD8                                                             
         XC    WORK,WORK           YES-VALIDATE NUMERIC                         
         MVZ   WORK(L'SSKDCOST),SSKDCOST                                        
         CLC   WORK(L'SSKDCOST),ZEROS                                           
         BNE   ESKDCST                                                          
         CLI   SVPRD,X'FF'         TEST POL                                     
         BNE   ESKDCST             NO-COST OVERRIDES INVALID                    
         PACK  DUB,SSKDCOST                                                     
         CVB   R1,DUB                                                           
         ST    R1,COSTOVR          SAVE COST OVERRIDE                           
         MVI   COSTOVSW,C'Y'                                                    
*                                                                               
SKD8     LA    R1,SSKDCNTR         LOOP THROUGH SPOT COUNTERS                   
         LA    R0,14                                                            
         LA    RE,SSKDCNTQ         RE=FIELD NUMBER                              
         MVC   WORK3(L'SKDTAB),SKDTAB   SAVE SKED TABLE                         
         LA    R2,UPDATES+L'UPDATES     R2=A(END OF DATES TABLE)                
*                                                                               
SKD10    CLC   0(2,R1),SPACES      TEST ANYTHING SIGNIFICANT                    
         BNH   SKD12               FOR THIS WEEK                                
         CLC   0(2,R1),ZEROS                                                    
         BE    SKD12                                                            
         CR    R5,R2               YES-TEST DROPPED OF END OF DATES TAB         
         BNL   ESKDCNT                                                          
         OC    0(2,R5),0(R5)       TEST END OF DATES TABLE                      
         BZ    ESKDCNT                                                          
         CLC   0(2,R6),=X'FFFF'    TEST WEEK INVALID FOR THIS ROTATION          
         BE    ESKDCNT                                                          
         CLI   2(R6),0             TEST SPOTS ALREADY FOR THIS WEEK             
         BNE   ESKDCNT             YES-CONFLICTING SKD* OBJECTS                 
         XC    HALF,HALF           VALIDATE NUMERIC                             
         MVZ   HALF,0(R1)                                                       
         CLC   HALF,ZEROS                                                       
         BNE   ESKDCNT                                                          
         CLC   PERSTART,0(R6)      SET PERIOD START DATE                        
         BNH   *+14                                                             
         MVC   PERSTART,0(R6)                                                   
         ST    R6,ASTART                                                        
         CLC   PEREND,0(R6)        SET PERIOD END                               
         BNL   *+14                                                             
         MVC   PEREND,0(R6)                                                     
         ST    R6,AEND                                                          
         PACK  DUB,0(2,R1)                                                      
         CVB   RF,DUB                                                           
         STC   RF,2(R6)            NUMBER OF SPOTS                              
         MVI   ANYSPOTS,C'Y'                                                    
         OC    AFRSTSKD,AFRSTSKD   SAVE A(FIRST SKD OBJECT W/ SPOTS)            
         BNZ   *+8                                                              
         ST    R4,AFRSTSKD                                                      
         CLI   COSTOVSW,C'Y'                                                    
         BNE   *+14                                                             
         OI    3(R6),X'80'         COST OVERRIDE                                
         MVC   4(4,R6),COSTOVR                                                  
         MVC   8(2,R6),BDMASPRD    PRODUCT(S)                                   
         MVC   10(1,R6),BDSEC                                                   
         CLI   BDMASPRD+1,0        TEST PIGGYBACK                               
         BE    SKD12                                                            
         MVC   10(1,R6),UPELSEC                                                 
         MVC   11(1,R6),UPELSEC2   YES-FILL IN SPOT LENGTHS                     
*                                                                               
SKD12    LA    R1,2(R1)            NEXT WEEK                                    
         LA    R5,2(R5)                                                         
         LA    R6,SKDTABL(R6)                                                   
         LA    RE,1(RE)                                                         
         BCT   R0,SKD10                                                         
         XC    SSKDERNO,SSKDERNO   CLEAR ERROR NUMBER AND                       
         B     SKD2                LOOK FOR MORE SKD* OBJECTS                   
*                                                                               
SKD14    CLI   ANYSPOTS,C'Y'       TEST ANY SPOTS                               
         BNE   ESKDNONE                                                         
         LA    R1,SKDTAB           ADD SPOTS TO BUY                             
         ST    R1,ASPOTTAB                                                      
         MVI   MAXSPOTS,14   <===  IN THIS CASE IT IS # OF WKS                  
         BAS   RE,ADDSPOTS                                                      
         BNE   SKDXNE                                                           
         CLI   TOOMANY,C'Y'        TEST TOO MANY SPOTS                          
         BNE   SKDXEQ                                                           
         ICM   R4,15,AFRSTSKD      YES-MARK FIRST SKD OBJECT                    
         BZ    ESKDOVER                                                         
         MVI   SSKDERNO,0                                                       
         MVI   SSKDERNO+1,MAXELEMS                                              
         MVI   SSKDERF,SSKDCNTQ                                                 
         B     SKDXNE                                                           
*                                                                               
SKDXEQ   L     RE,SVRE             EXIT CC EQ IF ADDED SPOTS                    
         CR    RE,RE                                                            
         BR    RE                                                               
*                                                                               
SKDXNE   ICM   RE,15,SVRE          EXIT CC NE IF NO SPOTS ADDED                 
         BR    RE                                                               
         SPACE 2                                                                
ESKDSDT  MVI   SSKDERNO,0          INVALID DATE                                 
         MVI   SSKDERNO+1,INVDATE                                               
         MVI   SSKDERF,SSKDSDTQ                                                 
         B     SKDXNE              AND RETURN                                   
***      B     SKD2                                                             
*                                                                               
ESKDCST  MVI   SSKDERNO,0          INVALID COST                                 
         MVI   SSKDERNO+1,BADCOST                                               
         MVI   SSKDERF,SSKDCOSQ                                                 
         B     SKDXNE              AND RETURN                                   
***      B     SKD2                                                             
*                                                                               
ESKDCNT  MVI   SSKDERNO,0          INVALID SPOT COUNTER                         
         MVI   SSKDERNO+1,INVERR                                                
         STC   RE,SSKDERF          POSITIONAL FIELD NUMBER                      
         MVC   SKDTAB,WORK3        RESTORE SKED TABLE                           
         B     SKDXNE              AND RETURN                                   
***      B     SKD2                ON TO NEXT SKD* OBJECT                       
*                                                                               
ESKDOVER L     R4,UPACURBY         TOO MANY SPOTS -                             
         USING SBUYD,R4                                                         
         MVI   SBUYERNO,0          PUT ERROR IN BUY OBJECT                      
         MVI   SBUYERNO+1,MAXELEMS                                              
         MVI   SBUYERF,0                                                        
         B     SKDXNE              AND RETURN                                   
*                                                                               
ESKDNONE L     R4,UPACURBY         NO SPOTS AT ALL -                            
         USING SBUYD,R4                                                         
         TM    UPSW,UPNOERR                                                     
         BO    SKDXNE                                                           
         MVI   SBUYERNO,0          PUT ERROR IN BUY OBJECT                      
         MVI   SBUYERNO+1,NOELEMS                                               
         MVI   SBUYERF,0                                                        
         B     SKDXNE              AND RETURN                                   
         EJECT                                                                  
***********************************************************************         
* ADD BUY ROTATION                                                    *         
***********************************************************************         
         SPACE 1                                                                
ROT      ST    RE,SVRE                                                          
*                                                                               
         ZIC   R0,UPDAY1IN         SPEC SAID DATE ALIGNED WITH MONDAY           
         L     R1,AREC             LOOK IN BUY FOR BDSEDAY                      
         USING BUYREC,R1                                                        
         ZIC   RF,BDSEDAY          GET START DAY FROM THE BUY                   
         DROP  R1                                                               
         SRL   RF,4                SO IT WILL ALIGN CORRECTLY                   
         STC   RF,UPDAY1IN                                                      
         BAS   RE,BLDSKD           BUILD SKELETON SCHEDULE TABLE                
         STC   R0,UPDAY1IN         RESTORE ORIGINAL                             
*                                                                               
         MVC   PERSTART,=X'FFFF'                                                
         XC    PEREND,PEREND                                                    
         L     R1,AREC2            ROTATION TABLE WILL BE BUILT IN REC2         
         LA    R0,209              MAXIMUM # OF SPOTS IS NOW 208                
         XC    0(SKDTABL,R1),0(R1)                                              
         LA    R1,SKDTABL(R1)                                                   
         BCT   R0,*-10                                                          
         L     R4,UPACURBY         FIND THE ROT OBJECTS                         
         XC    AFRSTROT,AFRSTROT                                                
*                                                                               
ROT2     SR    R1,R1               ADVANCE TO NEXT OBJECT                       
         ICM   R1,3,0(R4)                                                       
         LA    R4,2(R1,R4)                                                      
         ST    R4,UPACUROB                                                      
         CLC   10(4,R4),=C'EBY*'   END-OF-BUY                                   
         BE    ROT18                                                            
         C     R4,UPANXTBY         END-OF-BUY                                   
         BNL   ROT18                                                            
         CLC   10(4,R4),=C'ROT*'                                                
         BNE   ROT2                                                             
         USING SROTD,R4                                                         
         XC    DUB,DUB                                                          
         MVZ   DUB(6),SROTDATE                                                  
         CLC   DUB(6),ZEROS                                                     
         BNE   EROTDATE                                                         
         CLC   SROTDATE,ZEROS                                                   
         BE    EROTDATE                                                         
*                                                                               
         GOTO1 VDATCON,DMCB,SROTDATE,(2,HALF)                                   
         LA    R5,UPDATES                                                       
         LA    R6,SKDTAB                                                        
*                                                                               
ROT4     OC    0(2,R5),0(R5)       DATE MUST BE IN ONE OF OUR TABLES            
         BZ    EROTDATE                                                         
         CLC   0(2,R6),=X'FFFF'    TEST INVALID WEEK FOR THIS ROTATION          
         BE    EROTDATE                                                         
         CLC   HALF,0(R5)                                                       
         BE    ROT6                R6=A(SKED TABLE ENTRY)                       
         CLC   HALF,0(R6)                                                       
         BE    ROT6                                                             
         LA    R5,2(R5)                                                         
         LA    R6,SKDTABL(R6)                                                   
         B     ROT4                                                             
*                                                                               
ROT6     MVI   COSTOVSW,C'N'                                                    
         CLC   SROTCOST,SPACES     TEST FOR COST OVERRIDE                       
         BNH   ROT8                                                             
         XC    WORK,WORK           YES-VALIDATE NUMERIC                         
         MVZ   WORK(L'SROTCOST),SROTCOST                                        
         CLC   WORK(L'SROTCOST),ZEROS                                           
         BNE   EROTCST                                                          
         PACK  DUB,SROTCOST                                                     
         CVB   R1,DUB                                                           
         ST    R1,COSTOVR          SAVE COST OVERRIDE                           
         MVI   COSTOVSW,C'Y'                                                    
*                                                                               
ROT8     LA    R5,1                                                             
         CLC   SROTSPTS,SPACES                                                  
         BNH   ROT9                                                             
         XC    HALF,HALF                                                        
         MVZ   HALF,SROTSPTS                                                    
         CLC   HALF,ZEROS                                                       
         BNE   EROTSPT                                                          
         PACK  DUB,SROTSPTS                                                     
         CVB   R5,DUB              R5=NUMBER OF SPOTS                           
         LTR   R5,R5                                                            
         BZ    EROTSPT                                                          
*                                                                               
ROT9     XC    FULL,FULL           VALIDATE PRODUCT ALLOCATIONS                 
*                                                                               
         LA    R1,SROTPRD1                                                      
         BAS   RE,VALPRD                                                        
         BNE   EROTPRD1                                                         
         CLI   GLHALF,X'FF'        NO SUCH ESTIMATE FOR PRODUCT 1?              
         BE    EROTEST1            NONE                                         
         MVC   FULL(1),GLHALF      SAVE OFF PRODUCT 1'S BINARY                  
*                                                                               
         LA    R1,SROTPRD2                                                      
         BAS   RE,VALPRD                                                        
         BNE   EROTPRD2                                                         
         CLI   GLHALF,X'FF'        NO SUCH ESTIMATE FOR PRODUCT 2?              
         BE    EROTEST2            NONE                                         
*                                                                               
         CLI   FULL,0              TEST PRODUCT 1 ALLOCATED                     
         BNE   *+12                YES, WE HAVE A PRODUCT 1                     
         CLI   GLHALF,0            NO, PIGGY, IF ANY, WILL BE INVALID           
         BNE   EROTNOPR                                                         
*                                                                               
         MVC   FULL+1(1),GLHALF    PRODUCT 2                                    
         CLI   GLHALF,0            TEST PIGGYBACK                               
         BNE   ROT10                                                            
         MVC   FULL+2(1),BDSEC     SET BUY LENGTH                               
         CLC   SROTLEN1,SPACES     NO-LENGTH IS INVALID                         
         BNH   ROT12                                                            
         CLC   SROTLEN1,ZEROS                                                   
         BNE   EROTSLN                                                          
         B     ROT12                                                            
*                                                                               
ROT10    CLC   SROTLEN1,SPACES     YES-VALIDATE SPOT LENGTH 1                   
         BNH   EROTSLN                                                          
         XC    DUB(3),DUB                                                       
         MVZ   DUB(3),SROTLEN1                                                  
         CLC   DUB(3),ZEROS                                                     
         BNE   EROTSLN                                                          
         PACK  DUB,SROTLEN1                                                     
         CVB   R1,DUB                                                           
         LTR   R1,R1                                                            
         BZ    EROTSLN                                                          
         BAS   RE,VALSLN                                                        
         BNE   EROTSLN                                                          
         STC   R1,FULL+2           LENGTH 1                                     
         ZIC   RF,BDSEC                                                         
         SR    RF,R1                                                            
         BNP   EROTPTNR                                                         
         LR    R1,RF                                                            
         BAS   RE,VALSLN                                                        
         BNE   EROTPTNR                                                         
         STC   R1,FULL+3           LENGTH 2                                     
*                                                                               
*                                  FILL IN SCHEDULE TABLE ENTRY                 
ROT12    MVI   2(R6),0             LEAVE SPOTS EMPTY FOR NOW                    
         CLI   COSTOVSW,C'Y'       COST OVERRIDE                                
         BNE   *+14                                                             
         OI    3(R6),X'80'                                                      
         MVC   4(4,R6),COSTOVR                                                  
         MVC   8(4,R6),FULL        PRD1-PRD2/LEN1-LEN2                          
         OC    AFRSTROT,AFRSTROT   SAVE A(FIRST ROTATION W/ SPOTS)              
         BNZ   *+8                                                              
         ST    R4,AFRSTROT                                                      
*                                                                               
         L     R1,AREC2            BUILD SKED TABLE IN REC2                     
*****    LA    R0,75               OVER 75 IS DEFINITELY TOO MANY               
         LA    R0,209              MAX IS NOW 208, BUT CHECK FOR 1 MORE         
*                                                                               
ROT14    OC    0(SKDTABL,R1),0(R1)                                              
         BNZ   *+18                                                             
         MVC   0(SKDTABL,R1),0(R6) NEW ENTRY                                    
         STC   R5,2(R1)            SPOT COUNT                                   
         B     ROT16                                                            
         ZIC   RF,2(R1)            SAVE N'SPOTS                                 
         MVI   2(R1),0                                                          
         CLC   0(SKDTABL,R1),0(R6) TEST ALL EXCEPT N'SPOTS ARE THE SAME         
         BNE   *+14                                                             
         AR    RF,R5               YES-JUST ACCUMULATE SPOTS                    
         STC   RF,2(R1)                                                         
         B     ROT16                                                            
         STC   RF,2(R1)            RESTORE N'SPOTS                              
         LA    R1,SKDTABL(R1)                                                   
         BCT   R0,ROT14                                                         
         B     EROTMAX                                                          
*                                                                               
ROT16    CLC   PERSTART,0(R1)      SET PERIOD START DATE                        
         BNH   ROT17                                                            
         ST    R1,ASTART                                                        
         BRAS  RE,SETROTDT                                                      
         MVC   PERSTART,HALF                                                    
*                                                                               
ROT17    CLC   PEREND,0(R1)        SET PERIOD END                               
         BNL   ROT2                                                             
         ST    R1,AEND                                                          
         BRAS  RE,SETROTDT                                                      
         MVC   PEREND,HALF                                                      
         B     ROT2                GET NEXT ROTATION OBJECT                     
*                                                                               
ROT18    MVC   ASPOTTAB,AREC2      ADD SPOTS TO BUY                             
         MVI   MAXSPOTS,208                                                     
         BAS   RE,ADDSPOTS                                                      
         BNE   ROTXNE                                                           
         CLI   TOOMANY,C'Y'        TEST TOO MANY SPOTS                          
         BNE   ROTXEQ                                                           
         ICM   R4,15,AFRSTROT      YES-MARK FIRST ROT OBJECT                    
         BZ    EROTOVER                                                         
         MVI   SROTERNO,0                                                       
         MVI   SROTERNO+1,MAXELEMS                                              
         MVI   SROTERF,0                                                        
         B     ROTXNE                                                           
*                                                                               
ROTXEQ   L     RE,SVRE             EXIT CC EQ IF ADDED SPOTS                    
         CR    RE,RE                                                            
         BR    RE                                                               
*                                                                               
ROTXNE   ICM   RE,15,SVRE          EXIT CC NE IF NO SPOTS ADDED                 
         BR    RE                                                               
         SPACE 2                                                                
VALSLN   CLI   SVESLN,0            TEST LIMITED TO ONE SLN                      
         BE    VALSLN2                                                          
         CLM   R1,1,SVESLN                                                      
         BNE   VALSLNER                                                         
*                                                                               
VALSLN2  LA    RF,SLNTAB           ** VALIDATE SPOT LENGTH **                   
         LA    R0,L'SLNTAB                                                      
         CLM   R1,1,0(RF)                                                       
         BER   RE                                                               
         LA    RF,1(RF)                                                         
         BCT   R0,*-10                                                          
VALSLNER LTR   RE,RE                                                            
         BR    RE                                                               
         SPACE 2                                                                
VALPRD   NTR1  ,                   ** VALIDATE PRODUCT **                       
         MVI   GLHALF,0                                                         
         CLC   0(3,R1),SPACES                                                   
         BNH   EQXIT                                                            
         CLC   0(3,R1),=C'AAA'     PRODUCT AAA INVALID                          
         BE    NEXIT                                                            
         L     R6,ASVCLIST         FIND PRODUCT IN CLIENT LIST                  
*                                                                               
VALPRD2  CLC   0(3,R6),0(R1)                                                    
         BE    VALPRD4                                                          
         LA    R6,4(R6)                                                         
         CLI   0(R6),C'A'                                                       
         BNL   VALPRD2                                                          
         B     NEXIT                                                            
*                                                                               
VALPRD4  CLI   3(R6),X'FF'         POL IS INVALID ALLOCATION                    
         BE    NEXIT                                                            
         ZIC   R4,3(R6)            PRD NUM                                      
         STC   R4,GLHALF           RETURN PRODUCT NUMBER IN GLHALF              
         BCTR  R4,0                SUB 1                                        
         SRDL  R4,3                DIVIDE BY 8                                  
         LA    R4,SVPRDEST(R4)     POINT TO TBL ENTRY                           
         LA    R6,X'0100'                                                       
         SRL   R5,29               SHIFT REMAINDER FOR BCT                      
         LA    R5,1(R5)            SET FOR BCT                                  
         SRL   R6,1                                                             
         BCT   R5,*-4                                                           
         EX    R6,*+8              TEST BIT ON                                  
         B     *+8                                                              
         TM    0(R4),0 *EXECUTED*                                               
         BO    EQXIT               PRD IN LIST - ALREADY CHECKED EST            
         XC    KEY,KEY             ELSE CHECK EST HDR EXISTS                    
         L     RE,ADRSVKEY                                                      
         MVC   KEY+1(3),0(RE)      A-M/CLT                                      
         MVC   KEY+4(3),0(R1)      PRD                                          
         MVC   KEY+7(1),9(RE)      EST                                          
         GOTO1 HIGH                                                             
         CLC   KEY(13),KEYSAVE                                                  
         BE    *+12                                                             
         MVI   GLHALF,X'FF'        ESTIMATE NOT ON FILE                         
         B     EQXIT                                                            
         STC   R6,BYTE                                                          
         OC    0(1,R4),BYTE                                                     
         B     EQXIT                                                            
***********************************************************************         
* SET ROTATION DATE BASED ON BUY RECORD'S BDSEDAY                               
*                                                                               
* ON ENTRY:    AREC                A(BUY RECORD)                                
*              (R4)                CURRENT ROTATION OBJECT                      
*                                                                               
* ON EXIT:     HALF                DATE BASED ON BUY'S START DAY #              
***********************************************************************         
SETROTDT NTR1                                                                   
         L     R1,AREC             LOOK IN BUY FOR BDSEDAY                      
         USING BUYREC,R1                                                        
         ZIC   RF,BDSEDAY          GET START DAY # FROM THE BUY                 
         DROP  R1                                                               
         SRL   RF,4                1=MON, 2=TUE, 3=WED, ETC ....                
*                                                                               
         ZIC   RE,SVEOWSDY                                                      
         CLI   SVEOWSDY,0                                                       
         BNE   *+8                                                              
         LA    RE,1                                                             
*                                                                               
         SR    RF,RE               SAME ROTATION START DAY?                     
         BZ    SROTDT10            YES                                          
         BP    *+8                                                              
         AHI   RF,7                                                             
*                                                                               
SROTDT10 ST    RF,DMCB+8                                                        
         GOTO1 VADDAY,DMCB,SROTDATE,WORK,,0  BUMP TO START DAY                  
         GOTO1 VDATCON,DMCB,WORK,(2,HALF)                                       
         J     EXIT                                                             
         EJECT                                                                  
***********************************************************************         
* FIND THE A(SCHEDULE LINE)                                                     
*                                                                               
* ON ENTRY:    (R4)                A(BUY*) LINE                                 
***********************************************************************         
GETSKD   NTR1                                                                   
         XC    ASKEDLIN,ASKEDLIN   A(SCHEDULE LINE)                             
         TM    UPBUYSW,UPBSKD      ANY SKD* OBJECT?                             
         BZ    GETSKDNO            NO, STUCK WITH UPSTART                       
         LR    RE,R4                                                            
GETSKD10 ZICM  R1,0(RE),2                                                       
         LA    RE,2(R1,RE)                                                      
         CLC   10(4,RE),=C'EBY*'   END OF BUY ALREADY?                          
         BNE   *+6                                                              
         DC    H'0'                YES, DIE.DIE.DIE                             
         CLC   10(4,RE),=C'SKD*'   SKD* OBJECT?                                 
         BNE   GETSKD10                                                         
*                                                                               
         ST    RE,ASKEDLIN         SAVE A(SCHEDULE LINE)                        
         B     EQXIT                                                            
*                                                                               
GETSKDNO B     NEXIT                                                            
         EJECT                                                                  
EROTDATE MVI   SROTERNO+1,INVDATE  INVALID DATE                                 
         MVI   SROTERF,SROTDATQ                                                 
         B     EROTX                                                            
*                                                                               
EROTCST  MVI   SROTERNO+1,BADCOST  INVALID COST                                 
         MVI   SROTERF,SROTCOSQ                                                 
         B     EROTX                                                            
*                                                                               
EROTSPT  MVI   SROTERNO+1,INVERR   INVALID SPOT COUNTER                         
         MVI   SROTERF,SROTSPTQ                                                 
         B     EROTX                                                            
*                                                                               
EROTSLN  MVI   SROTERNO+1,SLNERR   INVALID SPOT LENGTH                          
         MVI   SROTERF,SROTLEQ                                                  
         B     EROTX                                                            
*                                                                               
EROTPTNR MVI   SROTERNO+1,PTNRLEN  P/B LENGTH NOT VALID                         
         MVI   SROTERF,SROTLEQ                                                  
         B     EROTX                                                            
*                                                                               
EROTPRD1 MVI   SROTERNO+1,PRDERR   INVALID PRODUCT                              
         MVI   SROTERF,SROTPRDQ                                                 
         B     EROTX                                                            
*                                                                               
EROTPRD2 MVI   SROTERNO+1,PRDERR   INVALID SECOND PRODUCT                       
         MVI   SROTERF,SROTPR2Q                                                 
         B     EROTX                                                            
*                                                                               
EROTNOPR MVI   SROTERNO+1,1        MISSING PRODUCT                              
         MVI   SROTERF,SROTPRDQ                                                 
         B     EROTX                                                            
*                                                                               
EROTEST1 MVI   SROTERNO+1,NOPRDEST BRAND 1 ESTIMATE NOT FOUND                   
         MVI   SROTERF,SROTPRDQ                                                 
         B     EROTX                                                            
*                                                                               
EROTEST2 MVI   SROTERNO+1,NOPRDEST BRAND 2 ESTIMATE NOT FOUND                   
         MVI   SROTERF,SROTPR2Q                                                 
         B     EROTX                                                            
*                                                                               
EROTMAX  MVI   SROTERNO+1,MAXELEMS TOO MANY SPOTS                               
         MVI   SROTERF,0                                                        
         B     EROTX                                                            
*                                                                               
EROTX    MVI   SROTERNO,0                                                       
         B     ROT2                NEXT ROT* OBJECT                             
*                                                                               
EROTOVER L     R4,UPACURBY         TOO MANY SPOTS -                             
         USING SBUYD,R4                                                         
         MVI   SBUYERNO,0          PUT ERROR IN BUY OBJECT                      
         MVI   SBUYERNO+1,MAXELEMS                                              
         MVI   SBUYERF,0                                                        
         B     SKDXNE              AND RETURN                                   
*                                                                               
EROTNONE L     R4,UPACURBY         NO SPOTS AT ALL -                            
         USING SBUYD,R4                                                         
         TM    UPSW,UPNOERR                                                     
         BO    SKDXNE                                                           
         MVI   SBUYERNO,0          PUT ERROR IN BUY OBJECT                      
         MVI   SBUYERNO+1,NOELEMS                                               
         MVI   SBUYERF,0                                                        
         B     SKDXNE              AND RETURN                                   
         EJECT                                                                  
***********************************************************************         
* ADD ORBIT                                                           *         
* RETURN CONDITION CODE - EQ = COMMENTS TO ADD                        *         
*                         NE = NO COMMENTS TO ADD                     *         
***********************************************************************         
         SPACE 1                                                                
ORB      ST    RE,SVRE                                                          
*                                                                               
         TM    UPSW,UPORB          TEST ORBIT SCREEN DISPLAYED                  
         BO    ORB2                                                             
*****  SAVE OFF THE UPDATED BUY RECORD IN AREC3                                 
         L     R0,AREC3            MOVE UPDATED BUY INTO REC3                   
         LHI   R1,4000                                                          
         L     RE,AREC1                                                         
         LR    RF,R1                                                            
         MVCL  R0,RE                                                            
*****  SAVE OFF THE UPDATED BUY RECORD IN AREC3                                 
         LA    R2,BUYINP1H         NO-BRING UP ORBIT SCREEN FIRST               
         MVI   4(R2),0                                                          
         MVI   5(R2),6                                                          
         OI    6(R2),X'80'                                                      
         XC    BUYINP1,BUYINP1                                                  
         LA    R2,BUYINP1                                                       
         ZIC   RE,UPBYLINE         FORMAT IS NNNORB                             
         CVD   RE,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  0(3,R2),DUB                                                      
         MVC   3(3,R2),=C'ORB'                                                  
         OI    UPSW,UPORB                                                       
         CR    RE,RE               SET THE CONDITION CODE TO EQUAL              
         B     ORBX                                                             
*                                                                               
ORB2     DS    0H                                                               
*****  RESTORE UPDATED BUY RECORD IN AREC1                                      
         L     R0,AREC1            RESTORE UPDATED BUY IN REC1                  
         LHI   R1,4000                                                          
         L     RE,AREC3                                                         
         LR    RF,R1                                                            
         MVCL  R0,RE                                                            
*****  RESTORE UPDATED BUY RECORD IN AREC1                                      
         NI    UPSW,255-UPORB      NO LONGER ORBIT DISPLAY                      
         NI    UPBUYSW,255-UPBORB  ORBITS PROCESSED THIS TIME                   
         OI    UPBUYSW2,UPBORADD   INDICATE ADDING ORBITS                       
         LA    R2,BUYINP1H         INPUT LINE = CHANGE                          
         MVI   4(R2),0                                                          
         MVI   5(R2),1                                                          
         OI    6(R2),X'80'                                                      
         XC    BUYINP1,BUYINP1                                                  
         MVI   BUYINP1,C'C'                                                     
         SR    R0,R0                                                            
         LA    RF,4                FIND 1ST UNPROT FIELD AFTER 4                
*                                  INPUT LINES                                  
ORB4     ICM   R0,1,0(R2)                                                       
         BNZ   *+6                                                              
         DC    H'0'                                                             
         AR    R2,R0                                                            
         TM    1(R2),X'20'                                                      
         BO    ORB4                                                             
         BCT   RF,ORB4                                                          
*                                                                               
         L     R4,UPACURBY         FIND THE ORBIT OBJECTS                       
         LA    R6,8                MAX 8 ORBIT INPUT LINES                      
         B     ORB8                                                             
*                                                                               
         USING SORBD,R4                                                         
*                                                                               
ORB6     MVI   4(R2),0                                                          
         MVI   5(R2),0                                                          
***      XC    8(40,R2),8(R2)      CLEAR THE INPUT LINE                         
         XC    8(29,R2),8(R2)      40 BYTES WIPES OUT THE NEXT FIELD            
         LA    R5,8(R2)                                                         
         SR    RF,RF               FORMAT ROTATION                              
         BCTR  RF,0                                                             
         SR    RE,RE                                                            
         LA    R1,SORBPOS                                                       
         LA    R0,7                                                             
*                                                                               
ORB7     CLI   0(R1),C'Y'                                                       
         BNE   *+12                                                             
         SLDL  RE,1                                                             
         B     *+8                                                              
         SLL   RE,1                                                             
         LA    R1,1(R1)                                                         
         BCT   R0,ORB7                                                          
         LTR   RE,RE                                                            
         BZ    EORBROT                                                          
         STCM  RE,1,BYTE                                                        
         GOTO1 VDAYUNPK,DMCB,(0,BYTE),(R5)                                      
         LA    R5,7(R5)                                                         
         LA    R0,8                                                             
         CLI   0(R5),C' '                                                       
         BH    *+14                                                             
         BCTR  R5,0                                                             
         BCT   R0,*-10                                                          
         B     EORBROT                                                          
         MVI   1(R5),C','                                                       
         LA    R5,2(R5)                                                         
*                                                                               
         XC    DUB,DUB             START/END TIMES                              
         MVZ   DUB(4),SORBPSTM                                                  
         CLC   DUB(4),ZEROS                                                     
         BNE   EORBTIMS                                                         
         MVZ   DUB(4),SORBPETM                                                  
         CLC   DUB(4),ZEROS                                                     
         BNE   EORBTIME                                                         
         PACK  DUB,SORBPSTM                                                     
         CVB   R1,DUB                                                           
         CH    R1,=H'3000'         3000 MAX                                     
         BH    EORBTIMS                                                         
         STCM  R1,3,FULL                                                        
         PACK  DUB,SORBPETM                                                     
         CVB   R1,DUB                                                           
         CH    R1,=H'3000'         3000 MAX                                     
         BH    EORBTIME                                                         
         STCM  R1,3,FULL+2                                                      
         CLC   FULL(2),FULL+2      CHECK START NOT LATER THAN END               
         BH    EORBTIMS                                                         
         GOTO1 VUNTIME,DMCB,FULL,(R5)                                           
         LA    R5,10(R5)                                                        
         LA    R0,11                                                            
         CLI   0(R5),C' '                                                       
         BH    *+14                                                             
         BCTR  R5,0                                                             
         BCT   R0,*-10                                                          
         B     EORBTIMS                                                         
         MVI   1(R5),C','                                                       
         LA    R5,2(R5)                                                         
*                                                                               
         LA    R1,SORBPPRG+L'SORBPPRG-1   PROGRAM NAME                          
         LA    RE,L'SORBPPRG                                                    
         CLI   0(R1),C' '                                                       
         BH    *+10                                                             
         BCTR  R1,0                                                             
         BCT   RE,*-10                                                          
         BCTR  RE,0                                                             
         LTR   RE,RE                                                            
         BM    ORB7A                                                            
         EX    RE,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R5),SORBPPRG                                                 
         LA    R5,1(RE,R5)                                                      
*                                                                               
ORB7A    LA    R1,8(R2)            INPUT FIELD LENGTH                           
         SR    R5,R1                                                            
         STC   R5,5(R2)                                                         
         OI    6(R2),X'80'         TRANSMIT                                     
         BCT   R6,*+8              NEXT ORBIT LINE                              
         B     ORB10                                                            
*                                                                               
         SR    R0,R0                ADVANCE TO NEXT UNPROT FIELD                
         ICM   R0,1,0(R2)                                                       
         AR    R2,R0                                                            
         TM    1(R2),X'20'                                                      
         BO    *-10                                                             
*                                                                               
ORB8     SR    R1,R1               ADVANCE TO NEXT OBJECT                       
         ICM   R1,3,0(R4)                                                       
         LA    R4,2(R1,R4)                                                      
         ST    R4,UPACUROB                                                      
         CLC   10(4,R4),=C'EBY*'   TEST END OF BUY                              
         BE    ORB10                                                            
         C     R4,UPANXTBY                                                      
         BNL   ORB10                                                            
         CLC   10(4,R4),=C'ORB*'                                                
         BE    ORB6                                                             
         B     ORB8                                                             
*                                                                               
ORB10    DS    0H                                                               
         OI    MISCFLG1,MF1ORBOK   WE'RE DONE WITH ORBS FOR THIS LINE           
***10    NI    UPBUYSW,255-UPBCOM  PROCESSED ALL COMMENTS                       
***      NI    UPBUYSW,255-UPBCOMNX                                             
*                                                                               
***      CH    R6,=H'4'            TEST ANY COMMENTS THIS SCREEN                
***      BNE   COMY                YES-RETURN TO ADD THE COMMANTS               
         CR    RE,RE               SET EQUAL CONDITIONS                         
*                                                                               
ORBX     L     RE,SVRE                                                          
         BR    RE                                                               
         SPACE 2                                                                
EORBROT  MVI   SORBERNO+1,DAYERR   ROTATION ERROR                               
         MVI   SORBERF,SORBPOSQ                                                 
         B     EORBX                                                            
*                                                                               
EORBTIMS MVI   SORBERNO+1,TIMERR   START TIME ERROR                             
         MVI   SORBERF,SORBPSTQ                                                 
         B     EORBX                                                            
*                                                                               
EORBTIME MVI   SORBERNO+1,TIMERR   END TIME ERROR                               
         MVI   SORBERF,SORBPETQ                                                 
         B     EORBX                                                            
*                                                                               
EORBX    MVI   SORBERNO,0                                                       
         B     ORB8                NEXT ORB* OBJECT                             
         DROP  R4                                                               
         EJECT                                                                  
***********************************************************************         
* BUILD SCHEDULE TABLE                                                *         
* IT HAS ONE ENTRY PER WEEK : +0(2)  ROTATION START DATE              *         
*                             +2(1)  NUMBER OF SPOTS                  *         
*                             +3(1)  INDICATOR  X'80' = COST OVERRIDE *         
*                             +4(4)  COST OVERRIDE                    *         
*                             +8(1)  PRODUCT1                         *         
*                             +9(1)  PRODUCT2                         *         
*                             +10(1) LENGTH1                          *         
*                             +11(1) LENGTH2                          *         
* THIS ROUTINE JUST FILLS IN THE DATES                                *         
***********************************************************************         
         SPACE 1                                                                
         USING SSKDD,R4                                                         
BLDSKD   NTR1                                                                   
         LA    R5,UPDATES                                                       
         LA    R6,SKDTAB           BUILD SCHEDULE DATES TABLE                   
         XC    SKDTAB,SKDTAB                                                    
*                                                                               
         CLC   10(4,R4),=C'SKD*'   NOT A SCHEDULE OBJECT?                       
         BNE   BLDSKD2             NO, START DATE DOESN'T MAKE SENSE            
*                                                                               
         CLC   SSKDSDT,ZEROS       ANY SKED START DATE?                         
         BNH   BLDSKD2             NO                                           
         GOTO1 VDATCON,DMCB,(0,SSKDSDT),(2,HALF)                                
*                                                                               
BLDSKD1  OC    0(2,R5),0(R5)       TEST END OF DATES                            
         BZ    BLDSKD1X                                                         
         OC    2(2,R5),2(R5)       ANY DATES AFTER 0(R5)?                       
         BZ    BLDSKD1A                                                         
         CLC   HALF,2(R5)          1ST SKED DATE IS >= NEXT WEEK?               
         BNL   BLDSKD1B            YES, CHECK NEXT WEEK INSTEAD                 
BLDSKD1A LH    R1,HALF                                                          
         LH    RE,0(R5)                                                         
         SR    R1,RE                                                            
         BM    BLDSKD1X                                                         
         CH    R1,=H'7'            WITHIN 7 DAYS                                
         BNH   BLDSKD2                                                          
BLDSKD1B LA    R5,2(R5)                                                         
         B     BLDSKD1                                                          
*                                                                               
BLDSKD1X LA    R5,UPDATES                                                       
BLDSKD2  OC    0(2,R5),0(R5)       TEST END OF DATES                            
         BZ    BLDSKDX                                                          
         GOTO1 VDATCON,DMCB,(2,(R5)),DUB                                        
         GOTO1 VGETDAY,DMCB,DUB,FULL       GET DAY OF WEEK OF START             
         CLC   FULL(3),SPACES              OF THIS WEEK                         
         BNE   *+6                                                              
         DC    H'0'                                                             
         ZIC   RE,0(R1)                                                         
         ST    RE,DAYOFWK                                                       
         MVC   HALF,0(R5)                                                       
         XR    R1,R1                                                            
         ICM   R1,1,UPDAY1IN       START DAY OF ROTATION                        
         BNZ   *+8                                                              
         LA    R1,1                NONE MEANS MONDAY                            
         SR    R1,RE               GET DATE ROTATION STARTS THIS WEEK           
         BZ    BLDSKD4                                                          
         BP    *+8                                                              
         LA    R1,7(R1)                                                         
         ST    R1,DMCB+8                                                        
         GOTO1 VADDAY,DMCB,DUB,WORK                                             
         CLC   WORK(6),UPEND       TEST AFTER END                               
         BNH   *+14                                                             
         MVC   0(2,R6),=X'FFFF'    YES-INVALID WEEK                             
         B     BLDSKD8                                                          
         GOTO1 VDATCON,DMCB,WORK,(2,HALF)                                       
*                                                                               
BLDSKD4  MVC   0(2,R6),HALF        MOVE DATE TO SCHEDULE TABLE                  
         OC    2(2,R5),2(R5)       TEST THIS IS LAST WEEK                       
****     BZ    BLDSKD6                                                          
         BZ    BLDSKD8             ALLOW IT                                     
         CLC   HALF,2(R5)          NO-IF DATE IS IN NEXT WEEK,                  
         BL    BLDSKD8                                                          
         MVC   0(2,R6),=X'FFFF'    MAKE THIS WEEK INVALID                       
         B     BLDSKD8                                                          
*&&DO                                                                           
BLDSKD6  ZIC   R1,UPDAYXIN         YES-GET ROTATION END DATE                    
         S     R1,DAYOFWK                                                       
         BZ    BLDSKD8                                                          
         BP    *+8                                                              
         LA    R1,7(R1)                                                         
         ST    R1,DMCB+8                                                        
         GOTO1 VADDAY,DMCB,DUB,WORK                                             
         CLC   WORK(6),UPEND       COMPARE TO END DATE                          
         BNH   BLDSKD8                                                          
         MVC   0(2,R6),=X'FFFF'    AFTER - THIS WEEK IN INVALID                 
*&&                                                                             
BLDSKD8  LA    R5,2(R5)            NEXT WEEK                                    
         LA    R6,SKDTABL(R6)                                                   
         B     BLDSKD2                                                          
*                                                                               
BLDSKDX  B     EXIT                                                             
         DROP  R4                                                               
         EJECT                                                                  
***********************************************************************         
* ADD SPOTS TO BUY                                                    *         
***********************************************************************         
ADDSPOTS NTR1  ,WORK=(R4,10)                                                    
**  R4 = A(WORK SPACE FOR PERVAL)                                               
         BAS   RE,DELSPOT          DELETE EXISTING SPOT ELEMENT                 
         OI    UPBUYSW2,UPBREGEL   REGELEMS ABOUT TO BE SUCCESSFULLY            
         LTR   R5,R5               ADDED                                        
         BNZ   *+6                                                              
         DC    H'0'                                                             
         ZIC   R0,1(R5)            SET R5=A(INSERTION POINT FOR SPOTS)          
         AR    R5,R0                                                            
         MVI   TOOMANY,C'N'                                                     
*                                                                               
         OC    PEREND,PEREND       TEST ANY SPOTS                               
         BZ    ADDXNE              NO-EXIT                                      
         LA    R2,BUYINP1H         YES-FORMAT PERIOD CHANGE LINE                
         MVI   4(R2),0                                                          
         OI    6(R2),X'80'                                                      
         XC    BUYINP1,BUYINP1                                                  
         LA    R6,BUYINP1                                                       
         MVC   0(6,R6),=C'C,PER='                                               
         GOTO1 VDATCON,DMCB,(2,PERSTART),(5,6(R6))                              
         MVI   14(R6),C'-'                                                      
*&&DO                                                                           
         CLI   SVCPROF,C'0'        HDR PRODUCT IS POL - TEST TRUE POL           
         BE    ADD03               YES                                          
         L     RF,AEND             NO, ASSUME BRAND POL                         
         S     RF,ASTART                                                        
         LA    R1,SKDTABL                                                       
         SR    RE,RE                                                            
         DR    RE,R1                                                            
         LA    RF,1(RF)                                                         
         CVD   RF,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  15(2,R6),DUB                                                     
         B     ADD06                                                            
*&&                                                                             
ADD03    XC    WORK,WORK                                                        
         GOTO1 VDATCON,DMCB,(2,PERSTART),(5,WORK)                               
         MVI   WORK+8,C'-'                                                      
         GOTO1 VDATCON,DMCB,(2,PEREND),(5,WORK+9)                               
*                                                                               
         L     RF,VCOMFACS                                                      
         L     RF,CPERVAL-COMFACSD(RF)                                          
         XC    0(56,R4),0(R4)                                                   
         GOTO1 (RF),DMCB,(17,WORK),0(R4)                                        
*                                                                               
         XR    RF,RF               GET NUMBER OF WKS FROM PERVAL                
         ICM   RF,3,2(R4)                                                       
         CVD   RF,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  15(2,R6),DUB                                                     
*                                                                               
ADD06    MVC   17(2,R6),=C'W,'                                                  
         MVC   19(8,R6),UPDAYS                                                  
         LA    R1,27                                                            
         LA    RE,UPDAYS+L'UPDAYS-1                                             
         CLI   0(RE),C' '                                                       
         BH    *+10                                                             
         BCTR  RE,0                                                             
         BCT   R1,*-10                                                          
         STC   R1,5(R2)                                                         
*                                                                               
         L     R6,ASPOTTAB         ADD SPOT ELEMENTS                            
         ZIC   R0,MAXSPOTS                                                      
         LR    R2,R5               R2=A(INSERTION POINT FOR SPOTS)              
         LA    R5,ELEM                                                          
         USING REGELEM,R5                                                       
*                                                                               
ADD10    OC    0(2,R6),0(R6)       TEST VALID WEEK DATE                         
         BZ    ADD22                                                            
         CLC   0(2,R6),=X'FFFF'                                                 
         BE    ADD22                                                            
         SR    R4,R4               YES-TEST ANY SPOTS THIS WEEK                 
         ICM   R4,1,2(R6)                                                       
         BZ    ADD22                                                            
         XC    ELEM,ELEM           YES-BUILD ELEMENT                            
         MVI   RCODE,6                                                          
         CLI   SVPRD,X'FF'                                                      
         BNE   *+8                                                              
         MVI   RCODE,X'0B'                                                      
         MVI   RLEN,10                                                          
         MVC   RDATE,0(R6)                                                      
*                                                                               
         CLI   SVPRD,X'FF'                                                      
         BE    ADD12                                                            
*                                                                               
         STC   R4,RNUM             SET NPW IN ELEMENT                           
         LA    R4,1                ADD ONLY ONE ELEMENT                         
         B     ADD20                                                            
*                                                                               
ADD12    TM    3(R6),X'80'         COST OVERRIDE                                
         BZ    *+14                                                             
         OI    RSTATUS,X'20'                                                    
         MVC   RPCOST,5(R6)                                                     
         CLI   8(R6),0             ALLOCATION                                   
         BE    ADD18                                                            
         MVI   RLEN,14                                                          
         MVC   RPPRD,8(R6)                                                      
         MVC   RPTIME,10(R6)                                                    
         CLI   9(R6),0             PIGGYBACK                                    
         BE    ADD18                                                            
         MVI   RLEN,18                                                          
         MVC   RPPRD+4(1),9(R6)                                                 
         MVC   RPTIME+4(1),11(R6)                                               
*                                                                               
ADD18    TM    SVPOLNPW,X'80'      TEST POL NPW BUYING                          
         BZ    ADD20                                                            
         OI    BDSTAT,X'80'        SET FLAG                                     
         SR    RE,RE                                                            
         ICM   RE,7,RPCOST         GET COST OVERRIDE                            
         SLL   R4,18               POL NPW SETS SPOTS IN 6 BITS                 
         OR    RE,R4                                                            
         STCM  RE,7,RPCOST                                                      
         LA    R4,1                                                             
*                                                                               
ADD20    DS    0H                  ADD ELEMENT(S)                               
         CLI   SVCXTRA+8,C'P'      TEST P&G                                     
         BNE   ADD21                                                            
*                                                                               
         MVC   GLHALF(2),RPPRD     MOVE PRD/SLN 1                               
         GOTOR TSTGLS,DMCB,ELEM    TEST GOALS FOR THIS PRD/SLN                  
         JNZ   ERR                                                              
         CLI   RLEN,14             TEST P/B                                     
         BNH   ADD20B                                                           
         MVC   GLHALF,RPPRD+4                                                   
         GOTOR TSTGLS              TEST GOALS FOR THIS PRD/SLN                  
         JNZ   ERR                                                              
*                                                                               
ADD20B   DS    0H                                                               
         GOTO1 VDATCON,DMCB,(2,RDATE),(3,WORK)                                  
         CLC   WORK(L'BDEND),BDEND                                              
         JL    ADD21                                                            
         MVC   BDEND,WORK                                                       
*                                                                               
ADD21    GOTO1 SVRECUP,DMCB,BUYREC,ELEM,(C'R',(R2))                             
         CLI   8(R1),0             TEST RECORD OVERFLOW                         
         BNE   *+12                                                             
         MVI   TOOMANY,C'Y'        YES-FLAG AND EXIT NORMALLY                   
         B     ADDXEQ                                                           
         ZIC   RF,1(R2)                                                         
         AR    R2,RF                                                            
         BCT   R4,ADD21                                                         
*                                                                               
ADD22    LA    R6,SKDTABL(R6)                                                   
         BCT   R0,ADD10            14 WEEKS                                     
*                                                                               
         L     R6,ASTART           FIND MOST FREQUENT NUMBER OF                 
         SR    RE,RE               SPOTS PER WEEK                               
         SR    RF,RF                                                            
         XC    ELEM,ELEM                                                        
*                                                                               
ADD24    IC    RE,2(R6)                                                         
         IC    RF,ELEM(RE)                                                      
         LA    RF,1(RF)                                                         
         STC   RF,ELEM(RE)                                                      
         LA    R6,SKDTABL(R6)                                                   
         C     R6,AEND                                                          
         BNH   ADD24                                                            
         LA    R1,ELEM                                                          
         LA    RE,ELEM+1                                                        
         LA    RF,99                                                            
         MVI   BYTE,0                                                           
*                                                                               
ADD26    CLC   BYTE,0(RE)                                                       
         BNL   *+12                                                             
         MVC   BYTE,0(RE)                                                       
         LR    R1,RE                                                            
         LA    RE,1(RE)                                                         
         BCT   RF,ADD26                                                         
         LA    RE,ELEM                                                          
         SR    R1,RE                                                            
*                                                                               
ADD30    STC   R1,BDNOWK           NUMBER OF SPOTS PER WEEK                     
         STC   R1,BUNPW                                                         
         EJECT                                                                  
*********                                                                       
* MAXIMUM 208 REGELS PER BUYLINE                                                
*********                                                                       
         MVI   BYTE,X'0B'                                                       
         CLI   BUYKEY+3,X'FF'                                                   
         BE    *+8                                                              
         MVI   BYTE,X'06'                                                       
*                                                                               
         LA    RE,BDELEM                                                        
         LA    RF,209              MAX 208 REGELS, CHECK FOR 1 MORE             
         SR    R0,R0                                                            
*                                                                               
ADD32    ICM   R0,1,1(RE)                                                       
         BNZ   *+6                                                              
         DC    H'0'                                                             
         AR    RE,R0                                                            
         CLI   0(RE),0                                                          
         BE    ADDXEQ                                                           
         CLC   0(1,RE),BYTE                                                     
         BNE   ADD32                                                            
         BCT   RF,ADD32                                                         
         MVI   TOOMANY,C'Y'        WE HAVE 209 OR MORE, FLAG AND EXIT           
*                                       NORMALLY  <WHY????>                     
ADDXEQ   B     EQXIT                CC EQ - SPOTS ADDED                         
*                                                                               
ADDXNE   B     NEXIT                CC NE - NO SPOTS ADDED                      
         EJECT                                                                  
***********************************************************************         
* DELETE EXISTING SPOT ELEMENT                                        *         
* RETURNS R5=A(LAST ELEMENT BEFORE SPOTS)                             *         
***********************************************************************         
         SPACE 1                                                                
DELSPOT  LR    R0,RE                                                            
         LA    R6,BDELEM           DELETE ANY EXISTING SPOT ELEMENT             
         SR    R5,R5                                                            
*                                                                               
DEL2     CLI   0(R6),0                                                          
         BE    DELX                                                             
         CLI   0(R6),6                                                          
         BNL   *+10                                                             
         LR    R5,R6               SAVE R5=A(LAST ELEM BEFORE SPOTS)            
         B     DEL6                                                             
         CLI   0(R6),8                                                          
         BNH   DEL4                                                             
         CLI   0(R6),X'0B'                                                      
         BL    DEL6                                                             
         CLI   0(R6),X'1F'                                                      
         BH    DEL6                                                             
*                                                                               
DEL4     DS    0H                                                               
         GOTO1 VRECUP,DMCB,BUYREC,(R6)                                          
         B     DEL2                                                             
*                                                                               
DEL6     ZIC   R1,1(R6)                                                         
         AR    R6,R1                                                            
         B     DEL2                                                             
*                                                                               
DELX     LR    RE,R0                                                            
         BR    RE                                                               
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO PUT BUY RECORD FOR ACTION = CHANGE                       *         
* READS BUY RECORD IN ITS ORIGINAL STATE TO SEE IF ANYTHING'S CHANGED *         
* IF NOT THEN NO ACTION IS TAKEN.                                     *         
***********************************************************************         
         SPACE 1                                                                
PUT      ST    RE,SVRE                                                          
         MVC   SVDMBTS,DMINBTS     SAVE CURRENT DMINBTS                         
         MVI   DMINBTS,X'C8'       SET TO PASS DELETES                          
*                                                                               
         TM    UPBUYSW2,UPBREGEL   TEST REGELEMS SUCCESSFULLY ADDED             
         BO    *+8                                                              
         BAS   RE,DELSPOT          NO-DELETE ANY EXISTING REGELEMS              
         XC    KEY,KEY             GET THE BUY RECORD INTO REC2                 
         MVC   KEY(10),SVKEY                                                    
         MVC   KEY+11(2),UPBYLINE                                               
         GOTO1 HIGH                                                             
         CLC   KEY(13),KEYSAVE                                                  
         BE    PUT1                                                             
* IF THE BUY ISN'T THERE, ADD IT !                                              
         L     R1,AREC1                                                         
         ST    R1,AREC                                                          
         GOTO1 ADDREC                                                           
         B     PUTX                                                             
*                                                                               
PUT1     TM    KEY+13,X'80'        TEST KEY DELETED                             
         BZ    PUT1A                                                            
         NI    KEY+13,X'7F'        UNDELETE IT                                  
         MVC   COMMAND,=C'DMWRT'                                                
         MVI   GBYACT,C'W'         TELL SPGETBUY IT'S DMWRT                     
         GOTO1 DIR                                                              
*                                                                               
PUT1A    L     R6,AREC2                                                         
         ST    R6,AREC                                                          
         GOTO1 GETREC                                                           
         TM    15(R6),X'80'        TEST DELETED                                 
         BZ    PUT1X               NO                                           
* BUILD LIST OF PRODUCTS IN DELETED BUY                                         
         XC    ELEM,ELEM           CLEAR OLD PRD LIST                           
         XC    WORK3,WORK3         CLEAR NEW PRD LIST                           
         L     R6,AREC1                                                         
         USING BUYREC,R6                                                        
         LA    R1,BDELEM-BUYREC(R6)                                             
*                                                                               
PUT1B    CLI   0(R1),0                                                          
         BE    PUT22               DONE WITH RECORD                             
         CLI   0(R1),11            TEST REGEL                                   
         BL    PUT1C               NO                                           
         CLI   0(R1),13                                                         
         BH    PUT1C                                                            
         CLI   1(R1),10            TEST ALLOCATED                               
         BNH   PUT1C               NO                                           
         ZIC   RE,10(R1)                                                        
         LA    RF,WORK3(RE)                                                     
         MVI   0(RF),1                                                          
         CLI   1(R1),14            TEST PIGGYBACK                               
         BNH   PUT1C               NO                                           
         IC    RE,14(R1)                                                        
         LA    RF,WORK3(RE)                                                     
         MVI   0(RF),1                                                          
*                                                                               
PUT1C    SR    R0,R0                                                            
         IC    R0,1(R1)            BUMP TO NEXT ELEMENT                         
         AR    R1,R0                                                            
         B     PUT1B               PUT1B USED TO ALSO BUMP TO NEXTELEM          
*                                                                               
PUT1X    CLI   UPACTN,C'C'         TEST ACTION = CHANGE                         
         BNE   PUT8                                                             
         USING BUYREC,R6           YES---                                       
         LA    R1,BDELEM-BUYREC(R6)  CHECK FOR PAID OR MATCHED SPOTS            
         SR    R0,R0                                                            
*                                                                               
PUT2     CLI   0(R1),0                                                          
         BE    PUT8                                                             
         CLI   0(R1),6                                                          
         BL    PUT6                                                             
         CLI   0(R1),8                                                          
         BNH   PUT4                                                             
         CLI   0(R1),11                                                         
         BL    PUT6                                                             
         CLI   0(R1),13                                                         
         BH    PUT6                                                             
*                                                                               
PUT4     OC    4(2,R1),4(R1)       TEST PAID                                    
         BNZ   PUT97               YES                                          
         LR    RE,R1                                                            
         IC    R0,1(RE)                                                         
         AR    RE,R0                                                            
         CLI   0(RE),X'10'         TEST MATCHED                                 
         BL    PUT6                                                             
         CLI   0(RE),X'18'                                                      
         BNH   PUT98               YES                                          
*                                                                               
PUT6     IC    R0,1(R1)                                                         
         AR    R1,R0                                                            
         B     PUT2                                                             
*                                                                               
PUT8     L     R0,AREC3            MOVE UPDATED BUY INTO REC3                   
         LH    R1,=H'4000'                                                      
         L     RE,AREC1                                                         
         LR    RF,R1                                                            
         MVCL  R0,RE                                                            
         L     R6,AREC2                                                         
         XC    ELEM,ELEM                                                        
         LA    R4,ELEM                                                          
*                                                                               
         USING BUYREC,R6                                                        
PUT10    MVI   BDWHY,0             REMOVE REASONS FOR LAST CHANGE               
         MVI   BDWHY2,0                                                         
         MVI   BDWHY3,0                                                         
         LA    R1,BDELEM           REMOVE ACTIVITY AND UPLOAD ELEMENTS          
         SR    R0,R0                                                            
         XC    DUB,DUB                                                          
*                                                                               
PUT12    CLI   0(R1),0                                                          
         BE    PUT19               DONE WITH RECORD                             
         CLI   0(R1),BUPCODEQ      TEST FOR X'95' ELEMENT                       
         BE    PUT14               YES - DELETE                                 
         CLI   0(R1),X'99'                                                      
         BE    PUT14                                                            
         CLI   0(R1),11            TEST REGEL                                   
         BL    PUT13               NO                                           
         CLI   0(R1),13                                                         
         BH    PUT13                                                            
         CLI   1(R1),10            TEST ALLOCATED                               
         BNH   PUT13               NO                                           
         ZIC   RE,10(R1)                                                        
         LA    RF,0(RE,R4)                                                      
         MVI   0(RF),1                                                          
         CLI   1(R1),14            TEST PIGGYBACK                               
         BNH   PUT13               NO                                           
         IC    RE,14(R1)                                                        
         LA    RF,0(RE,R4)                                                      
         MVI   0(RF),1                                                          
*                                                                               
PUT13    SR    R0,R0                                                            
         IC    R0,1(R1)            BUMP TO NEXT ELEMENT                         
         AR    R1,R0                                                            
         B     PUT12                                                            
*                                                                               
PUT14    CLI   UPACTN,C'C'         FOR ACTION = CHANGE,                         
         BNE   PUT13                                                            
* DELETE 95 AND 99 ELEMENTS                                                     
         LR    R0,R1               SAVE A(LAST ELEMENT)                         
         GOTO1 VRECUP,DMCB,BUYREC,(R0)                                          
         LR    R1,R0               RESTORE A(LAST ELEMENT)                      
*                                                                               
PUT18    B     PUT12                                                            
*                                                                               
PUT19    C     R6,AREC2                                                         
         BNE   PUT20                                                            
         L     R6,AREC3                                                         
         XC    WORK3,WORK3                                                      
         LA    R4,WORK3                                                         
         B     PUT10                                                            
*                                                                               
PUT20    CLI   UPACTN,C'C'         FOR ACTION = CHANGE,                         
         BNE   PUT22                                                            
         L     R6,AREC2            COMPARE ORIGINAL AND UPDATED BUYS            
         L     RE,AREC3                                                         
         LH    R5,BUYRLEN-BUYKEY(R6)                                            
         LH    RF,BUYRLEN-BUYKEY(RE)                                            
         CR    R5,RF                                                            
         BNE   *+10                RECORD LENGTHS DIFFER                        
         CLCL  R4,RE                                                            
         BE    PUTX                NO CHANGE                                    
*                                                                               
*                                  RECORD HAS CHANGED -------                   
PUT22    XC    PRDLIST,PRDLIST     IF ANY PRODUCTS HAVE BEEN ADDED TO           
         LA    R1,PRDLIST          THE BUY, ADD THEM TO PRDLIST FOR             
         LA    RE,1                DATAMGR                                      
         LA    R0,254                                                           
*                                                                               
PUT24    LA    RF,WORK3(RE)                                                     
         CLI   0(RF),0                                                          
         BE    PUT26                                                            
         LA    RF,ELEM(RE)                                                      
         CLI   0(RF),0                                                          
         BNE   PUT26                                                            
         STC   RE,0(R1)                                                         
         LA    R1,1(R1)                                                         
*                                                                               
PUT26    LA    RE,1(RE)                                                         
         BCT   R0,PUT24                                                         
         SR    RE,RE               DATAMGR'S PARM6 = A(PRODUCT LIST)            
         OC    PRDLIST,PRDLIST                                                  
         BZ    *+8                                                              
         LA    RE,PRDLIST                                                       
         ST    RE,DMCB+20                                                       
*                                                                               
         L     R1,AREC1                                                         
         ST    R1,AREC                                                          
         GOTO1 PUTREC                                                           
         B     PUTX                                                             
*                                                                               
PUT97    LA    R1,REPBPERR         PAID SPOTS                                   
         B     PUT99                                                            
*                                                                               
PUT98    LA    R1,REPMTERR         MATCHED SPOTS                                
*                                                                               
PUT99    L     R4,UPACURBY         FLAG THE BUY* OBJECT                         
         USING SBUYD,R4            AND DON'T WRITE UPDATED BUY                  
         MVI   SBUYERNO,0                                                       
         STC   R1,SBUYERNO+1                                                    
         MVI   SBUYERF,0                                                        
*                                                                               
PUTX     MVC   AREC,AREC1          RESTORE AREC                                 
         MVC   DMINBTS,SVDMBTS     RESTORE ORIGINAL DMINBTS                     
         L     RE,SVRE                                                          
         BR    RE                                                               
         EJECT                                                                  
***********************************************************************         
* HDR ERRORS                                                          *         
***********************************************************************         
         SPACE 1                                                                
         USING SHDRD,R4                                                         
EPRD2    MVI   SHDRERNO+1,INVPTNR  PIGGYBACK PRODUCT ERROR                      
         MVI   SHDRERF,SHDRPR2Q                                                 
         B     EEXIT                                                            
*                                                                               
ESTART   MVI   SHDRERNO+1,PERERR   PERIOD START ERROR                           
         MVI   SHDRERF,SHDRSTPQ                                                 
         B     EEXIT                                                            
*                                                                               
EEND     MVI   SHDRERNO+1,PERERR   PERIOD END ERROR                             
         MVI   SHDRERF,SHDRENPQ                                                 
         B     EEXIT                                                            
*                                                                               
EDEMMIS  MVI   SHDRERNO+1,1        MISSING DEMOS                                
         MVI   SHDRERF,SHDRDEMQ                                                 
         B     EEXIT                                                            
*                                                                               
EDEM     MVI   SHDRERNO+1,DEMERR   DEMO ERROR                                   
         LA    R1,SHDRDEMQ                                                      
         AR    R1,R5               R5=DEMO NUMBER (1,2,....,14)                 
         BCTR  R1,0                R5 COULD GO UP TO 20 IF SBY2                 
         STC   R1,SHDRERF                                                       
         B     EEXIT                                                            
*                                                                               
EEXIT    MVI   SHDRERNO,0                                                       
         BAS   RE,END              TERMINATE THE UPLOAD                         
         B     EXIT                                                             
         EJECT                                                                  
***********************************************************************         
* GET TEMPSTR PAGE                                                    *         
***********************************************************************         
         SPACE 1                                                                
READTWA  NTR1  ,                                                                
         CLI   UPCURTWA,10         DON'T READ BEYOND TWA10                      
         BNH   *+6                                                              
         DC    H'0'                                                             
         IC    R4,UPCURTWA         READ TWA3 INTO TIA                           
         SLL   R4,24                                                            
         ICM   R4,3,2(R3)          TERMINAL NUMBER                              
         MVC   DMCB+20(2),=C'L='   LARGE TEMPSTR SIZE                           
         MVC   DMCB+22(2),LENTWA                                                
         GOTO1 VDATAMGR,DMCB,DMREAD,TEMPSTR,(R4),VTIA                           
         CLI   8(R1),0                                                          
         BE    *+6                                                              
         DC    H'0'                                                             
         B     EQXIT                                                            
         EJECT                                                                  
***********************************************************************         
* WRITE TIA BACK TO TEMPSTOR PAGE                                     *         
***********************************************************************         
         SPACE 1                                                                
WRTTWA   NTR1  ,                                                                
         IC    R4,UPCURTWA                                                      
         SLL   R4,24                                                            
         ICM   R4,3,2(R3)          TERMINAL NUMBER                              
         GOTO1 VDATAMGR,DMCB,DMWRITE,TEMPSTR,(R4),VTIA                          
         CLI   8(R1),0                                                          
         BE    *+6                                                              
         DC    H'0'                                                             
         B     EXIT                                                             
         EJECT                                                                  
***********************************************************************         
* BUY ERRORS                                                          *         
***********************************************************************         
         SPACE 1                                                                
         USING SBUYD,R4                                                         
ESKD     TM    UPSW,UPNOERR        C'Y' - NOELEMS NOT AN ERROR                  
         BO    NEXTBUY                                                          
         MVI   SBUYERNO+1,NOELEMS  NO SCHEDULE                                  
         MVI   SBUYERF,0                                                        
         B     NEXTBUY                                                          
*                                                                               
EROT     MVI   SBUYERNO+1,DAYERR   ROTATION ERROR                               
         MVI   SBUYERF,SBUYROTQ                                                 
         B     NEXTBUY                                                          
*                                                                               
EROTST   MVI   SBUYERNO+1,DAYERR   ROTATION START DAY ERROR                     
         MVI   SBUYERF,SBUYRDYQ                                                 
         B     NEXTBUY                                                          
*                                                                               
ETIMST   MVI   SBUYERNO+1,TIMERR   START TIME ERROR                             
         MVI   SBUYERF,SBUYSTMQ                                                 
         B     NEXTBUY                                                          
*                                                                               
ETIMEND  MVI   SBUYERNO+1,TIMERR   END TIME ERROR                               
         MVI   SBUYERF,SBUYETMQ                                                 
         B     NEXTBUY                                                          
*                                                                               
ELEN     MVI   SBUYERNO+1,SLNERR   SPOT LENGTH ERROR                            
         MVI   SBUYERF,SBUYSLNQ                                                 
         B     NEXTBUY                                                          
*                                                                               
ELENUNT  MVI   SBUYERNO+1,INVERR   LENGTH UNIT ERROR                            
         MVI   SBUYERF,SBUYLUNQ                                                 
         B     NEXTBUY                                                          
*                                                                               
ECOST    MVI   SBUYERNO+1,COSTERR  COST ERROR                                   
         MVI   SBUYERF,SBUYCSTQ                                                 
         B     NEXTBUY                                                          
*                                                                               
EPRD1SHR MVI   SBUYERNO+1,SLNERR   PRODUCT 1 TIME SHARE ERROR                   
         MVI   SBUYERF,SBUYP1SQ                                                 
         B     NEXTBUY                                                          
*                                                                               
EMAS     MVI   SBUYERNO+1,BADMAS   MASTER PRODUCT ERROR                         
         MVI   SBUYERF,SBUYMASQ                                                 
         B     NEXTBUY                                                          
*                                                                               
EMAS2    MVI   SBUYERNO+1,BADMAS   MASTER PRODUCT 2 ERROR                       
         MVI   SBUYERF,SBUYMA2Q                                                 
         B     NEXTBUY                                                          
*                                                                               
EMASMIS  MVI   SBUYERNO+1,NOMASPRD MASTER PRODUCT MISSING                       
         MVI   SBUYERF,SBUYMASQ                                                 
         B     NEXTBUY                                                          
*                                                                               
EDEMVAL  L     R1,ADEMOBJ          DEMO VALUE ERROR                             
         USING SDEMD,R1                                                         
         MVI   SDEMERNO,0                                                       
         MVI   SDEMERNO+1,DEMERR                                                
         STC   R5,SDEMERF          R5=DEMO NUMBER                               
         B     NEXTBUY2                                                         
         DROP  R1                                                               
*                                                                               
NEXTBUY  MVI   SBUYERNO,0                                                       
NEXTBUY2 L     R4,UPANXTBY         GO TO NEXT BUY                               
         B     NEXT1                                                            
         EJECT                                                                  
***********************************************************************         
* END OF PROCESSING                                                   *         
***********************************************************************         
         SPACE 1                                                                
END      ST    RE,SVRE                                                          
         MVI   UPMODE,UPMEND       DATA END                                     
         BAS   RE,WRTTWA           WRITE BACK LAST TEMPSTOR PAGE                
         XC    WORK,WORK                                                        
         LA    R1,WORK             CALL GLOBBER WITH TRANSFER CONTROL           
         USING GLVXFRSY,R1         ELEMENT                                      
         MVC   GLVXFRSY,=C'SPO'    FROM SPOT  BUY                               
         MVC   GLVXFRPR,=C'BUY'                                                 
         MVC   GLVXTOSY,=C'CON'    TO CONTROL $MAD                              
         MVC   GLVXTOPR,=C'MAD'                                                 
         OI    GLVXFLG1,GLV1RETN+GLV1RETG   RETURN AND RESTORE MAD SCRN         
         DROP  R1                                                               
         GOTO1 VGLOBBER,DMCB,=C'PUTD',WORK,24,GLVXCTL                           
         CLI   DMCB+8,0                                                         
         BE    ENDX                                                             
         DC    H'0'                                                             
*                                                                               
ENDX     L     RE,SVRE                                                          
         BR    RE                                                               
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO CLEAR ALL UNPROTECTED FIELDS IN THE TWA                  *         
***********************************************************************         
         SPACE 1                                                                
CLEAR    LR    R0,RE                                                            
         LA    R2,BUYMDH                                                        
         SR    RF,RF                                                            
*                                                                               
CLEAR2   TM    1(R2),X'20'         SKIP PROTECTED FIELDS                        
         BO    CLEAR4                                                           
         ICM   RF,1,0(R2)          TOTAL FIELD LENGTH                           
         BZ    CLEARX              0=END OF SCREEN                              
         SH    RF,=H'8'                                                         
         TM    1(R2),X'02'                                                      
         BZ    *+8                                                              
         SH    RF,=H'8'                                                         
         BCTR  RF,0                                                             
         EX    RF,*+8              CLEAR THE FIELD                              
         B     *+10                                                             
         XC    8(0,R2),8(R2)                                                    
         OI    6(R2),X'80'         TRANSMIT                                     
         NI    6(R2),X'BF'         UNSET THE CURSOR                             
         MVI   4(R2),0             CLEAR INPUT INDICATORS                       
         MVI   5(R2),0             LENGTH 0                                     
*                                                                               
CLEAR4   IC    RF,0(R2)            BUMP TO NEXT FIELD                           
         AR    R2,RF                                                            
         B     CLEAR2                                                           
*                                                                               
CLEARX   LR    RE,R0                                                            
         BR    RE                                                               
         EJECT                                                                  
***********************************************************************         
* ROUTINE MOVES DATA TO A TWA FIELD AND SETS APPROPRIATE HEADER BITS  *         
* ON ENTRY, R2=A(FIELD HEADER)                                        *         
*           R5=A(DATA FIELD)                                          *         
*           R1=L'DATA FIELD                                           *         
***********************************************************************         
         SPACE 1                                                                
SETIN    LR    R0,RE                                                            
         LR    RF,R5           RF=A(DATA)                                       
         ZIC   RE,0(R2)                                                         
         SH    RE,=H'8'        RE=L'FIELD                                       
         TM    1(R2),X'02'     TEST FOR EXTENDED HEADER                         
         BZ    *+8                                                              
         SH    RE,=H'8'                                                         
         CR    R1,RE           COMPARE L'DATA TO L'FIELD                        
         BNH   *+6                                                              
         LR    R1,RE           HIGH - USE L'FIELD                               
         AR    RF,R1                                                            
         BCTR  RF,0            RF=A(LAST BYTE THAN CAN FIT IN FIELD)            
         CLI   0(RF),C' '      SCAN BACKWARDS FOR NON SPACE                     
         BH    *+14                                                             
         BCTR  RF,0                                                             
         BCT   R1,*-10                                                          
         B     SETINX         NO INPUT                                          
         STC   R1,5(R2)       INPUT LENGTH                                      
         OI    1(R2),X'01'    MODIFIED BIT                                      
         OI    6(R2),X'80'    TRANSMIT BIT                                      
         BCTR  R1,0                                                             
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   8(0,R2),0(R5)   MOVE THE DATA                                    
         LA    R1,1(R1)        READJUST R1                                      
*                                                                               
SETIN2   CLI   0(RF),C'0'     TEST NUMERIC                                      
         BL    SETINX                                                           
         CLI   0(RF),C'9'                                                       
         BH    SETINX                                                           
         BCTR  RF,0                                                             
         BCT   R1,SETIN2                                                        
         OI    4(R2),X'08'    SET VALID NUMERIC                                 
*                                                                               
SETINX   LR    RE,R0                                                            
         BR    RE                                                               
         EJECT                                                                  
***********************************************************************         
* CONSTANTS AND STORAGE AREAS                                         *         
***********************************************************************         
         SPACE 1                                                                
         DS    0D                                                               
RELO     DS    F                                                                
*                                                                               
LENTWA   DC    H'18432'                                                         
*                                                                               
TEMPSTR  DC    C'TEMPSTR'                                                       
DMREAD   DC    C'DMREAD '                                                       
DMWRITE  DC    C'DMWRT  '                                                       
*                                                                               
ZEROS    DC    CL16'0000000000000000'                                           
         LTORG                                                                  
         EJECT                                                                  
*============================================================*                  
* SUBROUTINE TO TEST GOALS PRESENT FOR WEEK OF SPOT          *                  
* TABLE IS BUILT IN SPBUY01 WHENEVER HEADLINES ARE EDITED    *                  
* GLHALF(2) HAS PRD/SLN TO BE TESTED                         *                  
* AND R1 POINTS TO THE ADDRESS OF THE NEW ELEMENT            *                  
*============================================================*                  
         SPACE 1                                                                
TSTGLS   NTR1  BASE=*,LABEL=*                                                   
         CLI   GLHALF,0            TEST UNALLOCATED                             
         BE    TSTGLX                                                           
                                                                                
* SEARCH THE TABLE                                                              
* PG GOALS HAVE PRD/SLN IN PLACE OF DAYPART                                     
*                                                                               
TSTGL10  MVI   ERRCD,NOGOALS                                                    
         LHI   R4,WSGLTAB-SPBUYWKD   GET DSPL TO GOAL WEEKS                     
         AR    R4,RC                                                            
         LA    R4,WSGLDPTS-WSGLTABD(R4)   POINT TO DPT DATA                     
*                                                                               
TSTGL20  CLC   GLHALF,0(R4)                                                     
         BE    TSTGL30                                                          
         LA    R4,10(R4)                                                        
         CLI   0(R4),0             TEST END OF TABLE                            
         BNE   TSTGL20                                                          
         B     TSTGL80                                                          
                                                                                
* R4 POINTS TO TABLE ENTRY FOR THIS DPT/SLN OR PRD/SLN FOR PG                   
                                                                                
TSTGL30  MVI   ERRCD,0             SET FLAG WE ARE SEARCHING WEEKS              
*                                                                               
TSTGL40  L     R6,0(R1)            POINT TO NEW ELEMENT                         
         LHI   RF,WSGLTAB-SPBUYWKD   GET DSPL TO GOAL DATES                     
         AR    RF,RC                                                            
         SR    RE,RE               CLEAR COUNTER                                
*                                                                               
TSTGL50  CLC   2(2,R6),0(RF)       TEST SPOT IN WEEK                            
         BNH   TSTGL60                                                          
         LA    RF,2(RF)            TABLE ENDS WITH X'FFFF'                      
         BCT   RE,TSTGL50          SO NO TEST FOR END OF TABLE NEEDED           
*                                                                               
TSTGL60  LPR   RE,RE                                                            
         SRDL  RE,3                DIVIDE BY 8                                  
         LA    RE,2(R4,RE)         POINT TO BYTE IN TABLE ENTRY                 
         SRL   RF,29               SHIFT REMAINDER                              
         IC    R0,GLBITTAB(RF)     GET BIT VALUE                                
         STC   R0,BYTE                                                          
         NC    BYTE,0(RE)          TEST BIT ON (W/O CHANGING TABLE)             
         BNZ   TSTGLX                                                           
                                                                                
TSTGL80  CLI   ERRCD,0             TEST WEEKLY ERROR                            
         BNE   TSTGL84                                                          
                                                                                
* BUILD ERROR MESSAGE FOR WEEK WITHOUT GOALS *                                  
                                                                                
TSTGL82  MVI   ERRCD,NEWERRS                                                    
         MVC   NERRCD,=Y(NOGLWK)                                                
         GOTO1 VDATCON,DMCB,(2,2(R6)),(4,ERRTEXT)                               
*                                                                               
TSTGL84  CLI   SVCXTRA+8,C'P'      TEST PG GOALS                                
         BNE   TSTGL86                                                          
         MVI   ERRCD,NEWERRS                                                    
         MVC   NERRCD,=Y(NOPGGOAL)                                              
*****    JE    TSTGLX              IGNORE AS PER  0114544T                      
*                                                                               
TSTGL86 J      NEQXIT                                                           
*                                                                               
TSTGLX   J     EQXIT                                                            
*                                                                               
GLBITTAB DC    X'8040201008040201'                                              
         EJECT                                                                  
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
* WORKING STORAGE                                                     *         
***********************************************************************         
         SPACE 1                                                                
WORKD    DSECT                                                                  
*                                                                               
ADEMOBJ  DS    A                                                                
ANEXTOBJ DS    A                                                                
ASTART   DS    A                                                                
AEND     DS    A                                                                
ASPOTTAB DS    A                                                                
AFRSTSKD DS    A                                                                
ASKEDLIN DS    A                                                                
AFRSTROT DS    A                                                                
SVRE     DS    F                                                                
COSTOVR  DS    F                                                                
DAYOFWK  DS    F                                                                
WORK3    DS    CL256                                                            
SVDMBTS  DS    XL1                                                              
TOTLEN   DS    XL1                                                              
COSTOVSW DS    CL1                                                              
SKDTAB   DS    XL(14*SKDTABL)                                                   
SKDTABL  EQU   12                                                               
PERSTART DS    XL2                                                              
PEREND   DS    XL2                                                              
PERSTDAY DS    XL1                 DAY NUMBER OF THE PERIOD START DATE          
SKEDSTDT DS    CL6                 SKD* START DATE                              
UPID2    DS    CL12                NEW 12 CHARACTER UNIQUE ID (SBY2)            
MAXSPOTS DS    XL1                                                              
ANYSPOTS DS    CL1                                                              
TOOMANY  DS    CL1                                                              
*                                                                               
MISCFLG1 DS    CL1                 MISCELLANEOUS FLAG 1                         
MF1SBY2  EQU   X'80'                - WE ARE DOING SBY2 (20 DEMOS)              
MF1ORBOK EQU   X'40'                - WE HAVE ADDED THE ORBS ALREADY            
*                                                                               
* DEDBLOCK                                                                      
         PRINT OFF                                                              
       ++INCLUDE DEDBLOCK                                                       
         PRINT ON                                                               
*                                                                               
WORKL    EQU   *-WORKD                                                          
         PRINT OFF                                                              
         EJECT                                                                  
       ++INCLUDE SPBUYWORK                                                      
         EJECT                                                                  
       ++INCLUDE DDGLOBEQUS                                                     
         EJECT                                                                  
       ++INCLUDE DDGLVXCTLD                                                     
         EJECT                                                                  
AGYHDRD  DSECT                                                                  
       ++INCLUDE SPGENAGY                                                       
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'044SPBUYUPL  11/19/19'                                      
         END                                                                    
