*          DATA SET DDAPOST    AT LEVEL 029 AS OF 05/01/02                      
*CATALP DDAPOST                                                                 
         TITLE 'DDAPOST- MEDIA/ACCT POSTING MODULE'                             
DDAPOST  CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 80,DDAPOST                                                       
         SPACE 2                                                                
         USING WORKD,RC                                                         
*                                                                               
         L     R8,0(R1)                                                         
         USING DDAPOSTD,R8                                                      
         CLI   0(R1),X'FF'                                                      
         BE    MAEND               CLOSE                                        
         SPACE 2                                                                
*                                  RCVBL POSTING                                
         LA    R2,RCVACC                                                        
         LA    R3,RCVCNTR                                                       
         BAS   RE,BLDHEAD                                                       
         LA    R2,MAPDGRS          DDS BILLINGS                                 
         LA    R3,DEBIT                                                         
         BAS   RE,BLDTRNS                                                       
         MVI   POSTSW,C'R'         REC                                          
         BAS   RE,POST                                                          
         SPACE 3                                                                
*                                  INCOME POSTING                               
         LA    R2,INCACC                                                        
         LA    R3,INCCNTR                                                       
         BAS   RE,BLDHEAD                                                       
         LA    R2,MAPDGRS          DDS BILLINGS                                 
         LA    R3,CREDIT                                                        
         BAS   RE,BLDTRNS                                                       
         MVI   POSTSW,C'I'         INCOME                                       
         BAS   RE,POST                                                          
         SPACE 3                                                                
*                                                                               
MA8      DS    0H                                                               
         B     EXIT                                                             
         SPACE 3                                                                
*                                  WRITE TRAILER RECORD                         
MAEND    DS    0H                                                               
         XC    REC,REC                                                          
         MVC   REC+2(2),=H'2'      SET INITIAL LENGTH                           
         XC    WORK,WORK                                                        
         LA    R7,WORK                                                          
         USING PSSUBFD,R7                                                       
         MVI   PSSBEL,X'52'                                                     
         MVI   PSSBLEN,29                                                       
         MVC   PSSBDESC,=CL15'DDS PRINT BILLS'                                  
         CLI   INCACC+3,C'P'                                                    
         BE    *+10                                                             
         MVC   PSSBDESC+4(11),=C'SPOT BILLS '                                   
         ZAP   PSSBRECS,MAPRECNT                                                
         ZAP   PSSBCASH,MAPCASH                                                 
         LA    R2,REC+4                                                         
         GOTO1 MAPRECUP,DMCB,(X'FF',REC+2),WORK,(R2)                            
*                                                                               
         LH    RF,REC+2            MOVE LENGTH                                  
         LA    RF,2(RF)                                                         
         STH   RF,REC                                                           
         XC    REC+2(2),REC+2                                                   
         GOTO1 MAPWRKR,DMCB,=C'ADD',MAPFILE,MAPID,REC                           
         GOTO1 (RF),(R1),=C'CLOSE'                                              
         B     EXIT                                                             
         EJECT                                                                  
*                                  CREATE HEADER ELEM                           
BLDHEAD  DS    0H                                                               
         XC    HDELEM,HDELEM                                                    
         USING PSHEADD,R7                                                       
         LA    R7,HDELEM                                                        
         MVI   PSHDEL,X'50'                                                     
         MVI   PSHDLEN,70                                                       
         MVI   PSHDANAL,C' '                                                    
         MVI   PSHDANAL+1,C' '                                                  
*                                                                               
         MVC   PSHDACC,0(R2)                                                    
         MVC   PSHDSBAC,0(R3)                                                   
         MVC   PSHDSBNM,15(R3)                                                  
*                                                                               
         BR    RE                                                               
         EJECT                                                                  
*                                  CREATE TRANSACTION ELEM                      
BLDTRNS  DS    0H                                                               
         XC    TRELEM,TRELEM                                                    
         USING TRANSD,R7                                                        
         LA    R7,TRELEM                                                        
         MVI   TRNSEL,X'44'                                                     
         MVI   TRNSLEN,28                                                       
         PACK  DUB(4),MAPDATE(7)                                                
         MVC   TRNSDATE,DUB                                                     
         MVC   TRNSREF,MAPINV                                                   
         MVI   TRNSTYPE,38                                                      
         MVC   TRNSANAL(1),MAPOFF                                               
         MVI   TRNSANAL+1,C' '                                                  
         MVC   TRNSBTCH(1),MAPMOS+1                                             
         ZIC   RF,MAPMOS+3                                                      
         CLI   MAPMOS+2,C'1'                                                    
         BNE   *+8                                                              
         SH    RF,=H'47'                                                        
         STC   RF,TRNSBTCH+1                                                    
         MVC   TRNSBTCH+2(4),=4C' '                                             
*                                                                               
         STC   R3,TRNSSTAT                                                      
         ZAP   TRNSAMNT,0(6,R2)                                                 
         TM    TRNSSTAT,DEBIT                                                   
         BZ    *+10                                                             
         AP    MAPCASH,TRNSAMNT                                                 
         BR    RE                                                               
         EJECT                                                                  
*                                  BUILD RECORD AND ADD TO FILE                 
POST     NTR1                                                                   
         SPACE 2                                                                
         XC    REC,REC                                                          
         MVC   REC+2(2),=H'2'      SET INITIAL LENGTH                           
         LA    R2,REC+4                                                         
         GOTO1 MAPRECUP,DMCB,(X'FF',REC+2),HDELEM,(R2)                          
         ZIC   R0,1(R2)                                                         
         AR    R2,R0                                                            
         GOTO1 (RF),(R1),,TRELEM,(R2)                                           
         ZIC   R0,1(R2)                                                         
         AR    R2,R0                                                            
*                                                                               
POST2    DS    0H                                                               
         CLI   POSTSW,C'I'         INCOME                                       
         BNE   POST8                                                            
         XC    WORK,WORK                                                        
         LA    R7,WORK                                                          
         USING TRCASHD,R7                                                       
         MVI   TRCSEL,X'50'                                                     
         MVI   TRCSLEN,9                                                        
         MVI   TRCSTYPE,C'G'                                                    
         ZAP   TRCSAMNT,MAPAGRS    AGY BILLINGS GROSS                           
         GOTO1 (RF),(R1),,WORK,(R2)                                             
POST8    DS    0H                                                               
         LH    RF,REC+2            MOVE LENGTH                                  
         LA    RF,2(RF)                                                         
         STH   RF,REC                                                           
         XC    REC+2(2),REC+2                                                   
         GOTO1 MAPWRKR,DMCB,=C'ADD',MAPFILE,MAPID,REC                           
         AP    MAPRECNT,=P'1'                                                   
POSTX    DS    0H                                                               
         XIT1                                                                   
         SPACE 3                                                                
EXIT     DS    0H                                                               
         XIT1                                                                   
         SPACE 3                                                                
         LTORG                                                                  
         SPACE 3                                                                
*                                                                               
DDAPOSTD DSECT                                                                  
       ++INCLUDE DDAPOSTW                                                       
*                                                                               
         SPACE 3                                                                
WORKD    DSECT                                                                  
WORK     DS    XL64                                                             
HDELEM   DS    XL100                                                            
TRELEM   DS    XL100                                                            
REC      DS    XL256                                                            
DUB      DS    D                                                                
DMCB     DS    6F                                                               
CDSW     DS    C                                                                
POSTSW   DS    C                                                                
RETAIL   DS    C                                                                
*                                                                               
DEBIT    EQU   X'80'                                                            
CREDIT   EQU   X'00'                                                            
         SPACE 3                                                                
*                                                                               
       ++INCLUDE ACGENPOST                                                      
         SPACE 3                                                                
*                                                                               
       ++INCLUDE ACGENBOTH                                                      
         SPACE 2                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'029DDAPOST   05/01/02'                                      
         END                                                                    
