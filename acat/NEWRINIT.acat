*          DATA SET NEWRINIT   AT LEVEL 009 AS OF 05/01/02                      
*CATALP NEWRINIT                                                                
         TITLE 'NEWRINIT - INITIALIZATION FOR OVERNIGHT'                        
         PRINT NOGEN                                                            
NEWRINIT CSECT                                                                  
         NMOD1 0,**INIT**                                                       
         LA    R9,2048(RB)                                                      
         LA    R9,2048(R9)                                                      
         USING NEWRINIT+4096,R9                                                 
         L     RC,0(R1)                                                         
         USING GEND,RC                                                          
         L     RA,ATWA                                                          
         USING T320FFD,RA                                                       
         L     R8,ASPOOLD                                                       
         USING SPOOLD,R8                                                        
         L     R6,ASYSD                                                         
         USING NETSYSD,R6                                                       
         L     R7,ANETWS2                                                       
         USING OVERD,R7                                                         
         SPACE 1                                                                
         EJECT                                                                  
*              INITIALIZATION ROUTINES                                          
         SPACE 3                                                                
         MVI   NBDATA,C'U'         READ UNIT RECORDS                            
         OI    NBSPLOPT,X'80'      TURN ON SPLIT OPTION                         
         CLI   SPLFLAV,C'E'        IF ESTIMATE FLAVOR                           
         BNE   INIT5                                                            
         CLI   SPLALLU,C'Y'        ALLUNS=Y                                     
         BNE   INIT5                                                            
         OI    NBSPLOPT,X'20'      ASK FOR BILLED SPLITS                        
INIT5    CLI   THOU,C'Y'           OPTION TO STAY IN THOUSANDS                  
         BE    *+8                                                              
         MVI   NBHUNOPT,C'Y'       WE CAN DEAL IN HUNDREDS                      
         MVC   NBREROPT,REROPT     PASS RERATE OPTION                           
         MVI   NBRESUME,NBPROCPK   RESUME READING PACKAGES                      
         SPACE 1                                                                
INITIO   NETGO NSNETIO,DMCB,NETBLOCK      INITIALIZE NETBLOCK                   
         CLI   NBERROR,0                                                        
         BE    *+6                                                              
         DC    H'0'                                                             
         CLI   NBMODE,NBVALDAT                                                  
         BNE   INITIO                                                           
         SPACE 1                                                                
         MVI   NBESTOPT,C'Y'                                                    
         L     RF,=V(TRIMCOLS)                                                  
         A     RF,RELO                                                          
         BASR  RE,RF                                                            
         L     RF,=V(COMPDISP)     COMPUTE DISPLACEMENTS                        
         A     RF,RELO                                                          
         BASR  RE,RF                                                            
         BAS   RE,SETDATE                                                       
         BAS   RE,SETGROUP                                                      
         BAS   RE,SETCPOOL                                                      
         BAS   RE,SETDEMOS                                                      
         BAS   RE,SETSORT          SET UP SORT DETAILS                          
         CLI   SPLFLAV,C'E'                                                     
         BE    IN2                                                              
         CLI   SPLFLAV,C'V'                                                     
         BNE   IN2                                                              
         MVI   NBSELUOP,C'E'                                                    
         CLI   SPLFLAV+1,C'2'      FOR V2 USE ACT SKED                          
         BNE   IN2                                                              
         MVI   NBSELUOP,C'A'                                                    
         MVI   NBESTOPT,C'A'       GET EST DEMOS FOR MGS                        
*                                  IF MISSED UNIT WAS NOT PFB                   
         CLI   PFBOPT,C'Y'         PFB DEMO OPTION                              
         BNE   *+8                                                              
         MVI   NBESTOPT,C'P'                                                    
         SPACE 1                                                                
IN2      CLC   SPLFLAV(2),=C'V3'                                                
         BNE   IN3                                                              
         MVI   NBACTOPT,C'Y'                                                    
         MVI   NBSELUOP,C'A'                                                    
         SPACE 1                                                                
IN3      CLI   SPLFLAV,C'M'                                                     
         BE    IN3B                                                             
         CLI   SPLFLAV,C'P'                                                     
         BNE   IN4                                                              
         SPACE 1                                                                
IN3B     MVI   NBACTOPT,C'Y'                                                    
         MVI   NBESTOPT,C'A'       GET EST DEMOS FOR MGS                        
*                                  IF MISSED UNIT WAS NOT PFB                   
         CLI   PFBOPT,C'Y'         PFB DEMO OPTION                              
         BNE   *+8                                                              
         MVI   NBESTOPT,C'P'                                                    
         CLI   SPLFLAV+1,C'M'                                                   
         BNE   *+8                 IF FLAVOR=PM                                 
         MVI   NBESTOPT,C'Y'       DONT GET DEMOS FOR MISSED,PFBS               
         CLI   SPLFLAV+1,C'A'                                                   
         BNE   *+8                 IF FLAVOR=PA                                 
         MVI   NBESTOPT,C'Y'       DONT GET DEMOS FOR MISSED,PFBS               
         SPACE 1                                                                
IN4      CLI   SPLFLAV,C'E'                                                     
         BNE   XIT                                                              
         MVI   NBUSER+13,C'N'      OVERRIDE PROF. DONT FILT PRE-EMPTS           
         MVI   NBSELUOP,C'A'       USE ACTUAL SCHEDULE UNLESS ALLU=Y            
         CLI   SPLALLU,C'Y'                                                     
         BNE   *+8                                                              
         MVI   NBSELUOP,0                                                       
         CLI   SPLFLAV+1,C'P'      OR FLAVOR IS EP                              
         BNE   *+8                                                              
         MVI   NBSELUOP,0                                                       
         CLI   SPLFLAV+1,C'E'      EE FLAVOR = ESTIMATED SCHEDULE               
         BNE   *+8                                                              
         MVI   NBSELUOP,C'E'                                                    
         SPACE 1                                                                
         CLI   ANYDEMOS,0          MAY NEED DEMO LOOKUP FOR ESTIMATES           
         BE    XIT                                                              
         MVI   NBESTOPT,C'A'                                                    
         CLI   PFBOPT,C'Y'         PFB DEMO OPTION                              
         BNE   *+8                                                              
         MVI   NBESTOPT,C'P'                                                    
         MVI   NBACTOPT,C'Y'                                                    
*******  MVC   NBREVOPT,NBUSER+11  REVALUE OPTION (DISABLED FOR NOW?)           
         B     XIT                                                              
         EJECT                                                                  
*              ROUTINES TO SET UP SORT DETAILS                                  
         SPACE 3                                                                
SETSORT  NTR1                                                                   
         MVI   ANYSORT,C'N'                                                     
         SPACE 1                                                                
*                                  FIGURE OUT FLAVOR AND N'COLUMNS              
         MVI   FLAVOR,C'P'         ESTIMATES (N+1) PACKED                       
         ZIC   RF,NCOLS                                                         
         LA    RF,1(RF)                                                         
         STC   RF,NSORTCOL                                                      
         CLI   SPLFLAV,C'E'                                                     
         BE    SETSORT2                                                         
         MVI   FLAVOR,C'B'         EVALUATION - 40 BINARY                       
         MVI   NSORTCOL,40                                                      
         CLI   SPLFLAV,C'V'                                                     
         BE    SETSORT2                                                         
         MVI   NSORTCOL,50         OTHERS - 50 BINARY                           
         SPACE 1                                                                
SETSORT2 ZIC   R2,NSORTCOL         NOW FIGURE OUT L'SORT REC                    
         SLL   R2,2                                                             
         CLI   FLAVOR,C'P'                                                      
         BNE   *+8                                                              
         SLL   R2,1                WIDTH OF COLUMNS                             
         ST    R2,COLWIDE                                                       
         LA    R2,32+96+80(R2)     +PAGE +KEY +COMMENTS                         
         LA    R3,RECCARD+21                                                    
         EDIT  (R2),(3,0(R3)),FILL=0                                            
         GOTO1 SORTER,DMCB,SORTCARD,RECCARD,0                                   
         B     XIT                                                              
         SPACE 1                                                                
SORTCARD DC    CL80'SORT FIELDS=(1,128,A),FORMAT=BI,WORK=1'                     
RECCARD  DC    CL80'RECORD TYPE=F,LENGTH=NNN'                                   
         EJECT                                                                  
*              ROUTINES TO SET UP DATE STACKS                                   
         SPACE 3                                                                
SETDATE  NTR1                                                                   
         MVC   NBPEROVR,PEROPT     OPTIONAL PROFILE OVERRIDE                    
         MVI   NREPTYP,C'M'        SET REPORT TYPE TO MEDIA                     
         CLI   SPLFLAV,C'E'                                                     
         BNE   *+8                                                              
         MVI   NREPTYP,C'A'        OR ACCOUNTING                                
         XC    PERTYPE,PERTYPE                                                  
         SPACE 1                                                                
         MVI   PERTYPE,C'M'        BUILD MONTH LIST                             
         XC    MLIST,MLIST                                                      
         NETGO NVWKLST,DMCB,=F'24',MLIST,PERTYPE                                
         SPACE 1                                                                
         MVI   PERTYPE,C'W'        THEN WEEK LIST                               
         XC    WLIST(200),WLIST                                                 
         XC    WLIST+200(221),WLIST+200                                         
         NETGO NVWKLST,DMCB,=F'105',WLIST,PERTYPE                               
         LA    R2,MLIST                                                         
         LA    R3,QLIST                                                         
         LA    R4,8                                                             
         SPACE 1                                                                
SD4      MVC   0(4,R3),0(R2)       BUILD QUARTER LIST FROM MONTHS               
         CLI   4(R2),X'FF'                                                      
         BE    SD6                                                              
         CLI   4(R2),0                                                          
         BE    SD6                                                              
         MVC   2(2,R3),6(R2)                                                    
         CLI   8(R2),X'FF'                                                      
         BE    SD6                                                              
         CLI   8(R2),0                                                          
         BE    SD6                                                              
         MVC   2(2,R3),10(R2)                                                   
         CLI   12(R2),X'FF'                                                     
         BE    SD6                                                              
         CLI   12(R2),0                                                         
         BE    SD6                                                              
         LA    R2,12(R2)                                                        
         LA    R3,4(R3)                                                         
         BCT   R4,SD4                                                           
         SH    R3,=H'4'                                                         
         SPACE 1                                                                
SD6      MVI   4(R3),X'FF'                                                      
         BAS   RE,EDLIST                                                        
         B     XIT                                                              
         EJECT                                                                  
*              ROUTINE TO EXPAND DATE LISTS TO EDITED FORMAT                    
         SPACE 3                                                                
EDLIST   NTR1                                                                   
         LA    R2,WLIST                                                         
         LA    R3,EWLIST                                                        
         MVI   DUB,C'W'                                                         
         BAS   RE,EL2                                                           
         LA    R2,MLIST                                                         
         LA    R3,EMLIST                                                        
         MVI   DUB,C'M'                                                         
         BAS   RE,EL2                                                           
         LA    R2,QLIST                                                         
         LA    R3,EQLIST                                                        
         MVI   DUB,C'Q'                                                         
         BAS   RE,EL2                                                           
         B     XIT                                                              
         SPACE 1                                                                
EL2      NTR1                                                                   
         SPACE 1                                                                
EL4      CLI   DUB,C'M'            WEEK AND QUARTER                             
         BE    EL6                                                              
         GOTO1 DATCON,DMCB,(2,0(R2)),(4,0(R3))                                  
         LA    R3,5(R3)                                                         
         CLI   DUB,C'W'            QUARTER ONLY                                 
         BE    EL8                                                              
         MVI   0(R3),C'-'                                                       
         GOTO1 DATCON,DMCB,(2,2(R2)),(4,1(R3))                                  
         LA    R3,6(R3)                                                         
         B     EL8                                                              
         SPACE 1                                                                
EL6      GOTO1 DATCON,DMCB,(2,0(R2)),(0,WORK)     MONTH                         
         CLI   PEROPT,C'C'         PERIOD OPTION OVERRIDE                       
         BE    EL7                                                              
         CLI   PEROPT,C'S'                                                      
         BE    EL66                                                             
         CLI   PEROPT,C'B'                                                      
         BE    EL66                                                             
         CLI   SPLFLAV,C'E'        IF FLAV=EST USE NBUSER+2                     
         BNE   EL65                                                             
         CLI   NBUSER+2,C'C'       IF CALENDAR MONTH WE HAVE MONTH              
         BE    EL7                 ELSE MAKE SURE BY BUMPING END                
         B     EL66                                                             
*                                  ELSE USE NBUSER+3                            
EL65     CLI   NBUSER+3,C'C'       IF CALENDAR MONTH WE HAVE MONTH              
         BE    EL7                 ELSE MAKE SURE BY BUMPING END                
         SPACE 1                                                                
EL66     GOTO1 DATCON,DMCB,(2,2(R2)),(0,WORK)                                   
         GOTO1 GETDAY,DMCB,WORK,WORK+6                                          
         ZIC   R4,DMCB             TO THE END OF THE BROADCAST WEEK             
         ZIC   R5,NBUSER+4         (START DOW)                                  
         LTR   R5,R5                                                            
         BNZ   *+8                                                              
         LA    R5,1                (DEFAULT IS MONDAY)                          
         BCT   R5,*+8              (PREVIOUS DAY)                               
         LA    R5,7                (BUT MONDAY ENDS ON SUNDAY)                  
         SR    R5,R4                                                            
         BZ    EL7                                                              
         BP    *+8                                                              
         AH    R5,=H'7'                                                         
         GOTO1 ADDAY,DMCB,WORK,WORK+6,(R5)                                      
         MVC   WORK(6),WORK+6                                                   
         SPACE 1                                                                
EL7      PACK  WORK+8(8),WORK+2(2)                                              
         CVB   R1,WORK+8                                                        
         MH    R1,=H'3'                                                         
         LA    R1,MONTHS-3(R1)                                                  
         MVC   0(3,R3),0(R1)                                                    
         MVC   3(2,R3),WORK                                                     
         LA    R3,5(R3)                                                         
         SPACE 1                                                                
EL8      LA    R2,4(R2)                                                         
         CLI   0(R2),0                                                          
         BNE   *+8                                                              
         MVI   0(R2),X'FF'                                                      
         CLI   0(R2),X'FF'                                                      
         BNE   EL4                                                              
         B     XIT                                                              
         EJECT                                                                  
*              ROUTINE TO SET UP PRODUCT GROUP                                  
         SPACE 3                                                                
SETGROUP NTR1                                                                   
         MVC   CODEDISP,=H'10'     PRESET DISPLACEMENTS                         
         MVC   NAMEDISP,=H'18'                                                  
         CLI   SPLPRO+1,C'='                                                    
         BNE   SGXIT                                                            
         NETGO NVSETSPT,DMCB                                                    
         USING PRDHDR,R4                                                        
         LA    R4,KEY                                                           
         L     R2,=V(GROUPLST)                                                  
         A     R2,RELO                                                          
         XC    KEY,KEY                                                          
         MVC   KEY+1(3),NBACTAM                                                 
         SPACE 1                                                                
SG2      LA    R4,KEY                                                           
         AI    PKEYPRD+2,1         SKIP TO NEXT PRODUCT                         
         MVC   FILENAME,=C'SPTDIR  '                                            
         GOTO1 HIGH                                                             
         CLC   KEY(4),KEYSAVE                                                   
         BNE   SG4                                                              
         MVC   FILENAME,=C'SPTFIL  '                                            
         GOTO1 GETREC                                                           
         LA    R4,IO                                                            
         MVC   0(1,R2),PCODE+1     PRODUCT CODE                                 
         MVC   1(3,R2),PGRP1       FIND MATCH OF PRODUCT GROUP SCHEME           
         CLC   SPLPRO(1),1(R2)     MAY BE FIRST                                 
         BE    SG3                                                              
         MVC   1(3,R2),PGRP2       OR SECOND                                    
         CLC   SPLPRO(1),1(R2)                                                  
         BE    SG3                                                              
         MVC   1(3,R2),PGRP3       OR THIRD                                     
         CLC   SPLPRO(1),1(R2)                                                  
         BE    SG3                                                              
         MVC   1(3,R2),PGRP4       OR FOURTH                                    
         CLC   SPLPRO(1),1(R2)                                                  
         BE    SG3                                                              
         MVC   1(3,R2),PGRP5       OR FIFTH                                     
         CLC   SPLPRO(1),1(R2)                                                  
         BE    SG3                                                              
         XC    1(3,R2),1(R2)                                                    
         SPACE 1                                                                
SG3      LA    R2,4(R2)                                                         
         B     SG2                                                              
         SPACE 1                                                                
SG4      MVI   0(R2),X'FF'                                                      
         LA    R4,KEY              GET SCHEME RECORD                            
         XC    KEY,KEY                                                          
         USING PRGKEY,R4                                                        
         MVC   PRGKTYP,=X'0D01'                                                 
         MVC   PRGKAGMD(3),NBACTAM                                              
         MVC   PRGKID,SPLPRO                                                    
         MVC   FILENAME,=C'SPTDIR  '                                            
         GOTO1 READ                                                             
         MVC   FILENAME,=C'SPTFIL  '                                            
         GOTO1 GETREC                                                           
         LA    R2,IO                                                            
         MVI   ELCODE,X'01'                                                     
         BAS   RE,GETEL                                                         
         USING PRGEL01,R2                                                       
         MVC   BREAK,PRGBK1        DIG OUT BREAK NAME AND LENGTH                
         OC    BREAK,SPACES                                                     
         CLC   BREAK+9(3),SPACES   ALLOW FOR BIG BREAK NAME                     
         BE    *+16                                                             
         MVC   CODEDISP,=H'13'                                                  
         MVC   NAMEDISP,=H'20'                                                  
         MVC   BRLEN,PRGBK1LN                                                   
         MVC   BREAK2,SPACES                                                    
         MVI   BRLEN2,0                                                         
         CLI   PRGBK2LN,0                                                       
         BE    SGXIT                                                            
         MVC   BREAK2,PRGBK2                                                    
         OC    BREAK2,SPACES                                                    
         CLC   BREAK2+9(3),SPACES                                               
         BE    *+16                                                             
         MVC   CODEDISP,=H'13'                                                  
         MVC   NAMEDISP,=H'20'                                                  
         MVC   BRLEN2,BRLEN                                                     
         AC    BRLEN2,PRGBK2LN                                                  
         SPACE 1                                                                
SGXIT    XC    FILENAME,FILENAME   RESET TO UNT FILE                            
         NETGO NVSETUNT,DMCB                                                    
         B     XIT                                                              
         EJECT                                                                  
*              ROUTINE TO SET UP COST POOL FOR PRODUCTS                         
         SPACE 3                                                                
SETCPOOL NTR1                                                                   
         CLC   ACCPCTG,=C'COST'                                                 
         BNE   XIT                                                              
         NETGO NVSETSPT,DMCB                                                    
         USING PRDHDR,R4                                                        
         LA    R4,KEY                                                           
         L     R2,=V(COSTPOOL)                                                  
         A     R2,RELO                                                          
         XC    KEY,KEY                                                          
         MVC   KEY+1(3),NBACTAM                                                 
         SPACE 1                                                                
SCP2     LA    R4,KEY                                                           
         AI    PKEYPRD+2,1         SKIP TO NEXT PRODUCT                         
         MVC   FILENAME,=C'SPTDIR  '                                            
         GOTO1 HIGH                                                             
         CLC   KEY(4),KEYSAVE                                                   
         BNE   SCPXIT                                                           
         MVC   FILENAME,=C'SPTFIL  '                                            
         GOTO1 GETREC                                                           
         LA    R4,IO                                                            
         ZIC   R3,PCODE+1          PICK UP PRODUCT CODE                         
         BCTR  R3,0                                                             
         SLL   R3,2                                                             
         AR    R3,R2               DISPLACE INTO COST POOL                      
         MVC   DUB(4),PBILLCOM     (SIGNED) COMMISSION RATE                     
         L     R1,DUB                                                           
         TM    PBILLBAS,X'01'      COMMISSION BASED ON NET?                     
         BNO   SCP4                                                             
         M     R0,=F'85'                                                        
         D     R0,=F'100'                                                       
         SPACE 1                                                                
SCP4     L     R0,=F'1000000'                                                   
         TM    PBILLBAS,X'10'      COST BASED ON NET?                           
         BNO   *+8                                                              
         L     R0,=F'850000'                                                    
         AR    R1,R0                                                            
         ST    R1,0(R3)                                                         
         B     SCP2                                                             
         SPACE 1                                                                
SCPXIT   XC    FILENAME,FILENAME   RESET TO UNT FILE                            
         NETGO NVSETUNT,DMCB                                                    
         B     XIT                                                              
         EJECT                                                                  
*              SEE IF WE NEED DEMO LIST FOR PRODUCT=ALL                         
         SPACE 3                                                                
SETDEMOS NTR1                                                                   
         MVI   SEPTARGS,C'N'       NOT NEEDED                                   
         CLC   SPLPRO(3),=C'ALL'   UNLESS THERE IS PRODUCT=ALL                  
         BNE   XIT                                                              
         CLC   SPLCLI(3),=C'ALL'   NOT FOR CLIENT=ALL                           
         BE    XIT                                                              
         SPACE 1                                                                
SETDEM2  CLI   SPLDEMH+5,0         NOT NEEDED IF DEMOS OVERRIDDEN               
         BNE   XIT                                                              
         CLI   SPLFLAV,C'E'        OR FOR ESTIMATE FLAVOR                       
         BNE   SEPDEM4                                                          
         CLI   ANYDEMOS,0          WHEN NO DEMOS                                
         BE    XIT                                                              
         SPACE 1                                                                
SEPDEM4  MVI   SEPTARGS,C'Y'                                                    
         SPACE 1                                                                
         L     R2,=V(GOALBLOK)     GOING TO USE NETGOAL TO BUILD LIST           
         A     R2,RELO                                                          
         USING NETGOALD,R2                                                      
         XC    NGBLOCK,NGBLOCK                                                  
         LA    R1,NETBLOCK                                                      
         ST    R1,NGANTBLK                                                      
         L     R1,=V(TARLIST)      TARGET LIST                                  
         A     R1,RELO                                                          
         ST    R1,ATARLIST                                                      
         ST    R1,NGAPLIST                                                      
         MVI   NGMAXPRD,200                                                     
         MVI   NGFUNCT,NGBUILD                                                  
         MVI   NGEXTOPT,8                                                       
         GOTO1 NBCALLOV,DMCB,0,X'D9000A35'                                      
         L     RF,DMCB                                                          
         GOTO1 (RF),DMCB,NGBLOCK                                                
         B     XIT                                                              
         EJECT                                                                  
*              ODDMENTS FOR MAIN CSECT                                          
         SPACE 3                                                                
XIT      XIT1                                                                   
         SPACE 1                                                                
         GETEL (R2),DATADISP,ELCODE                                             
         SPACE 1                                                                
RELOC    DC    A(*)                                                             
FF       DC    X'FFFFFFFFFFFFFFFF'                                              
         LTORG                                                                  
         EJECT                                                                  
       ++INCLUDE NEWRIOVERD                                                     
*              SPGENEST            INCLUDES HERE                                
*              SPGENPRG                                                         
*              SPGENCLT                                                         
*              SPGENPRD                                                         
*              NEGENUNIT                                                        
*              NEMEDFFD                                                         
*              NEMEDF6D                                                         
*              NETINCLS                                                         
*              NECOMBLOK                                                        
*              NEGENCOM                                                         
*              DDBIGBOX                                                         
*              NENETGOALD                                                       
         PRINT OFF                                                              
DUMMYD   DSECT                                                                  
       ++INCLUDE SPGENEST                                                       
       ++INCLUDE SPGENPRG                                                       
CLIDSECT DSECT                                                                  
       ++INCLUDE SPGENCLT                                                       
PDDUMMY  DSECT                                                                  
       ++INCLUDE SPGENPRD                                                       
       ++INCLUDE SPGENBUY                                                       
       ++INCLUDE NEGENPACK                                                      
       ++INCLUDE NEGENUNIT                                                      
       ++INCLUDE NEWRIFFD                                                       
         ORG   CONTAGH                                                          
       ++INCLUDE NEWRIF6D                                                       
       ++INCLUDE NETINCLS                                                       
       ++INCLUDE NECOMBLOK                                                      
       ++INCLUDE NEGENCOM                                                       
       ++INCLUDE DDBIGBOX                                                       
       ++INCLUDE NENETGOALD                                                     
         PRINT ON                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'009NEWRINIT  05/01/02'                                      
         END                                                                    
