*          DATA SET SPMEDPRDRN AT LEVEL 003 AS OF 11/12/03                      
*CATALP MEDPRDRN  <<<<<<=======                                                 
         SPACE 1                                                                
*=====================================================================*         
* THIS IS A NEW VERSION USED ONLY WHEN RQPOLFLT=Y.                    *         
* SPFILCON DYNAMICALLY REPLACES THE ADDRESS                           *         
* IT CAN SUPPORT 25 DEMOS FOR NETWORK, BUT ACTUALLY SUPPORTS 21       *         
* IT ALLOWS A FLAG MAINLY FOR ALLOCATIONS THAT SAYS TO IGNORE PRODUCT *         
* FILTERS AND USE ONLY THE POL ESTIMATE FILTERS (RQPOLFLT)            *         
* THE EXPANDED BUFFER SIZE FOR LARGER ENTRIES IS LIVE                 *         
* MHER 12JUN00                                                        *         
*=====================================================================*         
         TITLE 'MEDIA SUMMARY - BUILD PRODUCT/DEMO LIST'                        
*                                                                               
* PARAMETERS                       REGISTER USAGE                               
*  1 = A(WORKC)                    RA-RC     A(WORKC)                           
*                                  R2        IO AREA                            
*                                  R3        PRODUCT BUFFER                     
*                                  R4        RECORD DSECTS                      
*                                                                               
VMDPRDRN CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 WORKX-WORKD,**MPRDRD                                             
         LR    R2,RC                                                            
         USING WORKD,R2                                                         
         L     RA,0(R1)                                                         
         MVC   EST#,0(R1)          SET FILTER ESTIMATE                          
         LA    RC,2048(RA)                                                      
         LA    RC,2048(RC)                                                      
         USING SPWORKD,RA,RC                                                    
*                                                                               
         LA    R5,IOA                                                           
         ST    R5,AREC                                                          
         MVC   IOANAME,=C'*IOAIOA*'   SET EYECATCHER                            
*                                                                               
         XC    SVPOLFLT,SVPOLFLT                                                
*                                                                               
         LA    R3,PTBUILD                                                       
         USING PTBUFFD,R3                                                       
         XC    PTBUILD,PTBUILD                                                  
*                                                                               
         MVC   PRDBUFLN,=Y(PTBUFFL) SET PRDBUFF ENTRY LEN                       
         L     RF,VMASTC                                                        
         USING MASTD,RF                                                         
         CLI   MCNETPAK,C'Y'                                                    
         BNE   PR0                                                              
         MVC   PRDBUFLN,=Y(PTXBUFFL)  NETPAK USES LONGER ENTRIES                
         DROP  RF                                                               
* ALWAYS CLEAR EXTENDED LENGTH BUFFER                                           
PR0      L     RE,PRDBUFF                                                       
         LHI   RF,255*PTXBUFFL     MAX PRDS                                     
         SR    R0,R0                                                            
         SR    R1,R1                                                            
         MVCL  RE,R0               CLEAR PRDBUFF                                
         SPACE 1                                                                
*                                                                               
         CLI   QDEMOVRD,C'Y'       CHECK DEMO MENU IN REQ                       
         BNE   PR8                                                              
*                                                                               
         XC    KEY,KEY                                                          
         LA    R5,KEY                                                           
         USING DMNRECD,R5                                                       
         MVC   DMNKTYP,=X'0D26'                                                 
         MVC   DMNKAGMD,BAGYMD                                                  
         MVC   DMNKCODE,QDEMNOS    MENU CODE                                    
         GOTO1 READ                                                             
         GOTO1 GET                                                              
*                                                                               
         L     R5,AREC                                                          
         LA    R5,24(R5)                                                        
         LA    R6,PTDEMO                                                        
PR2      DS    0H                                                               
         CLI   0(R5),0                                                          
         BE    PR8                                                              
         CLI   0(R5),X'05'                                                      
         BNE   PR4                                                              
*                                                                               
         USING DMNEL05,R5                                                       
         MVC   0(3,R6),DMNRTN                                                   
         LA    R6,3(R6)                                                         
*                                                                               
PR4      DS    0H                                                               
         SR    R0,R0                                                            
         IC    R0,1(R5)                                                         
         AR    R5,R0                                                            
         B     PR2                                                              
         DROP  R5                                                               
         EJECT                                                                  
*=================================================================*             
* IF POL REQUEST WITH ESTIMATE FILTERS, GET POL ESTIMATE NOW      *             
*=================================================================*             
         SPACE 1                                                                
PR8      DS    0H                                                               
         CLC   QPRD,=C'POL'                                                     
         BNE   PR20                                                             
         CLC   QEST(2),=C'NO'                                                   
         BNE   PR20                                                             
*                                                                               
         XC    KEY,KEY                                                          
         LA    R4,KEY                                                           
         USING ESTHDR,R4                                                        
         MVC   EKEYAM,BAGYMD                                                    
         MVC   EKEYCLT,BCLT                                                     
         MVC   EKEYPRD,=C'POL'                                                  
         MVI   EKEYEST,1                                                        
         CLI   EST#,0                                                           
         BE    PR10                                                             
         MVC   EKEYEST,EST#                                                     
*                                                                               
PR10     GOTO1 HIGH                                                             
         B     PR14                                                             
*                                                                               
PR12     GOTO1 SEQ                                                              
*                                                                               
PR14     LA    R4,KEY                                                           
         CLC   KEY(7),KEYSAVE      ANY MORE POL ESTIMATES                       
         BNE   EXIT                IF NO POL ESTIMATES, NO ESTIMATES            
         CLI   EST#,0              TEST ESTIMATE FILTER SET                     
         BE    *+14                                                             
         CLC   EKEYEST,EST#        YES - IT MUST MATCH                          
         BNE   EXIT                                                             
*                                                                               
         GOTO1 GET                                                              
         L     R4,AREC                                                          
*                                                                               
         CLC   EEND,QSTART         DATE FILTERS                                 
         BL    PR12                                                             
         CLC   ESTART,QEND                                                      
         BH    PR12                                                             
*                                                                               
         BAS   RE,FILTER                                                        
         BNE   PR12                                                             
         MVC   SVPOLFLT,EPROF      SAVE POL FILTER VALUES                       
         EJECT                                                                  
*================================================================*              
* BUILD A LIST OF PRODUCTS THAT HAVE ESTIMATES FOR THIS REQUEST  *              
*================================================================*              
         SPACE 1                                                                
PR20     XC    KEY,KEY                                                          
         MVC   KEY+1(1),BAGYMD                                                  
         MVC   KEY+2(2),BCLT                                                    
         MVI   ONEPRD,C'N'         SET MULTIPRD FLAG                            
         CLC   QPRD,=C'ALL'                                                     
         BE    PR30                                                             
         CLC   QPRD,=C'POL'                                                     
         BE    PR30                                                             
         CLI   QPRD,C'0'           TEST PRDGRP                                  
         BNL   PR30                                                             
* SINGLE PRODUCT REQUEST                                                        
         MVI   ONEPRD,C'Y'                                                      
         MVC   KEY+4(3),QPRD                                                    
         GOTO1 HIGH                                                             
         CLC   KEY(7),KEYSAVE                                                   
         BE    *+6                                                              
         DC    H'0'                                                             
         B     PR40                                                             
*                                                                               
* MULTIPLE PRD REQUEST                                                          
*                                                                               
PR30     MVC   KEY+7(6),=6X'FF'                                                 
*                                                                               
         GOTO1 HIGH                                                             
         CLC   KEY(4),KEYSAVE      SAME 00/A-M/CLT                              
         BNE   EXIT                                                             
                                                                                
PR40     GOTO1 GET                 READ PRDHDR                                  
*                                                                               
         L     R4,AREC                                                          
         USING PRDHDR,R4                                                        
* SAVE STUFF IN PTBUILD                                                         
         MVC   PTPRDA,PKEYPRD                                                   
         MVC   PTPRDN,PCODE+1                                                   
         MVC   PTNAME,PNAME                                                     
         MVC   PTCLASS,PCLASS                                                   
         EJECT                                                                  
*=============================================================*                 
* READ ESTIMATES FOR THIS PRODUCT                                               
*=============================================================*                 
         SPACE 1                                                                
         MVC   KEY+7(1),BEST       SET LOW ESTIMATE                             
         CLI   BEST,0              TEST EST=NO REQUEST                          
         BE    PR50                GO PROCESS                                   
         SPACE 1                                                                
*=============================================================*                 
* ONE EST OR SERIES                                                             
*=============================================================*                 
         SPACE 1                                                                
PR42     GOTO1 HIGH                                                             
         CLC   KEY(7),KEYSAVE      A-M/CLT/PRD                                  
         BNE   PR80                                                             
         CLC   KEY+7(1),BEST                                                    
         BE    PR45                                                             
         CLC   KEY+7(1),BESTEND    TEST REACHED HI LIMIT                        
         BH    PR80                                                             
*                                                                               
PR45     OC    KEY+8(5),KEY+8      TEST BILL                                    
         BZ    PR47                NO - USE THIS EST                            
         MVC   KEY+8(5),=5X'FF'                                                 
         B     PR42                                                             
*                                                                               
PR47     GOTO1 GET                 READ THE ESTIMATE RECORD                     
         L     R4,AREC                                                          
         USING ESTHDR,R4                                                        
         B     PR60                                                             
         EJECT                                                                  
*==============================================================*                
* PROCESSING FOR EST=NO REQUESTS                               *                
*==============================================================*                
         SPACE 1                                                                
PR50     CLI   EST#,0              TEST ESTIMATE FILTER SET                     
         BE    PR52                                                             
         LA    R4,KEY              YES - READ FOR SPECIFIED ESTIMATE            
         MVC   EKEYEST,EST#                                                     
         GOTO1 HIGH                                                             
         CLC   EKEY(EKEYEST+L'EKEYEST-EKEY),KEYSAVE                             
         BNE   PR80                                                             
         GOTO1 GET                 READ THE ESTIMATE RECORD                     
         L     R4,AREC                                                          
         CLC   EEND,QSTART         DATE FILTERS                                 
         BNL   *+6                 THEY SHOULD MATCH POOL ESTIMATE              
         DC    H'0'                WHICH WAS INCLUDED                           
         CLC   ESTART,QEND         SO DIE IF THIS WOULD BE REJECTED             
         BNH   PR60                                                             
         DC    H'0'                                                             
                                                                                
PR52     LA    R4,KEY                                                           
         MVC   KEY+8(5),=5X'FF'                                                 
         GOTO1 HIGH                                                             
         CLC   KEY(7),KEYSAVE      A-M/CLT/PRD                                  
         BNE   PR80                                                             
*                                                                               
         GOTO1 GET                 READ THE ESTIMATE RECORD                     
         L     R4,AREC                                                          
*                                                                               
         CLC   EEND,QSTART         DATE FILTERS                                 
         BL    PR52                                                             
         CLC   ESTART,QEND                                                      
         BH    PR52                                                             
*                                                                               
         BAS   RE,FILTER                                                        
         BNE   PR52                                                             
         EJECT                                                                  
*================================================================               
* THIS PRODUCT WILL GO INTO TABLE                                               
*================================================================               
         SPACE 1                                                                
PR60     CLI   QDEMOVRD,C'Y'       CHECK FOR DEMO OVERRIDES                     
         BE    PR70                IF SO, DEMOS ALREADY SET                     
*                                                                               
         MVC   PTDEMO,EDEMLST                                                   
         MVC   PTWGHT,EWGTLST                                                   
*                                                                               
         L     RF,VMASTC                                                        
         USING MASTD,RF                                                         
         CLI   MCNETPAK,C'Y'                                                    
         BNE   PR61                                                             
*===============================================================                
*===============================================================                
         DROP  RF                                                               
* AS OF 07JUN00, THERE ARE ONLY 21 DEMOS IN THE ESTHDR                          
         MVC   PTXDEM(60),EDEMLST                                               
         MVC   PTXDEM+60(3),EDEM21 MOVE 21ST DEMO FOR NETPAK                    
         MVC   PTXWGHT(20),EWGTLST                                              
         MVC   PTXWGHT+20(1),EDEM21WT                                           
*                                  SET  ALLOCATION TARGETS                      
PR61     LA    R7,ETRGLST                                                       
         LA    R5,PTTRGS                                                        
         LA    R0,L'PTTRGS                                                      
         XC    PTTRGS,PTTRGS                                                    
*                                                                               
PR62     DS    0H                                                               
         OC    0(3,R7),0(R7)       THIS TARGET NOT USED                         
         BZ    PR68                                                             
*                                  GET ITS POSITION IN DEMOLIST                 
         LA    RF,PTDEMO                                                        
         LA    R6,1                                                             
*                                                                               
PR64     CLC   0(3,R7),0(RF)                                                    
         BE    PR66                                                             
         LA    RF,3(RF)                                                         
         LA    R6,1(R6)                                                         
         B     PR64                                                             
*                                                                               
PR66     STC   R6,0(R5)            SET  POSITION NO. IN PTTRGS                  
*                                                                               
PR68     LA    R7,3(R7)            NEXT TARG                                    
         LA    R5,1(R5)                                                         
         BCT   R0,PR62                                                          
*                                                                               
PR70     SR    RF,RF               FIND TABLE SLOT                              
         IC    RF,PTPRDN                                                        
         BCTR  RF,0                                                             
         MH    RF,PRDBUFLN                                                      
         L     RE,PRDBUFF                                                       
         LA    RF,0(RE,RF)                                                      
         LH    RE,PRDBUFLN         LENGTH OF ENTRY                              
         BCTR  RE,0                                                             
         EX    RE,*+8                                                           
         B     *+10                                                             
         MVC   0(0,RF),PTBUILD                                                  
         DROP  R4                                                               
         EJECT                                                                  
PR80     CLI   ONEPRD,C'Y'                                                      
         BE    EXIT                                                             
* PROCESS NEXT PRODUCT                                                          
         MVC   KEY,KEYSAVE         RESTORE LAST SEARCH KEY                      
         B     PR30                AND PROCESS NEXT PRODUCT                     
*                                                                               
EXIT     LHI   RF,255              COPY PRD 255 TO PRD 220                      
         BCTR  RF,0                                                             
         MH    RF,PRDBUFLN                                                      
         L     RE,PRDBUFF                                                       
         LA    R3,0(RE,RF)         POINT R3 TO POL ENTRY                        
         MVC   PTBUILD,0(R3)       AND SAVE WHAT IS THERE                       
*                                                                               
         LHI   RF,220                                                           
         BCTR  RF,0                                                             
         MH    RF,PRDBUFLN                                                      
         L     RE,PRDBUFF                                                       
         LA    R3,0(RE,RF)                                                      
         LH    RF,PRDBUFLN                                                      
         BCTR  RF,0                                                             
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R3),PTBUILD     MOVE THE ENTRY DATA                          
*                                                                               
         LHI   RF,219              COPY PRD 255 TO PRD 219                      
         BCTR  RF,0                                                             
         MH    RF,PRDBUFLN                                                      
         L     RE,PRDBUFF                                                       
         LA    R3,0(RE,RF)         POINT R3 TO UNALLOCATED ENTRY                
                                                                                
         XC    0(28,R3),0(R3)                                                   
         MVI   PTPRDN,219                                                       
         MVC   PTPRDA,=C'***'                                                   
         MVC   PTNAME,=CL20'UNALLOCATED'                                        
         CLI   RCLANG,4            TEST FRENCH                                  
         BNE   *+10                                                             
         MVC   PTNAME,=CL20'NONALLOUES'                                         
         MVC   PTDEMO,PTBUILD+PTDEMO-PTBUFFD  MOVE POL DEMOS                    
         MVC   PTWGHT,PTBUILD+PTWGHT-PTBUFFD                                    
*                                                                               
         XIT1                                                                   
         EJECT                                                                  
*================================================================*              
* FILTER ESTIMATES ON VALUES IN QESTEND                          *              
*================================================================*              
         SPACE 1                                                                
FILTER   NTR1                                                                   
         USING ESTHDR,R4                                                        
*                                                                               
         CLC   QEST,=C'NO '                                                     
         BNE   EQXIT                                                            
         CLC   QESTEND,SPACES                                                   
         BE    EQXIT                                                            
*                                                                               
         LA    R0,3                ESTIMATE FILTERS                             
         LA    RE,QESTEND                                                       
         LA    RF,EPROF                                                         
*                                                                               
         CLI   RQPOLFLT,C'Y'       USE POL FILTER VALUES                        
         BNE   FILTER2                                                          
         CLC   EKEYPRD,=C'POL'     UNLESS THIS IS POL                           
         BE    *+8                                                              
         LA    RF,SVPOLFLT                                                      
*                                                                               
FILTER2  CLI   0(RE),C'*'                                                       
         BE    FILTER6                                                          
         CLI   0(RE),C' '                                                       
         BE    FILTER6                                                          
         TM    0(RE),X'40'         TEST NEGATIVE FILTERS                        
         BZ    FILTER4              YES                                         
*                                                                               
         CLC   0(1,RE),0(RF)       POSITIVE FILTER MUST MATCH                   
         BNE   NEQXIT                                                           
         B     FILTER6                                                          
*                                                                               
FILTER4  MVC   BYTE,0(RE)                                                       
         OI    BYTE,C' '           MAKE CHAR UPPER CASE FOR COMPARE             
         CLC   BYTE,0(RF)                                                       
         BE    NEQXIT                                                           
*                                                                               
FILTER6  LA    RE,1(RE)                                                         
         LA    RF,1(RF)                                                         
         BCT   R0,FILTER2                                                       
*                                                                               
EQXIT    CR    RB,RB                                                            
         B     *+6                                                              
NEQXIT   LTR   RB,RB                                                            
         XIT1                                                                   
         DROP  R4                                                               
         LTORG                                                                  
         EJECT                                                                  
WORKD    DSECT                                                                  
RECFLAG  DS    C                   END OF RECORDS                               
ONEPRD   DS    C                                                                
EST#     DS    X                   ESTIMATE NUMBER FILTER                       
EKSAVE   DS    CL13                                                             
SVPOLFLT DS    CL3                 SAVED POL FILTER VALUES                      
         DS    0D                                                               
PTBUILD  DS    XL200                                                            
IOANAME  DS    CL8                                                              
IOA      DS    2000C                                                            
WORKX    DS    0C                                                               
         SPACE 3                                                                
*                                                                               
       ++INCLUDE SPREPPTBUF                                                     
*                                                                               
       ++INCLUDE SPGENDMN                                                       
*                                                                               
         PRINT OFF                                                              
         EJECT                                                                  
* SPREPWORKD                                                                    
       ++INCLUDE SPREPWORKD                                                     
         EJECT                                                                  
* SPGENPRD                                                                      
       ++INCLUDE SPGENPRD                                                       
         EJECT                                                                  
* SPGENEST                                                                      
       ++INCLUDE SPGENEST                                                       
         EJECT                                                                  
* SPGENCLT                                                                      
       ++INCLUDE SPGENCLT                                                       
AGYHDRD  DSECT                                                                  
       ++INCLUDE SPGENAGY                                                       
* MASTD                                                                         
       ++INCLUDE DDREPMASTD                                                     
         PRINT ON                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'003SPMEDPRDRN11/12/03'                                      
         END                                                                    
