*          DATA SET DDMANPRTR  AT LEVEL 004 AS OF 05/01/02                      
*CATALP MANPRTR                                                                 
         PRINT NOGEN                                                            
         SPACE 1                                                                
PRINTER CSECT                                                                   
         NMOD1 42,PRINTER                                                       
         USING DCONT,RC                                                         
         L     RA,=V(CPRINT)                                                    
         USING DPRINT,RA                                                        
         SPACE 1                                                                
         TM    MODE,X'0E'          PAGING FLAGS?                                
         BNZ   PAGING                                                           
         TM    MODE,X'C0'          HEAD LINE - FITS ON PAGE                     
         BM    PRNTUSER                                                         
         TM    MODE,X'10'          NEW COLUMN HEADS                             
         BO    PRTCLHDS              YES                                        
         SPACE 1                                                                
         PACK  DUB(2),SPACING+2(2)                                              
         AP    DUB(2),LINE                                                      
         CP    DUB(2),MAXLINE      WILL LINE FIT?                               
         BH    FOOTHEAD              NO                                         
         SPACE 1                                                                
PRNTUSER MVC   PLINE,SPACES                                                     
         MVC   PLINE+15(87),P      CENTER ON 110-CHAR WIDTH                     
         MVC   P,SPACES                                                         
         SPACE 1                                                                
         TM    MODE,X'C0'          UNDERSCORED HEAD?                            
         BM    DOHEAD                YES                                        
         SPACE 1                                                                
         MVC   PCONTROL,SPACING      NO                                         
         BAS   R9,PRINTEM                                                       
EXIT     XMOD1 1                                                                
         SPACE 1                                                                
PRINTEM  CLI   ULCFLAG,C'Y'        TN TRAIN                                     
         BE    *+10                  YES                                        
         OC    PLINE(95),SPACES      NO - CAP ALL SMALL LETTERS                 
         GOTO1 =V(PRINT),PARAS,PBYTE,PCONTROL                                   
         PACK  DUB(2),PCONTROL+2(2)                                             
         AP    LINE,DUB(2)                                                      
         BR    R9                                                               
         SPACE 1                                                                
DOHEAD   MVC   PCONTROL,=C'AL02'                                                
         MVC   HEAD,PLINE+7        SAVE HEAD FOR USCORE                         
         TM    MODE,X'40'                                                       
         BO    *+8                                                              
         MVI   PCONTROL+3,C'0'                                                  
         NI    MODE,X'3F'                                                       
         BAS   R8,USCORE                                                        
         B     EXIT                                                             
         TITLE 'MANUALS PRINTING MODULE'                                        
*                   SPECIAL PAGE HANDLING                                       
         SPACE 2                                                                
PAGING   TM    MODE,X'02'          FINISH PAGE AND RETURN?                      
         BO    FOOTHEAD              YES                                        
         SPACE 1                                                                
         TM    MODE,X'04'          WIDE-PAGE CAPTION                            
         BZ    PAGING0               NO  - TRY STARTS                           
         TM    PAGE+1,X'10'        PAGE NOW ODD OR EVEN                         
         BO    CAPFOOT                                                          
         B     RUNHD                 EVEN                                       
         SPACE 1                                                                
PAGING0  XI    MODE,X'08'          RESET START BIT                              
         TM    PAGE+1,X'10'        PAGE NOW ODD OR EVEN                         
         BO    ODDST                 ODD                                        
         B     RUNHD                 EVEN                                       
         SPACE 2                                                                
*                   RUNNING HEAD OR FOOT                                        
         SPACE 1                                                                
FOOTHEAD TM    PAGE+1,X'10'        IS CURRENT PAGE ODD (RECTO)?                 
         BO    FOOT                  YES - FINISH RUNNING FOOT                  
         SPACE 1                                                                
         BAS   R8,ENDPAGE                                                       
         TM    MODE,X'02'          FINISH PAGE AND RETURN                       
         BZ    ODDST                 NO                                         
         XI    MODE,X'02'            YES  (RESET BIT)                           
         B     EXIT                                                             
         SPACE 1                                                                
ODDST    MVC   PCONTROL,=C'AC01'                                                
         BAS   R9,MOVEIT                                                        
         MVC   PCONTROL,=C'BL02'                                                
         BAS   R9,MOVEIT                                                        
         TM    MODE,X'20'          START ODD WIDE PAGE                          
         BZ    NEWTITLE              NO                                         
         XI    MODE,X'20'            YES                                        
         B     EXIT                                                             
         EJECT                                                                  
FOOT     CLC   TITLE(8),SPACES                                                  
         BE    FOOTOFF                                                          
         SPACE 1                                                                
CAPFOOT  MVC   PCONTROL,=C'BL03'                                                
         ZAP   FOOTLINE,MAXLINE                                                 
         SP    FOOTLINE,LINE                                                    
         CP    FOOTLINE,=P'0'                                                   
         BNH   FOOTSET                                                          
         UNPK  PCONTROL+2(2),FOOTLINE                                           
         BAS   R9,MOVEIT                                                        
         SPACE 1                                                                
FOOTSET  MVC   PCONTROL,=C'AL03'   SKIP 2 LINES BEFORE FOOT (CAPTION)           
         TM    MODE,X'04'          WIDE-PAGE CAPTION                            
         BZ    FOOTSET1              NO                                         
         BAS   R9,SETCAPT            YES                                        
         BAS   R9,PRINTEM                                                       
         B     FOOTOFF                                                          
         SPACE 1                                                                
FOOTSET1 BAS   R9,SETHDFT                                                       
         BAS   R8,USCORE           PRINT FOOT                                   
         SPACE 1                                                                
FOOTOFF  BAS   R8,ENDPAGE                                                       
         TM    MODE,X'06'          FINISH PG OR WD-PG CAPTION                   
         BZ    RUNHD            NO  - GO ON                                     
         NI    MODE,X'F9'            YES - RESET BITS                           
         B     EXIT                   GET OUT                                   
         SPACE 1                                                                
FOOTLINE DC    PL2'0'                                                           
         SPACE 2                                                                
ENDPAGE  MVC   PCONTROL,=C'AC01'                                                
         AP    PAGE,=P'1'                                                       
         ZAP   LINE,=P'0'                                                       
         BR    R8                                                               
         SPACE 2                                                                
MOVEIT   GOTO1 =V(PRINT),PARAS,SPACES-1,PCONTROL                                
         BR    R9                                                               
         EJECT                                                                  
RUNHD    MVC   PCONTROL,=C'AC01'                                                
         TM    MODE,X'04'                                                       
         BO    RUNHDC                                                           
         CLC   TITLE(8),SPACES                                                  
         BNE   RUNHD1                                                           
         SPACE 1                                                                
         BAS   R9,MOVEIT                                                        
         TM    MODE,X'20'          WIDE-PAGE START                              
         BZ    *+12                  NO                                         
         XI    MODE,X'20'            YES                                        
         B     EXIT                                                             
         SPACE 1                                                                
         MVC   PCONTROL,=C'BL03'   SKIP TWO LINES AFTER                         
         BAS   R9,MOVEIT                                                        
         B     PRTCLHDS                                                         
         SPACE 1                                                                
RUNHDC   NI    MODE,X'01'          RESET X'2C'                                  
         BAS   R9,SETCAPT                                                       
         BAS   R9,PRINTEM                                                       
         MVC   PCONTROL,=C'BL03'   SKIP TWO LINES AFTER                         
         BAS   R9,MOVEIT                                                        
         B     EXIT                                                             
         SPACE 1                                                                
RUNHD1   CLC   LOFTITLE,=H'0'      NEW TITLE                                    
         BNE   RUNHD2                                                           
         BAS   R9,MOVEIT                                                        
         MVC   PCONTROL,=C'BL03'   SKIP TWO LINES AFTER                         
         BAS   R9,MOVEIT                                                        
         B     NEWTITLE                                                         
         SPACE 1                                                                
RUNHD2   BAS   R9,SETHDFT                                                       
         BAS   R8,USCORE                                                        
         ZAP   LINE,=P'0'                                                       
         TM    MODE,X'20'          WIDE-PAGE START                              
         BZ    NEWTITLE              NO                                         
         XI    MODE,X'20'            YES                                        
         B     EXIT                                                             
         EJECT                                                                  
SETCAPT  MVC   PLINE,SPACES                                                     
         MVC   PLINE+7(73),P                                                    
         MVC   P,SPACES                                                         
         B     COMMSET                                                          
         SPACE 1                                                                
SETHDFT  MVC   PLINE,SPACES                                                     
         MVC   PLINE+7(60),TITLE                                                
         MVC   HEAD,PLINE+7        SAVE TITLE FOR UNDERSCORE                    
COMMSET  MVC   PLINE+81(10),REVDATE                                             
         BAS   R8,PAGENUM                                                       
         LA    R4,PLINE+101                                                     
         SR    R4,R3               LOFPGNO IN R3                                
         MVC   0(8,R4),PGNOWORK                                                 
         BR    R9                                                               
         SPACE 1                                                                
NEWTITLE CLC   LOFTITLE,=H'0'      NEW TITLE                                    
         BNE   PRTCLHDS              NO                                         
         SPACE 1                                                                
         MVI   LOFTITLE+1,X'01'      YES - RESET TO OLD                         
         MVC   PCONTROL,=C'BL00'                                                
         MVC   PLINE,SPACES                                                     
         MVC   PLINE+15(60),TITLE                                               
         OC    PLINE+15(60),SPACES      ALL CAPS                                
         MVC   HEAD,PLINE+7                                                     
         BAS   R8,USCORE                                                        
         MVC   PLINE,SPACES                                                     
         MVI   PCONTROL+3,C'2'     SKIP TWO MORE LINES                          
         BAS   R9,PRINTEM                                                       
         EJECT                                                                  
PRTCLHDS CLC   COLHEADS,SPACES     ANYTHING TO PRINT                            
         BE    CLEARBIT              NO                                         
         SPACE 1                                                                
         TM    MODE,X'10'          NEW COLUMN HEADS                             
         BZ    CHPRINT               NO  - GO PRINT                             
         MVC   CHUSCORE,SPACES       YES                                        
         MVC   P,SPACES                                                         
         ZAP   CHCTDFLG,=P'0'                                                   
         LA    R3,COLHEADS                                                      
         LA    R4,CHUSCORE                                                      
         LA    R0,77(R3)                                                        
         SPACE 1                                                                
CHLP1    CLI   0(R3),C' '          LEADING SPACE                                
         BNE   CHLP2                 NO                                         
         LA    R3,1(R3)              YES                                        
         LA    R4,1(R4)                                                         
         CR    R3,R0               MORE IN COLHEADS                             
         BNH   CHLP1                 YES                                        
         B     CHPRINT               NO  - GO PRINT                             
         SPACE 1                                                                
CHLP2    CLI   ULCFLAG,C'Y'                                                     
         BE    *+12                                                             
         MVI   0(R4),C'-'                                                       
         B     *+8                                                              
         MVI   0(R4),X'6D'                                                      
         LA    R3,1(R3)                                                         
         LA    R4,1(R4)                                                         
         CR    R3,R0               CHECK LIMIT                                  
         BH    CHPRINT               REACHED                                    
         CLI   0(R3),C' '          TRAILING SPACE                               
         BNE   CHLP2                 NO - CONTINUE                              
         SPACE 1                                                                
         CLC   0(2,R3),SPACES      NON-FLOWING TEXT                             
         BNE   CHLP2                 NO - CONTINUE UNDERSCORE                   
         AP    CHCTDFLG,=P'1'        YES - COUNT ENTRY FOUND                    
         LA    R3,2(R3)                                                         
         LA    R4,2(R4)                                                         
         CR    R3,R0               CHECK LIMIT                                  
         BH    CHPRINT               REACHED                                    
         B     CHLP1                       LOOK FOR MORE                        
         SPACE 1                                                                
CLEARBIT NI    MODE,X'EF'          RESET NEW COLHEADS BIT                       
         B     PRNTUSER                                                         
         EJECT                                                                  
CHPRINT  MVC   PLINE,SPACES                                                     
         MVC   PLINE+15(80),COLHEADS                                            
         MVC   PCONTROL,=C'BL00'                                                
         CLI   ULCFLAG,C'Y'                                                     
         BE    CHPR1                                                            
         MVI   PCONTROL+3,C'1'                                                  
         CLI   PROD,C'N'                                                        
         BNE   CHPR1                                                            
         SP    LINE,=P'1'                                                       
CHPR1    BAS   R9,PRINTEM                                                       
         MVC   PLINE+15(80),CHUSCORE                                            
         MVI   PCONTROL+3,C'2'     ONE LINE AFTER COL HEADS                     
         BAS   R9,PRINTEM                                                       
         TM    MODE,X'10'          NEW COLUMN HEADS                             
         BZ    PRNTUSER              NO                                         
         SPACE 1                                                                
         XI    MODE,X'10'            YES - RESET BIT                            
         CP    CHCTDFLG,=P'1'      SINGLE ENTRY                                 
         BNE   EXIT                  NO                                         
         SPACE 1                                                                
         LA    R3,COLHEADS+79        YES                                        
         LA    R4,0                                                             
         CLI   0(R3),C' '          FIND PLACE FOR (CONT'D)                      
         BNE   *+12                                                             
         LA    R4,1(R4)                                                         
         BCT   R3,*-12                                                          
         SPACE 1                                                                
         CH    R4,=H'9'            ROOM FOR IT                                  
         BL    EXIT                  NO                                         
         MVC   2(8,R3),=X'4DC39695A37D845D'     (CONT'D)                        
         B     EXIT                                                             
         EJECT                                                                  
*                   UNDERSCORE ANYTHING IN HEAD                                 
         SPACE 1                                                                
USCORE   BAS   R9,PRINTEM                                                       
         MVC   PLINE,SPACES                                                     
         LA    R2,HEAD                                                          
         LA    R5,PLINE+7                                                       
         LA    R4,80                                                            
         SPACE 1                                                                
PR6      CLI   0(R2),C' '          A(FIRST NON-SPACE) TO R2                     
         BNE   PR8                                                              
         LA    R2,1(R2)                                                         
         LA    R5,1(R5)                                                         
         BCT   R4,PR6                                                           
         B     PR16                                                             
         SPACE 1                                                                
PR8      LA    R3,HEAD+79          A(LAST NON-SPACE) TO R3                      
         LA    R6,PLINE+86                                                      
         SPACE 1                                                                
PR10     CLI   0(R3),C' '                                                       
         BNE   PR12                                                             
         BCTR  R6,R0                                                            
         BCT   R3,PR10                                                          
         SPACE 1                                                                
PR12     CLI   ULCFLAG,C'Y'        U/LC TRAIN                                   
         BNE   *+12                                                             
         MVI   0(R5),X'6D'           YES - USE USCORE CHAR                      
         B     *+8                                                              
         MVI   0(R5),C'-'            NO  - USE DASH                             
         SR    R6,R5               GET LENGTH OF UNDERSCORE                     
         BNP   PR16                                                             
         BCTR  R6,0                                                             
         EX    R6,UFINISH                                                       
         B     PR14                                                             
         SPACE 1                                                                
UFINISH  MVC   1(0,R5),0(R5)                                                    
         SPACE 1                                                                
PR14     CLI   ULCFLAG,C'Y'        SAME-LINE UNDERSCORE                         
         BE    PR16                  YES                                        
         MVC   PCONTROL,=C'AL01'     NO                                         
         BAS   R9,PRINTEM                                                       
         MVC   PLINE,SPACES                                                     
         CLI   PROD,C'N'                                                        
         BNE   PR16                                                             
         SP    LINE,=P'1'                                                       
         SPACE 1                                                                
PR16     MVC   PCONTROL,=C'BL03'                                                
         BAS   R9,PRINTEM                                                       
         BR    R8                                                               
         EJECT                                                                  
*                   FORMAT PAGE NUMBER FOR PRINTING                             
         SPACE 1                                                                
PAGENUM  LA    R3,PGNOWORK                                                      
         MVC   PGNOWORK,SPACES                                                  
         TM    MODE,X'01'          FRONT MATTER                                 
         BZ    ARABIC                NO                                         
         SPACE 1                                                                
         TM    PAGE,X'03'                                                       
         BZ    DECADE0                                                          
         BO    DECADE3                                                          
         SPACE 1                                                                
         TM    PAGE,X'02'                                                       
         BO    DECADE2                                                          
         SPACE 1                                                                
         MVI   0(R3),X'A7'         TEN AND TEENS                                
         LA    R3,1(R3)                                                         
         B     DECADE0                                                          
         SPACE 1                                                                
DECADE2  MVC   0(2,R3),=X'A7A7'    TWENTIES                                     
         LA    R3,2(R3)                                                         
         B     DECADE0                                                          
         SPACE 1                                                                
DECADE3  MVC   0(3,R3),=X'A7A7A7'  THIRTIES                                     
         LA    R3,3(R3)                                                         
         SPACE 1                                                                
DECADE0  SR    R4,R4                                                            
         IC    R4,PAGE+1           UNITS DIGIT                                  
         SRA   R4,4                GET RID OF SIGN BITS                         
         SLA   R4,2                MULTIPLY UNITS DIGIT BY 4                    
         LA    R5,ROMANTBL(R4)     SET TABLE ADDR PLUS OFFSET                   
         MVC   0(4,R3),0(R5)                                                    
         B     PGNOLEN                                                          
         SPACE 1                                                                
ARABIC   CLI   SECTNUM,C' '        SECTIONAL PAGE NUMBERING                     
         BE    CONTIN                NO                                         
         SPACE 1                                                                
         CLI   SECTNUM,C'I'        INDEX                                        
         BE    ONEDIGIT                                                         
         TM    SECTNUM,X'F0'       YES - NUMERIC                                
         BO    ONEDIGIT                    YES                                  
         EJECT                                                                  
         MVN   P-1(1),SECTNUM                                                   
         NI    P-1,X'0F'                                                        
         SR    R4,R4                                                            
         IC    R4,P-1                                                           
         BCTR  R4,0                                                             
         SLA   R4,1                DOUBLE IT                                    
         LA    R4,DIGTAB(R4)                                                    
         MVC   0(2,R3),0(R4)                                                    
         MVI   2(R3),C'-'                                                       
         LA    R3,3(R3)                                                         
         B     CONTIN                                                           
         SPACE 1                                                                
ONEDIGIT MVC   0(1,R3),SECTNUM                                                  
         MVI   1(R3),C'-'                                                       
         LA    R3,2(R3)                                                         
CONTIN   EDIT  (P2,PAGE),(3,(R3)),ALIGN=LEFT                                    
         SPACE 1                                                                
PGNOLEN  LA    R3,8                                                             
         LA    R4,PGNOWORK+7                                                    
         CLI   0(R4),C' '                                                       
         BNE   *+10                                                             
         BCTR  R4,0                                                             
         BCT   R3,*-10                                                          
         SPACE 1                                                                
         STH   R3,LOFPGNO                                                       
         BR    R8                                                               
         SPACE 2                                                                
*                   WORK SPACE                                                  
         SPACE 1                                                                
HEAD     DS    CL80                AUTO U'SCORE LINE                            
LOFPGNO  DC    H'0'                                                             
PGNOWORK DC    CL8' '                                                           
ROMANTBL DS    0CL40                                                            
         DC    CL4' '              UNIT = 0                                     
         DC    X'89404040'                1                                     
         DC    X'89894040'                2                                     
         DC    X'89898940'                3                                     
         DC    X'89A54040'                4                                     
         DC    X'A5404040'                5                                     
         DC    X'A5894040'                6                                     
         DC    X'A5898940'                7                                     
         DC    X'A5898989'                8                                     
         DC    X'89A74040'                9                                     
CHUSCORE DS    CL80                COLUMN HEADS UNDERSCORE LINE                 
CHCTDFLG DS    PL1                 COLUMN HEADS CONTINUED FLAG                  
         DS    0H                                                               
DIGTAB   DC    C'1011121314151617'                                              
         EJECT                                                                  
         LTORG                                                                  
         SPACE 1                                                                
*                  DSECT FOR PRINT CONTROLLER                                   
         SPACE 1                                                                
DCONT    DSECT                                                                  
PBYTE    DS    CL1                                                              
PLINE    DS    CL132                                                            
DUB      DS    D                                                                
WORK     DS    CL32                                                             
PCONTROL DS    CL4                                                              
PARAS    DS    CL8                                                              
         SPACE 1                                                                
       ++INCLUDE DDCMANPRNT                                                     
         EJECT                                                                  
       ++INCLUDE DDDMANPRNT                                                     
         SPACE 1                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'004DDMANPRTR 05/01/02'                                      
         END                                                                    
