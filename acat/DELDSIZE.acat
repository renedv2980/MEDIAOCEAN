*          DATA SET DELDSIZE   AT LEVEL 001 AS OF 01/19/10                      
*CATALP DELDSIZE                                                                
         TITLE 'DMLDSIZE - LOAD/DUMP DA FILE REC SIZE COUNT/PRINT'              
*        PARAMS VIA R1                                                          
*        XL2   COUNT/PRINT  X'0000'=COUNT  X'FF..'=PRINT                        
*        XL2   LENGTH                                                           
*        AL4   A(FILE NAME)                                                     
*                                                                               
         PRINT NOGEN                                                            
LDSIZE   CSECT                                                                  
         NMOD1 WRKX-WRKD,**SIZE**                                               
         USING WRKD,RC                                                          
         CLI   0(R1),X'FF'                                                      
         BE    PRINT                                                            
*                                                                               
COUNT    LA    R3,LENTAB+6         SET FOR BXLE                                 
         LH    R4,LENTAB                                                        
         L     R5,LENTAB+2                                                      
*                                                                               
C1       CLC   2(2,R1),0(R3)       TEST REC LEN TO TABLE LEN                    
         BNH   C2                  ADD TO COUNTER IF FITS                       
         BXLE  R3,R4,C1                                                         
         SR    R3,R4               END OF TABLE ADD INTO LAST COUNTER           
C2       AP    2(6,R3),=P'1'                                                    
*                                                                               
XIT      XMOD1                                                                  
         EJECT                                                                  
PRINT    L     RA,=V(CPRINT)       PRINT HEADING                                
         USING DPRINT,RA                                                        
         MVC   P,SPACES                                                         
         GOTO1 =V(PRINTER)                                                      
         L     RF,4(R1)            GET DAFILE NAME                              
         MVC   HDL+8(8),0(RF)                                                   
         MVC   P(L'HDL),HDL                                                     
         GOTO1 =V(PRINTER)                                                      
         MVC   P(L'UDL),UDL                                                     
         GOTO1 =V(PRINTER)                                                      
*                                                                               
         LA    R3,LENTAB+6         SET FOR BXLE                                 
         LH    R4,LENTAB                                                        
         L     R5,LENTAB+2                                                      
         SR    R7,R7               USE AS FROM LENTAB                           
         ZAP   TOTCOUNT,=P'0'      TOTAL COUNTER                                
*                                                                               
PR1      AP    TOTCOUNT,2(6,R3)    BUMP TOTAL                                   
         LA    R7,1(R7)            SET 'FROM' LENTAB                            
         BAS   RF,FORMAT           FORMAT FROM-TO                               
         EDIT  (P6,2(R3)),PCOUNT,ZERO=NOBLANK,COMMAS=YES                        
*                                                                               
         MVC   P(PRTLQ),PRT                                                     
         GOTO1 =V(PRINTER)                                                      
         LH    R7,0(R3)            SET NEXT FROM                                
         BXLE  R3,R4,PR1                                                        
                                                                                
PR2      MVC   P(L'UDL),UDL        PRINT TOTAL AT END OF TABLE                  
         GOTO1 =V(PRINTER)                                                      
*                                                                               
         EDIT  TOTCOUNT,PCOUNT,ZERO=NOBLANK,COMMAS=YES                          
*                                                                               
         MVC   P(PRTLQ),PRT                                                     
         MVC   P(15),=CL15'TOTAL'                                               
         GOTO1 =V(PRINTER)                                                      
         MVC   P(L'UDL),UDL                                                     
         GOTO1 =V(PRINTER)                                                      
                                                                                
PR3      LA    R3,LENTAB+6         CLEAR COUNTERS                               
         LH    R4,LENTAB                                                        
         L     R5,LENTAB+2                                                      
         ZAP   2(6,R3),=P'0'                                                    
         BXLE  R3,R4,*-6                                                        
         B     XIT                                                              
*                                                                               
FORMAT   LR    R0,R7               SET FROM NUMBER                              
         CVD   R0,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  FROM(4),DUB                                                      
         LH    R0,0(R3)            SET TO NUMBER                                
         CVD   R0,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  TO(4),DUB                                                        
         BR    RF                                                               
         EJECT                                                                  
         CNOP  2,4                                                              
LENTAB   DC    H'8'                TABLE OF REC LENGTHS AND COUNTERS            
         DC    A(LENTABX-1)                                                     
         DC    AL2(0064),PL6'0'                                                 
         DC    AL2(0128),PL6'0'                                                 
         DC    AL2(0256),PL6'0'                                                 
         DC    AL2(0512),PL6'0'                                                 
         DC    AL2(0768),PL6'0'                                                 
         DC    AL2(1024),PL6'0'                                                 
         DC    AL2(1536),PL6'0'                                                 
         DC    AL2(1636),PL6'0'                                                 
         DC    AL2(1734),PL6'0'                                                 
         DC    AL2(1735),PL6'0'                                                 
         DC    AL2(1736),PL6'0'                                                 
         DC    AL2(1950),PL6'0'                                                 
         DC    AL2(1998),PL6'0'                                                 
         DC    AL2(1999),PL6'0'                                                 
         DC    AL2(2000),PL6'0'                                                 
         DC    AL2(2048),PL6'0'                                                 
         DC    AL2(4096),PL6'0'                                                 
         DC    AL2(6144),PL6'0'                                                 
LENTABX  DS    0C                                                               
*                                                                               
HDL      DC    C'REC LEN XXXXXXX       COUNT    '                               
UDL      DC    C'---------------  --------------'                               
*                                                                               
PRT      DS    0CL26                                                            
FROM     DC    CL4' '              LOWER BOUND                                  
         DC    CL4' TO '                                                        
TO       DC    CL4' '              UPPER BOUND                                  
         DC    CL5' '                                                           
PCOUNT   DC    CL14' '             RECORD COUNT                                 
PRTLQ    EQU   *-PRT                                                            
         EJECT                                                                  
         LTORG                                                                  
         SPACE 3                                                                
WRKD     DSECT                                                                  
DUB      DS    D                                                                
WORK     DS    CL17                                                             
TOTCOUNT DS    PL6                                                              
WRKX     EQU   *                                                                
         SPACE 2                                                                
* DDDPRINT                                                                      
         PRINT OFF                                                              
       ++INCLUDE DDDPRINT                                                       
         PRINT ON                                                               
         SPACE 2                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'001DELDSIZE  01/19/10'                                      
         END                                                                    
