*          DATA SET PPBILEXPS  AT LEVEL 006 AS OF 05/11/93                      
*CATALP PPBILEXP                                                                
         TITLE 'PPBILEXP - CREATE EXPANDED INVOICE NUMBERS'                     
         SPACE 1                                                                
***********************************************************************         
*                                                                               
*   PARAMETER LIST-                                                             
*                                                                               
*   P1/0       NOT USED                                                         
*   P1/1-3     A(CONTROL DSECT) - PPBILEXD                                      
*                                                                               
***********************************************************************         
         SPACE 2                                                                
PPBILEXP CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 WORKDL,PPBEXP                                                    
         USING WORKD,RC                                                         
         ST    R1,SAVR1                                                         
         L     RA,0(R1)            A(CONTROL DSECT)                             
         USING PPBILEXD,RA                                                      
*                                                                               
         CLC   SVSVKEY,PPBXAGY     TEST SAME AGY/MED/CLIENT/OFFICE              
         BE    BX10                                                             
*                                                                               
         MVC   SVSVKEY,PPBXAGY                                                  
         XC    WORK,WORK           READ B1 PROFILE                              
         MVC   WORK(4),=C'P0B1'                                                 
         MVC   WORK+4(2),SVAGY                                                  
         MVC   WORK+6(1),SVMED                                                  
         MVC   WORK+7(3),SVCLT                                                  
         CLI   SVCOFF,C' '                                                      
         BNH   *+14                                                             
         MVI   WORK+10,C'*'        SET OFFICE CODE                              
         MVC   WORK+11(1),SVCOFF                                                
*                                                                               
         L     RF,PPBXACOM         A(COMFACS)                                   
         L     RE,CDATAMGR-COMFACSD(RF)                                         
         ST    RE,DMCB+8                                                        
         L     RF,CGETPROF-COMFACSD(RF)                                         
         GOTO1 (RF),DMCB,WORK,SVB1PRF                                           
*                                                                               
BX10     DS    0H                                                               
*                                  FORMAT INVOICE NUMBER                        
         MVC   DUB(2),SVB1PRF+2    'MEDIA'                                      
         CLI   DUB,C'*'                                                         
         BNE   *+8                                                              
         MVI   DUB,C' '                                                         
         CLI   DUB+1,C'*'                                                       
         BNE   *+8                                                              
         MVI   DUB+1,C' '                                                       
         CLC   DUB(2),SPACES                                                    
         BNE   *+10                                                             
         MVC   DUB(1),PPBXMED                                                   
         CLI   DUB,C' '                                                         
         BH    *+14                                                             
         MVC   DUB(1),DUB+1                                                     
         MVI   DUB+1,C' '                                                       
*                                                                               
         LA    R3,PPBXEXP          OUTPUT AREA                                  
         CLI   SVB1PRF+4,C'1'      FORMAT 1 = MED-MONTH                         
         BNE   BX12                                                             
*                                                                               
         MVC   0(2,R3),DUB         MEDIA                                        
         LA    R3,1(R3)                                                         
         CLI   0(R3),C' '                                                       
         BE    *+8                                                              
         LA    R3,1(R3)                                                         
         MVI   0(R3),C'-'                                                       
         MVC   1(2,R3),PPBXINV     SET MONTH FROM INVOICE INPUT                 
         MVI   3(R3),C'-'                                                       
         MVC   4(4,R3),PPBXINV+2   INVOICE NUMBER                               
         B     BX14                                                             
*                                                                               
BX12     DS    0H                  OTHERWISE MONTH-MED                          
         MVC   0(2,R3),PPBXINV     MONTH                                        
         MVI   2(R2),C'-'                                                       
         MVC   3(2,R3),DUB         MEDIA                                        
         LA    R3,4(R3)                                                         
         CLI   0(R3),C' '                                                       
         BE    *+8                                                              
         LA    R3,1(R3)                                                         
         MVI   0(R3),C'-'                                                       
         MVC   4(4,R3),PPBXINV+2   INVOICE NUMBER                               
         B     BX14                                                             
*                                                                               
BX14     DS    0H                                                               
*                                                                               
EXIT     XIT1                                                                   
         SPACE 2                                                                
SPACES   DC    10C' '                                                           
         SPACE 2                                                                
         LTORG                                                                  
         SPACE 2                                                                
WORKD    DSECT                                                                  
DUB      DS    D                                                                
DMCB     DS    6F                                                               
WORK     DS    XL64                                                             
SAVR1    DS    F                                                                
WORKDL   EQU   *-WORKD                                                          
         EJECT                                                                  
       ++INCLUDE PPBILEXD                                                       
*                                                                               
         ORG   PPBXSAV                                                          
SVSVKEY  DS    0CL7                                                             
SVAGY    DS    CL2                                                              
SVMED    DS    CL1                                                              
SVCLT    DS    CL3                                                              
SVCOFF   DS    CL1                                                              
*                                                                               
SVB1PRF  DS    XL16                                                             
SVB1XPRF DS    XL16                                                             
*                                                                               
**INCLUDE DDCOMFACSD                                                            
         PRINT OFF                                                              
       ++INCLUDE DDCOMFACSD                                                     
         PRINT ON                                                               
*                                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'006PPBILEXPS 05/11/93'                                      
         END                                                                    
