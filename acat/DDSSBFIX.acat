*          DATA SET DDSSBFIX   AT LEVEL 013 AS OF 05/12/11                      
*CATALP SSBFIX                                                                  
SSBFIX   CSECT                                                                  
         NMOD1 0,*SSBFIX*                                                       
         USING TCBD,R2                                                          
         USING SSBD,R3                                                          
         LR    R2,R1                                                            
         L     R3,=V(SSB)                                                       
         TM    SSBSYSFL,SSBSYTST   DEAL HARSHLY WITH THE TEST SYSTEMS           
         BO    TSTLOOP                                                          
*                                                                               
         CLI   SSBCTTSK,1          IS CTRL ENQUE COUNT                          
         BH    XX02                                                             
         CLM   R2,7,SSBCTTSK+1                                                  
         BNE   XX02                                                             
         XC    SSBCTTSK,SSBCTTSK                                                
         MVC   ENQMIN,=CL8'CTRL'                                                
         BRAS  RE,DEQ                                                           
         BRAS  RE,WTO                                                           
*                                                                               
XX02     DS    0H                                                               
*&&UK                                                                           
         CLI   SSBMZTSK,1          IS MEDZ ENQUE COUNT                          
         BH    XX04                                                             
         CLM   R2,7,SSBMZTSK+1                                                  
         BNE   XX04                                                             
         XC    SSBMZTSK,SSBMZTSK                                                
         MVC   ENQMIN,=CL8'MEDZ'                                                
         BRAS  RE,DEQ                                                           
         BRAS  RE,WTO                                                           
*&&                                                                             
XX04     CLI   SSBSRTSK,1          IS SERV ENQUE COUNT                          
         BH    XX06                YES                                          
         CLM   R2,7,SSBSRTSK+1                                                  
         BNE   XX06                                                             
         XC    SSBSRTSK,SSBSRTSK                                                
**??     MVC   ENQMIN,=CL8'????'                                                
**??     BRAS  RE,DEQ                                                           
         BRAS  RE,WTO                                                           
*                                                                               
XX06     CLI   SSBEWTSK,1          IS EASI ENQUE COUNT                          
         BH    XX08                                                             
         CLM   R2,7,SSBEWTSK+1                                                  
         BNE   XX08                                                             
         XC    SSBEWTSK,SSBEWTSK                                                
         MVC   ENQMIN,=CL8'EASI'                                                
         BRAS  RE,DEQ                                                           
         BRAS  RE,WTO                                                           
*                                                                               
XX08     CLI   SSBFWTSK,1          IS FACWRK ENQUE COUNT                        
         BH    XX10                                                             
         CLM   R2,7,SSBFWTSK+1                                                  
         BNE   XX10                                                             
         XC    SSBFWTSK,SSBFWTSK                                                
         MVC   ENQMIN,=CL8'FACW'                                                
         BRAS  RE,DEQ                                                           
         BRAS  RE,WTO                                                           
*                                                                               
XX10     CLI   SSBWKTSK,1          IS WKFILE ENQUE COUNT                        
         BH    PQ01                                                             
         CLM   R2,7,SSBWKTSK+1                                                  
         BNE   PQ01                                                             
         XC    SSBWKTSK,SSBWKTSK                                                
         MVC   ENQMIN,=CL8'WRKR'                                                
         BRAS  RE,DEQ                                                           
         BRAS  RE,WTO                                                           
                                                                                
***********************************************************************         
* HANDLE PRTQ FILES                                                             
***********************************************************************         
PQ01     LHI   R5,16                                                            
         LA    R4,SSBPQINF         PQ FILES                                     
         LA    R6,CHARS                                                         
         MVC   ENQMIN,PRTQ                                                      
*                                                                               
PQ02     MVC   ENQMIN+2(1),0(R6)                                                
         CLC   1(1,R4),TCBID+6     MATCH CHARACTER?                             
         BNE   PQ04                NO                                           
*                                                                               
         CLI   0(R4),1             ONLY US?                                     
         BH    BADDUN              NO                                           
         XC    0(2,R4),0(R4)                                                    
         BRAS  RE,SETPQ                                                         
         BRAS  RE,DEQ                                                           
         BRAS  RE,WTO                                                           
*                                                                               
PQ04     AHI   R4,2                NEXT PQ ENTRY                                
         AHI   R6,1                                                             
         BCT   R5,PQ02                                                          
                                                                                
***********************************************************************         
* HANDLE WRKF FILES                                                             
***********************************************************************         
WF01     LHI   R5,16                                                            
         LA    R4,SSBWFINF         WRKF FILES                                   
         MVC   ENQMIN,WRKF                                                      
*                                                                               
WF02     MVC   ENQMIN+2(1),0(R6)                                                
         CLC   1(1,R4),TCBID+6     MATCH CHARACTER?                             
         BNE   WF04                NO                                           
         CLI   0(R4),1                                                          
         BH    BADDUN                                                           
         XC    0(2,R4),0(R4)                                                    
         BRAS  RE,SETWF                                                         
         BRAS  RE,DEQ                                                           
         BRAS  RE,WTO                                                           
*                                                                               
WF04     AHI   R4,2                NEXT WKF ENTRY                               
         AHI   R6,1                                                             
         BCT   R5,WF02                                                          
                                                                                
***********************************************************************         
* HANDLE WRKZ FILES                                                             
***********************************************************************         
WZ01     LHI   R5,16                                                            
         LA    R4,SSBWZINF         WRKZ FILES                                   
         MVC   ENQMIN,WRKZ                                                      
*                                                                               
WZ02     MVC   ENQMIN+2(1),0(R6)                                                
         CLC   1(1,R4),TCBID+6     MATCH CHARACTER?                             
         BNE   WZ04                NO                                           
         CLI   0(R4),1                                                          
         BH    BADDUN                                                           
         XC    0(2,R4),0(R4)                                                    
         BRAS  RE,SETWZ                                                         
         BRAS  RE,DEQ                                                           
         BRAS  RE,WTO                                                           
*                                                                               
WZ04     AHI   R4,2                NEXT WKZ ENTRY                               
         AHI   R6,1                                                             
         BCT   R5,WZ02                                                          
         XIT1  ,                                                                
         EJECT ,                                                                
***********************************************************************         
* HANDLE TST SYSTEM                                                             
***********************************************************************         
TSTLOOP  CLM   R2,7,SSBCTTSK+1     CONTROL FILE ENQUE COUNT                     
         BNE   *+6                                                              
         DC    H'0'                                                             
*&&UK                                                                           
         CLM   R2,7,SSBMZTSK+1     MEDZ                                         
         BNE   *+6                                                              
         DC    H'0'                                                             
*&&                                                                             
         CLM   R2,7,SSBSRTSK+1     SERV                                         
         BNE   *+6                                                              
         DC    H'0'                                                             
*                                                                               
         CLM   R2,7,SSBEWTSK+1     EASI                                         
         BNE   *+6                                                              
         DC    H'0'                                                             
***********************************************************************         
* HANDLE PRTQ FILES                                                             
***********************************************************************         
         USING SSBD,R3                                                          
TLOOPPQ  L     R3,=V(SSB)                                                       
         LHI   R5,16                                                            
         LA    R4,SSBPQINF         PQ FILES                                     
*                                                                               
TLOOPPQ1 CLC   1(1,R4),TCBID+6     MATCH CHARACTER?                             
         BNE   TLOOPPQ2            NO                                           
         CLI   0(R4),1             ONLY US?                                     
         BH    BADDUN              NO                                           
         DC    H'0'                                                             
*                                                                               
TLOOPPQ2 AHI   R4,2                NEXT PQ ENTRY                                
         BCT   R5,TLOOPPQ1                                                      
***********************************************************************         
* HANDLE WRKF FILES                                                             
***********************************************************************         
TLOOPWF  LHI   R5,16                                                            
         LA    R4,SSBWFINF         WRKF FILES                                   
*                                                                               
TLOOPWF1 CLC   1(1,R4),TCBID+6     MATCH CHARACTER?                             
         BNE   TLOOPWF2            NO                                           
         CLI   0(R4),1                                                          
         BH    BADDUN                                                           
         DC    H'0'                                                             
*                                                                               
TLOOPWF2 AHI   R4,2                NEXT WKF ENTRY                               
         BCT   R5,TLOOPWF1                                                      
***********************************************************************         
* HANDLE WRKZ FILES                                                             
***********************************************************************         
TLOOPWZ  LHI   R5,16                                                            
         LA    R4,SSBWZINF         WRKZ FILES                                   
*                                                                               
TLOOPWZ1 CLC   1(1,R4),TCBID+6     MATCH CHARACTER?                             
         BNE   TLOOPWZ2            NO                                           
         CLI   0(R4),1                                                          
         BH    BADDUN                                                           
         DC    H'0'                                                             
*                                                                               
TLOOPWZ2 AHI   R4,2                NEXT WKZ ENTRY                               
         BCT   R5,TLOOPWZ1                                                      
         XIT1  ,                                                                
*                                                                               
BADDUN   DC    H'0'                                                             
         EJECT ,                                                                
***********************************************************************         
* OTHER ROUTINES                                                                
***********************************************************************         
WTO      NTR1  ,                                                                
         WTO   TEXT=HITIT                                                       
         XIT1  ,                                                                
*                                                                               
DEQ      NTR1  ,                                                                
         LA    R2,ENQMAJ                                                        
         LA    R3,ENQMIN                                                        
         DEQ   ((2),(3),8,STEP),RET=HAVE                                        
         XIT1  ,                                                                
*                                                                               
SETPQ    NTR1  ,                                                                
         ICM   RF,15,=V(PRTQLST)                                                
         LA    RF,8(RF)                                                         
         B     SET10                                                            
*                                                                               
SETWF    NTR1  ,                                                                
         ICM   RF,15,=V(WRKFLST)                                                
         LA    RF,8(RF)                                                         
         B     SET10                                                            
*                                                                               
SETWZ    NTR1  ,                                                                
         ICM   RF,15,=V(WRZFLST)                                                
         LA    RF,8(RF)                                                         
         B     SET10                                                            
*                                                                               
SET10    CLI   0(RF),0             SEARCH FOR CHR  (PQ, WKF, WKZ)               
         BE    SETXIT                                                           
         CLC   1(1,RF),0(R6)                                                    
         BE    *+12                                                             
         LA    RF,8(RF)                                                         
         B     SET10                                                            
*                                                                               
         USING DTFPHD,RF                                                        
         MVC   FILINFO(8),0(RF)   SAVE PRTQ INFO FROM LIST                      
                                                                                
         SAM31                                                                  
         XR    RF,RF                                                            
         ICM   RF,7,FILDTF         GET FILE SUBID INTO DUB                      
         BZ    SETXIT                                                           
         LA    RE,DMTX                                                          
         TM    DIND,DINDXAM        HIGH CORE EXTENT MATRIX                      
         BZ    *+8                                                              
         ICM   RE,15,DMTX                                                       
         DROP  RF                                                               
                                                                                
         USING EXTENTD,RE                                                       
         SR    R0,R0                                                            
         ICM   R0,3,EXT#TRKS       USE NUM OF TRACKS IN FIRST EXTENT            
         BZ    SETXIT                                                           
         DROP  RE                                                               
                                                                                
         CVD   R0,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  ENQMIN+4(4),DUB                                                  
SETXIT   XIT1  ,                                                                
         EJECT                                                                  
***********************************************************************         
* LITERALS AND CONSTANTS                                              *         
***********************************************************************         
         LTORG                                                                  
HITIT    DC    AL2(38),CL18'HIT SSBFIX CODE - '                                 
ENQMAJ   DC    CL8'ENQDEQ  '                                                    
ENQMIN   DC    CL8'        '                                                    
         DC    CL4' '                                                           
*                                                                               
PRTQ     DC    CL8'PRTQ'                                                        
WRKF     DC    CL8'WRKF'                                                        
WRKZ     DC    CL8'WRKZ'                                                        
CHARS    DC    C'123456789ABCEDFG'                                              
*                                                                               
FILINFO  DS    0XL8                THIS USED FOR PQ, WKF, WRZ                   
FILINT   DS    X                                                                
FILCHR   DS    X                                                                
         DS    XL2                                                              
FILEXT   DS    X                                                                
FILDTF   DS    AL3                                                              
*                                                                               
DUB      DS    D                                                                
         EJECT                                                                  
***********************************************************************         
* OTHER INCLUDED DSECTS                                               *         
***********************************************************************         
* FASSB                                                                         
         PRINT OFF                                                              
       ++INCLUDE FASSB                                                          
         PRINT ON                                                               
* FATCB                                                                         
         PRINT OFF                                                              
       ++INCLUDE FATCB                                                          
         PRINT ON                                                               
* DMDTFPH                                                                       
         PRINT OFF                                                              
       ++INCLUDE DMDTFPH                                                        
         PRINT ON                                                               
* DMXTNTD                                                                       
         PRINT OFF                                                              
       ++INCLUDE DMXTNTD                                                        
         PRINT ON                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'013DDSSBFIX  05/12/11'                                      
         END                                                                    
