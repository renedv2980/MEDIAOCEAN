*          DATA SET DDCLUNPK   AT LEVEL 009 AS OF 11/06/85                      
*CATALP CLUNPK                                                                  
         TITLE 'MODULE CONVERTS TWO CHARACTER CLIENT CODES TO THREE'            
********************************************************************            
*                                                                               
* P1+0(1) = C'Y'  TO TRANSLATE CLIENT CODE AS AAN WHERE N=0-9                   
*         = OTHER TO TRANSLATE CLIENT CODE AS AAA                               
*                                                                               
* P1+1(3) = AL3(PACKED CLIENT CODE)                                             
*                                                                               
* P2+1(3) = AL3(OUTPUT FIELD)                                                   
*                                                                               
********************************************************************            
         TITLE 'MODULE CONVERTS TWO CHARACTER CLIENT CODES TO THREE'            
CLUNPK   CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 1,*CLUNPK*                                                       
         USING CLUD,RC                                                          
         LM    R2,R3,0(R1)                                                      
         MVC   0(2,R3),0(R2)                                                    
         MVI   2(R3),C' '                                                       
         TM    0(R2),X'80'         (X'80' = 2 BYTE CODE)                        
         BNO   XIT                                                              
         SPACE 1                                                                
*                                  GET 15-BIT CODE LEFT-ALIGNED IN R5           
         SR    R5,R5                                                            
         IC    R5,0(R2)                                                         
         SLL   R5,8                                                             
         IC    R5,1(R2)                                                         
         SLL   R5,17                                                            
         LA    R6,3                                                             
         SPACE 2                                                                
CU2      SR    R4,R4                                                            
         SLDL  R4,5                                                             
         LA    R4,TABLE(R4)                                                     
         MVC   0(1,R3),0(R4)                                                    
         LA    R3,1(R3)                                                         
         BCT   R6,CU2                                                           
*                                                                               
         CLI   0(R1),C'Y'          TEST TO FORCE LAST CHAR NUMERIC              
         BNE   XIT                                                              
         BCTR  R3,0                BACK UP                                      
         CLI   0(R3),C'F'                                                       
         BL    XIT                                                              
         CLI   0(R3),C'J'                                                       
         BH    XIT                                                              
         OI    0(R3),X'F0'         MAKE NUMERIC (F=6,G=7,H=8,I=9,J=0)           
         CLI   0(R3),C'1'          TEST FOR J                                   
         BNE   XIT                                                              
         MVI   0(R3),C'0'          WHICH BECOMES 0                              
         SPACE 2                                                                
XIT      XIT1                                                                   
         SPACE 2                                                                
TABLE    DC    C'ABCDEFGHIJKLMNOPQRSTUVWXYZ12345 '                              
CLUD     DSECT                                                                  
DUB      DS    D                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'009DDCLUNPK  11/06/85'                                      
         END                                                                    
