*          DATA SET NEUBILLRDR AT LEVEL 061 AS OF 04/28/11                      
*          DATA SET NEUBILLRDN AT LEVEL 067 AS OF 06/18/07                      
*CATALP NETBLRDR                                                                
         TITLE 'NETBLRDR- NEGENUBILL READER FOR NETWORK'                        
*************************************************************                   
*                                                                               
* THIS IS 'NEW' BILLRDR WITH RECUP (OLD IS NEUBILLRDS IN PZIR LIBRARY)          
*                                                                               
* NETBLRDR READS NEGENUBILL RECORDS                                             
* NETBLRDR READS NEGENUBILL RECORDS                                             
* HOOKS BACK TO CALLER OR SEEDS WLWMWNTS ON UNIT RECORD                         
*                                                                               
* NBLFUNC/NBLFUNC2 DETERMINES ACITON                                            
*                                                                               
* *** THESE FUNCTIONS SEED BILLING ELEMS TO UNIT IN I/O AREA                    
* NBLSEED - SEEDS UNIT REC WITH ALL X'10', REQUIRES VIRTUAL I/O AREA            
* NBLCMPR - SEEDS UNIT WITH X'10' ELEMS, ALL CHARGES BUT                        
*           T/I ARE COMPRESSED INTO ONE X'10' FOR EACH CHARGE TYPE              
*           EXPANDED I/O AREA NOT NECESSARY                                     
*                                                                               
* *** HOOK FUNCTIONS DO NOT AFFECT THE UNIT IN I/O AREA                         
* NBLHRAW - RETURNS ALL 'RAW' NEGENUBILL X'10' - NO FILTERING                   
* NBLHOOK - RETURNS FORMATTED X'10', FILTERING ON NBSPLPRN                      
* NBLHOOK2- RETURNS FORMATTED  X'10' - NO FILTERING                             
*                                                                               
* NBLBLD  - RETURNS 'UNIT HAS BEEN BILLED', NBLFUNC='NBLBILD'                   
*           DOES NOT AFFECT UNIT                                                
*                                                                               
* NOTE->    APPLICATION DOES NOT HAVE TO PASS WORKING STORAGE FOR               
*           BILLRDR DSECT TO BILLREADER- BILLREADER WILL USE ITS OWN            
*           SO NBLCMPR CAN BE SET IN NBINDS2(NETBLOCKD) AND                     
*           BILLREADER WILL TURN ON CORRESPONDING BIT IN NBLFUNC                
*************************************************************                   
NETBLRDR RSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 MYDX-MYD,**NEBL**,R8,CLEAR=YES,RR=R2                             
         USING MYD,RC                                                           
         ST    R2,RELO                                                          
         MVC   USERRD,4(RD)                                                     
         L     RA,0(R1)                                                         
         USING NETBLOCK,RA                                                      
         NI    NBINDS4,X'FF'-X'40' CLEAR 'BILLED' UNIT FLAG                     
         ICM   R9,15,NBABILRD      USER PROVIDE BILLRD DSECT AREA?              
         BNZ   NBL10               YES                                          
         LA    R9,BLRDAREA         NO-USE OURS                                  
         ST    R9,NBABILRD                                                      
         USING NBLBILLD,R9                                                      
NBL10    TM    NBLFUNC,NBLDONT     DONT READ NEGENUBILL RECS?                   
         BO    EXIT                                                             
         TM    NBINDS2,NBNOBLRD    DONT READ NEGENUBILL RECS?                   
         BO    EXIT                                                             
*                                                                               
         TM    NBINDS2,NBLCMPR     CMPRS/SEED ELEMS ?                           
         BZ    *+8                                                              
         OI    NBLFUNC,NBLCMPR     YES/TURN ON FLAG                             
*                                                                               
         TM    NBINDS2,NBAGYB1S    B1S PROFILE TURNED ON?                       
         BO    NBL20               YES/DON'T RE-READ PROFILE                    
*                                                                               
         TM    NBLFUNC2,NBLWBFLT    NEED WBFLITID                               
         BNO   NBL10C                                                           
         MVI   WORK+25,C'Y'        SET TO READ X'0E0A' KEY                      
         B     NBL12               YES/MUST READ BILLRECS FOR THOSE             
*                                      BILLED THEN CHANGED FLIGHT IDS           
* READ BS1 PROFILE FOR NEW BILL REC READ                                        
NBL10C   OI    NBINDS2,NBNOBLRD    DEFAULT-DONT READ NEGENUBILL RECS            
         XC    WORK,WORK                                                        
         MVC   WORK(4),=C'SB1S'                                                 
         NI    WORK,X'BF'          LOWER CASE                                   
         MVC   WORK+4(2),NBSELAGY     AGENCY                                    
         GOTO1 NBGTPROF,DMCB,(X'C0',WORK),WORK+20,NBDM                          
         CLI   WORK+23,C'Y'        MUST READ NEW RECORDS?                       
         BE    NBL12                                                            
         CLI   WORK+23,C'R'        MUST READ NEW RECORDS?                       
         BNE   EXIT                                                             
NBL12    NI    NBINDS2,X'FF'-NBNOBLRD     CLEAR DEFAULT BIT SET ABOVE           
         OI    NBINDS2,NBAGYB1S           SET TO READ                           
         CLI   WORK+25,C'Y'        READ X'0E0A' KEY ?                           
         BNE   NBL20               NO                                           
         CLI   NBSEQ,C'Q'          NETIO USING X'84' KEY?                       
         BE    NBL20               YES-DON'T USE X'0E0A'                        
         OI    NBINDS4,NBI40E0A    SET X'0E0A'                                  
         B     NBL20                                                            
*                                                                               
EXIT     XIT1                                                                   
*                                                                               
         EJECT                                                                  
* INITIAL CHECKS                                                                
*********************************************************                       
NBL20    DS    0H                                                               
*                                                                               
NBL21    ICM   R2,15,NBLUNAIO      UNIT I/O AREA PROVIDED                       
         BNZ   NBL22                                                            
         MVC   NBLUNAIO,NBAIO      ELSE USE NBAIO                               
         ICM   R2,15,NBLUNAIO      AND SET INTO R2!!!!!!                        
*                                                                               
NBL22    CLI   0(R2),X'04'         MUST BE UNIT                                 
         BNE   EXIT                                                             
*                                                                               
NBL24    DS    0H                     CHECK NBLFUNC2 FIRST                      
         TM    NBLFUNC2,NBLDOLS       RETURN $$$ ONLY?                          
         BZ    NBL24C                                                           
         OC    NBLADLRS,NBLADLRS   RETURN AREA SUPPLIED?                        
         BNZ   NBL28                                                            
         DC    H'0'                                                             
*                                                                               
NBL24C   TM    NBLFUNC,NBLHOOK+NBLHRAW+NBLHOOK2     HOOKING?                    
         BZ    NBL26                                                            
         OC    NBL10AIO,NBL10AIO   YES/MUST SUPPLY RETURN AREA                  
         BNZ   NBL28                                                            
         DC    H'0'                NO HOOK RETURN AREA                          
*                                                                               
NBL26    DS    0H                   NOT HOOKING                                 
         TM    NBLFUNC,NBLBLD     ONLY CHK FOR BILLED STATUS?                   
         BNO   NBL27                   NO                                       
         NI    NBLFUNC,X'FF'-NBLBILD   YES/CLEAR RETURN STATUS BIT              
         NI    NBINDS4,X'FF'-NBI40E0A     DON'T READ X'0E0A' KEY                
         B     NBL28                                                            
*                                                                               
NBL27    DS    0H                  SEEDING UNIT                                 
         TM    NBLFUNC,NBLSEED+NBLCMPR                                          
         BNZ   *+6                                                              
         DC    H'0'                      MUST HAVE FUNCTION                     
         CLI   NBWHERE,C'I'              FROM NETIO?                            
         BE    NBL28                     SEEDING MUST COME FROM NETIO           
         DC    H'0'                                                             
****************************************************************                
* INITITAL CHECK ENDED                                                          
                                                                                
         EJECT                                                                  
* READ FOR NEW BILLING RECORD                                                   
NBL28    DS    0H                                                               
         TM    NBLFUNC,NBLBLD      RETURN BILLED FLAG ONLY?                     
         BO    NBL28X              DON'T STRIP UNIT                             
         TM    NBLFUNC2,NBLDOLS     RETURN $$$ IN I/O AREA                      
         BO    NBL28X              DON'T STRIP UNIT                             
         L     R5,NBLUNAIO              ,,STRIP X'10' FROM UNIT                 
         MVC   WORK(8),=C'UNTFILE '                                             
         GOTO1 NBHELLO,DMCB,(C'D',WORK),(X'10',(R5)),0,0                        
NBL28X   DS    0H                                                               
*                                                                               
* CREATE NEGENUBILL KEY FROM UNIT KEY                                           
                                                                                
         MVI   FIRSTIME,C'Y'                                                    
         L     R2,NBLUNAIO        R2 -> UNIT KEY                                
         USING NURECD,R2                                                        
         XC    KEY,KEY                                                          
         LA    R3,KEY             R3-> NEGENUBILL KEY                           
         USING NUBKEY,R3                                                        
         TM    NBINDS4,NBI40E0A   READ 0E0A?                                    
         BNO   USE84KY                                                          
*                                                                               
***************************************************                             
* USE X'04' KEY AS DEFAULT                                                      
         MVI   NUBKSYS,NUBK0SYQ    X'0E'                                        
         MVI   NUBKSTYP,NUBK0STQ   X'0A'                                        
         MVC   NUBK0AM(19),1(R2)                                                
         MVC   NUBK0BDT,NBLRDAT    BILL RUN FILTER?                             
*                                                                               
         TM    NBINDS5,NBI5XRD     DO SPECIAL READING?                          
         BNO   NBL28Z              NO                                           
         CLC   KEYCHK+2(19),KEY+2  YES                                          
         BNE   NBL28Z                                                           
* CHECK LAST XSPDIR KEY READ                                                    
         OC    ADISPKEY,ADISPKEY   DO I ALREADY HAVE ADDRESS?                   
         BNZ   NBL28Y                                                           
         GOTO1 NBDM,DMCB,=C'DTFAD',=C'XSPDIR  ',0,0                             
         ICM   R1,15,12(R1)                                                     
         BZ    NBL28Z                                                           
         USING ISDTF,R1                                                         
         ICM   R1,15,ISPDKEY    A(HIGHKEY)                                      
         BZ    NBL28Z                                                           
         STCM  R1,15,ADISPKEY                                                   
NBL28Y   DS    0H                                                               
         DROP  R1                                                               
*                                                                               
         LA    RF,*+10          SET 31 BIT MODE                                 
         O     RF,=X'80000000'                                                  
         BSM   0,RF                                                             
*                                                                               
         ICM   R1,15,ADISPKEY                                                   
         MVC   KEYSAVE(32),0(R1)    LAST XDIR KEY READ                          
*                                                                               
         LA    RF,*+6           RETURN TO 24 BIT MODE                           
         BSM   0,RF                                                             
*                                                                               
         CLC   KEYSAVE(32),KEYCHK      LAST XDIR KEY = KEYCHK?                  
         BNE   NBL28Z              NOT=, THEN READHI                            
****     BE    *+6                 YES                                          
****     DC    H'0'                SHOULD NOT GET HERE!                         
         MVC   KEY,KEYCHK                                                       
         MVC   KEYSAVE,KEY                                                      
         B     NBL50                                                            
NBL28Z   DS   0H                                                                
         MVC   KEYSAVE,KEY                                                      
         GOTO1 NBDM,DMCB,(X'08',=CL8'DMRDHI'),=C'XSPDIR  ',KEY,KEY,0            
NBL29    TM    DMCB+8,X'FF'-X'02'  KEY MIGHT BE DELETED                         
         BZ    *+6                                                              
         DC    H'0'                ALL ELSE TAKE A HIT                          
         MVC   KEYCHK,KEY          NEED THIS HERE -                             
         CLC   KEY(21),KEYSAVE     MUST MATCH!                                  
         BNE   EXIT                                                             
         TM    NUBKSTAT,X'80'      IF DELETED?                                  
         BO    NBLSEQ              TRY AGAIN                                    
***->                                                                           
******   MVC   KEYCHK,KEY                                                       
         CLC   KEY(32),KEYSAVE     FULL KEY                                     
         BE    NBL50                                                            
         OC    NBLRDAT,NBLRDAT     BILL RUN FILTER?                             
         BNZ   EXIT                YES/NO MATCH                                 
         CLC   KEY(21),KEYSAVE     NO/X'04' KEY MATCH?                          
         BNE   EXIT                                                             
         OC    NBLRSTRT,NBLREND    BILL RUN DATE FILTER?                        
         BZ    NBL50               NO                                           
         CLC   NUBK0BDT,NBLRSTRT   BILL RUN DATE<START DATE FILTER              
         BL    NBLSEQ                                                           
         CLC   NUBK0BDT,NBLREND    BILL RUN DATE>END DATE FILTR                 
         BH    NBLSEQ                                                           
         B     NBL50                                                            
********************************************************                        
                                                                                
* USE X'84' KEY                                                                 
USE84KY  DS    0H                                                               
         MVI   NUBKSYS,NUBKSYSQ    X'0E'                                        
         MVI   NUBKSTYP,NUBKSTYQ   X'06'                                        
         MVC   NUBKAM,NUKAM          X'84' KEY                                  
         MVC   NUBKCLI,NUKCLT                                                   
         MVC   NUBKNET,NUKNET                                                   
         MVC   NUBKPROG,NUKPROG                                                 
         MVC   NUBKDATE,NUKDATE                                                 
         MVC   NUBKEST,NUKEST                                                   
         MVC   NUBKSUB,NUKSUB                                                   
         MVC   NUBKDPT,NUKDP                                                    
*                                                                               
         MVC   NUBKBDAT,NBLRDAT    BILL RUN FILTER?                             
*                                                                               
         MVC   KEYSAVE,KEY                                                      
         GOTO1 NBDM,DMCB,(X'08',=CL8'DMRDHI'),=C'XSPDIR  ',KEY,KEY,0            
NBL30    TM    DMCB+8,X'FF'-X'02'  KEY MIGHT BE DELETED                         
         BZ    *+6                                                              
         DC    H'0'                ALL ELSE TAKE A HIT                          
         CLC   KEY(20),KEYSAVE     MUST MATCH                                   
         BNE   EXIT                                                             
         TM    NUBKSTAT,X'80'      IF DELETED                                   
         BO    NBLSEQ              TRY AGAIN                                    
*                                                                               
         CLC   KEY(32),KEYSAVE     FULL KEY                                     
         BE    NBL50                                                            
         OC    NBLRDAT,NBLRDAT     BILL RUN FILTER?                             
         BNZ   EXIT                YES/NO MATCH                                 
         CLC   KEY(20),KEYSAVE     NO/X'84' KEY MATCH?                          
         BNE   EXIT                                                             
******** GOTO1 =V(PRNTBL),DMCB,=C'KHI',KEY,C'DUMP',50,=C'1D'                    
         OC    NBLRSTRT,NBLREND    BILL RUN DATE FILTER?                        
         BZ    NBL50               NO                                           
         CLC   NUBKBDAT,NBLRSTRT   BILL RUN DATE<START DATE FILTER              
         BL    NBLSEQ                                                           
         CLC   NUBKBDAT,NBLREND    BILL RUN DATE>END DATE FILTR                 
         BH    NBLSEQ                                                           
         B     NBL50                                                            
         DROP  R2                                                               
*                                                                               
NBL50    DS    0H                                                               
         BAS   RE,PROCBILL         PROCESS THIS BILL REC                        
         TM    NBLFUNC,NBLBLD      ,,UNIT BILLED STATUS ?                       
         BNO   NBLSEQ              ,,NO                                         
         TM    NBLFUNC,NBLBILD     ,,IS UNIT BILLED?                            
         BO    EXIT                ,,THEN EXIT                                  
NBLSEQ   DS    0H                GET NEXT BLL REC                               
         GOTO1 NBDM,DMCB,(X'08',=CL8'DMRSEQ'),=C'XSPDIR  ',KEY,KEY,0            
         TM    NBINDS4,NBI40E0A                                                 
         BO    NBL29               X'04' KEY                                    
         B     NBL30               X'84' KEY                                    
         EJECT                                                                  
***********************************************************                     
* PROCBILL READS NEGENUBILL RECORD                                              
* POINTS R3 TO 1ST BILLELEM                                                     
* BASED ON NBLFUNC CHOOSES HOW TO PROCESS BILLELEMS                             
***********************************************************                     
PROCBILL NTR1                                                                   
         GOTO1 GETREC              GET NEGENUBILL REC                           
         LA    R3,UNBILL                                                        
         USING NUBKEY,R3                                                        
         CLC   0(2,R3),=X'0E06'       0E06 KEY                                  
         BNE   PRB0A                                                            
         MVC   NBLRUNDT,NUBKBDAT      SET TO BILLRDR DSECT                      
         MVC   RUNDTSV,NUBKBDAT       SAVE DATE OF BILLING RUN                  
         B     PRB0B                                                            
PRB0A    CLC   0(2,R3),=X'0E0A'       0E0A KEY                                  
         BE    *+6                                                              
         DC    H'0'                                                             
         MVC   NBLRUNDT,NUBK0BDT      SET TO BILLRDR DSECT                      
         MVC   RUNDTSV,NUBK0BDT       SAVE DATE OF BILLING RUN                  
         B     PRB0B                                                            
PRB0B    LA    R3,NUBELDQ(R3)      POINT TO FIRST ELEM                          
PRB01    CLI   0(R3),0                                                          
         BE    EXIT                                                             
         CLI   0(R3),X'10'                                                      
         BE    PRB10                                                            
         ZIC   R1,1(R3)                                                         
         AR    R3,R1                                                            
         B     PRB01                                                            
*                                                                               
PRB10    TM    NBLFUNC2,NBLDOLS    RETURN GROSS/NET DOLLARS                     
         BZ    PRB10C                                                           
         BAS   RE,GETDOLS                                                       
         B     PRBX                                                             
PRB10C   TM    NBLFUNC,NBLHRAW     HOOK WITH RAW NEGENUBILL ELEM?               
         BNO   PRB15                                                            
PRB12    BAS   RE,FORMATRW         YES/FORMAT AND HOOK                          
         NI    NBLFUNC2,X'FF'-NBLCNVRT                                          
         B     PRBX                                                             
*                                                                               
PRB15    TM    NBLFUNC,NBLBLD      BILLED STATUS ONLY?                          
         BNO   PRB20                                                            
         BAS   RE,BLDSTAT          YES/IS IT BILLED?                            
         B     PRBX                                                             
*                                                                               
PRB20    TM    NBLFUNC,NBLSEED+NBLCMPR  ,,SEED UNIT WITH BILL ELEM?             
         BZ    PRB25                                                            
         CLI   NBWHERE,C'I'             ,,MUST COME FROM NETIO                  
         BNE   EXIT                     ,,ELSE EXIT                             
         MVI   FIRSTIME,C'N'                                                    
                                                                                
PRB25    BAS   RE,FORMAT10         FORMAT UNIT X'10' ELEM                       
*                                                                               
PRBX     B     EXIT                                                             
         DROP  R3                                                               
         EJECT                                                                  
**********************************************************                      
* FORMATS UNIT X'10' ELEM FROM NEW UNIT BILLING RECORD ELEM                     
* EXPECTS R3 -> FIRST X'10' ELEM IN NEGENUBILL                                  
*                                                                               
* BASED ON NBLFUNC EITHER HOOKS TO APPLICATION OR                               
* ADDS X'10' ELEMS TO UNIT RECORD                                               
***********************************************************                     
FORMAT10 NTR1                                                                   
         USING NBILD,R3                                                         
*                                                                               
         TM    NBLFUNC,NBLSEED     SEED UNIT WITH ALL BILL RECS?                
         BZ    FORMAT30                                                         
FORMAT20 BAS   RE,FMTELEM          FORMAT X'10' ELEMENT                         
***      BAS   RE,ADDELEM          ADD TO UNIT REC                              
*******************************************************                         
         MVI   ELCODE,X'01'        FIND WHERE TO ADD ELEMENT                    
         MVC   DATDSP,=X'001B'                                                  
         L     R2,NBLUNAIO                                                      
         BAS   RE,GETEL                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
FORMAT22 CLI   0(R2),X'10'                                                      
         BH    FORMAT25                                                         
         ZIC   R1,1(R2)                                                         
         LTR   R1,R1                                                            
         BNZ   *+6                                                              
         DC    H'0'                                                             
         AR    R2,R1                                                            
         B     FORMAT22                                                         
FORMAT25 L     RF,=V(RECUP)                                                     
         LTR   RF,RF                                                            
         BNZ   *+6                                                              
         DC    H'0'                                                             
         A     RF,RELO                                                          
* ELEM START IS X'001B' INTO RECORD                                             
* RECORD LEN IS X'0014' INTO RECORD                                             
* MAX RECORD LENGTH IS X'1388' (5000)                                           
                                                                                
         GOTO1 (RF),DMCB,(X'FE',NBLUNAIO),ELEM,(R2),=X'001B00141388'            
                                                                                
         BAS   RE,BUMP             GET NEXT BILL REC ELEM                       
***      BE    FORMAT20                                                         
***      B     EXIT                                                             
         BNE   FORMAT26                                                         
         BAS   RE,FMTELEM                                                       
         B     FORMAT25            R2 SHOULD STILL POINT TO WHERE               
*                                  TO ADD RECORD                                
FORMAT26 L     R1,NBLUNAIO         ADD ZERO TO REC END-RECUP DOES NOT           
         USING NURECD,R1                                                        
         SR    RE,RE                                                            
         ICM   RE,3,NURLEN         BUMP REC LEN                                 
         LA    RE,1(RE)                                                         
         STCM  RE,3,NURLEN                                                      
         BCTR  RE,0                PREPARE FOR ADD                              
         AR    R1,RE               POINT R1 TO LAST POSITION IN REC             
         MVI   0(R1),0             SET ZERO                                     
         B     EXIT                                                             
*                                  TO ADD RECORD                                
*******************************************************                         
*                                                                               
FORMAT30 TM    NBLFUNC,NBLHOOK2    RETURN ALL X'10' TO USER                     
         BZ    FORMAT50                                                         
FORMAT40 BAS   RE,FMTELEM                                                       
         BAS   RE,HOOKUSER                                                      
         BAS   RE,BUMP                                                          
         BE    FORMAT40                                                         
         B     EXIT                                                             
*                                                                               
FORMAT50 TM    NBLFUNC,NBLHOOK     FILTER/RETURN X'10' TO USER                  
         BZ    FORMAT70                                                         
FORMAT60 CLI   NBSPLPRN,0                                                       
         BE    *+14                                                             
         CLC   NBSPLPRN,NBILPRD    MATCH PROD ON BILL ELEM?                     
         BNE   FORMAT65                                                         
         BAS   RE,FMTELEM          YES/FORMAT X'10' ELEM                        
         BAS   RE,HOOKUSER                                                      
FORMAT65 BAS   RE,BUMP                                                          
         BE    FORMAT60                                                         
         B     EXIT                                                             
*                                                                               
FORMAT70 TM    NBLFUNC,NBLCMPR     MUST BE COMPRESS FUNTION                     
         BNZ   FORMAT75            ELSE EXIT                                    
         DC    H'0'                MUST BE SOMETHING !                          
FORMAT75 CLI   NBSPLPRN,0          NETBLOCK PROD FILTER?                        
         BE    *+14                                                             
         CLC   NBSPLPRN,NBILPRD    MATCH BILLED PROD?                           
         BNE   FRMT110             NO.GET NEXT ELEM                             
*                                                                               
FORMAT80 CLI   NBILCHGT,C'T'       COMPRESS ALL CHARGES BUT T/I                 
         BE    *+12                                                             
         CLI   NBILCHGT,C'I'       INTO ONE X'10' ELEM FOR EACH TYPE            
         BNE   FORMAT85            OF SPECIAL CHARGE                            
         BAS   RE,FMTELEM                                                       
         BAS   RE,ADDELEM                                                       
         B     FRMT110             GET NEXT ELEM                                
*                                                                               
         EJECT                                                                  
* CHECK SPCHGTBL HAS ALL SPECIAL CHARGE CODES                                   
FORMAT85 TM    NBILST,NBILUBQ      UNBILLED?                                    
         BO    FRMT110                                                          
         LA    RE,SPCHGTBL         TABEL OF SPECIAL CHARGES                     
         LA    RF,SPCHGEQ          NUMBEROF SPECIAL CHARGES                     
FORMAT90 CLC   NBILCHGT,0(RE)      SPECIAL CHARGE MATCHES TABLE?                
         BE    FORMAT95                                                         
         LA    RE,1(RE)                                                         
         BCT   RF,FORMAT90                                                      
         DC    H'0'                NEED TO ADD CHARGE CODE TO TABLE             
*                                                                               
FORMAT95 LA    RE,SPCHGRS          SPECIAL CHARGE TABLE SAVE AREA               
         MVI   SPCHGEOF,X'FF'      SET EOF                                      
         CLI   0(RE),0             TABLE EMPTY?                                 
         BE    FRMT105             YES                                          
*                                                                               
FRMT100  CLC   0(1,RE),NBILCHGT    ,,IF CODE ALREADY IN TABLE                   
         BE    FRMT105             ,,ADD $$$ TO IT                              
         LA    RE,12(RE)           BUMP TABLE                                   
         CLI   0(RE),0                                                          
         BE    FRMT105                                                          
         CLI   0(RE),X'FF'         EOF                                          
         BNE   FRMT100                                                          
         DC    H'0'                SHOULD NEVER GET HERE                        
FRMT105  MVC   0(1,RE),NBILCHGT    SET CODE                                     
         L     R4,NBILGRS                                                       
         L     R1,4(RE)                                                         
         AR    R4,R1                                                            
         ST    R4,4(RE)                                                         
         L     R4,NBILNET                                                       
         L     R1,8(RE)                                                         
         AR    R4,R1                                                            
         ST    R4,8(RE)                                                         
*                                                                               
FRMT110  BAS   RE,BUMP             GET NEXT ELEM                                
         BE    FORMAT75                                                         
*                                  NO MORE ELEMS ONBILL REC                     
         LA    R3,SPCHGRS          ANY SPECIAL CHARGES?                         
FRMT115  CLI   0(R3),0                                                          
         BE    FRMT130             NO-EXIT                                      
         LA    R2,ELEM             YES-ADD TO UNIT                              
         USING NUBILD,R2                                                        
         XC    ELEM,ELEM           ADD COMPOSITE NON-T/I ELEM TO UNIT           
         MVI   0(R2),X'10'                                                      
         MVI   1(R2),28            LENGTH=28(WILL USE NUBILGR2)                 
         MVC   NUBILTYP,0(R3)      SET CHARGE TYPE                              
         MVC   NUBILGRS,4(R3)      GROSS BILLING                                
         MVC   NUBILNET,8(R3)      NET BILLING                                  
         BAS   RE,ADDELEM                                                       
         LA    R3,12(R3)                                                        
         CLI   0(R3),X'FF'         EOF?                                         
         BNE   FRMT115             NO/GET NEXT CHARGE                           
*                                                                               
FRMT130  LA    RE,SPCHGRS          CLEAR SPECIAL CHARGE TABLE                   
*******  LA    RF,SPCHGLEN                                                      
*******  XCEF                                                                   
         LHI   RF,SPCHGLEN                                                      
         SR    R0,R0                                                            
         SR    R1,R1                                                            
         MVCL  RE,R0                                                            
         B     EXIT                                                             
         EJECT                                                                  
********************************************************                        
* GO THROUGH BILLING ELEMS TO SEE IF UNBILLED                                   
* EXPECTS R3 -> FIRST X'10' ELEM IN NEGENUBILL                                  
* RETURNS NBLFUNC=NBLBILD  IF BILLED                                            
********************************************************                        
* RETURNS GROSS AND NET DOLLARS IN CALLER SUPPLIED 2 PL8 AREAS                  
GETDOLS  NTR1                                                                   
         USING NBILD,R3                                                         
         L     R2,NBLADLRS         2 PL8 AREAS                                  
GETDL10  DS    0H                                                               
         TM    NBILST,NBILUBQ      UNBILLED?                                    
         BO    GETDL12             YES                                          
         L     R1,NBILGRS                                                       
         CVD   R1,DUB                                                           
         AP    0(8,R2),DUB                                                      
         L     R1,NBILNET                                                       
         CVD   R1,DUB                                                           
         AP    8(8,R2),DUB                                                      
GETDL12  BAS   RE,BUMP             BUMP TO NEXT ELEM                            
         BE    GETDL10                                                          
GETDLX   B     EXIT                                                             
*                                                                               
********************************************************                        
BLDSTAT  NTR1                                                                   
         USING NBILD,R3                                                         
BLDST10  DS    0H                                                               
         TM    NBILST,NBILUBQ      UNBILLED?                                    
         BO    BLDST12             YES                                          
         OI    NBLFUNC,NBLBILD     NO/SET BILLED FLAG                           
         B     BLDSTX              EXIT                                         
****->   BAS   RE,GOHOOK           NOT NECESSARY??                              
BLDST12  BAS   RE,BUMP             BUMP TO NEXT ELEM                            
         BE    BLDST10                                                          
BLDSTX   B     EXIT                                                             
*                                                                               
                                                                                
**********************************************************                      
* STRIP BILLING ELEMS OFF NEGENUBILL REC/SEND ONE AT A TIME                     
* EXPECTS R3 -> FIRST X'10' ELEM IN NEGENUBILL                                  
**********************************************************                      
FORMATRW NTR1                                                                   
         USING NBILD,R3                                                         
         ICM   R2,15,NBL10AIO      USER SUPPLIED RETURN AREA                    
         BNZ   *+6                                                              
         DC    H'0'                SHOULD NEVER GET HERE                        
         LR    R4,R3               USE R4                                       
FRMRW5   ZIC   R1,1(R4)                                                         
         LTR   R1,R1                                                            
         BZ    FRMRW10                                                          
         AR    R4,R1                                                            
         CLI   0(R4),X'99'         SUSHEEL CONVERTED BUY?                       
         BNE   FRMRW7                                                           
         OI    NBLFUNC2,NBLCNVRT   SUSHEELCONVERTED REC                         
         B     FRMRW10                                                          
FRMRW7   CLI   0(R4),0                                                          
         BNE   FRMRW5                                                           
*                                                                               
FRMRW10  DS    0H                                                               
         ZIC   R1,NBILLEN                                                       
         BCTR  R1,0                FOR EX MOVE                                  
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R2),0(R3)       MOVE ELEM                                    
         BAS   RE,GOHOOK                                                        
         BAS   RE,BUMP             BUMP TO NEXT ELEM                            
         BE    FRMRW10                                                          
FRMRWX   B     EXIT                                                             
         EJECT                                                                  
*                                                                               
**************************************************************                  
* WILL USE NUBILGR2 + SPARE TO STORE MARKET/STATION                             
* ONLY BILLING USES THIS FIELD AND BILLING WILL NO                              
* LONGER READ THESE ELMENTS BUT WILL READ THE NEW BILLING RECORDS               
* *** SEP9/04  ABOVE IS NO LONGER THE CASE                                      
FMTELEM  NTR1                                                                   
         LA    R2,ELEM                                                          
         USING NUBILD,R2                                                        
         XC    ELEM,ELEM                                                        
         MVI   0(R2),X'10'                                                      
         MVI   1(R2),29            LENGTH=29                                    
         CLI   NBILPRDC,X'40'      ***IF NO 3 CHARACTER PROD                    
         BH    *+8                                                              
         MVI   1(R2),28            ***PUT OUT OLD STYLE ELEMENT                 
*                                                                               
         CLI   NBILLEN,X'31'       WBFLIGHT ID ON ELEM?                         
         BL    FMTEL10                                                          
         TM    NBLFUNC2,NBLWBFLT    DO WE WANT WBFLIGHT DATA ?                  
         BZ    FMTEL10             NO                                           
         MVI   1(R2),NUBILLN3      YES-ADD 10 FOR WBFLIGHT ID                   
         MVC   NUBILFLT,NBILFLT                                                 
*                                                                               
FMTEL10  MVC   NUBILTYP,NBILCHGT   TYPE (T=TIME,I=INTG, ETC)                    
         MVC   NUBILDAT,RUNDTSV    DATE OF BILLING RUN                          
         MVC   NUBILPRD,NBILPRD    BILLING PRODUCT                              
         MVC   NUBILIDT,NBILIDT    DATE ON INVOICE                              
         MVC   NUBILPRC,NBILPRDC   3 CHAR PROD CODE                             
*                                                                               
**       LH    RE,NBILNUM                                                       
**       CVD   RE,DUB                                                           
**       OI    DUB+7,X'0F'                                                      
**       UNPK  NUBILNUM,DUB                                                     
*****    GOTO1 NBDATCON,DMCB,(5,0),(X'20',WORK)                                 
         L     RF,=V(SPFMTINO)                                                  
         GOTO1 (RF),DMCB,,(C'U',NBILNUM),,,RR=RELO                              
         L     R1,DMCB+4                                                        
         MVC   NUBILNUM,0(R1)                                                   
*                                                                               
         MVC   NUBILGRS,NBILGRS    GROSS BILLING                                
         MVC   NUBILNET,NBILNET    NET BILLING                                  
         MVC   NUBILST,NBILST      BILLING STATUS                               
         MVC   NUBILBTY,NBILBTYP   BILLING TYPE (4=B4, ETC)                     
         MVC   NUBILGR2,NBILGR2    COST2                                        
         B     EXIT                                                             
*                                                                               
                                                                                
*                                                                               
BUMP     SR    R0,R0               BUMP TO NEXT ELEM                            
         IC    R0,1(R3)                                                         
         AR    R3,R0                                                            
         CLI   0(R3),X'10'         BILLING ELEM?                                
         BR    RE                                                               
*                                                                               
                                                                                
*                                                                               
HOOKUSER NTR1                                                                   
         L     R1,NBL10AIO         YES                                          
         MVC   0(28,R1),ELEM       SET TO USER SUPPLIED AREA                    
         BAS   RE,GOHOOK                                                        
         B     EXIT                                                             
*                                                                               
                                                                                
*                                                                               
ADDELEM  NTR1                      ADD ELEM TO UNIT                             
         L     R5,NBLUNAIO                                                      
***      GOTO1 NBHELLO,DMCB,(C'P',=C'UNTFILE '),(R5),ELEM,0                     
         GOTO1 NBHELLO,DMCB,(C'P',=C'UNTFILE '),(R5),ELEM,=C'ADD=CODE'          
         OI    NBINDS4,X'40'       SET NETBLOCK UNIT BILLED FLG                 
         B     EXIT                                                             
*                                                                               
         EJECT                                                                  
*              CONTROL SENDING OF BILL ELEMS                                    
         SPACE 1                                                                
GOHOOK   NTR1                                                                   
         ICM   RF,15,NBLAHOOK      HOOK                                         
         BNZ   *+6                                                              
         DC    H'0'                SHOULD NEVER GET HERE                        
         L     RE,USERRD           USERS RD                                     
         LM    R0,RC,20(RE)        RESTORE USERS R0-RC                          
         BASR  RE,RF               RF CONTAINED A(HOOK ROUTINE)                 
GOHOOKX  XIT1                                                                   
         SPACE 1                                                                
         GETEL (R2),DATDSP,ELCODE                                               
         EJECT                                                                  
* DATAMGR INTERFACE                                                             
                                                                                
HIGH     NTR1                                                                   
         MVC   KEYSAVE,KEY                                                      
****     TM    NBINDS5,NBI5CLSD    CLOSED OUT RECS                              
****     BNO   HIGH5                                                            
****     GOTO1 NBDM,DMCB,(X'08',=CL8'DMRDHI'),=C'XSPDIR  ',KEY,KEY,0            
****     B     HIGH6                                                            
HIGH5    GOTO1 NBDM,DMCB,(0,=CL8'DMRDHI'),=C'XSPDIR  ',KEY,KEY,0                
HIGH6    CLI   8(R1),0                                                          
         BE    HIGHX                                                            
         DC    H'0'                                                             
HIGHX    B     EXIT                                                             
*                                                                               
SEQ      NTR1                                                                   
****     TM    NBINDS5,NBI5CLSD    CLOSED OUT RECS                              
****     BNO   SEQ5                                                             
****     GOTO1 NBDM,DMCB,(X'08',=CL8'DMRSEQ'),=C'XSPDIR  ',KEY,KEY,0            
****     B     SEQ6                                                             
SEQ5     GOTO1 NBDM,DMCB,(0,=CL8'DMRSEQ'),=C'XSPDIR  ',KEY,KEY,0                
SEQ6     CLI   8(R1),0                                                          
         BE    SEQX                                                             
         DC    H'0'                                                             
SEQX     B     EXIT                                                             
*                                                                               
         DROP  R3                                                               
GETREC   NTR1                                                                   
         LA    R2,UNBILL           CL4000                                       
         LA    R3,KEY                                                           
         USING NUBRECD,R3                                                       
         LA    R3,NUBDA                                                         
****     TM    NBINDS5,NBI5CLSD    CLOSED OUT RECS                              
****     BNO   GETREC5                                                          
****     GOTO1 NBDM,DMCB,(X'08',=CL8'GETREC'),=C'XSPFIL  ',(R3),(R2),           
****           DMWORK                                                           
****     B     GETREC6                                                          
GETREC5  GOTO1 NBDM,DMCB,(0,=CL8'GETREC'),=C'XSPFIL  ',(R3),(R2),DMWORK         
GETREC6  CLI   8(R1),0                                                          
         BE    SEQX                                                             
         DC    H'0'                                                             
GETRECX  B     EXIT                                                             
         DROP  R3                                                               
         EJECT                                                                  
         LTORG                                                                  
         EJECT                                                                  
**SPCHGRS  DS    0F                  SPECIAL CHARGES SAVE AREA                  
*                                  WHEN NBSPLPRN=0                              
**         DS    CL(12*SPCHGEQ)      (CODE/GROSS/NET) X SPCHGEQ                 
**SPCHGLEN EQU   *-SPCHGRS                                                      
**         DC    X'FF'               EOF                                        
SPCHGEQ  EQU   12                                                               
SPCHGTBL DC    C'UBSXAEOLDQCM'                                                  
*                                                                               
*              DSECTS ETC                                                       
         SPACE 3                                                                
*                                                                               
MYD      DSECT                                                                  
DMWORK   DS    12D                                                              
MYDBLK   DS    0D                                                               
DUB      DS    D                                                                
DMCB     DS    6F                                                               
USERRD   DS    F                                                                
RELO     DS    F                                                                
BILGRSSV DS    F                   SAVE NO-T/I BILLED CHARGES                   
BILNETSV DS    F                   SAVE NO-T/I BILL NET CHARGE                  
KEY      DS    CL40                                                             
KEYSAVE  DS    CL40                                                             
WORK     DS    CL64                                                             
DATDSP   DS    H                                                                
FILE     DS    CL8                                                              
ELCODE   DS    CL1                                                              
RUNDTSV  DS    XL2                                                              
ELEM     DS    CL100                                                            
BLRDAREA DS    CL200               USE FOR DSECT IF USER NOT PROVIDE            
FIRSTIME DS    CL1                                                              
KEYCHK   DS    CL40                                                             
ADISPKEY DS    F                                                                
*                                                                               
SPCHGRS  DS    0F                  SPECIAL CHARGES SAVE AREA                    
*                                  WHEN NBSPLPRN=0                              
         DS    CL(12*SPCHGEQ)      (CODE/GROSS/NET) X SPCHGEQ                   
SPCHGLEN EQU   *-SPCHGRS                                                        
SPCHGEOF DS    XL1                 X'FF' EOF                                    
*                                                                               
UNBILL   DS    CL4000              I/O AREA FOR NEGENUBILL                      
*                                                                               
MYDX     EQU   *                                                                
*                                                                               
         SPACE 3                                                                
         EJECT                                                                  
       ++INCLUDE NETBLOCKD                                                      
       ++INCLUDE NEGENUNIT                                                      
       ++INCLUDE NEGENUBILL                                                     
       ++INCLUDE NETBILLRD                                                      
       ++INCLUDE DMDTFIS                                                        
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'061NEUBILLRDR04/28/11'                                      
         END                                                                    
