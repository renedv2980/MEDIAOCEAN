*          DATA SET PRLDCPTR   AT LEVEL 037 AS OF 10/26/19                      
*CATALP PRLDCPTR                                                                
         TITLE 'LDCPTR - PRT - CREATE DIRECTORY POINTERS'                       
*        PARA1 A(RECORD)                                                        
*        PARA2 A(AREA TO CREATE DIRECTORY POINTERS)                             
*              LAST POINTER FOLLOWED BY X'00'                                   
*                                                                               
*        CHANGE LOG                                                             
*                                                                               
* TZIH 09/24/14 DO NOT USE KEYS X'F5'-X'F9' FOR PASSIVE POINTERS!!              
*               WE HAVE AGENCY CODES STARTING WITH C'5' ON FILE                 
*                                                                               
* BOBY 04/09/12 ELIMINATE DUPLICATE B4 & B5 PASSIVE POINTERS                    
*                                                                               
* KWAN 03/19/12 ELIMINATE DUPLICATE B4 PASSIVE POINTERS                         
*                                                                               
* KWAN 10/18/11 NEW CREATION DATE PASSIVE FOR INVOICE RECORD                    
*                                                                               
* KWAN 11/05/08 CHECK FOR DUPLICATE MATERIAL COMMENT PASSIVES (X'A0')           
*                                                                               
* KWAN 05/27/08 ADDED X'A0' PASSIVE FOR SUB AD CODES                            
*                                                                               
*  SMYE  04/07  X'A8' BUY PASSIVE POINTER FOR VARIOUS BUY DATES                 
*                                                                               
* KWAN 09/21/05 ADDED X'A0' PASSIVE FOR MAT= COMMENTS REPEAT                    
*                                                                               
*  SMYE  08/05  ADDED TWO ESR RECORD (X'75') PASSIVE POINTERS                   
*                 (X'B5' & X'BD') AND TWO SETUP RECORD (X'72') PASSIVE          
*                 POINTERS (X'6202' & X'6203')                                  
*                                                                               
*  SMYE  05/05  JOB RECORD "AD-ID ONLY" PASSIVE POINTER (X'C2')                 
*                 AND FIX "DUPLICATING" BUG IN X'C1' POINTER                    
*                                                                               
*  SMYE  01/05  JOB RECORD AD-ID PASSIVE POINTER (X'C1')                        
*                                                                               
*  SMYE  07/04  WEBIO RECORD PASSIVE POINTERS (X'B4', X'BE')                    
*                                                                               
*  SMYE  01/04  CUSTOM COLUMN RECORD PASSIVE POINTER (X'D1')                    
*                                                                               
*  SMYE  07/03  PNVREC PASSIVE POINTERS (X'B1', X'B2', X'B3',X'BF')             
*                                                                               
*  BPLA  09/01  TRAFFIC OFFICE PASSIVE POINTERS                                 
*                                                                               
*  SMYE  01/01  ADDED SERIAL NUMBER PASSIVE POINTER (X'99')                     
*                                                                               
*  SMYE  12/00  ADDED PRODUCT PASSIVE POINTERS (X'A3' & X'A4')                  
*                                                                               
         PRINT NOGEN                                                            
LDCPTR   CSECT                                                                  
         NMOD1 0,PRLDCPTR                                                       
         SPACE 2                                                                
         LM    R2,R3,0(R1)         R2 = A(REC)                                  
*                                  R3 = A(SPACE)                                
         L     R1,16(R1)                                                        
         MVC   DA,0(R1)            SAVE DISK ADDRESS OF RECORD                  
*                                                                               
         BASR  R9,0                                                             
         AHI   R9,GLOBALS-*                                                     
         USING GLOBALS,R9          R9=A(GLOBAL LITERALS)                        
*                                                                               
         OC    STX1,STX1           SET STXIT ADDRESSES                          
         BNZ   CP4                                                              
         ST    R2,STX1                                                          
         ST    R3,STX2                                                          
         LA    RF,SRCHBLK                                                       
         ST    RF,STX3                                                          
         GOTO1 =V(STXITER),DMCB,DMPLST                                          
         MVC   MATPPWKS,=C'MATPPTAB'                                            
         XC    SVBUYKEY,SVBUYKEY                                                
         LA    RE,MATPPTAB                                                      
         LHI   RF,2000*15                                                       
         XCEFL                                                                  
*                                                                               
CP4      DS    0H                                                               
         USING PBUYREC,R2                                                       
         MVC   0(25,R3),PBUYKEY    KEY                                          
         MVC   25(2,R3),PBUYCNTL   CONTROL                                      
         LA    R3,31(R3)                                                        
         CLI   PBUYKRCD,X'20'      TEST BUY REC                                 
         BNE   PB5                 GO SEE IF BILLREC                            
*                                                                               
         MVC   0(25,R3),PBUYKEY    CREATE CLIENT/PUB POINTER                    
         MVI   3(R3),X'21'                                                      
         MVC   7(6,R3),PBUYKEY+10  PUB                                          
         MVC   13(3,R3),PBUYKEY+7  PRD                                          
         MVC   25(2,R3),PBUYCNTL                                                
         LA    R3,31(R3)                                                        
*                                                                               
         SR    R0,R0                                                            
         LA    R4,PBUYREC+33                                                    
         SR    R5,R5                                                            
         IC    R5,PBUYLEN                                                       
         SLL   R5,8                                                             
         IC    R5,PBUYLEN+1                                                     
         AR    R5,R2                                                            
         MVI   0(R5),0             SET 0 AT EOR                                 
*                                                                               
         CLC   PBUYKPRD,=C'ZZZ'    TEST POL                                     
         BNE   PC6                 NO                                           
         EJECT                                                                  
* FIND PRODUCT ELEMENTS                                                         
*                                                                               
PC4      IC    R0,1(R4)                                                         
         AR    R4,R0                                                            
         CLI   0(R4),0             EOR                                          
         BE    PC6                                                              
         CLI   0(R4),X'21'         PROD ELEM                                    
         BNE   PC4                                                              
         USING PPRELEM,R4                                                       
         MVC   0(25,R3),PBUYKEY    KEY                                          
         MVC   25(2,R3),PBUYCNTL   CONTROL                                      
         MVC   7(3,R3),PPRCODE     PRODUCT                                      
         MVC   21(3,R3),=C'ZZZ'    POL                                          
         LA    R3,31(R3)                                                        
         B     PC4                 GET NEXT EL                                  
*                                                                               
*        INSERTION ORDER POINTERS                                               
*                                                                               
PC6      DS    0H                                                               
         L     RF,=V(PTRTAB)       FIRST SEE IF IN PZ PUB TRANS TABLE           
         USING PTRTABD,RF                                                       
*                                                                               
PC7      DS    0H                                                               
         CLI   0(RF),X'FF'         EOT                                          
         BE    PC12                NO IO POINTERS                               
         CLC   PBUYKAGY,PTRUSCOD   RIGHT AGENCY                                 
         BNE   PC8                                                              
         CLC   PBUYKMED,PTRMED     AND MEDIA                                    
         BNE   PC8                                                              
         CLC   PBUYKPUB,PTRPUB     AND PUB (NOTE- ALL EDITIONS OK??)            
         BE    PC9                                                              
*                                                                               
PC8      DS    0H                                                               
         LA    RF,PTRTABL(RF)                                                   
         B     PC7                                                              
         DROP  RF                                                               
*                                                                               
PC9      DS    0H                  IO POINTERS OK FOR THIS PUB/AGY              
*                                  FIND INSERTION ORDER ELEMENTS                
         LA    R4,PBUYREC+33                                                    
*                                                                               
PC10     DS    0H                                                               
         SR    R0,R0                                                            
         IC    R0,1(R4)                                                         
         AR    R4,R0                                                            
         CLI   0(R4),0             EOR                                          
         BE    PC12                                                             
         CLI   0(R4),X'70'         IO ELEM                                      
         BNE   PC10                                                             
         USING PIOELEMD,R4                                                      
         OC    PIODATE,PIODATE     IF NO IO DATE                                
         BZ    PC10                NOT A REAL INS ORDER                         
         XC    0(25,R3),0(R3)                                                   
         MVC   0(3,R3),PBUYKEY     AGYMED                                       
         MVI   3(R3),X'23'         RECORD CODE                                  
         MVC   4(6,R3),PBUYKPUB    PUB/ZONE/EDIT                                
         GOTO1 =V(DATCON),DMCB,(3,PIODATE),(2,10(R3))  DATE                     
         MVC   12(2,R3),PIONUM     IO NUMBER                                    
         MVC   14(3,R3),PBUYKCLT   CLIENT                                       
         MVC   17(3,R3),PBUYKPRD   PRODUCT                                      
         MVC   20(2,R3),PBUYKEST   ESTIMATE                                     
         GOTO1 =V(DATCON),DMCB,(3,PBUYKDAT),(2,22(R3))  INS DATE                
         MVC   24(1,R3),PBUYKLIN   LINE NUMBER                                  
         MVC   25(2,R3),PBUYCNTL   CONTROL BYTES                                
*                                                                               
PC11     DS    0H                                                               
         LA    R3,31(R3)                                                        
         B     PC10                GET NEXT EL                                  
*                                                                               
PC12     DS    0H                  SEE IF HAS SERIAL # ELEM (X'99')             
         LA    R4,PBUYREC+33                                                    
*                                                                               
PC12L    DS    0H                                                               
         ZIC   R0,1(R4)                                                         
         AR    R4,R0                                                            
         CLI   0(R4),0             EOR                                          
         BE    PC13                                                             
         CLI   0(R4),X'99'         SERIAL # ELEMENT                             
         BNE   PC12L                                                            
         XC    0(25,R3),0(R3)                                                   
         MVC   0(7,R3),PBUYKEY     AGY/MED/RCD/CLIENT                           
         MVI   3(R3),X'99'         RECORD CODE                                  
         ZAP   DUB,2(5,R4)         SERIAL NUMBER FROM ELEM INTO DUB             
         ZAP   DOUBLE,=P'1000000000'                                            
         SP    DOUBLE,DUB                                                       
         MVC   7(5,R3),DOUBLE+3    9'S COMPLEMENT OF SERIAL NUMBER              
         MVC   25(2,R3),PBUYCNTL   CONTROL BYTES                                
         LA    R3,31(R3)                                                        
*                                                                               
PC13     TM    PBUYCNTL,X'80'      DELETED?                                     
         BNZ   PC14                                                             
         LA    R4,PBUYREC+33                                                    
PC13F    ZIC   R0,1(R4)                                                         
         AR    R4,R0                                                            
         CLI   0(R4),0             EOR                                          
         BE    PC13K                                                            
         CLI   0(R4),PWIOELCQ      EIO ELEM?                                    
         BNE   PC13F                                                            
         LR    RF,R4                                                            
         B     PC13F                                                            
*                                                                               
PC13K    LR    R4,RF                                                            
         CLI   0(R4),PWIOELCQ      LAST EIO ELEM?                               
         BNE   PC14                                                             
         USING PWIOELEM,R4                                                      
         OC    PWIODATE,PWIODATE                                                
         BZ    PC14                                                             
         LA    RE,PBUYREC+33                                                    
         USING PBDELEM,RE                                                       
         CLC   PWIOADCD,PBDJOB     AD CODE IS NOT CHANGED?                      
         BNE   PC14                                                             
         MVC   BYTE3,PWIOSTAT      SAVE IT FOR UNIT AD CODE LATER               
         XC    00(31,R3),0(R3)                                                  
         USING PMATPPKD,R3                                                      
         MVC   PMTPKEY(10),PBUYKEY AGY/MED/RCD/CLT/PRD                          
         MVI   PMTPKRCD,PMTPKRCQ   RECORD CODE                                  
         MVC   PMTPKADC,PWIOADCD   AD CODE                                      
         MVC   PMTPKPUB,PBUYKPUB   BASE PUB                                     
         TM    PWIOSTAT,PWIOSMAQ   MAT= REPEAT ACROSS ZONE/EDITION?             
         BNZ   *+16                                                             
         MVC   PMTPKZON,PBUYKZON   ZONE                                         
         MVC   PMTPKEDT,PBUYKEDT   EDITION                                      
         MVC   PMTPKLIN,PBUYKLIN   LINE NUMBER                                  
         MVC   PMTPCNTL,PBUYCNTL   CONTROL BYTES                                
         MVI   PMTPCNT2,0                                                       
         CLI   PBDFREQ,C'M'        MONTHLY?                                     
         BNE   *+8                                                              
         OI    PMTPCNT2,PMTPC2MQ   TO INDICATE MONTHLY INSERTION                
*                                                                               
         GOTOR =V(DATCON),DMCB,(3,PBUYKDAT),(2,PMTPKDAT)                        
*                                                                               
         MVC   PMTPDISK,DA                                                      
*                                                                               
         BRAS  RE,CKMATPPT                                                      
         OC    0(31,R3),0(R3)      DUPLICATE?                                   
         BZ    *+8                                                              
         LA    R3,31(R3)           NEXT ENTRY                                   
*                                                                               
         DROP  R3,R4,RE                                                         
*                                                                               
         LA    R4,PBUYREC+33                                                    
PC13M_5  ZIC   R0,1(R4)                                                         
         AR    R4,R0                                                            
         CLI   0(R4),0             EOR                                          
         BE    PC14                                                             
         CLI   0(R4),PSADELCQ      UNIT OR SUB AD CODE ELEM?                    
         BNE   PC13M_5                                                          
*                                                                               
         USING PSADELMD,R4                                                      
         SR    R5,R5                                                            
         IC    R5,PSAD#ADC         NUMBER OF AD CODES TO PROCESS                
         LA    R6,PSADADCS         POINT TO FIRST UNIT OR SUB AD CODE           
*                                                                               
PC13M_7  XC    00(31,R3),0(R3)                                                  
         USING PMATPPKD,R3                                                      
         LA    RE,PBUYREC+33                                                    
         USING PBDELEM,RE                                                       
         MVC   PMTPKEY(10),PBUYKEY AGY/MED/RCD/CLT/PRD                          
         MVI   PMTPKRCD,PMTPKRCQ   RECORD CODE                                  
         MVC   PMTPKADC,0(R6)      AD CODE                                      
         MVC   PMTPKPUB,PBUYKPUB   BASE PUB                                     
         TM    BYTE3,PWIOSMAQ      MAT= REPEAT ACROSS ZONE/EDITION?             
         BNZ   *+16                                                             
         MVC   PMTPKZON,PBUYKZON   ZONE                                         
         MVC   PMTPKEDT,PBUYKEDT   EDITION                                      
         MVC   PMTPKLIN,PBUYKLIN   LINE NUMBER                                  
         MVC   PMTPCNTL,PBUYCNTL   CONTROL BYTES                                
         MVI   PMTPCNT2,0                                                       
         CLI   PBDFREQ,C'M'        MONTHLY?                                     
         BNE   *+8                                                              
         OI    PMTPCNT2,PMTPC2MQ   TO INDICATE MONTHLY INSERTION                
         OI    PMTPCNT2,PMTPC2SQ   TO INDICATE UNIT OR SUB AD CODE              
*                                                                               
         GOTOR =V(DATCON),DMCB,(3,PBUYKDAT),(2,PMTPKDAT)                        
*                                                                               
         MVC   PMTPDISK,DA                                                      
*                                                                               
         BRAS  RE,CKMATPPT                                                      
         OC    0(31,R3),0(R3)      DUPLICATE?                                   
         BZ    *+8                                                              
         LA    R3,31(R3)           NEXT ENTRY                                   
         LA    R6,L'PSADADCS(R6)                                                
         BCT   R5,PC13M_7                                                       
*                                                                               
         DROP  R3,R4,RE                                                         
*                                  CREATE PASSIVES BY VARIOUS DATES             
PC14     DS    0H                  MATERIAL CLOSING DATE                        
         LA    R4,PBUYREC+33                                                    
         USING PBDELEM,R4          BUY DESCRIPTION ELEMENT                      
         USING PBYPPSVD,R3         DATE PASSIVE KEY                             
         XC    PBYPKEY(25),PBYPKEY                                              
         MVC   PBYPAGY(3),PBUYKAGY  AGENCY/MEDIA                                
         MVI   PBYPRCD,PBYPRIDQ    RECORD CODE (X'A8')                          
         MVI   PBYPDTYP,PBYPMCLQ   DATE TYPE   (X'01')                          
         MVC   PBYPCLT,PBUYKCLT    CLIENT                                       
         MVC   PBYPPRD,PBUYKPRD    PRODUCT                                      
         MVC   PBYPEST,PBUYKEST    ESTIMATE                                     
         MVC   PBYPDTE,PBDMDATE    MATERIAL CLOSING DATE                        
         MVC   PBYPDADR,DA         DISK ADDRESS                                 
         MVC   PBYPCNTL,PBUYCNTL   CONTROL BYTES                                
         LA    R3,31(R3)           NEXT ENTRY                                   
*                                  SPACE CLOSING DATE                           
         XC    PBYPKEY(25),PBYPKEY                                              
         MVC   PBYPAGY(3),PBUYKAGY  AGENCY/MEDIA                                
         MVI   PBYPRCD,PBYPRIDQ    RECORD CODE (X'A8')                          
         MVI   PBYPDTYP,PBYPCLSQ   DATE TYPE   (X'02')                          
         MVC   PBYPCLT,PBUYKCLT    CLIENT                                       
         MVC   PBYPPRD,PBUYKPRD    PRODUCT                                      
         MVC   PBYPEST,PBUYKEST    ESTIMATE                                     
         MVC   PBYPDTE,PBDCDATE    SPACE CLOSING DATE                           
         MVC   PBYPDADR,DA         DISK ADDRESS                                 
         MVC   PBYPCNTL,PBUYCNTL   CONTROL BYTES                                
         LA    R3,31(R3)           NEXT ENTRY                                   
*                                  BILLABLE DATE                                
         XC    PBYPKEY(25),PBYPKEY                                              
         MVC   PBYPAGY(3),PBUYKAGY  AGENCY/MEDIA                                
         MVI   PBYPRCD,PBYPRIDQ    RECORD CODE (X'A8')                          
         MVI   PBYPDTYP,PBYPBLBQ   DATE TYPE   (X'03')                          
         MVC   PBYPCLT,PBUYKCLT    CLIENT                                       
         MVC   PBYPPRD,PBUYKPRD    PRODUCT                                      
         MVC   PBYPEST,PBUYKEST    ESTIMATE                                     
         MVC   PBYPDTE,PBDBDATE    BILLABLE DATE                                
         MVC   PBYPDADR,DA         DISK ADDRESS                                 
         MVC   PBYPCNTL,PBUYCNTL   CONTROL BYTES                                
         LA    R3,31(R3)           NEXT ENTRY                                   
*                                  PAYABLE DATE                                 
         XC    PBYPKEY(25),PBYPKEY                                              
         MVC   PBYPAGY(3),PBUYKAGY  AGENCY/MEDIA                                
         MVI   PBYPRCD,PBYPRIDQ    RECORD CODE (X'A8')                          
         MVI   PBYPDTYP,PBYPPAYQ   DATE TYPE   (X'04')                          
         MVC   PBYPCLT,PBUYKCLT    CLIENT                                       
         MVC   PBYPPRD,PBUYKPRD    PRODUCT                                      
         MVC   PBYPEST,PBUYKEST    ESTIMATE                                     
         MVC   PBYPDTE,PBDPDATE    PAYABLE DATE                                 
         MVC   PBYPDADR,DA         DISK ADDRESS                                 
         MVC   PBYPCNTL,PBUYCNTL   CONTROL BYTES                                
*                                                                               
         LA    R3,31(R3)           NEXT ENTRY                                   
*                                                                               
         DROP  R3,R4                                                            
*                                                                               
PC15     DS    0H                                                               
*                                                                               
         B     EXIT                                                             
*                                                                               
CKMATPPT ST    RE,FULL1                                                         
         CLC   PBUYKEY(PBUYKZON-PBUYKEY),SVBUYKEY                               
         BE    CKMATP20                                                         
         MVC   SVBUYKEY,PBUYKEY                                                 
         LA    RE,MATPPTAB                                                      
         LHI   RF,2000*15                                                       
         XCEFL                                                                  
*                                                                               
CKMATP20 SR    RF,RF               TABLE ENTRY COUNTER                          
         LA    RE,MATPPTAB                                                      
CKMATP30 CHI   RF,2000                                                          
         BH    CKMATP_X            TABLE IS FULL, DUP MIGHT OCCUR               
         OC    0(15,RE),0(RE)                                                   
         BZ    CKMATP70                                                         
         CLC   0(15,RE),10(R3)     MATERIAL PASSIVE IN TABLE?                   
         BE    CKMATP80                                                         
         LA    RE,15(RE)           NEXT ENTRY IN TABLE                          
         AHI   RF,1                                                             
         B     CKMATP30                                                         
*                                                                               
CKMATP70 MVC   0(15,RE),10(R3)     ADD TO TABLE                                 
         B     CKMATP_X                                                         
*                                                                               
CKMATP80 XC    0(31,R3),0(R3)      SKIP DUPLICATE, CLEAR ENTRY                  
         B     CKMATP_X                                                         
*                                                                               
CKMATP_X L     RE,FULL1                                                         
         BR    RE                                                               
*                                                                               
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *         
*                                                                               
         EJECT                                                                  
PB5      CLI   PBUYKRCD,X'08'      TEST BILLREC                                 
         BNE   PB15                NO-                                          
         DROP  R2                                                               
         USING PBILRECD,R2                                                      
*                                                                               
         OC    PBILPOST,PBILPOST   TEST POSTED TO ACC                           
         BZ    PB10                                                             
         LR    RE,R3               GET CURRENT POINTER ADDRESS                  
         SH    RE,=H'31'           BACK UP TO FIRST POINTER                     
         OI    PBILLCTL+1,X'01'    SET 'TRANSFERRED' BIT IN RECORD              
         OI    26(RE),X'01'        SET 'TRANSFERRED' BIT IN POINTER             
*                                                                               
PB10     CLI   PBILLTYP,C'M'       SEE IF NEW MANUAL BILL                       
         BNE   EXIT                                                             
         MVC   0(25,R3),PBILLKEY  CREATE PASSIVE MANUAL BILL POINTER            
         MVI   3(R3),X'88'                                                      
         MVC   25(2,R3),PBILLCTL                                                
         LA    R3,31(R3)                                                        
*                                                                               
         DROP  R2                                                               
         EJECT                                                                  
PB15     DS    0H                                                               
         CLI   3(R2),X'06'      TEST PRODUCT RECORD                             
         BNE   PB17                NO-                                          
         USING PPRDRECD,R2                                                      
         LA    RE,PPRDEXCL                                                      
         LA    R5,3         FOR BCT                                             
PB15B    CLI   0(RE),C' '                                                       
         BNH   PB15F                                                            
         XC    0(25,R3),0(R3)                                                   
         MVC   0(7,R3),PPRDKEY  CREATE PASSIVE ADJ/PRD POINTER                  
         MVI   3(R3),X'A6'                                                      
         MVC   7(1,R3),0(RE)                                                    
         MVC   8(3,R3),PPRDKPRD                                                 
         MVC   25(2,R3),PPRDCNTL                                                
         LA    R3,31(R3)                                                        
*                                                                               
PB15F    LA    RE,1(RE)                                                         
         BCT   R5,PB15B                                                         
*                                                                               
PB15H    DS    0H                                                               
         CLI   PPRDOFFC,C' '       ANY PRODUCT OFFICE CODE ?                    
         BNH   PB15K               NO                                           
         XC    0(25,R3),0(R3)                                                   
         MVC   0(7,R3),PPRDKEY  CREATE PASSIVE OFFICE/PRD POINTER               
         MVI   3(R3),X'A3'                                                      
         MVC   7(1,R3),PPRDOFFC                                                 
         MVC   8(3,R3),PPRDKPRD                                                 
         MVC   25(2,R3),PPRDCNTL                                                
         LA    R3,31(R3)                                                        
*                                                                               
PB15K    DS    0H                                                               
         CLI   PPRDTRAF,C' '       ANY PRODUCT TRAFFIC CODE ?                   
         BNH   PB17                NO                                           
         XC    0(25,R3),0(R3)                                                   
         MVC   0(7,R3),PPRDKEY  CREATE PASSIVE TRAFFIC/PRD POINTER              
         MVI   3(R3),X'A4'                                                      
         MVC   7(1,R3),PPRDTRAF                                                 
         MVC   8(3,R3),PPRDKPRD                                                 
         MVC   25(2,R3),PPRDCNTL                                                
         LA    R3,31(R3)                                                        
*                                                                               
         DROP  R2                                                               
         EJECT                                                                  
PB17     DS    0H                                                               
         CLI   3(R2),X'02'         CLIENT                                       
         BNE   PB20                                                             
         USING PCLTRECD,R2                                                      
         CLI   PCLTOFF,C' '                                                     
         BNH   PB18               STILL TRY TRAFFIC OFFICE                      
         MVC   0(25,R3),PCLTKEY   CREATE PASSIVE OFFICE/CLIENT PTR              
         MVI   3(R3),X'A2'                                                      
         MVC   4(1,R3),PCLTOFF                                                  
         MVC   5(3,R3),PCLTKCLT                                                 
         MVC   25(2,R3),PCLTCNTL                                                
         LA    R3,31(R3)                                                        
*                                                                               
PB18     DS    0H                                                               
         LA    R4,PCLTREC+33                                                    
PB18A    SR    R0,R0                                                            
         IC    R0,1(R4)                                                         
         AR    R4,R0                                                            
         CLI   0(R4),0             EOR                                          
         BE    PB20                                                             
         CLI   0(R4),X'50'         TRAFFIC OFFICE ELEMENT                       
         BNE   PB18A               NO - KEEP LOOKING                            
         USING PCLTTOEL,R4                                                      
         CLI   PCLTTOFC,C' '                                                    
         BNH   PB20               DONE WITH CLIENT RECORD                       
         MVC   0(25,R3),PCLTKEY   CREATE PASSIVE OFFICE/CLIENT PTR              
         MVI   3(R3),X'A1'                                                      
         MVC   4(1,R3),PCLTTOFC                                                 
         MVC   5(3,R3),PCLTKCLT                                                 
         MVC   25(2,R3),PCLTCNTL                                                
         LA    R3,31(R3)                                                        
*                                                                               
         DROP  R2                                                               
         DROP  R4                                                               
*                                                                               
PB20     DS    0H                                                               
         CLI   3(R2),X'02'         CLIENT                                       
         BE    PB22                                                             
         CLI   3(R2),X'06'         PRODUCT                                      
         BE    PB22                                                             
         CLI   3(R2),X'07'         ESTIMATE                                     
         BE    PB22                                                             
         CLI   3(R2),X'11'         REP                                          
         BE    PB22                                                             
         CLI   3(R2),X'15'         JOB (ADCODE) RECORD                          
         BE    PJB00                                                            
         CLI   3(R2),X'61'         CUSTOM COLUMN RECORD                         
         BE    PCC00                                                            
         CLI   3(R2),X'70'         NEW INVOICE RECORD                           
         BE    PNV00                                                            
         CLI   3(R2),X'72'         INSERTION ORDER SETUP RECORD                 
         BE    PSET00                                                           
         CLI   3(R2),X'74'         WEB INSERTION ORDER RECORD                   
         BE    PWIO00                                                           
         CLI   3(R2),X'75'         ENHANCED SPACE RESERVATION RECORD            
         BE    PESR00                                                           
         B     EXIT                                                             
*                                                                               
PB22     DS    0H                                                               
         LA    R6,SRCHBLK          POINT TO SEARCH CONTROL BLOCK                
         USING SEARCHD,R6 ESTABLISH CONTROL BLOCK                               
         MVI   SBID,C' '           INIT ID AREA                                 
         MVC   SBID+1(L'SBID-1),SBID TO SPACES                                  
         MVC   SBSYSTEM,=CL6'PRINT' SET SYSTEM                                  
         MVI   SBCTRY,0                                                         
         MVI   SBLANG,0                                                         
         ST    R2,SBAIN            PASS RECORD ADDRESS                          
         MVI   SBSTYLI,SBSTREQ     SET INPUT STYLE TO RECORD                    
         ST    R3,SBAOUT           SET OUTPUT ADDRESS                           
         MVI   SBSTYLO,SBSTPASQ    SET OUTPUT STYLE TO PASSIVES                 
         MVI   SBMODE,1            SET TO CREATE PASSIVE PTRS                   
*                                                                               
         GOTO1 =V(SRCHEXEC),DMCB,(R6)                                           
*                                                                               
         BE    PB24                                                             
         CLI   SBRETURN,SBREOLQ    OKAY IF NO ENTRIES IN LIST                   
         BE    PB24                                                             
         DC    H'0'                NO ERRORS ALLOWED                            
*                                                                               
PB24     DS    0H                                                               
         L     R3,SBAOUT           POINT TO NEXT SPOT IN LIST                   
         B     EXIT                                                             
         DROP  R6                                                               
         EJECT                                                                  
***********************************************************************         
*   R2 POINTING TO RECORD                                             *         
*   R3 POINTING TO SPACE FOR KEYS                                     *         
***********************************************************************         
PJB00    DS    0H                  CREATE AD-ID PASSIVE POINTER                 
         USING PJOBRECD,R2         FOR THE JOB (ADCODE) RECORD                  
*                                                                               
         OC    PJOBKPUB,PJOBKPUB   JOB HEADER RECORD ?                          
         BNZ   EXIT                NO - INSTRUCT. RECORD - NO PASSIVES          
*                                                                               
         CLI   PJOBADID,C' '       ANYTHING IN AD-ID FIELD ?                    
         BNH   EXIT                NO - NO KEY TO ADD                           
*                                                                               
         XC    0(25,R3),0(R3)      CLEAR KEY SPACE                              
         MVC   0(3,R3),PJOBKEY     AGENCY/MEDIA                                 
         MVI   3(R3),X'C1'         RECORD CODE                                  
         MVC   4(3,R3),PJOBKCLT    CLIENT                                       
         MVC   7(3,R3),PJOBKPRD    PRODUCT                                      
         MVC   10(12,R3),PJOBADID  AD-ID                                        
         MVC   25(2,R3),27(R2)     CONTROL BYTES                                
         LA    R3,31(R3)                                                        
*                                                                               
         CLI   PJOBKJOB,X'FF'      "AD-ID ONLY" RECORD ?                        
         BNE   EXIT                NO - DONE                                    
*                                                                               
         XC    0(25,R3),0(R3)      CLEAR KEY SPACE                              
         MVC   0(3,R3),PJOBKEY     AGENCY/MEDIA                                 
         MVI   3(R3),X'C2'         RECORD CODE                                  
         MVC   4(3,R3),PJOBKCLT    CLIENT                                       
         MVC   7(3,R3),PJOBKPRD    PRODUCT                                      
         MVC   10(3,R3),PJOBKJOB   "AD-ID ONLY" ADCODE                          
         MVC   25(2,R3),27(R2)     CONTROL BYTES                                
         LA    R3,31(R3)                                                        
*                                                                               
         B     EXIT                                                             
         DROP  R2                                                               
         EJECT                                                                  
***********************************************************************         
*   R2 POINTING TO RECORD                                             *         
*   R3 POINTING TO SPACE FOR KEYS                                     *         
***********************************************************************         
PCC00    DS    0H                  CREATE UNIQUE SEQUENCE NO. PASSIVE           
         USING PCOLRECD,R2         POINTER FOR THE CUSTOM COLUMN RECORD         
*                                                                               
* TEST FOR SOMETHING IN PCOLSQN FIRST (IF NOTHING, EXIT)                        
*                                                                               
         OC    PCOLSQN,PCOLSQN     ANYTHING IN SEQUENCE NUMBER FIELD ?          
         BZ    EXIT                NO - NO KEY TO ADD                           
*                                                                               
         XC    0(25,R3),0(R3)      CLEAR KEY SPACE                              
         MVC   0(3,R3),PCOLKEY     AGENCY/MEDIA                                 
         MVI   3(R3),X'D1'         RECORD CODE                                  
         MVC   4(2,R3),PCOLSQN     SEQUENCE NUMBER FROM RECORD                  
         XC    4(2,R3),=X'FFFF'    2'S COMPLEMENT                               
         MVC   25(2,R3),PCOLCNTL                                                
         LA    R3,31(R3)                                                        
*                                                                               
         B     EXIT                                                             
         DROP  R2                                                               
         EJECT                                                                  
***********************************************************************         
*   R2 POINTING TO RECORD (WEB INSERTION ORDER RECORD)                *         
*   R3 POINTING TO SPACE FOR KEYS                                     *         
***********************************************************************         
PWIO00   DS    0H                  PASSIVE POINTERS FOR WEB IO RECORD           
         USING WIORECD,R2                                                       
*                                  CREATE A PASSIVE POINTER BY PERIOD           
* TEST FOR A WEB IO HEADER ELEMENT FIRST (IF NOTHING, EXIT)                     
*                                                                               
         CLI   WIOFIRST,WIOHKIDQ   X'10' HEADER ELEMENT ?                       
         BNE   EXIT                NO - NO KEYS TO ADD                          
*                                                                               
         XC    0(25,R3),0(R3)      CLEAR KEY SPACE                              
         MVC   0(3,R3),WIOKEY      AGENCY/MEDIA                                 
         MVI   3(R3),WIO1RCDQ      RECORD CODE (X'B4')                          
         MVC   4(3,R3),WIOKCLT     CLIENT FROM KEY                              
         LA    R4,33+WIOHPRD-WIOHDRD(R2)                                        
         MVC   7(3,R3),0(R4)       PRODUCT FROM HDR ELEM                        
         MVC   10(6,R3),WIOKPBCD   PUB FROM KEY                                 
         LA    R4,33+WIOHEND-WIOHDRD(R2)                                        
         GOTOR =V(DATCON),DMCB,(3,0(R4)),(2,16(R3))   PERIOD END DATE           
         LA    R4,33+WIOHSTRT-WIOHDRD(R2)                                       
         GOTOR =V(DATCON),DMCB,(3,0(R4)),(2,18(R3))   PERIOD START DATE         
         MVC   20(5,R3),WIOKIO#   (IO# YR(1)/SEQ#(3)/IO REVISION#(1)            
         MVC   25(2,R3),WIOCNTL                                                 
*                                                                               
*        WE GET DUPE PASSIVES IF MINIO SPLITS THE RECORD                        
*        BUT COPIES HEADER ELM INSTEAD OF MOVING IT TO                          
*        NEW RECORD                                                             
*                                                                               
         CLC   SVWIO1KY,0(R3)      IF EQUAL TO LAST PASSIVE                     
         BNE   *+14                                                             
         XC    0(31,R3),0(R3)         ERASE PASSIVE KEY                         
         B     *+14                                                             
*                                                                               
         MVC   SVWIO1KY,0(R3)      ELSE SAVE COPY OF PASSIVE                    
         LA    R3,31(R3)                KEEP PASSIVE POINTER                    
*                                                                               
*                            CREATE PASSIVE POINTERS BY SERIAL NUMBERS          
*                                                                               
         L     R7,=A(NVSVTBL)     USING INVOICE SAVE AREA TO SAVE "KEY"         
*                                   OF LAST WEBIO RECORD                        
*                                                                               
         CLC   WIOKEY(WIOKRV#-WIOKEY),0(R7)   SAME UP TO REVISION # ?           
         BE    EXIT                           YES - DUPLICATE - NO ADD          
*                                                                               
         MVC   0((WIOKRV#-WIOKEY),(R7)),WIOKEY   SAVE KEY UP TO REV'SN#         
*                                                                               
         XC    0(25,R3),0(R3)      CLEAR KEY SPACE                              
         MVC   0(3,R3),WIOKEY      AGENCY/MEDIA                                 
         MVI   3(R3),WIO#RCDQ      RECORD CODE (X'BE')                          
         MVC   4(3,R3),WIOKCLT     CLIENT                                       
*NOP*    MVC   7(6,R3),WIOKPBCD    PUB                                          
         MVC   7(6,R3),=7X'FF'    ALL PUBS (7X'FF' IS EXISTING LITERAL)         
         MVC   13(4,R3),WIOKIO#   (IO# YR(1)/SEQ#(3))                           
         XC    14(3,R3),=X'FFFFFF'  2'S COMPLEMENT OF SEQ #                     
         MVC   25(2,R3),WIOCNTL                                                 
         LA    R3,31(R3)                                                        
*                                                                               
         B     EXIT                                                             
*                                                                               
         DROP  R2                                                               
         EJECT                                                                  
***********************************************************************         
*   R2 POINTING TO RECORD (ENHANCED SPACE RESERVATION RECORD)         *         
*   R3 POINTING TO SPACE FOR KEYS                                     *         
***********************************************************************         
PESR00   DS    0H                  PASSIVE POINTERS FOR ESR RECORD              
         USING ESRRECD,R2                                                       
*                                  CREATE A PASSIVE POINTER BY PERIOD           
*       TEST FOR AN ESR HEADER ELEMENT FIRST (IF NOTHING, EXIT)                 
*                                                                               
         CLI   ESRFIRST,ESRHKIDQ   X'10' HEADER ELEMENT ?                       
         BNE   EXIT                NO - NO KEYS TO ADD                          
*                                                                               
         LA    R4,ESRFIRST         POINT R4 TO HEADER ELEMENT                   
         USING ESRHDRD,R4                                                       
*                                                                               
         XC    0(25,R3),0(R3)      CLEAR KEY SPACE                              
         MVC   0(3,R3),ESRKEY      AGENCY/MEDIA                                 
         MVI   3(R3),ESR1RCDQ      RECORD CODE (X'B5')                          
         MVC   4(3,R3),ESRKCLT     CLIENT FROM KEY                              
         MVC   7(3,R3),ESRHPRD     PRODUCT FROM HDR ELEM                        
         MVC   10(6,R3),ESRKPUB    PUB FROM KEY                                 
         GOTOR =V(DATCON),DMCB,(3,ESRHEND),(2,16(R3))   PERIOD END DATE         
         GOTOR =V(DATCON),DMCB,(3,ESRHSTRT),(2,18(R3))  PERIOD START DT         
         MVC   20(5,R3),ESRKSR#   (IO# YR(1)/SEQ#(3)/IO REVISION#(1)            
         MVC   25(2,R3),ESRCNTL                                                 
*                                                                               
*        WE GET DUPE PASSIVES IF MINIO SPLITS THE RECORD                        
*        BUT COPIES HEADER ELM INSTEAD OF MOVING IT TO                          
*        NEW RECORD                                                             
*                                                                               
         CLC   SVWIO1KY,0(R3)      IF EQUAL TO LAST PASSIVE                     
         BNE   *+14                                                             
         XC    0(31,R3),0(R3)         ERASE PASSIVE KEY                         
         B     *+14                                                             
*                                                                               
         MVC   SVWIO1KY,0(R3)      ELSE SAVE COPY OF PASSIVE                    
         LA    R3,31(R3)                KEEP PASSIVE POINTER                    
*                                                                               
*                            CREATE PASSIVE POINTERS BY SERIAL NUMBERS          
*                                                                               
         L     R7,=A(NVSVTBL)     USING INVOICE SAVE AREA TO SAVE "KEY"         
*                                   OF LAST WEBIO RECORD                        
*                                                                               
         CLC   ESRKEY(ESRKRV#-ESRKEY),0(R7)   SAME UP TO REVISION # ?           
         BE    EXIT                           YES - DUPLICATE - NO ADD          
*                                                                               
         MVC   0((ESRKRV#-ESRKEY),(R7)),ESRKEY   SAVE KEY UP TO REV'SN#         
*                                                                               
         XC    0(25,R3),0(R3)      CLEAR KEY SPACE                              
         MVC   0(3,R3),ESRKEY      AGENCY/MEDIA                                 
         MVI   3(R3),ESR#RCDQ      RECORD CODE (X'BD')                          
         MVC   4(3,R3),ESRKCLT     CLIENT                                       
*NOP*    MVC   7(6,R3),ESRKPUB     PUB                                          
         MVC   7(6,R3),=7X'FF'    ALL PUBS (7X'FF' IS EXISTING LITERAL)         
         MVC   13(4,R3),ESRKSR#   (SR# YR(1)/SEQ#(3))                           
         XC    14(3,R3),=X'FFFFFF'  2'S COMPLEMENT OF SEQ #                     
         MVC   25(2,R3),ESRCNTL                                                 
         LA    R3,31(R3)                                                        
*                                                                               
         B     EXIT                                                             
         DROP  R2,R4                                                            
         EJECT                                                                  
***********************************************************************         
*   R2 POINTING TO RECORD (INSERTION ORDER SETUP RECORD)              *         
*   R3 POINTING TO SPACE FOR KEYS                                     *         
***********************************************************************         
PSET00   DS    0H                 PASSIVE POINTERS FOR I/O SETUP RECORD         
         USING SCHRECD,R2                                                       
*                                                                               
* TEST FOR A SETUP REC HEADER ELEMENT FIRST (IF NOTHING, EXIT)                  
*                                                                               
         CLI   SCHFIRST,SCHHDRQ    X'10' HEADER ELEMENT ?                       
         BNE   EXIT                NO - NO KEYS TO ADD                          
*                                  SEE IF EIO IN USE                            
         LA    R4,SCHFIRST         POINT TO HEADER ELEMENT                      
         USING SCHHDRD,R4                                                       
         CLI   SCHEIO,C'Y'         EIO IN USE ?                                 
         BNE   PSETESR             NO                                           
*                                  CREATE A PASSIVE POINTER FOR EIO             
         XC    0(25,R3),0(R3)      CLEAR KEY SPACE                              
         MVC   0(3,R3),SCHKKEY     AGENCY/MEDIA                                 
         MVC   3(2,R3),=X'6202'    EIO PASSIVE RECORD CODE                      
         MVC   5(3,R3),SCHKCLT     CLIENT FROM KEY                              
         MVC   8(3,R3),SCHKPRD     PRODUCT FROM KEY                             
         MVC   11(2,R3),SCHKEST    ESTIMATE FROM KEY                            
         MVC   13(6,R3),SCHKPUB    PUB FROM KEY                                 
         MVC   25(2,R3),SCHCNTL                                                 
         LA    R3,31(R3)                                                        
*                                  SEE IF ESR IN USE                            
PSETESR  DS    0H                                                               
         CLI   SCHESR,C'Y'         ESR IN USE ?                                 
         BNE   EXIT                NO                                           
*                                  CREATE A PASSIVE POINTER FOR ESR             
         XC    0(25,R3),0(R3)      CLEAR KEY SPACE                              
         MVC   0(3,R3),SCHKKEY     AGENCY/MEDIA                                 
         MVC   3(2,R3),=X'6203'    ESR PASSIVE RECORD CODE                      
         MVC   5(3,R3),SCHKCLT     CLIENT FROM KEY                              
         MVC   8(3,R3),SCHKPRD     PRODUCT FROM KEY                             
         MVC   11(2,R3),SCHKEST    ESTIMATE FROM KEY                            
         MVC   13(6,R3),SCHKPUB    PUB FROM KEY                                 
         MVC   25(2,R3),SCHCNTL                                                 
         LA    R3,31(R3)                                                        
*                                                                               
         B     EXIT                                                             
         DROP  R2,R4                                                            
         EJECT                                                                  
***********************************************************************         
*   R2 POINTING TO RECORD                                             *         
*   R3 POINTING TO SPACE FOR KEYS                                     *         
*   PASSIVES FOR THE NEW INVOICE RECORD WILL BE ADDED AT AND POINT TO *         
*   ONLY THE "FINAL" (X'FFFF....') PHYSICAL RECORD OF THE LOGICAL SET *         
***********************************************************************         
PNV00    DS    0H                  NEW INVOICE RECORD                           
         USING PNVRECD,R2                                                       
         LA    R4,PNVREC+33                                                     
         USING PNVHDRD,R4                                                       
         CLI   PNVHKCDE,PNVHKIDQ   X'10' ELEMENT ?                              
         BNE   PNV20               NO - GO LOOK FOR INV DTL ELEM'S              
         OC    PNVHKSPR,PNVHKSPR   INVOICE HEADER ELEMENT ?                     
         BNZ   PNV20               NO - GO LOOK FOR INV DTL ELEM'S              
***********************************************************************         
*      CLEAR AND INITIALIZE WORK AREAS FOR NEW INVOICE RECORD PASSIVES          
***********************************************************************         
         LA    RE,NVSVDTA          CLEAR ALL PREVIOUS SAVED DATA                
         LHI   RF,NVSVLNQ          AND SAVE HEADER DATA FOR PASSIVES            
         XCEFL                                                                  
         MVC   NVAGM,PNVKAGY       AGY/MEDIA                                    
         MVC   NVSER#,PNVKSER#     INVOICE SERIAL NUMBER                        
         MVC   NVHINV#,PNVHINV#    HEADER INVOICE NUMBER                        
         MVC   NVHSTRT,PNVHSTRT    HEADER PERIOD START DATE                     
         MVC   NVHEND,PNVHEND      HEADER PERIOD END   DATE                     
         MVC   NVHCLT,PNVHCLT      HEADER CLIENT CODE                           
         MVC   NVHPUB,PNVHPUB      HEADER PUB CODE                              
         MVC   NVHSRC,PNVHIVSR     HEADER INVOICE SOURCE                        
         CLI   NVHSRC,0            ADBUYER DEFAULT?                             
         JNE   *+8                                                              
         MVI   NVHSRC,INVADB_Q     SET TO RESERVED ADBUYER EQUATE               
         XC    NVCRDT,NVCRDT       CLEAR CREATION DATE                          
*                                                                               
         L     RE,=A(NVSVTBL)      POINT TO CSECT AREA FOR PASSIVES             
         ST    RE,NVSVTADR         ADDRESS FOR FIRST DTL PASSIVE                
         L     RF,=A(PSVTABLNQ)                                                 
         XCEFL                                                                  
*                                                                               
PNV20    DS    0H                  LOOK FOR HEADER ACTIVITY ELEMENTS            
         USING PNVACTHD,R4                                                      
         CLI   PNVAKCDE,PNVAKHDQ   HEADER ACTIVITY ELEMENT?                     
         JNE   PNV40                                                            
         CLI   PNVAKACT,PNVAKACQ   ACTIVITY ELEMENT CODE?                       
         JNE   PNV40                                                            
         OC    NVCRDT,NVCRDT       ALREADY DONE CREATION DATE PASSIVE?          
         JNZ   PNV50                                                            
         TM    PNVAHCH1,PNVAHADD   INVOICE HEADER IS ADDED?                     
         JZ    PNV50                                                            
         OC    PNVAHDTE,PNVAHDTE   HAVE ACTIVITY DATE?                          
         JZ    PNV50                                                            
         MVC   NVCRDT,PNVAHDTE     DATE OF CHANGE - INVOICE ADDED               
*                                                                               
         L     R7,NVSVTADR         POINT TO "NEXT" SAVE AREA                    
         CLI   0(R7),X'FF'         END OF TABLE ?                               
         BNE   *+6                 NO                                           
         DC    H'0'                MAXKEYS MUST BE INCREASED                    
*                                                                               
         USING PNV6KEYD,R7         INVOICE CREATION DATE PASSIVE                
         MVC   PNV6AGY(3),NVAGM                                                 
         MVI   PNV6RCD,PNV6RCDQ                                                 
         MVC   PNV6CDTE,NVCRDT     INV CREATION DATE FROM ACTIVITY ELEM         
         MVC   PNV6SRCE,NVHSRC     INVOICE SOURCE FROM HEADER                   
         MVC   PNV6CLT,NVHCLT                                                   
         MVC   PNV6PUB,NVHPUB                                                   
         MVC   PNV6SER#,NVSER#     INVOICE SERIAL NUMBER                        
*                                                                               
         LA    R7,L'PNVKEY(R7)     BUMP TO NEXT SAVE AREA                       
         CLI   0(R7),X'FF'         END OF TABLE ?                               
         BNE   *+6                 NO                                           
         DC    H'0'                MAXKEYS MUST BE INCREASED                    
*                                                                               
         ST    R7,NVSVTADR         SAVE ADDRESS FOR NEXT PASSIVE KEY            
         J     PNV50                                                            
*                                                                               
PNV40    DS    0H                  LOOK FOR INVOCIE DETAIL ELEMENTS             
         USING PNVDTLD,R4                                                       
         CLI   PNVDKCDE,PNVDKIDQ   X'20' ELEMENT ?                              
         BNE   PNV50               NO - NEXT ELEM                               
         CLI   PNVDKTYP,PNVDKDSQ   INVOICE DETAIL ELEMENT ?                     
         BNE   PNV50               NO - NEXT ELEM                               
*                                                                               
*                                  SAVE DETAIL DATA FOR PASSIVES                
         L     R7,NVSVTADR         POINT TO "NEXT" SAVE AREA                    
         CLI   0(R7),X'FF'         END OF TABLE ?                               
         BNE   *+6                 NO                                           
         DC    H'0'                MAXKEYS MUST BE INCREASED                    
*                                                                               
         USING PNV1KEYD,R7         CLT/PUB/SER# PASSIVE                         
         MVC   PNV1AGY(3),NVAGM    AGY/MED                                      
         MVI   PNV1RCD,PNV1RCDQ    RECORD CODE                                  
         MVC   PNV1CLT,NVHCLT      HEADER CLIENT CODE                           
         OC    PNVDCLT,PNVDCLT     DETAIL CLIENT CODE PRESENT ?                 
         BZ    *+10                NO                                           
         MVC   PNV1CLT,PNVDCLT     YES - USE IT INSTEAD OF HEADER               
         MVC   PNV1PUB,NVHPUB      HEADER PUB CODE                              
         OC    PNVDPUB,PNVDPUB     DETAIL PUB CODE PRESENT ?                    
         BZ    *+10                NO                                           
         MVC   PNV1PUB,PNVDPUB     YES - USE IT INSTEAD OF HEADER               
         MVC   PNV1SER#,NVSER#     INVOICE SERIAL NUMBER                        
         LA    R7,L'PNVKEY(R7)     BUMP TO NEXT SAVE AREA                       
         CLI   0(R7),X'FF'         END OF TABLE ?                               
         BNE   *+6                 NO                                           
         DC    H'0'                MAXKEYS MUST BE INCREASED                    
*                                                                               
         USING PNV2KEYD,R7         PASSIVE BASED ON PERIOD                      
         MVC   PNV2AGY(3),NVAGM    AGY/MED                                      
         MVI   PNV2RCD,PNV2RCDQ    RECORD CODE                                  
         MVC   PNV2CLT,NVHCLT      HEADER CLIENT CODE                           
         OC    PNVDCLT,PNVDCLT     DETAIL CLIENT CODE PRESENT ?                 
         BZ    *+10                NO                                           
         MVC   PNV2CLT,PNVDCLT     YES - USE IT INSTEAD OF HEADER               
         MVC   PNV2PUB,NVHPUB      HEADER PUB CODE                              
         OC    PNVDPUB,PNVDPUB     DETAIL PUB CODE PRESENT ?                    
         BZ    *+10                NO                                           
         MVC   PNV2PUB,PNVDPUB     YES - USE IT INSTEAD OF HEADER               
         MVC   PNV2SDTE,NVHSTRT    INVOICE PERIOD START                         
         MVC   PNV2EDTE,NVHEND     INVOICE PERIOD END                           
         MVC   PNV2SER#,NVSER#     INVOICE SERIAL NUMBER                        
         LA    R7,L'PNVKEY(R7)     BUMP TO NEXT SAVE AREA                       
         CLI   0(R7),X'FF'         END OF TABLE ?                               
         BNE   *+6                 NO                                           
         DC    H'0'                MAXKEYS MUST BE INCREASED                    
*                                                                               
         ST    R7,NVSVTADR       SAVE ADDRESS FOR NEXT PASSIVE KEY              
*                                                                               
PNV50    DS    0H                  ELEMENT SEARCHING                            
         CLI   0(R4),0             EOR ?                                        
         BE    PNV60               YES - SEE IF "FINAL" REC OF SET              
         ZIC   R0,1(R4)                                                         
         AR    R4,R0               NEXT ELEMENT                                 
         B     PNV20               GO TEST FOR MORE INVOICE DTL ELEMS           
*                                                                               
*                                                                               
PNV60    DS    0H                  TEST FOR FINAL MINIO REC                     
         CLC   PNVKELMK,=7X'FF'                                                 
         BNE   EXIT                NOT FINAL - NO PASSIVES "YET"                
*                                                                               
         DROP  R4,R7                                                            
*                                                                               
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *           
*   NOW WE HAVE THE LAST (POSSIBLY ONLY) RECORD OF THIS MINIO SET   *           
*    SO OUTPUT THE LAST PASSIVES (BASED ON INVOICE HEADER DATA)     *           
*     TO THE WORK AREA, JOINING (POSSIBLY) OTHER PASSIVES FROM      *           
*           INVOICE DETAIL DATA, AND FINALLY:                       *           
* ADD SAVED PASSIVE KEYS TO A BINSRCH TABLE TO ELIMINATE DUPLICATES *           
*     AND THEN UNLOAD THIS TABLE SEQUENTIALLY OUTPUTTING ALL NEW    *           
*           PASSIVES FOR THIS INVOICE MINIO RECORD SET              *           
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *           
*                                  FINAL MINIO REC PROCESSING                   
*                                                                               
         CLI   NVAGM,0             ANYTHING SAVED ?                             
         BNH   EXIT                NO - MUST BE "EMPTY" MINIO SET               
         CLC   NVAGM,PNVKAGY       SAME AGY/MEDIA SAVED AS IN RECORD ?          
         BNE   EXIT                NO - MUST BE MISSING X'10' "HEADER"          
*NOP*    BE    *+6                 YES                                          
*NOP*    DC    H'0'                SOMETHING WENT WRONG                         
         CLC   NVSER#,PNVKSER#     SAME SERIAL#   SAVED AS IN RECORD ?          
         BNE   EXIT                NO - MUST BE MISSING X'10' "HEADER"          
*NOP*    BE    *+6                 YES                                          
*NOP*    DC    H'0'                SOMETHING WENT WRONG                         
*                                  SAVE HEADER DATA FOR PASSIVES                
         L     R7,NVSVTADR         POINT TO "NEXT" SAVE AREA                    
         CLI   0(R7),X'FF'         END OF TABLE ?                               
         BNE   *+6                 NO                                           
         DC    H'0'                MAXKEYS MUST BE INCREASED                    
*                                                                               
         USING PNV1KEYD,R7         CLT/PUB/SER# PASSIVE                         
         MVC   PNV1AGY(3),NVAGM    AGY/MED                                      
         MVI   PNV1RCD,PNV1RCDQ    RECORD CODE                                  
         MVC   PNV1CLT,NVHCLT      CLIENT CODE                                  
         MVC   PNV1PUB,NVHPUB      PUB CODE                                     
         MVC   PNV1SER#,NVSER#     INVOICE SERIAL NUMBER                        
         LA    R7,L'PNVKEY(R7)     BUMP TO NEXT SAVE AREA                       
         CLI   0(R7),X'FF'         END OF TABLE ?                               
         BNE   *+6                 NO                                           
         DC    H'0'                MAXKEYS MUST BE INCREASED                    
*                                                                               
         USING PNV2KEYD,R7         PASSIVE BASED ON PERIOD                      
         MVC   PNV2AGY(3),NVAGM    AGY/MED                                      
         MVI   PNV2RCD,PNV2RCDQ    RECORD CODE                                  
         MVC   PNV2CLT,NVHCLT      CLIENT CODE                                  
         MVC   PNV2PUB,NVHPUB      PUB CODE                                     
         MVC   PNV2SDTE,NVHSTRT    INVOICE PERIOD START                         
         MVC   PNV2EDTE,NVHEND     INVOICE PERIOD END                           
         MVC   PNV2SER#,NVSER#     INVOICE SERIAL NUMBER                        
         LA    R7,L'PNVKEY(R7)     BUMP TO NEXT SAVE AREA                       
         CLI   0(R7),X'FF'         END OF TABLE ?                               
         BNE   *+6                 NO                                           
         DC    H'0'                MAXKEYS MUST BE INCREASED                    
*                                                                               
         USING PNV3KEYD,R7         PASSIVE BASED ON INVOICE NUMBER              
         MVC   PNV3AGY(3),NVAGM    AGY/MED                                      
         MVI   PNV3RCD,PNV3RCDQ    RECORD CODE                                  
         MVC   PNV3CLT,NVHCLT      CLIENT CODE                                  
         MVC   PNV3PBCD,NVHPUB     PUB CODE                                     
         MVC   PNV3INV#,NVHINV#    INVOICE NUMBER                               
         LA    R7,L'PNVKEY(R7)     BUMP TO NEXT SAVE AREA                       
         CLI   0(R7),X'FF'         END OF TABLE ?                               
         BNE   *+6                 NO                                           
         DC    H'0'                MAXKEYS MUST BE INCREASED                    
*                                                                               
         ST    R3,NVSVR3           SAVE ADDRESS OF KEY SPACE                    
         ST    R2,NVSVR2           AND OF RECORD                                
*                                                                               
         DROP  R2,R7                                                            
*                                                                               
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *           
*           ADD SAVED PASSIVE KEYS TO BINSRCH TABLE                 *           
*              TO ELIMINATE POSSIBLE DUPLICATES                     *           
*  AND THEN UNLOAD THIS TABLE SEQUENTIALLY ADDING ALL NEW PASSIVES  *           
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *           
*                                                                               
         L     RE,=A(NVBINTBL)     POINT TO BINTBL AREA                         
         L     RF,=A(PSVTABLNQ)                                                 
         XCEFL                                                                  
*                                  SET UP BINARY SEARCH PARAMETERS              
         SR    R0,R0               ADDRS OF INSERT                              
         L     R1,=A(NVBINTBL)     ADDRS OF TBL                                 
         SR    R2,R2               NUM OF RECS SO FAR                           
         LA    R3,L'PNVKEY         L'REC                                        
         LA    R4,L'PNVKEY         BYTE 0=KEY DISP,1-3=L'KEY                    
         LA    R5,1000             MAX NUM OF RECS                              
         STM   R0,R5,NVBINPAR                                                   
*                                                                               
         L     R3,NVSVR3           RESTORE ADDRESS OF KEY SPACE                 
         L     R2,NVSVR2           AND OF RECORD                                
         USING PNVRECD,R2                                                       
*                                                                               
         L     R7,=A(NVSVTBL)      POINT TO BEGINNING OF SAVED KEYS             
*                                                                               
PNV100   DS    0H                  UNLOAD KEYS TO BINSRCH TABLE                 
         GOTO1 =V(BINSRCH),NVBINPAR,(X'01',0(R7))                               
         OC    0(4,R1),0(R1)                                                    
         BNZ   *+6                                                              
         DC    H'0'                TABLE IS FULL                                
*                                                                               
         LA    R7,L'PNVKEY(R7)     NEXT SAVED KEY                               
         CLI   0(R7),0             END OF KEYS ?                                
         BH    PNV100              NO                                           
*                                  UNLOAD TABLE TO PASSIVE KEY AREA             
         ZICM  R6,NVBINPAR+8,4     USE R6 AS LOOP (ENTRY COUNT) COUNTER         
         BNZ   *+6                                                              
         DC    H'0'                NO GOOD - CANNOT BE "EMPTY"                  
*                                                                               
         L     R7,=A(NVBINTBL)     POINT TO BEGINNING OF BINSRCH TABLE          
*                                                                               
PNV200   DS    0H                  MOVE KEY ENTRIES TO PASSIVES SPACE           
         MVC   0(L'PNVKEY,R3),0(R7)           CREATE A PASSIVE KEY              
         MVC   L'PNVKEY(L'PNVCNTL,R3),PNVCNTL    CONTROL BYTES                  
         LA    R3,31(R3)           NEXT                                         
         LA    R7,L'PNVKEY(R7)     NEXT TABLE ENTRY                             
         BCT   R6,PNV200           DONE WITH TABLE ?                            
*                                  YES - NOW ADD FINAL INVOICE PASSIVE          
*                                                                               
         XC    0(25,R3),0(R3)      CLEAR                                        
         USING PNV#SERD,R3         PASSIVE BASED ON 9'S COMP SERIAL#            
         MVC   PNV#AGY(3),NVAGM    AGY/MED                                      
         MVI   PNV#RCD,PNV#RCDQ    RECORD CODE                                  
         ZAP   DOUBLE,=P'100000000000'  SER# STARTED AT 100,000                 
         ZAP   DUB,=P'0'                                                        
         MVC   DUB+2(L'NVSER#),NVSER#   SERIAL# FROM KEY                        
         SP    DOUBLE,DUB          9'S COMPLEMENT                               
         MVC   PNV#SER#,DOUBLE+2   9'S COMPLEMENTED SERIAL#                     
         MVC   PNV#CNTL,PNVCNTL    CONTROL BYTES                                
         LA    R3,31(R3)           NEXT POINTER AREA                            
*                                  DONE                                         
*                                                                               
EXIT     XC    0(31,R3),0(R3)      SET END OF POINTER LIST                      
         XMOD1 1                                                                
*                                                                               
         DROP  R2,R3                                                            
*                                                                               
DMCB     DC    6F'0'                                                            
         DS    D                                                                
DUB      DS    D                                                                
DOUBLE   DS    D                                                                
BYTE1    DS    X                                                                
BYTE2    DS    X                                                                
BYTE3    DS    X                                                                
DMPLST   DS    0D                                                               
STX1     DC    A(0),A(4000)                                                     
STX2     DC    A(0),A(4000)                                                     
STX3     DC    A(0),A(4000)                                                     
         ORG   *-4                                                              
         DC    X'80'               EOL                                          
         SPACE 2                                                                
SRCHBLK  DC    (SEARCHDL)X'00'                                                  
         SPACE 2                                                                
DA       DS    XL4                 DISK ADDRESS                                 
NVSVDTA  DS    0D                                                               
NVAGM    DS    CL3                 INVOICE AGENCY/MEDIA                         
NVSER#   DS    XL5  PWOS           INVOICE SERIAL NUMBER                        
NVHINV#  DS    CL14                INVOICE NUMBER           FROM HEADER         
         DS    CL6                 SPARE FOR BIGGER INVOICE NUMBER              
NVHSTRT  DS    XL3                 PERIOD START             FROM HEADER         
NVHEND   DS    XL3                 PERIOD END               FROM HEADER         
NVHCLT   DS    CL3                 CLIENT CODE              FROM HEADER         
NVHPUB   DS    XL6                 PUB CODE                 FROM HEADER         
NVHSRC   DS    XL(L'PNVHIVSR)      INVOICE SOURCE           FROM HEADER         
NVCRDT   DS    XL3                 INVOICE CREATION DATE (PNVAHADD)             
*                                                                               
NVSVTADR DS    A                   ADDRESS TO STORE NEXT PASSIVE                
*                                                                               
NVSVR2   DS    F                   SAVE REGISTER 2                              
NVSVR3   DS    F                   SAVE REGISTER 3                              
*                                                                               
NVBINPAR DS    6F                  BINSRCH PARAMETERS                           
*                                                                               
NVSVLNQ  EQU   *-NVSVDTA                                                        
         SPACE 2                                                                
GLOBALS  DS    0D                  ** GLOBAL LITERALS **                        
         LTORG                                                                  
*                                                                               
         DS    0D                                                               
FULL1    DS    F                                                                
FULL2    DS    F                                                                
MATPPWKS DC    C'MATPPTAB'         LABEL                                        
SVBUYKEY DS    XL32                                                             
SVWIO1KY DS    XL(L'WIO1KEY)       PASSIVE SAVEAREA                             
*                                                                               
         DS    0D                                                               
MATPPTAB DS    2000XL15                                                         
*                                                                               
         TITLE 'LDCPTR - PRT - NEW INVOICE PASSIVES WORK AREA'                  
         SPACE 3                                                                
         DS    0D                                                               
*                           ** WORK AREA FOR NEW INVOICE PASSIVE KEYS           
NVSVTBL  CSECT                                                                  
         DC    (PSVTABLNQ)X'00'     ROOM FOR 2000 PASSIVE KEYS                  
*                                                                               
NVBINTBL CSECT                                                                  
         DC    (PSVTABLNQ)X'00'     ROOM FOR 2000 PASSIVE KEYS                  
*                                                                               
PSVTABLNQ EQU  50000                                                            
         EJECT                                                                  
*                   BUY REC DSECT                                               
PBUYRECD DSECT                                                                  
       ++INCLUDE PBUYREC                                                        
         SPACE 3                                                                
*                   PRODUCT ELEM DSECT                                          
PPRELEMD DSECT                                                                  
       ++INCLUDE PPRELEM                                                        
         SPACE 2                                                                
PBILRECD DSECT                                                                  
       ++INCLUDE PBILLREC                                                       
         SPACE 2                                                                
PCLTRECD DSECT                                                                  
       ++INCLUDE PCLTREC                                                        
         SPACE 2                                                                
PPRDRECD DSECT                                                                  
       ++INCLUDE PPRDREC                                                        
         SPACE 2                                                                
PESTRECD DSECT                                                                  
       ++INCLUDE PESTREC                                                        
         SPACE 2                                                                
PREPRECD DSECT                                                                  
       ++INCLUDE PREPREC                                                        
         SPACE 2                                                                
PJOBRECD DSECT                                                                  
       ++INCLUDE PJOBREC                                                        
         SPACE 2                                                                
PCOLRECD DSECT                                                                  
       ++INCLUDE PCOLREC                                                        
         SPACE 2                                                                
PIOELEMD DSECT                                                                  
       ++INCLUDE PIOELEM                                                        
         SPACE 2                                                                
       ++INCLUDE PPGENPZ                                                        
         SPACE 2                                                                
*                   PRINT INVOICE MATCH REC DSECTS                              
*PNVRECD                                                                        
*PNV1KEY                                                                        
*PNV2KEY                                                                        
*PNV3KEY                                                                        
*PNVHDRD                                                                        
*PNVDTLD                                                                        
*PNVCOMD                                                                        
*PNVACTD                                                                        
         SPACE 2                                                                
       ++INCLUDE PPGENPNV                                                       
         SPACE 2                                                                
       ++INCLUDE PPGENPNV#                                                      
         SPACE 2                                                                
*                   PRINT WEBIO RECORD DSECTS                                   
*WIORECD                                                                        
*WIO1KEYD                                                                       
*WIO#IO#D                                                                       
*WIOHDRD                                                                        
         SPACE 2                                                                
       ++INCLUDE PPGENWIO                                                       
         SPACE 3                                                                
*                   PRINT ESR   RECORD DSECTS                                   
*ESRRECD                                                                        
*ESR1KEYD                                                                       
*ESR#SR#D                                                                       
*ESRHDRD                                                                        
         SPACE 2                                                                
       ++INCLUDE PPGENESR                                                       
         SPACE 3                                                                
*                   PRINT SETUP RECORD DSECTS                                   
*SCHRECD                                                                        
*SCH1KEYD                                                                       
*SCH2KEYD                                                                       
*SCHHDRD                                                                        
         SPACE 2                                                                
       ++INCLUDE PPGENSCH                                                       
         SPACE 3                                                                
*** ++ INCLUDE GESRCHBLKD                                                       
*                                                                               
         PRINT OFF                                                              
       ++INCLUDE GESRCHBLKD                                                     
         PRINT ON                                                               
*                                                                               
         PRINT OFF                                                              
       ++INCLUDE PPGENMATP                                                      
*                                                                               
       ++INCLUDE PPGENBYPP                                                      
*                                                                               
PBDELEMD DSECT                                                                  
       ++INCLUDE PBDELEM                                                        
*                                                                               
PEIOELMD DSECT                                                                  
       ++INCLUDE PPGENBYIO                                                      
*                                                                               
       ++INCLUDE PPGENBYSA                                                      
*                                                                               
         PRINT ON                                                               
*                                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'037PRLDCPTR  10/26/19'                                      
         END                                                                    
