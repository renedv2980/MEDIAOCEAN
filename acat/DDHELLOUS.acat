*          DATA SET DDHELLOUS  AT LEVEL 009 AS OF 04/18/13                      
*PROCESS USING(WARN(15))                                                        
*CATALP HELLO                                                                   
         TITLE 'GENERAL ELEMENT GET/ADD/DELETE MODULE'                          
***********************************************************************         
* INPUT PARAMETER  1  BYTE 0       ACTION (A,P,D,G,p,d,g)             *         
*                                   LOWERCASE ACTIONS REQUIRE P5      *         
*                          1-3     P,D,G -- A(FILE NAME)              *         
*                                  A -- C'FIL' RETURNS A(HELEN)       *         
*                                       C'DIR' RETURNS A(HELENDIR)    *         
*                  2       0       ELEMENT CODE (D OR G)              *         
*                          1-3     A(RECORD)                          *         
*                  3       0       L'SEARCH ARGUMENT                  *         
*                          1-3     A(ELEMENT)-P,A(SRCH ARG)-D OR G    *         
*                  4       1-3     A(C'ADD=CODE' OR C'ADD=END' OR...  *         
*                                    C'ADD=HERE')                     *         
*                  5       0-3     MAXIMUM RECORD LENGTH OVERRIDE     *         
*                                   (IF ACTION IS LOWERCASE)          *         
*                  6       0       RESERVED: MUST BE ZERO FOR NOW!    *         
*                          1-3     A(INSERTION) IF P4 IS 'ADD=HERE'   *         
*                                                                     *         
* OUTPUT PARAMETER 4       0       0=SUCCESS OR ERROR INDICATOR       *         
*                          1-3     L'RECORD AFTER HELLO OR A(ELEMENT) *         
*                  5       1-3     A(ELEMENT AFTER ADD)               *         
*                                                                     *         
***********************************************************************         
         EJECT                                                                  
HELLO    CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 HELWRKL,**HELLO,RR=RE                                            
         USING HELWRKD,RC                                                       
         XC    HELWRKD(HELWRKL),HELWRKD                                         
         ST    RE,RELO                                                          
*                                                                               
         MVC   ACTION,0(R1)        CALLER'S ACTION PARAMETER                    
         OI    ACTION,X'40'        FORCE TO UPPERCASE                           
*                                                                               
         CLI   ACTION,C'A'         TEST ACTION A (FOR ADDRESS)                  
         BNE   HEL02                                                            
*                                                                               
         XR    RF,RF                                                            
         CLC   =CL3'FIL',1(R1)     RETURN A(HELEN)?                             
         BNE   *+8                 NO                                           
         L     RF,AHELEN                                                        
         CLC   =CL3'DIR',1(R1)     RETURN A(HELENDIR)?                          
         BNE   *+8                 NO                                           
         L     RF,AHELEND                                                       
*                                                                               
         LTR   RF,RF               A(HELEN) OR A(HELENDIR) SET?                 
         BNZ   *+12                YES                                          
         MVI   12(R1),7            INVALID ADDRESS REQUESTED                    
         B     EXIT                                                             
*                                                                               
         A     RF,RELO                                                          
         ST    RF,0(R1)            RETURN ADDRESS IN 1ST PARAMETER              
         MVI   12(R1),0            ZERO ERROR CODE                              
         B     EXIT                                                             
*                                                                               
HEL02    LM    R2,R5,0(R1)         LOAD PARAMETERS                              
         CLI   ACTION,C'P'         'PUT' ELEMENT?                               
         BNE   HEL04               NO                                           
*                                                                               
         LTR   R4,R4               HAVE TO PASS AN ELEMENT                      
         BNZ   *+12                                                             
         MVI   12(R1),3            MISSING ADD ELEMENT                          
         B     EXIT                                                             
*                                                                               
         CLI   1(R4),1             LENGTH MUST BE 2 OR MORE                     
         BH    *+12                                                             
         MVI   12(R1),4            INVALID ADD ELEMENT LENGTH                   
         B     EXIT                                                             
*                                                                               
         LTR   R5,R5               P4 PASSED?                                   
         BZ    HEL04               NO                                           
*                                                                               
         MVI   ADDSW,C'Y'                                                       
         CLC   ADDEND,0(R5)                                                     
         BNE   *+8                                                              
         MVI   ADDSW,C'E'                                                       
         CLC   ADDCODE,0(R5)                                                    
         BNE   *+8                                                              
         MVI   ADDSW,C'C'                                                       
         CLC   =C'ADD=HERE',0(R5)                                               
         BNE   *+8                                                              
         MVI   ADDSW,C'H'                                                       
         B     HEL04                                                            
         EJECT                                                                  
***********************************************************************         
* FIND FILE DEFINITION ENTRY IN LIST                                  *         
***********************************************************************         
         SPACE 1                                                                
HEL04    XC    12(4,R1),12(R1)                                                  
         L     R8,AHELEN                                                        
         A     R8,RELO                                                          
         USING HELEND,R8                                                        
         LHI   RF,L'HELENL                                                      
*                                                                               
         XR    R9,R9                                                            
HEL06    CLI   HELFLEN,X'FF'       END OF TABLE?                                
         BE    HEL08                                                            
         IC    R9,HELFLEN                                                       
         EX    R9,HELC                                                          
         BE    HEL10                                                            
         BXH   R8,RF,HEL06         PARSE TABLE                                  
         DC    H'0'                                                             
*                                                                               
HELC     CLC   HELNAME(0),0(R2)                                                 
*                                                                               
HEL08    MVI   12(R1),1            INVALID FILE NAME                            
         B     EXIT                                                             
         EJECT                                                                  
***********************************************************************         
* SET UP PARAMETERS AND GO TO ROUTINE                                 *         
***********************************************************************         
         SPACE 1                                                                
HEL10    XR    R9,R9                                                            
         LA    R3,0(R3)                                                         
         ST    R3,ARECORD                                                       
         LR    R5,R3                                                            
         IC    R9,HELEDIS          DISP OF FIRST ELEMENT                        
         AR    R3,R9                                                            
         ST    R3,AFIRSTEL                                                      
         IC    R9,HELLDIS          DISP OF LENGTH                               
         AR    R5,R9                                                            
         ST    R5,ALENGTH                                                       
*                                                                               
         XR    RF,RF               MAKE SURE END OF RECORD IS ZEROS             
         ICM   RF,3,0(R5)                                                       
*??      AR    RF,R3                                                            
*??      XC    0(2,RF),0(RF)                                                    
*                                                                               
         XC    MAXLEN,MAXLEN                                                    
         MVC   MAXLENH,HELMSIZE                                                 
         XC    MAXRLEN,MAXRLEN                                                  
         MVC   MAXRLENH,HELMSIZE                                                
*                                                                               
         CLC   =C'TAL',HELNAME     TEMP OVERRIDES FOR TALENT                    
         BNE   *+10                                                             
         MVC   MAXLENH,=AL2(12000)                                              
*                                                                               
         CLC   =C'CHK',HELNAME                                                  
         BNE   *+10                                                             
         MVC   MAXLENH,=AL2(12000)                                              
*                                                                               
         CLC   =C'SPTFIL',HELNAME  SPOT BUY RECORDS ARE 6K                      
         BNE   HEL20                                                            
         ICM   RF,15,ARECORD                                                    
         BZ    HEL20                                                            
         CLI   0(RF),X'10'         TEST SPOT BUY RECORD                         
         BNH   HEL20                                                            
         MVC   MAXLENH,=AL2(5972)                                               
         MVC   MAXRLEN,MAXLEN                                                   
*                                                                               
HEL20    CLC   =C'TRFFIL',HELNAME  TRAFFIC INST RECAPS ARE 4K                   
         BNE   HEL22                                                            
         ICM   RF,15,ARECORD                                                    
         BZ    HEL22                                                            
         CLC   =X'0A24',0(RF)      TEST INST RECAP RECORD                       
         BE    HEL21                                                            
         CLC   =X'0A2E',0(RF)      TEST TRAFFIC BUYACT REC                      
         BNE   HEL22                                                            
HEL21    MVC   MAXLENH,=AL2(3976)                                               
         MVC   MAXRLEN,MAXLEN                                                   
*                                                                               
HEL22    TM    0(R1),X'40'         ACTION WAS LOWERCASE?                        
         BO    *+16                                                             
         MVC   MAXLEN,16(R1)       YES: MAX RECLEN OVERRIDE IS IN P5            
         MVC   MAXRLEN,MAXLEN                                                   
*                                                                               
         CLI   ACTION,C'P'         ADD                                          
         BE    HELADD                                                           
         CLI   ACTION,C'D'         DELETE                                       
         BE    HELDEL                                                           
         CLI   ACTION,C'G'         GET                                          
         BE    HELGET                                                           
         MVI   12(R1),2            INVALID ACTION                               
         B     EXIT                                                             
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO ADD AN ELEMENT (R4 POINTS TO ADD)                        *         
***********************************************************************         
         SPACE 1                                                                
HELADD   L     RF,ALENGTH                                                       
         SLR   RE,RE                                                            
         IC    RE,1(R4)                                                         
         SLR   R0,R0                                                            
         ICM   R0,3,0(RF)                                                       
         LR    RF,R0               SAVE CURRENT RECORD LENGTH                   
         AR    R0,RE               R0 = L'RECORD WITH ELEMENT ADDED             
         C     R0,MAXLEN                                                        
         BNH   *+12                                                             
         MVI   12(R1),5            RECORD TOO LONG IF ELEMENT ADDED             
         B     EXIT                                                             
*                                                                               
         L     R2,AFIRSTEL                                                      
         XR    R6,R6                                                            
         L     R7,ARECORD                                                       
         A     R7,MAXLEN                                                        
*                                                                               
         CLI   ADDSW,C'H'          INSERTION POINT PASSED BY CALLER?            
         BNE   HELA02              NO                                           
         SR    RE,RE               RE WILL HOLD A(INSERTION POINT)...           
         ICM   RE,7,21(R1)         ...FROM P6                                   
         BNZ   *+6                                                              
         DC    H'0'                                                             
         C     RE,AFIRSTEL         VALIDATE INSERTION POINT ADDRESS             
         BL    HELAERR             A(INSERTION POINT) < A(1ST ELEMENT)!         
         L     R0,ARECORD                                                       
         AR    R0,RF               R0 = A(EOR)                                  
         BCTR  R0,0                R0 = A(LAST BYTE OF RECORD)                  
         CR    R0,RE                                                            
         BL    HELAERR             INSERTION POINT BEYOND EOR!                  
         LR    R2,RE               INSERTION POINT IS WITHIN THE RECORD         
         B     HELA06                                                           
*                                                                               
HELA02   CLI   0(R2),0             AT END OF RECORD?                            
         BE    HELA18              YES - ADD TO END                             
         CLI   ADDSW,C'E'          WANT TO ADD TO END OF RECORD?                
         BE    HELA04              YES - NEXT ELEMENT                           
*                                                                               
         CLC   0(1,R4),0(R2)       COMPARE INSERT ELEMENT WITH RECORD           
         BL    HELA06              INSERT ELEMENT LOWER - INSERT HERE           
         BH    HELA04              INSERT ELEMENT HIGHER - CONTINUE             
*                                                                               
         CLI   ADDSW,C'C'          INSERT ELEMENT IN CODE SEQ ONLY?             
         BE    HELA04              YES - CONTINUE                               
*                                                                               
* CHECK FOR CORRECT ELEMENT SEQUENCE TO INSERT                                  
*                                                                               
         MVC   BYTE,1(R2)          FIND SHORTEST ELEMENT LENGTH                 
         CLC   1(1,R4),1(R2)                                                    
         BH    *+10                                                             
         MVC   BYTE,1(R4)                                                       
*                                                                               
         XR    R5,R5                                                            
         IC    R5,BYTE                                                          
         AHI   R5,-3                                                            
         BNM   *+10                                                             
         XR    R5,R5                                                            
         B     HELA06                                                           
*                                                                               
         EX    R5,HELADDC                                                       
         BE    *+12                DUPLICATE DATA IN ADD/REC                    
         BNH   HELA06                                                           
         B     HELA04                                                           
*                                                                               
         CLC   1(1,R4),1(R2)       COMPARE ELEMENT LENGTHS                      
         BH    HELA04              IF HIGHER - GET NEXT ELEMENT                 
         B     HELA06                                                           
*                                                                               
HELADDC  CLC   2(0,R4),2(R2)       COMPARE DATA IN ADD TO THIS                  
*                                                                               
HELA04   ICM   R6,1,1(R2)          NEXT ELEMENT                                 
         BZ    HELAERR                                                          
         BXLE  R2,R6,HELA02                                                     
         B     HELAERR                                                          
         EJECT                                                                  
***********************************************************************         
* INSERT ELEMENT INTO RECORD - R2 = A(INSERTION POINT)                *         
* AIM IS TO MOVE RECORD TO RIGHT IN PIECES FROM RIGHT TO LEFT         *         
* SIZE OF MOVE IS GIVEN BY SIZE OF ELEMENT TO BE INSERTED             *         
***********************************************************************         
         SPACE 1                                                                
HELA06   LR    R3,R2               FIND END OF RECORD                           
*                                                                               
HELA08   CLI   0(R3),0             EOR                                          
         BE    HELA10                                                           
         ICM   R6,1,1(R3)                                                       
         BZ    HELAERR                                                          
         BXLE  R3,R6,HELA08                                                     
         B     HELAERR                                                          
*                                                                               
HELA10   AHI   R3,1                INCLUDE THE 0 AT THE END                     
         ICM   R6,1,1(R4)          R6 = LENGTH OF ELEMENT TO INSERT             
*                                                                               
         LR    RE,R3               FIND HOW MANY BYTES TO MOVE -                
         SR    RE,R2               NEED TO MOVE (RE) BYTES ALONG                
         CR    RE,R6                                                            
         BL    HELA11              NOT ENOUGH TO MOVE AS A LOOP                 
*                                                                               
         LR    R7,R3                                                            
         SR    R7,R6               MOVE FROM (R7) TO (R3)                       
         LR    RF,R6               START WITH INSERT AMOUNT                     
         B     HELA12                                                           
*                                                                               
HELA11   LR    R7,R3                                                            
         SR    R7,RE                                                            
         LR    RF,RE               START WITH AMOUNT LEFT                       
*                                                                               
         LR    R0,R2                                                            
         AR    R0,R6               MOVE TO POINT                                
         CR    R3,R0                                                            
         BH    *+6                                                              
         LR    R3,R0               MAKE SURE MOVING TO RIGHT FAR ENOUGH         
*                                                                               
HELA12   BCTR  RF,0                                                             
         EX    RF,HELASHFL         MOVE DATA OVER                               
         AHI   RF,1                                                             
         SR    RE,RF               REDUCE AMOUNT LEFT TO MOVE                   
         BZ    HELA14              FINISHED                                     
*                                                                               
         LR    RF,R6               DEFAULT TO REDUCE IS INSERT AMOUNT           
         CR    RE,R6               MORE THAN THIS LEFT?                         
         BH    *+6                 YES                                          
         LR    RF,RE               SET AMOUNT LEFT                              
         SR    R7,RF               GO BACKWARDS IN RECORD                       
         SR    R3,RF                                                            
         B     HELA12                                                           
*                                                                               
HELA14   BCTR  R6,0                MOVE IN ELEMENT TO INSERT POINT              
         EX    R6,HELAINS                                                       
         ST    R2,16(R1)           SAVE A(INSERTED ELEMENT)                     
*                                                                               
         L     R2,AFIRSTEL         FIND END OF RECORD                           
         XR    R6,R6                                                            
         L     R7,ARECORD                                                       
         A     R7,MAXLEN                                                        
*                                                                               
HELA16   CLI   0(R2),0                                                          
         BE    HELA20                                                           
         ICM   R6,1,1(R2)                                                       
         BZ    HELAERR                                                          
         BXLE  R2,R6,HELA16                                                     
         B     HELAERR                                                          
*                                                                               
HELASHFL MVC   0(0,R3),0(R7)       SHUFFLE RECORD TO RIGHT                      
HELAINS  MVC   0(0,R2),0(R4)       INSERT ELEMENT INTO GAP                      
*                                                                               
HELA18   XR    R6,R6           *** ADD AT END OF RECORD                         
         ICM   R6,1,1(R4)                                                       
         BZ    HELAERR                                                          
         BCTR  R6,0                                                             
         EX    R6,HELAMVC          MOVE IN ELEMENT                              
         ST    R2,16(R1)           SAVE A(INSERTED ELEMENT)                     
         LA    R2,1(R6,R2)                                                      
         MVI   0(R2),0             ADD A 0 AT THE END                           
*                                                                               
HELA20   AHI   R2,1                INCLUDE 0 AT END IN LENGTH                   
         S     R2,ARECORD                                                       
         ST    R2,12(R1)           PASS BACK NEW LENGTH                         
         L     RF,ALENGTH                                                       
         STCM  R2,3,0(RF)                                                       
*                                                                               
         C     R2,MAXRLEN          NOW SET RC BASED ON REAL LENGTH              
         BNH   *+8                                                              
         MVI   12(R1),5            RECORD TOO LONG IF ELEMENT ADDED             
         B     EXIT                                                             
*                                                                               
HELAMVC  MVC   0(0,R2),0(R4)                                                    
*                                                                               
HELAERR  DC    H'0'                                                             
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO DELETE AN ELEMENT                                                  
***********************************************************************         
         SPACE 1                                                                
HELDEL   L     R3,AFIRSTEL                                                      
         XR    R6,R6                                                            
         L     R7,ARECORD         SET BOUNDS FOR SEARCH                         
         A     R7,MAXLEN                                                        
*                                                                               
HELD02   CLI   0(R3),0            END OF RECORD?                                
         BE    HELD04             YES                                           
         ICM   R6,1,1(R3)                                                       
         BZ    HELDERR                                                          
         BRAS  RE,HELDCOM         MATCH ELEMENT?                                
         BE    HELD06             YES                                           
         BXLE  R3,R6,HELD02                                                     
         B     HELDERR                                                          
*                                                                               
HELD04   LA    R3,1(R3)            INCLUDE ZERO ON END                          
         S     R3,ARECORD                                                       
         L     RF,ALENGTH                                                       
         STCM  R3,3,0(RF)          SET NEW LENGTH IN RECORD                     
         ST    R3,12(R1)           AND PASS IT BACK                             
         B     EXIT                                                             
*                                                                               
HELD06   LR    R5,R3               FOUND ELEMENT NOW DELETE IT                  
*                                                                               
HELD08   CLI   0(R5),0             LOOP THROUGH TO END OF RECORD                
         BE    HELD10                                                           
         ICM   R6,1,1(R5)                                                       
         BZ    HELDERR                                                          
         BXLE  R5,R6,HELD08                                                     
         B     HELDERR                                                          
*                                                                               
HELD10   LR    R4,R3               R4 = MOVE TO ADDRESS                         
         LR    R6,R3                                                            
         XR    RF,RF                                                            
         IC    RF,1(R4)                                                         
         AR    R6,RF               R6 = MOVE FROM ADDRESS                       
         LR    R7,R5                                                            
         SR    R5,R4               R5 = MOVE TO LENGTH                          
         SR    R7,R6               R7 = MOVE FROM LENGTH                        
         MVCL  R4,R6               MOVE UP AND ZERO FILL                        
         B     HELDEL                                                           
*                                                                               
HELDERR  DC    H'0'                RECORD IS BAD SOMEHOW                        
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO GET AN ELEMENT                                           *         
***********************************************************************         
         SPACE 1                                                                
HELGET   L     R3,AFIRSTEL                                                      
         XR    R6,R6                                                            
         L     R7,ARECORD          SET BOUNDS ON SEARCH                         
         A     R7,MAXLEN                                                        
*                                                                               
HELG02   CLI   0(R3),0             END OF RECORD                                
         BE    HELGERR                                                          
         ICM   R6,1,1(R3)          ZERO LENGTH ELEMENT                          
         BZ    HELGERR                                                          
         BRAS  RE,HELDCOM                                                       
         BE    HELG04                                                           
         BXLE  R3,R6,HELG02                                                     
         B     HELGERR             RECORD IS A MESS - DC H'0' ?                 
*                                                                               
HELG04   ST    R3,12(R1)                                                        
         B     EXIT                                                             
*                                                                               
HELGERR  MVI   12(R1),6            NOT FOUND                                    
         B     EXIT                                                             
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO CHECK FOR ELEMENT FOUND                                            
***********************************************************************         
         SPACE 1                                                                
HELDCOM  CLC   4(1,R1),0(R3)       MATCH ELEMENT?                               
         BNER  RE                  NO                                           
         XR    R9,R9                                                            
         ICM   R9,1,8(R1)                                                       
         BZR   RE                                                               
         L     RA,8(R1)                                                         
         BCTR  R9,0                                                             
         EX    R9,*+6                                                           
         BR    RE                                                               
         CLC   2(0,R3),0(RA)                                                    
         EJECT                                                                  
***********************************************************************         
* EXIT POINTS                                                         *         
***********************************************************************         
         SPACE 1                                                                
EXIT     XIT1  ,                                                                
         EJECT                                                                  
***********************************************************************         
* LITERALS ETC.                                                                 
***********************************************************************         
         SPACE 1                                                                
AHELEN   DC    V(HELEN)                                                         
AHELEND  DC    V(HELENDIR)                                                      
ADDEND   DC    CL7'ADD=END'                                                     
ADDCODE  DC    CL8'ADD=CODE'                                                    
*                                                                               
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
* W/S DSECT                                                           *         
***********************************************************************         
         SPACE 1                                                                
HELWRKD  DSECT                                                                  
RELO     DS    A                                                                
ARECORD  DS    A                                                                
AFIRSTEL DS    A                                                                
ALENGTH  DS    A                                                                
*                                                                               
MAXLEN   DS    0F                  MAX LENGTH (16 BIT UNSIGNED)                 
         DS    H                                                                
MAXLENH  DS    H                   HALFWORD VERSION                             
*                                                                               
MAXRLEN  DS    0F                  REAL MAX LENGTH (16 BIT UNSIGNED)            
         DS    H                                                                
MAXRLENH DS    H                   HALFWORD VERSION                             
*                                                                               
ACTION   DS    C                   REQUESTED ACTION                             
*                                                                               
BYTE     DS    CL1                                                              
ADDSW    DS    CL1                                                              
HELWRKL  EQU   *-HELWRKD                                                        
         SPACE 1                                                                
***********************************************************************         
* DSECT TO COVER FILE DEFINITION TABLE                                          
***********************************************************************         
         SPACE 1                                                                
HELEND   DSECT                                                                  
HELENL   DS    0CL13                                                            
HELFLEN  DS    CL1                 L'FILE NAME - 1                              
HELNAME  DS    CL8                 NAME                                         
HELMSIZE DS    CL2                 MAXIMUM RECORD SIZE                          
HELEDIS  DS    CL1                 DISPLACEMENT OF FIRST ELEMENT                
HELLDIS  DS    CL1                 DISPLACEMENT OF LENGTH                       
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'009DDHELLOUS 04/18/13'                                      
         END                                                                    
