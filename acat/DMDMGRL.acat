*          DATA SET DMDMGRL    AT LEVEL 005 AS OF 11/27/18                      
*CATALP DMDMGRL                                                                 
         TITLE 'DMGRLINK - LINK TO DATAMGR MODULES VIA DDSIO'                   
***********************************************************************         
* THIS MODULE LINKS DATAMGR CALLS TO DDSIO. ON FIRST CALL DDSIO IS    *         
* LOADED INTO STORAGE. WHEN CALLING DDSIO THE HIGH ORDER BYTE OF R4   *         
* CONTAINS THE DATAMGR MODULE NUMBER. NOTE THAT THE ROUTINE NUMBER    *         
* CORRESPONDS TO THE LIST OF ADCONS IN DMDDSIO (VDMGRMOD).            *         
***********************************************************************         
         PRINT NOGEN                                                            
DMGRLINK CSECT                                                                  
         ENTRY DDSIO                                                            
         ENTRY ADDSIO                                                           
         ENTRY DMGRCNT                                                          
         REQUS                                                                  
         DC    C'DMGRLINK'                                                      
*                                                                               
         ENTRY DATAMGR                                                          
         USING *,RF                                                             
DATAMGR  MVI   VADWAIT,0                                                        
         B     DMGL                                                             
*                                                                               
         ENTRY DADDS                                                            
         USING *,RF                                                             
DADDS    MVI   VADWAIT,1                                                        
         B     DMGL                                                             
*                                                                               
         ENTRY ISDDS                                                            
         USING *,RF                                                             
ISDDS    MVI   VADWAIT,2                                                        
         B     DMGL                                                             
*                                                                               
         ENTRY DMDANDX                                                          
         USING *,RF                                                             
DMDANDX  MVI   VADWAIT,3                                                        
         B     DMGL                                                             
*                                                                               
         ENTRY DYNALLOC                                                         
         USING *,RF                                                             
DYNALLOC MVI   VADWAIT,4                                                        
         B     DMGL                                                             
*                                                                               
         ENTRY WORKER                                                           
         USING *,RF                                                             
WORKER   MVI   VADWAIT,5                                                        
         B     DMGL                                                             
*                                                                               
         ENTRY DMDABUFF                                                         
         USING *,RF                                                             
DMDABUFF MVI   VADWAIT,6                                                        
         B     DMGL                                                             
*                                                                               
         ENTRY DMDALINK                                                         
         USING *,RF                                                             
DMDALINK MVI   VADWAIT,7                                                        
         B     DMGL                                                             
*                                                                               
         ENTRY DMDAPTRS                                                         
         USING *,RF                                                             
DMDAPTRS MVI   VADWAIT,8                                                        
         B     DMGL                                                             
*                                                                               
         ENTRY PQOPEN                                                           
         USING *,RF                                                             
PQOPEN   MVI   VADWAIT,9                                                        
         B     DMGL                                                             
*                                                                               
         ENTRY DMENQDEQ                                                         
         USING *,RF                                                             
DMENQDEQ MVI   VADWAIT,10                                                       
         B     DMGL                                                             
*                                                                               
         ENTRY DMOD000                                                          
         USING *,RF                                                             
DMOD000  MVI   VADWAIT,11                                                       
         B     DMGL                                                             
*                                                                               
         ENTRY DMACCEMU                                                         
         USING *,RF                                                             
DMACCEMU MVI   VADWAIT,12                                                       
         B     DMGL                                                             
*                                                                               
         ENTRY LOCKSPC                                                          
         USING *,RF                                                             
LOCKSPC  MVI   VADWAIT,13                                                       
         B     DMGL                                                             
*                                                                               
         ENTRY DMDDNAME                                                         
         USING *,RF                                                             
DMDDNAME MVI   VADWAIT,14                                                       
         B     DMGL                                                             
*                                                                               
         ENTRY LOCKUP                                                           
         USING *,RF                                                             
LOCKUP   MVI   VADWAIT,15                                                       
         B     DMGL                                                             
*                                                                               
         ENTRY MQRPT                                                            
         USING *,RF                                                             
MQRPT    MVI   VADWAIT,16                                                       
         B     DMGL                                                             
*                                                                               
         ENTRY DMRCVUSS                                                         
         USING *,RF                                                             
DMRCVUSS MVI   VADWAIT,17                                                       
         B     DMGL                                                             
*                                                                               
         ENTRY DMISGENQ                                                         
         USING *,RF                                                             
DMISGENQ MVI   VADWAIT,18                                                       
         B     DMGL                                                             
*                                                                               
         ENTRY DMSHMUSS                                                         
         USING *,RF                                                             
DMSHMUSS MVI   VADWAIT,19                                                       
         B     DMGL                                                             
*                                                                               
         ENTRY DMSYSFIL                                                         
         ENTRY DMFATABS                                                         
         USING *,RF                                                             
DMFATABS DS    0H                                                               
DMSYSFIL MVI   VADWAIT,20                                                       
         B     DMGL                                                             
*                                                                               
         ENTRY GETRET                                                           
         USING *,RF                                                             
GETRET   MVI   VADWAIT,21                                                       
         B     DMGL                                                             
*                                                                               
         ENTRY DMDYNDD                                                          
         USING *,RF                                                             
DMDYNDD  MVI   VADWAIT,22                                                       
         B     DMGL                                                             
         EJECT                                                                  
         DROP  RF                                                               
DMGL     STM   RE,RC,12(RD)        SAVE CALLERS REGISTERS & CALL DDSIO          
         LA    RC,72(RD)                                                        
         ST    RD,4(RC)                                                         
         ST    RC,8(RD)                                                         
*                                                                               
         BASR  RB,0                ESTABLISH ADDRESSABILITY                     
         USING *,RB                                                             
         MVC   0(4,RD),DMGLLIT                                                  
         LR    RD,RC                                                            
         ST    R1,APARM                                                         
*                                                                               
         L     RF,DMGRCNT                                                       
         AHI   RF,1                                                             
         ST    RF,DMGRCNT                                                       
*                                                                               
         ICM   RF,15,ADDSIO        LOAD DDSIO ON FIRST CALL                     
         BNZ   DMGL2                                                            
         LOAD  EPLOC=DDSIO                                                      
         ST    R0,ADDSIO                                                        
         LR    RF,R0                                                            
*&&US                                                                           
         ST    R2,SAVER2                                                        
         MVC   WAIT,VADWAIT        DO DUMMY LOCKSPC CALL                        
         MVI   VADWAIT,13                                                       
         LA    R1,INISTR                                                        
         LM    R2,R4,VUTL                                                       
         ICM   R7,15,=CL4'ABEA'    NOW WE CAN BLAME ANNIE...                    
         L     R6,VMASTC           PASS V(MASTC) IN R6                          
         CLI   SSOXTND-SSOOFF(R3),X'FF'                                         
         BNE   *+12                                                             
         CLI   SSODSPAC-SSOOFF(R3),C'N'                                         
         BE    *+6                 DSPACE=N WAS INPUT                           
         BASR  RE,RF                                                            
         MVC   VADWAIT(1),WAIT                                                  
         L     R2,SAVER2                                                        
         L     RF,ADDSIO                                                        
*&&                                                                             
DMGL2    CLM   R2,8,=X'FF'         BOBL'S TEST FOR BOBL'S CALL                  
         BNE   DMGL3                                                            
         L     R1,APARM                                                         
         LM    R3,R4,VSSB                                                       
         L     R5,VUTL             R5 REPLACES R2 FOR BOBL'S CALL               
         B     DMGL4                                                            
*                                                                               
DMGL3    LM    R1,R4,APARM         CALL DDSIO PASSING R1 THRU R4                
*                                                                               
DMGL4    EQU   *                                                                
*&&US*&& ICM   R7,15,=CL4'ABEA'    NOW WE CAN BLAME ANNIE...                    
*&&US*&& L     R6,VMASTC           PASS V(MASTC) IN R6                          
         BASR  RE,RF                                                            
*                                                                               
         L     RD,4(RD)            RESTORE CALLERS REGISTERS & RETURN           
         LM    RE,RC,12(RD)                                                     
         BR    RE                                                               
*                                                                               
APARM    DC    A(0)                                                             
VUTL     DC    V(UTL)                                                           
VSSB     DC    V(SSB)                                                           
VADWAIT  DC    V(ADWAIT)                                                        
VMASTC   DC    V(MASTC)                                                         
         DC    4A(0)                                                            
*                                                                               
INISTR   DC    XL8'2000800100000000'                                            
*                                                                               
ADDSIO   DC    A(0)                                                             
DDSIO    DC    CL8'DDSIO'                                                       
DMGLLIT  DC    CL4'DMGL'                                                        
DMGRCNT  DC    F'0'                                                             
SAVER2   DC    F'0'                                                             
WAIT     DC    X'00'                                                            
*                                                                               
         LTORG                                                                  
*                                                                               
SSBD     DSECT                                                                  
*FASSBOFF                                                                       
       ++INCLUDE FASSBOFF                                                       
*                                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'005DMDMGRL   11/27/18'                                      
         END                                                                    
