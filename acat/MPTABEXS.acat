*          DATA SET MPTABEXS   AT LEVEL 052 AS OF 05/01/02                      
*CATALP TABEX                                                                   
         TITLE 'TABEX- CROSS-TAB EXTRACT MODULE'                                
         SPACE 2                                                                
*                                                                               
*        PARAMETER LIST                                                         
*        --------------                                                         
*                                                                               
* PARAM1     A(GEND)                                                            
* PARAM2     A(CONTROL BLOCK) -MPTXCTLD                                         
*                                                                               
*                                                                               
         PRINT NOGEN                                                            
TABEX    CSECT                                                                  
         NMOD1 WORKL,**TABX**,RR=R2                                             
         SPACE 2                                                                
         USING WORKD,RC                                                         
         L     R8,0(R1)            A(GEND)                                      
         USING GEND,R8                                                          
         L     RA,4(R1)            A(CONTROL DSECT)                             
         USING TXCTLD,RA                                                        
         ST    R2,TXRELO                                                        
         L     R7,=A(GENSUBS)      GENERAL SUBROUTINES                          
         A     R7,TXRELO                                                        
         USING GENSUBS,R7          ARE ADDRESSED VIA R7                         
         ST    RC,TXAWRK           SAVE A(WORK AREA)                            
         LA    R9,IO               POINT R9 TO SYSTEM WORK AREA                 
         AH    R9,=Y(IOLEN)                                                     
         USING SYSD,R9                                                          
*                                                                               
         OC    TXCORE,TXCORE          TEST NEED TO GET CORE                     
         BNZ   RR16                                                             
*                                                                               
         L     RE,=A(150*1024)           150K SHOULD DO IT                      
         ST    RE,DMCB+4                 MINIMUM                                
         ST    RE,DMCB+8                 MAXIMUM                                
         GOTO1 TXCOVAIL,DMCB,C'GET'                                             
         L     R1,DMCB+4                                                        
         LTR   R1,R1                                                            
         BNZ   *+6                                                              
         DC    H'0'                                                             
         MVC   0(8,R1),=C'*TXCORE*'                                             
         LA    R1,8(R1)                                                         
         ST    R1,TXCORE               SET START OF TABEX CORE                  
         L     RF,DMCB+8               AMOUNT OBTAINED                          
         SH    RF,=H'16'                                                        
         ST    RF,TXCORLEN                                                      
         AR    RF,R1                                                            
         ST    RF,TXCOREX              SET END                                  
         MVC   0(8,RF),=C'*TXCOTX*'                                             
*                                                                               
*                                  SET DUMP END TO INCLUDE NEW CORE             
         CLI   TXDMPOPT,C'F'       IF FULL DUMP REQ'D                           
         BNE   RR16                                                             
         L     R2,ATWA                                                          
         L     R2,TWAMASTC-T510FFD(R2)   A(MASTC)                               
         USING MASTD,R2                                                         
         L     RF,TXCOREX                                                       
         LA    RF,8(RF)                                                         
         ST    RF,MCDMPEND         SET NEW DUMP END                             
         DROP  R2                                                               
*                                                                               
*                                  FOR EACH REQUEST                             
*                                  ----------------                             
RR16     DS    0H                                                               
         GOTO1 =A(CORMAP),RR=TXRELO  SET CORE MAP                               
         GOTO1 =A(SETROWS),RR=TXRELO SET ROW TABLE                              
         GOTO1 =A(SETCOLS),RR=TXRELO SET COLUMN TABLE                           
*                                  CLEAR MATRIX AREA                            
         L     R1,TXCOLS           NO OF COLS                                   
         M     R0,TXROWS           X NO OR ROWS                                 
         SLL   R1,3                X 8 = LENGTH TO CLEAR                        
         L     R0,TXMTRX                                                        
         SR    RE,RE                                                            
         SR    RF,RF                                                            
         MVCL  R0,RE                                                            
*                                                                               
         L     R4,WVPARS+4           CLEAR WAVE STATUS                          
         ICM   R5,15,WVPARS+8        FOR BCT                                    
         BZ    RR19                                                             
         USING WVTABD,R4                                                        
*                                                                               
RR18     DS    0H                                                               
         NI    WVSTAT,X'3F'        CLEAR THIS TIME ACTIVE AND DONE              
         A     R4,WVPARS+12        ENTRY LENGTH                                 
         BCT   R5,RR18                                                          
         DROP  R4                                                               
*                                                                               
RR19     DS    0H                                                               
         XC    TXBASE,TXBASE                                                    
         L     R2,TXBASR           BASE RECORD                                  
         USING QTGREC,R2                                                        
         LTR   R2,R2                                                            
         BZ    *+10                                                             
         MVC   TXBASE,QTGKTRG      SET THIS BASE CODE                           
         DROP  R2                                                               
*                                                                               
         L     R6,VMPQBLK          SET FILTER LIST                              
         USING MPQBLKD,R6          NOTE- NONE SET ACTIVE                        
         LA    RF,FILTLST                                                       
         ST    RF,MPQBFLST                                                      
         XC    FILTLST,FILTLST                                                  
         MVC   FILTLST(4),BASBV    BASE VECTOR                                  
         LA    R0,MAXFILTS                                                      
         L     R1,TXFLTVS          FILTER VECTORS                               
*                                                                               
RR20     DS    0H                                                               
         LA    RF,4(RF)                                                         
         ST    R1,0(RF)            SET ADDRESS                                  
         A     R1,BVLEN                                                         
         BCT   R0,RR20                                                          
*                                                                               
         OI    0(RF),X'80'         SET EOL                                      
*                                                                               
         XC    NWAVES,NWAVES                                                    
         XC    BWVPOP,BWVPOP                                                    
*                                                                               
         CLI   SURWMRGE,C'Y'       FOR MERGED WAVE SURVEYS                      
         BNE   RR22                                                             
         MVC   THISWAV,=2X'FF'     SET NO WAVE                                  
         BAS   RE,PROCWAV          AND PROCESS                                  
         BNE   RR80                ERROR                                        
         B     RR30                DONE                                         
*                                                                               
RR22     DS    0H                                                               
         L     R4,TXWAVLST         ELSE DO EACH WAVE                            
*                                                                               
RR22D    DS    0H                                                               
         CLI   0(R4),0             EOL                                          
         BE    RR22F                                                            
         MVC   THISWAV,0(R4)                                                    
         BAS   RE,PROCWAV                                                       
         BNE   RR80                ERROR                                        
         LA    R4,2(R4)                                                         
         B     RR22D                                                            
*                                                                               
RR22F    DS    0H                                                               
RR30     DS    0H                    AFTER ALL WAVES PROCESSED                  
         GOTO1 =A(ARSET),RR=TXRELO   SET ARRAY DATA                             
*                                                                               
RR80     DS    0H                                                               
         L     R6,VMPQBLK                                                       
         USING MPQBLKD,R6                                                       
*                                                                               
         CLI   TXERROR,0                                                        
         BE    RR84                                                             
*                                                                               
         CLC   TXERROR,MPQBERR     MESSAGE WAS SET BY MPQFAC                    
         BE    RR84                                                             
*                                  SET ERROR MESSAGE                            
         L     R2,=A(ERRMSGS)                                                   
         A     R2,TXRELO                                                        
*                                                                               
RR82     DS    0H                                                               
         CLI   0(R2),0                                                          
         BNE   *+6                                                              
         DC    H'0'                BAD ERROR CODE                               
         CLC   TXERROR,0(R2)                                                    
         BE    RR82D                                                            
         LA    R2,L'TXERRMSG+1(R2)                                              
         B     RR82                                                             
*                                                                               
RR82D    DS    0H                                                               
         MVC   TXERRMSG,1(R2)                                                   
*                                                                               
RR84     DS    0H                                                               
         CLI   TXTRCOPT,C'N'       TEST TO PRINT TRACE                          
         BE    RR86                                                             
         CLI   TXTRCOPT,C' '                                                    
         BNH   RR86                                                             
         GOTO1 =A(TXTRCE),RR=TXRELO                                             
*                                                                               
RR86     DS    0H                                                               
XIT      XIT1                                                                   
         EJECT                                                                  
*        PROCWAV- PROCESS A WAVE                                                
         SPACE 2                                                                
PROCWAV  NTR1                                                                   
         L     R6,VMPQBLK                                                       
         USING MPQBLKD,R6                                                       
         XC    MPQBWVDT,MPQBWVDT   SET WAVE DATE                                
         CLI   THISWAV,X'FF'       NONE IF MERGED WAVE                          
         BE    PW3M                                                             
*                                                                               
         LA    R4,SURWAVS          LOOK FOR WAVE INFO                           
         USING SURWVD,R4                                                        
*                                                                               
PW3      DS    0H                                                               
         CLI   0(R4),X'FF'                                                      
         BNE   *+12                                                             
         MVI   TXERROR,TXWAVERR    WAVE NOT DEFINED                             
         B     PWX                                                              
*                                                                               
         CLC   THISWAV,SURWVDT                                                  
         BE    PW3D                                                             
         LA    R4,SURWVDL(R4)      NEXT ENTRY                                   
         B     PW3                                                              
*                                                                               
PW3D     DS    0H                                                               
         MVC   MPQBWVDT,THISWAV    SET WAVE INFO                                
         MVC   MPQBQVD,SURWVDN     DIR NAME                                     
         MVC   MPQBQVF,SURWVFN     FILE NAME                                    
         CLI   SURWVSOV,C' '       TEST SURVEY CODE OVERRIDE                    
         BNH   *+10                                                             
         MVC   MPQBFCD,SURWVSOV                                                 
         MVC   MPQBNRES,SURWVRC    RESP COUNT                                   
         MVC   POPDFW,SURWVPOP     SET POP FOR DEFAULT WEIGHTS                  
*                                                                               
PW3M     DS    0H                  FIND OR ADD WAVE TAB ENTRY                   
         XC    X,X                                                              
         LA    R4,X                                                             
         USING WVTABD,R4                                                        
         MVC   WVDATE,THISWAV                                                   
         GOTO1 BINSRCH,WVPARS,(1,WVDATE)                                        
         OC    1(3,R1),1(R1)       TEST FULL                                    
         BNZ   *+6                                                              
         DC    H'0'                WAVE TABLE FULL                              
         L     R4,0(R1)            A(ENTRY)                                     
*                                                                               
         TM    WVSTAT,X'40'        TEST DONE THIS TIME ALREADY                  
         BNZ   PWX                                                              
         OI    WVSTAT,X'C0'        SET ACTIVE AND DONE                          
*                                                                               
         MVI   FILTLST,X'80'       DE-ACTIVATE AND SET EOL                      
*                                                                               
         CLI   MPQBNOWT,C'Y'       TEST NEED WEIGHTS                            
         BE    PW4                                                              
         MVC   MPQBWSAV,WSVAREA                                                 
         GOTO1 MPQFAC,DMCB,=C'SAVW',MPQBLKD                                     
         CLI   MPQBERR,0                                                        
         BNE   PWQERR                                                           
*                                                                               
PW4      DS    0H                                                               
         OC    WVSAM,WVSAM         TEST HAVE WAVE COUNTS                        
         BNZ   PW4D                                                             
*                                                                               
         CLC   WGTCOD,SURDFWGT     IF HAVE DEFAULT WEIGHT                       
         BNE   PW4C                                                             
         MVC   WVSAM,MPQBNRES      SET WAVE COUNTS DIRECTLY                     
         MVC   WVPOP,POPDFW                                                     
*                                                                               
         CLI   MPQBPPRE,0          BUT MAY HAVE TO ADJUST PRECISION             
         BE    PW4D                (POPDFW IS IN UNITS)                         
         MVC   BYTE,MPQBPPRE                                                    
         NI    BYTE,X'7F'          STRIP HOB                                    
         ZIC   RF,BYTE                                                          
         MVI   BYTE,C'D'           SET TO DIVIDE                                
         TM    MPQBPPRE,X'80'      TEST 'NEGATIVE' PREC                         
         BZ    *+8                                                              
         MVI   BYTE,C'M'           YES- MULTIPLY                                
*                                                                               
         SLL   RF,2                                                             
         L     RF,FCTRTAB(RF)      SET DIV/MULT FACTOR                          
         L     R1,POPDFW                                                        
         CLI   BYTE,C'M'                                                        
         BNE   *+10                                                             
         MR    R0,RF                                                            
         B     PW4B4                                                            
*                                                                               
         M     R0,=F'1'                                                         
         BAS   RE,DIV                                                           
*                                                                               
PW4B4    DS    0H                                                               
         STCM  R1,15,WVPOP                                                      
         B     PW4D                                                             
*                                                                               
PW4C     DS    0H                  ELSE MUST COUNT POP                          
         MVI   MPQBNOFL,C'Y'       SUPPRESS FILTERS                             
         MVI   MPQBNOTW,C'Y'       AND COMPONENT WGTING                         
         MVC   FULL(3),=C'ALL'     COUNT ALL RESPONDENTS                        
         MVI   FULL+3,X'FF'                                                     
         GOTO1 MPQFAC,DMCB,(X'90',=C'EXEC'),MPQBLKD,(4,FULL)                    
         CLI   MPQBERR,0                                                        
         BNE   PWQERR                                                           
*                                                                               
         MVI   MPQBNOTW,C'N'       RESET FOR COMP WGT                           
         MVI   MPQBNOFL,C'N'       RE-ACTIVATE FILTERS                          
         MVC   WVSAM(8),MPQBSAM    SET SAMPLE AND POP FOR WAVE                  
*                                                                               
PW4D     DS    0H                                                               
         MVC   TWVPOP,WVPOP        SAVE THIS WAVE POP                           
         OC    BWVPOP,BWVPOP       SAVE POP OF BASE WAVE                        
         BNZ   *+10                                                             
         MVC   BWVPOP,WVPOP                                                     
         L     RF,NWAVES           BUMP WAVE COUNT                              
         LA    RF,1(RF)                                                         
         CH    RF,=Y(WVBN*8)       MAX WAVES PER CALL                           
         BNH   *+6                                                              
         DC    H'0'                                                             
         ST    RF,NWAVES                                                        
*                                                                               
         MVC   WVBEQU,=X'8000'     SET 'FIRST WAVE'                             
         CLI   THISWAV,X'FF'       IF MERGED WAVE SURVEY                        
         BE    PW5                                                              
*                                                                               
         L     RF,TXWAVLST         ELSE- FIND THIS WAVE IN WAVE LIST            
         LR    R0,RF                                                            
         CLC   THISWAV,0(RF)       AND USE POSITION IN LIST                     
         BE    *+12                TO SET BIT MASK                              
         LA    RF,2(RF)            (NB- WAVES MAY BE PROCESSED                  
         B     *-14                     OUT OF ORDER)                           
         SR    RF,R0                                                            
         LA    RF,WVBITLST(RF)     BIT MASK                                     
         MVC   WVBEQU,0(RF)                                                     
*                                                                               
PW5      DS    0H                                                               
         ICM   R1,15,WVSAM         ADD TO TOTAL SURVEY SAMPLE                   
         A     R1,TXSURSAM                                                      
         ST    R1,TXSURSAM                                                      
         ICM   R1,15,WVPOP         AND POP                                      
         BAS   RE,EQUIV            EQUIVALENCE TO BASE WAVE                     
         A     R1,TXSURPOP                                                      
         ST    R1,TXSURPOP                                                      
*                                                                               
PW6      DS    0H                   PROCESS BASE RECORD                         
         OC    TXBASR,TXBASR        TEST HAVE BASE                              
         BNZ   PW7                                                              
         MVC   MPQBSAM(8),WVSAM     NO- USE SURVEY SAM AND POP                  
         B     PW7D                                                             
*                                                                               
PW7      DS    0H                                                               
         MVC   MPQBTGRC,TXBASR     A(BASE RECORD)                               
         MVC   MPQBTGBV,BASBV      FOR BASE BIT VECTOR                          
         MVC   MPQBTGWV,BASWV      FOR BASE COMP WGT VECTOR                     
*                                                                               
         GOTO1 MPQFAC,DMCB,=C'TARGV',MPQBLKD                                    
         CLI   MPQBERR,0                                                        
         BNE   PWQERR                                                           
*                                                                               
         MVI   FILTLST,X'40'       ACTIVATE AS FILTER AND CLEAR EOL             
*                                                                               
PW7D     DS    0H                                                               
         MVC   WVBASSAM(8),MPQBSAM    SAVE COUNTS                               
         L     R1,MPQBSAM          ADD TO BASE SAMPLE                           
         A     R1,TXBSAM                                                        
         ST    R1,TXBSAM                                                        
         L     R1,MPQBPOP          AND POP                                      
         BAS   RE,EQUIV            EQUIV TO BASE WAVE                           
         A     R1,TXBPOP                                                        
         ST    R1,TXBPOP                                                        
*                                                                               
PW9      DS    0H                                                               
         GOTO1 =A(GETCOLS),RR=TXRELO     GET COLUMN VECTORS AND SAVE            
         BNE   PWX                                                              
         GOTO1 =A(GETROWS),RR=TXRELO     GET ROW VECTORS AND XMULT              
*                                                                               
PWX      DS    0H                                                               
         CLI   TXERROR,0           SET CC                                       
         XIT1                                                                   
         SPACE 1                                                                
PWQERR   MVC   TXERROR,MPQBERR     MPQFAC ERROR                                 
         MVC   TXERRMSG,MPQBEMSG                                                
         B     PWX                                                              
         SPACE 3                                                                
WVBITLST DC    X'80004000200010000800040002000100'   WAVES 1-8                  
         DC    X'00800040002000100008000400020001'   WAVES 9-16                 
*                                                                               
         DS    0F                                                               
FCTRTAB  DC    AL4(1,10,100,1000,10000,100000)                                  
         EJECT                                                                  
*        ARSET - SET DATA IN ARRAYS AND DIVIDE BY NO. OF WAVES                  
         SPACE 2                                                                
         DS    0F                                                               
ARSET    NMOD1 0,ARSET                                                          
         L     RC,TXAWRK           A(WORK AREA)                                 
*                                                                               
         CLC   NWAVES,=F'1'        IF MULTIPLE WAVES                            
         BNH   ARS3                                                             
         L     RF,NWAVES           DIVIDE POPS BY WAVE COUNT                    
         L     R1,TXSURPOP         SURVEY TOTAL                                 
         M     R0,=F'1'                                                         
         BAS   RE,DIV                                                           
         ST    R1,TXSURPOP                                                      
         L     R1,TXBPOP           BASE POP                                     
         M     R0,=F'1'                                                         
         BAS   RE,DIV                                                           
         ST    R1,TXBPOP                                                        
*                                                                               
ARS3     DS    0H                                                               
*                                                                               
ARS40    DS    0H                                                               
ARSX     DS    0H                                                               
         CLI   TXERROR,0           SET CC                                       
         XIT1                                                                   
         EJECT                                                                  
*        SETROWS - SET ROW TABLE                                                
*                                                                               
*        NOTE- EACH LINE MUST HAVE A SPEC ELEM, ANY ANNOTATION                  
*              ELEMS FOR LINES WITH NO SPEC ELEMS ARE IGNORED                   
*                                                                               
         SPACE 2                                                                
         DS    0F                                                               
SETROWS  NMOD1 0,SETROW                                                         
         L     RC,TXAWRK           A(WORK AREA)                                 
*                                                                               
         L     R3,TXROWTB          ROW DATA TABLE                               
         USING TXROWD,R3                                                        
         XC    TXROWS,TXROWS       ROW COUNT                                    
         L     R2,TXROWSR             A(ROWSET RECORD)                          
         LA    R2,QXRELS-QXRKEY(R2)   FIRST ELEM                                
         MVI   FELSW,C'Y'                                                       
         XC    SVLIN,SVLIN                                                      
*                                                                               
SR4      DS    0H                                                               
         MVI   ELCODE,X'51'                                                     
         BAS   RE,NEXTEL                                                        
         BNE   SR50                                                             
*                                                                               
         CLC   SVLIN,2(R2)         TEST SAME LINE                               
         BE    SR4                 YES, IGNORE                                  
*                                                                               
         MVC   SVLIN,2(R2)         START OF NEW LINE                            
         L     RF,TXROWS           BUMP ROW COUNT                               
         LA    RF,1(RF)                                                         
         ST    RF,TXROWS                                                        
         ST    R2,TXRSPEC          SET A(FIRST SPEC ELEM)                       
         MVC   TXRLIN,SVLIN        LINE NUMBER                                  
         XC    TXRWAVS,TXRWAVS                                                  
*                                                                               
         L     R2,TXROWSR          NOW LOOK FOR ANNOTATION ELEMS                
         LA    R2,QXRELS-QXRKEY(R2)                                             
         MVI   ELCODE,X'C2'                                                     
         LA    R4,TXROWLS          ANNOTATION LINES                             
         LA    R5,TXROWLN          MAX LINES                                    
         L     RF,ASPOOLD                                                       
         MVC   0(L'TXROWLS,R4),SPACES-SPOOLD(RF)                                
         LA    R4,L'TXROWLS(R4)                                                 
         BCT   R5,*-10                                                          
*                                                                               
         LA    R4,TXROWLS          ANNOTATION LINES                             
         LA    R5,TXROWLN          MAX LINES                                    
*                                                                               
SR8      DS    0H                                                               
         BAS   RE,NEXTEL                                                        
         BNE   SR10                                                             
         CLC   SVLIN,2(R2)        MUST MATCH LINE NUMBER                        
         BNE   SR8                                                              
*                                                                               
         ZIC   RF,1(R2)                                                         
         SH    RF,=H'7'                                                         
         BM    SR8                                                              
         CH    RF,=Y(L'TXROWLS-1)  MAX LENGTH                                   
         BNH   *+8                                                              
         LA    RF,L'TXROWLS-1                                                   
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R4),6(R2)                                                    
         LA    R4,L'TXROWLS(R4)    NEXT ROW                                     
         BCT   R5,SR8                                                           
*                                                                               
SR10     DS    0H                                                               
         LA    R0,TXROWLS          TEST ANY ANNO                                
         CR    R4,R0                                                            
         BNE   SR40                YES, GET NEXT LINE                           
*                                                                               
*              ***LOOK FOR QSPEC, TARGET ANNO***'                               
*                                                                               
SR40     DS    0H                                                               
         L     R2,TXRSPEC          RESTORE R2 TO SPEC ELEM                      
         LA    R3,TXROWDL(R3)      NEXT TABLE ENTRY                             
         B     SR4                                                              
*                                                                               
SR50     DS    0H                                                               
         OC    TXROWS,TXROWS                                                    
         BNZ   SETROWX                                                          
         MVI   TXERROR,TXNRWERR    N0 ROWS                                      
*                                                                               
SETROWX  DS    0H                                                               
         XIT1                                                                   
         DROP  R3                                                               
         SPACE 2                                                                
         LTORG                                                                  
         EJECT                                                                  
*        SETCOLS - SET COLUMN TABLE                                             
         SPACE 2                                                                
         DS    0F                                                               
SETCOLS  NMOD1 0,SETCOL                                                         
         L     RC,TXAWRK           A(WORK AREA)                                 
*                                                                               
         L     R3,TXCOLTB          COLUMN DATA TABLE                            
         USING TXCOLD,R3                                                        
         XC    TXCOLS,TXCOLS       COL COUNT                                    
         L     R2,TXCOLSR             A(COLSET RECORD)                          
         LA    R2,QXCELS-QXCKEY(R2)   FIRST ELEM                                
         MVI   FELSW,C'Y'                                                       
         XC    SVLIN,SVLIN                                                      
*                                                                               
SC4      DS    0H                                                               
         MVI   ELCODE,X'51'                                                     
         BAS   RE,NEXTEL                                                        
         BNE   SC50                                                             
*                                                                               
         CLC   SVLIN,2(R2)         TEST SAME LINE                               
         BE    SC4                 YES, IGNORE                                  
*                                                                               
         MVC   SVLIN,2(R2)         START OF NEW LINE                            
         L     RF,TXCOLS           BUMP COL COUNT                               
         LA    RF,1(RF)                                                         
         ST    RF,TXCOLS                                                        
         ST    R2,TXCSPEC          SET A(FIRST SPEC ELEM)                       
         MVC   TXCLIN,SVLIN        LINE NUMBER                                  
         XC    TXCWAVS,TXCWAVS                                                  
*                                                                               
         L     R2,TXCOLSR          NOW LOOK FOR ANNOTATION ELEMS                
         LA    R2,QXCELS-QXCKEY(R2)                                             
         MVI   ELCODE,X'C2'                                                     
         LA    R4,TXCCHDS          COLUMN HEADS                                 
         LA    R5,TXCCHDN          MAX                                          
         L     RF,ASPOOLD                                                       
         MVC   0(L'TXCCHDS,R4),SPACES-SPOOLD(RF)                                
         LA    R4,L'TXCCHDS(R4)                                                 
         BCT   R5,*-10                                                          
*                                                                               
         LA    R4,TXCCHDS          COLUMN HEADS                                 
         LA    R5,TXCCHDN          MAX                                          
*                                                                               
SC8      DS    0H                                                               
         BAS   RE,NEXTEL                                                        
         BNE   SC10                                                             
         USING QXCCHDEL,R2                                                      
         CLC   SVLIN,QXCCHLIN      MUST MATCH LINE NUMBER                       
         BNE   SC8                                                              
         MVC   0(L'TXCCHDS,R4),QXCCHD                                           
         LA    R4,L'TXCCHDS(R4)    NEXT COLUMN                                  
         BCT   R5,SC8                                                           
*                                                                               
SC10     DS    0H                                                               
         LA    R0,TXCCHDS          TEST ANY ANNO                                
         CR    R4,R0                                                            
         BNE   SC40                YES, GET NEXT LINE                           
*                                                                               
*              ***LOOK FOR QSPEC, TARGET ANNO***'                               
*                                                                               
SC40     DS    0H                                                               
         L     R2,TXCSPEC          RESTORE R2 TO SPEC ELEM                      
         LA    R3,TXCOLDL(R3)      NEXT TABLE ENTRY                             
         B     SC4                                                              
*                                                                               
SC50     DS    0H                                                               
         OC    TXCOLS,TXCOLS                                                    
         BNZ   SETCOLX                                                          
         MVI   TXERROR,TXNCLERR    N0 COLUMNS                                   
*                                                                               
SETCOLX  DS    0H                                                               
         XIT1                                                                   
         DROP  R3                                                               
         SPACE 2                                                                
         LTORG                                                                  
         EJECT                                                                  
*        GETCOLS - GET COLUMN VECTORS AND SAVE                                  
         SPACE 2                                                                
         DS    0F                                                               
GETCOLS  NMOD1 0,GETCOL                                                         
         L     RC,TXAWRK           A(WORK AREA)                                 
         L     R6,VMPQBLK                                                       
         USING MPQBLKD,R6                                                       
*                                                                               
         L     R2,TXCOLTB          COLUMN TABLE                                 
         USING TXCOLD,R2                                                        
         L     R3,TXCOLS           NO OF COLUMNS                                
         L     R4,TXCOLVS          SAVE AREA FOR COLUMN VECTORS                 
*                                  PASS MPQFAC A(FIRST ELEM) FOR LINE           
GC6      DS    0H                                                               
         GOTO1 MPQFAC,DMCB,(X'80',=C'EXEC'),MPQBLKD,TXCSPEC                     
         CLI   MPQBERR,0                                                        
         BNE   GCERR                                                            
*                                  MOVE TO COLUMN VECTOR TABLE                  
         LR    R0,R4               TO ADDRESS                                   
         L     R1,BVLEN            TO LEGNTH                                    
         L     RE,MPQBSTK          FROM ADDRESS                                 
         LR    RF,R1               FROM LENGTH = TO LENGTH                      
         MVCL  R0,RE                                                            
*                                                                               
         A     R4,BVLEN                                                         
         LA    R2,TXCOLDL(R2)                                                   
         BCT   R3,GC6                                                           
         CR    RD,RD               SET CC =                                     
         B     GETCOLX                                                          
*                                                                               
GCERR    DS    0H                                                               
         ST    R2,TXCERR           SET ADDRESS OF COL IN ERROR                  
         LTR   RD,RD               SET CC NOT =                                 
*                                                                               
GETCOLX  DS    0H                                                               
         XIT1                                                                   
         DROP  R6                                                               
         DROP  R2                                                               
         LTORG                                                                  
         EJECT                                                                  
*        GETROWS - GET ROW VECTORS AND XMULT                                    
         SPACE 2                                                                
         DS    0F                                                               
GETROWS  NMOD1 0,GETROW                                                         
         L     RC,TXAWRK           A(WORK AREA)                                 
         L     R6,VMPQBLK                                                       
         USING MPQBLKD,R6                                                       
*                                                                               
         L     R2,TXROWTB          ROW TABLE                                    
         USING TXROWD,R2                                                        
         L     R3,TXROWS           NO OF ROWS                                   
         L     R5,TXMTRX           START OF MATRIX                              
*                                  PASS MPQFAC A(FIRST ELEM) FOR LINE           
GR6      DS    0H                                                               
         GOTO1 MPQFAC,DMCB,(X'80',=C'EXEC'),MPQBLKD,TXRSPEC                     
         CLI   MPQBERR,0                                                        
         BNE   GRERR                                                            
*                                  SAVE ROW VECTOR IN WORK VECTOR 1             
         L     R0,BVW1             TO ADDRESS                                   
         L     R1,BVLEN            TO LENGTH                                    
         L     RE,MPQBSTK          FROM ADDRESS                                 
         LR    RF,R1               FROM LENGTH                                  
         MVCL  R0,RE                                                            
*                                  NOW XMULT WITH EACH COL                      
         L     R4,TXCOLVS          FIRST SAVED COL VECTOR                       
         L     R0,TXCOLS           NUMBER OF COLUMNS                            
*                                                                               
GR8      DS    0H                                                               
         GOTO1 MPQFAC,DMCB,=C'XMULT',MPQBLKD,(C'B',(R4)),              X        
               (C'B',BVW1)                                                      
         CLI   MPQBERR,0                                                        
         BNE   GRERR                                                            
*                                                                               
         L     RF,0(R5)            ADD INTO MATRIX                              
         A     RF,MPQBSAM          SAMPLE                                       
         ST    RF,0(R5)                                                         
         L     RF,4(R5)            POP                                          
         A     RF,MPQBPOP                                                       
         ST    RF,4(R5)                                                         
*                                                                               
         LA    R5,8(R5)                                                         
         A     R4,BVLEN            NEXT COLUMN                                  
         BCT   R0,GR8                                                           
*                                                                               
         LA    R2,TXROWDL(R2)      NEXT ROW                                     
         BCT   R3,GR6                                                           
*                                                                               
         CR    RD,RD               SET CC =                                     
         B     GETROWX                                                          
*                                                                               
GRERR    DS    0H                                                               
         ST    R2,TXRERR           SET ADDRESS OF ROW IN ERROR                  
         LTR   RD,RD               SET CC NOT =                                 
*                                                                               
GETROWX  DS    0H                                                               
         XIT1                                                                   
         DROP  R6                                                               
         DROP  R2                                                               
         LTORG                                                                  
         EJECT                                                                  
*        CORMAP - SET CORE MAP                                                  
         SPACE 2                                                                
         DS    0F                                                               
CORMAP   NMOD1 0,CORMAP                                                         
         L     RC,TXAWRK           A(WORK AREA)                                 
         L     R6,VMPQBLK                                                       
         USING MPQBLKD,R6                                                       
*                                                                               
         XC    ADDRS(ADDRSL),ADDRS                                              
*                                                                               
*              RESP COUNT AND BIT VECTOR LENGTH                                 
*              --------------------------------                                 
*                                                                               
         MVC   NRES,MPQBNRES       SET RESP COUNT DIRECTLY                      
         CLI   SURWMRGE,C'Y'       IF WAVES MERGED                              
         BE    CORM5                                                            
*                                  ELSE SET FROM LARGEST WAVE                   
         LA    R5,SURWAVS                                                       
         USING SURWVD,R5                                                        
*                                                                               
CORM3    DS    0H                                                               
         CLI   0(R5),X'FF'         END OF SURVEY WAVE LIST                      
         BE    CORM5                                                            
*                                                                               
         CLC   NRES,SURWVRC        TEST ALREADY HAVE LARGER                     
         BNL   *+10                                                             
         MVC   NRES,SURWVRC                                                     
*                                                                               
         LA    R5,SURWVDL(R5)                                                   
         B     CORM3                                                            
*                                                                               
CORM5    DS    0H                                                               
         L     RF,NRES             SET BIT VECTOR LENGTH                        
         LA    RF,7(RF)                                                         
         SRL   RF,3                                                             
         ST    RF,BVLEN                                                         
*                                                                               
         DROP  R5                                                               
         EJECT                                                                  
*                                                                               
*        SET VARIOUS ADDRESSES                                                  
*        ---------------------                                                  
*                                                                               
         L     R3,TXCORE           R3 FOR CORE POINTER                          
*                                                                               
         ST    R3,WVTAB            WAVE TABLE                                   
         LA    R3,WVTABLN(R3)                                                   
*                                                                               
         LA    R3,7(R3)            ALIGN FOR WGT AREA                           
         SRL   R3,3                                                             
         SLL   R3,3                                                             
         ST    R3,WSVAREA          WGT SAVE AREA                                
         ZIC   R1,MPQBWDLN         WGT ITEM LENGTH                              
         M     R0,NRES             X NRES                                       
         AR    R3,R1                                                            
*                                                                               
         LA    R3,7(R3)            ALIGN FOR BASE WGT AREA                      
         SRL   R3,3                                                             
         SLL   R3,3                                                             
         ST    R3,BASWV            TARG COMPONENT WGT AREA                      
         LA    R1,2                VECTOR ITEM LENGTH **(ASSUME 2)              
         M     R0,NRES                                                          
         AR    R3,R1                                                            
*                                                                               
         ST    R3,BASBV            BASE BIT VECTOR                              
         A     R3,BVLEN                                                         
*                                                                               
         ST    R3,BVW1             TWO WORK BIT VECTORS                         
         A     R3,BVLEN                                                         
         ST    R3,BVW2                                                          
         A     R3,BVLEN                                                         
*                                                                               
         ST    R3,TXROWTB          ROW TABLE                                    
         A     R3,=A(MAXROWS*TXROWDL)                                           
*                                                                               
         ST    R3,TXCOLTB          COLUMN TABLE                                 
         A     R3,=A(MAXCOLS*TXCOLDL)                                           
*                                                                               
         ST    R3,TXCOLVS          COLUMN VECTORS                               
         LA    R1,MAXCOLS                                                       
         M     R0,BVLEN                                                         
         AR    R3,R1                                                            
*                                                                               
         ST    R3,TXFLTVS          FILTER VECTORS                               
         LA    R1,MAXFILTS                                                      
         M     R0,BVLEN                                                         
         AR    R3,R1                                                            
*                                                                               
         LA    R3,7(R3)            ALIGN FOR MATRIX                             
         SRL   R3,R3                                                            
         SLL   R3,R3                                                            
         MVC   0(8,R3),=C'*MATRX*'                                              
         LA    R3,8(R3)                                                         
         ST    R3,TXMTRX           MATRIX                                       
*                                                                               
*                                  SET WVTAB BINSRCH PARS                       
         SR    R0,R0                                                            
         L     R1,WVTAB                                                         
         SR    R2,R2                                                            
         LA    R3,WVTABDL                                                       
         LA    R4,2                KEY LENGTH                                   
         LA    R5,MAXWAVS                                                       
         STM   R0,R5,WVPARS                                                     
*                                                                               
CORMX    XIT1                                                                   
         LTORG                                                                  
         EJECT                                                                  
*        TXTRCE- 'TRACE' - PRINT TABLES AND ARRAYS                              
         SPACE 2                                                                
         DS    0F                                                               
TXTRCE   NMOD1 0,TXTRCE                                                         
         L     RC,TXAWRK           A(WORK AREA)                                 
         L     R6,ASPOOLD                                                       
         USING SPOOLD,R6                                                        
*                                                                               
         MVI   SPACING,2                                                        
         GOTO1 SPOOL,DMCB,SPOOLD                                                
*                                                                               
         L     R4,=A(TOTWDS)                                                    
         A     R4,TXRELO                                                        
         LA    R5,TXSURSAM                                                      
         LA    R3,4                                                             
*                                                                               
TXTR08   DS    0H                                                               
         MVC   P(20),0(R4)                                                      
         EDIT  (B4,0(R5)),(10,P+21),COMMAS=YES,ZERO=NOBLANK                     
         GOTO1 SPOOL,DMCB,SPOOLD                                                
         LA    R4,20(R4)                                                        
         LA    R5,4(R5)                                                         
         BCT   R3,TXTR08                                                        
*                                                                               
         L     R4,WVPARS+4         WAVE TAB                                     
         USING WVTABD,R4                                                        
         ICM   R5,15,WVPARS+8                                                   
         BZ    TXTR2G                                                           
         GOTO1 SPOOL,DMCB,SPOOLD                                                
         MVC   P(5),=C'WVTAB'                                                   
         MVC   P2+10(47),=C'STAT    SAMPLE        POP   BASE SAM   BASEX        
                POP'                                                            
         MVI   SPACING,2                                                        
         GOTO1 SPOOL,DMCB,SPOOLD                                                
*                                                                               
TXTR2    DS    0H                                                               
         MVC   P(3),=C'ALL'                                                     
         CLI   WVDATE,X'FF'                                                     
         BE    TXTR2D                                                           
         GOTO1 DATCON,DMCB,(2,WVDATE),(8,P)                                     
TXTR2D   DS    0H                                                               
         GOTO1 HEXOUT,DMCB,WVSTAT,P+10,1,=C'N'                                  
         LA    R3,P+14                                                          
         LA    RE,WVSAM                                                         
         LA    R2,4                                                             
*                                                                               
TXTR2E   DS    0H                                                               
         EDIT  (B4,0(RE)),(10,0(R3)),ZERO=NOBLANK                               
         LA    R3,11(R3)                                                        
         LA    RE,4(RE)                                                         
         BCT   R2,TXTR2E                                                        
*                                                                               
         GOTO1 SPOOL,DMCB,SPOOLD                                                
*                                                                               
         A     R4,WVPARS+12        NEXT ENTRY                                   
         BCT   R5,TXTR2                                                         
*                                                                               
TXTR2G   DS    0H                                                               
TR10     DS    0H                                                               
         GOTO1 SPOOL,DMCB,SPOOLD                                                
*                                                                               
         ICM   R3,15,TXROWS                                                     
         BNP   TR20                                                             
         MVC   P(19),=C'**ROW DEFINITIONS**'                                    
         MVI   SPACING,2                                                        
         GOTO1 SPOOL,DMCB,SPOOLD                                                
*                                                                               
         XC    FULL,FULL                                                        
         L     R5,TXROWTB                                                       
         USING TXROWD,R5                                                        
*                                                                               
TR12     DS    0H                                                               
         L     RF,FULL                                                          
         LA    RF,1(RF)                                                         
         ST    RF,FULL                                                          
         EDIT  (B4,FULL),(3,P+0)                                                
         EDIT  (B2,TXRLIN),(3,P+4)  LINE NUMBER                                 
         GOTO1 HEXOUT,DMCB,TXRWAVS,P+8,2,=C'N'                                  
*                                                                               
         ICM   R2,15,TXRSPEC       SPECS                                        
         BP    *+6                                                              
         DC    H'0'                                                             
         MVC   SVLIN,2(R2)         SAVE LINE                                    
         LA    R4,P+13                                                          
         LA    R3,4                ONLY 4 LINES                                 
*                                                                               
TR14     DS    0H                                                               
         CLI   0(R2),0             EOR                                          
         BE    TR16                                                             
         CLI   0(R2),X'51'                                                      
         BE    TR15                                                             
*                                                                               
TR14D    DS    0H                                                               
         ZIC   R0,1(R2)            NEXT ELEM                                    
         AR    R2,R0                                                            
         B     TR14                                                             
*                                                                               
TR15     DS    0H                                                               
         CLC   SVLIN,2(R2)         TEST SAME LINE                               
         BNE   TR14D                                                            
*                                                                               
         GOTO1 HEXOUT,DMCB,(R2),(R4),6,=C'N'                                    
         ZIC   RF,1(R2)                                                         
         SH    RF,=H'7'                                                         
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   13(0,R4),6(R2)      SPEC                                         
         LA    R4,132(R4)          NEXT LINE                                    
         BCT   R3,TR14D            NEXT ELEM                                    
*                                                                               
TR16     DS    0H                                                               
         MVC   P1+77(30),TXROWLS+000                                            
         MVC   P2+77(30),TXROWLS+030                                            
         MVC   P3+77(30),TXROWLS+060                                            
         MVC   P4+77(30),TXROWLS+090                                            
*                                                                               
TR18     DS    0H                                                               
         GOTO1 SPOOL,DMCB,SPOOLD                                                
         LA    R5,TXROWDL(R5)      NEXT ROW                                     
         CLC   FULL,TXROWS         TEST AT END                                  
         BL    TR12                                                             
*                                                                               
TR20     DS    0H                  COLUMNS                                      
         GOTO1 SPOOL,DMCB,SPOOLD                                                
         ICM   R3,15,TXCOLS                                                     
         BNP   TR30                                                             
         MVC   P(22),=C'**COLUMN DEFINITIONS**'                                 
         MVI   SPACING,2                                                        
         GOTO1 SPOOL,DMCB,SPOOLD                                                
*                                                                               
         XC    FULL,FULL                                                        
         L     R5,TXCOLTB                                                       
         USING TXCOLD,R5                                                        
*                                                                               
TR22     DS    0H                                                               
         L     RF,FULL                                                          
         LA    RF,1(RF)                                                         
         ST    RF,FULL                                                          
         EDIT  (B4,FULL),(3,P+0)                                                
         EDIT  (B2,TXCLIN),(3,P+4)  LINE NUMBER                                 
         GOTO1 HEXOUT,DMCB,TXCWAVS,P+8,2,=C'N'                                  
*                                                                               
         ICM   R2,15,TXCSPEC       SPECS                                        
         BP    *+6                                                              
         DC    H'0'                                                             
         MVC   SVLIN,2(R2)         SAVE LINE                                    
         LA    R4,P+13                                                          
         LA    R3,4                ONLY 4 LINES                                 
*                                                                               
TR24     DS    0H                                                               
         CLI   0(R2),0             EOR                                          
         BE    TR26                                                             
         CLI   0(R2),X'51'                                                      
         BE    TR25                                                             
*                                                                               
TR24D    DS    0H                                                               
         ZIC   R0,1(R2)            NEXT ELEM                                    
         AR    R2,R0                                                            
         B     TR24                                                             
*                                                                               
TR25     DS    0H                                                               
         CLC   SVLIN,2(R2)         TEST SAME LINE                               
         BNE   TR24D                                                            
*                                                                               
         GOTO1 HEXOUT,DMCB,(R2),(R4),6,=C'N'                                    
         ZIC   RF,1(R2)                                                         
         SH    RF,=H'7'                                                         
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   13(0,R4),6(R2)      SPEC                                         
         LA    R4,132(R4)          NEXT LINE                                    
         BCT   R3,TR24D            NEXT ELEM                                    
*                                                                               
TR26     DS    0H                                                               
         MVC   P1+077(10),TXCCHDS+000                                           
         MVC   P1+088(10),TXCCHDS+010                                           
         MVC   P1+099(10),TXCCHDS+020                                           
         MVC   P1+110(10),TXCCHDS+030                                           
         MVC   P1+122(10),TXCCHDS+040                                           
         MVC   P2+077(10),TXCCHDS+050                                           
         MVC   P2+088(10),TXCCHDS+060                                           
         MVC   P2+099(10),TXCCHDS+070                                           
*                                                                               
         GOTO1 SPOOL,DMCB,SPOOLD                                                
         LA    R5,TXCOLDL(R5)      NEXT COL                                     
         CLC   FULL,TXCOLS         TEST AT END                                  
         BL    TR22                                                             
*                                                                               
TR30     DS    0H                  MATRIX                                       
         OC    TXROWS,TXROWS                                                    
         BZ    TR50                                                             
         OC    TXCOLS,TXCOLS                                                    
         BZ    TR50                                                             
*                                                                               
         MVI   BYTE,C'S'           SAMPLES                                      
         LA    R2,1                STARTING COLUMN                              
*                                                                               
TR32     DS    0H                                                               
         BAS   RE,MTXPRT                                                        
         LA    R2,11(R2)           NEXT SET                                     
         C     R2,TXCOLS           TEST DONE                                    
         BNH   TR32                                                             
*                                                                               
         MVI   BYTE,C'P'           POP                                          
         LA    R2,1                STARTING COLUMN                              
*                                                                               
TR33     DS    0H                                                               
         BAS   RE,MTXPRT                                                        
         LA    R2,11(R2)           NEXT SET                                     
         C     R2,TXCOLS           TEST DONE                                    
         BNH   TR33                                                             
*                                                                               
TR50     DS    0H                                                               
TR70     DS    0H                                                               
         CLI   TXERROR,0                                                        
         BE    TXTR83                                                           
         MVC   P(8),=C'**ERROR='                                                
         EDIT  (B1,TXERROR),(3,P+10)                                            
         MVC   P+15(40),TXERRMSG                                                
         GOTO1 SPOOL,DMCB,SPOOLD                                                
*                                                                               
TXTR83   DS    0H                                                               
*                                                                               
TXTRCX   DS    0H                                                               
         MVI   LINE,99                                                          
         XIT1                                                                   
         SPACE 3                                                                
*        MTXPRT - PRINT PART OF XTAB MATRIX                                     
*                                                                               
MTXPRT   NTR1                                                                   
         GOTO1 SPOOL,DMCB,SPOOLD                                                
         ST    R2,FIRSTCOL         STARTING COLUMN NUMBER                       
*                                                                               
         MVC   P(19),=C'** SAMPLE COUNTS **'                                    
         CLI   BYTE,C'S'                                                        
         BE    *+10                                                             
         MVC   P(23),=C'** POPULATION COUNTS **'                                
         MVC   P+30(17),=C'COLUMN XX THRU XX'                                   
         EDIT  (R2),(2,P+37),FILL=0                                             
         LA    RF,10(R2)                                                        
         C     RF,TXCOLS                                                        
         BNH   *+8                                                              
         L     RF,TXCOLS                                                        
         ST    RF,LASTCOL          LAST COLUMN NUMBER                           
         EDIT  (RF),(2,P+45),FILL=0                                             
         MVI   SPACING,2                                                        
         GOTO1 SPOOL,DMCB,SPOOLD                                                
*                                                                               
         L     RF,TXCOLS                                                        
         SLL   RF,3                X 8                                          
         ST    RF,ROWLEN           = ROW LENGTH                                 
         L     R3,TXMTRX           START OF FIRST ROW                           
         CLI   BYTE,C'P'           POPS                                         
         BNE   *+8                                                              
         LA    R3,4(R3)            ARE IN SECOND NUMBER                         
         LA    R5,1                ROW NUMBER                                   
*                                                                               
MTX6     DS    0H                                                               
         LR    R4,R2                                                            
         BCTR  R4,R0                                                            
         SLL   R4,3                                                             
         AR    R4,R3               FIRST COL TO SHOW                            
         MVI   P,C'R'                                                           
         EDIT  (R5),(3,P+1),FILL=0                                              
         LA    RE,P+6                                                           
*                                                                               
MTX7     DS    0H                                                               
         EDIT  (B4,0(R4)),(10,0(RE)),ZERO=NOBLANK                               
         LA    RE,11(RE)                                                        
         LA    R4,8(R4)                                                         
         LA    R2,1(R2)                                                         
         C     R2,LASTCOL                                                       
         BNH   MTX7                                                             
         GOTO1 SPOOL,DMCB,SPOOLD                                                
*                                                                               
         C     R5,TXROWS                                                        
         BNL   MTXPRTX                                                          
         LA    R5,1(R5)                                                         
         A     R3,ROWLEN           NEXT ROW                                     
         L     R2,FIRSTCOL                                                      
         B     MTX6                                                             
*                                                                               
MTXPRTX  DS    0H                                                               
         XIT1                                                                   
         LTORG                                                                  
         SPACE 3                                                                
         DS    0D                                                               
TOTWDS   DS    0X                                                               
         DC    CL20'SURVEY SAMPLE SIZE ='                                       
         DC    CL20'       POPULATION  ='                                       
         DC    CL20'BASE   SAMPLE SIZE ='                                       
         DC    CL20'       POPULATION  ='                                       
         SPACE 3                                                                
ERRMSGS  DS    0D                                                               
         DC    AL1(TXWAVERR)                                                    
         DC    CL40'WAVE NOT FOUND'                                             
         DC    AL1(TXSPCERR)                                                    
         DC    CL40'INVALID SPECIFICATION'                                      
         DC    AL1(TXNCWERR)                                                    
         DC    CL40'NO COMMON WAVE FOR CELL'                                    
         DC    AL1(TXNDWERR)                                                    
         DC    CL40'ROW/COL NOT DEFINED FOR ANY WAVE'                           
         DC    AL1(TXNRWERR)                                                    
         DC    CL40'NO ROWS DEFINED'                                            
         DC    AL1(TXNCLERR)                                                    
         DC    CL40'NO COLUMNS DEFINED'                                         
         DC    X'00'                                                            
         EJECT                                                                  
*                                                                               
GENSUBS  DS    0D                  **START OF GENERAL SUBROUTINES**             
*                                  --------------------------------             
         SPACE 2                                                                
*        COUNT BITS IN BYTE AT 0(R2)                                            
*        AND CUME COUNT IN RF                                                   
         SPACE 2                                                                
BITCOUNT DS    0H                                                               
         SR    R0,R0                                                            
         ICM   R0,1,0(R2)                                                       
         BZR   RE                                                               
         SR    R1,R1                                                            
*                                                                               
BITC4    DS    0H                                                               
         SRDL  R0,1                                                             
         LTR   R1,R1               TEST HOB ON                                  
         BNM   *+8                                                              
         LA    RF,1(RF)            YES- BUMP COUNT                              
         LTR   R0,R0               TEST ANY BITS LEFT ON                        
         BZR   RE                                                               
         B     BITC4                                                            
*                                                                               
         DROP  R5                                                               
         SPACE 2                                                                
         SPACE 3                                                                
BINSDEL  NTR1                      REMOVE ITEM FROM BINSRCH TABLE               
         L     R2,0(R1)            A(ITEM)                                      
         L     RF,8(R1)            LENGTH                                       
         L     R4,12(R1)           CURRENT COUNT                                
         LR    RE,R4                                                            
         BCTR  RE,R0                                                            
         ST    RE,12(R1)           SET NEW COUNT                                
*                                                                               
         MH    R4,10(R1)           COUNT X LENGTH                               
         A     R4,4(R1)            PLUS START = END OF TABLE                    
         BCTR  RF,R0                                                            
*                                                                               
BINSD4   DS    0H                                                               
         LA    RE,1(R2,RF)                                                      
         EX    RF,BDMVC                                                         
         LR    R2,RE                                                            
         CR    R2,R4                                                            
         BL    BINSD4                                                           
*                                                                               
         XIT1                                                                   
*                                                                               
BDMVC    MVC   0(0,R2),0(RE)                                                    
         SPACE 3                                                                
*                                  NB- DIV RETURNS VIA RE                       
DIV      DIV   (R0),(RF)                                                        
         SPACE 3                                                                
EQUIV    DS    0H                  EQUIVALENCE TO BASE WAVE                     
         CLC   BWVPOP,TWVPOP                                                    
         BER   RE                                                               
         M     R0,BWVPOP                                                        
         L     RF,TWVPOP                                                        
         B     DIV                                                              
         SPACE 3                                                                
NEXTEL   DS    0H                                                               
         CLI   FELSW,C'Y'          IF FIRST TIME                                
         MVI   FELSW,C'N'          SET OFF SW                                   
         BE    NEXTEL2             AND USE FIRST ELEM                           
NEXTEL1  ZIC   R0,1(R2)                                                         
         AR    R2,R0                                                            
NEXTEL2  DS    0H                                                               
         CLI   0(R2),0                                                          
         BNE   NEXTEL4                                                          
         LTR   RE,RE                                                            
         BR    RE                                                               
NEXTEL4  DS    0H                                                               
         CLC   0(1,R2),ELCODE                                                   
         BNE   NEXTEL1                                                          
         BR    RE                                                               
         SPACE 3                                                                
         LTORG                                                                  
         EJECT                                                                  
*        DSECT TO COVER WAVE TABLE                                              
         SPACE 2                                                                
WVTABD   DSECT                                                                  
WVDATE   DS    XL2                 WAVE DATE                                    
WVSTAT   DS    XL1                 STATUS  - X'80'= ACTIVE THIS REQ             
*                                            X'40'= ALREADY DONE                
         DS    XL1                 SPARE                                        
WVSAM    DS    XL4                 SURVEY SAMPLE                                
WVPOP    DS    XL4                 POPULATION                                   
WVBASSAM DS    XL4                 BASE SAMPLE                                  
WVBASPOP DS    XL4                 POPULATION                                   
WVTABDL  EQU   *-WVTABD                                                         
         SPACE 3                                                                
WVTABLN  EQU   (WVTABDL*MAXWAVS)+1 TOTAL LENGTH OF WAVE TABLE                   
*                                  (FOR REACH REPORT)                           
         SPACE 3                                                                
WORKD    DSECT                     TABEX WORK AREA DSECT                        
X        DS    XL20                                                             
X2       DS    XL20                                                             
WKSAM    DS    F                                                                
WKPOP    DS    F                                                                
WKSAM2   DS    F                                                                
WKPOP2   DS    F                                                                
ROWLEN   DS    F                                                                
LASTCOL  DS    F                                                                
FIRSTCOL DS    F                                                                
THISWAV  DS    XL2                 WAVE DATE                                    
FILTLST  DS    XL48                                                             
FELSW    DS    C                                                                
AVECT    DS    A                                                                
WVECT1   DS    XL4                                                              
WVECT2   DS    XL4                                                              
WVECT3   DS    XL4                                                              
WVECT4   DS    XL4                                                              
POPDFW   DS    F                                                                
TWVPOP   DS    F                                                                
BWVPOP   DS    F                                                                
NWAVES   DS    F                                                                
WVBEQU   DS    XL2                                                              
WVBN     EQU   L'WVBEQU            NO. OF BYTES                                 
SVLIN    DS    XL2                                                              
PCOUNT   DS    PL3                                                              
*                                                                               
*                                                                               
WORKL    EQU   *-WORKD                                                          
         EJECT                                                                  
*                                                                               
       ++INCLUDE MPTXCTLD                                                       
         SPACE 2                                                                
BINSRCH  EQU   TXBINSCH                                                         
         SPACE 2                                                                
TXCTLD   DSECT                                                                  
         ORG   TXWORK              REDEFINE TXWORK                              
         DS    A                   SPARE (ALIGN TXCORE WITH RXCORE)             
TXRELO   DS    A                                                                
TXCORE   DS    A                   A(START OF TABLE AREA)                       
TXCOREX  DS    A                   A(END)                                       
TXCORLEN DS    F                   CORE AREA LENGTH                             
TXAWRK   DS    A                                                                
TXCOLVS  DS    A                   A(FIRST COLUMN VECTOR)                       
TXFLTVS  DS    A                   A(FIRST FILTER VECTOR)                       
*                                                                               
TXBASE   DS    CL8                 THIS BASE                                    
*                                                                               
         DS    0F                                                               
WVPARS   DS    XL24                BINSRCH PARS FOR WAVE TAB                    
NRES     DS    F                   RESPONDENT COUNT                             
BVLEN    DS    F                   LENGTH OF BIT VECTOR                         
CALLNO   DS    XL1                 NUMBER OF READREX CALL                       
*                                  VARIOUS ADDRESSES                            
*                                  -----------------                            
ADDRS    DS    0F                                                               
WVTAB    DS    A                   WAVE TABLE AREA                              
WSVAREA  DS    A                   SAVE AREA FOR SURVEY WGTS                    
BASWV    DS    A                   BASE COMPONENET WEIGHT VECTOR                
BASBV    DS    A                   BASE BIT VECTOR                              
BVW1     DS    A                   WORK BIT VECTOR 1                            
BVW2     DS    A                   WORK BIT VECTOR 2                            
*                                                                               
ADDRSL   EQU   *-ADDRS                                                          
*                                                                               
         EJECT                                                                  
*                                                                               
         PRINT OFF                                                              
       ++INCLUDE DDSPOOLD                                                       
       ++INCLUDE MPRDRFFD                                                       
       ++INCLUDE DDGENTWA                                                       
       ++INCLUDE DDREPMASTD                                                     
       ++INCLUDE DDSPLWORKD                                                     
       ++INCLUDE MPQBLKD                                                        
       ++INCLUDE MPQERRQS                                                       
       ++INCLUDE DDNODBLKD                                                      
         PRINT ON                                                               
*                                                                               
       ++INCLUDE MPGENQS                                                        
       ++INCLUDE MPRDRWORKD                                                     
       ++INCLUDE MPEQUATES                                                      
         SPACE 2                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'052MPTABEXS  05/01/02'                                      
         END                                                                    
