*          DATA SET DDGRID     AT LEVEL 006 AS OF 10/01/10                      
*CATALP GRID                                                                    
                                                                                
*======================================================================         
* PARAMETERS ARE AS FOLLOWS                                                     
* P1  BYTE  0   0    = STANDARD CALL-PROCESS GRID DATA                          
*               C'C' = CLEAR GRID SCREEN                                        
*               C'E' = END GRID, NO MORE DATA                                   
*     BYTE 1-3  A(GRID FORMAT TABLE)                                            
*                                                                               
* P2            A(TWA) APPLICATION PROGRAMS TWA                                 
*                                                                               
* P3            A(W/S) APPLICATION PROGRAMS WORKING STORAGE)                    
*                                                                               
* P4            A(COMFACS)                                                      
*                                                                               
*======================================================================         
                                                                                
         TITLE 'DDGRID - ONLINE GRID OUTPUT ROUTINE'                            
         PRINT NOGEN                                                            
GRID     CSECT                                                                  
         NMOD1 WORKX,**GRID**,RA                                                
         USING WORKD,RC                  RC=A(WORKING STORAGE)                  
*                                                                               
         BRAS  RE,SETPARMS                                                      
         L     R9,APCD                                                          
         USING PCDRIVEN,R9                                                      
         L     R8,ADLCB                                                         
         USING DLCBD,R8                                                         
*                                                                               
         CLI   CALLTYPE,GRIDCLRQ         GRID SCREEN CLEAR                      
         BNE   GR008                                                            
         GOTOR DWNL,GCLR                                                        
         J     EXIT                                                             
*                                                                               
GR008    TM    PCGRIDS,PCGBEGQ           GRID ALREADY BEGUN?                    
         BNO   *+14                      . NO                                   
         OC    DLCBAPR,DLCBAPR           INIT?                                  
         BNZ   GR010                     . INIT ALREADY DONE                    
         GOTOR DWNL,GINI                                                        
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
GR010    CLI   CALLTYPE,GRIDENDQ         END OF REPORT                          
         BE    GR300                                                            
*                                                                               
         XC    RGRCID#,RGRCID#           RECURRING COLUMN COUNT                 
         OI    FLAG,FG1STQ               SPECIAL PROCESS FOR 1ST COL            
         L     R1,AGFT                   A(GRID FORMAT TABLE)                   
         LA    R4,GRFLNQ(R1)             FIRST COLUMN ENTRY                     
         USING GRIDCD,R4                                                        
         CLI   GRCID,GRLDQ               GRID DESCRIPTION LINE DATA?            
         BNE   GR020                                                            
         BRAS  RE,PGRDLDA                                                       
                                                                                
*----------------------------------------                                       
* PROCESS GRID COLUMNS                                                          
*----------------------------------------                                       
GR020    CLI   GRCID,X'FF'               END OF COLUMN TABLE?                   
         MVC   RGRCID,GRCID                                                     
         BE    GR200                                                            
         CLI   GRCID,GRLDQ               GRID DESCRIPTION LINE DATA?            
         BNE   GR022                                                            
         AHI   R4,GRLLNQ                 SKIP DESCRIPTION LINE DATA             
         B     GR020                                                            
                                                                                
*----------------------------------------                                       
* COLUMN SELECTION                                                              
*----------------------------------------                                       
GR022    TM    GRCCIN,GRCCDDS            DDS ONLY COLUMN?                       
         BZ    GR030                                                            
         TM    GRIDSTAT,GRFGDDS          DDS TERMINAL?                          
         BZ    GR182                                                            
*                                                                               
GR030    CLI   COLSEL,0                                                         
         BE    GR032                                                            
*                                      * NON-PRIMARY SELECTOR                   
         TM    GRCCIN,GRCCNPO            NON-PRIMARY ONLY COLUMN?               
         BO    GR050                     . YES, PROCESS                         
         TM    GRCCIN,GRCCPON            PRIMARY ONLY COLUMN?                   
         BO    GR182                     . YES, SKIP                            
         CLI   GRCCS,0                   PRIMARY COLUMN?                        
         BE    GR050                     . YES, PROCESS                         
         CLC   GRCCS,0(R1)               SPECIFIC COLUMN WANTED?                
         BE    GR050                     . YES, PROCESS                         
         B     GR182                                                            
*                                      * PRIMARY SELECTOR                       
GR032    TM    GRCCIN,GRCCPON            PRIMARY ONLY COLUMN?                   
         BO    GR050                     . YES, PROCESS                         
         TM    GRCCIN,GRCCNPO            NON-PRIMARY ONLY COLUMN?               
         BO    GR182                     . YES, SKIP                            
         CLI   GRCCS,0                   PRIMARY COLUMN?                        
         BNE   GR182                     . NO, SKIP                             
                                                                                
*----------------------------------------                                       
* CHECK FOR ROW CONTINUATION                                                    
*----------------------------------------                                       
GR050    TM    GRCCIN,GRCCRCC+GRCCVNM    RECURRING COLUMN?                      
         BNO   GR052                                                            
         TM    GRCDIN,GRCDROT            ROUTINE TO HANDLE CUSTOM DATA          
         BZ    GR052                                                            
         TM    GRCNIN,GRCNROT            MAKE SURE ALL BITS ARE ON              
         BZ    GR052                                                            
         OI    FLAG,FGRCCQ               . YES                                  
         L     RF,ACOM                                                          
         USING COMFACSD,RF                                                      
         GOTO1 CHEXOUT,PARAMS,RGRCID#,WORK,L'RGRCID#,=C'N'                      
         DROP  RF                                                               
         MVC   RGRCID,WORK+1                                                    
*                                                                               
GR052    CLI   PCGRCN,0                  FIRST COLUMN?                          
         BE    GR060                                                            
         CLC   PCGRCN,RGRCID             CONTINUATION COLUMN?                   
         BNE   GR181                                                            
         MVI   PCGRCN,0                                                         
                                                                                
*-----------------------------------                                            
* COLUMN NAMES IF NEEDED                                                        
*-----------------------------------                                            
GR060    TM    PCGRIDS,PCGCOFQ           ALREADY HAVE COLUMNS?                  
         BO    GR070                                                            
         BRAS  RE,PGRCON                 PROCESS GRID COLUMN NAME               
         BNE   GR182                                                            
         NI    FLAG,X'FF'-FG1STQ         NO SPECIAL PROCESS FOR NAMES           
         B     GRTEXT                                                           
                                                                                
*-----------------------------------                                            
* COLUMN DATA                                                                   
*-----------------------------------                                            
GR070    L     R1,AAIO                   A(IO AREA)                             
         L     R5,L'AAIO*2(R1)                                                  
         TM    GRCDIN,GRCDIO3                                                   
         BO    GR072                                                            
         L     R5,L'AAIO(R1)                                                    
         TM    GRCDIN,GRCDIO2                                                   
         BO    GR072                                                            
         L     R5,APTWA                  A(DATA AREA)                           
         TM    GRCDIN,GRCDTWA                                                   
         BO    GR072                                                            
         L     R5,APWS                                                          
         TM    GRCDIN,GRCDWS                                                    
         BO    GR072                                                            
         L     R5,0(R1)                                                         
*                                                                               
GR072    CLI   GRCELEC,0                 DATA IN AN ELEMENT ON RECORD?          
         BE    GR080                     . NO                                   
         SR    R1,R1                                                            
         ICM   R1,1,GRCORKL              GET RECORD KEY LENGTH                  
         BNZ   GR073                                                            
         L     RF,AGFT                                                          
         USING GRIDFD,RF                                                        
         ICM   R1,1,GRFDRKL              DEFAULT                                
         DROP  RF                                                               
GR073    AR    R5,R1                                                            
*                                                                               
GR074    CLI   0(R5),0                   END OF RECORD?                         
         BE    GRBLANK                   . YES                                  
         CLC   GRCELEC,0(R5)             FOUND ELEMENT?                         
         BNE   GR078                     . NO                                   
         OC    GRCELES,GRCELES           USING SUB-ELEMENT CODE?                
         BZ    GR080                     . NO                                   
         CLC   GRCELES,2(R5)             FOUND SUB-ELEMENT?                     
         BE    GR080                     . YES                                  
GR078    SR    RF,RF                     NEXT ELEMENT                           
         IC    RF,1(R5)                                                         
         AR    R5,RF                                                            
         B     GR074                                                            
*                                                                               
GR080    SR    R1,R1                                                            
         IC    R1,GRCDLEN                PICK UP LENGTH OF DATA                 
         SR    RF,RF                                                            
         ICM   RF,3,GRCDATA              DISPLACEMENT TO DATA IN ELEM           
         TM    GRCDIN,GRCDEVL            DATA IN ELEM IS VARIABLE LEN?          
         BZ    GR082                                                            
         IC    R1,1(R5)                  LENGTH OF ELEMENT                      
         SR    R1,RF                     R1 = LENGTH OF DATA                    
*                                                                               
GR082    AR    R5,RF                     R5 = A(DATA FIELD)                     
*                                                                               
         LTR   R1,R1                     TEST DATA LENGTH FIELD                 
         BNP   GRBLANK                                                          
*                                                                               
         TM    GRCDIN,GRCDROT            ROUTINE TO HANDLE CUSTOM DATA          
         BZ    GR088                                                            
         L     RF,0(R5)                                                         
         GOTO1 (RF),PARAMS,(C'D',DLCBFLX),GRIDCD,RGRCID#                        
         BE    GRTEXT                                                           
         NI    FLAG,X'FF'-FGRCCQ         <> MEANS NO MORE RECURRING             
         B     GR182                                                            
                                                                                
*------------------------------                                                 
* FORMAT OUTPUT                                                                 
*------------------------------                                                 
GR088    SR    RF,RF                                                            
         IC    RF,GRCTYPE                GRID COLUMN DATA TYPE                  
         SLL   RF,2                                                             
         B     *(RF)                                                            
         B     FGRTXT                    1 - GRCTTXT - TEXT                     
         B     FGRDAT                    2 - GRCTDAT - DATE                     
         B     FGRNUM                    3 - GRCTNUM - NUMBER                   
         B     FGRBOO                    4 - GRCTBOO - BOOLEAN VALUE            
*                                                                               
* FORMAT TEXT                                                                   
*                                                                               
FGRTXT   CLI   0(R5),0                   IF DATA IS ZERO, PROCESS BLANK         
         BE    GRBLANK                                                          
*                                                                               
         LA    RF,0(R1,R5)               POINT TO END OF DATA                   
FGRT10   BCTR  RF,0                                                             
         CLI   0(RF),C' '                SKIP OVER SPACES AND ZEROS             
         BH    *+12                                                             
         BCT   R1,FGRT10                 CALCULATE TRUE LENGTH                  
         B     GRTEXT                                                           
*                                                                               
         CHI   R1,L'DLCBFLX              FITS IN EXTENDED FIELD?                
         BNL   FGRT12                    . NO                                   
*                                                                               
         BCTR  R1,0                      RF=LENGTH OF DATA                      
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   DLCBFLX(0),0(R5)          MOVE INTO OUTPUT FIELD                 
         B     GRTEXT                                                           
*                                                                               
FGRT12   CHI   R1,255                    DATA TOO LONG FOR DLFLD                
         BNH   *+8                       MUST HANDLE MYSELF                     
         LHI   R1,255                    MAX IS ONE BYTE                        
*                                                                               
         LH    RE,DLCBNUMC               CHECK IF LONG TEXT WILL FIT            
         LA    RE,3(RE,R1)               ON SCREEN                              
         LH    RF,DLCXMAXL                                                      
         CR    RE,RF                                                            
         BL    FGRT20                    . YES, IT WILL                         
*                                                                               
         BRAS  RE,OUTGRITS               . NO, IT WON'T, OUTPUT DATA            
         B     GRBIX                     CURRENT FIELD DIDN'T FIT               
*                                                                               
FGRT20   L     RF,AGSB                   MOVE TEXT TO GRID SCREEN BLOCK         
         AH    RF,DLCBNUMC                                                      
         MVI   0(RF),C'"'                WITH QUOTES AROUND IT                  
         SHI   R1,1                                                             
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   1(0,RF),0(R5)                                                    
         LA    RF,2(R1,RF)                                                      
         MVI   0(RF),C'"'                                                       
*                                                                               
         LA    R1,4(R1)                                                         
         AH    R1,DLCBNUMC                                                      
         STH   R1,DLCBNUMC               BUMP NUM OF CHARS THIS LINE            
         LH    R1,DLCBNUMF                                                      
         LA    R1,1(R1)                                                         
         STH   R1,DLCBNUMF               BUMP NUM OF FIELDS THIS LINE           
         B     GR182                     CONTINUE PROCESSING COLUMNS            
*                                                                               
* FORMAT DATES                                                                  
*                                                                               
FGRDAT   OC    0(2,R5),0(R5)                                                    
         BZ    GRBLANK                                                          
         SR    R2,R2                                                            
         IC    R2,GRCDDTY                DATCON INPUT FORMAT NUMBER             
         MVC   WORK(10),0(R5)                                                   
         TM    GRCGIN,GRCGCOM            COMPLEMENTED?                          
         BZ    FGRDA10                                                          
         XC    WORK(10),=10X'FF'                                                
FGRDA10  L     RF,ACOM                                                          
         USING COMFACSD,RF                                                      
         GOTO1 CDATCON,PARAMS,((R2),WORK),(21,DLCBFLX)                          
         DROP  RF                                                               
         B     GRTEXT                                                           
*                                                                               
* FORMAT NUMBER                                                                 
*                                                                               
FGRNUM   XC    DLCBFLD,DLCBFLD                                                  
         TM    GRCGIN,GRCGPAC            BRANCH IF NOT BINARY                   
         BNZ   FGRNU10                                                          
         CHI   R1,4                      WITH LENGTH <4                         
         BH    GRBLANK                                                          
         SR    R2,R2                                                            
         EX    R1,*+8                                                           
         B     *+8                                                              
         ICM   R2,0,0(R5)                                                       
         TM    GRCGIN,GRCGCUR                                                   
         BO    FGRNU08                                                          
         EDIT  (R2),(16,DLCBFLD),FLOAT=-,ALIGN=LEFT,ZERO=NOBLANK,      +        
               COMMAS=Y                                                         
         B     GRNUM                                                            
FGRNU08  EDIT  (R2),(16,DLCBFLD),2,FLOAT=-,ALIGN=LEFT,ZERO=NOBLANK,    +        
               COMMAS=Y                                                         
         B     GRNUM                                                            
                                                                                
FGRNU10  TM    GRCGIN,GRCGPAC            NUMBER IS PACKED                       
         BNE   GRBLANK                                                          
         CHI   R1,8                      WITH LENGTH <8                         
         BH    GRBLANK                                                          
         SR    R2,R2                                                            
         SHI   R1,1                                                             
         EX    R1,*+8                                                           
         B     *+10                                                             
         OC    0(0,R5),0(R5)             IF ZERO THEN JUST PRINT BLANK          
         BZ    GRBLANK                                                          
         EX    R1,*+8                                                           
         B     *+10                                                             
         ZAP   PACK16(14),0(0,R5)        PICK UP PACKED VALUE FOR EDIT          
         AHI   R1,1                                                             
         TM    GRCGIN,GRCGCUR                                                   
         BO    FGRNU12                                                          
         EDIT  (P14,PACK16),(30,DLCBFLD),FLOAT=-,ALIGN=LEFT,           +        
               ZERO=NOBLANK,COMMAS=YES                                          
         B     GRNUM                                                            
FGRNU12  EDIT  (P14,PACK16),(30,DLCBFLD),2,FLOAT=-,ALIGN=LEFT,         +        
               ZERO=NOBLANK,COMMAS=YES                                          
         B     GRNUM                                                            
*                                                                               
* FORMAT BOOLEAN (Y/N)                                                          
*                                                                               
FGRBOO   IC    RF,GRCDFLG                BIT(S) WE NEED TO TEST                 
         EX    RF,*+8                                                           
         B     *+8                                                              
         TM    0(R5),0                                                          
         BZ    FGRBO10                                                          
*                                                                               
         TM    GRCGIN,GRCGYON                                                   
         BZ    *+14                                                             
         MVC   DLCBFLX(3),=C'YES'                                               
         B     GRTEXT                                                           
         MVC   DLCBFLX(L'GRCNAME),GRCNAME                                       
*                                                                               
         L     RF,AGFT                                                          
         USING GRIDFD,RF                                                        
DIC      USING DICTATED,PARAMS                                                  
         MVI   DIC.DDACTN,C'S'           SET UP FIRST DICTATE PARAM             
         MVI   DIC.DDRETN,C'L'                                                  
         MVI   DIC.DDSYS,C' '                                                   
         MVI   DIC.DDLANG,C' '                                                  
         CLI   GRFDDSY,0                 SPECIFIC SYSTEM FOR DDICT?             
         BE    *+10                                                             
         MVC   DIC.DDSYS,GRFDDSY                                                
         DROP  RF,DIC                                                           
*                                                                               
         L     RF,ACOM                                                          
         USING COMFACSD,RF                                                      
         GOTO1 CDICTATE,PARAMS,,DLCBFLX                                         
         DROP  RF                                                               
         B     GRTEXT                                                           
*                                                                               
FGRBO10  TM    GRCGIN,GRCGYON                                                   
         BZ    *+14                                                             
         MVC   DLCBFLX(2),=C'NO'                                                
         B     GRTEXT                                                           
         B     GRBLANK                                                          
                                                                                
*-----------------------------------                                            
* DISPLAY DATA                                                                  
*-----------------------------------                                            
GRBLANK  LHI   R1,GBLNK                  DISPLAY A BLANK                        
         TM    GRCCIN,GRCCOMB            OMIT COLUMN IF BLANK?                  
         BO    GR182                                                            
         B     GR180                                                            
*                                                                               
GRNUM    LHI   R1,GNUM                   DISPLAY A NUMBER                       
         B     GR180                                                            
*                                                                               
GRTEXT   TM    GRCCIN,GRCCOM0            OMIT COLUMN IF ZERO?                   
         BZ    *+12                                                             
         CLI   DLCBFLX,0                                                        
         BE    GR182                                                            
         TM    GRCCIN,GRCCOMB            OMIT COLUMN IF BLANK?                  
         BZ    *+12                                                             
         CLI   DLCBFLX,C' '                                                     
         BE    GR182                                                            
         CLC   DLCBFLX(2),=C'  '                                                
         BNH   GRBLANK                                                          
*                                                                               
         LHI   R1,GTXT                   TEXT                                   
         TM    FLAG,FG1STQ               FIRST COLUMN                           
         BZ    GR180                                                            
         MVC   BIGWORK(L'DLCBFLX),DLCBFLX SHIFT DLCBFLX                         
         MVI   DLCBFLX,C' '                                                     
         MVC   DLCBFLX+1(L'DLCBFLX-1),BIGWORK                                   
*                                                                               
GR180    GOTOR DWNL,(R1)                                                        
         BNE   GRBIX                                                            
GR181    NI    FLAG,X'FF'-FG1STQ         NO LONGER FIRST COLUMN                 
*                                                                               
GR182    TM    FLAG,FGRCCQ               RECURRING COLUMN?                      
         BZ    GR183                                                            
         SR    R1,R1                                                            
         ICM   R1,3,RGRCID#              INCREASE RECURRING COL COUNT           
         AHI   R1,1                                                             
         CHI   R1,2000                                                          
         BNH   *+6                                                              
         DC    H'0'                      SELF IMPOSED 2000 COLUMN LIMIT         
         STCM  R1,3,RGRCID#                                                     
         B     GR020                                                            
*                                                                               
GR183    AHI   R4,GRCLNQ                 NEXT COLUMN                            
         B     GR020                                                            
                                                                                
*----------------------------------------                                       
* END OF ROW AND EXIT                                                           
*----------------------------------------                                       
GR200    GOTOR DWNL,GEOL                                                        
         BNE   GRBIX                                                            
*                                                                               
         TM    PCGRIDS,PCGCOFQ           COLUMN NAMES DISPLAYED?                
         BO    GROKX                     . YES                                  
         OI    PCGRIDS,PCGCOFQ           SET LOCAL BIT                          
         OI    FLAG,FG1STQ               SPECIAL PROCESS FOR 1ST COL            
         B     GR010                                                            
                                                                                
*----------------------------------------                                       
* END OF REPORT / GRIDS                                                         
*----------------------------------------                                       
GR300    LH    RE,DLCBNUMC               CHECK IF EOL WILL FIT                  
         LA    RE,2(RE)                   NEED TO CHECK BECAUSE DWNL            
         LH    RF,DLCXMAXL                WILL TRY TO OUTPUT EOL ANYWAY         
         CR    RE,RF                                                            
         BL    GR310                     YES, IT WILL FIT                       
         BE    GR310                     YES, IT WILL STILL FIT                 
         BRAS  RE,OUTGRITS               NO, IT WON'T, OUTPUT DATA              
*MN                                                                             
         OI    PCGRIDS,PCGRTRN           TELL APPL TO RETURN FOR END            
*MN                                                                             
         MVI   RGRCID,0                                                         
         B     GR312                     MARK FINISHED, CALLER NEEDS            
*                                                       ANOTHER GEND            
GR310    GOTOR DWNL,GEND                                                        
GR312    OI    PCGRIDS,PCGFINQ                                                  
         TM    PCGRIDS,PCGCOFQ                                                  
         BNO   GRERX                                                            
         B     GRBIX                                                            
                                                                                
*----------------------------------------                                       
* GRID EXIT                                                                     
*----------------------------------------                                       
GROKX    MVI   PCGRCN,0                  START FROM FIRST COLUMN                
         MVI   BYTE,1                    SET EXIT CODE                          
         J     GRX                                                              
*                                                                               
GRBIX    XC    DLCBD(DLCBXLX),DLCBD      FIELD DID NOT FIT                      
         MVC   PCGRCN,RGRCID             KEEP TRACK OF FIELD                    
         TM    PCGRIDS,PCGCOFQ           COLUMNS HEADINGS FINISHED?             
         BO    *+8                       . YES                                  
         OI    PCGRIDS,PCGCOPQ           . NO, PARTIAL COL HEAD DISPLAY         
         MVI   BYTE,2                                                           
         B     GRX                                                              
*                                                                               
GRERX    MVI   BYTE,0                                                           
*                                                                               
GRX      CLI   BYTE,1                    SET CONDITION CODE                     
EXIT     XIT1                                                                   
         DROP  R4                                                               
         LTORG                                                                  
                                                                                
***********************************************************************         
* PROCESS GRIDS DESCRIPTION LINE DATA                                           
***********************************************************************         
         USING GRIDLD,R4                                                        
PGRDLDA  NTR1  BASE=*,LABEL=*                                                   
         SR    R3,R3                                                            
*                                                                               
         TM    PCGRIDS,PCGCOFQ+PCGCOPQ   BUILD THESE LINES, ONLY IF NO          
         BNZ   PGRDX                     COLUMNS BUILT YET                      
*                                                                               
PGRD008  LA    R3,DLCBFLX                R3=A(DOWNLOAD FIELD)                   
         BAS   RE,PGRDSLEN               DLCBLEN=MAX LEN OF DATA LINE           
*                                                                               
PGRD010  CLI   GRLID,GRLDQ               ANY DESCRIPTION LINE DATA?             
         BNE   PGRD050                   . NO, PRINT WHAT WE HAVE               
*----------------------------------------                                       
* DESCRIPTION NAME                                                              
*----------------------------------------                                       
         XC    BIGWORK(L'DLCBFLX),BIGWORK                                       
         LA    RE,BIGWORK                                                       
*                                                                               
         L     RF,APTWA                                                         
*                                                                               
         SR    R0,R0                                                            
         ICM   R0,3,GRLNADI                                                     
         AR    RF,R0                     RF=A(TWA FIELD HEADER)                 
         SR    R1,R1                                                            
         ICM   R1,1,GRLNALN              R1=LENGTH OF FIELD DATA                
         BZ    PGRD040                                                          
         CLI   0(RF),C' '                                                       
         BNH   PGRD040                                                          
*                                                                               
         SHI   R1,1                                                             
         BM    PGRD040                                                          
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   0(0,RE),0(RF)                                                    
*                                                                               
         LA    R1,0(R1,RE)                                                      
PGRD020  CLI   0(R1),C' '                                                       
         BH    PGRD022                                                          
         MVI   0(R1),C' '                                                       
         AHI   R1,-1                                                            
         CR    R1,RE                                                            
         BH    PGRD020                                                          
PGRD022  MVI   1(R1),C'='                                                       
         LA    R1,2(R1)                                                         
*                                                                               
         SR    R1,RE                     R1=LENGTH OF NAME                      
         AR    RE,R1                     RE=A(NEXT OUTPUT LOCATION)             
         LR    R2,R1                                                            
*                                                                               
*----------------------------------------                                       
* DESCRIPTION DATA                                                              
*----------------------------------------                                       
         L     RF,APTWA                                                         
         ICM   R0,3,GRLDADI                                                     
         AR    RF,R0                     RF=A(TWA FIELD HEADER)                 
         SR    R1,R1                                                            
         ICM   R1,1,GRLDALN              R1=LENGTH OF FIELD DATA                
         BZ    PGRD040                                                          
         CLI   0(RF),C' '                                                       
         BNH   PGRD040                                                          
*                                                                               
         SHI   R1,1                                                             
         BM    PGRD040                                                          
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   0(0,RE),0(RF)                                                    
*                                                                               
         LA    R1,0(R1,RE)                                                      
PGRD026  CLI   0(R1),C' '                                                       
         BH    PGRD028                                                          
         MVI   0(R1),C' '                                                       
         AHI   R1,-1                                                            
         CR    R1,RE                                                            
         BH    PGRD026                                                          
*                                                                               
PGRD028  DS    0H                                                               
         LA    R1,1(R1)                                                         
         MVI   0(R1),C','                                                       
         LA    R1,1(R1)                                                         
         MVI   0(R1),C' '                                                       
         SR    R1,RE                     R1=LENGTH OF NAME                      
         AR    R1,R2                     RE=A(NEXT OUTPUT LOCATION)             
         AHI   R1,1                                                             
*----------------------------------------                                       
* MOVE TO OUTPUT LINE                                                           
*----------------------------------------                                       
         SR    R0,R0                                                            
         IC    R0,DLCBLEN                                                       
         CR    R1,R0                                                            
         BH    PGRD050                                                          
         SR    R0,R1                                                            
         STC   R0,DLCBLEN                                                       
*                                                                               
         AHI   R1,-1                                                            
         BM    PGRD040                                                          
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R3),BIGWORK                                                  
         LA    R3,1(R1,R3)                                                      
*                                                                               
PGRD040  LA    R4,GRLLNQ(R4)                                                    
         B     PGRD010                                                          
*                                                                               
PGRD050  LA    R1,DLCBFLX                R3=A(DOWNLOAD FIELD)                   
         CR    R3,R1                     WAS THERE ANY DATA?                    
         BNH   PGRDX                     . NO                                   
         AHI   R3,-2                                                            
         CLI   0(R3),C','                                                       
         BNE   *+8                                                              
         MVI   0(R3),C' '                                                       
*                                                                               
         BAS   RE,PGRDSLEN               DLCBLEN=MAX LEN OF DATA LINE           
         GOTOR DWNL,GTXT                                                        
         GOTOR DWNL,GEOL                                                        
*                                                                               
         L     RF,AMESO                                                         
         LA    RF,GRMCODA1-GRMSG1(RF)                                           
         PACK  FULL,0(L'GRMCODA1,RF)                                            
         AP    FULL,=P'1'                                                       
         EDIT  (P4,FULL),(L'GRMCODA1,(RF))                                      
*                                                                               
         CLI   GRLID,GRLDQ                                                      
         BE    PGRD008                                                          
*                                                                               
PGRDX    XC    DLCBLEN,DLCBLEN                                                  
         CR    RB,RB                                                            
         J     EXIT                                                             
                                                                                
*----------------------------------------                                       
* SET DLCBLEN                                                                   
*----------------------------------------                                       
         USING GRIDFD,R1                                                        
PGRDSLEN L     R1,AGFT                                                          
         SR    R0,R0                                                            
         IC    R0,GRFLLEN                LENGTH OF SCREEN LINE                  
         AHI   R0,-5                                                            
         STC   R0,DLCBLEN                DLCBLEN=MAX LEN OF DATA LINE           
         BR    RE                                                               
         DROP  R1                                                               
         LTORG                                                                  
         EJECT                                                                  
                                                                                
***********************************************************************         
* PROCESS GRIDS COLUMN NAME                                                     
***********************************************************************         
         USING GRIDCD,R4                                                        
PGRCON   NTR1  BASE=*,LABEL=*                                                   
*                                                                               
         TM    GRCCIN,GRCCVNM            VARIABLE COLUMN NAME?                  
         BZ    PGRC012                   . NO                                   
         L     RF,AAIO                                                          
         L     RF,L'AAIO*2(RF)                                                  
         TM    GRCNIN,GRCNIO3                                                   
         BO    PGRC006                                                          
         L     RF,AAIO                                                          
         L     RF,L'AAIO(RF)                                                    
         TM    GRCNIN,GRCNIO2                                                   
         BO    PGRC006                                                          
         L     RF,APTWA                                                         
         TM    GRCNIN,GRCNTWA                                                   
         BO    PGRC006                                                          
         L     RF,APWS                                                          
         TM    GRCNIN,GRCNWS                                                    
         BO    PGRC006                                                          
         L     RF,AAIO                                                          
         L     RF,0(RF)                                                         
PGRC006  SR    R0,R0                                                            
         ICM   R0,3,GRCNADI                                                     
         AR    RF,R0                                                            
*                                                                               
         TM    GRCNIN,GRCNROT            ROUTINE TO HANDLE CUSTOM DATA          
         BZ    PGRC008                                                          
         L     RF,0(RF)                                                         
         GOTO1 (RF),PARAMS,(C'N',DLCBFLX),GRIDCD,RGRCID#                        
         BE    PGRC010                                                          
         NI    FLAG,X'FF'-FGRCCQ         <> MEANS NO MORE RECURRING             
         B     PGRCERX                                                          
*                                                                               
PGRC008  SR    R1,R1                                                            
         IC    R1,GRCNALN                                                       
         SHI   R1,1                                                             
         BNM   *+6                                                              
         DC    H'0'                                                             
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   DLCBFLX(0),0(RF)                                                 
*                                                                               
PGRC010  CLI   DLCBFLX,C' '              IF NO NAME THEN DON'T BUILD            
         BNH   PGRCOKX                                                          
         B     PGRC018                                                          
*                                                                               
PGRC012  MVC   DLCBFLX(L'GRCNAME),GRCNAME                                       
*                                                                               
         L     RF,AGFT                                                          
         USING GRIDFD,RF                                                        
DIC      USING DICTATED,PARAMS                                                  
         MVI   DIC.DDACTN,C'S'           SET UP FIRST DICTATE PARAM             
         MVI   DIC.DDRETN,C'L'                                                  
         MVI   DIC.DDSYS,C' '                                                   
         MVI   DIC.DDLANG,C' '                                                  
         CLI   GRFDDSY,0                 SPECIFIC SYSTEM FOR DDICT?             
         BE    *+10                                                             
         MVC   DIC.DDSYS,GRFDDSY                                                
         DROP  RF,DIC                                                           
*                                                                               
         L     RF,ACOM                                                          
         USING COMFACSD,RF                                                      
         GOTO1 CDICTATE,PARAMS,,DLCBFLX                                         
         DROP  RF                                                               
*                                                                               
PGRC018  SR    R1,R1                                                            
         ICM   R1,1,GRCSPOS              SPLIT POSITION                         
         BNZ   *+8                                                              
         LHI   R1,10                     USE 10 IF NONE GIVEN                   
         LA    R3,DLCBFLX                                                       
         AR    R1,R3                                                            
PGRC020  CR    R1,R3                                                            
         BNH   PGRC030                                                          
         CLI   0(R1),C' '                FIND BLANK IN HEADING                  
         BE    *+12                                                             
         SHI   R1,1                                                             
         B     PGRC020                                                          
*                                                                               
         LA    R1,1(R1)                                                         
         AHI   R3,L'DLCBFLX-1            SHIFT DATA OVER FOR SPLIT CHAR         
         SR    R3,R1                                                            
         SHI   R3,1                                                             
         BNP   PGRC030                                                          
         EX    R3,*+8                                                           
         B     *+10                                                             
         MVC   BIGWORK(0),0(R1)                                                 
         SHI   R3,1                                                             
         BNP   PGRC030                                                          
         EX    R3,*+8                                                           
         B     *+10                                                             
         MVC   1(0,R1),BIGWORK                                                  
         MVI   0(R1),GRCSEPQ             AND REPLACE WITH SPLIT CHAR            
*                                                                               
PGRC030  LA    R3,DLCBFLX+L'DLCBFLX-1                                           
PGRC032  CLI   0(R3),GRCSEPQ                                                    
         BE    *+12                                                             
         CLI   0(R3),C' '                                                       
         BH    PGRC040                                                          
         MVI   0(R3),C' '                                                       
         BCTR  R3,0                                                             
         B     PGRC032                                                          
*                                                                               
PGRC040  MVI   1(R3),C'*'                                                       
         LA    R3,2(R3)                                                         
         MVC   0(L'RGRCID,R3),RGRCID     GRID COLUMN ID                         
         LA    R3,L'RGRCID(R3)                                                  
         CLI   RGRCID+2,C' '             TWO CHAR ID                            
         BH    *+8                                                              
         AHI   R3,-1                     DON'T LEAVE A SPACE                    
         MVI   0(R3),C'*'                                                       
*                                                                               
         CLI   GRCTYPE,GRCTDAT           DATE                                   
         BNE   PGRC050                                                          
         MVI   1(R3),C'D'                                                       
         LA    R3,1(R3)                                                         
         B     PGRC060                                                          
*                                                                               
PGRC050  CLI   GRCTYPE,GRCTNUM           NUMBER                                 
         BNE   PGRC060                                                          
         MVI   1(R3),C'N'                                                       
         LA    R3,1(R3)                                                         
*                                                                               
PGRC060  TM    GRCFORM,GRCFHID           HIDE COLUMN                            
         BZ    PGRC070                                                          
         MVI   1(R3),C'H'                                                       
         LA    R3,1(R3)                                                         
*                                                                               
PGRC070  TM    GRCFORM,GRCFRGT           RIGHT JUSTIFY                          
         BZ    PGRC080                                                          
         MVI   1(R3),C'R'                                                       
         LA    R3,1(R3)                                                         
         B     PGRC090                                                          
*                                                                               
PGRC080  TM    GRCFORM,GRCFCEN           CENTER                                 
         BZ    PGRC090                                                          
         MVI   1(R3),C'C'                                                       
         LA    R3,1(R3)                                                         
*                                                                               
PGRC090  TM    GRCFORM,GRCFSIZ           SIZED (2")                             
         BZ    PGRC100                                                          
         MVI   1(R3),C'S'                                                       
         LA    R3,1(R3)                                                         
*                                                                               
PGRC100  CLI   0(R3),C'*'                IF NO ATTRIBUTES                       
         BNE   *+8                                                              
         MVI   0(R3),C' '                THEN REMOVE ASTERISK                   
*                                                                               
PGRCOKX  CR    RB,RB                                                            
         J     EXIT                                                             
PGRCERX  LTR   RB,RB                                                            
         J     EXIT                                                             
         LTORG                                                                  
         EJECT                                                                  
                                                                                
***********************************************************************         
* GRID OUTPUT MODULE                                                            
*   ON ENTRY R1=OUTPUT MODE                                                     
***********************************************************************         
         USING GRIDFD,R3                                                        
DWNL     NTR1  BASE=*,LABEL=*                                                   
         L     R3,AGFT                                                          
         STC   R1,BYTE                                                          
*                                                                               
         CHI   R1,GINI                   INITIALIZE                             
         BE    DWNL010                                                          
         CHI   R1,GCLR                   CLEAR GRID OUTPUT SCREEN               
         BE    DWNL015                                                          
         CHI   R1,GTXT                   OUTPUT TEXT                            
         BE    DWNL050                                                          
         CHI   R1,GBLNK                  OUTPUT A BLANK                         
         BE    DWNL060                                                          
         CHI   R1,GNUM                   OUTPUT NUMBER                          
         BE    DWNL070                                                          
         CHI   R1,GEOL                   END OF LINE                            
         BE    DWNL080                                                          
         CHI   R1,GEND                   END OF OUTPUT                          
         BE    DWNL090                                                          
         DC    H'0'                                                             
                                                                                
*----------------------------------------                                       
* INITIALIZATION                                                                
*----------------------------------------                                       
DWNL010  XC    DLCBD(DLCBXLX),DLCBD                                             
         MVI   DLCBACT,DLCBINIT          DOWN LOAD ACTION IS START              
*                                                                               
         L     R0,AGSB                   GRID SCREEN BLOCK                      
         ST    R0,DLCBAPL                                                       
*                                                                               
         LHI   R1,GSBLQ                  CLEAR THE GSB                          
         SR    RE,RE                                                            
         LHI   RF,X'40'                                                         
         SLL   RF,24                                                            
         MVCL  R0,RE                                                            
*                                                                               
         LA    RF,DWNHOOK                HOOK                                   
         ST    RF,DLCBAPR                                                       
*                                                                               
         LH    RE,GRFLINF                FIRST GRID SCREEN LINE                 
         LH    RF,GRFLINL                LAST GRID SCREEN LINE                  
         SR    RF,RE                                                            
         SR    RE,RE                                                            
         SR    R0,R0                                                            
         IC    R0,GRFLDIS                DISPL BETWEEN SCREEN LINES             
         DR    RE,R0                                                            
         SR    RE,RE                                                            
         IC    R0,GRFLLEN                LENGTH OF SCREEN LINE                  
         MR    RE,R0                                                            
         AR    RF,R0                                                            
         STH   RF,DLCXMAXL                                                      
*                                                                               
         MVI   DLCXDELC,C' '             DELIMITER                              
         MVI   DLCXEOTC,C'"'             TEXT DELIMITER                         
         MVI   DLCXEOLC,C':'             END-OF-LINE                            
         MVI   DLCXEORC,C':'             END-OF-REPORT                          
         L     RF,ACOM                                                          
         USING COMFACSD,RF                                                      
         GOTO1 CDLFLD,DLCBD                                                     
         BNE   DWNLHX                                                           
         DROP  RF                                                               
         MVI   DLCBFLD,C' '              MUST CLEAR FIRST TIME IN               
         MVC   DLCBFLD+1(L'DLCBFLD-1),DLCBFLD                                   
         XC    DLCBLEN,DLCBLEN                                                  
         MVI   DLCBFLX,C' '                                                     
         MVC   DLCBFLX+1(L'DLCBFLX-1),DLCBFLX                                   
*-----------------------------------                                            
*  CLEAR AND TRANSMIT SCREEN                                                    
*-----------------------------------                                            
DWNL015  L     R0,APTWA                                                         
         LR    R4,R0                                                            
         AH    R4,GRFLINF                FIRST LINE                             
         AH    R0,GRFLINL                LAST LINE                              
         SHI   R4,FHDAD                  BUMP BACK TO HEADERS                   
         SHI   R0,FHDAD                                                         
*                                                                               
         SR    RE,RE                                                            
         IC    RE,GRFLDIS                DISPLACEMENT BETWEEN LINES             
         SR    RF,RF                                                            
         IC    RF,GRFLLEN                LINE LENGTH                            
         SHI   RF,1                                                             
*                                                                               
         USING FHD,R4                                                           
DWNL016  EX    RF,*+8                    CLEAR THE SCREEN                       
         B     *+10                                                             
         MVC   FHDA(0),DWSPACES                                                 
         OI    FHOI,FHOITR                                                      
*                                                                               
         CR    R4,R0                     REACHED THE LAST LINE?                 
         BNL   DWNL018                   . YES                                  
         AR    R4,RE                     BUMP TO NEX LINE                       
         B     DWNL016                                                          
         DROP  R4                                                               
                                                                                
DWNL018  CLI   BYTE,GCLR                 IF CLEAR THEN EXIT                     
         BE    DWNLX                                                            
*----------------------------------------                                       
* CALCULATE # OF FIELDS                                                         
*----------------------------------------                                       
         USING FHD,R4                                                           
         L     R4,APTWA                                                         
         LR    R1,R4                                                            
         AH    R1,GRFLINF                FIRST GRID LINE                        
         AHI   R4,(L'TWAHDR1+L'TWAUSER)  DISPLACEMENT TO SCREEN                 
         SR    R5,R5                     CALCULATE # OF FIELDS + SPACES         
*                                                                               
DWNL020  CR    R4,R1                                                            
         BNL   DWNL025                                                          
*                                                                               
         TM    FHAT,FHATPR               PROTECTED, NEED TO CHECK FOR           
         BO    DWNL024                              PC FIELD CONFUSION          
*                                                                               
         TM    GRIDSTAT,GRFGDNK                                                 
         BO    DWNL024                                                          
         CHI   R5,2                      MESSAGE AND SERVICE REQUEST            
         BNH   DWNL024                   SKIP SERVICE REQUEST FIELD             
         TM    FHII,FHIITH                                                      
         BNO   DWNL024                                                          
         OI    FLAG,FKEYFQ                                                      
*                                                                               
DWNL024  SR    RF,RF                                                            
         IC    RF,FHLN                                                          
         AR    R4,RF                                                            
         AHI   R5,1                                                             
         B     DWNL020                                                          
*                                                                               
DWNL025  SR    R1,R1                                                            
         TM    GRFDORA,X'80'             CHECK IF NEGATIVE                      
         BZ    *+8                                                              
         LHI   R1,-1                                                            
         IC    R1,GRFDORA                                                       
         AR    R5,R1                                                            
         DROP  R4                                                               
                                                                                
*-----------------------------------                                            
* GRID RESET BEGINNING OF GRIDS                                                 
*-----------------------------------                                            
         TM    FLAG,FKEYFQ                                                      
         BO    *+12                                                             
         TM    PCGRIDS,PCGBEGQ           GRID ALREADY BEGUN?                    
         BO    DWNL027                   . YES                                  
         OI    PCGRIDS,PCGBEGQ           IT HAS NOW                             
         NI    PCGRIDS,X'FF'-PCGCOFQ-PCGCOPQ-PCGFINQ                            
         MVI   PCGRCN,0                  START WITH FIRST COLUMN                
         NI    FLAG,X'FF'-FKEYFQ                                                
                                                                                
*-----------------------------------                                            
* PROCESS GRID MESSAGE                                                          
*-----------------------------------                                            
DWNL027  L     R4,AMESO                  PROCESS MESSAGE FIELD                  
         MVC   0(GRMSGL,R4),DWSPACES                                            
*                                                                               
         TM    PCGRIDS,PCGCOPQ           COLUMNS STARTED?                       
         BO    *+12                      . YES                                  
         TM    PCGRIDS,PCGCOFQ           ALREADY HAVE COLUMNS?                  
         BZ    DWNL028                   . NO, THEN NEED 1ST GRID MESS          
*                                                                               
         MVC   0(GRMLNQ,R4),GRMSG                                               
         LA    R4,GRMCODA-GRMSG(R4)                                             
         EDIT  (R5),(L'GRMCODA,(R4))                                            
         B     DWNLX                                                            
*                                                                               
DWNL028  MVC   0(GRM1LNQ,R4),GRMSG1                                             
         LA    R2,GRMDOR-GRMSG1(R4)                                             
         EDIT  (R5),(L'GRMDOR,(R2))                                             
         LA    R2,GRMCODA1-GRMSG1(R4)                                           
         EDIT  (R5),(L'GRMCODA1,(R2))                                           
         CLI   GRFFIX,0                                                         
         BE    *+10                                                             
         MVC   GRMFIX-GRMSG1(L'GRFFIX,R4),GRFFIX                                
*                                                                               
         MVC   GRMFID-GRMSG1(L'GRFID,R4),GRFID                                  
         CLI   COLSEL,0                                                         
         BE    DWNLX                                                            
         MVC   GRMFCS-GRMSG1(L'GRFCS,R4),COLSEL                                 
         B     DWNLX                                                            
                                                                                
*-----------------------------------                                            
* DOWNLOAD A RECORD - TEXT                                                      
*-----------------------------------                                            
DWNL050  OI    DLCBFLG1,DLCBFXFL         USE EXTENDED FIELD                     
         MVI   DLCBACT,DLCBPUT           ACTION IS PUT                          
         MVI   DLCBTYP,DLCBTXT           TYPE IS TEXT                           
         B     DWNL100                   DOWN-LOAD FIELD                        
                                                                                
*-----------------------------------                                            
* DOWNLOAD A RECORD - BLANK (TEXT)                                              
*-----------------------------------                                            
DWNL060  MVI   DLCBACT,DLCBPUT           ACTION IS PUT                          
         MVI   DLCBTYP,DLCBTXT           TYPE IS TEXT                           
         MVI   DLCBLEN,1                                                        
         MVI   DLCBFLX,C' '                                                     
         OI    DLCBFLG1,DLCBFXFL         USE EXTENDED FIELD                     
         B     DWNL100                   DOWN-LOAD FIELD                        
                                                                                
*-----------------------------------                                            
* DOWNLOAD A RECORD - NUMBER                                                    
*-----------------------------------                                            
DWNL070  MVI   DLCBACT,DLCBPUT           ACTION IS PUT                          
         MVI   DLCBTYP,DLCBNUM           TYPE IS NUMBER                         
         B     DWNL100                   DOWN-LOAD FIELD                        
                                                                                
*-----------------------------------                                            
* END OF ROW                                                                    
*-----------------------------------                                            
DWNL080  MVI   DLCBACT,DLCBPUT           ACTION IS PUT                          
         MVI   DLCBTYP,DLCBNUM           NUMBER (SO NO DELIMITERS)              
         MVI   DLCBFLD,C';'                                                     
         B     DWNL100                                                          
                                                                                
*-----------------------------------                                            
* END OF OUTPUT                                                                 
*-----------------------------------                                            
DWNL090  MVI   DLCBACT,DLCBEOL           END OF RECORD(USE END OF LINE)         
                                                                                
*-----------------------------------                                            
* OUTPUT DOWNLOAD DATA                                                          
*-----------------------------------                                            
DWNL100  L     RF,ACOM                                                          
         USING COMFACSD,RF                                                      
         GOTO1 CDLFLD,DLCBD                                                     
         DROP  RF                                                               
         NI    DLCBFLG1,X'FF'-DLCBFXFL                                          
*                                                                               
         TM    DLCBRETC,DLCBRCPR                                                
         BO    DWNLHX                                                           
*                                                                               
DWNLX    CR    RB,RB                                                            
         J     EXIT                                                             
DWNLHX   LTR   RB,RB                                                            
         J     EXIT                                                             
                                                                                
*----------------------------------------------------------------------         
* DOWNLOAD HOOK                                                                 
*----------------------------------------------------------------------         
DWNHOOK  ST    RE,FULL                                                          
*                                                                               
         L     R1,AGSB                                                          
         L     R0,APTWA                  OUTPUT LINES TO SCREEN                 
         LR    R4,R0                                                            
         AH    R4,GRFLINF                FIRST LINE                             
         AH    R0,GRFLINL                LAST LINE                              
         SHI   R4,FHDAD                  BUMP BACK TO HEADERS                   
         SHI   R0,FHDAD                                                         
*                                                                               
         SR    RE,RE                                                            
         IC    RE,GRFLDIS                DISPLACEMENT BETWEEN LINES             
         SR    RF,RF                                                            
         IC    RF,GRFLLEN                LINE LENGTH                            
         SHI   RF,1                                                             
*                                                                               
         USING FHD,R4                                                           
DWNHO40  OI    FHOI,FHOITR                                                      
         EX    RF,*+8                    OUTPUT TO SCREEN                       
         J     *+10                                                             
         MVC   FHDA(0),0(R1)                                                    
         LA    R1,1(RF,R1)                                                      
*                                                                               
         CR    R4,R0                     REACHED THE LAST LINE?                 
         JNL   DWNHOOKX                  . YES                                  
         AR    R4,RE                     BUMP TO NEX LINE                       
         J     DWNHO40                                                          
         DROP  R4                                                               
*                                                                               
DWNHOOKX L     RE,FULL                                                          
         BR    RE                                                               
         DROP  R3                                                               
*----------------------------------------------------------------------         
         LTORG                                                                  
*                                                                               
DWSPACES DC    CL78' '                                                          
*                                                                               
GRMSG1   DC    C'GRID NEXTL '            GRID MESSAGE FOR FIRST SCREEN          
         DC    C'DOR '                                                          
GRMDOR   DC    C'17',C' '                START OF GRID INFORMATION              
         DC    C'DATA '                                                         
GRMCODA1 DC    C'20',C' '                START OF GRID COLUMN DATA              
         DC    C'EID '                                                          
GRMFID   DC    C'08'                     GRID FORMAT IDENTIFIER                 
GRMFCS   DC    C'0',C' '                 FORMAT COLUMN SELECTOR                 
         DC    C'FC',C' '                                                       
GRMFIX   DC    C'1',C' '                 NUMBER OF FIXED COLUMNS                
         DC    C'WR ',AL1(GRCSEPQ)                                              
GRM1LNQ  EQU   *-GRMSG1                                                         
*                                                                               
GRMSG    DC    C'GRID NEXTL '                                                   
         DC    C'DATA '                                                         
GRMCODA  DC    C'17',C' '                                                       
         DC    C'WR ',AL1(GRCSEPQ)                                              
GRMLNQ   EQU   *-GRMSG                                                          
GRMSGL   EQU   60                                                               
         EJECT                                                                  
                                                                                
***********************************************************************         
* OUTPUT GRIDS DATA TO SCREEN                                                   
***********************************************************************         
         USING GRIDFD,R3                                                        
OUTGRITS NTR1  BASE=*,LABEL=*                                                   
         L     R1,AGSB                   GRID FORMAT BLOCK                      
         L     R3,AGFT                   GRID FORMAT TABLE                      
*                                                                               
         L     R0,APTWA                  OUTPUT LINES TO SCREEN                 
         LR    R4,R0                                                            
         AH    R4,GRFLINF                FIRST LINE                             
         AH    R0,GRFLINL                LAST LINE                              
         SHI   R4,FHDAD                  BUMP BACK TO HEADERS                   
         SHI   R0,FHDAD                                                         
*                                                                               
         SR    RE,RE                                                            
         IC    RE,GRFLDIS                DISPLACEMENT BETWEEN LINES             
         SR    RF,RF                                                            
         IC    RF,GRFLLEN                LINE LENGTH                            
         SHI   RF,1                                                             
*                                                                               
         USING FHD,R4                                                           
OUTG020  OI    FHOI,FHOITR                                                      
         EX    RF,*+8                    OUTPUT TO SCREEN                       
         B     *+10                                                             
         MVC   FHDA(0),0(R1)                                                    
         LA    R1,1(RF,R1)                                                      
*                                                                               
         CR    R4,R0                     REACHED THE LAST LINE?                 
         BNL   OUTGX                     . YES                                  
         AR    R4,RE                     BUMP TO NEX LINE                       
         B     OUTG020                                                          
         DROP  R4                                                               
*                                                                               
OUTGX    J     EXIT                                                             
         DROP  R3                                                               
         LTORG                                                                  
         EJECT                                                                  
                                                                                
***********************************************************************         
* INITIALIZE ROUTINE VALUES                                                     
***********************************************************************         
         USING GRIDFD,R3                                                        
SETPARMS NTR1  BASE=*,LABEL=*                                                   
*                                                                               
         MVC   CALLTYPE,0(R1)      GRID COLUMN SELECTOR                         
         SR    R3,R3                                                            
         ICM   R3,7,1(R1)                                                       
         BNZ   *+6                                                              
         DC    H'0'                                                             
         ST    R3,AGFT             A(GRID FORMAT TABLE)                         
*                                                                               
         ICM   R4,15,4(R1)                                                      
         BNZ   *+6                                                              
         DC    H'0'                                                             
         ST    R4,APTWA            A(APP TWA)                                   
*                                                                               
         ICM   R5,15,8(R1)                                                      
         BNZ   *+6                                                              
         DC    H'0'                                                             
         ST    R5,APWS             A(APP WORKING STORAGE)                       
*                                                                               
         ICM   RF,15,12(R1)                                                     
         BNZ   *+6                                                              
         DC    H'0'                                                             
         ST    RF,ACOM             A(COMFACS)                                   
*                                                                               
         LR    R2,R5                                                            
         AH    R2,GRFCSEL          DISP IN APP W/S TO COLUMN SELECTOR           
         MVC   COLSEL,0(R2)        GRID COLUMN SELECTOR                         
*                                                                               
         LR    R2,R5                                                            
         AH    R2,GRFGSTAT         DISP IN APP W/S TO GRID STATUS BYTE          
         MVC   GRIDSTAT,0(R2)      GRID STATUS BYTE                             
*                                                                               
         LR    R2,R5                                                            
         AH    R2,GRFAIO           DISP IN APP W/S TO 1ST A(IOAREA)             
         ST    R2,AAIO             APPS FIRST A(IOAREA)                         
*                                                                               
         LR    R2,R5                                                            
         AH    R2,GRFDLCB          DISP IN APP W/S TO DLCB (BY DLFLD)           
         ST    R2,ADLCB            DOWNLOAD CONTROL BLOCK                       
*                                                                               
         LR    R2,R5                                                            
         AH    R2,GRFGSB           DISP IN APP W/S TO GSB (2K BUFFER)           
         ST    R2,AGSB             GRID SCREEN BLOCK                            
*                                                                               
         LR    R2,R5                                                            
         AH    R2,GRFMESO          DISP IN APP W/S TO MESSGE AREA               
         ST    R2,AMESO            MESSAGE OUTPUT FIELD                         
*                                                                               
         LR    R2,R5                                                            
         TM    GRFIND,GRFPCWS      PCDRIVE AREA IN W/S?                         
         BO    *+6                 . YES                                        
         LR    R2,R4               . NO, ASSUME IN TWA                          
         AH    R2,GRFPCDR          DISP IN TWA TO PCDRIVEN AREA                 
         ST    R2,APCD             PCDRIVEN                                     
*                                                                               
         XC    FLAG,FLAG                                                        
         J     EXIT                                                             
         DROP  R3                                                               
         LTORG                                                                  
         EJECT                                                                  
                                                                                
***********************************************************************         
* LOCAL W/S                                                                     
***********************************************************************         
WORKD    DSECT                                                                  
PARAMS   DS    8F                                                               
WORK     DS    XL64                                                             
DUB      DS    D                                                                
PACK16   DS    PL16                                                             
FULL     DS    F                                                                
HALF     DS    H                                                                
BYTE     DS    X                                                                
FLAG     DS    XL1                                                              
FG1STQ   EQU   X'80'               . SPECIAL PROCESS FOR 1ST COL                
FGRCCQ   EQU   X'08'               . RECURRING COLUMN                           
FKEYFQ   EQU   X'01'               . KEY FIELD CHANGE                           
*                                                                               
RGRCID   DS    XL(L'GRCID)         ROUTINE GRID COLUMN ID                       
RGRCID#  DS    XL2                 ROUTINE GRID RECURRING COL ID #              
COLSEL   DS    X                   COLUMN SELECTOR                              
CALLTYPE DS    C                   GRID CALL TYPE                               
GRIDENDQ EQU   C'E'                GRID ENDED                                   
GRIDCLRQ EQU   C'C'                CLEAR GRID SCREEN                            
GRIDSTAT DS    X                                                                
*                                                                               
BIGWORK  DS    XL256                                                            
*                                                                               
AGFT     DS    A                   GRID FORMAT TABLE                            
AAIO     DS    A                   APPS FIRST A(IOAREA)                         
APCD     DS    A                   PCDRIVEN                                     
APTWA    DS    A                   APPS TWA                                     
APWS     DS    A                   APPS WORKING STORAGE                         
AMESO    DS    A                   MESSAGE OUTPUT FIELD                         
AGSB     DS    A                   GRID SCREEN BLOCK                            
ACOM     DS    A                   COMFACS                                      
ADLCB    DS    A                   DOWNLOAD CONTROL BLOCK                       
WORKX    EQU   *-WORKD                                                          
                                                                                
***********************************************************************         
* GRID ROUTINE EQUATES                                                          
***********************************************************************         
GINI     EQU   1         DOWN-LOAD INITIALIZATION                               
GEOL     EQU   2         MARK END OF LINE                                       
GEND     EQU   3         MARK END OF OUTPUT                                     
GTXT     EQU   4         OUTPUT TEXT                                            
GNUM     EQU   5         OUTPUT NUMBER                                          
GBLNK    EQU   10        OUTPUT A BLANK                                         
GCLR     EQU   20        CLEAR OUTPUT SCREEN                                    
                                                                                
***********************************************************************         
* INCLUDED DSECTS                                                               
***********************************************************************         
** DDGRIDD                                                                      
** DDDLCB                                                                       
** DDDICTATED                                                                   
** DDFHD                                                                        
** DDCOMFACSD                                                                   
** FATWA                                                                        
         PRINT OFF                                                              
       ++INCLUDE DDGRIDD                                                        
       ++INCLUDE DDDLCB                                                         
       ++INCLUDE DDDICTATED                                                     
       ++INCLUDE DDFH                                                           
       ++INCLUDE DDCOMFACSD                                                     
       ++INCLUDE FATWA                                                          
         PRINT ON                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'006DDGRID    10/01/10'                                      
         END                                                                    
