*          DATA SET REREPVALM2 AT LEVEL 046 AS OF 05/01/02                      
*CATALP REPVALMN                                                                
         TITLE 'MODULE TO CALCULATE CONTRACT VALUES'                            
VALUEMON CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 20,**VALU**                                                      
         USING VALUED,RC                                                        
         MVC   SAVEPARA,0(R1)                                                   
         LM    R2,R7,0(R1)                                                      
*        R2    PARAMETER 1         A(CONTRACT)                                  
*        R3    PARAMETER 2         A(OUTPUT AREA)                               
*        R4    PARAMETER 3 0       C=CALENDAR  4=4-WEEK                         
*                                  B=BROADCAST 5=5WEEK                          
*                          1-3     A(INFORMATION BLOCK)                         
*        R5    PARAMETER 4         A(4-BYTE FORECAST DATE INFORMATION)          
*        R6    PARAMETER 5         A(08-BYTE) TO FROM ACTIVITY DATES            
*        R7    PARAMETER 6         KEYMONTH (LAST REQUESTED MONTH)              
         EJECT                                                                  
*              SET UP VALUES                                                    
         SPACE 3                                                                
         ST    R4,AMONINFO       SAVE ADDRESS OF MONINFO                        
         MVC   KEYMONTH(8),0(R7)                                                
         SLL   R6,8                                                             
         SRL   R6,8                                                             
         XC    WEEK(12),WEEK                                                    
         LTR   R6,R6                                                            
         BZ    VL1                                                              
         MVC   ACTSTART,0(R6)                                                   
         MVC   LASTACTS,4(R6)                                                   
         MVC   ACTEND,2(R6)                                                     
         MVC   LASTACTE,6(R6)                                                   
         SPACE 2                                                                
VL1      DS    0H                                                               
         MVC   WEEK,0(R5)                                                       
         MVC   LASTWEEK,2(R5)                                                   
         LA    R6,24                                                            
         EJECT                                                                  
*              LOOP THROUGH MONTH TABLE TO CONTROL VALUES                       
         SPACE 3                                                                
VL2      CLI   0(R3),0                                                          
         BE    VLEXT                                                            
         NI    6(R3),X'F0'                                                      
         XC    8(32,R3),8(R3)                                                   
         LR    R8,R3                                                            
         LA    R3,8(R3)            THIS YEAR                                    
         LA    R5,WEEK                                                          
         CLI   SAVEPARA+16,C'A'                                                 
         BE    VL10                                                             
         CLI   SAVEPARA+16,C'T'    FOR TAPE                                     
         BE    VL10                USE ACCOUNTING DATA                          
         MVI   INVANY,0                                                         
         BAS   RE,GETVALS                                                       
         OC    6(1,R8),INVANY      SETON/OFF INVOICE INDICATOR                  
         LA    R4,8(R4)                                                         
         LA    R3,32(R3)                                                        
         BCT   R6,VL2                                                           
         SPACE 2                                                                
VLEXT    L     R3,SAVEPARA+4       REPOINT TO OUTPUT AREA                       
         CLI   SAVEPARA+16,C'P'    TEST IF PENNIES WANTED                       
         BE    VLEXIT                                                           
         CLI   SAVEPARA+16,C'T'    FOR TAPE                                     
         BE    VLEXIT              DO NOT ROUND                                 
RD2      CLI   0(R3),0                                                          
*                                  ROUND OUT PENNIES                            
         BE    VLEXIT                                                           
         LA    R3,8(R3)                                                         
         LA    R5,8                                                             
*                                                                               
RD4      OC    0(4,R3),0(R3)                                                    
         BZ    RD6                                                              
*                                                                               
         L     R6,0(R3)                                                         
         SRDA  R6,31               DOUBLE                                       
         D     R6,=F'100'          DIVIDE OUT PENNIES                           
         LTR   R7,R7               TESP QUOTIENT NEGATIVE                       
         BL    *+8                                                              
         AH    R7,=H'1'            ADD 1 TO ROUND ONLY ON POSITIVE              
         SRA   R7,1                HALF                                         
         ST    R7,0(R3)                                                         
*                                                                               
RD6      LA    R3,4(R3)                                                         
         BCT   R5,RD4                                                           
         B     RD2                                                              
         SPACE 2                                                                
VLEXIT   XMOD1 1                                                                
         EJECT                                                                  
         SPACE 2                                                                
VL10     BAS   RE,ACCTVALS                                                      
         OC    6(1,R8),INVANY                                                   
         CLI   32(R3),0            TEST IF KEY MONTH=THIS MONTH                 
         BNE   VL12                                                             
         CLI   INVANY,0            YES- TEST IF ANY INVOICES                    
         BNE   VL16                     YES- ACCT DATA=INVOICE                  
         L     R0,0(R3)                 NO - ACCT DATA=ALL ESTIMATES            
         A     R0,4(R3)                                                         
         ST    R0,12(R3)                                                        
         B     VL20                                                             
*                                                                               
VL12     CLI   INVANY,0            TEST IF ANY INVOICES                         
         BNE   VL14                                                             
         MVC   12(4,R3),0(R3)      NO-ESTIMATES THIS MONTH ARE ADJUSTMT         
         B     VL20                                                             
*                                                                               
VL14     CLI   INVPREV,0           TEST ANY PREVIOUS INVOICES                   
         BNE   VL16                                                             
         L     R0,8(R3)            NO-ADJUST INVS LESS ALL PREV EST             
         S     R0,4(R3)                                                         
         ST    R0,12(R3)                                                        
         B     VL20                                                             
*                                                                               
VL16     MVC   12(4,R3),8(R3)      YES-ACCT DATA=INVOICE THIS MONTH             
*                                                                               
VL20     LA    R4,8(R4)                                                         
         LA    R3,32(R3)                                                        
         BCT   R6,VL2                                                           
         B     VLEXT                                                            
         EJECT                                                                  
*              SCAN THE CONTRACT FOR VALUES                                     
         SPACE 3                                                                
GETVALS  NTR1                                                                   
         LA    R2,34(R2)           R2 A(CONTRACT)                               
*                                  R3 A(OUTPUT)                                 
*                                  R4 A(SERVICE)                                
*                                  R5 A(2-BYTE MONDAY)                          
         SPACE 2                                                                
GV2      CLI   0(R2),0             TEST END OF CONTRACT                         
         BE    XIT                                                              
         CLI   0(R2),X'03'         TEST FOR ORDERED BUCKET                      
         BNE   GV10                                                             
         USING RCONBKEL,R2                                                      
         CLC   RCONBKYR(2),0(R4)   MATCH MONTH OF SERVICE - THIS YEAR           
         BNE   GV6                                                              
         MVC   DUB,RCONBKAM                                                     
         CLC   RCONBKWK,0(R5)      TEST ORDER PLACED BY AS AT DATE              
         BH    GV4                                                              
         SPACE 1                                                                
         L     R6,DUB                                                           
         A     R6,0(R3)                                                         
         ST    R6,0(R3)                                                         
*                                                                               
GV4      OC    SAVEPARA+17(3),SAVEPARA+17    TEST ACTIVITY PERIOD               
         BZ    GV20                                                             
         CLC   RCONBKWK,2(R5)      START DATE                                   
         BL    GV20                                                             
         CLC   RCONBKWK,4(R5)      END  DATE                                    
         BH    GV20                                                             
         L     R6,DUB              APPLY AMOUNT TO ACTIVITY PERIOD              
         A     R6,8(R3)            CURRENT GROSS                                
         ST    R6,8(R3)                                                         
         B     GV20                                                             
         SPACE 2                                                                
GV6      CLC   RCONBKYR(2),4(R4)   MATCH MONTH OF SERVICE - LAST YEAR           
         BNE   GV20                                                             
         MVC   DUB,RCONBKAM        APPLY TO PRIOR TOTAL ORDERED                 
         L     R6,DUB                                                           
         A     R6,24(R3)                                                        
         ST    R6,24(R3)                                                        
*                                                                               
         CLC   RCONBKWK,6(R5)      TEST LAST YEAR AS AT DATE                    
         BH    GV8                                                              
         L     R6,DUB              APPLY TO PRIOR AS AT ORDERED                 
         A     R6,12(R3)                                                        
         ST    R6,12(R3)                                                        
         SPACE 1                                                                
*                                                                               
GV8      OC    SAVEPARA+17(3),SAVEPARA+17    TEST FOR ACTIVITY PERIOD           
         BZ    GV20                                                             
         CLC   RCONBKWK,8(R5)      START DATE                                   
         BL    GV20                                                             
         CLC   RCONBKWK,10(R5)      END DATE                                    
         BH    GV20                                                             
         L     R6,DUB              APPLY TO ACTIVITY PER. PRIOR ORDERED         
         A     R6,20(R3)                                                        
         ST    R6,20(R3)                                                        
         B     GV20                                                             
         SPACE 2                                                                
GV10     CLI   0(R2),X'04'         TEST FOR INVOICE BUCKET                      
         BNE   GV20                                                             
         USING RCONSTEL,R2                                                      
*                                                                               
         CLC   RCONSTYR(2),0(R4)   MATCH MONTH OF SERVICE - THIS YEAR           
         BNE   GV12                                                             
         CLC   RCONSTWK,0(R5)      TEST THIS YEAR AS AT DATE                    
         BH    GV20                                                             
         MVC   DUB,RCONSTAM                                                     
         L     R6,DUB              APPLY TO CURRENT AS AT INVOICE               
         A     R6,4(R3)                                                         
         ST    R6,4(R3)                                                         
         OI    INVANY,X'01'        SET CURRENT INVOICE SWITCH                   
         SPACE 1                                                                
GV12     CLC   RCONSTYR(2),4(R4)   MATCH MONTH OF SERVICE - LAST YEAR           
         BNE   GV20                                                             
         MVC   DUB,RCONSTAM                                                     
         L     R6,DUB              APPLY TO PRIOR TOTAL INVOICE                 
         A     R6,28(R3)                                                        
         ST    R6,28(R3)                                                        
         OI    INVANY,X'02'        SET ANY PRIOR INVOICE SWITCH                 
*                                                                               
         CLC   RCONSTWK,6(R5)      TEST LAST YEAR AS AT DATE                    
         BH    GV20                                                             
         L     R6,DUB              APPLY TO PRIOR AS AT INVOICE                 
         A     R6,16(R3)                                                        
         ST    R6,16(R3)                                                        
         OI    INVANY,X'04'        SET PRIOR AS AT INVOICE SWITCH               
         B     GV20                                                             
         SPACE 2                                                                
GV20     SR    R6,R6                                                            
         IC    R6,1(R2)                                                         
         AR    R2,R6                                                            
         B     GV2                                                              
         SPACE 2                                                                
XIT      XIT1                                                                   
         EJECT                                                                  
ACCTVALS NTR1                                                                   
         LA    R2,34(R2)           R2 A(CONTRACT)                               
*                                  R3 A(OUTPUT TABLE)                           
*                                  R4 A(INFO TABLE)                             
         XC    INVPREV(2),INVPREV                                               
         SPACE 2                                                                
AV2      CLI   0(R2),0                                                          
         BE    XIT                                                              
         CLI   0(R2),X'03'         ESTIMATE BUCKET                              
         BNE   AV6                                                              
         USING RCONBKEL,R2                                                      
         CLC   RCONBKYR(2),0(R4)   MATCH MONTH OF SERVICE                       
         BE    AV4                                                              
         C     R4,AMONINFO      MONINFO AT FIRST REQUESTED MONTH                
         BNE   AV8        DO NOT NEED                                           
         CLC   RCONBKYR(2),0(R4)       MONTH OF SERVICE, LESS THAN 1ST          
*                                      REQUESTED MONTH                          
         BH    AV8          DO NOT NEED                                         
AV4      MVC   FULL,RCONBKAM                                                    
         L     R0,FULL                                                          
         LR    R7,R0                                                            
*                                  FIND IF ACTIVITY OCCURED IN KEYMONTH         
         CLC   RCONBKWK,KEYMONE                                                 
         BH    AV8                                                              
         CLC   RCONBKWK,KEYMONS                                                 
         BNL   *+16                                                             
         A     R0,4(R3)            PREVIOUS MONTH                               
         ST    R0,4(R3)                                                         
         B     AV8                                                              
         A     R0,0(R3)            THIS MONTH                                   
         ST    R0,0(R3)                                                         
         B     AV8                                                              
         DROP  R2                                                               
         SPACE 2                                                                
AV6      CLI   0(R2),X'04'         INVOICE BUCKET                               
         BNE   AV8                                                              
         USING RCONSTEL,R2                                                      
         CLC   RCONSTYR(2),0(R4)      MATCH ON MONTH OF SERVICE                 
         BE    AV7                                                              
         C     R4,AMONINFO     1ST REQUESTED MONTH                              
         BNE   AV8                                                              
         CLC   RCONSTYR(2),0(R4)     USE PRIOR MONTH                            
         BH    AV8                                                              
AV7      MVC   FULL,RCONSTAM                                                    
         L     R0,FULL                                                          
*        TEST ACTIVITY IN KEYMONTH                                              
         CLC   RCONSTWK,KEYMONE                                                 
         BH    AV8                                                              
         MVI   INVANY,1            YES-THERE ARE INVOICES                       
         CLC   RCONSTWK,KEYMONS                                                 
         BNL   *+12                                                             
         MVI   INVPREV,1           YES-THERE ARE PREVIOUS INVOICES              
         B     AV8                                                              
         A     R0,8(R3)                                                         
         ST    R0,8(R3)                                                         
         B     AV8                                                              
AV8      SR    R6,R6                                                            
         IC    R6,1(R2)                                                         
         AR    R2,R6                                                            
         B     AV2                                                              
         EJECT                                                                  
*              DSECT FOR THE MODULE                                             
         SPACE 3                                                                
VALUED   DSECT                                                                  
WORK     DS    CL32                                                             
PARA     DS    F                                                                
DUB      DS    D                                                                
WEEK     DS    CL2                                                              
ACTSTART DS    CL2                                                              
ACTEND   DS    CL2                                                              
LASTWEEK DS    CL2                                                              
LASTACTS DS    CL2                                                              
LASTACTE DS    CL2                                                              
THISSERV DS    CL8                                                              
LASTSERV DS    CL8                                                              
BASIS    DS    CL1                                                              
         DS    0F                                                               
SAVEPARA DS    CL20                                                             
ACCADJ   DS    D                                                                
INVPREV  DS    CL1                                                              
INVANY   DS    CL1                                                              
KEYMONTH DS    0F                                                               
KEYMONS  DS    H                                                                
KEYMONE  DS    H                                                                
LASTMONS DS    H                                                                
LASTMONE DS    H                                                                
FULL     DS    F                                                                
AMONINFO DS    F            ADDRESS OF MONINFO                                  
         PRINT OFF                                                              
         EJECT                                                                  
       ++INCLUDE REGENCON                                                       
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'046REREPVALM205/01/02'                                      
         END                                                                    
