*          DATA SET DDLIST     AT LEVEL 032 AS OF 07/26/13                      
*CATALP LISTIO                                                                  
LISTIO   TITLE '- INTERFACE TO LISTING SYSTEM RECORDS'                          
LISTIO   CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 WORKL,**LIST**,RA,RR=RE,CLEAR=YES                                
         USING WORKD,RC                                                         
         ST    RE,RELO                                                          
         LR    R8,R1                                                            
         USING LISTD,R8                  R8=A(CONTROL BLOCK)                    
*                                                                               
         BRAS  RE,INIT                   INITIALIZE                             
*                                                                               
         SR    RF,RF                                                            
         ICM   RF,1,LISTACTN                                                    
         BNZ   *+6                                                              
         DC    H'0'                      INVALID ACTION CODE                    
         CHI   RF,MAXACTS                                                       
         BNH   *+6                                                              
         DC    H'0'                      INVALID ACTION CODE                    
         BCTR  RF,0                                                             
         SLL   RF,2                                                             
*                                                                               
         LA    RE,LSTATAB                POINT TO LIST BRANCH TABLE             
         AR    RE,RF                     ADD INDEX TO ADDRESS                   
         BR    RE                        GO TO ACTION BRANCH                    
*                                                                               
LSTATAB  DS    0XL4                      ** LIST BRANCH TABLE **                
         B     LSTINF         LISTAINF - GET LIST INFORMATION                   
         B     LSTADD         LISTAADD - ADD A NEW LIST                         
         B     LSTDOR         LISTADEL - DELETE A LIST                          
         B     LSTDOR         LISTARES - RESTORE A LIST                         
         B     LSTINS         LISTAINS - INSERT DATA INTO A LIST                
         B     LSTREM         LISTAREM - REMOVE DATA FROM A LIST                
         B     LSTGET         LISTAGET - GET DATA VALUE FROM LIST               
         B     LSTGET         LISTANXT - GET NEXT DATA VALUE FROM LIST          
         B     LSTCHK         LISTACHK - CHECK LIST INCLUDES DATA               
         B     LSTHAS         LISTAHAS - CHECK LIST HAS DATA IN IT              
         B     LSTTYP         LISTATYP - CHECK LIST TYPE                        
         B     LSTINF         LISTANLG - GET NEXT LIST FOR GROUP                
         B     LSTCON         LISTACON - CONVERT DATA TO RECORD/DISPLAY         
         B     LSTDER         LISTAIDL - INSERT DERIVED LIST INTO LIST          
         B     LSTDER         LISTARDL - REMOVE DERIVED LIST FROM LIST          
*                                                                               
MAXACTS  EQU   (*-LSTATAB)/L'LSTATAB                                            
*----------------------------------------                                       
* EXITS                                                                         
*----------------------------------------                                       
DEX      MVC   LISTDMER,DMCB+8           EXIT W/DATAMGR RETURN ERROR            
         MVI   LISTERRS,LEIOER           IO ERROR                               
*                                                                               
LEX      CLI   SVTSYS,0                  DID WE SWAP SYSTEMS?                   
         BE    LEXIT                                                            
         ICM   R1,15,VMASTC              MASTC RESOLVED?                        
         BZ    LEXIT                     . NO, ONLINE                           
         L     R1,MCUTL-MASTD(R1)        A(UTL)                                 
         MVC   TSYS-UTLD(1,R1),SVTSYS    RESTORE CALLING SYSTEM NUMBER          
*                                                                               
LEXIT    CLI   LISTERRS,0                EXIT DDLIST                            
         J     EXIT                                                             
*                                                                               
NOX      LTR   RB,RB                     EXIT W/CC NOT EQUAL                    
         J     EXIT                                                             
OKX      CR    RB,RB                     EXIT W/CC EQUAL                        
         J     EXIT                                                             
*                                                                               
EXIT     XIT1                                                                   
         EJECT                                                                  
                                                                                
*======================================================================         
* GET LIST INFORMATION                                                          
*======================================================================         
LSTINF   XC    IOKEY,IOKEY                                                      
         LA    R2,IOKEY                                                         
*                                                                               
         ICM   R1,15,LISTINUM            DO WE HAVE A LIST NUMBER?              
         BZ    LINF030                   . NO, LOOK UP WITH CODE                
         CLI   LISTACTN,LISTAINF                                                
         BNE   LINF030                                                          
*                                                                               
         USING LNUMD,R2                                                         
         MVI   LNUMKMAJ,LNUMKMAQ         C'L' LIST SYSTEM RECORD                
         MVI   LNUMKMIN,LNUMKMIQ         C'S' SECURITY DATA ACCESS              
         MVI   LNUMKREC,LNUMKREQ         C'N' NUMBER RECORD                     
         MVC   LNUMKNUM,LISTINUM                                                
*                                                                               
         MVC   IOKEYSV,IOKEY                                                    
         GOTO1 VDMGR,DMCB,(X'08',DMRDHI),GENDIR,IOKEYSV,IOKEY,0                 
         BE    *+12                                                             
         TM    DMCB+8,X'02'              RECORD IS DELETED                      
         BZ    DEX                                                              
*                                                                               
         CLC   LNUMKEY(LNUMKAGY-LNUMD),IOKEYSV                                  
         BE    LINF020                                                          
         MVI   DMCB+8,X'10'              GIVE RECORD NOT FOUND ERROR            
         B     DEX                                                              
*                                                                               
LINF020  GOTO1 VDMGR,DMCB,(X'08',GETREC),GENFIL,LNDDA,AIO1,DMWORK               
         BE    *+12                                                             
         TM    DMCB+8,X'02'              RECORD IS DELETED                      
         BZ    DEX                                                              
         B     LINF050                                                          
*                                                                               
         USING LLSTD,R2                                                         
LINF030  MVI   LLSTKMAJ,LLSTKMAQ         C'L' LIST SYSTEM RECORD                
         MVI   LLSTKMIN,LLSTKMIQ         C'S' SECURITY DATA ACCESS              
         MVI   LLSTKREC,LLSTKREQ         C'L' LIST RECORD                       
         MVC   LLSTKAGY,LISTIAGY                                                
         MVC   LLSTKGRP,LISTIGRP                                                
         MVC   LLSTKSYS,LISTISYS                                                
         MVC   LLSTKTYP,LISTITYP                                                
*                                                                               
         CLI   LISTACTN,LISTAINF                                                
         BE    LINF040                                                          
         MVI   LLSTKEY+(LLSTKEYL-1),X'FF'                                       
*                                                                               
         MVC   IOKEYSV,IOKEY                                                    
         GOTO1 VDMGR,DMCB,(X'08',DMRDHI),GENDIR,IOKEYSV,IOKEY,0                 
         BE    *+12                                                             
         TM    DMCB+8,X'02'              RECORD IS DELETED                      
         BZ    DEX                                                              
         CLC   LLSTKEY(LLSTKSYS-LLSTKEY),IOKEYSV                                
         BE    LINF042                                                          
         MVI   DMCB+8,X'10'              GIVE RECORD NOT FOUND ERROR            
         B     DEX                                                              
*                                                                               
LINF040  MVC   IOKEYSV,IOKEY                                                    
         GOTO1 VDMGR,DMCB,(X'08',DMREAD),GENDIR,IOKEYSV,IOKEY,0                 
         BE    *+12                                                             
         TM    DMCB+8,X'02'              RECORD IS DELETED                      
         BZ    DEX                                                              
*                                                                               
LINF042  GOTO1 VDMGR,DMCB,(X'08',GETREC),GENFIL,LLDDA,AIO1,DMWORK               
         BE    *+12                                                             
         TM    DMCB+8,X'02'              RECORD IS DELETED                      
         BZ    DEX                                                              
*                                                                               
LINF050  L     R2,AIO1                                                          
         USING LLSTD,R2                                                         
         MVC   LISTIAGY,LLSTKAGY         AGENCY                                 
         MVC   LISTIGRP,LLSTKGRP         CODE                                   
         MVC   LISTISYS,LLSTKSYS         SYSTEM                                 
         MVC   LISTITYP,LLSTKTYP         TYPE                                   
         MVC   LISTIATT,LLFATT           ATTRIBUTES                             
*                                                                               
         XC    LISTINUM,LISTINUM                                                
         XC    ELEM,ELEM                                                        
         LA    R3,ELEM                                                          
         USING LLNUD,R3                                                         
         MVI   LLNUEL,LLNUELQ            GET LIST NUMBER ELEMENT                
         GOTO1 GETELS,LLSTD                                                     
         OC    FULL,FULL                                                        
         BZ    LINFLNFX                                                         
         L     R3,FULL                                                          
         MVC   LISTINUM,LLNUNU           LIST NUMBER                            
*                                                                               
         XC    LISTICNT,LISTICNT                                                
         XC    ELEM,ELEM                                                        
         LA    R3,ELEM                                                          
         USING LCNTD,R3                                                         
         MVI   LCNTEL,LCNTELQ            GET LIST DATA COUNT                    
         GOTO1 GETELS,LLSTD                                                     
         OC    FULL,FULL                                                        
         BZ    LINF060                                                          
         L     R3,FULL                                                          
         MVC   LISTICNT,LCNTCNT          LIST DATA COUNT                        
*---------------------------------------                                        
* DERIVED LIST CODE - CURRENTLY HANDLES ONE OFFICE LIST ONLY                    
*---------------------------------------                                        
LINF060  XC    ELEM,ELEM                                                        
         LA    R3,ELEM                                                          
         USING LDERD,R3                                                         
         MVI   LDEREL,LDERELQ            GET LIST DATA COUNT                    
         GOTO1 GETELS,LLSTD                                                     
         OC    FULL,FULL                                                        
         BZ    LINFX                                                            
         L     R3,FULL                                                          
         MVC   LISTIDER(L'LDEROFF),LDEROFF                                      
*                                                                               
LINFX    TM    LLFSTAT,X'80'             RETURN SAYING RECORD DELETED           
         BZ    LEX                                                              
         MVI   DMCB+8,X'02'              GIVE RECORD DELETED ERROR              
         B     DEX                                                              
*                                                                               
LINFLNFX MVI   LISTERRS,LELNNF           LIST # NOT FOUND ON LIST REC           
         B     LEX                                                              
         DROP  R2,R3                                                            
         EJECT                                                                  
                                                                                
*======================================================================         
* ADD A NEW LIST                                                                
*    ON ENTRY  LISTIGRP, LISTITYP, LISTIAGY, LISTISYS                           
*======================================================================         
LSTADD   LA    R2,IOKEY                                                         
         USING LNUMD,R2                                                         
         XC    IOKEY,IOKEY                                                      
         MVI   LNUMKMAJ,LNUMKMAQ         C'L' LIST SYSTEM RECORD                
         MVI   LNUMKMIN,LNUMKMIQ         C'S' SECURITY DATA ACCESS              
         MVI   LNUMKREC,LNUMKREQ         C'N' NUMBER RECORD                     
*                                                                               
         MVC   IOKEYSV,IOKEY                                                    
         GOTO1 VDMGR,DMCB,(X'88',DMRDHI),GENDIR,IOKEYSV,IOKEY,0                 
         BE    LADD010                                                          
         TM    DMCB+8,X'02'              RECORD IS DELETED                      
         BO    LADD010                                                          
         B     DEX                                                              
*                                                                               
LADD010  CLC   LNUMD(LNUMKNUM-LNUMD),IOKEYSV                                    
         BE    *+10                                                             
         MVC   LNUMKNUM,=X'FFFFFFFF'     FIRST EVER LIST X'FFFFFFFE'            
*                                                                               
         ICM   R1,15,LNUMKNUM            PICK UP NUMBER                         
         BCTR  R1,0                      DECREMENT FOR NEXT NUMBER              
*                                                                               
         LTR   R1,R1                     ZERO? WE DON'T ALLOW ZERO              
         BNZ   *+6                       . NOT ZERO, GOOD                       
         DC    H'0'                      4+ BILLION LISTS, OR A BUG?            
*                                                                               
LADD012  STCM  R1,15,LISTINUM            NEXT AVAILABLE                         
*                                                                               
         CLC   LNUMKGRP,ZEROS            ZERO GROUP NUMBER?                     
         BNE   *+6                                                              
         DC    H'0'                      THIS SHOULDN'T BE                      
         DROP  R2                                                               
*----------------------------------------                                       
* ADD LIST LIST RECORD - PRIMARY                                                
*----------------------------------------                                       
         L     R2,AIO1                                                          
         USING LLSTD,R2                                                         
         XC    LLSTD(LLFIRST+2),LLSTD                                           
         MVI   LLSTKMAJ,LLSTKMAQ         C'L' LIST SYSTEM RECORD                
         MVI   LLSTKMIN,LLSTKMIQ         C'S' SECURITY DATA ACCESS              
         MVI   LLSTKREC,LLSTKREQ         C'L' LIST RECORD                       
         MVC   LLSTKAGY,LISTIAGY                                                
         MVC   LLSTKGRP,LISTIGRP         PRIMARY LIST CODE RECORD               
         MVC   LLSTKTYP,LISTITYP                                                
         MVC   LLSTKSYS,LISTISYS                                                
         LHI   R1,LLFIRST                                                       
         STCM  R1,3,LLFLEN                                                      
*                                                                               
         XC    ELEM,ELEM                                                        
         LA    R3,ELEM                                                          
         USING LLNUD,R3                                                         
         MVI   LLNUEL,LLNUELQ                                                   
         MVI   LLNULN,LLNULNQ                                                   
         MVC   LLNUNU,LISTINUM                                                  
         GOTO1 ADDELS,LLSTD                                                     
*                                                                               
         GOTO1 SETACT,LLSTD                                                     
*                                                                               
         MVC   IOKEY,0(R2)                                                      
         GOTO1 VDMGR,DMCB,ADDREC,GENFIL,DMIODA,AIO1,DMWORK                      
         BNE   DEX                                                              
         DROP  R2                                                               
*----------------------------------------                                       
* ADD LIST NUMBER RECORD - PASSIVE                                              
*----------------------------------------                                       
         XC    IOKEY,IOKEY                                                      
         LA    R2,IOKEY                                                         
         USING LNUMD,R2                                                         
         MVI   LNUMKMAJ,LNUMKMAQ         C'L' LIST SYSTEM RECORD                
         MVI   LNUMKMIN,LNUMKMIQ         C'S' SECURITY DATA ACCESS              
         MVI   LNUMKREC,LNUMKREQ         C'N' NUMBER RECORD                     
         MVC   LNUMKNUM,LISTINUM                                                
         MVC   LNUMKAGY,LISTIAGY                                                
         MVC   LNUMKGRP,LISTIGRP                                                
         MVC   LNUMKSYS,LISTISYS                                                
         MVC   LNUMKTYP,LISTITYP                                                
         MVC   LNDDA,DMIODA              D/A FROM PREVIOUS ADDREC               
*                                                                               
         MVC   IOKEYSV,IOKEY                                                    
         GOTO1 VDMGR,DMCB,DMADD,GENDIR,IOKEYSV,IOKEY,0                          
         BE    *+6                                                              
         DC    H'0'                      NEED TO DIE HERE, BIG PROBLEM          
         B     LEX                                                              
         EJECT                                                                  
                                                                                
*======================================================================         
* DELETE OR RESTORE LIST                                                        
*======================================================================         
LSTDOR   MVI   BYTE,0                    USE BYTE TO FLAG WHAT'S DONE           
*                                                                               
         ICM   R1,15,LISTINUM                                                   
         BNZ   LDOR020                                                          
*                                                                               
LDOR010  LA    R2,IOKEY                                                         
         USING LLSTD,R2                                                         
         XC    IOKEY,IOKEY                                                      
         MVI   LLSTKMAJ,LLSTKMAQ         C'L' LIST SYSTEM RECORD                
         MVI   LLSTKMIN,LLSTKMIQ         C'S' SECURITY DATA ACCESS              
         MVI   LLSTKREC,LLSTKREQ         C'L' LIST RECORD                       
         MVC   LLSTKAGY,LISTIAGY                                                
         MVC   LLSTKGRP,LISTIGRP         PRIMARY LIST CODE RECORD               
         MVC   LLSTKSYS,LISTISYS                                                
         MVC   LLSTKTYP,LISTITYP                                                
*                                                                               
         MVC   IOKEYSV,IOKEY                                                    
         GOTO1 VDMGR,DMCB,(X'88',DMREAD),GENDIR,IOKEYSV,IOKEY,0                 
         BE    LDOR012                                                          
         TM    DMCB+8,X'02'              RECORD IS DELETED                      
         BO    *+6                                                              
         DC    H'0'                                                             
*                                                                               
LDOR012  GOTO1 VDMGR,DMCB,(X'88',GETREC),GENFIL,LLDDA,AIO1,DMWORK               
         BE    LDOR014                                                          
         TM    DMCB+8,X'02'              RECORD IS DELETED                      
         BO    *+6                                                              
         DC    H'0'                                                             
*                                                                               
LDOR014  L     R2,AIO1                                                          
         OI    LLFSTAT,X'80'             DELETE DATA RECORD                     
         CLI   LISTACTN,LISTARES         RESTORE?                               
         BNE   *+8                       . NO, LEAVE DELETE                     
         NI    LLFSTAT,X'FF'-X'80'       . YES, UNDELETE                        
*                                                                               
         XC    ELEM,ELEM                                                        
         LA    R3,ELEM                                                          
         USING LLNUD,R3                                                         
         MVI   LLNUEL,LLNUELQ            GET LIST NUMBER ELEMENT                
         GOTO1 GETELS,LLSTD                                                     
         OC    FULL,FULL                                                        
         BNZ   *+6                       NO NUMBER, THAT'S A PROBLEM            
         DC    H'0'                                                             
         L     R3,FULL                                                          
         MVC   LISTINUM,LLNUNU           LIST NUMBER                            
         DROP  R3                                                               
*                                                                               
         GOTO1 SETACT,LLSTD                                                     
         GOTO1 VDMGR,DMCB,PUTREC,GENFIL,AIO1,AIO1,DMWORK                        
         BNE   DEX                                                              
*                                                                               
         LA    R2,IOKEY                                                         
         OI    LLDSTAT,X'80'             DELETE DATA RECORD                     
         CLI   LISTACTN,LISTARES         RESTORE?                               
         BNE   *+8                       . NO, LEAVE DELETE                     
         NI    LLDSTAT,X'FF'-X'80'       . YES, UNDELETE                        
*                                                                               
         GOTO1 VDMGR,DMCB,DMWRT,GENDIR,IOKEY,IOKEY,0                            
         BE    *+6                                                              
         DC    H'0'                      NO TURNING BACK NOW                    
*                                                                               
         CLI   BYTE,C'C'                 ALREADY CHANGED NUMBER REC?            
         BE    LDOR060                   . YES, DON'T DO IT AGAIN               
         MVI   BYTE,C'C'                 FLAG LIST LIST RECORD CHANGED          
*----------------------------------------                                       
* DELETE LIST NUMBER RECORD - PASSIVE                                           
*----------------------------------------                                       
LDOR020  XC    IOKEY,IOKEY                                                      
         LA    R2,IOKEY                                                         
         USING LNUMD,R2                                                         
         MVI   LNUMKMAJ,LNUMKMAQ         C'L' LIST SYSTEM RECORD                
         MVI   LNUMKMIN,LNUMKMIQ         C'S' SECURITY DATA ACCESS              
         MVI   LNUMKREC,LNUMKREQ         C'N' NUMBER RECORD                     
         MVC   LNUMKNUM,LISTINUM                                                
*                                                                               
         MVC   IOKEYSV,IOKEY                                                    
         GOTO1 VDMGR,DMCB,(X'88',DMRDHI),GENDIR,IOKEYSV,IOKEY,0                 
         BE    LDOR050                                                          
         TM    DMCB+8,X'02'              RECORD IS DELETED                      
         BO    *+6                                                              
         DC    H'0'                      CAN'T CONTINUE                         
*                                                                               
LDOR050  CLC   LNUMKEY(LNUMKAGY-LNUMKEY),IOKEYSV                                
         BE    *+6                                                              
         DC    H'0'                      CAN'T CONTINUE                         
*                                                                               
         MVC   LISTIAGY,LNUMKAGY                                                
         MVC   LISTIGRP,LNUMKGRP                                                
         MVC   LISTISYS,LNUMKSYS                                                
         MVC   LISTITYP,LNUMKTYP                                                
*                                                                               
         LA    R2,IOKEY                                                         
         OI    LNDSTAT,X'80'             DELETE DATA RECORD                     
         CLI   LISTACTN,LISTARES         RESTORE?                               
         BNE   *+8                       . NO, LEAVE DELETE                     
         NI    LNDSTAT,X'FF'-X'80'       . YES, UNDELETE                        
*                                                                               
         GOTO1 VDMGR,DMCB,DMWRT,GENDIR,IOKEY,IOKEY,0                            
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         CLI   BYTE,C'C'                 ALREADY CHANGED LIST REC?              
         BE    LDOR060                   . YES, DON'T DO IT AGAIN               
         MVI   BYTE,C'C'                 FLAG LIST NUMBER REC CHANGED           
         B     LDOR010                   CHANGE THE LIST LIST RECORD            
*----------------------------------------                                       
* DELETE LIST DATA VALUE RECORDS                                                
*----------------------------------------                                       
LDOR060  LA    R2,IOKEY                                                         
         USING LDTAD,R2                                                         
         XC    IOKEY,IOKEY                                                      
         MVI   LDTAKMAJ,LDTAKMAQ         C'L' - LIST SYSTEM                     
         MVI   LDTAKMIN,LDTAKMIQ         C'S' - SECURITY DATA ACCESS            
         MVI   LDTAKREC,LDTAKREQ         C'D' - DATA RECORD                     
         MVC   LDTAKNUM,LISTINUM                                                
*                                                                               
         MVC   IOKEYSV,IOKEY                                                    
         GOTO1 VDMGR,DMCB,(X'88',DMRDHI),GENDIR,IOKEYSV,IOKEY,0                 
         BE    LDOR080                                                          
         TM    DMCB+8,X'02'              RECORD IS DELETED                      
         BO    LDOR080                                                          
         DC    H'0'                                                             
*                                                                               
LDOR070  MVC   IOKEYSV,IOKEY                                                    
         GOTO1 VDMGR,DMCB,(X'88',DMRSEQ),GENDIR,IOKEYSV,IOKEY,0                 
         BE    LDOR080                                                          
         TM    DMCB+8,X'02'              RECORD IS DELETED                      
         BO    LDOR080                                                          
         DC    H'0'                                                             
*                                                                               
LDOR080  CLC   LDTAD(LDTAKCOD-LDTAD),IOKEYSV                                    
         BNE   LDOR100                                                          
*                                                                               
         GOTO1 VDMGR,DMCB,(X'88',GETREC),GENFIL,LDDDA,AIO1,DMWORK               
         BE    LDOR090                                                          
         TM    DMCB+8,X'02'              RECORD IS DELETED                      
         BO    LDOR090                                                          
         DC    H'0'                                                             
*                                                                               
LDOR090  L     R2,AIO1                                                          
         OI    LDFSTAT,X'80'             DELETE DATA RECORD                     
         CLI   LISTACTN,LISTARES         RESTORE?                               
         BNE   *+8                       . NO, LEAVE DELETE                     
         NI    LDFSTAT,X'FF'-X'80'       . YES, UNDELETE                        
*                                                                               
         GOTO1 SETACT,LDTAD                                                     
         GOTO1 VDMGR,DMCB,PUTREC,GENFIL,AIO1,AIO1,DMWORK                        
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         LA    R2,IOKEY                                                         
         OI    LDDSTAT,X'80'             DELETE DATA RECORD                     
         CLI   LISTACTN,LISTARES         RESTORE?                               
         BNE   *+8                       . NO, LEAVE DELETE                     
         NI    LDDSTAT,X'FF'-X'80'       . YES, UNDELETE                        
*                                                                               
         GOTO1 VDMGR,DMCB,DMWRT,GENDIR,IOKEY,IOKEY,0                            
         BE    *+6                                                              
         DC    H'0'                                                             
         B     LDOR070                                                          
*                                                                               
LDOR100  B     LEX                                                              
         DROP  R2                                                               
         EJECT                                                                  
                                                                                
*======================================================================         
* INSERT A VALUE INTO A GIVEN LIST                                              
*======================================================================         
LSTINS   BAS   RE,SETVAL                 SET DATA VALUES                        
         BNE   LEX                                                              
*                                                                               
         LA    R2,IOKEY                                                         
         USING LDTAD,R2                                                         
         XC    IOKEY,IOKEY                                                      
         MVI   LDTAKMAJ,LDTAKMAQ         C'L' LIST RECORD SYSTEM                
         MVI   LDTAKMIN,LDTAKMIQ         C'S' SECURITY DATA ACCESS              
         MVI   LDTAKREC,LDTAKREQ         C'D' DATA RECORD                       
         MVC   LDTAKNUM,LISTINUM                                                
         MVC   LDTAKCOD,CURDCOD                                                 
*                                                                               
         MVC   IOKEYSV,IOKEY                                                    
         GOTO1 VDMGR,DMCB,(X'88',DMREAD),GENDIR,IOKEYSV,IOKEY,0                 
         BE    LINS020                                                          
         TM    DMCB+8,X'02'              RECORD IS DELETED                      
         BO    LINS040                                                          
*                                                                               
         L     R2,AIO1                                                          
         XC    LDTAD(LDFIRST+2),LDTAD                                           
         MVI   LDTAKMAJ,LDTAKMAQ         C'L' LIST RECORD SYSTEM                
         MVI   LDTAKMIN,LDTAKMIQ         C'S' SECURITY DATA ACCESS              
         MVI   LDTAKREC,LDTAKREQ         C'D' DATA RECORD                       
         MVC   LDTAKNUM,LISTINUM                                                
         MVC   LDTAKCOD,CURDCOD                                                 
         MVC   LDFATT,CURDATT                                                   
         LHI   R1,LDFIRST+1                                                     
         STCM  R1,3,LDFLEN                                                      
*                                                                               
         GOTO1 SETACT,LDTAD              SET ACTIVITY ELEMENT                   
*                                                                               
         MVC   IOKEY,0(R2)                                                      
         GOTO1 VDMGR,DMCB,ADDREC,GENFIL,DMIODA,AIO1,DMWORK                      
         BE    LEX                                                              
         DC    H'0'                                                             
*                                                                               
LINS020  CLC   CURDATT,LDDATT                                                   
         BE    LEX                                                              
         GOTO1 VDMGR,DMCB,(X'88',GETREC),GENFIL,LDDDA,AIO1,DMWORK               
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         L     R2,AIO1                                                          
         MVC   LDFATT,CURDATT            SET ATTRIBUTES                         
         GOTO1 SETACT,LDTAD                                                     
         GOTO1 VDMGR,DMCB,PUTREC,GENFIL,IOKEY,AIO1,DMWORK                       
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         LA    R2,IOKEY                                                         
         MVC   LDDATT,CURDATT            SET ATTRIBUTES                         
         GOTO1 VDMGR,DMCB,DMWRT,GENDIR,IOKEY,IOKEY,0                            
         BE    LEX                                                              
         DC    H'0'                                                             
*                                                                               
LINS040  GOTO1 VDMGR,DMCB,(X'88',GETREC),GENFIL,LDDDA,AIO1,DMWORK               
         BE    LINS044                                                          
         TM    DMCB+8,X'02'              RECORD IS DELETED                      
         BO    LINS044                                                          
         DC    H'0'                                                             
*                                                                               
LINS044  L     R2,AIO1                                                          
         NI    LDFSTAT,X'FF'-X'80'       RESTORE RECORD                         
         MVC   LDFATT,CURDATT                                                   
         GOTO1 SETACT,LDTAD                                                     
         GOTO1 VDMGR,DMCB,PUTREC,GENFIL,IOKEY,AIO1,DMWORK                       
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         LA    R2,IOKEY                                                         
         NI    LDDSTAT,X'FF'-X'80'       RESTORE RECORD                         
         MVC   LDDATT,CURDATT                                                   
         GOTO1 VDMGR,DMCB,DMWRT,GENDIR,IOKEY,IOKEY,0                            
         BE    LEX                                                              
         DC    H'0'                                                             
*                                                                               
         DROP  R2                                                               
         EJECT                                                                  
                                                                                
*======================================================================         
* REMOVE A VALUE FROM A GIVEN LIST                                              
*======================================================================         
LSTREM   BAS   RE,SETVAL                                                        
         BNE   LEX                                                              
*                                                                               
         LA    R2,IOKEY                                                         
         USING LDTAD,R2                                                         
         XC    IOKEY,IOKEY                                                      
         MVI   LDTAKMAJ,LDTAKMAQ         C'L' LIST RECORD SYSTEM                
         MVI   LDTAKMIN,LDTAKMIQ         C'S' SECURITY DATA ACCESS              
         MVI   LDTAKREC,LDTAKREQ         C'D' DATA RECORD                       
         MVC   LDTAKNUM,LISTINUM                                                
         MVC   LDTAKCOD,CURDCOD                                                 
*                                                                               
         MVC   IOKEYSV,IOKEY                                                    
         GOTO1 VDMGR,DMCB,(X'88',DMREAD),GENDIR,IOKEYSV,IOKEY,0                 
         BNE   DEX                                                              
*                                                                               
         GOTO1 VDMGR,DMCB,(X'88',GETREC),GENFIL,LDDDA,AIO1,DMWORK               
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         L     R2,AIO1                                                          
         OI    LDFSTAT,X'80'             DELETE DATA RECORD                     
         GOTO1 SETACT,LDTAD                                                     
         GOTO1 VDMGR,DMCB,PUTREC,GENFIL,AIO1,AIO1,DMWORK                        
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         LA    R2,IOKEY                                                         
         OI    LDDSTAT,X'80'             DELETE DATA RECORD                     
         GOTO1 VDMGR,DMCB,DMWRT,GENDIR,IOKEY,IOKEY,0                            
         BE    LEX                                                              
         DC    H'0'                                                             
*                                                                               
         DROP  R2                                                               
         EJECT                                                                  
                                                                                
*======================================================================         
* GET DATA FROM A LIST                                                          
*======================================================================         
LSTGET   BAS   RE,SETVAL                                                        
         MVI   LISTERRS,0                DON'T WORRY ABOUT ERRORS YET           
*                                                                               
         LA    R2,IOKEY                                                         
         USING LDTAD,R2                                                         
         XC    IOKEY,IOKEY                                                      
         MVI   LDTAKMAJ,LDTAKMAQ         C'L' LIST RECORD SYSTEM                
         MVI   LDTAKMIN,LDTAKMIQ         C'S' SECURITY DATA ACCESS              
         MVI   LDTAKREC,LDTAKREQ         C'D' DATA RECORD                       
         MVC   LDTAKNUM,LISTINUM                                                
         MVC   LDTAKCOD,CURDCOD                                                 
         CLI   LISTACTN,LISTAGET                                                
         BE    *+8                                                              
         MVI   LDTAKEY+(LDTAKEYL-1),X'FF'                                       
*                                                                               
         MVC   IOKEYSV,IOKEY                                                    
         GOTO1 VDMGR,DMCB,DMRDHI,GENDIR,IOKEYSV,IOKEY,0                         
         BNE   DEX                                                              
         CLC   LDTAD(LDTAKCOD-LDTAD),IOKEYSV                                    
         BNE   DEX                                                              
*                                                                               
         CLI   LISTACTN,LISTAGET                                                
         BNE   LSTGET10                                                         
         CLC   LDTAKCOD,CURDCOD                                                 
         BNE   DEX                                                              
*                                                                               
LSTGET10 MVC   CURDCOD,LDTAKCOD                                                 
         MVC   CURDATT,LDDATT                                                   
         BAS   RE,SETVAL                                                        
         B     LEX                                                              
         DROP  R2                                                               
         EJECT                                                                  
                                                                                
*======================================================================         
* CHECK IF LIST INCLUDES DATA VALUE                                             
*======================================================================         
LSTCHK   BAS   RE,SETVAL                                                        
         BNE   LEX                                                              
         XC    LISTDATT,LISTDATT                                                
*                                                                               
         LA    R2,IOKEY                                                         
         USING LDTAD,R2                                                         
         XC    IOKEY,IOKEY                                                      
         MVI   LDTAKMAJ,LDTAKMAQ         C'L' LIST RECORD SYSTEM                
         MVI   LDTAKMIN,LDTAKMIQ         C'S' SECURITY DATA ACCESS              
         MVI   LDTAKREC,LDTAKREQ         C'D' DATA RECORD                       
         MVC   LDTAKNUM,LISTINUM                                                
         MVC   LDTAKCOD,CURDCOD                                                 
*                                                                               
         MVC   IOKEYSV,IOKEY                                                    
         GOTO1 VDMGR,DMCB,DMREAD,GENDIR,IOKEYSV,IOKEY,0                         
         BNE   LSTCHK10                                                         
         MVC   LISTDATT,LDDATT                                                  
         TM    LDDATT,LDDANOQ            FOUND BUT NEGATIVE DATA                
         BO    DEX                                                              
         B     LEX                                                              
*                                                                               
LSTCHK10 TM    LISTIATT,LLFALLQ          NOT FOUND, BUT DEFAULT TO ALL          
         BO    LEX                                                              
         B     DEX                                                              
         DROP  R2                                                               
         EJECT                                                                  
                                                                                
*======================================================================         
* CHECK IF DATA VALUE IS LISTED IN THIS LIST                                    
*======================================================================         
LSTHAS   BAS   RE,SETVAL                                                        
         BNE   LEX                                                              
         XC    LISTDATT,LISTDATT                                                
*                                                                               
         LA    R2,IOKEY                                                         
         USING LDTAD,R2                                                         
         XC    IOKEY,IOKEY                                                      
         MVI   LDTAKMAJ,LDTAKMAQ         C'L' LIST RECORD SYSTEM                
         MVI   LDTAKMIN,LDTAKMIQ         C'S' SECURITY DATA ACCESS              
         MVI   LDTAKREC,LDTAKREQ         C'D' DATA RECORD                       
         MVC   LDTAKNUM,LISTINUM                                                
         MVC   LDTAKCOD,CURDCOD                                                 
*                                                                               
         MVC   IOKEYSV,IOKEY                                                    
         GOTO1 VDMGR,DMCB,DMREAD,GENDIR,IOKEYSV,IOKEY,0                         
         BNE   DEX                                                              
         MVC   LISTDATT,LDDATT                                                  
         B     LEX                                                              
         DROP  R2                                                               
         EJECT                                                                  
                                                                                
*======================================================================         
* CHECK LIST TYPE                                                               
*    ON ENTRY  LISTITYP=TYPE NUMBER,OR LISTITNM=TYPE NAME,LISTISYS              
*    ON EXIT   LISTITYP, LISTIRMX, LISTISYS, LISTITNM=TYPE NAME                 
*======================================================================         
LSTTYP   MVI   BYTE,0                                                           
*                                                                               
         XR    R1,R1                                                            
         ICM   R1,3,LISTITYP             ANY TYPE NUMBER?                       
         BNZ   LTYP040                   . YES, VALIDATE IT                     
*                                                                               
         LA    R3,LISTITNM               . NO, MUST VALIDATE NAME               
         LA    R4,L'LTTNAM-1(R3)         R4=A(LAST CHARACTER)                   
*                                                                               
LTYP020  CR    R3,R4                     CALC LENGTH W/O TRAILING SPACE         
         BH    LTYP100                                                          
         CLI   0(R4),C' '                                                       
         BH    LTYP030                                                          
         BCTR  R4,0                                                             
         B     LTYP020                                                          
*                                                                               
LTYP030  SR    R4,R3                     R4=(LENGTH OF TYPE NAME)               
*                                                                               
LTYP040  L     R2,ALTTAB                                                        
         USING LTTABD,R2                                                        
LTYP050  CLI   0(R2),X'FF'               END OF TABLE                           
         BE    LTYP100                   . YES, INVALID TYPE                    
         LTR   R1,R1                     DO WE HAVE A NUMBER                    
         BNZ   LTYP060                   . YES                                  
         EX    R4,*+8                    . NO, CHECK NAME                       
         B     *+10                                                             
         CLC   LTTNAM(0),0(R3)           COMPARE WITH DATA TYPE NAME            
         BE    LTYP080                                                          
         B     LTYP070                                                          
LTYP060  CLC   LISTITYP,LTTNUM           CHECK TYPE                             
         BE    LTYP080                                                          
LTYP070  LA    R2,LTTLQ(R2)              NEXT ENTRY                             
         B     LTYP050                                                          
*                                                                               
LTYP080  MVI   BYTE,C'T'                 TYPE FOUND                             
         CLC   LTTSYS,LISTISYS           SYSTEM                                 
         BNE   LTYP070                                                          
         MVC   LISTITYP,LTTNUM           LIST TYPE NUMBER                       
         MVC   LISTIRMX,LTTRMX           MAXIMUM LENGTH OF VALUE                
         MVC   LISTISYS,LTTSYS           SYSTEM NUMBER                          
         MVC   LISTITNM(L'LTTNAM),LTTNAM LIST TYPE NAME                         
         B     LEX                                                              
*                                                                               
LTYP100  CLI   BYTE,C'T'                 WAS TYPE FOUND?                        
         BNE   LTYP110                                                          
         MVI   LISTERRS,LETNS            TYPE INCOMPATIBLE WITH SYSTEM          
         B     LEX                                                              
LTYP110  MVI   LISTERRS,LETNF            TYPE NOT FOUND                         
         B     LEX                                                              
         DROP  R2                                                               
         EJECT                                                                  
                                                                                
*======================================================================         
* CONVERT DATA TO/FROM RECORD/DISPLAY FORMAT                                    
*======================================================================         
LSTCON   OI    LISTFLAG,LISTFCON                                                
         BAS   RE,SETVAL                 CONVERT VALUE                          
         BNE   LEX                                                              
         B     LSTTYP                    SET TYPE                               
         EJECT                                                                  
                                                                                
*======================================================================         
* INSERT/REMOVE A DERIVED LIST INTO/FROM A LIST                                 
*======================================================================         
LSTDER   XC    IOKEY,IOKEY                                                      
         LA    R2,IOKEY                                                         
*                                                                               
         ICM   R1,15,LISTINUM            DO WE HAVE A LIST NUMBER?              
         BZ    LDER030                   . NO, LOOK UP WITH CODE                
*                                                                               
         USING LNUMD,R2                                                         
         MVI   LNUMKMAJ,LNUMKMAQ         C'L' LIST SYSTEM RECORD                
         MVI   LNUMKMIN,LNUMKMIQ         C'S' SECURITY DATA ACCESS              
         MVI   LNUMKREC,LNUMKREQ         C'N' NUMBER RECORD                     
         MVC   LNUMKNUM,LISTINUM                                                
*                                                                               
         MVC   IOKEYSV,IOKEY                                                    
         GOTO1 VDMGR,DMCB,(X'88',DMRDHI),GENDIR,IOKEYSV,IOKEY,0                 
         BE    *+12                                                             
         TM    DMCB+8,X'02'              RECORD IS DELETED                      
         BZ    DEX                                                              
*                                                                               
         CLC   LNUMKEY(LNUMKAGY-LNUMD),IOKEYSV                                  
         BE    LDER020                                                          
         MVI   DMCB+8,X'10'              GIVE RECORD NOT FOUND ERROR            
         B     DEX                                                              
*                                                                               
LDER020  GOTO1 VDMGR,DMCB,(X'88',GETREC),GENFIL,LNDDA,AIO1,DMWORK               
         BE    *+12                                                             
         TM    DMCB+8,X'02'              RECORD IS DELETED                      
         BZ    DEX                                                              
         B     LDER050                                                          
*                                                                               
         USING LLSTD,R2                                                         
LDER030  MVI   LLSTKMAJ,LLSTKMAQ         C'L' LIST SYSTEM RECORD                
         MVI   LLSTKMIN,LLSTKMIQ         C'S' SECURITY DATA ACCESS              
         MVI   LLSTKREC,LLSTKREQ         C'L' LIST RECORD                       
         MVC   LLSTKAGY,LISTIAGY                                                
         MVC   LLSTKGRP,LISTIGRP                                                
         MVC   LLSTKSYS,LISTISYS                                                
         MVC   LLSTKTYP,LISTITYP                                                
*                                                                               
         CLI   LISTACTN,LISTAINF                                                
         BE    LDER040                                                          
         MVI   LLSTKEY+(LLSTKEYL-1),X'FF'                                       
*                                                                               
         MVC   IOKEYSV,IOKEY                                                    
         GOTO1 VDMGR,DMCB,(X'88',DMRDHI),GENDIR,IOKEYSV,IOKEY,0                 
         BE    *+12                                                             
         TM    DMCB+8,X'02'              RECORD IS DELETED                      
         BZ    DEX                                                              
         CLC   LLSTKEY(LLSTKSYS-LLSTKEY),IOKEYSV                                
         BE    LDER042                                                          
         MVI   DMCB+8,X'10'              GIVE RECORD NOT FOUND ERROR            
         B     DEX                                                              
*                                                                               
LDER040  MVC   IOKEYSV,IOKEY                                                    
         GOTO1 VDMGR,DMCB,(X'88',DMREAD),GENDIR,IOKEYSV,IOKEY,0                 
         BE    *+12                                                             
         TM    DMCB+8,X'02'              RECORD IS DELETED                      
         BZ    DEX                                                              
*                                                                               
LDER042  GOTO1 VDMGR,DMCB,(X'88',GETREC),GENFIL,LLDDA,AIO1,DMWORK               
         BE    *+12                                                             
         TM    DMCB+8,X'02'              RECORD IS DELETED                      
         BZ    DEX                                                              
*                                                                               
LDER050  L     R2,AIO1                                                          
         USING LLSTD,R2                                                         
         MVC   LISTIAGY,LLSTKAGY         AGENCY                                 
         MVC   LISTIGRP,LLSTKGRP         CODE                                   
         MVC   LISTISYS,LLSTKSYS         SYSTEM                                 
         MVC   LISTITYP,LLSTKTYP         TYPE                                   
         MVC   LISTIATT,LLFATT           ATTRIBUTES                             
*                                                                               
         CLI   LISTACTN,LISTAIDL                                                
         BNE   LDER060                                                          
         XC    ELEM,ELEM                                                        
         LA    R3,ELEM                                                          
         USING LDERD,R3                                                         
         MVI   LDEREL,LDERELQ            X'0D' DERIVED LIST ELEMENT             
         MVI   LDERLN,LDERLNQ                                                   
         MVI   LDERSUB,LDEROFFQ          C'O' OFFICE LIST                       
         MVC   LDEROFF,LISTIDER          OFFICE LIST VALUE                      
         GOTO1 ADDELS,LLSTD                                                     
         B     LDER100                                                          
*                                                                               
LDER060  XC    ELEM,ELEM                                                        
         LA    R3,ELEM                                                          
         USING LDERD,R3                                                         
         MVI   LDEREL,LDERELQ                                                   
         MVI   LDERLN,L'LDERSUB+L'LDEROFF                                       
         MVI   LDERSUB,LDEROFFQ          C'O' OFFICE LIST                       
         MVC   LDEROFF,LISTIDER          OFFICE LIST VALUE                      
         GOTO1 DELELS,LLSTD                                                     
*                                                                               
         XC    ELEM,ELEM                                                        
         LA    R3,ELEM                                                          
         USING LDERD,R3                                                         
         MVI   LDEREL,LDERELQ            X'0D' DERIVED LIST ELEMENT             
         GOTO1 GETELS,LLSTD              ANY DERIVED LIST ELEMENTS              
         BNE   LDER101                                                          
*                                                                               
LDER100  OI    LLFATT,LLFDERQ            LIST CONTAINS A DERIVED LIST           
LDER101  GOTO1 SETACT,LLSTD                                                     
*                                                                               
         GOTO1 VDMGR,DMCB,PUTREC,GENFIL,AIO1,AIO1,DMWORK                        
         BNE   DEX                                                              
*                                                                               
         MVC   LISTIATT,LLFATT                                                  
*                                                                               
         LA    R2,IOKEY                                                         
         MVC   LLDATT,LISTIATT                                                  
         GOTO1 VDMGR,DMCB,DMWRT,GENDIR,IOKEY,IOKEY,0                            
         BE    LEX                                                              
         DC    H'0'                                                             
                                                                                
*----------------------------------------                                       
* UPDATE LIST NUMBER RECORD - PASSIVE                                           
*----------------------------------------                                       
         XC    IOKEY,IOKEY                                                      
         LA    R2,IOKEY                                                         
         USING LNUMD,R2                                                         
         MVI   LNUMKMAJ,LNUMKMAQ         C'L' LIST SYSTEM RECORD                
         MVI   LNUMKMIN,LNUMKMIQ         C'S' SECURITY DATA ACCESS              
         MVI   LNUMKREC,LNUMKREQ         C'N' NUMBER RECORD                     
         MVC   LNUMKNUM,LISTINUM                                                
         MVC   LNUMKAGY,LISTIAGY                                                
         MVC   LNUMKGRP,LISTIGRP                                                
         MVC   LNUMKSYS,LISTISYS                                                
         MVC   LNUMKTYP,LISTITYP                                                
*                                                                               
         MVC   IOKEYSV,IOKEY                                                    
         GOTO1 VDMGR,DMCB,DMREAD,GENDIR,IOKEYSV,IOKEY,0                         
         BNE   DEX                                                              
*                                                                               
         MVC   LNDATT,LISTIATT                                                  
         GOTO1 VDMGR,DMCB,DMWRT,GENDIR,IOKEY,IOKEY,0                            
         BNE   DEX                                                              
         B     LEX                                                              
*                                                                               
         DROP  R2,R3                                                            
         EJECT                                                                  
                                                                                
***********************************************************************         
* INITIAL, EVERY TIME THROUGH, PROCESSING                                       
***********************************************************************         
INIT     NTR1                                                                   
*                                                                               
         MVI   SVTSYS,0                  SET NO SYSTEM SWAP                     
         MVI   LISTERRS,0                SET NO ERROR                           
         MVI   LISTDMER,0                SET NO ERROR                           
*                                                                               
         L     R1,LISTACOM               A(COMFACS)                             
         USING COMFACSD,R1                                                      
         MVC   VDMGR,CDATAMGR                                                   
         MVC   VCALLOV,CCALLOV                                                  
         MVC   VDATCON,CDATCON                                                  
         MVC   VHELLO,CHELLO                                                    
         MVC   VGETFACT,CGETFACT                                                
         MVC   VMASTC,CMASTC                                                    
         DROP  R1                                                               
*                                                                               
         LHI   R1,IO1-WORKD              AIO SETUP                              
         AR    R1,RC                                                            
         ST    R1,AIO1                                                          
         AHI   R1,-16                                                           
         MVC   0(16,R1),=CL16'*IO1*IO1*IO1*IO1'                                 
         LHI   R1,IO2-WORKD                                                     
         AR    R1,RC                                                            
         ST    R1,AIO2                                                          
         AHI   R1,-16                                                           
         MVC   0(16,R1),=CL16'*IO2*IO2*IO2*IO2'                                 
*                                                                               
         L     R1,=A(LTTAB)                                                     
         A     R1,RELO                                                          
         ST    R1,ALTTAB                 A(LIST TYPE TABLE)                     
*                                                                               
         XC    CURVALS(CURVALSL),CURVALS CURRENT TYPE TABLE ENTRY               
*                                                                               
INITX    J     EXIT                                                             
         EJECT                                                                  
                                                                                
***********************************************************************         
* SET DATA VALUES, BOTH FULL VALUE AND THE VALUE FOR THE RECORD KEY             
*    ON ENTRY  EITHER LISTITYP, LISTISYS, OR JUST LISTINUM (NEED READ)          
*    ON EXIT   CURDVAL - FULL DATA VALUE, CURDCOD - VALUE FOR REC KEY           
***********************************************************************         
SETVAL   NTR1                                                                   
         MVI   BYTE,0                                                           
         XR    R1,R1                                                            
*                                                                               
         ICM   R1,3,LISTITYP             TYPE GIVEN                             
         BNZ   SVAL020                                                          
         ICM   R1,15,LISTINUM            ELSE WILL HAVE TO READ LIST            
         BZ    SVAL080                                                          
*                                                                               
         LA    R2,IOKEY                                                         
         USING LNUMD,R2                                                         
         XC    IOKEY,IOKEY                                                      
         MVI   LNUMKMAJ,LNUMKMAQ         C'L' LIST SYSTEM RECORD                
         MVI   LNUMKMIN,LNUMKMIQ         C'S' SECURITY DATA ACCESS              
         MVI   LNUMKREC,LNUMKREQ         C'N' NUMBER RECORD                     
         MVC   LNUMKNUM,LISTINUM                                                
*                                                                               
         MVC   IOKEYSV,IOKEY                                                    
         GOTO1 VDMGR,DMCB,(X'08',DMRDHI),GENDIR,IOKEYSV,IOKEY,0                 
         BE    *+12                                                             
         TM    DMCB+8,X'02'              RECORD IS DELETED                      
         BZ    SVAL080                                                          
*                                                                               
         CLC   LNUMKEY(LNUMKAGY-LNUMD),IOKEYSV                                  
         BNE   SVAL080                   NOT THE REC WE'RE LOOKING FOR          
*                                                                               
         MVC   LISTIAGY,LNUMKAGY                                                
         MVC   LISTIGRP,LNUMKGRP                                                
         MVC   LISTISYS,LNUMKSYS                                                
         MVC   LISTITYP,LNUMKTYP                                                
         DROP  R2                                                               
*                                                                               
SVAL020  L     R3,ALTTAB                                                        
         USING LTTABD,R3                                                        
SVAL030  CLI   0(R3),X'FF'               END OF TABLE                           
         BE    SVAL060                   . YES, INVALID TYPE                    
         CLC   LTTNUM,LISTITYP           TYPE                                   
         BNE   SVAL040                                                          
         MVI   BYTE,C'T'                 FOUND THE TYPE                         
         CLC   LTTSYS,LISTISYS           SYSTEM                                 
         BE    SVAL050                                                          
SVAL040  LA    R3,LTTLQ(R3)                                                     
         B     SVAL030                                                          
*                                                                               
SVAL050  MVC   LISTIRMX,LTTRMX                                                  
         MVC   LISTIDMN,LTTDMN                                                  
         MVC   LISTIDMX,LTTDMX                                                  
*                                                                               
         OC    CURDCOD,CURDCOD           DO WE HAVE A CODE ALREADY              
         BNZ   SVAL054                   . YES                                  
         TM    LISTFLAG,LISTFCON         CONVERT RECORD TO DISPLAY              
         BO    SVAL052                                                          
*                                                                               
         MVC   CURDVAL,LISTDVAL          VALUE PASSED TO DDLIST                 
         MVC   CURDATT,LISTDATT                                                 
         B     SVAL056                                                          
*                                                                               
SVAL052  MVC   CURDCOD,LISTDVAL                                                 
         MVC   CURDATT,LISTDATT                                                 
         XC    LISTDVAL,LISTDVAL                                                
*                                                                               
SVAL054  XC    CURDVAL,CURDVAL           THEN BUILD IT FROM CODE                
         LA    RE,CURDCOD+L'CURDCOD                                             
         LLC   RF,LTTRMX                                                        
         SR    RE,RF                                                            
         BCTR  RF,0                                                             
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   CURDVAL(0),0(RE)                                                 
         MVC   LISTDATT,CURDATT                                                 
*                                                                               
SVAL056  ST    R3,CURLTN                 A(LIST TABLE ENTRY)                    
         XR    RF,RF                                                            
         ICM   RF,3,LTTDRT               DISPLCMNT TO DATA TYPE ROUTINE         
*                                                                               
         TM    LISTFLAG,LISTFRAW         USE RAW RECORD VALUES?                 
         BZ    *+8                       . NO                                   
         LHI   RF,PROGNR-LTTAB                                                  
*                                                                               
         A     RF,ALTTAB                 TYPE TABLE                             
         BASR  RE,RF                     GO VALIDATE AND FILL IN VALUES         
         BNE   SVAL070                                                          
         B     SVOKX                                                            
         DROP  R3                                                               
*                                                                               
SVAL060  CLI   BYTE,C'T'                                                        
         BE    *+12                                                             
         MVI   LISTERRS,LETNF            TYPE NOT FOUND                         
         B     SVNOX                                                            
         MVI   LISTERRS,LETNS            TYPE INCOMPATIBLE WITH SYSTEM          
         B     SVNOX                                                            
SVAL070  MVI   LISTERRS,LEIDAV           INVALID DATA VALUE                     
         B     SVNOX                                                            
SVAL080  MVI   LISTERRS,LEILIS           INVALID LIST                           
         B     SVNOX                                                            
*                                                                               
SVNOX    J     NOX                                                              
SVOKX    J     OKX                                                              
         EJECT                                                                  
                                                                                
***********************************************************************         
* SET ACTIVITY ELEMENT                                                          
***********************************************************************         
SETACT   NTR1                                                                   
         LR    R2,R1                     SAVE A(RECORD)                         
*                                                                               
         ICM   R1,15,VMASTC              MASTC RESOLVED?                        
         BZ    SETA020                   . NO, CONTINUE                         
         L     R1,MCUTL-MASTD(R1)        A(UTL)                                 
         MVC   SVTSYS,TSYS-UTLD(R1)      SAVE SYSTEM OF CALLING PROGRAM         
         MVI   TSYS-UTLD(R1),X'0A'       CONTROL                                
*                                                                               
         USING MASTD,R1                                                         
         TM    MCRHINFO,MCRHOLD          OLD STYLE MCRFHDR FORMAT               
         BO    SETA030                                                          
         MVC   CURPERN,MCRHPSWD          PERSON PASSWORD NUMBER                 
         MVC   CURSAGY,MCUSER            AGENCY ALPHA                           
         OC    MCRHSAGY,MCRHSAGY                                                
         BZ    *+10                                                             
         MVC   CURSAGY,MCRHSAGY          SECURITY AGENCY ALPHA                  
         OC    MCRHSAGP,MCRHSAGP                                                
         BZ    *+10                                                             
         MVC   CURSAGY,MCRHSAGP          SECURITY AGENCY FOR THE PERSON         
         B     SETA030                                                          
*                                                                               
SETA020  ICM   RF,15,VGETFACT                                                   
         BZ    SETA030                                                          
         GOTO1 (RF),DMCB,(X'80',0),F#UTLD                                       
         L     R1,0(R1)                                                         
         USING F@UTLD,R1                                                        
         MVC   CURPERN,F@TPERS           PERSON PASSWORD NUMBER                 
         MVC   CURSAGY,F@TAGY            AGENCY ALPHA                           
         OC    F@TAGYSC,F@TAGYSC                                                
         BZ    *+10                                                             
         MVC   CURSAGY,F@TAGYSC          SECURITY AGENCY ALPHA                  
         OC    F@TAGYPE,F@TAGYPE                                                
         BZ    *+10                                                             
         MVC   CURSAGY,F@TAGYPE          SECURITY AGENCY FOR THE PERSON         
         DROP  R1                                                               
*                                                                               
SETA030  GOTO1 VDATCON,DMCB,(X'05',0),(X'03',DUB)                               
*                                                                               
         LA    R3,ACTEL                  GENFIL ACTIVITY ELEMENT                
         USING GACTELD,R3                                                       
         XC    ACTEL,ACTEL                                                      
         XC    ELEM,ELEM                                                        
         MVI   ELEM,GACTELQ              X'FE' ACTIVITY ELEMENT                 
         GOTO1 GETELS,(R2)                                                      
         ICM   R1,15,FULL                                                       
         BNZ   SETA050                                                          
*                                                                               
         MVI   GACTEL,GACTELQ            ELEMENT CODE                           
         MVI   GACTLN,GACTLNQ            ELEMENT LENGTH                         
         MVC   GACTADT,DUB               DATE RECORD ADDED (BINARY)             
         MVC   GACTAAG,CURSAGY           ADDED AGENCY CODE                      
         MVC   GACTAPW,CURPERN           ADDED PERSON/PASSWORD NUMBER           
         B     SETA052                                                          
*                                                                               
SETA050  MVC   ACTEL,0(R1)               SAVE ELEMENT IN STORAGE                
SETA052  MVC   GACTCDT,DUB               DATE LAST CHANGED (BINARY)             
         MVC   GACTCAG,CURSAGY           CHANGED AGENCY CODE                    
         MVC   GACTCPW,CURPERN           CHANGED PERSON/PASSWORD NUMBER         
*                                                                               
         XC    ELEM,ELEM                                                        
         MVC   ELEM(1),ACTEL             SET ACTIVITY ELEMENT CODE              
         GOTO1 DELELS,(R2)               DELETE AND RE-ADD ELEMENT              
         MVC   ELEM(L'ACTEL),ACTEL                                              
         GOTO1 ADDELS,(R2)                                                      
         J     EXIT                                                             
         DROP  R3                                                               
         EJECT                                                                  
                                                                                
***********************************************************************         
* ROUTINE TO ADD AN ELEMENT TO A RECORD                                         
*    ON ENTRY  R1=A(RECORD TO ADD ELEMENT TO)                                   
*              ELEM CONTAINS ELEMENT                                            
***********************************************************************         
ADDELS   NTR1                                                                   
         LR    R0,R1                                                            
         GOTO1 VHELLO,DMCB,(C'P',GENFIL),(R0),ELEM,0                            
         CLI   12(R1),0                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
         J     EXIT                                                             
         EJECT                                                                  
                                                                                
***********************************************************************         
* ROUTINE TO DELETE AN ELEMENT FROM A RECORD                                    
*    ON ENTRY  R1=A(RECORD TO DELETE ELEMENT FROM)                              
*              ELEM CONTAINS ELEMENT CODE OF ELEMENT TO BE DELETED              
***********************************************************************         
DELELS   NTR1                                                                   
         LR    R0,R1                                                            
         GOTO1 VHELLO,DMCB,(C'D',GENFIL),(ELEM,(R0)),(ELEM+1,ELEM+2),0          
         CLI   12(R1),6                  ELEMENT NOT FOUND                      
         BE    DELELSX                                                          
         CLI   12(R1),0                                                         
         BE    DELELSX                                                          
         DC    H'0'                                                             
DELELSX  J     EXIT                                                             
         EJECT                                                                  
                                                                                
***********************************************************************         
* ROUTINE TO GET AN ELEMENT IN A RECORD                                         
*    ON ENTRY  R1=A(RECORD TO GET ELEMENT FROM)                                 
*              ELEM CONTAINS ELEMENT CODE AND DATA TO SEARCH FOR                
*    ON EXIT   FULL CONTAINS ADDRESS OF ELEMENT OR ZEROES IF NOT FOUND          
***********************************************************************         
GETELS   NTR1                                                                   
         LR    R0,R1                                                            
         GOTO1 VHELLO,DMCB,(C'G',GENFIL),(ELEM,(R0)),(ELEM+1,ELEM+2),0          
         XC    FULL(4),FULL                                                     
         CLI   12(R1),0                                                         
         BNE   *+10                                                             
         MVC   FULL(4),12(R1)                                                   
         J     EXIT                                                             
         EJECT                                                                  
                                                                                
***********************************************************************         
* LITERALS                                                                      
***********************************************************************         
         LTORG                                                                  
*                                                                               
GENDIR   DC    CL7'GENDIR '                                                     
GENFIL   DC    CL7'GENFIL '                                                     
DMADD    DC    CL7'DMADD  '                                                     
DMREAD   DC    CL7'DMREAD '                                                     
DMRDHI   DC    CL7'DMRDHI '                                                     
DMRSEQ   DC    CL7'DMRSEQ '                                                     
DMWRT    DC    CL7'DMWRT  '                                                     
ADDREC   DC    CL7'ADDREC '                                                     
GETREC   DC    CL7'GETREC '                                                     
PUTREC   DC    CL7'PUTREC '                                                     
SPACES   DC    CL132' '                                                         
ZEROS    DC    XL10'00'                                                         
*                                                                               
         EJECT                                                                  
                                                                                
***********************************************************************         
* DATA TYPE TABLE - (SEE LTTABD)                                                
*    DATA NUMBER, LENGTH, SYSTEM, FLAG, DATA NAME, VALIDATION ROUTINE           
***********************************************************************         
         DS    0D                                                               
         DC    CL16'*DTT*DTT*DTT*DTT'                                           
*                                                                               
LTTAB    DC    X'00',AL2(LISTTTXT),AL1(20)              ALL SYSTEMS             
         DC    AL1(0),AL1(20),AL2(0)                     TEXT                   
         DC    CL12'TEXT',AL2(PROGNR-LTTAB),AL2(0)                              
*                                                                               
         DC    X'02',AL2(LISTTCLT),AL1(2)               SPOT                    
         DC    AL1(2),AL1(3),AL2(0)                      CLIENT                 
         DC    CL12'CLIENT',AL2(PROCLI-LTTAB),AL2(0)                            
*                                                                               
         DC    X'03',AL2(LISTTCLT),AL1(2)               NET                     
         DC    AL1(2),AL1(3),AL2(0)                      CLIENT                 
         DC    CL12'CLIENT',AL2(PROCLI-LTTAB),AL2(0)                            
*                                                                               
         DC    X'04',AL2(LISTTCLT),AL1(3)               PRINT                   
         DC    AL1(2),AL1(3),AL2(0)                      CLIENT                 
         DC    CL12'CLIENT',AL2(PROCLI-LTTAB),AL2(0)                            
*                                                                               
         DC    X'FFFF'                                                          
         EJECT                                                                  
                                                                                
***********************************************************************         
* PROCESS GENERAL RECORD VALUE - RECORD VALUE AND DISPLAY VALUE EQUAL           
***********************************************************************         
PROGNR   NTR1                                                                   
*                                                                               
         OC    CURDCOD,CURDCOD           DO WE HAVE RECORD VALUE                
         BZ    PGNR010                                                          
         MVC   LISTDVAL,CURDVAL          THAN DISPLAY VALUE READY               
         B     PGNROKX                                                          
*                                                                               
PGNR010  XC    CURDCOD,CURDCOD           OTHERWISE BUILD RECORD VALUE           
         LLC   R5,LISTIRMX               RIGHT ALIGN FOR KEY WITH MAX           
         LA    R6,CURDCOD+L'CURDCOD                                             
         SR    R6,R5                                                            
         EX    R6,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R6),CURDVAL           CURDCOD IS DATA RIGHT ALIGNED          
*                                                                               
PGNROKX  J     OKX                                                              
PGNRNOX  J     NOX                                                              
         LTORG                                                                  
         EJECT                                                                  
                                                                                
***********************************************************************         
* PROCESS SPOT/NET/PRINT CLIENT VALUE                                           
***********************************************************************         
PROCLI   NTR1                                                                   
*                                                                               
         OC    CURDCOD,CURDCOD           DO WE HAVE A CODE ALREADY              
         BZ    PCLI020                   . NO                                   
         CLI   LISTISYS,X'04'            PRINT?                                 
         BNE   PCLI010                                                          
         MVC   LISTDVAL,CURDVAL          DON'T UNPACK PRINT CLIENTS             
         OC    LISTDVAL,SPACES                                                  
         B     PCLIOKX                                                          
*                                                                               
PCLI010  MVC   DMCB+4(4),=X'D9000A15'    CLUNPK                                 
         L     RF,VCALLOV                                                       
         GOTO1 (RF),DMCB,0                                                      
         CLI   DMCB+4,X'FF'                                                     
         BNE   *+6                                                              
         DC    H'0'                                                             
         L     RF,0(R1)                                                         
         GOTO1 (RF),(R1),CURDVAL,LISTDVAL                                       
         CLI   0(R1),0                                                          
         BNE   PCLINOX                                                          
         OC    LISTDVAL,SPACES                                                  
         B     PCLIOKX                                                          
*                                                                               
PCLI020  LLC   R5,LISTIRMX               RIGHT ALIGN FOR KEY WITH MAX           
         LA    R6,CURDCOD+L'CURDCOD                                             
         SR    R6,R5                                                            
*                                                                               
         CLC   =C'ALL',CURDVAL           ALL IS NOT ALLOWED                     
         BE    PCLINOX                                                          
         CLC   =C'DDS',CURDVAL           DDS IS NOT ALLOWED                     
         BE    PCLINOX                                                          
*                                                                               
         CLI   LISTISYS,X'04'            DON'T PACK PRINT CLIENTS               
         BE    PCLI050                                                          
*                                                                               
         CLI   CURDVAL,C'*'              CLPACK WON'T COMPLAIN ABOUT            
         BE    PCLINOX                   THESE BUT THEY'RE NO GOOD,             
         CLI   CURDVAL,C'$'              OFFICE, OFFICE LIST, AND               
         BE    PCLINOX                   MARKET VALUES                          
         CLI   CURDVAL,C'+'                                                     
         BE    PCLINOX                                                          
*                                                                               
         MVC   DMCB+4(4),=X'D9000A14'    CLPACK                                 
         L     RF,VCALLOV                                                       
         GOTO1 (RF),DMCB,0                                                      
         CLI   DMCB+4,X'FF'                                                     
         BNE   *+6                                                              
         DC    H'0'                                                             
         L     RF,0(R1)                                                         
         GOTO1 (RF),(R1),CURDVAL,(R6)                                           
         CLI   0(R1),0                                                          
         BE    PCLIOKX                                                          
         B     PCLINOX                                                          
*                                                                               
PCLI050  LA    R1,CURDVAL                                                       
         LLC   R4,LISTIDMX                                                      
*                                                                               
PCLI080  CLI   0(R1),C'A'                PRINT CLIENTS MUST BE ALPHA-           
         BL    PCLI099                    NUMERIC                               
         CLI   0(R1),C'Z'                                                       
         BNH   PCLI090                                                          
         CLI   0(R1),C'0'                                                       
         BL    PCLI099                                                          
         CLI   0(R1),C'9'                                                       
         BH    PCLI099                                                          
PCLI090  LA    R1,1(R1)                                                         
         BCT   R4,PCLI080                                                       
         B     PCLI100                                                          
*                                                                               
PCLI099  CHI   R4,1                      LAST CHARACTER?                        
         BNE   PCLINOX                                                          
         CLI   0(R1),C' '                CAN BE A SPACE                         
         BE    PCLI100                                                          
         CLI   0(R1),0                   OR ZERO, AS LONG AS WE MAKE IT         
         BNE   PCLINOX                    A SPACE                               
         MVI   0(R1),C' '                                                       
*                                                                               
PCLI100  BCTR  R5,0                                                             
         EX    R5,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R6),CURDVAL                                                  
*                                                                               
PCLIOKX  J     OKX                                                              
PCLINOX  J     NOX                                                              
         LTORG                                                                  
         EJECT                                                                  
                                                                                
***********************************************************************         
* WORKING STORAGE                                                               
***********************************************************************         
WORKD    DSECT                                                                  
RELO     DS    A                                                                
AIO1     DS    A                         A(IO AREA 1)                           
AIO2     DS    A                         A(IO AREA 2)                           
ALTTAB   DS    A                         A(LIST TYPE TABLE)                     
*                                                                               
VDMGR    DS    V                                                                
VCALLOV  DS    V                                                                
VDATCON  DS    V                                                                
VHELLO   DS    V                                                                
VGETFACT DS    V                                                                
VMASTC   DS    V                                                                
*                                                                               
DUB      DS    D                                                                
FULL     DS    F                                                                
HALF     DS    H                                                                
BYTE     DS    X                                                                
BYTE2    DS    X                                                                
DMCB     DS    6F                                                               
DMIODA   DS    XL4                       IO DISK ADDRESS                        
DMWORK   DS    XL64                      DATAMGR WORK                           
WORK     DS    XL64                                                             
ELEM     DS    XL256                                                            
ACTEL    DS    XL256                     ACTIVITY ELEMENT                       
*                                                                               
SVTSYS   DS    X                         SAVE SYSTEM                            
*                                                                               
CURSAGY  DS    CL2                       SECURITY AGENCY                        
CURPERN  DS    XL2                       PERSON/PASSWORD NUMBER                 
*                                                                               
CURVALS  DS    0F                        CURRENT VALUES                         
CURLTN   DS    A                         A(CURRENT LIST TABLE ENTRY)            
CURDCOD  DS    XL(L'LDTAKCOD)            DATA VALUE CODE FOR RECORD KEY         
CURDVAL  DS    XL(L'LISTDVAL)            FULL DATA VALUE                        
CURDATT  DS    X                         DATA ATTRIBUTES                        
CURVALSL EQU   *-CURVALS                                                        
*                                                                               
         DS    0D                                                               
IOKEY    DS    XL48                                                             
IOKEYSV  DS    XL48                                                             
*                                                                               
         DS    0D                                                               
         DS    CL16                                                             
IO1      DS    2048X                     GENFIL I/O AREA 1                      
         DS    0D                                                               
         DS    CL16                                                             
IO2      DS    2048X                     GENFIL I/O AREA 2                      
*                                                                               
WORKL    EQU   *-WORKD                                                          
                                                                                
***********************************************************************         
* LIST DATA TYPE TABLE DSECT                                                    
***********************************************************************         
LTTABD   DSECT                                                                  
LTTSYS   DS    X                   SYSTEM NUMBER                                
LTTNUM   DS    XL2                 TYPE NUMBER                                  
LTTRMX   DS    X                   MAXIMUM LENGTH OF RECORD VALUE               
LTTDMN   DS    X                   MINIMUM LENGTH OF DISPLAY VALUE              
LTTDMX   DS    X                   MAXIMUM LENGTH OF DISPLAY VALUE              
         DS    XL2                                                              
LTTNAM   DS    CL12                LIST TYPE NAME                               
LTTDRT   DS    AL2                 DISPLACEMENT TO VALIDATION ROUTINE           
         DS    XL2                                                              
LTTLQ    EQU   *-LTTABD                                                         
                                                                                
***********************************************************************         
* INCLUDED DSECTS                                                               
***********************************************************************         
* GEGENGEN                                                                      
* GEGENLST                                                                      
* DDLISTD                                                                       
* DDMASTC                                                                       
* DDCOMFACS                                                                     
* FAUTL                                                                         
* FAFACTS                                                                       
*                                                                               
LISTD    DSECT                                                                  
       ++INCLUDE DDLISTD                                                        
         EJECT                                                                  
       ++INCLUDE GEGENGEN                                                       
         EJECT                                                                  
       ++INCLUDE GEGENLST                                                       
         EJECT                                                                  
*                                                                               
         PRINT OFF                                                              
MASTD    DSECT                                                                  
       ++INCLUDE DDMASTC                                                        
       ++INCLUDE DDCOMFACS                                                      
       ++INCLUDE FAUTL                                                          
       ++INCLUDE FAFACTS                                                        
         PRINT ON                                                               
*                                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'032DDLIST    07/26/13'                                      
         END                                                                    
