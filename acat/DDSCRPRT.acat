*          DATA SET DDSCRPRT   AT LEVEL 007 AS OF 05/01/02                      
*CATALP SCRPRT                                                                  
         SPACE 1                                                                
*P1      XL1   00=NO HDR MSG / NN=HDR MSG LEN                                   
*        AL3   A(TWA)                                                           
         SPACE 1                                                                
*P2      XL1   N/D                                                              
*        AL3   A(HDR MSG) IF 1ST BYTE NONZERO                                   
         SPACE 1                                                                
*P3      CL2   SET TO C'DD' FOR DATA DICTIONARY                                 
*        CL1   SYSTEM LETTER FOR DATA DICTIONARY                                
*        CL1   LANGUAGE LETTER FOR DATA DICTIONARY                              
         TITLE 'SCRPRT - BUILDS A SCREEN IMAGE FROM TWA AND PRINTS IT'          
         PRINT NOGEN                                                            
SCRPRT   CSECT                                                                  
         NMOD1 0,**SCRP**,RR=R4                                                 
         ST    R4,RELO                                                          
         MVC   PARAMS,0(R1)                                                     
         L     R3,0(R1)            R3=A(TWA)                                    
         LA    R3,0(R3)                                                         
         LA    R4,SHDR1            R4=A(SCREEN IMAGE)                           
         XC    CADR(2),CADR        ZERO CURSOR ADDRESS                          
         SPACE 2                                                                
SETDD    MVI   SYS,1               DEFAULT SYSTEM/LANGUAGE                      
         MVI   LANG,0                                                           
         CLC   PDD,=C'DD'          TEST IF DATA DICT PASSED                     
         BNE   SETDDX                                                           
*                                                                               
SETDD0   LA    RE,SYSTAB           CONVERT SYSTEM CHR TO SYSTEM NUM             
         CLI   PDDSYS,15                                                        
         BH    SETDD1                                                           
         MVC   SYS,PDDSYS                                                       
         B     SETDD3                                                           
SETDD1   CLI   0(RE),X'00'                                                      
         BE    SETDD3                                                           
         CLC   0(1,RE),PDDSYS                                                   
         BE    SETDD2                                                           
         LA    RE,2(RE)                                                         
         B     SETDD1                                                           
SETDD2   MVC   SYS,1(RE)                                                        
*                                                                               
SETDD3   LA    RE,LANGTAB          CONVERT LANG CHR TO LANG NUM                 
         CLI   PDDLAN,15                                                        
         BH    SETDD4                                                           
         MVC   LANG,PDDLAN                                                      
         B     SETDDX                                                           
SETDD4   CLI   0(RE),X'00'                                                      
         BE    SETDDX                                                           
         CLC   0(1,RE),PDDLAN                                                   
         BE    SETDD5                                                           
         LA    RE,2(RE)                                                         
         B     SETDD4                                                           
SETDD5   MVC   LANG,1(RE)                                                       
*                                                                               
SETDDX   XC    DDP,DDP             SET PARAM LIST FOR V(DICTATE)                
         MVI   DDPACT,C'T'                                                      
         MVC   DDPSYS,SYS                                                       
         MVC   DDPLAN,LANG                                                      
         SPACE 2                                                                
         MVC   SHDR1,HDR1          BUILD FOUR HEADER LINES                      
         MVC   SHDR2,HDR2                                                       
         MVC   SHDR3,HDR3                                                       
         MVC   SHDR4,BLANKS                                                     
         MVI   SHDR4+03,C'*'                                                    
         MVI   SHDR4+84,C'*'                                                    
         L     RE,=A(STRL1-SHDR1)                                               
         LA    RF,SHDR1(RE)                                                     
         ST    RF,ATRL1                                                         
         MVC   0(85,RF),HDR3       BUILD FOUR TRAILER LINES                     
         LA    RF,85(RF)                                                        
         MVC   0(85,RF),BLANKS                                                  
         LA    RF,85(RF)                                                        
         MVC   0(85,RF),BLANKS                                                  
         LA    RF,85(RF)                                                        
         MVC   0(85,RF),BLANKS                                                  
         SPACE 2                                                                
         LA    R5,SLIN1            BUILD SCREEN EDGES AND LINE NUMS             
         LA    R6,1                                                             
OUT1     MVC   0(85,R5),BLANKS                                                  
         MVI   03(R5),C'*'                                                      
         MVI   84(R5),C'*'                                                      
         CVD   R6,DUB                                                           
         UNPK  DUB(3),DUB+6(2)                                                  
         OI    DUB+2,X'F0'                                                      
         MVC   0(2,R5),DUB+1                                                    
         LA    R5,85(R5)                                                        
         MVC   0(85,R5),BLANKS                                                  
         MVI   03(R5),C'*'                                                      
         MVI   84(R5),C'*'                                                      
         LA    R5,85(R5)                                                        
         LA    R6,1(R6)                                                         
         CH    R6,=H'24'                                                        
         BNH   OUT1                                                             
         SPACE 2                                                                
         LA    R3,64(R3)           BUILD SCREEN FIELDS FROM TWA                 
         SR    R4,R4                                                            
OUT2     IC    R4,0(R3)            R4=L'SCR FLD + HDR                           
         LTR   R4,R4                                                            
         BZ    OUT5                                                             
         CLI   1(R3),X'FF'         NOP FIELD                                    
         BE    OUT4B                                                            
         TM    6(R3),X'40'                                                      
         BZ    *+10                                                             
         MVC   CADR(2),2(R3)                                                    
         MVC   DUB(2),2(R3)                                                     
         LH    R6,DUB                                                           
         SRDL  R6,32               R6&R7=ABS SCR ADR                            
         D     R6,=F'80'           R6=COL NUM , R7=ROW NUM                      
         LA    R6,4(R6)               (00-79)      (00-23)                      
         SLL   R7,1                                                             
         MH    R7,=H'85'                                                        
         AR    R6,R7                                                            
         LA    R7,SLIN1                                                         
         AR    R6,R7               R6=A(SCR FLD IN W/S)                         
         LR    R7,R4                                                            
         SH    R7,=H'8'            R7=OUTPUT LENGTH                             
         TM    1(R3),X'02'         TEST EXTENDED HEADER                         
         BZ    *+8                                                              
         SH    R7,=H'8'            SUBTRACT LENGTH OF HEADER EXTENSION          
         LTR   R7,R7                                                            
         BZ    OUT4                                                             
         MVC   DUB(1),1(R3)                                                     
         MVC   BYTE,6(R3)                                                       
         NI    BYTE,X'C0'                                                       
         OC    DUB(1),BYTE                                                      
         TM    DUB,X'0C'           POINT TO BLANKS FOR ZERO INTENSITY           
         BNO   *+12                                                             
         LA    R8,BLANKS                                                        
         B     OUT4                                                             
         LA    R8,8(R3)            R8=A(FIELD DATA)                             
OUT3     BCTR  R7,0                                                             
         EX    R7,*+8                                                           
         B     *+10                                                             
         OC    0(0,R8),0(R8)       TEST NULLS                                   
         BNZ   OUT4                                                             
         LA    R8,DOTS             POINT TO NULL PROT FILL CHRS                 
         TM    1(R3),X'20'         TEST PROTECTED                               
         BNZ   *+8                                                              
         LA    R8,USCORE           POINT TO NULL UNPROT FILL CHRS               
OUT4     EX    R7,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R6),0(R8)       MOVE FROM TWA TO PRINT AREA                  
*                                                                               
OUT4A    ICM   RF,15,=V(DICTATE)   DATA DICTIONARY TRANSLATION                  
         BZ    OUT4B                                                            
         LA    R7,1(R7)                                                         
         STC   R7,DDPLEN           SET LEN AND ADR OF FIELD                     
         STCM  R6,7,DDPADR                                                      
         MVI   DDPRET,C'U'         SET UPPER/LOWER CASE                         
         TM    1(R3),X'40'                                                      
         BZ    *+8                                                              
         MVI   DDPRET,C'L'                                                      
         LA    R1,DDP                                                           
         BASR  RE,RF                                                            
*                                                                               
OUT4B    AR    R3,R4               BUMP TO NEXT TWA FIELD                       
         B     OUT2                                                             
         SPACE 2                                                                
OUT5     OC    CADR(2),CADR        MOVE CURSOR ADDR TO SCREEN TRAILER           
         BZ    OUT6                                                             
         LH    R6,CADR                                                          
         SRDL  R6,32                                                            
         D     R6,=F'80'                                                        
         LA    R6,1(R6)            R6=COL NUM  01-80                            
         LA    R7,1(R7)            R7=ROW NUM  01-24                            
         CVD   R7,DUB              MOVE CURSOR ROW NUM                          
         UNPK  DUB(3),DUB+6(2)                                                  
         OI    DUB+2,X'F0'                                                      
         MVC   TRL1+50(2),DUB+1                                                 
         CVD   R6,DUB              MOVE CURSOR COL NUM                          
         UNPK  DUB(3),DUB+6(2)                                                  
         OI    DUB+2,X'F0'                                                      
         MVC   TRL1+53(2),DUB+1                                                 
         L     RE,ATRL1            POINT TO FIRST TRAILER LINE                  
         LA    RE,85(RE)                                                        
         LA    RE,85(RE)                                                        
         MVC   0(85,RE),TRL1                                                    
         SPACE 2                                                                
OUT6     SR    R6,R6               SET UP HDR MSG IF ANY                        
         SR    R7,R7                                                            
         IC    R6,PLMSG                                                         
         LTR   R6,R6                                                            
         BZ    *+8                                                              
         ICM   R7,7,PAMSG                                                       
         LA    R8,SHDR1                                                         
         GOTO1 =V(PRNTBL),PRNPL,((R6),(R7)),(R8),55,85,=C'1C',RR=RELO           
         SPACE 2                                                                
OUTX     XMOD1 1                                                                
         SPACE 2                                                                
HDR1     DC    CL85'    ....+....1....+....2....+....3....+....4....+..X        
               ..5....+....6....+....7....+....8 '                              
HDR2     DC    CL85'                                                   X        
                                                 '                              
HDR3     DC    CL85'    ***********************************************X        
               ********************************* '                              
TRL1     DC    CL85'                                CURSOR ADDRESS = (NX        
               N,NN)                             '                              
BLANKS   DC    CL85' '                                                          
DOTS     DC    256C'.'                                                          
USCORE   DC    256C'_'                                                          
         SPACE 1                                                                
SYSTAB   DC    C'1',X'01'                                                       
         DC    C'2',X'02'                                                       
         DC    C'3',X'03'                                                       
         DC    C'4',X'04'                                                       
         DC    C'5',X'05'                                                       
         DC    C'6',X'06'                                                       
         DC    C'7',X'07'                                                       
         DC    C'8',X'08'                                                       
         DC    C'9',X'09'                                                       
         DC    C'A',X'0A'                                                       
         DC    C'B',X'0B'                                                       
         DC    C'C',X'0C'                                                       
         DC    C'D',X'0D'                                                       
         DC    C'E',X'0E'                                                       
         DC    C'F',X'0F'                                                       
         DC    X'0000'                                                          
         SPACE 1                                                                
LANGTAB  DC    C'E',X'01'                                                       
         DC    C'E',X'02'                                                       
         DC    C'G',X'03'                                                       
         DC    C'F',X'04'                                                       
         DC    C'S',X'05'                                                       
         DC    C'I',X'06'                                                       
         DC    X'0000'                                                          
         SPACE 1                                                                
DUB      DS    D                                                                
PRNPL    DS    5F                                                               
RELO     DS    A                                                                
ATRL1    DS    A                                                                
CADR     DS    H                                                                
BYTE     DS    X                                                                
SYS      DS    X                                                                
LANG     DS    X                                                                
         SPACE 1                                                                
         DS    0F                                                               
PARAMS   DS    0XL12                                                            
PLMSG    DS    X                                                                
PATWA    DS    AL3                                                              
         DS    X                                                                
PAMSG    DS    AL3                                                              
PDD      DS    CL2                                                              
PDDSYS   DS    C                                                                
PDDLAN   DS    C                                                                
         SPACE 1                                                                
         DS    0F                                                               
DDP      DS    0XL12                                                            
DDPACT   DS    X                                                                
DDPRET   DS    X                                                                
DDPSYS   DS    X                                                                
DDPLAN   DS    X                                                                
DDPLEN   DS    X                                                                
DDPADR   DS    AL3                                                              
         DS    XL4                                                              
         SPACE 2                                                                
         LTORG                                                                  
         EJECT                                                                  
SHDR1    DS    CL85                                                             
SHDR2    DS    CL85                                                             
SHDR3    DS    CL85                                                             
SHDR4    DS    CL85                                                             
SLIN1    DS    CL85                                                             
         DS    CL85                                                             
SLIN2    DS    23CL170                                                          
STRL1    DS    CL85                                                             
STRL2    DS    CL85                                                             
STRL3    DS    CL85                                                             
STRL4    DS    CL85                                                             
         SPACE 2                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'007DDSCRPRT  05/01/02'                                      
         END                                                                    
