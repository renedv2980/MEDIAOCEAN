*          DATA SET DDSTXITOLD AT LEVEL 008 AS OF 05/01/02                      
*CATALP STXITER                                                                 
         TITLE 'STXITER - CONTROL/PRINT PARTIAL DUMPS'                          
**********************************************************************          
* THIS VERSION SHOULD ONLY BE USED FOR MVS, AND NOT CATALOGED OVER              
* THE DOS VERSION, AS IT HAS BEEN RECREATED FROM A SOURCE LISTING,              
* AND MAY NOT WORK UNDER DOS.                                                   
*                                                                               
* THIS PROGRAM ALLOWS A CALLING PROGRAM TO PDUMP FROM 1 TO TEN AREAS            
* IF THERE IS AN ABEND IN THE PROGRAM.                                          
*                                                                               
* THE PARAMETER LIST IS                                                         
*                                                                               
*        P1 - START ADDRESS OF PDUMP                                            
*        P2 - LENGTH OF PDUMP OR ENDING ADDRESS                                 
* X'80' IN HI-ORDER BYTE = END OF LIST                                          
**********************************************************************          
STXITER  CSECT                                                                  
*        PRINT NOGEN                                                            
         NMOD1 0,**STXTER,RR=(R8)                                               
         ST    R8,RELO                                                          
         L     R3,=A(DMPLIST)                                                   
         A     R3,RELO                                                          
*                                                                               
         LA    R0,10               SET MAX PDUMPS                               
         L     R2,0(R1)            GET USER'S LIST                              
*                                                                               
STXIT2   LM    R4,R5,0(R2)         GET START & END ADDRESS                      
         LA    R5,0(R5)            CLEAR HIGH ORDER BYTE                        
         CR    R4,R5               IF START ADDR LESS THAN END                  
         BL    STXIT4              ALLOW IT, MUST BE ENDING ADDRESS             
         C     R5,=F'65536'        LENGTH CAN'T BE OVER 64K                     
         BNH   *+6                                                              
         DC    H'0'                                                             
         AR    R5,R4               START + LEN = END                            
*                                                                               
STXIT4   STM   R4,R5,0(R3)                                                      
*                                                                               
         TM    4(R2),X'80'         CHECK FOR E-O-L                              
         BO    STXITX                                                           
         LA    R2,8(R2)                                                         
         LA    R3,8(R3)                                                         
         BCT   R0,STXIT2                                                        
         SH    R3,=H'8'                                                         
*                                                                               
STXITX   MVI   4(R3),X'80'         FORCE E-O-L FLAG                             
*                                                                               
         L     R0,=A(STXPRT)                                                    
         A     R0,RELO                                                          
         L     R1,=A(STXREGS)                                                   
         A     R1,RELO                                                          
*                                                                               
*&&DO*&& STXIT PC,(0),(1)                                                       
*&&OS*&& SPIE  STXPRT,(1,15)                                                    
         XMOD1 1                                                                
         SPACE 2                                                                
RELO     DC    A(0)                                                             
         LTORG                                                                  
         EJECT                                                                  
STXPRT   DS    0D                                                               
         DROP  RB                                                               
         USING *,RF                                                             
         STM   RD,RC,SPIERD        DO NOT SAVE REGS AT RD ADDRESS               
*                                                                               
         DROP  RF                                                               
         LR    RB,RF                                                            
         USING STXPRT,RB                                                        
*                                                                               
         MVC   STXPSW,4(R1)        SAVE USER PSW                                
         MVC   STXREGS(12),20(R1)  SAVE USER REGS R0-R2 (FROM PIE)              
         MVC   STXREGS+12(40),SPIER3 R3-RC (FROM ENTRY REGS)                    
         ST    RD,STXREGS+52       RD (FROM REGS TO SPIE)                       
         MVC   STXREGS+56(8),12(R1) RE-RF (FROM PIE)                            
         ST    RD,STXSAVE+4        CAN'T USE LAST SAVE AREA                     
         LA    RD,STXSAVE          SET UP NEW                                   
*                                                                               
         LA    R3,DMPLIST-8                                                     
NXTDMP   LA    R3,8(R3)                                                         
         LM    R4,R5,0(R3)         GET START & END ADDRESSES                    
         LA    R5,0(R5)            CLEAR HIGH ORDER BYTE                        
         BAS   R9,PDUMP                                                         
         TM    4(R3),X'80'                                                      
         BZ    NXTDMP                                                           
*                                                                               
*&&DO*&& CANCEL                                                                 
*&&OS*&& ABEND 999                                                              
         EJECT                                                                  
PDUMP    GOTO1 =V(PDUMPER),P1,STXPSW,(R4),(R5)                                  
         BR    R9                                                               
*                                                                               
         DS    0D                                                               
         DC    CL8'STXREGS'                                                     
STXPSW   DS    D                                                                
STXREGS  DS    8D                                                               
DMPLIST  DC    20F'0'                                                           
         DC    CL8'STXSAVE'                                                     
STXSAVE  DS    16D                                                              
*                                                                               
P1       DS    A                                                                
P2       DS    A                                                                
P3       DS    A                                                                
P4       DS    A                                                                
P5       DS    A                                                                
P6       DS    A                                                                
SPIERD   DS    A                                                                
SPIERE   DS    A                                                                
SPIERF   DS    A                                                                
SPIER0   DS    A                                                                
SPIER1   DS    A                                                                
SPIER2   DS    A                                                                
SPIER3   DS    A                                                                
SPIER4   DS    A                                                                
SPIER5   DS    A                                                                
SPIER6   DS    A                                                                
SPIER7   DS    A                                                                
SPIER8   DS    A                                                                
SPIER9   DS    A                                                                
SPIERA   DS    A                                                                
SPIERB   DS    A                                                                
SPIERC   DS    A                                                                
         LTORG                                                                  
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'008DDSTXITOLD05/01/02'                                      
         END                                                                    
