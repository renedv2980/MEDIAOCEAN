*          DATA SET MPLDCPTR   AT LEVEL 002 AS OF 01/29/86                      
*CATALP MPLDCPTR                                                                
         TITLE 'LDCPTR - MEDIA PLANNING - CREATE DIRECTORY POINTERS'            
         PRINT NOGEN                                                            
*        PARA1 A(RECORD)                                                        
*        PARA2 A(AREA TO CREATE PASSIVE POINTERS)                               
*              LAST POINTER FOLLOWED BY X'00'                                   
*                                                                               
LDCPTR   CSECT                                                                  
         NMOD1 WRKX-WRKD,MPLDCPTR                                               
         USING WRKD,RC                                                          
         LM    R2,R3,0(R1)         R2=A(PRIME DATA RECORD)                      
         MVC   0(32,R3),0(R2)      SET ONE ACTIVE POINTER                       
         MVC   32(4,R3),34(R2)     STATUS FROM PARENT                           
         XC    36(4,R3),36(R3)                                                  
         LA    R3,40(R3)           R3=A(NEXT PASSIVE POINTER RECORD)            
         XC    0(40,R3),0(R3)                                                   
         SR    RF,RF                                                            
         LA    RE,42(R2)           RE=A(NEXT RECORD ELEMENT)                    
         SPACE 2                                                                
VEH      CLI   0(R2),C'V'          TEST VEHICLE RECORD                          
         BNE   VEHX                                                             
         LA    R0,100                                                           
VEH1     CLI   0(RE),0             TEST END OF RECORD                           
         BE    EXIT                                                             
         CLI   1(RE),2             TEST FUNNY ELEMENT                           
         BL    EXIT                                                             
         CLI   0(RE),X'20'         TEST VEHICLE PASSIVE ELEMENT                 
         BE    VEH3                                                             
VEH2     IC    RF,1(RE)            BUMP TO NEXT ELEMENT                         
         AR    RE,RF                                                            
         BCT   R0,VEH1                                                          
*                                                                               
VEH3     LA    R6,2(RE)            R6=A(LIST OF ALIASES)                        
         IC    RF,1(RE)                                                         
         SH    RF,=H'2'                                                         
         SR    RE,RE                                                            
         D     RE,=F'8'                                                         
         LTR   RE,RE               EXIT IF FUNNY ALIAS LIST                     
         BNZ   EXIT                                                             
         LTR   R5,RF               R5=NUM OF 8-CHR ALIASES                      
         BZ    EXIT                                                             
*                                                                               
VEH4     XC    00(32,R3),0(R3)     SET ALIAS POINTER                            
         MVI   00(R3),C'V'                                                      
         MVI   03(R3),C'P'                                                      
         MVC   04(8,R3),0(R6)                                                   
         MVC   32(4,R3),34(R2)     STATUS FROM PARENT                           
         OI    32(R3),X'01'        SET PASSIVE FLAG                             
         XC    36(4,R3),36(R3)                                                  
*                                                                               
VEH5     LA    R3,40(R3)           BUMP TO NEXT PASSIVE POINTER REC             
         XC    0(40,R3),0(R3)                                                   
         LA    R6,8(R6)            BUMP TO NEXT ALIAS NAME                      
         BCT   R5,VEH4                                                          
*                                                                               
VEHX     EQU   *                                                                
         SPACE 2                                                                
EXIT     XMOD1 1                                                                
         EJECT                                                                  
         LTORG                                                                  
         SPACE 2                                                                
WRKD     DSECT                                                                  
DUB      DS    D                                                                
WRKX     EQU   *                                                                
         SPACE 2                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'002MPLDCPTR  01/29/86'                                      
         END                                                                    
