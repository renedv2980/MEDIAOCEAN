*          DATA SET DRIVINP    AT LEVEL 004 AS OF 05/01/02                      
*          DATA SET DRIVIN     AT LEVEL 024 AS OF 03/10/94                      
*CATALP DRIVINP                                                                 
         TITLE 'DRIVIN - INPUT HANDLER FOR DRIVER'                              
DRIVIN   CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 DINDX-DIND,**DRIN**                                              
         USING DIND,RC                                                          
         L     RA,0(R1)                                                         
         USING GLOBALD,RA                                                       
         CLI   GLANYERR,C'Y'       DON'T PROCESS IF PREVIOUS ERRORS             
         BE    XIT                                                              
         EJECT                                                                  
*              BROWSE DRIVE TABLE - REC STATEMENT                               
         SPACE 1                                                                
         MVI   GLRECNO,0                                                        
         LA    R2,GLAINTD-4        R2=A(INTERNAL RECORD DETAILS)                
         L     R3,GLADTAB          R3=A(DRIVE TABLE)                            
*                                  R4=A(INPUT FIELD IN RECORD)                  
         SPACE 1                                                                
DIN2     CLI   0(R3),X'10'         LOOK FOR REC                                 
         BE    DIN10                                                            
         CLI   0(R3),X'20'         AND IN                                       
         BE    DIN20                                                            
         CLI   0(R3),0                                                          
         BNE   DINEND                                                           
         CLI   RECYN,C'N'                                                       
         BE    *+8                                                              
         BAS   RE,RECOUT           OUTPUT LAST RECORD                           
         B     XIT                                                              
         SPACE 1                                                                
         USING DRRECD,R3                                                        
DIN10    CLI   GLRECNO,0           UNLESS THIS IS THE FIRST                     
         BE    DIN12                                                            
         CLI   RECYN,C'N'          OR THIS REC REJECTED                         
         BE    DIN12                                                            
         BAS   RE,RECOUT           OUTPUT RECORDS                               
         SPACE 1                                                                
DIN12    AI    GLRECNO,1                                                        
         LA    R2,4(R2)                                                         
         MVI   RECYN,C'N'                                                       
         MVC   GLRECLAB,DRRECLAB   SET ANY RECORD LABEL                         
*                                  CHECK RECORD LEVEL IFS                       
         GOTO1 =V(DRIVTEST),DMCB,(RA),DRRECIFS                                  
         BNE   DINEND                                                           
         MVI   GLHOOK,GLINREC      TELL APPLIC WE'RE ABOUT TO PROCESS A         
         XC    GLAROUT,GLAROUT     RECORD FOR INPUT                             
         MVI   GLAROUT,1                                                        
         XC    GLARGS,GLARGS                                                    
         MVC   GLARGS(1),GLRECNO   PASS RECORD NUM IN ARGS                      
         ST    R3,GLADTENT                                                      
         BAS   RE,GOHOOK                                                        
         CLI   GLHOOK,GLDONT       APPLIC MAY REJECT THIS RECORD                
         BE    DINEND                                                           
         MVI   RECYN,C'Y'                                                       
         LH    RF,BGRECLEN                                                      
         L     RE,GLAIO                                                         
         XCEF                                                                   
         L     R4,GLAIO                                                         
         MVC   0(1,R4),GLRECNO     EACH RECORD STARTS WITH NO                   
         LA    R4,2(R4)                                                         
         B     DINEND                                                           
         EJECT                                                                  
*              DEAL WITH IN STATEMENTS IN DRIVE TABLE                           
         SPACE 3                                                                
*                                  R4=A(INPUT FIELD)                            
DIN20    CLI   RECYN,C'N'                                                       
         BE    DINEND                                                           
         BAS   RE,PRECLEAR                                                      
         USING DRIND,R3                                                         
         GOTO1 =V(DRIVTEST),DMCB,(RA),DRINIFS                                   
         BNE   DIN20X                                                           
         CLI   DRINELEN,96         IS THERE A LITERAL?                          
         BE    DIN20B                                                           
         ZIC   R1,DRINELEN         YES SO USE IT                                
         SH    R1,=H'97'                                                        
         ZIC   R0,DRINLEN          MAKE SURE FIELD IS LARGE ENOUGH              
         BCTR  R0,0                                                             
         CR    R1,R0                                                            
         BL    *+6                                                              
         LR    R1,R0                                                            
         EX    R1,*+8                                                           
         B     DIN20X                                                           
         MVC   0(0,R4),DRINLIT                                                  
         SPACE 1                                                                
DIN20B   MVI   GLHOOK,GLROUT       BUILD HOOK FOR INPUT ROUTINE                 
         MVC   GLLABEL,DRINROUT                                                 
         MVC   GLAROUT,DRINRADD                                                 
         MVC   GLARGS,DRINARGS                                                  
         ST    R4,GLAIFLD                                                       
         ST    R3,GLADTENT                                                      
         BAS   RE,GOHOOK                                                        
         B     DIN20X                                                           
         SPACE 1                                                                
DIN20X   BAS   RE,NUMCHECK         CHECK FOR ACTIVE NUMERIC                     
         ZIC   R1,DRINLEN                                                       
         AR    R4,R1                                                            
         SPACE 1                                                                
DINEND   ZIC   R1,1(R3)            BUMP TO NEXT DRIVE TABLE ENTRY               
         AR    R3,R1                                                            
         B     DIN2                                                             
         EJECT                                                                  
*              PRECLEAR FIELD                                                   
         SPACE 3                                                                
*                                  R3=A(IN ELEMENT)                             
*                                  R4=A(INPUT FIELD)                            
         SPACE 1                                                                
PRECLEAR NTR1                                                                   
         USING DRIND,R3                                                         
         ZIC   R1,DRINLEN          PRE CLEAR FIELD                              
         BCTR  R1,0                                                             
         EX    R1,*+8                                                           
         B     *+10                                                             
         XC    0(0,R4),0(R4)                                                    
         SPACE 1                                                                
         CLI   DRINTYPE,C'P'       IF TYPE IS PACKED                            
         BE    PRECL2                                                           
         CLI   DRINTYPE,C'M'          OR MASKED                                 
         BNE   XIT                                                              
         SPACE 1                                                                
PRECL2   ZIC   R1,DRINLEN                                                       
         AR    R1,R4                                                            
         BCTR  R1,0                                                             
         MVI   0(R1),X'0C'         ENSURE PACKED ZERO IN RECORD                 
         B     XIT                                                              
         EJECT                                                                  
*              CHECK FOR ACTIVE NUMERIC FIELDS                                  
         SPACE 3                                                                
*                                  R3=A(DRIVE TABLE IN ELEMENT)                 
*                                  R4=A(DATA)                                   
         SPACE 1                                                                
NUMCHECK NTR1                                                                   
         CLI   DRINTYPE+1,C'+'                                                  
         BNE   XIT                                                              
         CLI   DRINTYPE,C'C'       THESE NOT REALLY ADDITIVE                    
         BE    XIT                                                              
         CLI   DRINTYPE,C'X'                                                    
         BE    XIT                                                              
         CLI   DRINTYPE,C'W'                                                    
         BE    XIT                                                              
         CLC   DRINROUT(5),=C'COUNT'   (NOT A REAL ADDITIVE COL)                
         BE    XIT                                                              
         ZIC   R0,DRINREP                                                       
         SPACE 1                                                                
NUCH2    ZIC   R1,DRINFLEN                                                      
         BCTR  R1,0                R1=L'DATA-1                                  
         LR    R5,R4               R5=A(THIS DATA)                              
         CLI   DRINTYPE,C'B'                                                    
         BE    NUMCB                                                            
         CLI   DRINTYPE,C'P'                                                    
         BE    NUMCP                                                            
         CLI   DRINTYPE,C'E'                                                    
         BE    NUMCE                                                            
         LA    R5,1(R5)            GET PAST IRRELEVANT BYTE                     
         BCTR  R1,0                                                             
         B     NUMCP               SCALED AND MASKED                            
         SPACE 1                                                                
NUMCB    EX    R1,NUMCBTST         BINARY                                       
         BZ    NUMCHNO                                                          
         B     NUMCHYES                                                         
NUMCBTST OC    0(0,R5),0(R5)                                                    
         SPACE 1                                                                
NUMCP    EX    R1,NUMCBTST         (PROTECT AGAINST BINARY ZEROS)               
         BNZ   NUMCP2                                                           
         SLL   R1,4                                                             
         EX    R1,NUMPZAP          SUPPOSED TO BE PACKED SO ZAP                 
         B     NUMCHNO             AND TREAT AS ZERO                            
         SPACE 1                                                                
NUMCP2   SLL   R1,4                PACKED SCALED AND MASKED                     
         EX    R1,NUMCPTST                                                      
         BE    NUMCHNO                                                          
         B     NUMCHYES                                                         
NUMPZAP  ZAP   0(0,R5),=P'0'                                                    
NUMCPTST CP    0(0,R5),=P'0'                                                    
         SPACE 1                                                                
NUMCE    EX    R1,NUMCEADJ         EBCDIC - ADJUST BLANKS                       
         EX    R1,NUMCETST                  CHECK FOR ZEROS                     
         BE    NUMCHNO                                                          
         B     NUMCHYES                                                         
NUMCEADJ OC    0(0,R5),=16C'0'                                                  
NUMCETST CLC   0(0,R5),=16C'0'                                                  
         SPACE 1                                                                
NUMCHNO  CLI   ANYACTIV,0                                                       
         BNE   NUMCHX                                                           
         MVI   ANYACTIV,1                                                       
         B     NUMCHX                                                           
         SPACE 1                                                                
NUMCHYES MVI   ANYACTIV,2                                                       
         SPACE 1                                                                
NUMCHX   ZIC   R1,DRINFLEN                                                      
         AR    R4,R1                                                            
         BCT   R0,NUCH2                                                         
         B     XIT                                                              
         SPACE 1                                                                
ANYACTIV DC    X'00'                                                            
         EJECT                                                                  
*              ROUTINE TO WRITE OUT THE RECORDS                                 
         SPACE 3                                                                
RECOUT   NTR1                                                                   
         BAS   RE,TRAP                                                          
         TM    GLINDS,GLPALDET     OPTION TO GET INACTIVE RECORDS               
         BO    RECOUT1                                                          
         CLI   ANYACTIV,1          IGNORE ZERO ACTIVITY RECORDS                 
         BNE   RECOUT1                                                          
         MVI   ANYACTIV,0                                                       
         B     XIT                                                              
         SPACE 1                                                                
RECOUT1  MVI   ANYACTIV,0                                                       
         L     R2,0(R2)                                                         
         USING GLINTD,R2                                                        
         MVI   GLHOOK,0                                                         
         LA    R0,RECSAVE          SAVE THE RECORD                              
         LH    R1,GLRECLEN                                                      
         L     RE,GLAIO                                                         
         LR    RF,R1                                                            
         MVCL  R0,RE                                                            
*                                                                               
         LA    R3,GLTOTLEV+GLNUMLEV-1                                           
         LA    R4,GLLCBS+GLNUMLEV-1                                             
         LA    R0,GLNUMLEV                                                      
         SPACE 1                                                                
RECOUT2  L     R1,GLAIO            NOTE THE LEVEL NUMBER                        
         STC   R0,1(R1)                                                         
         STC   R0,TRIMLEV                                                       
         CLI   0(R3),X'10'         TEST DETAILED TOTAL                          
         BL    *+12                                                             
         BAS   RE,RECDET           YES                                          
         B     RECNEXT                                                          
         CLI   0(R3),1             SEE IF RECORD NEEDED AT THIS LEVEL           
         BL    RECNEXT             NO                                           
         BH    *+12                                                             
         BAS   RE,RECTOT           TOTAL RECORD AT THIS LEVEL                   
         B     RECNEXT                                                          
         TM    GLINDS,GLINODET     DETAIL REC - CHECK THAT IT'S NEEDED          
         BO    *+8                 NO                                           
         BAS   RE,RECPUT           YES - PUT DETAIL RECORD                      
         MVI   TRIMSW,0                                                         
         SPACE 1                                                                
RECNEXT  LTR   R0,R0                                                            
         BZ    XIT                                                              
         BCTR  R3,0                                                             
         BCTR  R4,0                                                             
         BCTR  R0,0                                                             
         B     RECOUT2                                                          
         SPACE 1                                                                
RECTOT   NTR1                                                                   
         CLI   GLMAXTLV,0          OPTIONS TO LIMIT TOTAL RECORDS               
         BE    *+14                                                             
         CLC   TRIMLEV,GLMAXTLV                                                 
         BL    XIT                                                              
         CLI   GLMINTLV,0                                                       
         BE    RECTOT2                                                          
         CLC   TRIMLEV,GLMINTLV                                                 
         BH    XIT                                                              
         SPACE 1                                                                
RECTOT2  ZIC   R1,0(R4)            R1=L'CONTROL BREAK                           
         L     RE,GLAIO            RE=A(PIECE OF KEY TO CLEAR)                  
         LA    RE,2(RE,R1)                                                      
         LH    RF,GLKEYLEN         RF=L'PIECE TO BE CLEARED                     
         SR    RF,R1                                                            
         BCTR  RF,0                                                             
         EX    RF,*+8                                                           
         B     *+10                                                             
         XC    0(0,RE),0(RE)                                                    
         BAS   RE,ANYTRIM                                                       
         BAS   RE,RECPUT                                                        
         B     XIT                                                              
         SPACE 1                                                                
RECDET   NTR1                                                                   
******** CLI   GLHOOK,GLDONT       TEST RECORD ALREADY REJECTED                 
******** BE    RDX                                                              
         OI    GLINDS2,GLPUTDT     INDICATE PUTTING DETAILED TOTALS NOW         
         CLI   GLMINTLV,0                                                       
         BE    *+14                                                             
         CLC   TRIMLEV,GLMINTLV                                                 
         BH    RDX                                                              
         ZIC   RF,GLDETLEV         CHECK CURRENT LEVEL LOW ENOUGH               
         BCTR  RF,0                FOR DETAILED TOTALS                          
         ZIC   RE,TRIMLEV                                                       
         SR    RF,RE                                                            
         BNP   RDX                                                              
         ZIC   R5,0(R3)            CHECK WE CAN SHOW THE DETAILS ASKED          
         SLL   R5,28               FOR                                          
         SRL   R5,28                                                            
         CR    RF,R5                                                            
         BL    RDX                                                              
         B     RD04                                                             
*                                                                               
RD02     TM    GLTOTIND,GLTITOT    CHECK TOT AT LEVEL ABOVE DETAIL LEV          
         BZ    RD04                                                             
         ZIC   RE,1(R1)            YES - THEN AUTO GENERATE A REGULAR           
         BCTR  RE,0                      TOTAL RECORD                           
         CLI   GLMAXTLV,0                                                       
         BE    *+12                                                             
         CLM   RE,1,GLMAXTLV       OPTIONS TO LIMIT REGULAR TOTAL RECS          
         BL    RD04                                                             
         CLI   GLMINTLV,0                                                       
         BE    *+12                                                             
         CLM   RE,1,GLMINTLV                                                    
         BH    RD04                                                             
         STC   RE,1(R1)                                                         
         ZIC   RE,GLDETDSP                                                      
         LA    RE,2(RE,R1)                                                      
         ZIC   RF,GLDETLEN                                                      
         BCTR  RF,0                                                             
         EX    RF,*+8                                                           
         B     *+10                                                             
         XC    0(0,RE),0(RE)                                                    
         L     RF,GLAIO                                                         
         ST    R1,GLAIO                                                         
         BAS   RE,RECPUT                                                        
         ST    RF,GLAIO                                                         
*                                                                               
RD04     CLI   GLMAXTLV,0          TEST OPTION TO LIMIT TOTALS                  
         BE    RD05                                                             
         CLC   TRIMLEV,GLMAXTLV    YES-TEST DETAIL TOTS MAY BE LIMITED          
         BNL   RD05                                                             
         ZIC   RF,GLDETLEV         YES-                                         
         SR    RF,R5               RF=LAST LEVEL TO RECEIVE FF'S                
         CLM   RF,1,GLMAXTLV       TEST MAX TOTAL LEV WILL RECEIVE FF'S         
         BNL   RDX                 YES-DO NOT GENERATE ANY MORE TOTALS          
*                                                                               
RD05     LA    R0,RECBUILD         BUILD RECORD                                 
         LH    R1,GLRECLEN                                                      
         LA    RE,RECSAVE                                                       
         LR    RF,R1                                                            
         MVCL  R0,RE                                                            
         LA    R1,RECBUILD         R1=A(RECORD)                                 
         MVC   0(1,R1),GLRECNO                                                  
         MVC   1(1,R1),GLDETLEV                                                 
*                                                                               
         ZIC   RF,GLDETLEV         MOVE FF'S TO LEVELS BELOW THIS ONE           
         SR    RF,R5               WHICH ARE NOT DETAILED                       
         LA    RF,GLLCBS-1(RF)                                                  
         ZIC   RE,0(RF)                                                         
         ZIC   RF,0(R4)                                                         
         SR    RE,RF                                                            
         BCTR  RE,0                                                             
         LA    R6,2(RF,R1)                                                      
         MVI   0(R6),X'FF'                                                      
         LTR   RE,RE                                                            
         BNP   RD06                                                             
         BCTR  RE,0                                                             
         EX    RE,*+8                                                           
         B     RD06                                                             
         MVC   1(0,R6),0(R6)                                                    
*                                                                               
RD06     L     RF,GLAIO            PUT DETAILED TOTAL RECORD                    
         ST    R1,GLAIO                                                         
         BAS   RE,RECPUT                                                        
         ST    RF,GLAIO                                                         
*                                                                               
RD08     BCT   R5,RD02             AUTO GENERATE DETAILED TOTALS AT             
*                                  ALL LOWER LEVELS                             
         TM    GLTOTIND,GLTITOT                                                 
         BZ    RDX                                                              
         L     R1,GLAIO            FINAL REGULAR TOTAL RECORD                   
         MVC   1(1,R1),TRIMLEV                                                  
         BAS   RE,RECTOT                                                        
*                                                                               
RDX      NI    GLINDS2,255-GLPUTDT                                              
         B     XIT                                                              
         EJECT                                                                  
*              ROUTINE TO PUT A RECORD TO SORTER                                
         SPACE 3                                                                
RECPUT   NTR1                                                                   
         BAS   RE,TRAP                                                          
         MVI   GLHOOK,GLPUTSRT     TELL APPLIC WE'RE WRITING                    
         XC    GLAROUT,GLAROUT                                                  
         MVI   GLAROUT,1                                                        
         BAS   RE,GOHOOK                                                        
         BAS   RE,TRAP                                                          
         CLI   GLHOOK,GLDONT       APPLIC CAN REJECT                            
         BE    XIT                                                              
         MVI   GLHOOK,GLPUTSRT     TELL SYSTEM DRIVER AS WELL                   
         XC    GLAROUT,GLAROUT                                                  
         MVI   GLAROUT,2                                                        
         BAS   RE,GOHOOK                                                        
         BAS   RE,TRAP                                                          
         CLI   GLHOOK,GLDONT       WHO CAN ALSO REJECT                          
         BE    XIT                                                              
         CLI   FIRSTSW,C'Y'        TEST FIRST TIME                              
         BNE   RECPUT2                                                          
         MVI   FIRSTSW,C'N'                                                     
         SR    RE,RE                                                            
         ICM   RE,3,GLMAXREC       YES-TEST MAXIMUM INPUT RECORD COUNT          
         BZ    RECPUT2                                                          
         CVD   RE,DUB              YES-SET THE MAX COUNT                        
         ZAP   MAXREC,DUB                                                       
         SPACE 1                                                                
RECPUT2  CP    MAXREC,=P'0'        TEST MAX INPUT RECORD COUNT                  
         BE    *+14                                                             
         CP    COUNT,MAXREC        YES-HAVE WE GOT THERE YET ?                  
         BNL   XIT                                                              
         AP    COUNT,=P'1'                                                      
         BAS   RE,TRASOME                                                       
         L     R4,GLAIO                                                         
         CLI   GLABCSW,0           ABC DOES NOT OUTPUT TO SORT                  
         BNE   XIT                                                              
         GOTO1 =V(DRIVPUT),DMCB,(RA),(R4)                                       
         B     XIT                                                              
         EJECT                                                                  
*              ROUTINE TO TRIM DOWN TOTAL RECORDS                               
         SPACE 3                                                                
ANYTRIM  NTR1                                                                   
         CLI   TRIMSW,1            ONLY NEEDED ONCE                             
         BE    XIT                                                              
         MVI   TRIMSW,1                                                         
         L     R3,GLAFIN                                                        
         USING DRIND,R3                                                         
         SPACE 1                                                                
AT2      CLI   0(R3),0             LOOKING FOR INS                              
         BE    XIT                                                              
         CLI   0(R3),X'10'                                                      
         BE    XIT                                                              
         CLI   0(R3),X'20'                                                      
         BNE   ATEND                                                            
         TM    GLINDS3,GLPNACTO    OPTION TO PRINT NON-ADDITIVE COLS            
         BO    ATEND               ON TOTALS                                    
         CLI   DRINTYPE+1,C'+'     DON'T CREAM ADDITIVE FIELDS                  
         BE    ATEND                                                            
         CLC   DRINLEV,TRIMLEV     OR KEY DATA AT LOW LEVEL                     
         BNH   ATEND                                                            
         L     R4,GLAIO            POISITION R4 TO DATA FIELD                   
         AH    R4,DRINDISP                                                      
         ZIC   R1,DRINLEN                                                       
         BCTR  R1,0                                                             
         EX    R1,*+8                                                           
         B     ATEND                                                            
         XC    0(0,R4),0(R4)       AND CLEAR IT                                 
         SPACE 1                                                                
ATEND    ZIC   R1,1(R3)                                                         
         AR    R3,R1                                                            
         B     AT2                                                              
         SPACE 1                                                                
TRIMSW   DC    X'00'                                                            
TRIMLEV  DC    X'00'                                                            
         SPACE 1                                                                
YES      SR    R1,R1                                                            
         B     *+8                                                              
         SPACE 1                                                                
NO       LA    R1,1                                                             
         LTR   R1,R1                                                            
         SPACE 1                                                                
XIT      XIT1                                                                   
         EJECT                                                                  
*              ERROR ROUTINES                                                   
         SPACE 3                                                                
FATAL1   DC    H'0'                                                             
         SPACE 1                                                                
FATAL2   DC    H'0'                                                             
         SPACE 1                                                                
ERROR    NTR1                                                                   
         LA    R1,ERRLIST                                                       
         SPACE 1                                                                
ERROR2   CLC   ERRNUM,0(R1)        LOOK UP FOR ERROR                            
         BE    ERROR4                                                           
         ZIC   R0,1(R1)                                                         
         AR    R1,R0                                                            
         CLI   0(R1),X'FF'                                                      
         BNE   ERROR2                                                           
         DC    H'0'                                                             
         SPACE 1                                                                
ERROR4   ZIC   RE,1(R1)            PICK IT OFF                                  
         SH    RE,=H'3'                                                         
         EX    RE,*+8                                                           
         B     *+10                                                             
         MVC   P(0),2(R1)                                                       
         BAS   RE,PRINTOUT                                                      
         BAS   RE,PRINTOUT                                                      
         B     XIT                                                              
         SPACE 1                                                                
ERRLIST  DS    0H                                                               
         DC    X'FF'                                                            
         EJECT                                                                  
*              HOOK TO APPLICATION OR SYSTEM DRIVER                             
         SPACE 3                                                                
GOHOOK   NTR1                                                                   
         CLI   GLAROUT,0                                                        
         BE    XIT                                                              
         CLI   GLAROUT,2           FIRST BYTE INDICATES WHER                    
*                                  1=APPLICATION 2=SYSDRIVER                    
         BE    GOH2                                                             
         BH    GOH3                                                             
         L     RF,GLAHOOK          PICK UP APPLICATION HOOK                     
         LTR   RF,RF                                                            
         BZ    XIT                                                              
         L     RE,USERRD           USERS RD                                     
         LM    R0,RC,20(RE)        RESTORE USERS R0-RC                          
         BASR  RE,RF               RF CONTAINED A(HOOK ROUTINE)                 
         XIT1                                                                   
         SPACE 1                                                                
GOH2     L     RF,GLASYSDR                                                      
         LTR   RF,RF                                                            
         BZ    XIT                                                              
         MVC   WORK,0(RF)                                                       
         GOTO1 (RF),DMCB,(RA)                                                   
         B     XIT                                                              
         SPACE 1                                                                
GOH3     GOTO1 =V(DRIVROUT),DMCB,(RA)                                           
         B     XIT                                                              
         SPACE 1                                                                
         EJECT                                                                  
*              TRACE AND PRINTING FACILITIES                                    
         SPACE 3                                                                
TRASOME  NTR1                                                                   
         CLI   TRACOPT,C'Y'        SELECTIVE - R2=A(ELEMENT)                    
         BNE   XIT                                                              
*****    CLI   TRACOUNT,25         TRACE FIRST 25 RECORDS                       
*****    BNL   XIT                                                              
*****    AI    TRACOUNT,1                                                       
         SPACE 1                                                                
         MVI   SPACES,C' '                                                      
         MVC   SPACES+1(132),SPACES                                             
         L     R2,GLAIO            R2=A(RECORD)                                 
         LH    R4,BGRECLEN                                                      
         SPACE 1                                                                
TR2      LR    R3,R4               R3=AMOUNT LEFT TO PRINT                      
         CH    R3,=H'132'                                                       
         BNH   *+8                                                              
         LH    R3,=H'132'          INSURE NOT MORE THAN L'PRINT LINE            
         BCTR  R3,0                -1 FOR EX MVC                                
         SPACE 1                                                                
         MVC   P,SPACES                                                         
         MVC   P2,SPACES                                                        
         MVC   P3,SPACES                                                        
         EX    R3,TRAM1                                                         
         BAS   RE,PRINTOUT                                                      
         LA    R3,1(R3)                                                         
         GOTO1 HEXOUT,DMCB,(R2),P2,(R3),=C'SEP'                                 
         BCTR  R3,0                                                             
         EX    R3,TRAM2                                                         
         BAS   RE,PRINTOUT                                                      
         LA    RF,P2+1(R3)                                                      
         EX    R3,TRAM3                                                         
         BAS   RE,PRINTOUT                                                      
         BAS   RE,PRINTOUT                                                      
         SPACE 1                                                                
         LA    R3,1(R3)                                                         
         AR    R2,R3               BUMP TO NEXT POSITION IN RECORD              
         SR    R4,R3               R4=AMOUNT LEFT TO PRINT                      
         BP    TR2                                                              
         B     XIT                                                              
         SPACE 1                                                                
TRAM1    MVC   P(0),0(R2)                                                       
TRAM2    MVC   P(0),P2                                                          
TRAM3    MVC   P(0),0(RF)                                                       
TRACOUNT DC    X'00'                                                            
FIRSTSW  DC    C'Y'                                                             
MAXREC   DC    PL3'0'                                                           
COUNT    DC    PL8'0'                                                           
         SPACE 1                                                                
PRINTOUT NTR1                                                                   
         OC    P,SPACES                                                         
         GOTO1 PRINT,DMCB,PFILL,=C'BL01'                                        
         MVC   P,SPACES                                                         
         B     XIT                                                              
*                                                                               
TRAP     NTR1                                                                   
         L     RF,GLAIO                                                         
         MVC   P,0(RF)                                                          
         CLI   42(RF),C'X'         X IN FOX IN DPTTABLE PRINT                   
         BNE   TRAP4                                                            
*                                                                               
         B     TRAP4                                                            
         DC    64X'00'                                                          
TRAP4    DS    0H                                                               
         MVI   P,C'*'                                                           
TRAPX    XIT1                                                                   
         EJECT                                                                  
*              DSECT FOR INPUT MODULE                                           
         SPACE 3                                                                
DIND     DSECT                                                                  
         SPACE 1                                                                
ADRIVEL  DS    A                                                                
PFILL    DS    CL1                                                              
P        DS    CL133                                                            
P2       DS    CL132                                                            
P3       DS    CL132                                                            
SPACES   DS    CL133                                                            
ELCODE   DS    CL1                                                              
ERRNUM   DS    CL1                                                              
RESULT   DS    CL1                                                              
RECYN    DS    CL1                                                              
         SPACE 1                                                                
DMWORK   DS    12D                                                              
KEY      DS    CL36                                                             
         SPACE 1                                                                
RECSAVE  DS    XL2000                                                           
RECBUILD DS    XL2000                                                           
         SPACE 1                                                                
DINDX    EQU   *                                                                
         PRINT OFF                                                              
       ++INCLUDE DRGLOBAL                                                       
       ++INCLUDE DRLOCAL                                                        
       ++INCLUDE DRINTRECD2                                                     
       ++INCLUDE DRIVETABLE                                                     
         PRINT ON                                                               
         SPACE 1                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'004DRIVINP   05/01/02'                                      
         END                                                                    
