*          DATA SET MPCHECK    AT LEVEL 004 AS OF 01/27/86                      
*CATALP MPCHECK                                                                 
         TITLE 'MPCHECK - CHECK AND INSERT PRINT COLS'                          
         PRINT NOGEN                                                            
MPCHECK  CSECT                                                                  
         SPACE 2                                                                
**********************************************************************          
*                                                                    *          
* MPCHECK :- DETERMINES ABSOLUTE COLUMN POSITIONS FOR PRINTING BOTH  *          
*            DATA AND COLUMN HEADINGS.                               *          
*            COLUMN POSITIONS ARE CALCULATED FROM A SUPPLIED START   *          
*            COLUMN AND GAP BETWEEN COLUMNS.                         *          
*            IF AN ABSOLUTE USER OVERRIDE IS SUPPLIED (I.E. PROCTAB  *          
*            HAS A VALUE IN DATA COL PRINT POSN ) THE VALUE ONLY     *          
*            APPLIES IF 1 SPACE EXISTS AFTER THE PREVIOUS COLUMN.    *          
*            IF A RELATIVE USER OVERRIDE IS SUPPLIED (I.E. PROCTAB   *          
*            HAS A VALUE IN HEADER COL PRINT POSN ) IT OVERIDES GAP= *          
*            OPTION.                                                 *          
*            HEADINGS ARE RIGHT JUSTIFIED OVER DATA IF THEY ARE      *          
*            NARROWER THAN THE DATA PRINT WIDTH OTHERWISE THE DATA   *          
*            IS CENTRED UNDER THE HEADING.                           *          
*                                                                    *          
**********************************************************************          
*                                                                               
*        PARAM 1 - ADDRESS OF PROCESSING TABLE    BYTES 1-4                     
*        PARAM 2 - START COLUMN NUMBER            BYTE  5                       
*        PARAM 3 - DEFAULT GAP SETTING            BYTE  6                       
*        PARAM 4 - MAX PRINT WIDTH                BYTE  7                       
*   RET  PARAM 5 - END COLUMN NUMBER              BYTE  8                       
*                + ACTUAL PRINT POSNS IN PROC TABLE                             
*        PARAM 7 - ADDRESS OF ERROR FIELD HEADER  BYTES 9-12                    
*        PARAM 8 - ADDRESS OF GENCONS STORAGE     BYTES13-16                    
         SPACE 3                                                                
         NMOD1 0,MPCHECK                                                        
         EJECT                                                                  
         L     RC,12(R1)           A(GENCON STORAGE)                            
         USING GEND,RC                                                          
         L     RA,ATWA                                                          
         USING CONHEADH-64,RA                                                   
         LR    R8,R1               SAVE PARMS ADDRESS                           
         L     R6,0(R1)            A(PROCESSING TABLE)                          
         USING PCMDD,R6                                                         
         ZIC   R5,4(R1)            START COLUMN NUMBER                          
         ZIC   R2,5(R1)            DEFAULT GAP SIZE                             
         LR    R3,R2               SAVE DEFAULT                                 
*                                                                               
         CLI   PCDPLOCC,0          HAS USER OVERRIDEN START COL ABS             
         BNE   CHK00               YES- LEAVE AS USER REQUESTED                 
         ZIC   R1,PCHPLOC          POSSIBLE OVERRIDE START COL RELATIVE         
         AR    R5,R1               INCREMENT BY RELATIVE OVERRIDE               
         STC   R5,PCDPLOCC         INSERT START LOCATION                        
         B     CHK03               CALC END OF 1ST FIELD                        
*                                                                               
CHK00    DS    0H                                                               
         ZIC   R5,PCDPLOCC         FORCE USER START POSN                        
         B     CHK03               CALC END OF 1ST FIELD                        
*                                                                               
CHK01    DS    0H                                                               
         CLI   PCLNTH,0            REACHED END OF TABLE ?                       
         BE    CHK90                                                            
         ZIC   R1,PCDPLOCC         GET USER OVERIDE ABSOLUTE (IF ANY)           
         CR    R1,R5               IS OVERRIDE BEYOND CURRENT COL ?             
         BH    CHK02               LEAVE USER OVERRIDE IF > CURR                
         CLI   PCHPLOC,0           GET USER OVERRIDE RELATIVE (IF ANY)          
         BE    CHK01A                                                           
         ZIC   R2,PCHPLOC          OVERRIDE STD GAP                             
CHK01A   AR    R5,R2               ADD GAP TO LAST COLUMN USED                  
         LR    R2,R3               RESTORE STD GAP                              
         B     CHK03                                                            
CHK02    DS    0H                                                               
         ZIC   R5,PCDPLOCC         POINT TO USER OVERIDE COLUMN NO              
CHK03    DS    0H                  CALC NEXT START COLUMN                       
         CLC   PCDWDTH,PCHWDTH     DATA SHORTER THAN HEADER ???                 
         BL    CHK04               YES - POSN BASED OH HEADER LENGTH            
*                                                                               
*                                  HEADER <= DATA SO RIGHT JUSTIFY              
         STC   R5,PCDPLOCC         DATA PRINT COLUMN                            
         ZIC   R1,PCDWDTH          DATA PRINT WIDTH                             
         AR    R1,R5               NEXT SPARE COL                               
         ZIC   R0,PCHWDTH          HEADER WIDTH                                 
         SR    R1,R0               COL POSN FOR HEADER                          
         STC   R1,PCHPLOC          SET IN PROC TAB                              
         ZIC   R1,PCDWDTH          WIDTH OF COL (TO CALC END POSN)              
         B     CHK05               CALC NEXT AVAILABLE COL                      
*                                                                               
*                                  HEADER > DATA SO CENTRE DATA -               
*                                  - UNDER HEADER                               
*                                                                               
CHK04    DS    0H                                                               
         STC   R5,PCHPLOC          SET HEADER POSN IN PROC TAB                  
         ZIC   R1,PCHWDTH          GET HEADER WIDTH                             
         ZIC   R0,PCDWDTH          GET DATA WIDTH                               
         SR    R1,R0                                                            
         LA    R1,1(R1)            FORCE TO NEXT EVEN NO (IF ODD)               
         SRL   R1,1                TOTAL DIFF / 2 ROUNDED                       
         AR    R1,R5               POSN FOR DATA TO START                       
         STC   R1,PCDPLOCC         SAVE IN PROC TABLE                           
         ZIC   R1,PCHWDTH          WIDTH OF CO, (TO CALC END POSN)              
*                                                                               
CHK05    DS    0H                                                               
         AR    R5,R1               BUMP TO END OF PRINT FIELD                   
         LA    R6,PCLEN(R6)        BUMP TO NEXT PROCTAB ENTRY                   
         B     CHK01               PROCESS THIS ENTRY                           
*                                                                               
CHK90    DS    0H                  END OF PROCESSING TABLE                      
*R5 POINTS AT OFFSET OF NEXT CHR (I.E. ACTUAL POSN OF LAST DATA FIELD)          
*MUST ADD 1 FOR RH BOX                                                          
         LA    R5,1(R5)            POINT TO POSN OF RH BOX                      
         STC   R5,7(R8)            PUT LENGTH IN PARAM BLOCK                    
         ZIC   R1,6(R8)            MAX LINE LENGTH                              
         CR    R1,R5               CHECK IF LINE TOO LONG                       
         BL    TRAPERR                                                          
*                                                                               
         B     XIT                                                              
         EJECT                                                                  
XIT      DS    0H                                                               
         XMOD1 1                                                                
         SPACE 2                                                                
TRAPERR  DS    0H                                                               
         XC    CONHEAD,CONHEAD                                                  
         LA    R2,CONHEADH                                                      
         OI    6(R2),X'80'                                                      
         LA    R2,8(R2)                                                         
         MVC   0(40,R2),=C'ERROR   PRINT WIDTH ...   PAGE WIDTH ...'            
         CVD   R5,DUB              PRINT WIDTH                                  
         UNPK  20(3,R2),DUB                                                     
         OI    22(R2),X'F0'                                                     
         CVD   R1,DUB              PAGE WIDTH                                   
         UNPK  37(3,R2),DUB                                                     
         OI    39(R2),X'F0'                                                     
         L     R2,8(R8)            A(FIELD HEADER) TO POSN CURSOR               
         GOTO1 ERREX2                                                           
*                                                                               
         EJECT                                                                  
         LTORG                                                                  
         SPACE 3                                                                
*THE FOLLOWING DSECTS ARE NOT PRINTED                                           
*MPPCMDD.MPEQUATES,MPRDRFFD,DDSPLWPORKD                                         
         PRINT OFF                                                              
       ++INCLUDE MPPCMDD                                                        
       ++INCLUDE MPEQUATES                                                      
       ++INCLUDE MPRDRFFD                                                       
       ++INCLUDE DDSPLWORKD                                                     
         PRINT ON                                                               
         SPACE 2                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'004MPCHECK   01/27/86'                                      
         END                                                                    
