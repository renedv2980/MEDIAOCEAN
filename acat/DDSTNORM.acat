*          DATA SET DDSTNORM   AT LEVEL 018 AS OF 05/01/02                      
*CATALP STNORM                                                                  
         TITLE 'MEAN, VARIANCE, STD DEV, AND VARIABLE NORMALIZATION'            
         PRINT NOGEN                                                            
STNORM   CSECT                                                                  
         NMOD1 10,STNORM                                                        
         SPACE 2                                                                
         L     RA,0(R1)                                                         
         USING STNORMD,RA          SEE DSECT FOR PROGRAM CONTROLS               
         USING WKD,RC                                                           
         SPACE 2                                                                
*                                  SET DEFAULT SPACINGS                         
         OC    STNIPSPC,STNIPSPC                                                
         BNZ   *+8                                                              
         MVI   STNIPSPC+3,4                                                     
         OC    STNOPSPC,STNOPSPC                                                
         BNZ   *+8                                                              
         MVI   STNOPSPC+3,4                                                     
*                                  SET PRECISION FACTOR                         
         MVC   PRECFCT,=F'1'                                                    
         CLI   STNPREC,0                                                        
         BE    VN4                                                              
         MVC   PRECFCT,=F'10'                                                   
         CLI   STNPREC,1                                                        
         BE    VN4                                                              
         MVC   PRECFCT,=F'100'                                                  
*                                                                               
VN4      DS    0H                                                               
         XC    STNACNT(16),STNACNT                                              
*                                  PASS 1 -SET MEAN AND COUNT ACTIVES           
         L     R2,STNIPVAR         START OF LIST                                
         L     R4,STNVCNT          COUNT                                        
         SR    R1,R1                                                            
         SR    RF,RF                                                            
         SR    R3,R3                                                            
*                                                                               
VN6      DS    0H                                                               
         CLC   0(4,R2),IGNVAL        TEST TO IGNORE                             
         BE    VN6B                                                             
         LA    RF,1(RF)                                                         
         A     R1,0(R2)                                                         
         C     R3,0(R2)            TEST VS LOWEST NEG                           
         BNH   *+8                                                              
         L     R3,0(R2)            SAVE LOWEST NEG VALUE                        
VN6B     DS    0H                                                               
         A     R2,STNIPSPC                                                      
         BCT   R4,VN6                                                           
*                                                                               
         LTR   RF,RF                                                            
         BZ    EXIT                NO ACTIVE ENTRIES                            
         ST    RF,STNACNT          SAVE COUNT OF ACTIVE ENTRIES                 
         ST    R3,LOWNEG           HOLD LOWEST NEG VALUE                        
         M     R0,PRECFCT          PRECISION FACTOR                             
         BAS   RE,DIV              /N                                           
         ST    R1,STNMEAN          MEAN                                         
*                                                                               
         TM    STNMASK,X'C0'       TEST NEED VARIANCE OR NORMALIZATION          
         BZ    VN20                NO -                                         
*                                  PASS 2 - VARIANCE                            
         L     R2,STNIPVAR         START OF LIST                                
         L     R3,STNVCNT          COUNT                                        
         SR    R4,R4               R4 AND R5 USED FOR DOUBLE WORD SUM           
         SR    R5,R5                                                            
*                                                                               
VN8      DS    0H                                                               
         CLC   0(4,R2),IGNVAL      TEST TO IGNORE                               
         BE    VN8B                                                             
         L     R1,0(R2)                                                         
         MH    R1,PRECFCT+2        PRECISION FACTOR                             
         S     R1,STNMEAN                                                       
         MR    R0,R1                                                            
         ALR   R5,R1               TEST CARRY                                   
         BC    12,*+8              NONE                                         
         A     R4,=F'1'                                                         
         AR    R4,R0                                                            
*                                                                               
VN8B     DS    0H                                                               
         A     R2,STNIPSPC                                                      
         BCT   R3,VN8                                                           
*                                                                               
         LR    R1,R5                                                            
         LR    R0,R4                                                            
         L     RF,STNACNT          / N                                          
         MH    RF,PRECFCT+2                                                     
         BAS   RE,DIV                                                           
         LTR   R1,R1                                                            
         BZ    VN20                EXIT IF VARIANCE = 0                         
         ST    R1,STNVRNCE         VARIANCE                                     
         M     R0,PRECFCT          TO GET CORRECT PLACES IN ROOT                
         ST    R1,DUB                                                           
*                                       SQRT OF VARIANCE = STD DEV              
         GOTO1 STNSQRT,DMCB,DUB                                                 
         MVC   STNSD,DMCB+4                                                     
*                                                                               
         TM    STNMASK,X'40'            TEST NEED NORMALIZED VARIABLE           
         BZ    VN20                     NO                                      
*                                  PASS 3 - CREATE NORMALIZED VARIABLE          
*                                           IN LIST 2                           
         L     R2,STNIPVAR         INPUT LIST                                   
         L     R3,STNOPVAR         OUTPUT LIST                                  
         L     R4,STNVCNT          COUNT                                        
         L     RF,STNSD                                                         
         LTR   RF,RF                                                            
         BZ    VN40                                                             
*                                                                               
VN12     DS    0H                                                               
         CLC   0(4,R2),IGNVAL      TEST TO IGNORE                               
         BE    VN12B                                                            
         L     R1,0(R2)                                                         
         MH    R1,PRECFCT+2        PRECISION FACTOR                             
         S     R1,STNMEAN                                                       
         M     R0,=F'100'          NORMALIZED RESULT ALWAYS N.NN                
         BAS   RE,DIV                                                           
         ST    R1,0(R3)                                                         
*                                                                               
VN12B    DS    0H                                                               
         A     R2,STNIPSPC                                                      
         A     R3,STNOPSPC                                                      
         BCT   R4,VN12                                                          
         B     VN40                                                             
*                                                                               
VN20     DS    0H                                                               
         TM    STNMASK,X'20'       TEST TO DO PCT NORMALIZATION                 
         BZ    VN40                                                             
*                                                                               
         L     R2,STNIPVAR         INPUT LIST                                   
         L     R3,STNOPVAR         OUTPUT LIST                                  
         L     R4,STNVCNT          COUNT                                        
         L     RF,STNMEAN          MEAN IS DIVISOR                              
         S     RF,LOWNEG           BUT ADJUST TO MAKE ALL VALUES +              
         BNP   VN40                MUST HAVE POSITIVE 'MEAN'                    
*                                                                               
VN22     DS    0H                                                               
         CLC   0(4,R2),IGNVAL      TEST TO IGNORE                               
         BE    VN24                                                             
         L     R1,0(R2)                                                         
         M     R0,PRECFCT                                                       
         S     R1,STNMEAN                                                       
         M     R0,=F'100'          FOR N.NN                                     
         BAS   RE,DIV                                                           
         ST    R1,0(R3)                                                         
*                                                                               
VN24     DS    0H                                                               
         A     R2,STNIPSPC                                                      
         A     R3,STNOPSPC                                                      
         BCT   R4,VN22                                                          
*                                                                               
VN40     DS    0H                                                               
EXIT     DS    0H                                                               
         XIT1                                                                   
         SPACE 2                                                                
DIV      DS    0H                                                               
         SLDA  R0,1                                                             
         DR    R0,RF                                                            
         LTR   R1,R1                                                            
         BNP   *+8                                                              
         A     R1,=F'1'                                                         
         SRA   R1,1                                                             
         BR    RE                                                               
         SPACE 2                                                                
IGNVAL   DC    X'80000000'         CONVENTIONAL VALUE TO IGNORE ENTRY           
         SPACE 3                                                                
         LTORG                                                                  
         SPACE 3                                                                
WKD      DSECT                                                                  
PRECFCT  DS    F                                                                
LOWNEG   DS    0H                                                               
DUB      DS    D                                                                
DMCB     DS    6F                                                               
         SPACE 3                                                                
*                                                                               
       ++INCLUDE DDSTNORMD                                                      
*                                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'018DDSTNORM  05/01/02'                                      
         END                                                                    
