*          DATA SET REGENPBBYS AT LEVEL 115 AS OF 05/01/02                      
*CATALP REGENPBB                                                                
         TITLE 'REGENPBYB - REPPAK BUY LINE DISPLAY- BLAIR DSM VERSION'         
*              PARAMETER 1 =       A(BUYREC)                                    
*                        2 =       A(OUTPUT AREA)                               
*                                       BYTE 0 = MAXIMUM LINES                  
*                                       OUTPUT AREA = 81 X MAX LINES            
*                                       BYTE 0 = NUMBER LINES ON RETURN         
*                                       THIS CAN BE 0 (DELETES)                 
*                        3 =       A(ROUTINE ADDRESS BLOCK)                     
*                                       WORD 1 = A(UNDAY)                       
*                                            2 = A(UNTIME)                      
*                                            3 = A(DATCON)                      
*                                            4 = A(ADDAY)                       
*                                            IF 4 IS 0 THEN ALTERNATE           
*                                            WEEKS ARE NOT EXPLODED             
*                        4 =       A(CONREC)                                    
*                                                                               
*                        5 =       A(CONTRACT PROGRAM PROFILES)                 
*                                                                               
***********************************************************************         
* HISTORY:                                                                      
*                                                                               
* 18MAR/92 (SKU) --- FIX MCI BUG                                                
*                                                                               
* 16SEP92 (BU ) --- CHANGE 'SEC' FIELD TO 'TYP', RELOCATE IT...                 
*                                                                               
* 12APR95 (BU ) --- MULTIPLE MISSED MG DATES                                    
*                                                                               
* 31MAY95 (SKU) --- MAKEGOOD CONTINUATION TO 2ND COMMENT LINE SUPPORT           
*                                                                               
* 13JUL95 (SKU) --- FIX CONFIRMATION PRINT BUG                                  
*                                                                               
* 13OCT95 (BU ) --- ADD KATZ DISPLAY CHANGES:  BAD INFO                         
*                                                                               
* 12FEB96 (SKU) --- DON'T CHANGE TOTAL SPOTS FOR CREDIT FOR SPOT TARGET         
*                                                                               
* 04MAR96 (SKU) --- FIX CONFIRMATION PRINT BUG AGAIN                            
*                                                                               
* 07JUL98 (SKU) --- DISPLAY DIRECT CREDIT HISTORY                               
*                                                                               
* 21DEC98 (RHV) --- HANDLE UNDELETED CANCELLED LINES                            
*                                                                               
* 18APR00 (SKU) --- PRINT MAKEGOOD REFERENCES BASED ON X'05' ELEMENTS           
*                   INSTEAD OF X'04' MG= COMMENT ELEMENTS                       
*                                                                               
* 02JUN00 (BU ) --- TRADE BUY PROCESSING                                        
*                                                                               
* 28SEP00 (BU ) --- FIXED POSITION FOR 'T' = TRADE INDICATOR                    
*                                                                               
*                                                                               
*                   ***  END TOMBSTONE  ***                                     
***********************************************************************         
REGENPBB CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 WORKX-WORKD,REGENPBB                                             
         USING WORKD,RC                                                         
         L     RA,0(R1)            DSECT FOR BUYREC                             
         ST    R1,SAVER1                                                        
         USING BUYREC,RA                                                        
         L     R9,4(R1)            OUTPUT AREA                                  
         USING PD,R9               DISPLAY LINE DSECT                           
         USING CONREC,R8                                                        
         L     R8,12(R1)                                                        
*                                                                               
         ICM   RE,15,16(R1)                                                     
         BNZ   *+6                                                              
         DC    H'0'                                                             
         MVC   PROFILES,0(RE)      SAVE CONTRACT PROFILES                       
*                                                                               
         L     R2,8(R1)            ROUTINES                                     
         MVC   VOUTDAY(16),0(R2)                                                
         MVI   MAX,0                                                            
         MVC   MAX+1(1),4(R1)      MAX LINES                                    
         MVI   4(R1),0             NO LINES SO FAR                              
         MVC   DATADISP,=H'34'                                                  
         TM    RBUYCNTL,X'C0'      VOID POINTER?                                
         BO    EXIT                                                             
         CLI   RBUYCHGI,C'C'       CANCELLED?                                   
         BE    PBBY0005                                                         
         TM    RBUYCNTL,X'80'      DELETED?                                     
         BZ    PBBY0010                                                         
         CLI   RBUYCHGI,C'X'       DELETED (NOT CANCELLED?)                     
         BE    EXIT                                                             
* MUST HAVE BEEN CANCELLED                                                      
PBBY0005 DS    0H                                                               
         TM    PROFILES+CNTDCANB,CNTDCANA  DISPLAY CANCELLED LINES?             
         BO    PBBY0010            YES                                          
         TM    RCONMODR+1,X'C0'    ACE/GRAPHNET                                 
         BNZ   PBBY0010            DO CANCELLED ACE/GRAPHNET BUYS LATER         
         CLC   RBUYCREA,RBUYCHGD   CREATED SAME DAY AS CANCEL?                  
         BE    EXIT                                                             
         CLC   RBUYKMOD,RCONMOD    BUY K LEVEL V K MOD NUM                      
         BL    EXIT                                                             
*                                                                               
* GET TODAY'S DATE                                                              
PBBY0010 GOTO1 VDATCON,DMCB,(5,0),(3,TODAY)                                     
*                                                                               
         LR    RE,R9                                                            
         LH    RF,MAX              MAXIMUM LINES                                
         MH    RF,=Y(L'PRTLN)      LINE COUNT                                   
*                                                                               
*         XCEF                      CLEAR OUTPUT AREA                           
         XCEF                                                                   
         CLI   RBUYKLIN,255        PLAN REC?                                    
         BNE   PBBY0020                                                         
* DISPLAY PLAN TOTAL REC LINE                                                   
         L     R1,SAVER1                                                        
         MVI   4(R1),1             1 LINE                                       
         MVC   PRAT-28(27),=C'WEEKLY RATE FOR PLAN XXX IS'                      
         MVC   PRAT-7(3),RBUYKPLN                                               
         MVC   DUB(4),RBUYCOS                                                   
         L     R4,DUB                                                           
         EDIT  (R4),(10,PRAT),2,FLOAT=-,COMMAS=YES,ALIGN=LEFT                   
         EDIT  (2,RBUYTSPT),(4,PTOT-1)                                          
         B     EXIT                                                             
*                                                                               
*                                                                               
*              BUILD PRINT LINE                                                 
* CHECK FOR MODIFICATION CODE PRINT                                             
PBBY0020 TM    RCONMODR+1,X'C0'    X'C0'=ACE/GRAPHNET CONTRACT                  
         BZ    PBBY0100                                                         
         SPACE 1                                                                
         SR    R4,R4                                                            
         LA    R1,RCONELEM                                                      
PBBY0030 ZIC   R4,1(R1)            FIND X'1F' ELEMENT                           
         AR    R1,R4                                                            
         CLI   0(R1),0                                                          
         BNE   *+6                                                              
         DC    H'0'                ELEMENT SHOULD BE THERE                      
         CLI   0(R1),X'1F'                                                      
         BNE   PBBY0030                                                         
         SPACE 1                                                                
         MVC   SVCONF,6(R1)        SAVE CONFIRMED STATUS                        
         TM    6(R1),X'80'         X'80'=NOT CONFIRMED                          
         BZ    PBBY0060                                                         
         SPACE 1                                                                
         ZIC   R4,1(R1)                                                         
         AR    R1,R4               GET X'20' (SEND) ELEMENT                     
         CLI   0(R1),X'20'                                                      
         BE    *+6                                                              
         DC    H'0'                                                             
         SPACE 1                                                                
         CLI   5(R1),1             DON'T PRINT CHANGES WHEN                     
         BNE   PBBY0040            REP VERSION IS 1 AND                         
         CLI   14(R1),0            STATION VERSION IS 0                         
         BNE   PBBY0040                                                         
         TM    PROFILES+CNTDCANB,CNTDCANA  DISPLAY CANCELLED LINES?             
         BZ    PBBY0120                                                         
         CLI   RBUYCHGI,C'C'       CANCELLED?                                   
         BNE   PBBY0120                                                         
         SPACE 1                                                                
PBBY0040 CLC   5(1,R1),14(R1)      COMPARE REP TO STATION VERSION               
         BH    PBBY0050  THEN COMPARE BUY VERSION TO HIGHER OF THE TWO          
*                        & DON'T PRINT CHANGES FOR OLD VERSIONS                 
         CLC   RBUYVER,14(R1)      STATION VERSION                              
         BE    PBBY0110                                                         
         CLI   RBUYCHGI,C'C'       CANCELLED?                                   
         BNE   PBBY0120            OTHERWISE, DON'T PRINT MOD CODE              
         TM    PROFILES+CNTDCANB,CNTDCANA  DISPLAY CANCELLED LINES?             
         BO    PBBY0110                                                         
         B     EXIT                DON'T PRINT LINE AT ALL                      
         SPACE 1                                                                
PBBY0050 CLC   RBUYVER,5(R1)       REP VERSION                                  
         BE    PBBY0110                                                         
         CLI   RBUYCHGI,C'C'       CANCELLED?                                   
         BNE   PBBY0120            OTHERWISE, DON'T PRINT MOD CODE              
         TM    PROFILES+CNTDCANB,CNTDCANA  DISPLAY CANCELLED LINES?             
         BO    PBBY0110                                                         
         B     EXIT                DON'T PRINT LINE AT ALL                      
         SPACE 1                                                                
* CONFIRMED -  ONLY PRINT CHANGES FROM LAST MOD NUMBER, AND DON'T               
*              PRINT CHANGE CODE O (ORDER COMMENT)                              
         SPACE 1                                                                
PBBY0060 DS    0H                                                               
*                                                                               
         CLI   RCONMOD,0           DON'T PRINT CHANGES FOR MOD 0                
         BE    PBBY0070                                                         
*                                                                               
         TM    PROFILES+CNTDCANB,CNTDCANA  DISPLAY CANCELLED LINES?             
         BO    PBBY0065                                                         
         CLI   RBUYCHGI,C'C'       CANCELLED?                                   
         BNE   PBBY0065            CHECK IF CANCELLED AND ADDED IN THE          
         LA    R6,RBUYREC          SAME MOD NUM                                 
         MVI   ELCODE,X'10'        EXTRA DESCRIPTION ELEMENT PRESENT?           
         BAS   RE,GETEL                                                         
         BNE   PBBY0065                                                         
         USING RBUYXXEL,R6         COMPARE                                      
         CLC   RBUYXXMD,RBUYKMOD   MOD NUMBER WHEN BUY WAS ADDED AND            
         BE    EXIT                MOD NUMBER WHEN IT WAS CANCELLED             
         DROP  R6                  DON'T PRINT LINE IF SAME                     
*                                                                               
PBBY0065 DS    0H                                                               
         ZIC   R4,RCONMOD                                                       
         BCTR  R4,0                GET LAST MOD NUMBER                          
         ZIC   R1,RBUYKMOD                                                      
         CR    R1,R4                                                            
         BE    PBBY0080                                                         
PBBY0070 DS    0H                                                               
         CLI   RBUYCHGI,C'C'       CANCELLED?                                   
         BNE   PBBY0120                                                         
         TM    PROFILES+CNTDCANB,CNTDCANA  DISPLAY CANCELLED LINES?             
         BZ    EXIT                DON'T PRINT LINE AT ALL                      
         SPACE 1                                                                
PBBY0080 DS    0H                                                               
         LA    R4,PCHG                                                          
         CLI   RBUYCHGI,C'O'       ORDER COMMENT                                
         BE    PBBY0090                                                         
         MVC   0(1,R4),RBUYCHGI                                                 
         LA    R4,1(R4)                                                         
         MVI   0(R4),C' '          MOVE BLANK TO 2ND CHARACTER                  
         SPACE 1                                                                
PBBY0090 CLI   RBUYCHGI+1,C'O'     ORDER COMMENT                                
         BE    PBBY0120                                                         
         CLI   RBUYCHGI+1,C' '     BLANK                                        
         BE    PBBY0120                                                         
         MVC   0(1,R4),RBUYCHGI+1                                               
         OC    PCHG,=C'  '         SPACES                                       
         B     PBBY0120                                                         
         SPACE 1                                                                
* FOR NON ACE/GRAPHNET CONTRACTS                                                
         SPACE 1                                                                
PBBY0100 DS    0H                                                               
         CLI   RBUYCHGI,C'C'       CANCELLED?                                   
         BNE   PBBY0105                                                         
         TM    PROFILES+CNTDCANB,CNTDCANA  DISPLAY CANCELLED LINES?             
         BO    PBBY0110                                                         
PBBY0105 DS    0H                                                               
         CLI   RCONMOD,0           K MOD NUM                                    
         BE    PBBY0120                                                         
         CLI   RCONMOD,X'FF'                                                    
         BE    PBBY0120                                                         
         CLI   RBUYKMOD,X'FF'                                                   
         BE    PBBY0120                                                         
         CLI   RBUYKMOD,0          BUY K MOD NUM                                
         BE    PBBY0120                                                         
         CLC   RBUYKMOD,RCONMOD                                                 
         BL    PBBY0120                                                         
*                                                                               
PBBY0110 MVC   PCHG,RBUYCHGI                                                    
*                                                                               
PBBY0120 EDIT  (1,RBUYKLIN),(3,PLIN)    LINE NUMBER                             
*                                                                               
         MVC   DMCB(2),RBUYDUR     LENGTH                                       
         NI    DMCB,X'7F'          MINUTE IND                                   
         EDIT  (2,DMCB),(3,PLEN)                                                
         TM    RBUYDUR,X'80'       MINUTES?                                     
         BZ    PBBY0130                                                         
         MVC   PLEN-1(3),PLEN                                                   
         MVI   PLEN+2,C'M'                                                      
*                                                                               
PBBY0130 MVC   PCLS,RBUYCLS        CLASS                                        
         MVC   PSEC,RBUYSEC        SECTION                                      
         MVC   PTYP,RBUYTYP        TYPE                                         
*                                                                               
* DISPLAY FLIGHT NUMBER IF NO SECTION EXISTS FOR BUY                            
*                                                                               
PBBY0140 EQU   *                                                                
*                                                                               
         CLI   RBUYFLT,0           DOES BUY HAVE A FLIGHT                       
         BE    PBBY0150            NO                                           
         OC    VADDAY,VADDAY       ONLY DO IT IF ON-LINE                        
         BNZ   PBBY0150                                                         
         CLI   PSEC,0              TEST FOR ABSENCE OF SECTION                  
         BE    *+12                                                             
         CLI   PSEC,C' '                                                        
         BNE   PBBY0150                                                         
         MVI   PSEC,C'F'           FLIGHT CODE PREFIX                           
         EDIT  (B1,RBUYFLT),(2,PSEC+1),ALIGN=LEFT                               
*                                                                               
PBBY0150 MVC   PPLN,RBUYKPLN       PLAN                                         
         CLC   RBUYKPLN,=3X'FF'                                                 
         BNE   *+10                                                             
         MVC   PPLN,=3C' '                                                      
*                                                                               
*                                                                               
         CLC   RBUYKPLN,=3X'FF'    PLAN                                         
         BE    *+14                                                             
         MVC   PRAT+3(4),=C'PLAN'                                               
         B     PBBY0160                                                         
         EDIT  (4,RBUYCOS),(10,PRAT),2,FLOAT=-,COMMAS=YES                       
         TM    RBUYFLG2,X'02'      TRADE BUY?                                   
         BNO   PBBY0160            NO                                           
         CLC   PRAT+8(2),=C'00'    NO PENNIES?                                  
*                                                                               
*   FORCE TRADE INDICATOR TO BEFORE RATE FIELD                                  
*                                                                               
**       BE    PBBY0158            NO PENNIES                                   
         MVI   PRAT-1,C'T'         SET TRADE INDICATOR AFTER NPW                
         B     PBBY0160                                                         
PBBY0158 EQU   *                                                                
         MVC   PRAT+7(3),=C'.TR'   NO PENNIES                                   
*                                                                               
PBBY0160 MVC   DUB(2),RBUYLEN      REC LENGTH                                   
         LH    R5,DUB                                                           
         LA    R5,RBUYREC(R5)      REC END FOR BXLE                             
*              PREPARE TO GET DAY-TIME ELEMENTS                                 
         BCTR  R5,R0                                                            
         BCTR  R5,R0                                                            
         SR    R4,R4                                                            
         LA    R3,RBUYELEM         FIRST ELEM                                   
* GET DAY SPAN                                                                  
         SR    RE,RE                                                            
         SR    RF,RF                                                            
         ZIC   RE,RBUYSTED                                                      
         SRDL  RE,4                                                             
         SRL   RF,28                                                            
         CR    RE,RF                                                            
         BNH   *+8                                                              
         LA    RF,7(RF)                                                         
         SR    RF,RE                                                            
         ST    RF,DAY1                                                          
*                                                                               
         SH    R9,=Y(L'PRTLN)                                                   
*                                                                               
         SR    R6,R6               LINE CTR FOR DAY-TIME LINES                  
         B     PBBY0180            NO DATE ELEM                                 
*              DISPLAY DAY-TIME                                                 
PBBY0170 EQU   *                                                                
         CLC   =X'FFFF',2(R3)      CONVERT ERROR DATA?                          
         BNE   PBBY0175            NO                                           
         BAS   RE,KZDAYERR         YES - RETRIEVE FROM ERROR ELEMENT            
         B     PBBY0177                                                         
PBBY0175 EQU   *                                                                
         GOTO1 VOUTDAY,DMCB,3(R3),2(R3),PDAY                                    
PBBY0177 EQU   *                                                                
         CLC   =X'FFFF',4(R3)      CONVERT ERROR DATA?                          
         BNE   PBBY0178            NO                                           
         BAS   RE,KZTIMERR         YES - RETRIEVE FROM ERROR ELEMENT            
         B     PBBY0179                                                         
PBBY0178 EQU   *                                                                
*                                                                               
         GOTO1 VUNTIME,DMCB,4(R3),PTIM                                          
PBBY0179 EQU   *                                                                
         B     PBBY0180                                                         
         EJECT                                                                  
*                                                                               
*   RETRIEVE THE X'22' ELEMENT, AND INSERT ITS CONTENTS TO THE                  
*        DAY FIELD ON THE SCREEN.  IF THIS ROUTINE IS REFERENCED,               
*        THE BUYLINE WILL ONLY CONTAIN A SINGLE X'02' ELEMENT,                  
*        BECAUSE IT WAS CONVERTED FROM KATZ THAT WAY.                           
*        R6 --> X'02' ELEMENT IN THE RECORD.                                    
*                                                                               
KZDAYERR NTR1                                                                   
         LA    R6,RBUYELEM                                                      
KZDA0020 EQU   *                                                                
         ZIC   RF,1(R6)                                                         
         AR    R6,RF               BUMP TO NEXT ELEMENT                         
         CLI   0(R6),0             END OF RECORD?                               
         BNE   *+6                 NO                                           
         DC    H'0'                ERROR RECORD MUST HAVE X'22'                 
         CLI   0(R6),X'22'         ERROR ELEMENT?                               
         BNE   KZDA0020            NO  - GO BACK FOR NEXT                       
         MVC   PDAY(10),2(R6)      YES - MOVE DATA TO FIELD                     
         XIT1                                                                   
         EJECT                                                                  
*                                                                               
*   RETRIEVE THE X'32' ELEMENT, AND INSERT ITS CONTENTS TO THE                  
*        TIME FIELD ON THE SCREEN.  IF THIS ROUTINE IS REFERENCED,              
*        THE BUYLINE WILL ONLY CONTAIN A SINGLE X'02' ELEMENT,                  
*        BECAUSE IT WAS CONVERTED FROM KATZ THAT WAY.                           
*        R6 --> X'02' ELEMENT IN THE RECORD.                                    
*                                                                               
KZTIMERR NTR1                                                                   
         LA    R6,RBUYELEM                                                      
KZTM0020 EQU   *                                                                
         ZIC   RF,1(R6)                                                         
         AR    R6,RF               BUMP TO NEXT ELEMENT                         
         CLI   0(R6),0             END OF RECORD?                               
         BNE   *+6                 NO                                           
         DC    H'0'                ERROR RECORD MUST HAVE X'32'                 
         CLI   0(R6),X'32'         ERROR ELEMENT?                               
         BNE   KZTM0020            NO  - GO BACK FOR NEXT                       
         MVC   PTIM(11),2(R6)      YES - MOVE DATA TO FIELD                     
         XIT1                                                                   
         EJECT                                                                  
*              LENGTH                                                           
*                                                                               
PBBY0180 ZIC   R4,1(R3)            ELEM LEN                                     
         BXLE  R3,R4,*+8           NEXT ELEM                                    
         B     PBBY4640                                                         
*                                                                               
         CLI   0(R3),2             DAY TIME ELEM?                               
         BNE   PBBY0190                                                         
         LA    R9,L'PRTLN(R9)      OUTPUT                                       
         LA    R6,1(R6)            DAY-TIME CTR                                 
         CH    R6,MAX              MAXIMUM LINES?                               
         BH    PBBY0180                                                         
         B     PBBY0170            DISPLAY NEXT DAY-TIME                        
*                                                                               
*              GET DATE ELEMENTS                                                
PBBY0190 SR    R7,R7               DATE LINE CTR                                
         L     R1,SAVER1                                                        
         L     R9,4(R1)                                                         
         SH    R9,=Y(L'PRTLN)                                                   
         CLI   0(R3),3             DATE ELEM                                    
         BNE   PBBY0350            IF NONE JUST CARRY ON                        
         B     PBBY0340                                                         
* PROCESS DATE ELEMENT                                                          
PBBY0200 SR    RE,RE                                                            
         SR    RF,RF                                                            
         ZIC   RF,9(R3)            NO. PER WEEK                                 
* DISPLAY NPW                                                                   
         EDIT  (RF),(3,PNPW)                                                    
         SR    R2,R2                                                            
         ZIC   R2,10(R3)           NO. OF WEEKS                                 
* DISPLAY NO. OF WEEKS IF NO CLASS                                              
         CLI   PCLS,0                                                           
         BE    *+12                                                             
         CLI   PCLS,C' '                                                        
         BNE   PBBY0210                                                         
         EDIT  (R2),(2,PCLS)                                                    
         MVI   PCLS+2,C'W'                                                      
* DISPLAY TOTAL SPOTS MINUS MISSED SPOTS                                        
***>0210 TM    RBUYCNTL,X'80'                                                   
PBBY0210 CLI   RBUYCHGI,C'C'       CANCELLED?                                   
         BE    PBBY0280                                                         
         SPACE 1                                                                
         LA    R1,RBUYELEM                                                      
         ZIC   R4,1(R1)                                                         
         AR    R1,R4                                                            
         CLI   0(R1),0                                                          
         BE    PBBY0220                                                         
         CLI   0(R1),4                                                          
         BNE   *-18                                                             
         CLC   2(3,R1),=C'CR='                                                  
         BE    PBBY0230            IGNORE THE MINUS BUY CHECK                   
         SPACE 1                                                                
PBBY0220 TM    RBUYCOS,X'80'                                                    
         BO    PBBY0280                                                         
PBBY0230 MR    RE,R2               TOTAL SPOTS IN RF                            
* GET MISSED SPOTS, IF ANY                                                      
         LA    R1,RBUYELEM                                                      
PBBY0240 CLI   0(R1),0             LAST?                                        
         BE    PBBY0270                                                         
         CLI   0(R1),6             MISSED ELEM?                                 
         BE    PBBY0260                                                         
*        CLI   0(R1),7             MISSED CREDIT ISSUED                         
*        BE    PBBY0260                                                         
PBBY0250 ZIC   R4,1(R1)                                                         
         AR    R1,R4               NEXT ELEM                                    
         B     PBBY0240                                                         
* CHECK MISSED DATE AGAINST DATE ELEM                                           
PBBY0260 CLC   2(3,R1),2(R3)        START                                       
         BL    PBBY0250                                                         
         CLC   2(3,R1),5(R3)       END                                          
         BH    PBBY0250                                                         
* MISSED ELEM APPLIES, SUBTRACT MISSED SPOTS                                    
         ZIC   R4,6(R1)            MISSED SPOTS                                 
         SR    RF,R4                                                            
         B     PBBY0250                                                         
* DISPLAY TOTAL SPOTS FOR DATE ELEM                                             
PBBY0270 EDIT  (RF),(4,PTOT-1)                                                  
* TEST FOR ALT WEEKS                                                            
PBBY0280 TM    8(R3),X'40'         ALT?                                         
         BZ    PBBY0310                                                         
         OC    VADDAY,VADDAY                                                    
         BZ    PBBY0310                                                         
* MUST BE OFF-LINE CONTRACT - EXPLODE ALTERNATE WEEKS                           
*                                                                               
* GET START-END IN WORK                                                         
         GOTO1 VDATCON,DMCB,(3,2(R3)),WORK                                      
*                                                                               
         GOTO1 (RF),(R1),(3,5(R3)),WORK+6                                       
PBBY0290 GOTO1 VDATCON,(R1),WORK,(4,PDAT)                                       
         MVI   PDAT+11,C'A'                                                     
         L     R2,DAY1                                                          
         LTR   R2,R2               ROTATOR?                                     
         BZ    PBBY0300                                                         
* GET END DATE OF WEEK                                                          
         GOTO1 VADDAY,(R1),WORK,WORK+12,(R2)                                    
*                                                                               
         GOTO1 VDATCON,(R1),WORK+12,(4,PDAT+6)                                  
         MVI   PDAT+5,C'-'                                                      
* GET NEXT ALTERNATE WEEK                                                       
PBBY0300 MVC   DUB(6),WORK                                                      
         GOTO1 VADDAY,(R1),DUB,WORK,14                                          
         CLC   WORK(6),WORK+6      PAST ELEM END DATE?                          
         BH    PBBY0330                                                         
*                                                                               
* DISPLAY NEXT ALTERNATE WEEK                                                   
         LA    R9,L'PRTLN(R9)      OUTPUT                                       
         LA    R7,1(R7)            LINE CTR                                     
         CH    R7,MAX                                                           
         BNH   PBBY0290                                                         
         LR    R6,R7                                                            
         B     PBBY4640                                                         
PBBY0310 GOTO1 VDATCON,DMCB,(3,2(R3)),(4,PDAT)                                  
         CLC   2(3,R3),5(R3)       SAME START-END?                              
         BE    PBBY0320                                                         
*                                                                               
         MVI   PDAT+5,C'-'                                                      
*              CONVERT ELEMENT END DATE TO MMMDD                                
         GOTO1 VDATCON,DMCB,(3,5(R3)),(4,PDAT+6)                                
*                                                                               
PBBY0320 TM    8(R3),X'40'         ALTERNATE WEEKS?                             
         BZ    PBBY0330                                                         
         MVI   PDAT+11,C'A'                                                     
         LA    R9,L'PRTLN(R9)                                                   
         LA    R7,1(R7)                                                         
         CH    R7,MAX                                                           
         BNH   *+10                                                             
         LR    R6,R7                                                            
         B     PBBY4640                                                         
         MVC   PDAT(15),=C'ALTERNATE WEEKS'                                     
*              GET NEXT ELEM                                                    
PBBY0330 ZIC   R4,1(R3)            ELEM LEN                                     
         BXLE  R3,R4,*+8           NEXT ELEM                                    
         B     PBBY0350                                                         
*                                                                               
         CLI   0(R3),3             ANOTHER DATE ELEM?                           
         BNE   PBBY0350                                                         
         TM    8(R3),X'40'         ALT WEEKS                                    
         BZ    PBBY0340                                                         
         OC    VADDAY,VADDAY                                                    
         BZ    PBBY0340                                                         
         MVI   PDAT+79,C'*'        IND ANOTHER ALT ELEM                         
PBBY0340 LA    R7,1(R7)            DATE CTR                                     
         LA    R9,L'PRTLN(R9)                                                   
         CH    R7,MAX                                                           
         BNH   PBBY0200            DISPLAY NEXT DATE ELEM                       
         LR    R6,R7                                                            
         B     PBBY4640                                                         
*              PREPARE TO DISPLAY COMMENTS                                      
PBBY0350 DS    0H                                                               
         BAS   RE,DISPMKGS                                                      
*                                                                               
         CR    R6,R7               FIND HIGHEST LINE SO FAR                     
         BNL   *+6                                                              
         LR    R6,R7                                                            
         CH    R6,MAX                                                           
         BE    PBBY4640                                                         
         CLI   0(R3),4             COMMENT ELEM?                                
         BE    PBBY0360                                                         
         CLI   0(R3),0             END OF RECORD?                               
         BE    PBBY4640                                                         
         B     PBBY4480            CHECK FOR ORDER COMMENTS                     
         SPACE 1                                                                
PBBY0360 LR    R7,R6                                                            
         MH    R7,=Y(L'PRTLN)                                                   
         L     R1,SAVER1                                                        
         L     R9,4(R1)            OUTPUT                                       
         LA    R9,0(R7,R9)         POSITION TO NEXT AVAILABLE LINE              
*                                                                               
*              DISPLAY COMMENT                                                  
PBBY0370 CLC   2(3,R3),=C'CR='     CREDIT FOR MISSED                            
         BNE   PBBY0390                                                         
         STM   R3,R5,DMCB+12       SAVE BXLE                                    
PBBY0380 ZIC   R4,1(R3)                                                         
         BXLE  R3,R4,*+8           NEXT ELEM                                    
         B     PBBY4400                                                         
         CLI   0(R3),0                                                          
         BE    PBBY4400                                                         
         CLI   0(R3),5             MAKE-GOOD ELEMENT?                           
         BNE   PBBY0380                                                         
         SPACE 1                                                                
         MVC   PDAY+6(29),=C'CREDIT FOR SPOT          LINE'                     
         CLI   9(R3),2             NO. OF MISSED                                
         BL    *+10                                                             
         MVC   PDAY+6(29),=C'CREDIT FOR SPOTS         LINE'                     
         SPACE 1                                                                
         EDIT  (1,2(R3)),(2,PDAY+36),ALIGN=LEFT                                 
         B     PBBY4440                                                         
         SPACE 1                                                                
PBBY0390 DS    0H                                                               
         CLC   2(3,R3),=C'MG='     MAKE-GOOD?                                   
         BE    PBBY4445                                                         
*                                                                               
PBBY4400 DS    0H                                                               
         CLC   =C'CREDIT:',2(R3)   SKIP PRINTING CREDIT: COMMENT                
         BE    PBBY4445                                                         
*                                                                               
PBBY4410 DS    0H                                                               
         ZIC   R4,1(R3)            ELEM LEN                                     
         AHI   R4,-3                                                            
         EX    R4,MOVECOM          MOVE COMMENT TO DISPLAY LINE                 
*                                                                               
PBBY4440 LA    R6,1(R6)                                                         
         LA    R9,L'PRTLN(R9)                                                   
PBBY4445 ZIC   R4,1(R3)            ELEM LEN                                     
         BXLE  R3,R4,*+8           NEXT ELEM                                    
         B     PBBY4640                                                         
*                                                                               
         CLI   0(R3),4             COMMENT?                                     
         BNE   PBBY4490            CHECK FOR ORDER COMMENT                      
         CH    R6,MAX              MAXIMUM LINES?                               
         BNH   PBBY4410            DISPLAY NEXT COMMENT                         
         B     PBBY4640            OR RETURN                                    
*                                                                               
PBBY4450 DS    0H                  CONTINUING MAKEGOOD IN 2ND CMMT LINE         
         LA    R6,1(R6)                                                         
         LA    R9,L'PRTLN(R9)                                                   
         ZIC   R4,1(R3)            ELEM LEN                                     
         BXLE  R3,R4,*+8           NEXT ELEM                                    
         B     PBBY4640                                                         
*                                                                               
         CLI   0(R3),4             COMMENT?                                     
         BNE   PBBY4490            CHECK FOR ORDER COMMENT                      
         CH    R6,MAX              MAXIMUM LINES?                               
         BH    PBBY4640            DISPLAY NEXT COMMENT OR RETURN               
*                                                                               
         LR    RE,R3                                                            
         SR    RF,RF                                                            
         ZIC   RF,1(R3)            ELEM LEN                                     
         SR    R1,R1                                                            
PBBY4460 CLI   0(RE),C' '                                                       
         BE    PBBY4470                                                         
         LA    RE,1(RE)                                                         
         LA    R1,1(R1)                                                         
         CR    R1,RF                                                            
         BL    PBBY4460                                                         
         B     PBBY4475                                                         
* MOVE REST OF COMMENT                                                          
PBBY4470 SR    RF,R1                                                            
         AHI   RF,-2                                                            
         BM    PBBY4475                                                         
         CH    R6,MAX                                                           
         BH    PBBY4640                                                         
*                                                                               
         EX    RF,*+8                                                           
         B     PBBY4475                                                         
         MVC   PDAY+6(0),1(RE)                                                  
*                                                                               
         ZIC   R4,1(R3)            ELEM LEN                                     
         AHI   R4,-3                                                            
         EX    R4,MOVECOM          MOVE COMMENT TO DISPLAY LINE                 
*                                                                               
PBBY4475 LA    R6,1(R6)                                                         
         LA    R9,L'PRTLN(R9)                                                   
         ZIC   R4,1(R3)            ELEM LEN                                     
         BXLE  R3,R4,*+8           NEXT ELEM                                    
         B     PBBY4640                                                         
*                                                                               
PBBY4480 LR    R7,R6               IF NO REGULAR BUY CMTS,                      
         MH    R7,=Y(L'PRTLN)      POSITION TO NEXT AVAILABLE LINE              
         L     R1,SAVER1                                                        
         L     R9,4(R1)            OUTPUT                                       
         LA    R9,0(R7,R9)                                                      
*                                                                               
* DISPLAY DIRECT CREDIT HISTORY                                                 
*                                                                               
PBBY4490 DS    0H                                                               
         SR    R7,R7               MAX 4 AUDIT TRAIL ELEMENTS PER LINE          
*                                                                               
         LA    R4,PDAY+6                                                        
         USING RBUYCAEL,R3                                                      
*                                                                               
PBBY4493 DS    0H                                                               
         CLI   0(R3),X'16'                                                      
         BE    PBBY4500                                                         
         BH    PBBY4520                                                         
*                                                                               
PBBY4495 DS    0H                                                               
         ZIC   RF,1(R3)                                                         
         LTR   RF,RF                                                            
         BZ    PBBY4520                                                         
         AR    R3,RF                                                            
         B     PBBY4493                                                         
*                                                                               
PBBY4500 DS    0H                                                               
         CH    R6,MAX                                                           
         BNL   PBBY4640                                                         
*                                                                               
         LTR   R7,R7                                                            
         BZ    PBBY4505                                                         
         MVI   0(R4),C','                                                       
         AHI   R4,1                                                             
         B     PBBY4508                                                         
*                                                                               
PBBY4505 DS    0H                                                               
         MVC   0(7,R4),=C'CREDIT:'                                              
         AHI   R4,7                                                             
*                                                                               
PBBY4508 DS    0H                                                               
         GOTO1 VDATCON,DMCB,(3,RBUYCASD),(4,(R4))                               
         AHI   R4,5                START DATE                                   
         OC    RBUYCAED,RBUYCAED                                                
         BZ    PBBY4510                                                         
         CLC   RBUYCASD,RBUYCAED                                                
         BE    PBBY4510                                                         
         MVI   0(R4),C'-'          END DATE IF ANY                              
         AHI   R4,1                                                             
         GOTO1 VDATCON,DMCB,(3,RBUYCAED),(4,(R4))                               
         AHI   R4,5                                                             
         B     PBBY4511                                                         
*                                                                               
PBBY4510 DS    0H                                                               
         CLI   RBUYCANW,1                                                       
         BNH   PBBY4513                                                         
         MVI   0(R4),C'-'                                                       
         AHI   R4,1                                                             
         EDIT  RBUYCANW,(3,0(R4)),ALIGN=LEFT                                    
         AR    R4,R0                                                            
         MVI   0(R4),C'W'                                                       
         AHI   R4,1                                                             
*                                                                               
PBBY4511 DS    0H                                                               
         TM    RBUYCAFL,X'80'      ALTERNATING WEEKS??                          
         BZ    PBBY4513                                                         
         MVI   0(R4),C'A'                                                       
         AHI   R4,1                                                             
*                                                                               
PBBY4513 DS    0H                  NUMBER OF SPOTS CREDITED                     
         MVI   0(R4),C'('                                                       
         AHI   R4,1                                                             
         EDIT  RBUYCASP,(3,(R4)),ALIGN=LEFT                                     
         AR    R4,R0                                                            
         MVI   0(R4),C')'                                                       
         AHI   R4,1                                                             
         AHI   R7,1                                                             
         CHI   R7,4                                                             
         BL    PBBY4495                                                         
         SR    R7,R7                                                            
         LA    R9,L'PRTLN(R9)      OUTPUT                                       
         LA    R4,PDAY+6                                                        
*                                                                               
PBBY4515 DS    0H                                                               
         AHI   R6,1                DAY-TIME CTR                                 
         CH    R6,MAX                                                           
         BNL   PBBY4640                                                         
         B     PBBY4495                                                         
         DROP  R3                                                               
*                                                                               
PBBY4520 DS    0H                                                               
         LTR   R7,R7                                                            
         BZ    PBBY4550                                                         
         LA    R9,L'PRTLN(R9)      OUTPUT                                       
         LA    R4,PDAY+6                                                        
         AHI   R6,1                DAY-TIME CTR                                 
         CH    R6,MAX                                                           
         BNL   PBBY4640                                                         
*                                                                               
* DISPLAY THE MAKEGOOD LINES WHICH THIS BUY HAS BEEN MADE GOOD BY               
*                                                                               
PBBY4550 DS    0H                                                               
         CLI   0(R3),X'56'                                                      
         BE    PBBY4553                                                         
         BH    PBBY4580                                                         
         ZIC   RF,1(R3)                                                         
         LTR   RF,RF                                                            
         BZ    PBBY4640                                                         
         AR    R3,RF                                                            
         B     PBBY4550                                                         
*                                                                               
PBBY4553 DS    0H                                                               
         BAS   RE,BLDBLIST                                                      
         LA    R7,BUYLIST                                                       
         LA    RE,1                                                             
*                                                                               
PBBY4555 DS    0H                                                               
         MVC   PDAY+6(20),=C'MADE-GOOD BY LINE(S)'                              
         LA    R2,PDAY+31                                                       
*                                                                               
PBBY4560 DS    0H                  IF CELL IS MARKED WITH AN X                  
         CLI   0(R7),C'X'          THERE IS A X'56' ELEMENT FOR THIS            
         BE    PBBY4563            BUY                                          
         AHI   RE,1                                                             
         C     RE,=F'255'                                                       
         BH    PBBY4570                                                         
         AHI   R7,1                                                             
         B     PBBY4560                                                         
*                                                                               
PBBY4563 DS    0H                                                               
         EDIT  (RE),(3,0(R2)),ALIGN=LEFT                                        
*                                                                               
PBBY4565 DS    0H                                                               
         AHI   RE,1                                                             
         C     RE,=F'255'                                                       
         BH    PBBY4570                                                         
         AHI   R7,1                                                             
         AR    R2,R0                                                            
         AHI   R2,1                                                             
*                                                                               
         LA    RF,PD+76            CHECK WE DON'T EXCEED DISPLAY LINE           
         CR    R2,RF                                                            
         BNH   PBBY4560                                                         
         LA    R9,L'PRTLN(R9)      OUTPUT                                       
         AHI   R6,1                                                             
         CH    R6,MAX                                                           
         BNL   PBBY4640                                                         
         B     PBBY4555                                                         
*                                                                               
PBBY4570 DS    0H                                                               
         LA    R9,L'PRTLN(R9)      OUTPUT                                       
         AHI   R6,1                                                             
         CH    R6,MAX                                                           
         BNL   PBBY4640                                                         
*                                                                               
PBBY4580 DS    0H                                                               
         CLI   0(R3),X'84'         ORDER COMMENT?                               
         BE    PBBY4590                                                         
         ZIC   R4,1(R3)                                                         
         AR    R3,R4                                                            
         CLI   0(R3),0             END OF RECORD?                               
         BNE   PBBY4580                                                         
         B     PBBY4640                                                         
         SPACE 1                                                                
PBBY4590 CH    R6,MAX              MAXIMUM LINES?                               
         BNL   PBBY4640                                                         
         SPACE 1                                                                
         TM    RCONMODR+1,X'C0'    IF ACE OR GRAPHNET                           
         BZ    PBBY4600                                                         
         TM    SVCONF,X'40'        AND CONFIRMED                                
         BZ    PBBY4600                                                         
***>     TM    RBUYCNTL,X'80'      AND CANCELLED                                
         CLI   RBUYCHGI,C'C'       CANCELLED?                                   
         BE    PBBY4640            DON'T PRINT ORDER COMMENTS                   
         SPACE 1                                                                
PBBY4600 MVC   PDAY+6(4),=C'SOC-'  STATION ORDER CMT                            
         TM    2(R3),X'80'                                                      
         BZ    *+8                                                              
         MVI   PDAY+6,C'R'         REP ORDER CMT (ROC)                          
         ZIC   R4,1(R3)                                                         
         AHI   R4,-4                                                            
         EX    R4,*+8                                                           
         B     *+10                                                             
         MVC   PDAY+10(0),3(R3)                                                 
         CLC   3(3,R3),=C'#DS'     INDICATES DON'T SEND THIS LINE               
         BNE   *+8                                                              
         MVI   NOSEND,C'D'                                                      
         B     PBBY4440            LOOP BACK FOR OTHER ORDER CMTS               
*                                                                               
*              RETURN                                                           
PBBY4640 L     R1,SAVER1                                                        
         CH    R6,MAX                                                           
         BNH   *+8                                                              
         LH    R6,MAX                                                           
         STC   R6,4(R1)            LINE CTR                                     
         MVI   8(R1),0                                                          
         CLI   NOSEND,C'D'                                                      
         BNE   *+8                                                              
         MVI   8(R1),C'D'          INDICATE THIS LINE IS 'DON'T SEND'           
EXIT     XMOD1 1                                                                
*                                                                               
MOVECOM  MVC   PDAY+6(0),2(R3)                                                  
*                                                                               
         EJECT                                                                  
*&&DO                                                                           
*                                                                               
*   SETPAREN:  INSERTS ( BEFORE NUMBER OF SPOTS)                                
*                                                                               
SETPAREN NTR1                                                                   
         LA    RE,4(R2)                                                         
         LA    RE,1(RE)                                                         
         CLI   0(RE),C' '                                                       
         BE    *-8                                                              
         BCTR  RE,R0                                                            
         MVI   0(RE),C'('                                                       
         XIT1                                                                   
*&&                                                                             
         EJECT                                                                  
**********************************************************************          
* BUILD MADE-GOOD BUY LIST IN SORTED ORDER                                      
* SINCE X'56' IS IN DATE ORDER, WE NEED TO PUT THE MADEGOOD BUY                 
* NUMBERS IN SORTED ORDER FOR DISPLAY                                           
**********************************************************************          
BLDBLIST NTR1                                                                   
*                                                                               
         LA    R6,RBUYREC          FOR EACH BUY WE COME ACROSS,                 
         MVI   ELCODE,X'56'        TURN ON THE APPROPRIATE CELL                 
         BAS   RE,GETEL                                                         
         BNE   BLX                                                              
         USING RBYMGSEL,R6         IN BUYLIST                                   
*                                                                               
         XC    BUYLIST,BUYLIST                                                  
BL10     DS    0H                                                               
         LA    R3,BUYLIST          255 CELLS                                    
*                                                                               
         ZIC   R4,RBYMGSLI                                                      
         AR    R3,R4                                                            
         BCTR  R3,0                                                             
*                                                                               
         MVI   0(R3),C'X'                                                       
*                                                                               
         BAS   RE,NEXTEL                                                        
         BE    BL10                                                             
*                                                                               
BLX      DS    0H                                                               
         XIT1                                                                   
         EJECT                                                                  
         LTORG                                                                  
         EJECT                                                                  
         GETEL R6,DATADISP,ELCODE                                               
         EJECT                                                                  
**********************************************************************          
* DISPLAY MAKEGOOD REFERECES BASED ON EXISTENCE OF X'05' ELEMENTS               
* ALSO DISPLAY IF BUY IS A LATE RUN BONUS                                       
**********************************************************************          
DISPMKGS NTR1  BASE=*,LABEL=*                                                   
         CR    R6,R7               FIND HIGHEST LINE SO FAR                     
         BNL   *+6                                                              
         LR    R6,R7                                                            
         CH    R6,MAX                                                           
         BE    DMKGS20                                                          
         LR    R7,R6                                                            
         MH    R7,=Y(L'PRTLN)                                                   
         L     R1,SAVER1                                                        
         L     R9,4(R1)            OUTPUT                                       
         AR    R9,R7               POSITION TO NEXT AVAILABLE LINE              
         LR    R7,R6                                                            
*                                                                               
         TM    RBUYRTS,X'20'+X'04'                                              
         BNO   DMKGS10                                                          
         MVC   PDAY+6(14),=C'LATE RUN BONUS'                                    
         B     DMKGS80                                                          
*                                                                               
DMKGS10  DS    0H                                                               
         CLI   0(R3),0                                                          
         BE    DMKGS20                                                          
         CLI   0(R3),X'04'                                                      
         BE    DMKGS15                                                          
         CLI   0(R3),X'05'                                                      
         BE    DMKGS30                                                          
         BH    DMKGS20                                                          
*                                                                               
DMKGS13  DS    0H                                                               
         ZIC   R0,1(R3)                                                         
         AR    R3,R0                                                            
         B     DMKGS10                                                          
*                                                                               
DMKGS15  DS    0H                  SKIP FOR CREDIT FOR SPOT                     
         CLC   =C'CR=',2(R3)                                                    
         BNE   DMKGS13                                                          
*                                                                               
DMKGS20  DS    0H                                                               
         XIT1                      NO X'05', RESTORE ALL REGISTERS              
*                                                                               
DMKGS30  DS    0H                                                               
         TM    RBUYRTS,X'08'+X'04'                                              
         BZ    DMKGS40                                                          
         MVC   PDAY+6(20),=C'LATE RUN FOR LINE(S)'                              
         B     DMKGS50                                                          
*                                                                               
DMKGS40  DS    0H                                                               
         MVC   PDAY+6(21),=C'MAKE-GOOD FOR LINE(S)'                             
*                                                                               
DMKGS50  DS    0H                                                               
         USING RBUYMGEL,R3                                                      
         LA    R5,PDAT             SET A(PRINTOUT)                              
         LA    R4,3                                                             
*                                                                               
DMKGS60  DS    0H                                                               
         LR    R2,R5               SET A(PRINTOUT)                              
*                                                                               
         GOTO1 VDATCON,DMCB,(X'83',RBUYMGD1),(4,(R2))                           
         ZIC   R1,DMCB+4                                                        
         AR    R2,R1                                                            
         MVI   0(R2),C'('                                                       
         AHI   R2,1                                                             
         EDIT  RBUYMGSP,(3,0(R2)),ALIGN=LEFT,ZERO=NOBLANK                       
         AR    R2,R0                                                            
         MVC   0(2,R2),=C')-'                                                   
         AHI   R2,2                                                             
         EDIT  RBUYMGLI,(3,0(R2)),ALIGN=LEFT                                    
*                                                                               
         ZIC   R0,1(R3)                                                         
         AR    R3,R0                                                            
         CLI   0(R3),0                                                          
         BE    DMKGS80                                                          
         CLI   0(R3),X'05'                                                      
         BE    DMKGS70                                                          
         BH    DMKGS80                                                          
*                                                                               
DMKGS70  DS    0H                                                               
         AHI   R5,15                                                            
         BCT   R4,DMKGS60                                                       
*                                                                               
         AHI   R6,1                                                             
         AHI   R9,L'PRTLN                                                       
         CH    R6,MAX                                                           
         BNH   DMKGS50                                                          
         B     DMKGSX                                                           
*                                                                               
DMKGS80  DS    0H                                                               
         AHI   R6,1                                                             
         AHI   R9,L'PRTLN                                                       
*                                                                               
DMKGSX   DS    0H                                                               
         XIT1  REGS=(R6,R9)                                                     
         EJECT                                                                  
         LTORG                                                                  
         EJECT                                                                  
*                                  # TO DISPLAY - BUMP R2                       
* PRINT LINE DSECT                                                              
       ++INCLUDE REGENPBYB                                                      
*                                                                               
WORKD    DSECT                                                                  
WORK     DS    CL17                                                             
SVCONF   DS    CL1                 CONFIRMED STATUS FOR ACE/GRAPHNET            
VOUTDAY  DS    A                   A(OUTDAY)                                    
VUNTIME  DS    A                   A(UNTIME)                                    
VDATCON  DS    A                   A(DATCON)                                    
VADDAY   DS    A                   A(ADDAY)                                     
DMCB     DS    6F                                                               
DUB      DS    D                                                                
SAVER1   DS    F                                                                
MAX      DS    H                                                                
TODAY    DS    CL3                                                              
DAY1     DS    F                                                                
NOSEND   DS    X                   DON'T SEND STATUS                            
MGSPOT#  DS    X                                                                
MGCNTR#  DS    X                                                                
MGTARGET DS    CL3                                                              
DATADISP DS    H                                                                
ELCODE   DS    X                                                                
BUYLIST  DS    XL255                                                            
PROFILES DS    XL8                 CONTRACT PROGRAM PROFILES                    
WORKX    EQU   *                                                                
BUYREC   DSECT                                                                  
       ++INCLUDE REGENBUY                                                       
CONREC   DSECT                                                                  
       ++INCLUDE REGENCON                                                       
       ++INCLUDE RECNTPROF                                                      
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'115REGENPBBYS05/01/02'                                      
         END                                                                    
