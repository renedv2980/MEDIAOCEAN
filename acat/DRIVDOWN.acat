*          DATA SET DRIVDOWN   AT LEVEL 056 AS OF 09/20/07                      
*CATALP DRIVDOWN                                                                
         TITLE 'DRIVDOWN - DOWN-LOADING OUTPUT ROUTINES'                        
*-----------------------------------------------------------------*             
*                                                                 *             
* GLD2128 OPTION ADDED ON 7/14/06.  IT'S PROBABLY MUCH CLOSER TO  *             
* CORRECT THAN DRIVDOWN WAS BEFORE AND FIXES A FEW SHORTCOMINGS,  *             
* BUT I DON'T HAVE THE COURAGE TO INSTALL IT EVERYWHERE, SO IT'S  *             
* ONLY THERE AS AN OPTION.  IT SHOULD BE SET BY DEFAULT IN ANY    *             
* CALLING APPS WHEN THEY ARE GOING TO OUTPUT ANY LARGE TEXT FIELDS*             
* (LIKE COMMENTS).                                                *             
*                                                                 *             
*-----------------------------------------------------------------*             
DRIVDOWN CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 0,DRDOWN**,R9,R8                                                 
         USING GLOBALD,RA                                                       
         L     RC,AOCOND                                                        
         USING DOUTD,RC                                                         
         EJECT                                                                  
*              DOWN LOADING CONTROLS                                            
         SPACE 3                                                                
         CLI   DLFIRST,0           IS THIS FIRST FOR REPORT                     
         BNE   DOWN2                                                            
         MVI   DLFIRST,1                                                        
*                                                                               
*        FIND TRANSLATE TABLE FOR CLEANING UP OUPUT                             
*                                                                               
         L     RF,GLCOMFAC         GET COMFACS ADDRESS                          
         L     RF,CGETFACT-COMFACSD(RF)   GETFACT ADDRESS                       
         GOTO1 (RF),DMCB,0         GET FACTS                                    
         L     RF,DMCB             A(FAFACTS)                                   
         L     RE,FAXLATES-FACTSD(RF)  A(CTRYXLAT)                              
*                                                                               
         L     RF,=A(DRIVCTRY-GLOBALD) DISPLACEMENT OF COUNTRY CODE             
         LA    RF,GLOBALD(RF)      POINT TO COUNTRY CODE                        
*                                                                               
         LA    R0,16               MAX 16 COUNTRIES                             
*                                                                               
         CLC   0(1,RF),0(RE)       MATCH COUNTRY TO THAT IN TABLES              
         BE    *+16                                                             
         LA    RE,16(RE)           NEXT ENTRY IN COUNTRY TABLE                  
         BCT   R0,*-14                                                          
         L     RE,DMCB             DEFAULT TO FIRST TABLE ENTRY                 
*                                                                               
         CLI   GLTWORKD,GLTMBASE   IF MEDIABASE                                 
         BNE   *+12                                                             
         L     RE,12(RE)              USE LOWERCASE TABLE                       
         B     *+8                                                              
         L     RE,8(RE)            ELSE USE UPPERCASE TABLE                     
*                                                                               
         ST    RE,DLTRTBLA                                                      
*                                                                               
         LA    R2,DLPOOL           CLEAR POOL                                   
         LA    R0,DLMAXLIN                                                      
         SPACE 1                                                                
DOWN1    MVC   0(165,R2),SPACES                                                 
         LA    R2,165(R2)                                                       
         BCT   R0,DOWN1                                                         
         SPACE 1                                                                
         GOTO1 PRINT,DMCB,PFILL,=C'BC01'                                        
         LA    R1,DLPOOL                                                        
         ST    R1,DLPADD                                                        
         MVI   DLCOUNT,0                                                        
         MVI   DLLINE,1                                                         
         BAS   RE,DOWNHEAD         DOWNLOAD HEADINGS                            
         SPACE 1                                                                
DOWN2    L     R2,GLATHID                                                       
         USING GLINTD,R2                                                        
         L     R3,GLAFOUT          PICK UP ADDRESS OF FIRST OUT                 
         USING DROD,R3                                                          
         ZIC   R1,GLLEVEL                                                       
         CLI   LINETYPE,C'D'                                                    
         BNE   *+8                                                              
         IC    R1,GLDETLEV                                                      
         SLL   R1,2                                                             
         LA    R1,GLARECL0(R1)     A(ADDRESS OF RECORD AT THIS LEVEL)           
         L     R1,0(R1)            A(RECORD AT THIS LEVEL)                      
         ST    R1,ATHISREC                                                      
         LA    R1,DLPOOL                                                        
         ST    R1,DLPADD                                                        
         MVI   DLCOUNT,0                                                        
         MVI   DLLINE,1                                                         
         SPACE 1                                                                
*                                  CHECK THIS OUT FOR CONDITIONALS              
DOWN4    GOTO1 =V(DRIVTEST),DMCB,(RA),DROIFS                                    
         BNE   DOWNEND                                                          
         TM    GLDOWNLD,GLDLHEAD   UNLESS USER ASKS FOR THEM,                   
         BO    *+12                                                             
         CLI   DROLTYP,C'H'        DON'T BOTHER WITH HEADLINES HERE             
         BE    DOWNEND                                                          
*                                                                               
         CLI   LINETYPE,C'T'       TEST TOTAL LINE                              
         BNE   DOWN5                                                            
         TM    DROOPTS,X'40'       AND COLUMN ASKS US NOT TO TOTAL              
         BO    DOWN4A                                                           
         CLC   DROLEV,GLLEVEL      OR LEVEL HIGHER THAN TOTAL LEVEL             
         BNH   DOWN5               BUT NOT IN THE COLUMNS                       
         CLC   DROLEV,GLDETLEV                                                  
         BH    DOWN5                                                            
         DROP  R2                                                               
*                                                                               
DOWN4A   L     R4,AMYP1            YES-JUST OUTPUT ASTERISKS                    
         MVI   0(R4),C'*'                                                       
         CLI   GLDLCHAR,C' '       OR USER PROVIDED CHARACTER                   
         BNH   *+10                                                             
         MVC   0(1,R4),GLDLCHAR                                                 
         MVC   1(197,R4),0(R4)                                                  
         ST    R4,DLIADD                                                        
         ZIC   R1,DROLEN                                                        
         TM    GLDOWNLD,GLDLNOTR   USE OUTPUT LENGTH IF NOT TRUNCATING          
         BO    *+16                                                             
         CLI   DROLEN,3            ELSE OUTPUT 3 ASTERISKS                      
         BNH   *+8                                                              
         LA    R1,3                                                             
         STC   R1,DLLEN                                                         
         BAS   RE,DLALPHA                                                       
         MVC   0(198,R4),SPACES                                                 
         B     DOWNEND                                                          
*                                                                               
DOWN5    CLI   LINETYPE,C'D'       TEST DETAIL LINE                             
         BNE   *+12                                                             
         TM    DROOPTS,X'20'       AND COLUMN ASKS US FOR TOTALS ONLY           
         BO    DOWNEND             YES-SKIP THIS COLUMN                         
*                                                                               
         ST    R3,GLADTENT                                                      
         L     R5,DROAPOS          R5=A(OUTPUT)                                 
         CLI   DROELEN,140         DO WE NEED TO HANDLE A LITERAL               
         BE    DOWN6                                                            
         ZIC   R1,DROELEN          YES                                          
         SH    R1,=H'140'                                                       
         STC   R1,DLLEN            PASS ITS LENGTH                              
         LA    R1,DROLIT                                                        
         ST    R1,DLIADD           ITS ADDRESS                                  
         BAS   RE,DLALPHA                                                       
         TM    GLDWNLD2,GLD2FIX    FIX EXTRA BLANK COL?                         
         BNZ   DOWNEND                                                          
         SPACE 1                                                                
DOWN6    XC    GLAIFLD,GLAIFLD                                                  
         MVI   DLINPLEN,0                                                       
         L     R1,DROIADD                                                       
         LTR   R1,R1                                                            
         BZ    DOWN8                                                            
         USING DRIND,R1                                                         
         ZIC   RE,DRINLEN                                                       
         STC   RE,DLINPLEN                                                      
         LH    RF,DRINDISP                                                      
         A     RF,ATHISREC                                                      
         ST    RF,GLAIFLD                                                       
         DROP  R1                                                               
         SPACE 1                                                                
DOWN8    OC    DRORADD,DRORADD     IS THERE A USER ROUTINE FOR THIS             
         BZ    DOWN10                                                           
         MVI   GLHOOK,GLROUT       YES SO SET UP FOR A HOOK                     
         MVC   GLLABEL,DROROUT                                                  
         MVC   GLAROUT,DRORADD                                                  
         MVC   GLARGS,DROARGS                                                   
*                                                                               
         OC    DROACOMP,DROACOMP   DO WE NEED TO COMPUTE THIS FIELD             
         BZ    DOWN8B                                                           
         MVC   DMCB+4(4),DROACOMP                                               
         MVC   DMCB+8(4),ATHISREC                                               
         GOTO1 =V(DRIVCOMP),DMCB,(RA)                                           
         TM    DROOPTS,X'10'       IF ABSOLUTE VALUE                            
         BNO   DOWN8A                                                           
         CP    DUB,=P'0'           AND IF NEGATIVE NUMBER                       
         BNL   DOWN8A                                                           
         MP    DUB,=P'-1'          MAKE POSITIVE                                
DOWN8A   DS    0H                                                               
         LA    R1,DUB              NOW HAVE RESULT IN DUB                       
         ST    R1,GLAIFLD          OFFER THE ADDRESS OF THIS TO USER            
         SPACE 1                                                                
DOWN8B   MVC   DLOUTLEN,DROLEN     EXECUTE OUTPUT ROUTINE                       
         BAS   RE,DOWNRTN                                                       
         CLI   GLHOOK,GLEDIT                                                    
         BNE   DOWNEND                                                          
         SPACE 1                                                                
DOWN10   MVC   DLLEN,DROLEN                                                     
         MVC   DLIADD,GLAIFLD                                                   
         CLI   DROTYPE,C'N'                                                     
         BE    DOWN12                                                           
         CLI   DROTYPE,C'D'                                                     
         BE    DOWN14                                                           
         OC    DLIADD,DLIADD                                                    
         BZ    DOWNEND                                                          
         CLI   DLINPLEN,0                                                       
         BE    DOWN11                                                           
         CLC   DLLEN,DLINPLEN      TEST LEN IS GREATER THAN INPUT LEN           
         BNH   DOWN11                                                           
         MVC   DLLEN,DLINPLEN      YES-SET TO INPUT LENGTH                      
         SPACE 1                                                                
DOWN11   BAS   RE,DLALPHA          EITHER DOWN LOAD ALPHA                       
         B     DOWNEND                                                          
         SPACE 1                                                                
DOWN12   BAS   RE,DLNUM            OR NUMERIC                                   
         B     DOWNEND                                                          
         SPACE 1                                                                
DOWN14   MVC   DLOUTPUT,SPACES     OR DATES                                     
         L     R1,GLAIFLD                                                       
         ST    R1,DMCB                                                          
         LA    R1,DLOUTPUT                                                      
         ST    R1,DMCB+4                                                        
         ST    R1,DLIADD                                                        
         L     R1,DROIADD                                                       
         USING DRIND,R1                                                         
*                                                                               
* MAKE SURE THERE IS A DATE BEFORE CALLING DATCON!                              
         ZIC   RE,DRINLEN                                                       
         BCTR  RE,0                                                             
         L     RF,GLAIFLD                                                       
         EX    RE,*+8                                                           
         B     *+10                                                             
         OC    0(0,RF),0(RF)                                                    
         BZ    DOWN16                                                           
*                                                                               
         MVC   DMCB(1),DRINTYPE+1                                               
         DROP  R1                                                               
         MVC   DMCB+4(1),DROTYPE+1                                              
         TM    GLDWNLD2,GLDCHGD5   IF USER ASKED TO CHANGE D5 DATES             
         BZ    DOWN15                                                           
         CLI   DMCB+4,5            AND THIS DATE HAS OUTPUT TYPE 5              
         BNE   DOWN15                                                           
         MVI   DMCB+4,10           CHANGE TO MM/DD/YY                           
DOWN15   GOTO1 DATCON,DMCB                                                      
         SPACE 1                                                                
DOWN16   BAS   RE,DLALPHA          (TREAT AS ALPHA)                             
         SPACE 1                                                                
DOWNEND  MVI   TOTPRT,C'N'                                                      
         CLI   LINETYPE,C'T'       ARE WE AT THE TOTAL LEVEL?                   
         BNE   DOWNEND1                                                         
         CLC   DROLEV,GLLEVEL                                                   
         BNE   DOWNEND1                                                         
         TM    GLDWNLD2,GLDLTTXT   YES-HAS USER ASKED FOR TOTAL TEXT?           
         BZ    DOWNEND1                                                         
         BAS   RE,DOWNTOT          YES-EXECUTE ANY TOTAL ROUTINE                
*                                                                               
DOWNEND1 DS    0H                                                               
         ZIC   R1,1(R3)                                                         
         AR    R3,R1                                                            
         CLI   0(R3),X'30'         NEXT OUT ?                                   
         BNE   DOWNEND2                                                         
         CLI   TOTPRT,C'Y'         YES-IF TOTAL TEXT JUST PRINTED,              
         MVI   TOTPRT,C'N'             SKIP THIS AND GO ON TO NEXT              
         BE    DOWNEND1                                                         
         B     DOWN4                                                            
         SPACE 1                                                                
DOWNEND2 CLI   0(R3),0                                                          
         BE    DOWNEND3                                                         
         CLI   0(R3),X'10'                                                      
         BNE   DOWNEND1                                                         
         SPACE 1                                                                
DOWNEND3 MVI   GLAROUT,1           HOOK TO APPLICATION TO SAY                   
         MVI   GLHOOK,GLPRINT      WE'RE ABOUT TO 'PRINT'                       
         BAS   RE,GOHOOK                                                        
         CLI   GLHOOK,GLDONT       APPLIC. CAN THEN REJECT                      
         BE    DOWNEND6                                                         
         CLI   GLHOOK,GLBCKOUT     APPLIC. CAN THEN REJECT                      
         BE    DOWNEND6                                                         
         L     R1,DLPADD                                                        
         MVI   0(R1),C';'          SEMI-COLON IS END OF LINE                    
         LA    R2,DLPOOL                                                        
         LA    R4,DLMAXLIN                                                      
         MVC   P,SPACES                                                         
         SPACE 1                                                                
DOWNEND4 MVC   P(165),0(R2)         NOW OUTPUT THE LINES                        
         L     RE,DLTRTBLA         POINT TO TRANSLATE TABLE                     
         TR    P(165),0(RE)        CLEAN UP OUTPUT                              
         CLC   P(165),SPACES                                                    
         BE    DOWNEND6                                                         
*                                                                               
         ICM   R0,15,AOUTPDCB      OUTPUT TO FILE???                            
         BZ    DOWNEND5             NO                                          
         PUT   (R0),P                                                           
         B     DOWNEN5A                                                         
*                                                                               
DOWNEND5 GOTO1 PRINT,DMCB,PFILL,=C'BL01'                                        
DOWNEN5A LA    R2,165(R2)                                                       
         BCT   R4,DOWNEND4                                                      
         SPACE 1                                                                
DOWNEND6 LA    R2,DLPOOL           CLEAR POOL                                   
         LA    R0,DLMAXLIN                                                      
         SPACE 1                                                                
DOWNEND8 MVC   0(165,R2),SPACES                                                 
         LA    R2,165(R2)                                                       
         BCT   R0,DOWNEND8                                                      
         MVC   P,SPACES                                                         
         LA    R1,DLPOOL                                                        
         ST    R1,DLPADD                                                        
         MVI   DLCOUNT,0                                                        
         MVI   DLLINE,1                                                         
*                                                                               
DOWNEN10 B     XIT                                                              
         EJECT                                                                  
*              EXECUTE OUTPUT ROUTINE AND FORMAT THE OUTPUT                     
         SPACE 3                                                                
DOWNRTN  NTR1  ,                                                                
         L     R1,AMYP1            GIVE USERS BIG BUFFER FOR OUTPUT             
         LA    R1,8(R1)            (SOMETIMES THEY PUT DATA BEFORE!)            
         ST    R1,GLAOFLD                                                       
         BAS   RE,GOHOOK                                                        
         CLI   GLHOOK,GLEDIT       DRIVOUT WILL DO EDITING SO DON'T             
         BE    DOWNR6              CHECK THE OUTPUT STACK                       
         SH    R1,=H'8'                                                         
         SPACE 1                                                                
*                                                                               
*        MAKE SURE OUTPUT CONTAINS VALID CHARACTERS                             
*          ALL SYSTEMS BUT MEDIABASE DO IT VIA A LOWER TO UPPERCASE             
*            TRANSLATE TABLE                                                    
*          MEDIABASE USES A LOWERCASE TRANSLATE TABLE THAT                      
*            ELIMINATES BAD CHARACTERS                                          
*                                                                               
         LR    RF,R1               COPY START OF OUTPUT LINES                   
         LA    R0,8                MAX 8 LINES                                  
         TM    GLDWNLD2,GLD2128                                                 
         BZ    *+8                                                              
         LHI   R0,10               MAX 10 LINES                                 
         L     RE,DLTRTBLA         POINT TO TRANSLATE TABLE                     
*                                                                               
DOWNTRLP DS    0H                                                               
         TR    0(198,RF),0(RE)     CLEAN UP THE OUTPUT                          
         LA    RF,198(RF)                                                       
         BCT   R0,DOWNTRLP                                                      
*                                                                               
         SPACE 1                                                                
         LA    RE,DLOUTPUT                                                      
         LA    RF,4                                                             
         MVC   0(198,RE),SPACES                                                 
         LA    RE,198(RE)                                                       
         BCT   RF,*-10                                                          
         SPACE 1                                                                
         CLC   0(198,R1),SPACES                                                 
         BE    DOWNR6                                                           
         ST    R1,DMCB                                                          
         TM    GLDOWNLD,GLDLCUT                                                 
         BO    DOWNR2                                                           
         MVC   DMCB+4,=F'1584'     8 LINES SUPPORTED                            
         TM    GLDWNLD2,GLD2128                                                 
         BZ    *+10                                                             
         MVC   DMCB+4,=F'1980'     10 LINES SUPPORTED                           
         GOTO1 SQUASHER,DMCB                                                    
         SPACE 1                                                                
DOWNR2   L     RF,DMCB+4           NEW LENGTH OF DATA                           
         L     R1,AMYP1            FIND START OF DATA                           
         CLI   0(R1),C' '                                                       
         BNE   *+14                                                             
         LA    R1,1(R1)                                                         
         BCTR  RF,0                                                             
         B     *-14                                                             
*                                                                               
**NOP    MVC   DLOUTPUT(165),0(R1)  MOVE INTO FIXED AREA                        
*                                                                               
         TM    GLDOWNLD,GLDLCUT    BUT, IF CUT OPTION IS ON                     
         BO    DOWNR3                                                           
*                                                                               
         CHI   RF,4*L'DLOUTPUT     I DON'T THINK THIS CAN EVER HAPPEN           
         BNH   *+8                                                              
         LHI   RF,4*L'DLOUTPUT     BUT I'LL PROTECT AGAINST IT!                 
         LA    RE,DLOUTPUT                                                      
         LR    R0,R1                                                            
         LR    R1,RF                                                            
         MVCL  RE,R0               MOVE INTO FIXED AREA                         
         B     DOWNR4                                                           
*                                                                               
DOWNR3   MVC   DLOUTPUT(165),SPACES                                             
         ZIC   RF,DLOUTLEN                                                      
         BCTR  RF,0                                                             
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   DLOUTPUT(0),0(R1)   ONLY MOVE WIDTH OF 1 LINE                    
         SPACE 1                                                                
DOWNR4   BAS   RE,STRIP            STRIP OUT NUMERIC FIELDS                     
         SPACE 1                                                                
DOWNR6   L     R1,AMYP1                                                         
         LA    R0,20                                                            
         MVC   0(198,R1),SPACES    CLEAR THE BUFFER                             
         LA    R1,198(R1)                                                       
         BCT   R0,*-10                                                          
         CLI   GLHOOK,GLEDIT                                                    
         BE    DOWNRX                                                           
         ZIC   R0,DLOUTLEN                                                      
         TM    GLDOWNLD,GLDLNOTR   NO TRUNCATION OF TRAILING SPACES             
         BO    DOWNR8                                                           
         TM    GLDWNLD2,GLD2128    WIDE DL? (>128 CHARS)                        
         BNZ   DOWNR100                                                         
         LA    R1,DLOUTPUT+159     FIGURE OUT LENGTH                            
         LA    R0,160                                                           
         SPACE 1                                                                
         CLI   0(R1),C' '                                                       
         BNE   DOWNR8                                                           
         BCTR  R1,0                                                             
         BCT   R0,*-10                                                          
         LA    R0,1                (MINIMUM OF 1!)                              
         SPACE 1                                                                
DOWNR8   STC   R0,DLLEN                                                         
         TM    GLDWNLD2,GLDROLEN       USE DROLEN HERE?                         
         BNO   *+10                                                             
         MVC   DLLEN,DROLEN            YES                                      
         LA    R1,DLOUTPUT                                                      
         ST    R1,DLIADD                                                        
         BAS   RE,DLALPHA          DOWNLOAD USER EXPRESSION                     
         B     DOWNRX                                                           
*                                                                               
DOWNR100 LA    R1,DLOUTPUT+791     FIGURE OUT LENGTH                            
         LHI   R0,792                                                           
         SPACE 1                                                                
         CLI   0(R1),C' '                                                       
         BNE   DOWNR110                                                         
         BCTR  R1,0                                                             
         BCT   R0,*-10                                                          
         LA    R0,1                (MINIMUM OF 1!)                              
         SPACE 1                                                                
DOWNR110 LA    R1,DLOUTPUT                                                      
         STC   R0,DLLEN                                                         
         CHI   R0,128                                                           
         BNH   DOWNR120                                                         
         MVI   DLLEN,128                                                        
         MVI   DLPASS,DLP1ST       TELL DLALPHA ABOUT MULTIPLE PASSES           
*                                                                               
DOWNR120 TM    GLDWNLD2,GLDROLEN       USE DROLEN HERE?                         
         BNO   *+10                                                             
         MVC   DLLEN,DROLEN            YES                                      
         ST    R1,DLIADD                                                        
         BAS   RE,DLALPHA          DOWNLOAD USER EXPRESSION                     
         CHI   R0,128                                                           
         BNH   DOWNRX                                                           
         AHI   R1,128              ADVANCE INPUT POINTER                        
         AHI   R0,-128             DECREMENT OUTPUT LENGTH REMAINING            
         STC   R0,DLLEN                                                         
         CHI   R0,128                                                           
         BH    DOWNR130                                                         
         MVI   DLPASS,DLPLAST                                                   
         B     DOWNR120                                                         
*                                                                               
DOWNR130 MVI   DLLEN,128                                                        
         B     DOWNR120                                                         
*                                                                               
DOWNRX   B     XIT                                                              
         EJECT                                                                  
*              EXECUTE TOTAL ROUTINE AND LITERALS                               
         SPACE 3                                                                
DOWNTOT  NTR1  ,                                                                
         MVI   TOTPRT,C'N'                                                      
         SR    R0,R0                                                            
         ICM   R0,1,DROTLFOL       R0=N'TOTAL ELEMENTS                          
         BZ    DOWNTOTX                                                         
         SR    R1,R1                                                            
         LR    RE,R3                                                            
         MVI   DLOUTLEN,1                                                       
*                                                                               
DOWNTOT1 ICM   R1,1,1(RE)          GET OUTPUT LENGTH BY FINDING OUT             
         BZ    DOWNTOT2            ELEMENT FOR NEXT LEVEL                       
         AR    RE,R1                                                            
         CLI   0(RE),X'30'                                                      
         BNE   *+14                                                             
         MVC   DLOUTLEN,DROLEN-DROD(RE)   EXTRACT OUTPUT LENGTH                 
         B     DOWNTOT2                                                         
         CLI   0(RE),0                                                          
         BE    DOWNTOT2                                                         
         CLI   0(RE),X'10'                                                      
         BNE   DOWNTOT1                                                         
*                                                                               
DOWNTOT2 SR    R1,R1                                                            
         ICM   R1,1,1(R3)                                                       
         BZ    DOWNTOTX                                                         
         AR    R3,R1                                                            
         CLI   0(R3),X'48'         TOTAL ELEMENT                                
         BNE   DOWNTOT2                                                         
         USING DRFLD,R3                                                         
         GOTO1 =V(DRIVTEST),DMCB,(RA),DRFLIFS                                   
         BNE   DOWNTOT6                                                         
         OC    DRFLRADD,DRFLRADD   TOTAL ROUTINE?                               
         BZ    DOWNTOT4                                                         
         MVI   GLHOOK,GLROUT       YES-EXECUTE IT                               
         MVC   GLLABEL,DRFLROUT                                                 
         MVC   GLAROUT,DRFLRADD                                                 
         MVC   GLARGS,DRFLARGS                                                  
         OI    GLINDS,GLINTOTL                                                  
         BAS   RE,DOWNRTN                                                       
         NI    GLINDS,255-GLINTOTL                                              
         CLI   GLHOOK,GLEDIT       UNLESS USER ASKS US TO EDIT,                 
         BE    DOWNTOT4                                                         
         MVI   TOTPRT,C'Y'         INDICATE TOTAL TEXT HAS BEEN                 
*                                  FORMATTED                                    
DOWNTOT4 CLI   DRFLELEN,60                                                      
         BE    DOWNTOT6                                                         
         ZIC   R1,DRFLELEN         LITERAL                                      
         SH    R1,=H'60'                                                        
         STC   R1,DLLEN                                                         
         LA    R1,DRFLLIT                                                       
         ST    R1,DLIADD                                                        
         BAS   RE,DLALPHA                                                       
         MVI   TOTPRT,C'Y'                                                      
*                                                                               
DOWNTOT6 CLI   TOTPRT,C'Y'         TOTAL TEXT HAS BEEN FORMATTED?               
         BE    DOWNTOTX                                                         
         BCT   R0,DOWNTOT2         NO-LOOK FOR MORE TOTAL ELEMENTS              
*                                                                               
DOWNTOTX B     XIT                                                              
         SPACE 1                                                                
TOTPRT   DS    CL1                                                              
         EJECT                                                                  
*              DOWN LOADING HEADINGS                                            
         SPACE 3                                                                
DOWNHEAD NTR1                                                                   
         TM    GLDOWNLD,GLDLNOHD   OPTION NOT TO DOWNLOAD HEADINGS              
         BO    XIT                                                              
         LA    R4,DLHDPOOL         R4=A(HEADLINE POOL)                          
         LA    R6,DLHDPOLX         R6=A(END OF HEADLINE POOL)                   
         ZIC   R0,GLLHEADL         R0=NUMBER OF HEAD LINES                      
         ZIC   R1,GLFHEADL                                                      
         SR    R0,R1                                                            
         AH    R0,=H'1'                                                         
         LA    R1,1                                                             
         SPACE 1                                                                
DH2      STC   R1,DHLINE           DEALING WITH LINES 1-4                       
         OI    GLDWNLD2,GLDLHDLS   INDICATE DOWNLOAD OF HEADLINES               
         BAS   RE,DH4                                                           
         LA    R1,1(R1)                                                         
         BCT   R0,DH2                                                           
         NI    GLDWNLD2,X'FF'-GLDLHDLS   TURN OFF INDICATOR                     
         B     XIT                                                              
         SPACE 1                                                                
DH4      NTR1                                                                   
         L     R2,GLATHID                                                       
         USING GLINTD,R2                                                        
         L     R3,GLAFOUT                                                       
         MVI   DLPEND,C'N'                                                      
         SPACE 1                                                                
DH6      CLI   0(R3),0                                                          
         BE    DHLAST                                                           
         CLI   0(R3),X'10'                                                      
         BE    DHLAST                                                           
         CLI   0(R3),X'30'         LOOK FOR OUT                                 
         BE    DH30                                                             
         CLI   0(R3),X'40'         AND HEAD ELEMENTS                            
         BE    DH40                                                             
         B     DHEND                                                            
         SPACE 1                                                                
         USING DROD,R3                                                          
DH30     ST    R3,LAST30                                                        
         CLI   DLPEND,C'Y'                                                      
         BNE   DH32                                                             
         LA    R1,DLOUTPUT                                                      
         ST    R1,DLIADD                                                        
         MVI   DLLEN,1                                                          
         MVI   DLOUTPUT,C' '                                                    
         BAS   RE,DLALPHA                                                       
         SPACE 1                                                                
DH32     MVI   DLPEND,C'N'                                                      
         CLI   DHLINE,1            TEST FIRST TIME ROUND                        
         BNE   *+10                                                             
         XC    DRODLADR(12),DRODLADR    YES-CLEAR ADDRS OF HEADS 2-4            
*                                                                               
         TM    GLDOWNLD,GLDLHEAD   IF DOWNHEAD, GIVE HEADINGS FOR H & M         
         BZ    DH34                 ELSE ONLY FOR PRINT LINES                   
         CLI   DROLTYP,C'H'        HEADER?                                      
         BE    DH36                                                             
         CLI   DROLTYP,C'M'        MIDLINE?                                     
         BE    DH36                                                             
         SPACE 1                                                                
DH34     CLI   DROLTYP,C'P'                                                     
         BNE   DHEND                                                            
         SPACE 1                                                                
DH36     GOTO1 =V(DRIVTEST),DMCB,(RA),DROIFS                                    
         BNE   DHEND                                                            
         MVI   DLPEND,C'Y'         OUT IS OK SO WE NEED A HEADING               
         B     DHEND                                                            
         SPACE 1                                                                
         USING DRHDD,R3                                                         
DH40     CLI   DLPEND,C'N'         IGNORE IF NOT PENDING                        
         BE    DHEND                                                            
         GOTO1 =V(DRIVTEST),DMCB,(RA),DRHDIFS                                   
         BNE   DHEND                                                            
         TM    DRHDSTAT,X'80'      IGNORE DELETED HEADS                         
         BO    DHEND                                                            
         CLI   DHLINE,1            TEST FIRST HEADLINE                          
         BNE   *+16                                                             
         CLI   DRHDLINE,1          YES-MUST MATCH ON LINE                       
         BE    DH41                                                             
         B     DHEND                                                            
         CLC   DRHDLINE,DHLINE     NOT 1ST HEADLINE-IF MATCH,                   
         BE    DH41                                 THEN CONTINUE               
         CLI   DRHDLINE,1          TEST THIS HEAD ENTRY IS FOR HEAD 1           
         BNE   DHEND                                                            
         L     R1,LAST30           YES-                                         
         USING DROD,R1                                                          
         ZIC   RE,DHLINE                                                        
         BCTR  RE,0                                                             
         BCTR  RE,0                                                             
         SLL   RE,2                                                             
         LA    RE,DRODLADR(RE)                                                  
         ICM   RF,15,0(RE)         TEST HEAD ALREADY FORMATTED BY               
         BZ    DHEND               HEADLINE 1 ROUTINE                           
         MVI   DLPEND,C'N'         YES-SO DOWNLOAD IT NOW                       
         LA    R1,39(RF)           FIGURE OUT LENGTH                            
         LA    R0,40                                                            
         SPACE 1                                                                
DH40A    CLI   0(R1),C' '                                                       
         BNE   *+10                                                             
         BCTR  R1,0                                                             
         BCT   R0,DH40A                                                         
         STC   R0,DLLEN                                                         
         ST    RF,DLIADD                                                        
         BAS   RE,DLALPHA          DOWNLOAD USER EXPRESSION                     
         B     DHEND                                                            
         SPACE 1                                                                
DH41     MVI   DLPEND,C'N'                                                      
         CLI   DRHDELEN,56         DO WE NEED TO HANDLE A LITERAL               
         BE    DH42                                                             
         ZIC   R1,DRHDELEN         YES                                          
         SH    R1,=H'56'                                                        
         STC   R1,LITLEN           PASS ITS LENGTH                              
         LA    R1,DRHDLIT                                                       
         ST    R1,ALITIN           ITS ADDRESS                                  
         LA    R1,DLOUTPUT                                                      
         ST    R1,ALITOUT                                                       
         MVC   DLOUTPUT,SPACES                                                  
         BAS   RE,DOLIT                                                         
         B     DH44                                                             
         SPACE 1                                                                
DH42     LA    R1,DLOUTPUT                                                      
         ST    R1,GLAOFLD                                                       
         LA    RF,4                                                             
         MVC   0(198,R1),SPACES                                                 
         LA    R1,198(R1)                                                       
         BCT   RF,*-10                                                          
         XC    GLAIFLD,GLAIFLD                                                  
         L     R1,LAST30                                                        
         USING DROD,R1                                                          
         L     R1,DROIADD          PICK UP A(IN) IF ANY                         
         LTR   R1,R1                                                            
         BZ    DH43                                                             
         USING DRIND,R1                                                         
         LH    R1,DRINDISP                                                      
         DROP  R1                                                               
         A     R1,AACTREC                                                       
         ST    R1,GLAIFLD          RECORD A(INPUT FIELD IN RECORD)              
         SPACE 1                                                                
DH43     OC    DRHDRADD,DRHDRADD   IS THERE A USER ROUTINE FOR THIS             
         BZ    DH44                                                             
         MVI   GLHOOK,GLROUT       YES SO SET UP FOR A HOOK                     
         MVC   GLLABEL,DRHDROUT                                                 
         MVC   GLAROUT,DRHDRADD                                                 
         MVC   GLARGS,DRHDARGS                                                  
         ST    R3,GLADTENT                                                      
         BAS   RE,GOHOOK                                                        
         CLI   DHLINE,1            TEST FIRST HEADLINE                          
         BNE   DH44                                                             
         L     R1,LAST30           YES-TEST SUBSEQUENT HEADLINES WERE           
         USING DROD,R1                 FORMATTED BY ROUTINE FOR FIRST           
         LA    R0,3                    HEADLINE                                 
         LA    RE,DLOUTPUT+198                                                  
         LA    RF,DRODLADR                                                      
         DROP  R1                                                               
         SPACE 1                                                                
DH43A    CLC   0(40,RE),SPACES                                                  
         BE    DH43B                                                            
         CR    R4,R6               YES-CHECK WE HAVEN'T FILLED HEADLINE         
         BL    *+6                     POOL YET                                 
         DC    H'0'                                                             
         MVC   0(40,R4),0(RE)      SAVE HEADLINE IN HEAD POOL AND SAVE          
         ST    R4,0(RF)            ITS ADDRESS IN DRIVE TABLE 'OUT'             
         LA    R4,40(R4)           ENTRY                                        
         SPACE 1                                                                
DH43B    LA    RE,198(RE)                                                       
         LA    RF,4(RF)                                                         
         BCT   R0,DH43A                                                         
         SPACE 1                                                                
DH44     LA    R0,1                                                             
         CLC   DLOUTPUT,SPACES                                                  
         BE    DH48                                                             
         LA    R1,DLOUTPUT+159     FIGURE OUT LENGTH                            
         LA    R0,160                                                           
         SPACE 1                                                                
DH46     CLI   0(R1),C' '                                                       
         BNE   DH48                                                             
         BCTR  R1,0                                                             
         BCT   R0,DH46                                                          
         SPACE 1                                                                
DH48     STC   R0,DLLEN                                                         
         LA    R1,DLOUTPUT                                                      
         ST    R1,DLIADD                                                        
         BAS   RE,DLALPHA          DOWNLOAD USER EXPRESSION                     
         SPACE 1                                                                
DHEND    ZIC   R1,1(R3)                                                         
         AR    R3,R1                                                            
         B     DH6                                                              
         SPACE 1                                                                
DHLAST   CLI   DLPEND,C'Y'                                                      
         BNE   DOWNEND3                                                         
         LA    R1,DLOUTPUT                                                      
         ST    R1,DLIADD                                                        
         MVI   DLLEN,1                                                          
         MVI   DLOUTPUT,C' '                                                    
         BAS   RE,DLALPHA                                                       
         B     DOWNEND3                                                         
         EJECT                                                                  
*              DOWN LOADING FACILITIES - ALPHA                                  
         SPACE 3                                                                
DLALPHA  NTR1                                                                   
         LTR   R5,R5               TEST FOR P=NO                                
         BZ    XIT                                                              
         MVC   DLACTLEN,DLLEN                                                   
         TM    GLDOWNLD,GLDLNOTR   NO TRUNCATION OF TRAILING SPACES             
         BO    *+8                                                              
         BAS   RE,SETACTLN         FIRST FIGURE OUT ACTUAL LENGTH               
         CLI   DLACTLEN,128        ABSOLUTE FIELD MAX OF 128                    
         BL    *+8                                                              
         MVI   DLACTLEN,128                                                     
         SPACE 1                                                                
DLALPH10 ZIC   R1,DLCOUNT          IS THERE ROOM FOR THIS                       
         ZIC   R0,DLACTLEN                                                      
         AR    R1,R0                                                            
         CLI   DLPASS,0                                                         
         BNE   *+12                                                             
         AHI   R1,3                AND 2 QUOTES AND 1 SPACE                     
         B     DLALPH12                                                         
         TM    DLPASS,DLP1ST                                                    
         BZ    *+12                                                             
         AHI   R1,1                ONLY 1 QUOTE                                 
         B     DLALPH12                                                         
         TM    DLPASS,DLPLAST                                                   
         BZ    DLALPH12                                                         
         AHI   R1,2                1 QUOTE AND 1 SPACE                          
*                                                                               
DLALPH12 STC   R1,DLCOUNT                                                       
         CH    R1,=H'131'                                                       
         BNH   DLALPH20                                                         
         LA    R1,DLPOOL           POINT TO NEXT LINE IN POOL                   
         ZIC   R0,DLLINE                                                        
         MH    R0,=H'165'                                                       
         AR    R1,R0                                                            
         ST    R1,DLPADD                                                        
         AI    DLLINE,1            BUMP LINE                                    
         CLI   DLLINE,DLMAXLIN     CHECK WE HAD ROOM                            
         BNH   *+6                                                              
         DC    H'0'                                                             
         MVI   DLCOUNT,0                                                        
         B     DLALPH10            (GO BACK - WILL NOW FIT)                     
         SPACE 1                                                                
DLALPH20 L     R2,DLIADD                                                        
         L     R3,DLPADD                                                        
         ZIC   R0,DLACTLEN                                                      
         BAS   RE,ISITNUM                                                       
         MVI   0(R3),C' '                                                       
         CLI   ITSNUM,C'Y'                                                      
         BNE   DLALPH30                                                         
         TM    GLDWNLD2,GLD2FIX    FIX EXTRA BLANK COL?                         
         BNZ   DLALPH40             YES                                         
         LA    R3,1(R3)             NO - SKIP SPACE                             
         B     DLALPH40                                                         
*                                                                               
DLALPH30 CLI   DLPASS,0            DOING MULTIPLE PASSES?                       
         BE    *+16                 NO                                          
         TM    DLPASS,DLP1ST        YES - IS THIS FIRST PASS?                   
         BZ    DLALPH40              NO - SKIP QUOTE                            
         MVI   DLPASS,DLPSUB       SET FOR SUBSEQUENT PASS                      
         MVI   0(R3),C'"'          DOUBLE QUOTE TO START                        
         LA    R3,1(R3)                                                         
*                                                                               
DLALPH40 MVC   0(1,R3),0(R2)       NOW MOVE OUT THE DATA                        
         CLI   0(R3),C'"'          REPLACE A DOUBLE QUOTE                       
         BNE   *+8                                                              
         MVI   0(R3),C''''         WITH A SINGLE QUOTE                          
*                                                                               
         TM    GLDWNLD2,GLD2128    IF WIDE TEXT ALLOWED                         
         BZ    DLALPH50                                                         
         CLI   0(R3),SEMICOL       REPLACE A SEMI-COLON WITH A COMMA            
         BNE   *+8                                                              
         MVI   0(R3),C','                                                       
         CLI   0(R3),COLON         REPLACE A COLON WITH A PERIOD                
         BNE   *+8                                                              
         MVI   0(R3),C'.'                                                       
*                                                                               
DLALPH50 CLI   ITSNUM,C'Y'         IF ITS NUMERIC                               
         BNE   DLALPH60                                                         
         CLI   0(R3),C'$'          TAKE OUT DOLLAR SIGNS                        
         BNE   DLALPH60                                                         
         MVI   0(R3),C' '                                                       
         SPACE 1                                                                
DLALPH60 LA    R2,1(R2)                                                         
         LA    R3,1(R3)                                                         
         BCT   R0,DLALPH40                                                      
         MVI   0(R3),C' '                                                       
         CLI   ITSNUM,C'Y'                                                      
         BNE   DLALPH70                                                         
         TM    GLDWNLD2,GLD2FIX    FIX EXTRA BLANK COL?                         
         BNZ   DLALPH80             YES                                         
         LA    R3,1(R3)             NO - SKIP SPACE                             
         B     DLALPH80                                                         
*                                                                               
DLALPH70 TM    DLPASS,DLPSUB       WILL THERE BE A SECOND+ PASS?                
         BNZ   DLALPH90             YES - DON'T END QUOTE OR SET SPACE          
         MVI   0(R3),C'"'          DOUBLE QUOTE TO END                          
         LA    R3,1(R3)                                                         
         MVI   DLPASS,0                                                         
*                                                                               
DLALPH80 MVI   0(R3),C' '          THEN A SPACE                                 
         LA    R3,1(R3)                                                         
*                                                                               
DLALPH90 ST    R3,DLPADD                                                        
         B     XIT                                                              
         EJECT                                                                  
*              CHECK IF FIELD IS REALLY NUMERIC                                 
         SPACE 3                                                                
ISITNUM  NTR1                      CHECK IF FIELD IS NUMERIC                    
         MVI   ITSNUM,0                                                         
         CLI   GLABCSW,0                                                        
         BNE   XIT                                                              
         TM    GLDOWNLD,GLDLALPH    ALL FIELDS SHOULD BE ALPHA                  
         BO    XIT                                                              
         L     R3,GLADTENT                                                      
         LTR   R3,R3                                                            
         BZ    XIT                                                              
         CLI   0(R3),X'30'                                                      
         BNE   XIT                                                              
         USING DROD,R3                                                          
         LR    RF,R3               SAVE POINTER                                 
         L     R3,DROIADD                                                       
         USING DRIND,R3                                                         
         LTR   R3,R3                                                            
         BZ    ISITNUM2                                                         
         CLI   DRINTYPE+1,C'+'     CAN'T BE UNLESS FIELDS ADDITIVE              
         BNE   XIT                                                              
         TM    GLDWNLD2,GLDADDCH   KEEP ADDITIVE CHAR FLDS AS CHAR.             
         BNO   ISITNUM2                                                         
         USING DROD,RF                RE-ESTABLISH OUTPUT DESCRIPTION           
         CLI   DROTYPE,C'N'           CHECK NUMERIC IF NUMERIC OUTPUT           
         BE    ISITNUM2                                                         
         B     XIT                    ELSE KEEP AS ALPHA                        
         DROP  RF                                                               
*                                                                               
ISITNUM2 CLI   0(R2),C' '          SPACE ALLOWED                                
         BE    ISITNUM4                                                         
         CLI   0(R2),C'.'          SO IS PERIOD                                 
         BE    ISITNUM4                                                         
         CLI   0(R2),C','          SO IS COMMA                                  
         BE    ISITNUM4                                                         
         CLI   0(R2),C'-'          AND MINUS SIGN                               
         BE    ISITNUM4                                                         
         CLI   0(R2),C'+'          AND PLUS SIGN                                
         BE    ISITNUM4                                                         
         CLI   0(R2),C'$'          AND $ ALTHOUGH THIS WILL BE                  
         BE    ISITNUM4            STRIPPED OUT LATER                           
         CLI   0(R2),C'0'                                                       
         BNL   ISITNUM3            AND, OF COURSE, 0-9                          
         MVI   ITSNUM,C'N'                                                      
         B     XIT                                                              
         SPACE 1                                                                
ISITNUM3 MVI   ITSNUM,C'Y'         AT LEAST ONE DIGIT FOUND!                    
         SPACE 1                                                                
ISITNUM4 LA    R2,1(R2)                                                         
         BCT   R0,ISITNUM2                                                      
         B     XIT                                                              
         SPACE 1                                                                
ITSNUM   DC    C'N'                                                             
         EJECT                                                                  
*              STRIP NUMERIC FIELDS AND ZERO FILL                               
         SPACE 3                                                                
STRIP    NTR1                                                                   
         TM    GLDOWNLD,GLDLSTRP   ONLY APPLICABLE IF STRIP ON                  
         BNO   XIT                                                              
         L     R3,GLADTENT                                                      
         LTR   R3,R3                                                            
         BZ    XIT                                                              
         CLI   0(R3),X'30'                                                      
         BNE   XIT                                                              
         USING DROD,R3                                                          
         L     R3,DROIADD                                                       
         USING DRIND,R3                                                         
         LTR   R3,R3                                                            
         BZ    STRIP2                                                           
         CLI   DRINTYPE+1,C'+'     CAN'T BE UNLESS FIELDS ADDITIVE              
         BNE   XIT                                                              
         SPACE 1                                                                
*                                  DATA CURRENTLY IN DLOUTPUT                   
STRIP2   MVC   DLBORROW,DLOUTPUT   SO SAVE INTO DLBORROW                        
         MVI   ITSNEG,C'N'                                                      
         L     R3,GLADTENT                                                      
         USING DROD,R3                                                          
         MVI   DLOUTPUT,C'0'                                                    
         ZIC   R1,DROLEN           AND PRE-FILL OUTPUT WITH 0'S                 
         LTR   R1,R1                                                            
         BNP   XIT                                                              
         SH    R1,=H'2'                                                         
         BM    STRIP3                                                           
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   DLOUTPUT+1(0),DLOUTPUT                                           
         SPACE 1                                                                
STRIP3   ZIC   R1,DROLEN                                                        
         LA    RE,DLBORROW                                                      
         LA    RF,DLOUTPUT                                                      
         BCTR  R1,0                                                             
         AR    RE,R1               RE=A(END OF INPUT)                           
         AR    RF,R1               RF=A(END OF OUTPUT)                          
         LA    R1,1(R1)                                                         
         SPACE 1                                                                
STRIP4   CLI   0(RE),C'-'          CHECK FOR NEGATIVE                           
         BNE   *+8                                                              
         MVI   ITSNEG,C'Y'                                                      
         CLI   0(RE),C'0'          STRIP OUT NON NUMERIC                        
         BL    STRIP6                                                           
         CLI   0(RE),C'9'                                                       
         BH    STRIP6                                                           
         MVC   0(1,RF),0(RE)       PASS A NUMERIC CHARACTER                     
         BCTR  RF,0                                                             
         SPACE 1                                                                
STRIP6   BCTR  RE,0                                                             
         BCT   R1,STRIP4                                                        
         CLI   ITSNEG,C'Y'                                                      
         BNE   XIT                                                              
         MVI   DLOUTPUT,C'-'       RETURN NEGATIVE AS FIRST FIELD               
         B     XIT                                                              
         SPACE 1                                                                
ITSNEG   DC    C'N'                                                             
         EJECT                                                                  
*              DOWN LOADING FACILITIES - SET LENGTH OF ALPHA                    
         SPACE 3                                                                
SETACTLN NTR1                                                                   
         L     R2,DLIADD           POSITION R2 TO END OF FIELD                  
         ZIC   R3,DLLEN                                                         
         BCTR  R2,0                                                             
         AR    R2,R3                                                            
         SPACE 1                                                                
SETACT2  STC   R3,DLACTLEN                                                      
         CLI   0(R2),C' '          FIND LAST NON BLANK                          
         BH    SETACT3                                                          
         BCTR  R2,0                                                             
         BCT   R3,SETACT2                                                       
         MVI   DLACTLEN,1          ALWAYS SEND AT LEAST 1 SPACE                 
*                                                                               
SETACT3  ICM   R3,15,GLADTENT      HAVE A(DRIVE TABLE ENTRY)?                   
         BZ    XIT                 NO                                           
         CLI   0(R3),X'30'         IS THIS THE OUTPUT FIELD?                    
         BNE   XIT                 NO                                           
         USING DROD,R3                                                          
         CLI   DROLTYP,C'M'        MIDLINE?                                     
         BNE   XIT                 NO                                           
         DROP  R3                                                               
         CLC   GLMIDXTR,SPACES     ANY EXTRA MIDLINE DATA?                      
         BNH   XIT                 NO                                           
         CLI   LINETYPE,C'T'       TOTAL LINE?                                  
         BE    XIT                 YES                                          
*                                                                               
         L     R2,DLIADD           FIELD TO BE OUTPUT                           
         ZIC   R3,DLACTLEN         OUTPUT LENGTH W/O SPACES                     
         LA    R2,2(R3,R2)         LEAVE TWO SPACES BEFORE MIDLINE DATA         
         MVC   0(L'GLMIDXTR,R2),GLMIDXTR                                        
         LA    R2,L'GLMIDXTR-1(R2) R2 AT LAST POSSIBLE CHARACTER                
         AHI   R3,L'GLMIDXTR+2     R3 AT MAX POSSIBLE LENGTH                    
*                                                                               
SETACT4  CLI   0(R2),C' '          LAST CHARACTER                               
         BH    SETACT5             YES                                          
         BCTR  R2,0                MOVE 1 SPACE BACK                            
         BCT   R3,SETACT4          DECREMENT LENGTH                             
*                                                                               
SETACT5  STC   R3,DLACTLEN         ACTUAL LENGTH W/O SPACES                     
         MVC   GLMIDXTR,SPACES     CLEAR EXTRA MIDLINE DATA                     
*                                                                               
         B     XIT                                                              
         EJECT                                                                  
*              DOWN LOADING FACILITIES - NUMERIC                                
         SPACE 3                                                                
DLNUM    NTR1                                                                   
         LTR   R5,R5               TEST FOR P=NO                                
         BZ    XIT                                                              
         CLI   GLABCMOD,1          IF ABC IS JUST PRINTING HEADERS              
         BE    XIT                 THEN IT DOESN'T HAVE NUMBERS HERE            
         USING DROD,R3                                                          
         OC    DROACOMP,DROACOMP   DO WE NEED TO COMPUTE THIS FIELD             
         BZ    DLNUM2                                                           
         OC    DRORADD,DRORADD     IF HAVE USER ROUTINE DONT RECOMPUTE          
         BNZ   DLNUM1                                                           
         MVC   DMCB+4(4),DROACOMP                                               
         MVC   DMCB+8(4),ATHISREC                                               
         GOTO1 =V(DRIVCOMP),DMCB,(RA)                                           
         TM    DROOPTS,X'10'       IF ABSOLUTE VALUE                            
         BNO   DLNUMA                                                           
         CP    DUB,=P'0'           AND IF NEGATIVE NUMBER                       
         BNL   DLNUMA                                                           
         MP    DUB,=P'-1'          MAKE POSITIVE                                
DLNUMA   DS    0H                                                               
*                                  NUMERIC VALUE IS PACKED IN DUB               
DLNUM1   XC    EBLOCK,EBLOCK                                                    
         LA    R1,DUB                                                           
         ST    R1,EBAIN                                                         
         MVI   EBTIN,C'P'                                                       
         MVI   EBLIN,8                                                          
         B     DLNUM4                                                           
         SPACE 1                                                                
DLNUM2   XC    EBLOCK,EBLOCK       GENERAL NUMERIC                              
         MVC   EBAIN,DLIADD                                                     
         L     R2,DROIADD          PICK UP A(IN)                                
         USING DRIND,R2                                                         
         MVC   EBTIN,DRINTYPE      PICK UP INPUT TYPE                           
         MVC   EBLIN,DRINFLEN      AND LENGTH                                   
         DROP  R2                                                               
         SPACE 1                                                                
DLNUM4   LA    R1,DLOUTPUT                                                      
         ST    R1,EBAOUT                                                        
         MVC   DLOUTPUT,SPACES                                                  
         MVI   EBLOUT,16                                                        
         CLI   DROALIGN,C'R'       CHECK FOR RIGHT ALIGNMENT                    
         BNE   *+10                                                             
         MVC   EBLOUT,DROLEN       USE OUTPUT LENGTH                            
         MVC   EBDECS,DRODEC                                                    
         MVC   EBROUND,DRODIV                                                   
         MVI   EBOPT,X'20'         SET ZERO=NOBLANK                             
         TM    DROEDIT,X'48'                                                    
         BZ    *+8                                                              
         OI    EBOPT,X'40'                                                      
         MVI   EBALIGN,C'L'        LEFT ALIGN FIELD                             
         CLI   DROALIGN,C'R'       CHECK FOR RIGHT ALIGNMENT                    
         BNE   *+8                                                              
         MVI   EBALIGN,C'R'                                                     
         CLI   DROALIGN,C'C'       CHECK FOR CENTERING                          
         BNE   *+8                                                              
         MVI   EBALIGN,C'C'                                                     
         CLI   DROFILL,C'0'        FILL=0 IS OK                                 
         BNE   *+10                                                             
         MVC   EBFILL,DROFILL                                                   
         CLI   DROFLOAT,C'-'       SO IS FLOAT=-                                
         BNE   *+10                                                             
         MVC   EBFLOAT,DROFLOAT                                                 
***      TM    GLDOWNLD,GLDLSTRP   OPTION TO STRIP NUMBERS                      
***      BNO   DLNUM5                                                           
***      NI    EBOPT,X'FF'-EBOQCEY NO COMMAS                                    
***      NI    EBOPT,X'FF'-EBOQMEY NO MINUS SIGNS YET                           
***      MVI   EBDECS,0            NO DECIMAL POINTS                            
***      MVI   EBFILL,C'0'         FILL WITH 0                                  
***      MVC   EBLOUT,DROLEN       USE REAL OUTPUT LENGTH                       
***      MVI   EBALIGN,C'R'        AND RIGHT ALIGN                              
         SPACE 1                                                                
DLNUM5   GOTO1 EDITOR,DMCB,EBLOCK                                               
         BAS   RE,STRIP                                                         
         SPACE 1                                                                
         ZIC   R1,DROLEN                                                        
         TM    GLDOWNLD,GLDLNOTR   DO NOT TRUNCATE TRAILING SPACES              
         BO    DLNUM8                                                           
         LA    RE,DLOUTPUT+15      FIGURE OUT L'OUTPUT                          
         LA    R1,16                                                            
         SPACE 1                                                                
DLNUM6   CLI   0(RE),C' '                                                       
         BNE   DLNUM8                                                           
         BCTR  RE,0                                                             
         BCT   R1,DLNUM6                                                        
         SPACE 1                                                                
DLNUM8   ZIC   R0,DLCOUNT          IS THERE ROOM FOR THIS                       
         STC   R1,DLOLEN                                                        
         AR    R1,R0                                                            
         LA    R1,1(R1)            AND 1 SPACE?                                 
         TM    GLDOWNLD,GLDLALPH   TEST ALL FIELDS SHOULD BE ALPHA              
         BZ    *+8                                                              
         LA    R1,2(R1)            YES-ADD 2 FOR DINKS                          
         STC   R1,DLCOUNT                                                       
*****    CH    R1,=H'163'                                                       
         CH    R1,=H'131'                                                       
         BNH   DLNUM10                                                          
         LA    R1,DLPOOL           POINT TO NEXT LINE IN POOL                   
         ZIC   R0,DLLINE                                                        
         MH    R0,=H'165'                                                       
         AR    R1,R0                                                            
         ST    R1,DLPADD                                                        
         AI    DLLINE,1            BUMP LINE                                    
         CLI   DLLINE,DLMAXLIN     CHECK WE HAD ROOM                            
         BNH   *+6                                                              
         DC    H'0'                                                             
         MVI   DLCOUNT,0           SET NEW COUNTER FOR NEW OUTPUT LINE          
         ZIC   R1,DLOLEN           OUTPUT LENGTH OF DATA                        
         LA    R1,1(R1)            PLUS 1                                       
         TM    GLDOWNLD,GLDLALPH   AND IF ALPHA                                 
         BZ    *+8                 NO                                           
         LA    R1,2(R1)            ADD 2 DINKS                                  
         STC   R1,DLCOUNT                                                       
         SPACE 1                                                                
DLNUM10  ZIC   R1,DLOLEN           NOW OUTPUT THE DATA                          
         L     R2,DLPADD                                                        
         TM    GLDOWNLD,GLDLALPH   DINKS IF ALL FIELDS SHOULD BE ALPHA          
         BZ    *+12                                                             
         MVI   0(R2),C'"'                                                       
         LA    R2,1(R2)                                                         
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R2),DLOUTPUT                                                 
         AR    R2,R1                                                            
         TM    GLDOWNLD,GLDLALPH                                                
         BZ    *+12                                                             
         MVI   0(R2),C'"'                                                       
         LA    R2,1(R2)                                                         
         MVI   0(R2),C' '          END WITH A SPACE                             
         LA    R2,1(R2)                                                         
         ST    R2,DLPADD                                                        
         B     XIT                                                              
         SPACE 1                                                                
*** IF YOU THINK YOU WILL EVER EXECUTE THIS CODE, YOU'RE SORELY                 
*** MISTAKEN!  LOOK IN DRIVOCON.  (EJOR 7/13/07, MANY HOURS LATER)              
DOWNLAST NTR1                                                                   
         TM    GLDOWNLD,GLDLACTV                                                
         BNO   XIT                                                              
         MVI   DLFIRST,0           RESET AT END OF REPORT                       
         MVI   P,C':'              SEND COLON FOR REPORT END                    
         GOTO1 PRINT,DMCB,PFILL,=C'BL01'                                        
         B     XIT                                                              
         EJECT                                                                  
       ++INCLUDE DRIVOSHR                                                       
         EJECT                                                                  
*              BLOCK FOR EDITOR                                                 
         SPACE 3                                                                
       ++INCLUDE DDEBLOCK                                                       
         EJECT                                                                  
*              LTORG                                                            
         SPACE 1                                                                
         LTORG                                                                  
         EJECT                                                                  
*              AREAS USED FOR DOWNLOADING                                       
         SPACE 3                                                                
DLPASS   DS    XL1                 PASS FLAGS                                   
DLP1ST   EQU   X'80'               FIRST PASS                                   
DLPSUB   EQU   X'40'               SUBSEQUENT PASS                              
DLPLAST  EQU   X'20'               LAST PASS                                    
*                                                                               
SEMICOL  EQU  X'5E'                                                             
COLON    EQU  X'7A'                                                             
*                                                                               
DLCOUNT  DS    XL1                 SUPPORT FOR DOWNLOAD                         
DLPADD   DS    A                                                                
DLIADD   DS    A                                                                
DLTRTBLA DS    A                   TRANSLATE TABLE ADDRESS                      
DLLEN    DS    CL1                                                              
DLACTLEN DS    CL1                                                              
DLOLEN   DS    CL1                                                              
DLPEND   DS    CL1                                                              
DHLINE   DS    XL1                                                              
DLLINE   DS    XL1                                                              
DLOUTLEN DS    XL1                                                              
DLBORROW DS    CL160                                                            
DLFILLER DS    CL8                                                              
DLINPLEN DS    XL1                                                              
DLOUTPUT DS    4CL198                                                           
DLPOOL   DS    (DLMAXLIN*165)C                                                  
DLMAXLIN EQU   10                                                               
         SPACE 1                                                                
DLHDPOOL DS    60CL40                                                           
DLHDPOLX EQU   *                                                                
         SPACE 1                                                                
         EJECT                                                                  
       ++INCLUDE DRIVOUTD                                                       
         SPACE 1                                                                
         PRINT OFF                                                              
       ++INCLUDE DRGLOBAL                                                       
       ++INCLUDE DRLOCAL                                                        
       ++INCLUDE DRINTRECD2                                                     
       ++INCLUDE DRIVETABLE                                                     
       ++INCLUDE DDBIGBOX                                                       
       ++INCLUDE DDBUFFALOD                                                     
       ++INCLUDE DDCOMFACSD                                                     
       ++INCLUDE FAFACTS                                                        
         PRINT ON                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'056DRIVDOWN  09/20/07'                                      
         END                                                                    
