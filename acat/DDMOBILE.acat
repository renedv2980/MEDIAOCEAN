*          DATA SET DDMOBILE   AT LEVEL 052 AS OF 07/14/19                      
*CATALP MOBILE                                                                  
*                                                                               
*------------------------------------------------------------------*            
* ONLINE CORERES = T00A72 -- USE LINK BOOK LMMOBILE                *            
* WE ARE STUCK WITH A LITTLE PROBLEM HERE - MOBILE CONTAINS        *            
* REFERENCES TO =V(SYSPROFS) WHICH IS RESOLVED ONLY WHEN MOBILE    *            
* IS LINKED IN SPONSOR AND REFERS TO THE SP00 PROFILE.             *            
* IT THEN USES THE SP00 PROFILE DISPLACEMENTS FOR ANY PROFILE IT   *            
* IS PASSED.                                                                    
* ZEN AND I COULD NOT SORT THIS OUT, AS THERE ARE A LOT OF PHASES  *            
* CALLING MOBILE.                                                  *            
* SO WE JUDGED IT BEST TO JUST LEAVE WELL ENOUGH ALONE.            *            
* SO MOBILE NEEDS TO BE LINKED INTO SPONSOR.  (MHER 7/97)          *            
*                                                                  *            
* 21JUL98 MHER CALL GETBROAD WITH X'02' TO PASS A (SYSPROFS)       *            
*------------------------------------------------------------------*            
*                                                                               
* INPUT PARAMETERS ARE                                                          
*                                                                               
* ALWAYS PASS  P1    A(INPUT)                                                   
*              P2    A(OUTPUT)                                                  
*                                                                               
* ONLINE ONLY  P3    A(ADCON LIST) - GETBROAD/ADDAY/GETDAY/DATCON               
*              P4    A(SYSPROF) OR A(0)                                         
********************************************************************            
         TITLE 'MONTH/WEEK LIST BUILDER'                                        
MOBILE   CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 MOBILDX-MOBILD,**MOBILE,RR=R8                                    
*                                                                               
         USING MOBILD,RC                                                        
         LR    R9,R1               R9=A(PARAMETER LIST)                         
         LM    R2,R3,0(R9)                                                      
*                                  GET ADRESSES OF EXTERNALS                    
         MVC   WADCONS(ADCONSL),ADCONS                                          
         LA    RE,WADCONS          RELOCATE LINKED ADCONS                       
         LA    R0,ADCONSN                                                       
MB0      ICM   R1,15,0(RE)                                                      
         BZ    *+10                DON'T RELOCATE IF NOT LINKED                 
         AR    R1,R8                                                            
         ST    R1,0(RE)                                                         
         LA    RE,4(RE)                                                         
         BCT   R0,MB0                                                           
*                                                                               
         L     RE,8(R9)            RE=A(USER PASSED ADCONS)                     
         LA    RF,WADCONS          RF=A(W/S ADCONS)                             
         LA    R0,ADCONSN          R0=N'ADCONS PASSABLE                         
MB1      OC    0(4,RF),0(RF)       TEST EXTERNAL IS LINKED                      
         BNZ   *+10                                                             
         MVC   0(4,RF),0(RE)       NO - GET FROM USER LIST                      
         LA    RE,4(RE)                                                         
         LA    RF,4(RF)                                                         
         BCT   R0,MB1                                                           
         ICM   RE,15,=V(SYSPROFS)  SET A(SYSTEM PROFILE)                        
         BNZ   *+8                                                              
         L     RE,12(R9)                                                        
         ST    RE,SYSPROFS                                                      
*                                                                               
         MVC   MAX,0(R9)                                                        
         MVI   0(R9),0                                                          
         MVC   START(12),0(R2)                                                  
         MVC   TYPE,4(R9)                                                       
*                                                                               
         MVI   PNGSW,C'N'          RESET SPECIAL CASE SWITCHES                  
         MVI   MM3SW,C'N'                                                       
         MVI   NA9SW,C'N'                                                       
*                                                                               
         CLI   TYPE,19             SPECIAL P&G PERIOD ?                         
         BNE   *+8                                                              
         MVI   TYPE,C'P'           SPECIAL P&G PERIOD ?                         
         CLI   TYPE,C'P'           SPECIAL P&G PERIOD ?                         
         JNE   MB1A                                                             
         MVI   PNGSW,C'Y'                                                       
         MVI   TYPE,0              RESET TYPE TO BROADCAST                      
         J     MB1X                                                             
*                                                                               
MB1A     CLI   TYPE,C'M'           SPECIAL MM3 PERIOD ?                         
         JNE   MB1B                                                             
         MVI   MM3SW,C'Y'                                                       
         MVI   TYPE,10             RESET TYPE TO BROADCAST                      
         J     MB1X                                                             
*                                                                               
MB1B     TM    TYPE,X'80'          SPECIAL CALL FOR A9 ON?                      
         JZ    MB1X                                                             
         MVI   NA9SW,C'Y'          YES                                          
         NI    TYPE,X'7F'          TURN OF X'80' BIT                            
*                                                                               
MB1X     CLI   TYPE,1                                                           
         BE    MB2                                                              
         CLI   TYPE,3                                                           
         BE    MB2                                                              
         CLC   START+4(2),=C'  '                                                
         BNE   BM4                                                              
*                                                                               
MB2      MVC   START+4(2),=C'01'                                                
         EJECT                                                                  
*                                                                               
*              BROADCAST MONTHS                                                 
*                                                                               
BM4      CLI   TYPE,1                                                           
         BH    CM2                                                              
         LA    RE,START                                                         
*                                                                               
BM4B     L     RF,GETBROAD                                                      
         LA    R1,PARAS                                                         
         ST    RE,0(R1)                                                         
         LA    RE,WORK                                                          
         ST    RE,4(R1)                                                         
         MVC   8(4,R1),GETDAY                                                   
         MVC   12(4,R1),ADDAY                                                   
         MVC   16(4,R1),SYSPROFS                                                
         MVI   0(R1),X'01'+X'02'   SET ADCONS/SYSPROFS SUPPLIED                 
         BASR  RE,RF                                                            
         CLI   0(R1),X'FF'                                                      
         BNE   *+6                                                              
         DC    H'0'                                                             
         CLC   WORK2,START                                                      
         BNL   BM5                 CAN HAPPEN                                   
         GOTO1 ADDAY,PARAS,START,START2,7                                       
         LA    RE,START2                                                        
         B     BM4B                                                             
*                                                                               
BM5      CLI   TYPE,1                                                           
         BE    BM6                                                              
         MVC   WORK,START                                                       
*                                                                               
BM6      BAS   RE,SPLAT                                                         
         GOTO1 ADDAY,PARAS,WORK2,START,15                                       
         L     RF,GETBROAD                                                      
         LA    R1,PARAS                                                         
         LA    RE,START                                                         
         ST    RE,0(R1)                                                         
         LA    RE,WORK                                                          
         ST    RE,4(R1)                                                         
         MVC   8(4,R1),GETDAY                                                   
         MVC   12(4,R1),ADDAY                                                   
         MVC   16(4,R1),SYSPROFS                                                
         MVI   0(R1),X'01'+X'02'   SET ADCONS/SYSPROFS SUPPLIED                 
         BASR  RE,RF                                                            
         B     BM6                                                              
         EJECT                                                                  
*                                                                               
*              CALENDAR MONTHS                                                  
*                                                                               
         SPACE 3                                                                
CM2      CLI   TYPE,3                                                           
         BH    BW2                                                              
         BL    CM4                                                              
         MVC   START+4(2),=C'01'   TYPE 3=BACK UP TO START OF MONTH             
*                                                                               
CM4      MVC   WORK2,START         WORK OUT END DATE (START OF NEXT-1)          
         MVC   WORK2+4(2),=C'01'                                                
         GOTO1 ADDAY,PARAS,WORK2,SAVEDATE,45                                    
         MVC   SAVEDATE+4(2),=C'01'                                             
         L     R4,=F'-1'                                                        
         GOTO1 (RF),(R1),SAVEDATE,WORK2,(R4)                                    
         MVC   WORK,START                                                       
         BAS   RE,SPLAT                                                         
         MVC   START,SAVEDATE                                                   
         B     CM4                                                              
         EJECT                                                                  
*                                                                               
*              BROADCAST WEEKS                                                  
*                                                                               
BW2      CLI   TYPE,5                                                           
         BH    SM2                                                              
         GOTO1 GETDAY,PARAS,START,WORK                                          
         MVC   WORK,START                                                       
         SR    R4,R4                                                            
         IC    R4,PARAS            (DAY OF WEEK NUMBER (1-7) IN R4)             
         MVC   WORK2,WORK                                                       
         LA    R5,7                                                             
         ICM   RF,15,SYSPROFS                                                   
         BZ    BW3                                                              
         CLI   8(RF),0                                                          
         BE    BW3                                                              
         ZIC   R5,8(RF)                                                         
         BCT   R5,BW3                                                           
         LA    R5,7                                                             
*                                                                               
BW3      CR    R4,R5                                                            
         BE    BW4                                                              
         BL    *+8                                                              
         LA    R5,7(R5)                                                         
         SR    R5,R4               R5=NUMBER OF DAYS UNTIL SUNDAY               
         GOTO1 ADDAY,PARAS,WORK,WORK2,(R5)                                      
*                                                                               
BW4      CLI   TYPE,5              OPTION TO BACK UP TO PREVIOUS MONDAY         
         BNE   BW6                                                              
         SR    R5,R5                                                            
         SH    R5,=H'6'                                                         
         GOTO1 ADDAY,PARAS,WORK2,WORK,(R5)                                      
*                                                                               
BW6      BAS   RE,SPLAT                                                         
         GOTO1 ADDAY,PARAS,WORK2,WORK,1                                         
         GOTO1 (RF),(R1),WORK,WORK2,6                                           
         B     BW6                                                              
         EJECT                                                                  
*                                                                               
*              SPECIAL MONTH FOMULAE                                            
*                                                                               
SM2      CLI   TYPE,9              FIRST STAGE IS BUILD A 4-YEAR CYCLE          
         BE    DAY1                                                             
* WORK OUT START OF CYCLE LAST YEAR                                             
         GOTO1 ADDAY,PARAS,(C'Y',START),CYCLE,F'-1'                             
         MVC   CYCLE+2(4),=C'0101'                                              
         MVI   STARTDAY,1                                                       
         ICM   RF,15,SYSPROFS      IS THERE A FISCAL BASE AVAILABLE             
         BZ    SM6                                                              
         CLI   8(RF),0             OPTION TO VARY START DAY                     
         BE    *+10                                                             
         MVC   STARTDAY,8(RF)                                                   
         ZIC   R2,6(RF)            MONTH                                        
         LTR   R2,R2                                                            
         BZ    SM4                                                              
         CVD   R2,DUB              YES - USE IT                                 
         UNPK  CYCLE+2(2),DUB                                                   
         OI    CYCLE+3,X'F0'                                                    
*                                                                               
SM4      ZIC   R2,7(RF)            DAY                                          
         LTR   R2,R2                                                            
         BZ    SM6                                                              
         CVD   R2,DUB              YES - USE IT                                 
         UNPK  CYCLE+4(2),DUB                                                   
         OI    CYCLE+5,X'F0'                                                    
*                                                                               
SM6      MVC   BASE,CYCLE                                                       
         GOTO1 GETDAY,PARAS,BASE,WORK                                           
         ZIC   R4,PARAS                                                         
         ZIC   R5,STARTDAY         BACK UP TO START DAY OF WEEK                 
         CR    R4,R5                                                            
         BE    SM7                                                              
         BH    *+8                                                              
         LA    R4,7(R4)                                                         
         SR    R5,R4                                                            
         GOTO1 ADDAY,PARAS,BASE,CYCLE,(R5)                                      
*                                                                               
SM7      LA    R2,CYCLE                                                         
         LA    R4,7                NEED 7  YEARS FOR SPOT WRITER                
         CLI   NA9SW,C'Y'                                                       
         BNE   *+8                                                              
         LA    R4,20               NEED 20 YEARS FOR NET A9                     
*                                                                               
SM8      LA    R7,BUMP20                                                        
         CLI   TYPE,20                                                          
         BE    SM10                                                             
         LA    R7,BUMP18                                                        
         CLI   TYPE,18                                                          
         BE    SM10                                                             
         LA    R7,BUMP10           LOCATE CYCLE FORMULA                         
         CLI   TYPE,10             10=4,4,4                                     
         BE    SM10                                                             
         CLI   TYPE,14             14-17=4,4,4                                  
         BNL   SM10                                                             
         LA    R7,BUMP11                                                        
         CLI   TYPE,11             11=4,4,5,4,5,4,4,4,5,4,4,5 !                 
         BE    SM10                                                             
         LA    R7,BUMP12                                                        
         CLI   TYPE,12             12=3,5,5                                     
         BE    SM10                                                             
         LA    R7,BUMP13                                                        
         CLI   TYPE,13             13=4,5,4 4,4,5 4,4,5 4,4,5                   
         BE    SM10                                                             
         LA    R7,BUMP678                                                       
         CLI   TYPE,7                                                           
         BH    SM10                8=4,4,5                                      
         LA    R7,1(R7)                                                         
         BE    SM10                7=4,5,4                                      
         LA    R7,1(R7)                                                         
         B     SM10                6=5,4,4                                      
*                                                                               
BUMP678  DC    AL1(27,27,34,27,27,34,27,27,34,27,27,34,27,27,34)                
BUMP10   DC    AL1(27,27,27,27,27,27,27,27,27,27,27,27,27,27,27)                
BUMP11   DC    AL1(27,27,34,27,34,27,27,27,34,27,27,34,27,27,34)                
BUMP12   DC    AL1(21,34,34,21,34,34,21,34,34,21,34,34,21,34,34)                
BUMP13   DC    AL1(27,34,27,27,27,34,27,27,34,27,27,34,27,34,27)                
BUMP18   DC    AL1(27,27,34,27,27,34,27,27,34,34,27,27,27,27,34)                
BUMP20   DC    AL1(27,27,34,27,34,27,27,34,27,27,34,27,27,27,34)                
BUMP1417 DC    AL1(4,3,3,3,4,3,3)                                               
                                                                                
SM10     LA    R5,13               YEARLY CYCLES HAVE 13 PERIODS                
         CLI   TYPE,10             10                                           
         BE    SM14                                                             
         CLI   TYPE,14             AND 14                                       
         BL    SM11                                                             
         CLI   TYPE,17             THRU 17                                      
         BNH   SM14                                                             
SM11     DS    0H                                                               
         LA    R5,12               OR 12 PERIODS                                
*                                                                               
SM14     ZIC   R6,0(R7)                                                         
         GOTO1 ADDAY,PARAS,(R2),6(R2),(R6)                                      
         GOTO1 ADDAY,PARAS,6(R2),12(R2),1                                       
         LA    R2,12(R2)                                                        
         LA    R7,1(R7)            ADVANCE IN BUMP PATTERN                      
         BCT   R5,SM14                                                          
*                                                                               
* BUMP FISCAL BASE TO NEXT YEAR                                                 
*                                                                               
         GOTO1 ADDAY,PARAS,(C'Y',BASE),DUB,F'1'                                 
         MVC   BASE(6),DUB                                                      
         LR    R6,R2                                                            
         SHI   R6,6                                                             
         GOTO1 ADDAY,PARAS,(R6),WORK,7                                          
         CLC   WORK(6),BASE                                                     
         BNL   SM16                CAN WE SQUEEZE ANOTHER WEEK IN               
         MVC   0(6,R6),WORK                                                     
         GOTO1 ADDAY,PARAS,WORK,(R2),1                                          
*                                                                               
SM16     BCT   R4,SM8              7 OR 18 YEARS                                
*                                                                               
         CLI   TYPE,14             TEST 4-WEEK QTRS (TYPES 14-17)               
         BL    SP17                                                             
         CLI   TYPE,17                                                          
         BH    SP17                                                             
*                                                                               
         LA    R2,QTRCYCLE         YES-BUILD LIST OF QUARTERS                   
         LA    R6,CYCLE                                                         
         LA    R4,7                                                             
         LA    R7,BUMP1417                                                      
         CLI   TYPE,14             14=4,3,3,3                                   
         BE    SM16A                                                            
         LA    R7,1(R7)                                                         
         CLI   TYPE,17             17=3,3,3,4                                   
         BE    SM16A                                                            
         LA    R7,1(R7)                                                         
         CLI   TYPE,16             16=3,3,4,3                                   
         BE    SM16A                                                            
         LA    R7,1(R7)            15=3,4,3,3                                   
*                                                                               
SM16A    LR    R8,R7                                                            
         LA    R5,4                                                             
*                                                                               
SM16B    MVC   0(6,R2),0(R6)                                                    
         ZIC   RF,0(R8)             NUMBER OF MONTHS THIS QUARTER               
         BCTR  RF,0                                                             
         LA    R6,12(R6)                                                        
         BCT   RF,*-4                                                           
         MVC   6(6,R2),6(R6)                                                    
         LA    R2,12(R2)                                                        
         LA    R6,12(R6)                                                        
         LA    R8,1(R8)                                                         
         BCT   R5,SM16B            4 QUARTERS PER YEAR                          
         BCT   R4,SM16A            7 YEARS                                      
         LA    R2,QTRCYCLE                                                      
         B     SP18                                                             
         EJECT                                                                  
*                                                                               
*              SPECIAL MONTH FORMULAE - SECOND STAGE                            
*                                                                               
SP17     LA    R2,CYCLE                                                         
*                                                                               
SP18     CLC   START,6(R2)         FIND REQUEST START DATE IN CYCLE             
         BNH   SP20                                                             
         LA    R2,12(R2)                                                        
         B     SP18                                                             
*                                                                               
SP20     MVC   WORK,START          NOW GENERATE OUTPUT DATES                    
         MVC   WORK2,6(R2)                                                      
         BAS   RE,SPLAT                                                         
*                                                                               
SP22     LA    R2,12(R2)                                                        
         MVC   WORK(12),0(R2)                                                   
         BAS   RE,SPLAT                                                         
         B     SP22                                                             
         EJECT                                                                  
*                                                                               
*              DAILY DATES FROM PREVIOUS MONDAY                                 
*                                                                               
DAY1     CLI   TYPE,9                                                           
         BNE   XIT                                                              
         GOTO1 GETDAY,PARAS,START,WORK                                          
         MVC   WORK,START                                                       
         SR    R4,R4                                                            
         IC    R4,PARAS            (DAY OF WEEK (1-7) IN R4)                    
         MVC   WORK2,WORK                                                       
         LA    R5,7                                                             
*                                                                               
         ICM   RF,15,SYSPROFS      HANDLE SHIFTED WEEK START                    
         BZ    DAY3                                                             
         CLI   8(RF),0                                                          
         BE    DAY3                                                             
         ZIC   R5,8(RF)                                                         
         BCT   R5,DAY3                                                          
         LA    R5,7                                                             
*                                                                               
DAY3     CR    R4,R5               FOR OFFSET WEEKS                             
         BE    DAY4                                                             
         BL    *+8                                                              
         LA    R5,7(R5)                                                         
*                                                                               
         SR    R5,R4               R5=NUMBER OF DAYS UNTIL SUNDAY               
         BZ    DAY4                (THERE ALREADY)                              
         GOTO1 ADDAY,PARAS,WORK,WORK2,(R5)                                      
*                                                                               
DAY4     SR    R5,R5               BACK UP TO PREVIOUS MONDAY                   
         SH    R5,=H'6'              (START OF WEEK)                            
         GOTO1 ADDAY,(R1),WORK2,WORK,(R5)                                       
         MVC   WORK2,WORK                                                       
*                                                                               
DAY6     BAS   RE,SPLAT                                                         
         GOTO1 ADDAY,PARAS,WORK2,WORK,1                                         
         MVC   WORK2,WORK                                                       
         B     DAY6                                                             
         EJECT                                                                  
*                                                                               
*              ROUTINE TO OUTPUT DATES, AND CHECK IF WE'RE DONE                 
*                                                                               
SPLAT    ST    RE,SAVERE                                                        
         CLC   WORK2,END                                                        
         BL    SPLAT2                                                           
         CLC   END+4(2),=C'  '                                                  
         BE    SPLAT2                                                           
         MVC   WORK2,END                                                        
*                                                                               
SPLAT2   GOTO1 DATCON,PARAS,(0,WORK),(2,0(R3))                                  
         GOTO1 (RF),(R1),(0,WORK2),(2,2(R3))                                    
         MVI   4(R3),X'FF'                                                      
         SR    R4,R4                                                            
         IC    R4,0(R9)                                                         
         LA    R4,1(R4)                                                         
         STC   R4,0(R9)            SET NUMBER USED TO USER                      
         CLC   WORK2,END           IF WE'VE REACHED END DATE                    
         BNL   XIT                                                              
         CLC   0(1,R9),MAX         OR WE'VE GENERATED ENOUGH,                   
         BE    XIT                 WE'RE THROUGH                                
         LA    R3,4(R3)            OTHERWISE, BACK FOR MORE                     
         L     RE,SAVERE                                                        
         BR    RE                                                               
*                                                                               
XIT      CLI   MM3SW,C'Y'          SPECIAL MM3 PERIOD?                          
         BE    XIT40                                                            
         CLI   PNGSW,C'Y'          SPECIAL PNG PERIOD?                          
         BNE   XITOUT                                                           
         L     R3,4(R9)            RE-POINT TO PERIODS TABLE                    
*                                                                               
*                                                                               
*Y2K*                                                                           
*IT20    CLC   =X'D0DC',0(R3)      COMPARE WITH JUN28/04                        
*        BNE   *+10                                                             
*        MVC   0(2,R3),=X'D0E1'    RESET START TO JUL1/04                       
*                                                                               
*        CLC   =X'D0DB',2(R3)      COMPARE WITH JUN27/04                        
*        BNE   *+10                                                             
*        MVC   2(2,R3),=X'D0DE'    SET END OF PERIOD TO JUN30/04                
*                                                                               
*        LA    R3,4(R3)            NEXT PERIOD                                  
*        CLI   0(R3),X'FF'         EOT?                                         
*        BE    XITOUT                                                           
*        B     XIT20                                                            
*                                                                               
*                                                                               
*                                  1ST TIME, CHK IF STARTING IN JULY            
*        SR    RE,RE               CHECK IF STARTING FROM JULY                  
*        ICM   RF,12,2(R3)         GET THE END PERIOD IN HIGH NIBBLE            
*        SLDL  RE,7                PUT YEAR IN RE                               
*        SR    RE,RE               CLEAR IT                                     
*        SLDL  RE,4                NOW GET MONTH                                
*        CHI   RE,7                IS IT JULY MOS ?                             
*        BNE   XIT30               NO, NEXT                                     
*        CLC   =X'D0DC',0(R3)      COMPARE WITH JUN28/04 (WHEN STARTED)         
*        BH    XIT30               IF BEFORE THAT DATE, DON'T CHANGE            
*        GOTO1 DATCON,PARAS,(2,0(R3)),(3,WORK)    ELSE, START ON JUL/01         
*        MVC   WORK+1(2),=X'0701'  PUT IN JUL01                                 
*        GOTO1 DATCON,PARAS,(3,WORK),(2,0(R3))                                  
*        B     XIT30                                                            
*                                                                               
***      GOTO1 DATCON,PARAS,(2,2(R3)),(3,WORK)    TAKE END DATE                 
***      CLI   WORK+1,7                                                         
***      CLI   4(R3),X'FF'         EOT?                                         
***      GOTO1 DATCON,PARAS,(2,4(R3)),(3,WORK)    START OF JULY MOS             
***      MVC   WORK+1(2),=X'0701'  PUT IN JUL01                                 
*                                                                               
*IT10    CLC   =X'D0DB',2(R3)      COMPARE WITH JUN27/04 (WHEN STARTED)         
*        BH    XIT30               IF BEFORE, GET NEXT ONE                      
*                                                                               
XIT10    SR    RE,RE                                                            
         ICM   RF,12,2(R3)         GET THE END PERIOD IN HIGH NIBBLE            
         SLDL  RE,7                PUT YEAR IN RE                               
         SR    RE,RE               CLEAR IT                                     
         SLDL  RE,4                NOW GET MONTH                                
         CHI   RE,3                IS IT MAR MOS ?                              
         BE    XIT20                                                            
         CHI   RE,7                IS IT JUL MOS ?                              
         BE    XIT15               YES                                          
         CHI   RE,6                IS IT JUNE MOS ?                             
         BNE   XIT30               NO, NEXT                                     
*                                                                               
         GOTO1 DATCON,PARAS,(2,2(R3)),(3,WORK)    TAKE END DATE                 
         MVC   WORK+1(2),=X'061E'  PUT IN JUN30                                 
         GOTO1 DATCON,PARAS,(3,WORK),(2,2(R3))                                  
         B     XIT30               PROCESS NEXT DATE                            
*                                                                               
XIT15    GOTO1 DATCON,PARAS,(2,0(R3)),(3,WORK)    START OF JULY MOS             
         MVC   WORK+1(2),=X'0701'  PUT IN JUL01                                 
         GOTO1 DATCON,PARAS,(3,WORK),(2,0(R3))                                  
         CLC   0(2,R3),2(R3)       SEE IF NEXT PERIOD'S START IS AFTER          
         BL    XIT30               ITS END                                      
         CLI   4(R3),X'FF'         CAN ONLY HAPPEN IF REQUEST LAST DATE         
         BE    *+6                 IS IN JUNE, BUT IS PART OF JULY MOS          
         DC    H'0'                EX. IF JUNE29/04 IS END DATE, LAST           
*                                                                               
         SHI   R3,4                GO BACK TO PREVIOUS PERIOD                   
         L     RE,4(R9)            RE-POINT TO PERIODS TABLE                    
         CR    R3,RE               BEFORE START OF TABLE?                       
         BL    XITOUT              YES - WE'RE DONE                             
         MVC   2(2,R3),6(R3)       PERIOD WILL BE JUNE28-JUNE29/04              
         MVI   4(R3),X'FF'         SO IN STEAD OF MOVING IN JUNE30,             
         B     XITOUT              MOVE IN JUNE29 AND EXIT                      
*                                                                               
XIT20    CLC   =X'EC79',2(R3)      COMPARE WITH 3/25/18                         
         BNE   XIT30                                                            
         MVC   2(2,R3),=X'EC81'    SET END OF PERIOD TO 4/1/18                  
         CLI   4(R3),X'FF'         EOT?                                         
         BE    XIT30                                                            
         CLC   =X'EC7A',4(R3)      IS NEXT START 3/26/18?                       
         BNE   XIT30                                                            
         MVC   4(2,R3),=X'EC82'    CHANGE TO 4/2/18                             
         CLC   4(2,R3),6(R3)       MAKE SURE EO APR PERIOD IS AFTER             
         BL    XIT30                                                            
         MVC   6(2,R3),=X'EC82'    APRIL WILL ONLY HAVE 1 DATE                  
*                                                                               
XIT30    LA    R3,4(R3)            NEXT PERIOD                                  
         CLI   0(R3),X'FF'         EOT?                                         
         BE    XITOUT                                                           
         B     XIT10                                                            
*                                                                               
XIT40    L     R3,4(R9)            RE-POINT TO PERIODS TABLE                    
         LR    R4,R3               REMEMBER WHERE STARTED                       
         LA    R4,0(R4)            CLEAR HOB                                    
XIT45    CLC   =X'D185D221',0(R3)  FIND PERIOD OF DEC5/04-JAN1/05               
         BE    XIT50               IF FOUND, GO CHANGE IT                       
         LA    R3,4(R3)            NEXT PERIOD                                  
         CLI   0(R3),X'FF'         EOT? IF DIDN'T FIND, JUST EXIT...            
         BE    XITOUT              ...SOMETHING'S CHANGED?..                    
         B     XIT45                                                            
*                                                                               
XIT50    MVC   0(2,R3),=X'D17C'    CHANGE IT TO NOV28/04-JAN1/05                
XIT55    AHI   R3,-4               NOW START BACKING UP THROUGH DATES           
         CR    R3,R4               PUSHING THEM 1 WEEK BACK                     
         BL    XITOUT              DONE WHEN BACK UP BEFORE START               
         CLC   =X'CF7ED023',0(R3)  FIND PERIOD OF NOV30/03-JAN3/04              
         BE    XIT60               WHEN FOUND, SUBTR ONLY FROM JAN3/04          
         GOTO1 DATCON,PARAS,(2,0(R3)),(0,WORK)    TAKE START DATE               
         GOTO1 ADDAY,PARAS,WORK,WORK,-7           SUBTRACT 1 WEEK               
         GOTO1 DATCON,PARAS,(0,WORK),(2,0(R3))                                  
XIT60    GOTO1 DATCON,PARAS,(2,2(R3)),(0,WORK)    TAKE END DATE                 
         GOTO1 ADDAY,PARAS,WORK,WORK,-7           SUBTRACT 1 WEEK               
         GOTO1 DATCON,PARAS,(0,WORK),(2,2(R3))                                  
         CLC   =X'CF7ECF9B',0(R3)  NOV30/03-DEC27/03                            
         BE    XITOUT                                                           
         B     XIT55                                                            
*                                                                               
*                                                                               
XITOUT   XIT1                                                                   
*                                                                               
ADCONS   DS    0F                                                               
         DC    V(GETBROAD)                                                      
         DC    V(ADDAY)                                                         
         DC    V(GETDAY)                                                        
         DC    V(DATCON)                                                        
ADCONSL  EQU   *-ADCONS                                                         
ADCONSN  EQU   (*-ADCONS)/4                                                     
         EJECT                                                                  
*                                                                               
*              DSECT FOR MOBILE                                                 
*                                                                               
MOBILD   DSECT                                                                  
WADCONS  DS    0F                                                               
GETBROAD DS    A                                                                
ADDAY    DS    A                                                                
GETDAY   DS    A                                                                
DATCON   DS    A                                                                
SYSPROFS DS    A                   A(SYSTEM PROFILE) OR A(0)                    
START    DS    CL6                                                              
END      DS    CL6                                                              
SAVEDATE DS    CL6                                                              
WORK     DS    CL6                                                              
WORK2    DS    CL6                                                              
START2   DS    CL6                                                              
PARAS    DS    6F                                                               
TYPE     DS    CL1                                                              
PNGSW    DS    CL1                                                              
MM3SW    DS    CL1                                                              
NA9SW    DS    CL1                 Y/N - EXPAND TO 18 MONTHS                    
MAX      DS    CL1                                                              
STARTDAY DS    CL1                                                              
SAVERE   DS    F                                                                
DUB      DS    D                                                                
BASE     DS    CL6                                                              
BUMP     DS    CL3                                                              
CYCLE    DS    3200C               SUPPORTS UP TO 18 YEARS                      
QTRCYCLE DS    400C                SUPPORTS 7 YEARS                             
MOBILDX  EQU   *                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'052DDMOBILE  07/14/19'                                      
         END                                                                    
