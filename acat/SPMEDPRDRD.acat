*          DATA SET SPMEDPRDRD AT LEVEL 063 AS OF 03/10/17                      
*CATALP MEDPRDRD                                                                
*=====================================================================*         
* THIS IS THE OLD VERSION OF THE PROGRAM. IT IS USED BY ANY PROGRAM   *         
* THAT DOES NOT SET THE FLAG RQPOLFLT (WHICH SAYS TO IGNORE PRODUCT   *         
* ESTIMATE FILTERS AND FILTER ONLY ON THE VALUES IN POL ESTIMATES     *         
* THAT VERSION OF THE PROGRAM IS CALLED SPMEDPRDRN.                   *         
* BOTH VERSIONS ARE LINKED INTO SPONSOR WHICH DYNAMICALLY RESETS THE  *         
* ADDRESS BASED ON THE VALUE OF RQPOLFLT.                             *         
*=====================================================================*         
         TITLE 'MEDIA SUMMARY - BUILD PRODUCT/DEMO LIST'                        
*                                                                               
* PARAMETERS                       REGISTER USAGE                               
*  1 = A(WORKC)                    RA-RC     A(WORKC)                           
*                                  R2        IO AREA                            
*                                  R3        PRODUCT BUFFER                     
*                                  R4        RECORD DSECTS                      
*                                                                               
VMDPRDRD CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 PRDRDWX-PRDRDWD,MEDPRDRD                                         
         LR    R2,RC                                                            
         USING PRDRDWD,R2                                                       
         L     RA,0(R1)                                                         
         LA    RC,2048(RA)                                                      
         LA    RC,2048(RC)                                                      
         USING SPWORKD,RA,RC                                                    
         LA    R5,EIOAREA                                                       
         ST    R5,AREC                                                          
         XC    EKSAVE,EKSAVE                                                    
         LA    R3,PTBUILD                                                       
         USING PTBUFFD,R3                                                       
         XC    PTBUILD,PTBUILD                                                  
         MVC   PRDBUFLN,=Y(PTBUFFL) SET PRDBUFF ENTRY LEN                       
         L     RE,PRDBUFF                                                       
         LA    RF,220              MAX PRDS                                     
         MH    RF,PRDBUFLN         LENGTH OF LIST ENTRY                         
         XCEF                                                                   
                                                                                
* CHECK DEMO MENU IN REQ                                                        
                                                                                
         CLI   QDEMOVRD,C'Y'                                                    
         BNE   PR10                                                             
         XC    KEY,KEY                                                          
         LA    R5,KEY                                                           
         USING DMNRECD,R5                                                       
         MVC   DMNKTYP,=X'0D26'                                                 
         MVC   DMNKAGMD,BAGYMD                                                  
         MVC   DMNKCODE,QDEMNOS    MENU CODE                                    
         GOTO1 READ                                                             
         GOTO1 GET                                                              
*                                                                               
         L     R5,AREC                                                          
         LA    R5,24(R5)                                                        
         LA    R6,PTDEMO                                                        
*                                                                               
         L     R7,VPOL50EL         POINT TO COMSCORE 50 ELEM                    
         XC    0(200,R7),0(R7)                                                  
         MVC   0(2,R7),=X'5002'    SET ELEM CODE/LEN                            
         LA    R7,2(R7)                                                         
         SR    R0,R0                                                            
         L     R8,VNONTNMS         POINT TO SAVED POL ENONTDMS                  
*                                                                               
PR2      CLI   0(R5),0                                                          
         BE    PR10                                                             
         CLI   0(R5),X'05'                                                      
         BNE   PR4                                                              
*                                                                               
         USING DMNEL05,R5                                                       
         MVC   0(3,R6),DMNRTN      MOVE DEMO TO PRDBUFF LIST                    
         LA    R6,3(R6)                                                         
*                                                                               
         CLI   DMNRTN+2,0          TEST COMSCORE DEMO                           
         BNE   PR4                 NO                                           
*                                                                               
         MVC   0(8,R7),DMNRTG      MOVE COMSCORE DEMO DESC                      
         LA    R7,9(R7)                                                         
*                                                                               
         MVC   0(8,R8),DMNRTG                                                   
         LA    R8,8(R8)                                                         
*                                                                               
         L     RE,VPOL50EL                                                      
         IC    R0,1(RE)                                                         
         AHI   R0,9                                                             
         STC   R0,1(RE)                                                         
*                                                                               
PR4      IC    R0,1(R5)                                                         
         AR    R5,R0                                                            
         B     PR2                                                              
         DROP  R5                                                               
*                                                                               
PR10     DS    0H                                                               
                                                                                
READEST  BAS   RE,GETES                                                         
*                                                                               
         CLI   RECFLAG,1           TEST ERROR                                   
         BE    EXIT                                                             
*                                                                               
         L     R4,AREC                                                          
         USING ESTHDR,R4                                                        
         MVC   PTPRDA,EKEYPRD                                                   
*                                                                               
         CLI   QDEMOVRD,C'Y'       CHECK FOR DEMO OVERRIDES                     
         BE    REST2               IF SO, DEMOS SET ABOVE                       
*                                                                               
         MVC   PTDEMO,EDEMLST                                                   
         MVC   PTWGHT,EWGTLST                                                   
                                                                                
*================================================================               
* IF REQUEST IS FOR A PRDGRP, ALL, OR POL, POL EST WILL BE READ                 
* AND WILL BE THE DEFINITIVE NONT DEMO LIST                                     
* IF REQUEST IS FOR SINGLE PRODUCT, USE ITS DEMO LIST                           
*================================================================               
                                                                                
REST2    CLC   QPRD,=C'ALL'                                                     
         BE    REST4                                                            
         CLC   QPRD,=C'POL'                                                     
         BE    REST4                                                            
         CLI   QPRD,X'F0'          PRODUCT GROUP                                
         BL    REST4X              DOING SINGLE PRD                             
*                                                                               
REST4    CLI   EPRDCD+1,X'FF'      IS THIS POL EST                              
         BNE   REST6                                                            
*                                                                               
REST4X   MVC   COMSCRSD,ECSSDTE    SAVE COMSCORE SURVEY DATES                   
         CLI   QDEMOVRD,C'Y'                                                    
         BE    SET                                                              
*                                                                               
         L     RE,VPOL50EL           POINT TO POL NONT 50EL                     
         XC    0(200,RE),0(RE)       CLEAR                                      
         L     R1,VNONTNMS           AND ENONT NAMES LIST                       
         XC    0(200,R1),0(R1)                                                  
         CLC   13(2,R4),=Y(ESTHDR2Q) TEST EST HAS NONT LIST                     
         JL    REST6                 NO                                         
*                                                                               
         MVI   0(RE),X'50'                                                      
         MVI   1(RE),182           =20*9+2                                      
         LA    RE,2(RE)                                                         
*                                                                               
         LA    RF,ENONTDMS                                                      
         LA    R0,20                                                            
*                                                                               
REST5    MVC   0(8,RE),0(RF)       MOVE DEMO                                    
         LA    RE,9(RE)            NEXT 'TO' NAME IN 50EL                       
*                                                                               
         MVC   0(8,R1),0(RF)       NEXT 'TO' NAME IN SAVED NAMES                
         LA    R1,8(R1)                                                         
*                                                                               
         LA    RF,8(RF)            NEXT 'FROM' NAME                             
         BCT   R0,REST5                                                         
                                                                                
* SET ALLOCATION TARGETS                                                        
                                                                                
REST6    LA    R7,ETRGLST                                                       
         LA    R5,PTTRGS                                                        
         LA    R0,L'PTTRGS                                                      
         XC    PTTRGS,PTTRGS                                                    
*                                                                               
REST8    DS    0H                                                               
         OC    0(3,R7),0(R7)       THIS TARGET NOT USED                         
         BZ    REST10                                                           
*                                  GET ITS POSITION IN DEMOLIST                 
         LA    RF,PTDEMO                                                        
         LA    R6,1                                                             
*                                                                               
         CLC   0(3,R7),0(RF)                                                    
         BE    *+16                                                             
         LA    RF,3(RF)                                                         
         LA    R6,1(R6)                                                         
         B     *-18                                                             
*                                                                               
         STC   R6,0(R5)            SET POSITION NO. IN PTTRGS                   
*                                                                               
REST10   DS    0H                                                               
         LA    R7,3(R7)            NEXT TARG                                    
         LA    R5,1(R5)                                                         
         BCT   R0,REST8                                                         
*                                                                               
SET      BAS   RE,GETPROD                                                       
*                                                                               
         MVC   BYTE,DMOUTBTS                                                    
         NC    BYTE,DMCB+8                                                      
         BNZ   SETERR                                                           
*                                                                               
         L     R4,AREC                                                          
         USING PRDHDR,R4                                                        
         MVC   PTPRDN,PCODE+1                                                   
         MVC   PTNAME,PNAME                                                     
         MVC   PTCLASS,PCLASS                                                   
         SR    RF,RF               FIND TABLE SLOT                              
         IC    RF,PTPRDN                                                        
         CLI   PTPRDN,X'FF'                                                     
         BNE   *+8                                                              
         LA    RF,220              POL = 220                                    
         BCTR  RF,0                                                             
         MH    RF,PRDBUFLN                                                      
         L     RE,PRDBUFF                                                       
         LA    RF,0(RE,RF)                                                      
         LH    RE,PRDBUFLN         LENGTH OF ENTRY                              
         BCTR  RE,R0                                                            
         EX    RE,*+8                                                           
         B     *+10                                                             
         MVC   0(0,RF),PTBUILD                                                  
         DROP  R4                                                               
SETERR   LA    R4,KEY                                                           
         USING ESTHDR,R4                                                        
         MVI   EKEYEST,X'FF'                                                    
         MVC   EKEYEST+1(5),EKEYEST                                             
         MVC   EKSAVE,KEY                                                       
         B     READEST                                                          
*                                                                               
EXIT     LA    RF,219              SET UNALLOCATED SLOT NAME AND DEMOS          
         MH    RF,PRDBUFLN                                                      
         L     RE,PRDBUFF                                                       
         LA    R3,0(RE,RF)         R3 POINTS TO POL ENTRY                       
         MVC   PTBUILD,0(R3)                                                    
         SH    R3,PRDBUFLN                                                      
         XC    0(28,R3),0(R3)                                                   
         MVI   PTPRDN,219                                                       
         MVC   PTPRDA,=C'***'                                                   
         MVC   PTNAME,=CL20'UNALLOCATED'                                        
         CLI   RCLANG,4            TEST FRENCH                                  
         BNE   *+10                                                             
         MVC   PTNAME,=CL20'NONALLOUES'                                         
*                                                                               
*                                  MOVE DEMOS FROM POL ENTRY                    
         MVC   PTDEMO,PTBUILD+PTDEMO-PTBUFFD                                    
         MVC   PTWGHT,PTBUILD+PTWGHT-PTBUFFD                                    
         XIT1                                                                   
         EJECT                                                                  
* READ ESTIMATES AND FILTER THEM                                                
                                                                                
GETES    NTR1                                                                   
         MVI   RECFLAG,0                                                        
         XC    KEY,KEY                                                          
         LA    R4,KEY                                                           
         USING ESTHDR,R4                                                        
*                                                                               
         MVC   EKEYAM,BAGYMD                                                    
         MVC   EKEYCLT,BCLT                                                     
         MVC   EKEYPRD,QPRD                                                     
*                                                                               
         TM    QPRD,X'F0'          PRODUCT GROUP                                
         BNO   *+10                                                             
         XC    EKEYPRD,EKEYPRD                                                  
*                                                                               
         CLC   EKEYPRD,=C'ALL'                                                  
         BNE   *+10                                                             
         XC    EKEYPRD,EKEYPRD                                                  
*                                                                               
         CLC   EKEYPRD,=C'POL'                                                  
         BNE   *+10                                                             
         XC    EKEYPRD,EKEYPRD                                                  
*                                                                               
         OC    EKSAVE,EKSAVE                                                    
         BZ    *+10                                                             
         MVC   KEY,EKSAVE                                                       
*                                                                               
GEHIGH   GOTO1 HIGH                                                             
         B     GEREC                                                            
*                                                                               
GESEQ    GOTO1 SEQ                                                              
*                                                                               
GEREC    LA    R4,KEY                                                           
         CLC   KEY(4),KEYSAVE                                                   
         BE    *+12                                                             
         MVI   RECFLAG,1                                                        
         B     GEEXIT                                                           
*                                                                               
         TM    QPRD,X'F0'                                                       
         BO    GEFEST                                                           
         CLC   QPRD,=C'ALL'                                                     
         BE    GEFEST                                                           
         CLC   QPRD,=C'POL'                                                     
         BE    GEFEST                                                           
         CLC   EKEYPRD,QPRD        CHECK PRODUCT                                
         BE    GEFEST                                                           
         MVI   RECFLAG,1                                                        
         B     GEEXIT                                                           
                                                                                
* FILTER KEY ON ESTIMATE NUMBER                                                 
                                                                                
GEFEST   CLI   EKEYEST,0                                                        
         BNE   GEFESTA                                                          
         MVI   EKEYEST,1                                                        
         CLI   BEST,0                                                           
         BE    *+10                                                             
         MVC   EKEYEST,BEST                                                     
         B     GEHIGH                                                           
GEFESTA  DS    0H                                                               
         CLI   EKEYEST+1,0                                                      
         BE    GEFEST2                                                          
GEFEST1  MVI   EKEYEST+1,X'FF'     SET NEXT PRODUCT                             
         MVC   EKEYEST+2(4),EKEYEST+1                                           
         B     GEHIGH                                                           
*                                                                               
GEFEST2  CLI   BEST,0              TEST EST=NO                                  
         BE    GEFEST5                                                          
         CLC   EKEYEST,BEST        ESTIMATE LOW                                 
         BNL   GEFEST3                                                          
         MVC   EKEYEST,BEST         YES - SET FIRST ESTIMATE                    
         B     GEHIGH                                                           
*                                                                               
GEFEST3  CLI   BESTEND,0           END ESTIMATE GIVEN                           
         BNE   GEFEST4              YES - CHECK IT                              
         CLC   EKEYEST,BEST         NO - MUST BE EQUAL                          
         BE    GEFEST5                                                          
         B     GEFEST1                                                          
*                                                                               
GEFEST4  CLC   EKEYEST,BESTEND     ESTIMATE WITHIN RANGE                        
         BH    GEFEST1              NO - NEXT PRODUCT                           
         GOTO1 GET                 BYPASS DATE FILTER ON EST SERIES             
         B     GEEXIT                                                           
*                                                                               
GEFEST5  GOTO1 GET                 KEY FILTERS PASSED - GET RECORD              
*                                                                               
         L     R4,AREC                                                          
         CLC   EEND,QSTART         DATE FILTERS                                 
         BL    GESEQ                                                            
         CLC   ESTART,QEND                                                      
         BH    GESEQ                                                            
*                                                                               
         CLC   QEST,=C'NO '                                                     
         BNE   GEEXIT                                                           
         CLC   QESTEND,SPACES                                                   
         BE    GEEXIT                                                           
         LA    R0,3                ESTIMATE FILTERS                             
         LA    RE,QESTEND                                                       
         LA    RF,EPROF                                                         
*                                                                               
GEFEST6  CLI   0(RE),C'*'                                                       
         BE    GEFEST8                                                          
         CLI   0(RE),C' '                                                       
         BE    GEFEST8                                                          
         TM    0(RE),X'40'         TEST NEGATIVE FILTERS                        
         BZ    GEFEST7              YES                                         
*                                                                               
         CLC   0(1,RE),0(RF)       POSITIVE FILTER MUST MATCH                   
         BNE   GESEQ                                                            
         B     GEFEST8                                                          
*                                                                               
GEFEST7  MVC   BYTE,0(RE)                                                       
         OI    BYTE,C' '           MAKE CHAR UPPER CASE FOR COMPARE             
         CLC   BYTE,0(RF)                                                       
         BE    GESEQ                                                            
*                                                                               
GEFEST8  LA    RE,1(RE)                                                         
         LA    RF,1(RF)                                                         
         BCT   R0,GEFEST6                                                       
*                                                                               
GEEXIT   XIT1  1                                                                
         DROP  R4                                                               
         EJECT                                                                  
* READ A PRODUCT HEADER                                                         
                                                                                
GETPROD  NTR1                                                                   
         MVC   EKSAVE,KEY                                                       
         MVC   EKSAVE,KEYSAVE                                                   
         LA    R4,KEY                                                           
         USING PRDHDR,R4                                                        
         XC    KEY,KEY                                                          
         MVC   PKEYAM,BAGYMD                                                    
         MVC   PKEYCLT,BCLT                                                     
         MVC   PKEYPRD,PTPRDA                                                   
         GOTO1 READ                                                             
*                                                                               
         MVC   BYTE,DMOUTBTS                                                    
         NC    BYTE,DMCB+8                                                      
         BNZ   GETPRODX                                                         
*                                                                               
         GOTO1 GET                                                              
         MVC   KEY(13),EKSAVE                                                   
*                                                                               
GETPRODX XIT1                                                                   
         DROP  R4                                                               
         LTORG                                                                  
         EJECT                                                                  
PRDRDWD  DSECT                                                                  
         DS    0D                                                               
EIOAREA  DS    2000C                                                            
PTBUILD  DS    XL200                                                            
RECFLAG  DS    C                   END OF RECORDS                               
EKSAVE   DS    CL13                                                             
PRDRDWX  EQU   *                                                                
*                                                                               
       ++INCLUDE SPREPPTBUF                                                     
*                                                                               
       ++INCLUDE SPGENDMN                                                       
*                                                                               
         PRINT OFF                                                              
         EJECT                                                                  
* SPREPWORKD                                                                    
       ++INCLUDE SPREPWORKD                                                     
         EJECT                                                                  
* SPGENPRD                                                                      
       ++INCLUDE SPGENPRD                                                       
         EJECT                                                                  
* SPGENEST                                                                      
       ++INCLUDE SPGENEST                                                       
         PRINT ON                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'063SPMEDPRDRD03/10/17'                                      
         END                                                                    
