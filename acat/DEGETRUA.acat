*          DATA SET DEGETRUA   AT LEVEL 080 AS OF 07/20/09                      
*CATALP DEGETRUA                                                                
         TITLE 'DEGETRUA - RADIO COUNTY AVG READ CONTROLLER'                    
***************************CHANGE LOG*******************************            
*      DATE       LVL    REASON                                                 
* 1:                                                                            
********************************************************************            
* REGISTER USAGE: IMPORTANT!                                                    
* R3: MCTY DBEXTEND LOOP CONTROL                                                
* R9: WORKING STORAGE                                                           
********************************************************************            
GETRUA   CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 RUAWORKL,**GETRUA,RA,CLEAR=YES                                   
         LR    R9,RC                                                            
         USING RUAWORKD,R9                                                      
         L     RC,0(R1)                                                         
         USING DEGETD,RC           RC=A(W/S)                                    
         USING COMFACSD,R8         R8=A(COMFACS)                                
         USING DBLOCKD,R6          R6=A(DBLOCK)                                 
************************************************************                    
* INITIALIZATION PORTION                                                        
************************************************************                    
         XC    DBFACTOR,DBFACTOR                                                
         XC    DBDIVSOR,DBDIVSOR                                                
         MVI   TOTAFLG,C'N'                                                     
************************************************************                    
* DBEXTEND PORTION                                                              
************************************************************                    
*                                                                               
*        LA    R3,MCTYTAB                                                       
*        USING MCTYD,R3                                                         
*        MVC   MCTYTAB(13),=X'06177A061F4A061F9A1D1F9BFF'                       
*        MVI   MCTYFLG,C'Y'                                                     
*        B     MCTYNO                                                           
*                                                                               
         L     RE,DBEXTEND         CHECK FOR MULIPLE STATE/COUNTY               
         LTR   RE,RE                                                            
         BZ    MCTYNO                                                           
         USING DBCNTYD,RE                                                       
         CLC   DBCNTYID,=C'MCTY'   THIS IS THE ONE WE WANT                      
         BE    *+12                                                             
         L     RE,DBCNTNXT                                                      
         B     *-20                                                             
*                                                                               
         LA    R3,DBCNTSTL         LIST OF COUNTIES                             
         USING MCTYD,R3                                                         
         MVI   MCTYFLG,C'Y'                                                     
*                                                                               
MCTYNO   OC    DBSELSTA,DBSELSTA                                                
         BNZ   TOTANO                                                           
         MVI   TOTAFLG,C'Y'                                                     
*                                                                               
TOTANO   DS    0H                                                               
         DROP  RE                                                               
*                                                                               
************************************************************                    
* ACCESS PORTION                                                                
************************************************************                    
         CLI   TOTAFLG,C'Y'        SOMEHOW COUNTY LEVEL LOOKUP                  
         BE    GETRUA1             GETS ERROR X'02'                             
*                                                                               
         ZIC   R1,DBFUNCT          SAVE THE REQUESTED FUNCTION                  
         MVI   DBFUNCT,DBTSTACS    SET FOR VALIDATE                             
         GOTO1 ATSTACS             CALL TO VALIDATE ACCESS                      
         STC   R1,DBFUNCT          RESTORE ORIGINAL REQUEST                     
         CLI   DBERROR,0           EXIT IF NOT VALID                            
         BNE   GETX                                                             
*                                                                               
GETRUA1  OC    DBSELBK,DBSELBK     BOOK SET? WE ARE NOT DOING LATEST            
         BZ    GETRUAX             RETURN NOT FOUND                             
GETRUA2  DS    0H                                                               
         LA    R1,DBSELBK                                                       
         L     RF,AGETCTRL                                                      
         OC    ACONHDR,ACONHDR     TEST IF A(CONTROL TABLE) SET                 
         BNZ   *+6                                                              
         BASR  RE,RF               NO - GET AGENCY CONTROLS                     
*                                                                               
************************************************************                    
* DIRECTORY PORTION (MAJOR KEYS ONLY)                                           
* PASSIVES ARE HANDLED IN MAIN DEMAND MODULE (DEGET)                            
* MINOR KEY:  TRANSLATE REQUEST INTO DAYPART ID NUMBER                          
************************************************************                    
GETRUA10 DS    0H                                                               
         MVI   DBINTMED,C'U'                                                    
*                                                                               
         OC    DBSELDAY(5),DBSELDAY                                             
         BNZ   GETRUA25                                                         
         MVI   DBERROR,EOF                                                      
         B     GETX                                                             
*                                                                               
************************************************************                    
* R-POINTER PORTION: RUA RECORDS. (MAIN DEMO RECORDS)                           
************************************************************                    
GETRUA25 LA    R2,DBKEY            BUILD KEY OF DEMO RECORD                     
         USING DRKEY,R2                                                         
         XC    DRKMAJOR,DRKMAJOR                                                
         MVI   DRCODE,DRCODEQU                                                  
         MVC   DRMEDIA,DBINTMED                                                 
         MVC   DRSRC,DBACTSRC                                                   
         MVC   DRBTYP,DBBTYPE                                                   
         MVC   DRBOOK,DBSELBK                                                   
         MVC   DRKMKT,DBSELMK                                                   
         XC    DRBOOK,=X'FFFF'                                                  
         MVC   DRSTAT,DBSELSTA                                                  
*                                                                               
         CLI   MCTYFLG,C'Y'                                                     
         BNE   *+16                                                             
         MVC   DRBTYP,MCTYSTA                                                   
         MVC   DRKMKT,MCTYCNT                                                   
*                                                                               
         CLI   TOTAFLG,C'Y'                                                     
         BNE   GETRUA30                                                         
*                                                                               
         ZICM  R1,MCTYCNT,2                                                     
         CVD   R1,DUB                                                           
         UNPK  DRSTAT(4),DUB                                                    
         OI    DRSTAT+3,X'F0'                                                   
         MVI   DRSTAT+4,C'A'                                                    
*                                                                               
GETRUA30 MVI   IOFLAG,DIR+HIGH+DEM READ HIGH/DIRECTORY                          
         GOTO1 AIO,IOFLAG                                                       
         BL    GETX                                                             
*                                                                               
         CLC   DBKEY(DRBTYP-DRKEY),KEYSAVE                                      
         BE    GETRUA40                                                         
         MVI   DBERROR,NOTFOUND                                                 
         B     GETRUA60                                                         
*                                                                               
************************************************************                    
* FILE PORTION (MINOR KEYS)                                                     
*        -  DAYPART ID WILL NEED TO BE SET                                      
************************************************************                    
GETRUA40 BAS   RE,TRNDPT                                                        
         BE    *+12                                                             
         MVI   DBERROR,NOTFOUND                                                 
         B     GETX                                                             
*                                                                               
         XC    DBFACTOR,DBFACTOR                                                
         MVC   DBMINKEY(1),WORKDPT WORKDPT: DAYPART ID                          
         MVI   IOFLAG,FILE+HIGH+DEM                                             
GETRUA50 GOTO1 AIO,IOFLAG                                                       
         L     R2,DBAREC                                                        
         TM    DBERROR,EOF                                                      
         BO    GETRUA60                                                         
*                                                                               
*********************************************************************           
* RETURN RECORD TO USER                                                         
*********************************************************************           
GETRUA55 MVI   DBMODE,DBMNEXT      TRY TO RETURN TO USER                        
         MVI   PASSFLG,C'Y'                                                     
*                                                                               
         LA    RE,DRFRSTEL                                                      
         ST    RE,DBAQUART                                                      
*                                                                               
         GOTO1 RUAGETEL,DMCB,CYRCODEQ   X'07'                                   
         ZIC   R1,DMCB+12                                                       
         CLI   DMCB+12,0                                                        
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         L     R1,DMCB+12          TOTAL QH IN DAYPART                          
         USING CYRELEM,R1                                                       
         SR    RE,RE                                                            
         ICM   RE,3,CYRDQHDR       UPDATE FACTOR AND DIVISOR                    
         STH   RE,DBFACTOR                                                      
         AH    RE,DBDIVSOR                                                      
         STH   RE,DBDIVSOR                                                      
*                                                                               
         BAS   RE,GETX                                                          
*                                                                               
************************************************************                    
* LOOP CONTROL PORTION                                                          
*        - USING THE MULTIPLE COUNTY DBEXTEND                                   
************************************************************                    
GETRUA60 CLI   MCTYFLG,C'Y'                                                     
         BNE   GETRUAX                                                          
         AHI   R3,MCTYLEN                                                       
         CLI   MCTYSTA,MCTYEND                                                  
         BE    GETRUAX                                                          
         B     GETRUA25                                                         
*                                                                               
GETRUAX  CLI   PASSFLG,C'Y'        AT LEAST 1 ENTRY FOUND                       
         BNE   *+12                                                             
         MVI   DBERROR,EOF                                                      
         B     *+8                                                              
         MVI   DBERROR,NOTFOUND                                                 
         B     GETXX                                                            
************************************************************                    
* EXIT PORTION: EXIT TO HOOK, REPORT ERROR, END PROCEDURE.                      
************************************************************                    
GETX     CLI   DBERROR,0           TEST IF ERROR SET                            
         BE    *+14                                                             
         XC    DBAQUART,DBAQUART   YES - CLEAR QTR HOUR ADDRESS                 
         MVI   DBMODE,DBMFRST      AND RESET SEQUENTIAL MODE                    
         MVC   DBLSTFNC,DBFUNCT    EQUATE LAST FUNCTION TO THIS                 
*                                                                               
         OC    AUSERRTN,AUSERRTN   TEST IF USER HOOK SUPPLIED                   
         BZ    GETXX               NO - RETURN TO CALLER                        
         CLI   DBERROR,0           TEST IF ERROR SET                            
         BNE   GETXX               YES - RETURN TO CALLER                       
*                                                                               
         CLI   DBMODE,DBMNEXT      TEST IF SEQUENTIAL MODE                      
         BE    *+8                                                              
         LA    RE,GETXX            NO - SET RETURN TO EXIT                      
*                                  SAVE REGS & GO TO USER HOOK                  
         NTR1                                                                   
         L     RF,AUSERRTN                                                      
         L     RE,ROOTRD                                                        
         LR    R0,RE               R0=A(CALLER'S RD)                            
         LM    R1,RC,24(RE)        R1-RC=CALLERS REGISTERS                      
         BASR  RE,RF               GO TO USER HOOK                              
*                                                                               
GETXX    XIT1  ,                   EXIT TO CALLER OR DEMAND                     
         EJECT                                                                  
* LITERALS ETC.                                                                 
*                                                                               
         LTORG                                                                  
         DROP  R2                                                               
************************************************************                    
TRNDPT   NTR1                                                                   
         LA    R1,DPTTAB                                                        
TD05     CLC   DBSELDAY(5),0(R1)                                                
         BNE   TD10                                                             
         MVC   WORKDPT,5(R1)                                                    
         B     TDXTRUE                                                          
TD10     LA    R1,L'DPTTAB(R1)                                                  
         CLI   0(R1),X'FF'                                                      
         BE    TDXFALSE                                                         
         B     TD05                                                             
*                                                                               
TDXTRUE  CR    R1,R1                                                            
         B     TDX                                                              
TDXFALSE LA    R1,1(RE)                                                         
         CR    R1,RE                                                            
TDX      XIT1                                                                   
************************************************************                    
RUAGETEL NTR1                                                                   
         MVC   BYTE,DMCB+3                                                      
         GOTO1 CHELLO,DMCB,(C'G',=C'DEMFIL'),(BYTE,DBAREC),0                    
         XIT1                                                                   
************************************************************                    
*                                                                               
         SPACE 1                                                                
         DS    0H                                                               
EFFS     DC    X'FFFFFFFF'                                                      
ARZERO   DC    16F'0'                                                           
*                                                                               
DPTTAB   DS    0XL(6)                                                           
         DC    X'7F',AL2(0600,2400),AL1(MSU612A)                                
         DC    X'7C',AL2(0600,1900),AL1(MFR6A7P)                                
         DC    X'FF'                                                            
*                                                                               
         DROP  RA,RB                                                            
         LTORG                                                                  
*********************************************************************           
         EJECT                                                                  
*                                                                               
       ++INCLUDE DEGETD                                                         
         PRINT OFF                                                              
       ++INCLUDE FASSB                                                          
       ++INCLUDE FASYSFAC                                                       
       ++INCLUDE DEDBEXTRAD                                                     
       ++INCLUDE DDMASTD                                                        
         SPACE 1                                                                
         PRINT ON                                                               
         TITLE 'DEGETRUA - RADIO COUNTY AVG READ CONTROLLER (LOCAL WORK+        
               AREA)'                                                           
***********************************************************************         
*====================== GETRUA'S LOCAL WORK AREA =====================*         
*                                                                               
RUAWORKD DSECT                                                                  
*                                                                               
WORKDPT  DS    XL1                                                              
TEMPDUR  DS    XL2                 USED MORE OR LESS FOR COMPARES               
TEMPMIN  DS    H                   USED MORE OR LESS FOR COMPARES               
*                                                                               
SVMED    DS    XL1                 SAVE THE MEDIA                               
SVDTM    DS    XL5                 SAVE DBSELDAY AND TIME                       
*                                                                               
MCTYFLG  DS    XL1                                                              
PASSFLG  DS    XL1                 CLEAR DBERROR IF PASSFLG = Y                 
TOTAFLG  DS    XL1                 LOOKING FOR COUNTY TOTALS                    
MCTYTAB  DS    30XL3                                                            
*                                                                               
RUAWORKX EQU   *                                                                
RUAWORKL EQU   RUAWORKX-RUAWORKD                                                
*                                                                               
*                                                                               
MSU612A  EQU   1                   M-SU 6A-12A                                  
MFR6A7P  EQU   2                   M-FR 6A-7P                                   
*                                                                               
MCTYD    DSECT                     MULTIPLE COUNTY DBEXTEND TABLE               
MCTYSTA  DS    XL1                 STATE                                        
MCTYCNT  DS    XL2                 COUNTY                                       
MCTYEND  EQU   X'FF'                                                            
MCTYLEN  EQU   *-MCTYD                                                          
*                                                                               
***********************************************************************         
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'080DEGETRUA  07/20/09'                                      
         END                                                                    
