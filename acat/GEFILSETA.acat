*          DATA SET GEFILSETA  AT LEVEL 014 AS OF 09/04/97                      
*CATALP GEFILSET                                                                
GEFILSET TITLE '- GENERAL INITIALIZATION'                                       
***********************************************************************         
* NTRY: R9=A(GLOBAL W/S)                                              *         
* EXIT: CC=LOW IF ERROR                                               *         
*       CC=HIGH IF FIRST TIME CALL                                    *         
*       CC=EQUAL IF NOT FIRST TIME AND NO ERROR                       *         
***********************************************************************         
GEFILSET CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 0,GEFILSET,RR=RE                                                 
         USING WORKD,R9            R9=A(GLOBAL WORKING STORAGE)                 
         ST    RE,BORELO                                                        
         USING IWORKD,BOWORK1      LOCAL WORKING STORAGE                        
         MVC   IPARMS,0(R1)        SAVE INCOMING PARAMS                         
         SPACE 1                                                                
***********************************************************************         
* INITIALISE ADDRESSES                                                *         
***********************************************************************         
         SPACE 1                                                                
         ST    RE,BCRELO                                                        
         L     RF,4(RD)            SAVE ROOT'S RD VALUE                         
         ST    RF,BCSVRD                                                        
         L     RE,64(RF)           SAVE ROOT'S BASE REGISTER VALUES             
         ST    RE,BCBASER1                                                      
         LH    R0,=Y(4096)                                                      
         AR    RE,R0                                                            
         ST    RE,BCBASER2                                                      
         AR    RE,R0                                                            
         ST    RE,BCBASER3                                                      
         AR    RE,R0                                                            
         ST    RE,BCBASER4                                                      
*                                                                               
         L     RF,4(RF)            GET ROOT'S ORIGINAL R1 VALUE                 
         L     R1,24(RF)                                                        
         MVC   CUABIN,0(R1)        COMPANY CODE                                 
         MVC   AINP,00(R1)         A(TIOB)                                      
         MVC   ATWA,04(R1)         A(TWA)                                       
         MVC   ASYS,08(R1)         A(SYSTEM FACILITY LIST)                      
         MVC   ATIA,12(R1)         A(TIA)                                       
         MVC   ACOM,16(R1)         A(COMMON FACILITY LIST)                      
         L     R1,20(R1)           R1=A(FACPAK EXTRA INFORMATION)               
         MVC   CUCTRY,1(R1)        SET AGENCY COUNTRY                           
         MVC   CULANG,3(R1)                                                     
*                                                                               
         L     RA,ATWA                                                          
         USING TWAD,RA                                                          
*                                                                               
         L     R1,ACOM                                                          
         USING COMFACSD,R1                                                      
         MVC   VDMGR,CDATAMGR                                                   
         MVC   VCOLY,CCALLOV                                                    
         MVC   VGETMSG,CGETMSG                                                  
         MVC   VGETTXT,CGETTXT                                                  
         MVC   VHELLO,CHELLO                                                    
         MVC   VSCANNER,CSCANNER                                                
         MVC   VUNSCAN,CUNSCAN                                                  
         MVC   VHEXIN,CHEXIN                                                    
         MVC   VHEXOUT,CHEXOUT                                                  
         MVC   VCASHVAL,CCASHVAL                                                
         MVC   VDATVAL,CDATVAL                                                  
         MVC   VDATCON,CDATCON                                                  
         MVC   VSCUNKEY,CSCUNKEY                                                
         MVC   VADDAY,CADDAY                                                    
         MVC   VPERVERT,CPERVERT                                                
         MVC   VGETFACT,CGETFACT                                                
         MVC   VGETDAY,CGETDAY                                                  
         MVC   VPERVAL,CPERVAL                                                  
         MVC   VMINIO,CMINIO                                                    
         MVC   VPARSNIP,CPARSNIP                                                
         MVC   VSWITCH,CSWITCH                                                  
         MVC   CUREDIT,CCUREDIT                                                 
         MVC   VDICTAT,CDICTATE                                                 
         MVC   VGETPROF,CGETPROF                                                
         MVC   VEDITOR,CEDITOR                                                  
         MVC   VSECRET,CSECRET                                                  
         MVC   VGETHELP,CGETHELP                                                
*&&UK*&& MVC   VCONVERT,CCONVERT                                                
*&&UK*&& MVC   VPRORATA,CPRORATA                                                
         MVC   VGETCUR,CGETCUR                                                  
         MVC   VBLDCUR,CBLDCUR                                                  
         MVC   VREQTWA,CREQTWA                                                  
         SPACE 1                                                                
***********************************************************************         
* GRAB ADDRESS SPACE FOR NEW FILE CONTROLLER W/S                      *         
***********************************************************************         
         SPACE 1                                                                
         TM    BCINDS1,BCIGRAB                                                  
         BO    ADDS02                                                           
         ICM   R1,15,=AL4(GWORKL)  STORAGE LENGTH TO ACQUIRE                    
         L     RE,4(RD)            SET NEW WORKING-STORAGE LINKAGES             
         LR    R8,RE                                                            
         ST    R8,AGWORK                                                        
         USING GWORKD,R8           NEW FILE CONTROLLER W/S ON END               
         AR    RD,R1                                                            
         LR    RF,RE                                                            
         AR    RF,R1                                                            
         MVC   0(72,RF),0(RE)                                                   
         ST    RF,4(RD)                                                         
         ST    RF,BCSVRD           MODIFY SAVED REGISTER D                      
         ST    RD,8(RF)                                                         
         L     RE,4(RF)                                                         
         ST    RF,8(RE)                                                         
*                                                                               
         LR    R0,R8               CLEAR STORAGE TO B. ZEROS                    
         SR    RE,RE                                                            
         SR    RF,RF                                                            
         MVCL  R0,RE                                                            
*                                                                               
ADDS02   OI    BCINDS1,BCIGRAB     INITIALISED W/S                              
         SPACE 1                                                                
***********************************************************************         
* INITIALISE ADDRESSES                                                *         
***********************************************************************         
         SPACE 1                                                                
         L     R1,ACOM             SET UP AG_____ ADDRESSES                     
         USING COMFACSD,R1                                                      
         MVC   AGREPORT,CREPORT                                                 
         MVC   AGREQTWA,CREQTWA                                                 
         MVC   AGETFLD,CGETFLD                                                  
         DROP  R1                                                               
*                                                                               
         ICM   R0,14,=X'D9000A'                                                 
         ICM   R0,1,=AL1(QSPOON)                                                
         GOTOX VCOLY,BCPARM,0,(R0)                                              
         MVC   AGSPOON,0(R1)       VSPOON                                       
*                                                                               
         ICM   R0,1,=AL1(QTSAR)                                                 
         GOTOX (RF),(R1),0,(R0)                                                 
         MVC   AGTSAR,0(R1)        VTSAR                                        
*                                                                               
         IC    R0,=AL1(QREPORT)                                                 
         GOTOX (RF),(R1),0,(R0)                                                 
         MVC   VREPORT,0(R1)       VREPORT                                      
*                                                                               
         IC    R0,=AL1(QGETIDS)                                                 
         GOTOX (RF),(R1),0,(R0)                                                 
         MVC   VGETIDS,0(R1)       VGETIDS                                      
*                                                                               
         ICM   R0,15,=X'D9000AB0'  T00AB0                                       
         GOTOX (RF),(R1),0,(R0)                                                 
         MVC   AGROUTS,0(R1)       A(GENERAL ROUTINES)                          
*                                                                               
         IC    R0,=X'B1'           T00AB1                                       
         GOTOX (RF),(R1),0,(R0)                                                 
         MVC   AGEN,0(R1)          A(GENERAL OBJECTS)                           
*                                                                               
         IC    R0,=X'B2'           T00AB2                                       
         GOTOX (RF),(R1),0,(R0)                                                 
         MVC   AGEN2,0(R1)         A(GENERAL OBJECTS II)                        
*                                                                               
         IC    R0,=X'B3'           T00AB3                                       
         GOTOX (RF),(R1),0,(R0)                                                 
         MVC   AGENLST,0(R1)       A(LIST OBJECTS)                              
*                                                                               
         IC    R0,=X'B5'           T00AB5                                       
         GOTOX (RF),(R1),0,(R0)                                                 
         MVC   AGENRPT,0(R1)       A(REPORT OBJECTS)                            
*                                                                               
         GOTOX (RF),(R1),(X'04',0),0                                            
         MVC   APRG,0(R1)          A(PROGRAM OBJECTS)                           
*                                                                               
         ICM   R0,15,=X'D9000A6C'                                               
         GOTOX (RF),(R1),0,(R0)                                                 
         CLI   4(R1),X'FF'         SET ACGEN6C ROUTINE ADDRESSES                
         BNE   *+6                                                              
         DC    H'0'                                                             
*                                                                               
         L     RF,0(R1)                                                         
         LA    R0,AROUT6CN                                                      
         XR    RE,RE                                                            
         LA    R1,AROUT6C                                                       
INITAD02 ST    RF,0(R1)                                                         
         STC   RE,0(R1)                                                         
         LA    RE,1(RE)                                                         
         LA    R1,4(R1)                                                         
         BCT   R0,INITAD02                                                      
*                                                                               
         L     RF,ATWA                                                          
         CLI   TWASESNL-TWAD(RF),0 SET NESTING LEVEL IF FIRST TIME              
         BNE   *+8                                                              
         MVI   TWASESNL-TWAD(RF),1                                              
*                                                                               
         L     R1,=A(NFITAB)       SET IO ROUTINE FILE TABLES                   
         A     R1,BORELO                                                        
         ST    R1,AXFILTAB                                                      
*                                                                               
         L     R1,=A(CMDTAB)       SET IO ROUTINE COMMAND TABLES                
         A     R1,BORELO                                                        
         ST    R1,AXCMDTAB                                                      
*                                                                               
         L     R1,=A(ANAWS)        SET SUNDRY STORAGE ADDRESSES                 
         A     R1,BORELO                                                        
         LA    R0,ANAWSN                                                        
INITAD04 ICM   RE,15,0(R1)         RE=AL2(AREA),AL2(ADDRESS)                    
         SRDL  RE,16                                                            
         SRL   RF,16                                                            
         LA    RE,WORKD(RE)                                                     
         LA    RF,WORKD(RF)                                                     
         STCM  RE,15,0(RF)         SET AREA ADDRESS                             
         LA    R1,L'ANAWS(R1)                                                   
         BCT   R0,INITAD04                                                      
*                                                                               
         LH    RF,=Y(IOAREC+L'IODA+L'IOWORK-GWORKD)                             
         LA    RF,GWORKD(RF)                                                    
         ST    RF,AIOREC           A(CONTROLLER IO AREA)                        
*                                                                               
         LH    RF,=Y(FDRADDR-GWORKD)                                            
         LA    RF,GWORKD(RF)                                                    
         ST    RF,AFDRADDR         A(FDREL SAVE AREA)                           
*                                                                               
         LH    RF,=Y(TLSTREC-GWORKD)                                            
         LA    RF,GWORKD(RF)                                                    
         ST    RF,ATLST            A(TSAR RECORD AREA)                          
*                                                                               
         LH    RF,=Y(FLTELSV-TWAD)                                              
         A     RF,ATWA                                                          
         ST    RF,AFLTELSV         A(FLTEL SAVE AREA)                           
*                                                                               
         LH    RF,=Y(KYFLDS-TWAD)                                               
         A     RF,ATWA                                                          
         ST    RF,AKYFLD           A(KEY FIELD SAVE AREA)                       
*                                                                               
         LH    RF,=Y(TWUSER-TWAD)                                               
         A     RF,ATWA                                                          
         ST    RF,AUSEBLK          A(USER SAVE AREA)                            
*                                                                               
         LH    RF,=Y(TWSAVE-TWAD)                                               
         A     RF,ATWA                                                          
         ST    RF,ASVEBLK          A(NTRSES SAVE/RESTORE AREA)                  
*                                                                               
         LH    RF,=Y(TWDICT-TWAD)                                               
         A     RF,ATWA                                                          
         ST    RF,ADICBLK          A(DICTIONARY SAVE AREA)                      
*                                                                               
         LH    RF,=Y(TWSECBLK-TWAD)                                             
         A     RF,ATWA                                                          
         ST    RF,ASECBLK          A(SECURITY BLOCK)                            
         SPACE 2                                                                
***********************************************************************         
* INITIALISE GENERAL VALUES IN GEGENWORK                              *         
***********************************************************************         
         SPACE 1                                                                
*                                                                               
*&&UK*&& MVC   BCSYSTEM,=C'UK'     SET DEFAULT SYSTEM                           
*&&US*&& MVC   BCSYSTEM,=C'US'                                                  
*                                                                               
         MVI   BCEFFS,FF           SET X'FF'`S                                  
         MVC   BCEFFS+1(L'BCEFFS-1),BCEFFS                                      
         MVI   BCSPACES,C' '       SET C' '`S                                   
         MVC   BCSPACES+1(L'BCSPACES-1),BCSPACES                                
         ZAP   BCPZERO,=P'0'                                                    
*                                                                               
         MVC   CUACCS,TWAACCS      LIMIT ACCESS                                 
         MVC   CUUSER,TWAUSRID     USER-ID NUMBER                               
         MVC   CUAUTH,TWAAUTH      AUTHORIZATION CODE                           
         MVC   CUAALF,TWAAGY       AGENCY ALPHA-ID                              
*                                                                               
         GOTOX VGETFACT,BCPARM,0                                                
         L     R2,0(R1)                                                         
         USING FACTSD,R2           R1=A(SYSTEM DEFINITION BLOCK)                
         L     RF,FASYSLST                                                      
         ST    RF,ASYSLST                                                       
         MVC   ACTRY,FAACTRY       SET A(COUNTRY & LANGUAGE TABLES)             
         MVC   ALANG,FAALANG                                                    
*                                                                               
         MVC   ASTIME,FATIME       SYSTEM TIME (STANDARD FORMAT)                
         MVC   ASSYSN,FASYS        SYSTEM NUMBER                                
         MVC   ASSYSO,FAOVSYS      SYSTEM NUMBER (FOR CALLOV)                   
         MVC   ASSIN,FASIN         SYSTEM INPUT NUMBER                          
         MVC   ASIOASTR,FAIOASTR   SYSTEM EXTRA AREA ADDRESS                    
         MVC   ASIOALEN,FAIOALEN   SYSTEM EXTRA AREA LENGTH                     
         MVC   CUTSYM,FASYM                                                     
         MVC   ASEDAT,FADATE       SYSTEM DATES (VARIOUS FORMATS)               
         MVC   ASBDAT,FADATEB                                                   
*                                                                               
         MVC   BCSWSYSN,ASSYSO     SET SWITCHED SYSTEM INFO                     
         MVC   BCSWSYSC,ASSYSO                                                  
         MVC   BCSWSYSP,ASSYSO                                                  
         MVC   GCSWSYSN,ASSYSO     INITIALISE SWITCHED SYSTEM INFO              
         MVC   GCSWSYSC,ASSYSO                                                  
         MVC   GCSWSYSP,ASSYSO                                                  
*                                                                               
         TM    FATFLAG,1           TEST OFFLINE OR ONLINE                       
         BNZ   *+12                                                             
         MVI   ASONOFF,ASON        SET ONLINE SWITCH                            
         B     INITON                                                           
         EJECT                                                                  
***********************************************************************         
* OFFLINE INITIALISATION FOR REPORTING                                *         
***********************************************************************         
         SPACE 1                                                                
         MVI   ASONOFF,ASOFF       OFFLINE                                      
         MVI   CUSTAT,CUSDDS                                                    
         MVC   CUPASS,FAPASSWD     SET CONNECTED PASSWORD                       
         ZAP   ASTIME,=PL4'0'      CLEAR TIME                                   
*                                                                               
         MVC   TWMODE,TWAOMODE     COPY REDEFINED INFO FROM TWA                 
         MVC   TWBUF,TWAOVBUF                                                   
         MVC   TWSRT,TWAOVSRT                                                   
         MVC   TWPCH,TWAOVPCH                                                   
         MVC   TWWRK,TWAOVWRK                                                   
         MVC   TWBOX,TWAOVBOX                                                   
         MVC   TWMASTC,TWAOVMST                                                 
*                                                                               
         ICM   R0,14,=X'D9000A'                                                 
         ICM   R0,1,=AL1(QTSAROFF)                                              
         GOTOX VCOLY,BCPARM,0,(R0)                                              
         MVC   AGTSAR,0(R1)        VTSAR REDEFINED TO TSAROFF                   
*                                                                               
         ICM   R3,15,TWAOVADC      ADCONS FROM SPOOF                            
         USING TWADCOND,R3                                                      
         ICM   RF,15,TTSABLK       GET A(MY TSAR BLOCK)                         
         BNZ   INOF01              ALREADY HAVE ONE                             
*                                                                               
         XR    R0,R0                                                            
         ICM   R0,3,GCTSPAGS       OVERRIDE SIZE OF BUFFER (4K PAGES)           
         BNZ   *+8                                                              
         LA    R0,128              STANDARD SIZE IS APP. 500K                   
         SLL   R0,12                                                            
         GETMAIN RU,LV=(0),LOC=(ANY,ANY),BNDRY=PAGE                             
         LTR   RF,RF                                                            
         BZ    *+6                                                              
         DC    H'0'                                                             
                                                                                
         STCM  R1,15,TTSABLK                                                    
         STCM  R1,15,ATSOBUF                                                    
         STCM  R0,15,LTSOBUF                                                    
         DROP  R3                                                               
*                                                                               
INOF01   L     RF,ASECBLK          RESTORE TWUSER FROM SECBLK                   
         MVC   TWAUSER,0(RF)                                                    
*                                                                               
         L     R1,TWMASTC          SET INFO FROM MASTC                          
         USING MASTD,R1                                                         
         MVC   CUUSER,MCORIGID                                                  
         MVC   CULANG,MCLANG                                                    
         MVC   CUCTRY,MCCTRY                                                    
         MVC   BCAUTL,MCUTL                                                     
         MVC   ASEDAT,MCDATE                                                    
         DROP  R1                                                               
*                                                                               
         GOTOX VDATCON,BOPARM,(4,ASEDAT),(3,ASBDAT)                             
*                                                                               
         LA    R1,IOKEY            READ ID RECORD TO BUILD SWCH TABLE           
         USING CTIREC,R1                                                        
         XC    CTIKEY,CTIKEY                                                    
         MVI   CTIKTYP,CTIKTYPQ                                                 
         MVC   CTIKNUM,CUUSER                                                   
         L     RF,AIO1                                                          
         SH    RF,=Y(L'IOWORK)                                                  
*                                                                               
         GOTO1 VDMGR,BODMCB,=C'DMREAD',=C'CTFILE',IOKEY,AIO1,(RF)               
         CLI   8(R1),0                                                          
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         XC    BODMCB(24),BODMCB                                                
*                                                                               
         L     R1,AIO1                                                          
         LA    R1,CTIDATA          LOOK FOR SYSTEM ELEMENTS                     
         USING CTSYSD,R1                                                        
         XR    RF,RF                                                            
         L     RE,ASWSTAB                                                       
         USING SYSSWTAB,RE                                                      
INOF02   CLI   CTSYSEL,0           END OF RECORD?                               
         BE    INOF06                                                           
         CLI   CTSYSEL,CTSYSELQ                                                 
         BNE   INOF04                                                           
*                                                                               
         MVC   SYSSWSYS,CTSYSSE                                                 
         MVC   SYSSWSOV,CTSYSNUM                                                
         MVC   SYSSWAGB,CTSYSAGB                                                
         MVC   SYSSWACS,CTSYSLMT                                                
         CLC   CTSYSSE,GCSWSYSN    FLAG CURRENT OPEN SYSTEM                     
         BNE   *+8                                                              
         OI    SYSSINDS,SYSSIOPN                                                
         LA    RE,SYSSWLEN(RE)                                                  
*                                                                               
INOF04   ICM   RF,1,CTSYSLEN                                                    
         LA    R1,0(RF,R1)                                                      
         B     INOF02                                                           
         DROP  R1,RE                                                            
*                                                                               
INOF06   B     INITGV              NOW INITIALISE ALL ELSE                      
         SPACE 2                                                                
***********************************************************************         
* INITIALISE ONLINE ONLY VALUES                                       *         
***********************************************************************         
         SPACE 1                                                                
INITON   TM    TWAOFFC,C'*'        TEST DDS OFFICE CODE                         
         BNO   *+8                                                              
         MVI   CUSTAT,CUSDDS       SET DDS STATUS INDICATOR                     
         OC    CUPASS,FAPASSWD     TEST CONNECTED WITH PASSWORD                 
         BNZ   *+6                                                              
*&&UK*&& DC    H'0'                                                             
*&&US*&& XR    R0,R0               FUCKERS                                      
         TM    FATFLAG,X'08'       YES - TEST PERSONAL PASSWORD                 
         BNZ   *+6                                                              
*&&UK*&& DC    H'0'                                                             
*&&US*&& XR    R0,R0               FUCKERS                                      
         OI    CUSTAT,CUSPER                                                    
*                                                                               
         MVC   BCPARM(4),BCEFFS    SET A(UTL ENTRY)                             
         GOTOX VSWITCH,BCPARM                                                   
         MVC   BCAUTL,0(R1)                                                     
         L     RF,BCAUTL                                                        
         USING UTLD,RF                                                          
         MVC   CUTERM,TCFN                                                      
         XC    ASTEST,ASTEST                                                    
         MVC   BOBYTE1,TTEST                                                    
         NI    BOBYTE1,TTESTLVL                                                 
         BZ    *+14                                                             
         MVI   ASTEST,X'C0'                                                     
         OC    ASTEST,BOBYTE1      SET TEST LEVEL                               
*                                                                               
         XC    BOBYTE1,BOBYTE1                                                  
         MVC   BCPARM(4),BCEFFS    SET A(SYSFACS)                               
         MVI   BCPARM,X'FE'                                                     
         GOTOX VSWITCH,BCPARM                                                   
         MVC   ASYSFAC,0(R1)                                                    
*                                                                               
         GOTOX VGETFACT,BCPARM,(X'80',0),F#TCBD                                 
         L     R1,0(R1)                                                         
         USING F@TCBD,R1                                                        
         SR    R0,R0                                                            
         ICM   R0,1,F@BSWNUM       R0=N'ENTRIES IN TCBSWTAB                     
         BZ    INON04                                                           
*                                                                               
         CLM   R0,1,=AL1(SYSSWMAX)                                              
         BNH   *+8                                                              
         LA    R0,SYSSWMAX                                                      
         LA    R1,F@BSWTAB                                                      
         USING F@BSWTAB,R1         R1=A(TCB SWITCH TABLE)                       
         L     RE,ASWSTAB                                                       
         USING SYSSWTAB,RE         RE=A(LOCAL SWITCH TABLE)                     
INON02   MVC   SYSSWSYS,F@BSWSYS                                                
         MVC   SYSSWSOV,F@BSWSOV                                                
         MVC   SYSSWAGB,F@BSWAGB                                                
         MVC   SYSSWACS,F@BSWACS                                                
         MVC   SYSSWAC2,F@BSWAC2                                                
         LA    R1,F@BSWLEN(R1)     BUMP TO NEXT                                 
         LA    RE,SYSSWLEN(RE)                                                  
         BCT   R0,INON02           DO FOR ALL ENTRIES                           
         DROP  R1,RE                                                            
*                                                                               
INON04   LA    RF,L'TWSECBLK                                                    
         L     R2,ASECBLK                                                       
         USING SECD,R2                                                          
         GOTOX VSECRET,BCPARM,('SECPINIT',SECD),(RF)                            
         ORG   *-2                                                              
         TM    SECINDS,SECIINIT    TEST SECRET BLOCK INITIALISED                
         BNZ   *+12                                                             
         BASR  RE,RF                                                            
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         MVC   CSBPID,SECPID       EXTRACT USER PUBLIC-ID                       
         DROP  R2                                                               
*                                                                               
         LA    R2,IOKEY                                                         
         USING CTIREC,R2           BUILD KEY OF ID RECORD                       
         XC    CTIKEY,CTIKEY                                                    
         MVI   CTIKTYP,CTIKTYPQ                                                 
         MVC   CTIKNUM,CUUSER                                                   
         L     R1,=AL4(XORD+XOCONFIL+XIO1)                                      
         GOTOX ('XIO',AGROUTS)                                                  
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         TM    TWAINDS1,TW1INIT    TEST VALUES SAVED LAST TIME                  
         BO    INON14                                                           
         L     R2,AIO1                                                          
         LA    R1,CTIDATA                                                       
         XR    RF,RF                                                            
INON06   CLI   0(R1),0             TEST E-O-R                                   
         BE    INON14                                                           
*                                                                               
         USING CTDSCD,R1                                                        
         CLI   CTDSCEL,CTDSCELQ    TEST FOR DESCRIPTION (CODE) ELEMENT          
         BNE   INON08                                                           
         MVC   BCUSERID,CTDSC                                                   
         B     INON12                                                           
*                                                                               
         USING CTDSTD,R1                                                        
INON08   CLI   CTDSTD,CTDSTELQ     TEST FOR DESTINATION ELEMENT                 
         BNE   INON10                                                           
         MVC   BCDSTNAM,CTDSTNAM                                                
         MVC   BCDSTAD1,CTDSTADD                                                
         CLI   CTDSTLEN,CTDSTAD2-CTDSTD                                         
         BNH   INON12                                                           
         MVC   BCDSTAD2,CTDSTAD2                                                
         CLI   CTDSTLEN,CTDSTAD3-CTDSTD                                         
         BNH   INON12                                                           
         MVC   BCDSTAD3,CTDSTAD3                                                
         B     INON12                                                           
*                                                                               
         USING CTORGD,R1                                                        
INON10   CLI   CTORGEL,CTORGELQ    TEST FOR ORIGIN ELEMENT                      
         BNE   INON12                                                           
         MVC   BCORGNAM,CTORGNAM                                                
         MVC   BCORGADD,CTORGADD                                                
         DROP  R1                                                               
*                                                                               
INON12   IC    RF,1(R1)                                                         
         BXH   R1,RF,INON06                                                     
*                                                                               
INON14   L     R1,AINP             EXTRACT PFKEY NUMBER                         
         USING TIOBD,R1                                                         
         ICM   RE,1,TIOBAID                                                     
         BZ    *+12                                                             
         OI    BCINDS1,BCIANYPF    SET USER ENTERED PF KEY THIS TIME            
         STC   RE,BCPFKEY          AND SET PFKEY NUMBER                         
         SR    RE,RE                                                            
         ICM   RE,3,TIOBCURD                                                    
         LA    RE,TWAD(RE)                                                      
         ST    RE,BCACUR           SET A(CURSOR)                                
         DROP  R1                                                               
*                                                                               
         B     INITGV                                                           
         SPACE 2                                                                
***********************************************************************         
* GENERAL INITIALISATION FOR BOTH ONLINE AND OFFLINE                  *         
***********************************************************************         
         SPACE 1                                                                
INITGV   GOTOX VDATCON,BCPARM,(3,ASBDAT),(1,ASPDAT)                             
         GOTOX (RF),(R1),(3,ASBDAT),(2,ASCDAT)                                  
*                                                                               
         L     R1,BCAUTL           AGENCY ALPHA VALUE FOR SECURITY              
         MVC   CUASEC,TAGYSEC-UTLD(R1)                                          
*                                                                               
         CLI   CULANG,0            SET DEFAULT LANGUAGE                         
         BNE   *+8                                                              
*&&UK*&& MVI   CULANG,LANGEUK                                                   
*&&US*&& MVI   CULANG,LANGEUS                                                   
         XR    R1,R1                                                            
         IC    R1,CULANG                                                        
         SLL   R1,3                                                             
         L     RF,=A(LANGCHAR-L'LANGCHAR)                                       
         A     RF,BCRELO                                                        
         LA    R1,0(R1,RF)                                                      
         MVC   BCCHARS,0(R1)       SET SPECIAL CHARACTERS                       
*                                                                               
         CLI   CUCTRY,0            SET DEFAULT COUNTRY                          
         BNE   *+8                                                              
*&&UK*&& MVI   CUCTRY,CTRYGBR                                                   
*&&US*&& MVI   CUCTRY,CTRYUSA                                                   
         CLI   CUCTRY,CTRYSCA      CONVERT SCANDINAVIA TO GBR                   
         BNE   *+8                                                              
         MVI   CUCTRY,CTRYGBR                                                   
*                                                                               
         TM    TWAINDS1,TW1INIT    TEST VALUES SAVED LAST TIME                  
         BZ    INGN02                                                           
         LA    R0,BCVALS           MOVE SAVE VALUES TO GLOBAL W/S               
         LH    RE,=Y(TWVALS-TWAD)                                               
         A     RE,ATWA                                                          
         LA    R1,BCVALSL                                                       
         LR    RF,R1                                                            
         MVCL  R0,RE                                                            
         NI    IINDS,FF-IIFRST                                                  
         B     INGN04                                                           
*                                                                               
INGN02   OI    IINDS,IIFRST        SET FIRST TIME                               
*                                                                               
INGN04   L     R1,AINP             EXTRACT CURSOR POSITION                      
         MVC   CSCURDSP,TIOBCURS-TIOBD(R1)                                      
*                                                                               
         CR    RB,RB               SET CC EQUAL                                 
         XMOD1 ,                                                                
         EJECT                                                                  
*                                                                               
FF       EQU   X'FF'                                                            
         SPACE 1                                                                
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
* SYSTEM FILE NAMES TABLE (FILE NUMBERS 1 THRU 9)                     *         
***********************************************************************         
*                                                                               
NFITAB   DS    0X                                                               
*                                  ** ACCOUNT SYSTEM FILES **                   
         DC    AL1(XOACCDIR/256,QSACC,0),C'ACCDIR '                             
         DC    AL1(NFIIIS,NFIIID+NFIIARC)                                       
         DC    AL1(XOACCMST/256,42,08),AL2(54)                                  
         DC    AL1(NFIABOOL),XL1'40',AL1(XOACCARC/256)                          
         DC    8XL1'00'                                                         
*                                                                               
         DC    AL1(XOACCMST/256,QSACC,0),C'ACCMST '                             
         DC    AL1(NFIIDA,NFIIDI+NFIIARC)                                       
         DC    AL1(XOACCDIR/256,42,56),AL2(2000)                                
         DC    AL1(NFIABOOL+NFICOND),XL1'40',AL1(XOACCMST/256)                  
         DC    8XL1'00'                                                         
*                                                                               
         DC    AL1(XOACCARC/256,QSACC,0),C'ACCARC '                             
         DC    AL1(NFIIDA,NFIIDI+NFIIARC)                                       
         DC    AL1(XOACCDIR/256,42,56),AL2(2000)                                
         DC    AL1(0),XL1'00',AL1(0)                                            
         DC    8XL1'00'                                                         
*                                                                               
*&&US                                                                           
*                                  ** REP SYSTEM FILES **                       
*                                                                               
         DC    AL1(XOREPDIR/256,QSREP,0),C'REPDIR '                             
         DC    AL1(NFIIIS,NFIIID)                                               
         DC    AL1(XOREPFIL/256,27,01),AL2(32)                                  
         DC    AL1(0),XL1'00',AL1(0)                                            
         DC    XL8'00'                                                          
*                                                                               
         DC    AL1(XOREPFIL/256,QSREP,0),C'REPFIL '                             
         DC    AL1(NFIIDA,NFIIDI)                                               
         DC    AL1(XOREPDIR/256,27,29),AL2(2000)                                
         DC    AL1(0),XL1'00',AL1(0)                                            
         DC    XL8'00'                                                          
*&&                                                                             
*                                  ** MEDIA SYSTEM FILES **                     
*&&UK                                                                           
         DC    AL1(XOMEDDIR/256,QSMED,0),C'MEDDIR '                             
         DC    AL1(NFIIIS,NFIIID)                                               
         DC    AL1(XOMEDFIL/256,20,08),AL2(32)                                  
         DC    AL1(0),XL1'00',AL1(0)                                            
         DC    8XL1'00'                                                         
*                                                                               
         DC    AL1(XOMEDFIL/256,QSMED,0),C'MEDFIL '                             
         DC    AL1(NFIIDA,NFIIDI)                                               
         DC    AL1(XOMEDDIR/256,20,34),AL2(2000)                                
         DC    AL1(0),XL1'00',AL1(0)                                            
         DC    8XL1'00'                                                         
*                                                                               
         DC    AL1(XOMEDZDR/256,QSMED,QSMEDZ),C'MEDDIR '                        
         DC    AL1(NFIIIS,NFIIID)                                               
         DC    AL1(XOMEDZFL/256,20,08),AL2(32)                                  
         DC    AL1(0),XL1'00',AL1(0)                                            
         DC    8XL1'00'                                                         
*                                                                               
         DC    AL1(XOMEDZFL/256,QSMED,QSMEDZ),C'MEDFIL '                        
         DC    AL1(NFIIDA,NFIIDI)                                               
         DC    AL1(XOMEDZFL/256,20,34),AL2(2000)                                
         DC    AL1(0),XL1'00',AL1(0)                                            
         DC    8XL1'00'                                                         
*&&                                                                             
*                                  ** CONTROL SYSTEM FILES **                   
         DC    AL1(XOGENDIR/256,QSCON,QSCON),C'GENDIR '                         
         DC    AL1(NFIIIS,NFIIID)                                               
         DC    AL1(XOGENFIL/256,32,04),AL2(40)                                  
         DC    AL1(0),XL1'00',AL1(0)                                            
         DC    8XL1'00'                                                         
*                                                                               
         DC    AL1(XOGENFIL/256,QSCON,QSCON),C'GENFIL '                         
         DC    AL1(NFIIDA,NFIIDI)                                               
         DC    AL1(XOGENDIR/256,32,42),AL2(2000)                                
         DC    AL1(0),XL1'00',AL1(0)                                            
         DC    8XL1'00'                                                         
*                                                                               
         DC    AL1(XOCONFIL/256,QSCON,QSCON),C'CTFILE '                         
         DC    AL1(NFIIVL+NFIIIS,0)                                             
         DC    AL1(0,25,01),AL2(2000)                                           
         DC    AL1(0),XL1'00',AL1(0)                                            
         DC    8XL1'00'                                                         
*                                                                               
         DC    AL1(EOT)                                                         
         EJECT                                                                  
***********************************************************************         
* SYSTEM FILE COMMANDS TABLE                                          *         
***********************************************************************         
         SPACE 1                                                                
CMDTAB   DS    0X                                                               
*                                  ** INDEX SEQUENTIAL COMMANDS **              
CMDIS    DC    AL1(NFIIVL+NFIIIS,0),AL2(CMDISX-*)                               
         DC    C'DMRDHI ',AL1(XOHI,0,0)                                         
         DC    C'DMREAD ',AL1(XORD,0,0)                                         
         DC    C'DMRSEQ ',AL1(XOSQ,0,0)                                         
         DC    C'DMADD  ',AL1(XOADD,0,0)                                        
         DC    C'DMWRT  ',AL1(XOWRITE,0,0)                                      
CMDISX   DC    AL1(EOT)                                                         
*                                  ** DIRECT ACCESS COMMANDS **                 
CMDDA    DC    AL1(NFIIDA,0),AL2(CMDDAX-*)                                      
         DC    C'GETREC ',AL1(XOHI,CMDIDARQ+CMDIDAXC,0)                         
         DC    C'GETREC ',AL1(XORD,CMDIDARQ+CMDIDAXC,0)                         
         DC    C'GETREC ',AL1(XOSQ,CMDIDARQ+CMDIDAXC,0)                         
         DC    C'GETREC ',AL1(XOGET,CMDIDARQ,0)                                 
         DC    C'ADDREC ',AL1(XOADDREC,CMDIDADD,0)                              
         DC    C'PUTREC ',AL1(XOPUTREC,CMDIDARQ,0)                              
CMDDAX   DC    AL1(EOT)                                                         
*                                                                               
CMDTABX  DC    AL1(EOT)                                                         
         SPACE 1                                                                
LANGCHAR DS    0CL8                ** LANGUAGE DEPENDANT CHARACTERS **          
         DC    CL8',=?/#.'         ENGLISH                                      
         DC    CL8',=?/#.'         AMERICAN                                     
         DC    CL8'#=?/#,'         GERMAN                                       
         DC    CL8',=?/#,'         FRENCH                                       
         DC    CL8',=?/#,'         SPANISH                                      
         DC    CL8',=?/#,'         ITALIAN                                      
         DC    CL8',=?/#,'         DUTCH                                        
         DC    CL8',=?/#,'         ???                                          
         DC    CL8',=?/#,'         ???                                          
         DC    CL8',=?/#,'         ???                                          
         DC    CL8',=?/#,'         ???                                          
         DC    CL8',=?/#,'         ???                                          
*                                                                               
ANAWS    DS    0F          ** A(NON-ADDRESSABLE AREAS IN WORKD)                 
         DC    AL2(IOAREA1+L'IODA+L'IOWORK-WORKD,AIO1-WORKD)                    
         DC    AL2(IOAREA2+L'IODA+L'IOWORK-WORKD,AIO2-WORKD)                    
         DC    AL2(IOAREA3+L'IODA+L'IOWORK-WORKD,AIO3-WORKD)                    
         DC    AL2(IOAREA4+L'IODA+L'IOWORK-WORKD,AIO4-WORKD)                    
         DC    AL2(IOAREA5+L'IODA+L'IOWORK-WORKD,AIO5-WORKD)                    
         DC    AL2(IOAREA6+L'IODA+L'IOWORK-WORKD,AIO6-WORKD)                    
         DC    AL2(IOAREA7+L'IODA+L'IOWORK-WORKD,AIO7-WORKD)                    
         DC    AL2(IOAREA8+L'IODA+L'IOWORK-WORKD,AIO8-WORKD)                    
         DC    AL2(IOAREA9+L'IODA+L'IOWORK-WORKD,AIO9-WORKD)                    
         DC    AL2(IOAREAA+L'IODA+L'IOWORK-WORKD,AIOA-WORKD)                    
         DC    AL2(SWSTAB-WORKD,ASWSTAB-WORKD)                                  
         DC    AL2(TSABLK-WORKD,ATSABLK-WORKD)                                  
         DC    AL2(OVERWRK-WORKD,AOVERWRK-WORKD)                                
         DC    AL2(REPWRK-WORKD,AREP-WORKD)                                     
ANAWSN   EQU   (*-ANAWS)/L'ANAWS                                                
*                                                                               
         EJECT                                                                  
***********************************************************************         
* INITIALIZATION LOCAL W/S                                            *         
***********************************************************************         
         SPACE 1                                                                
IWORKD   DSECT                                                                  
IPARMS   DS    A                                                                
*                                                                               
IINDS    DS    XL1                 * INDICATORS *                               
IIFRST   EQU   X'80'               FIRST TIME                                   
IWORKL   EQU   *-IWORKD                                                         
         SPACE 1                                                                
* DDCOMFACS                                                                     
         PRINT OFF                                                              
       ++INCLUDE DDCOMFACS                                                      
         PRINT ON                                                               
         SPACE 1                                                                
* FAFACTS                                                                       
         PRINT OFF                                                              
       ++INCLUDE FAFACTS                                                        
         PRINT ON                                                               
         SPACE 1                                                                
* FASECRETD                                                                     
         PRINT OFF                                                              
       ++INCLUDE FASECRETD                                                      
         PRINT ON                                                               
         SPACE 1                                                                
* FAUTL                                                                         
         PRINT OFF                                                              
       ++INCLUDE FAUTL                                                          
         PRINT ON                                                               
         SPACE 1                                                                
* FATCB                                                                         
         PRINT OFF                                                              
       ++INCLUDE FATCB                                                          
         PRINT ON                                                               
         SPACE 1                                                                
* DDCOREQUS                                                                     
         PRINT OFF                                                              
       ++INCLUDE DDCOREQUS                                                      
         PRINT ON                                                               
         SPACE 1                                                                
* CTGENFILE                                                                     
         PRINT OFF                                                              
       ++INCLUDE CTGENFILE                                                      
         PRINT ON                                                               
         SPACE 1                                                                
* DDGETHELPD                                                                    
         PRINT OFF                                                              
       ++INCLUDE DDGETHELPD                                                     
         PRINT ON                                                               
         SPACE 1                                                                
* DDTWADCOND                                                                    
         PRINT OFF                                                              
       ++INCLUDE DDTWADCOND                                                     
         PRINT ON                                                               
         SPACE 1                                                                
* DDMASTD                                                                       
         PRINT OFF                                                              
       ++INCLUDE DDMASTD                                                        
         PRINT ON                                                               
         SPACE 1                                                                
* DMDTFIS                                                                       
         PRINT OFF                                                              
       ++INCLUDE DMDTFIS                                                        
         PRINT ON                                                               
         EJECT                                                                  
* GEFILWORK                                                                     
       ++INCLUDE GEFILWORK                                                      
         SPACE 1                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'014GEFILSETA 09/04/97'                                      
         END                                                                    
