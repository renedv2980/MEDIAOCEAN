*          DATA SET DDOFFOUT   AT LEVEL 011 AS OF 05/16/97                      
*CATALP OFFOUT                                                                  
         TITLE 'OFFOUT - CONDITIONAL HEX DISPLAY OF CLIENT OFFICE'              
*                                                                               
* PARAM1  BYTE  0                                                               
*         BYTES 1-3      A(CLIENT OFFICE)                                       
* PARAM2  BYTE  0                                                               
*         BYTES 1-3      V(HEXOUT)                                              
* PARAM3  BYTE  0        L (C'L') = LEFT-JUSTIFY 1-CHARACTER OUTPUT             
*         BYTES 1-3      A(OUTPUT)                                              
*                                                                               
         EJECT                                                                  
OFFOUT   CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 OFFWRKL,OFFOUT,CLEAR=YES                                         
         USING OFFWRKD,RC                                                       
         OC    0(4,R1),0(R1)       INPUT ADDRESS SUPPLIED ?                     
         BZ    OFFERR              NO - ERROR                                   
         OC    4(4,R1),4(R1)       HEXOUT ADDRESS SUPPLIED ?                    
         BZ    OFFERR              NO - ERROR                                   
         OC    8(4,R1),8(R1)       OUTPUT ADDRESS SUPPLIED ?                    
         BZ    OFFERR              NO - ERROR                                   
         L     R3,0(R1)            A(INPUT)                                     
         MVC   HEXOUT,4(R1)        A(HEXOUT)                                    
         L     R4,8(R1)            A(OUTPUT)                                    
         MVC   0(2,R4),=C'  '      SET OUTPUT AREA TO SPACES                    
*                                                                               
         CLI   0(R3),X'40'         CONVERT TO HEX ?                             
         BE    OFFXIT              NO - DONE                                    
         CLI   0(R3),X'00'         CONVERT TO HEX ?                             
         BE    OFFXIT              NO - DONE                                    
         CLI   0(R3),X'D0'         CONVERT TO HEX ?                             
         BE    OFFHEX              YES                                          
         CLI   0(R3),X'E0'         CONVERT TO HEX ?                             
         BE    OFFHEX              YES                                          
         CLI   0(R3),C'A'          CONVERT TO HEX ?                             
         BL    OFFHEX              YES                                          
         CLI   0(R3),C'9'          CONVERT TO HEX ?                             
         BNH   OFFX                NO - MOVE 1-CHARACTER OFFICE                 
*                                  YES - OFFICE GT 9                            
OFFHEX   GOTO1 HEXOUT,DMCB,0(R3),0(R4),1,0                                      
         B     OFFXIT                                                           
OFFERR   MVI   0(R1),X'FF'                                                      
         B     OFFXIT                                                           
OFFX     DS    0H                  OUTPUT 1-CHAR OFFICE ONLY                    
         CLI   8(R1),C'L'          LEFT JUSTIFY ?                               
         BE    OFFXL               YES                                          
         MVC   1(1,R4),0(R3)       NO                                           
         B     OFFXIT                                                           
OFFXL    MVC   0(1,R4),0(R3)                                                    
*                                                                               
OFFXIT   XMOD1 1                                                                
         LTORG                                                                  
         SPACE 3                                                                
OFFWRKD  DSECT                                                                  
HEXOUT   DS    V                                                                
DMCB     DS    6F                                                               
OFFWRKL  EQU   *-OFFWRKD                                                        
*                                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'011DDOFFOUT  05/16/97'                                      
         END                                                                    
