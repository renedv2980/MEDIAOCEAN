*          DATA SET DDCODEX    AT LEVEL 010 AS OF 05/01/02                      
*CATALP CODEX                                                                   
         TITLE 'CODEX - MULTI-LEVEL CODE PARSER AND DISPLAY ROUTINE'            
CODEX    CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 CXWRKL,*CODEX*                                                   
         USING CXWRKD,RC                                                        
         USING CDXTABD,RA          RA USED FOR CONTROL TABLE                    
*                                                                               
         ST    R1,SAVR1                                                         
         MVC   PARAMS(24),0(R1)                                                 
         L     R2,PARAMS           A(LEVEL TABLE)                               
         LA    R2,0(R2)                                                         
         ST    R2,ACXTAB                                                        
         L     R2,PARAMS+4         A(INTERNAL)                                  
         LA    R2,0(R2)                                                         
         ST    R2,AINTRN                                                        
         L     R2,PARAMS+8         A(EXTERNAL)                                  
         LA    R2,0(R2)                                                         
         ST    R2,AEXTRN                                                        
*                                                                               
         BAS   RE,SETCTLS          SET VARIOUS CONTROLS                         
         BNE   EXIT                                                             
         CLI   COMMAND,C'V'        VALIDATE                                     
         BE    VALIDATE                                                         
         CLI   COMMAND,C'D'        DISPLAY                                      
         BE    DISPLAY                                                          
         CLI   COMMAND,C'C'        COMPARE                                      
         BE    COMPARE                                                          
         CLI   COMMAND,C'S'        SPLIT                                        
         BE    SPLIT                                                            
         CLI   COMMAND,C'T'        ADJUST TABLE ENTRIES                         
         BE    EXIT                (DONE IN SETCTLS)                            
         DC    H'0'                                                             
*                                                                               
EQRET    DS    0H                                                               
         CR    RD,RD                                                            
         BR    RE                                                               
*                                                                               
NEQRET   DS    0H                                                               
         LTR   RD,RD                                                            
         BR    RE                                                               
*                                                                               
EQXIT    DS    0H                                                               
         CR    RD,RD                                                            
         B     EXIT                                                             
*                                                                               
NEQXIT   DS    0H                                                               
         LTR   RD,RD                                                            
*                                                                               
EXIT     DS    0H                                                               
         XIT1                                                                   
         EJECT                                                                  
*        VALIDATION ROUTINE                                                     
*                                                                               
VALIDATE DS    0H                                                               
         L     R3,AEXTRN           R3 FOR EXTERNAL CODE POSITION                
         L     R2,AINTRN           R2 FOR INTERNAL                              
         ZIC   RF,EXTRNL           INPUT LENGTH                                 
         LTR   RF,RF               IF ZERO, HAVE SCREEN FIELD HEADER            
         BNZ   V3                                                               
         IC    RF,5(RF)                                                         
         TM    1(R3),X'02'         TEST EXTENDED FIELD HEADER                   
         BZ    *+8                                                              
         LA    R3,8(RF)                                                         
         LA    R3,8(RF)                                                         
         ST    R3,AEXTRN                                                        
*                                                                               
V3       DS    0H                                                               
         AR    RF,R3                                                            
         ST    RF,EOINPT           SET END                                      
         L     RA,ACXTAB           A(CONTROL TABLE)                             
         SR    R4,R4               FOR LEVEL COUNTER                            
         MVI   ERROR,0                                                          
*                                                                               
V4       DS    0H                                                               
         CLI   CDXFMT,C' '         TEST END-OF-LIST                             
         BNH   V60                                                              
         LA    R4,1(R4)            BUMP LEVEL                                   
*                                  PREPARE FOR NEW FIELD                        
         MVI   WORK,C' '                                                        
         MVC   WORK+1(L'WORK-1),WORK                                            
         LA    R7,WORK                                                          
         ZIC   RF,CDXMAXL                                                       
         AR    RF,R7                                                            
         ST    RF,EOOPT            SET CHECK FOR MAX LENGTH                     
         LA    RF,TSTNUM                                                        
         CLI   CDXFMT,C'N'         NUMERIC                                      
         BE    V8                                                               
         LA    RF,TSTALPH                                                       
         CLI   CDXFMT,C'A'         ALPHABETIC                                   
         BE    V8                                                               
         LA    RF,TSTALN           ALPHA-NUMERIC                                
*                                                                               
V8       DS    0H                                                               
         BAS   RE,DELSPC           SKIP LEADING SPACES                          
*                                                                               
V9       DS    0H                                                               
         C     R3,EOINPT           TEST AT END OF INPUT                         
         BNL   V16                                                              
         BAS   RE,TSTSEP           TEST HAVE SEPARATOR                          
         BNE   *+12                                                             
         LA    R3,1(R3)            YES- BUMP PAST IT                            
         B     V16                 EOF                                          
         C     R7,EOOPT            TEST HAVE REACHED MAX LENGTH                 
         BNL   V16                                                              
         BASR  RE,RF               TRY FOR VALID CHARACTER                      
         BNE   V16                 BAD CHARACTER MARKS EOF                      
*                                                                               
         MVC   0(1,R7),0(R3)       MOVE CHARACTER TO WORK                       
         LA    R7,1(R7)                                                         
         LA    R3,1(R3)                                                         
         B     V9                                                               
*                                                                               
V16      DS    0H                  END OF FIELD                                 
         LA    R0,WORK                                                          
         SR    R7,R0               R7 HAS FIELD LENGTH                          
         BP    V18                                                              
         CLI   HAVSEP,C'Y'         IF NO INPUT AND NO SEPARATOR                 
         BE    *+12                                                             
V17      DS    0H                                                               
         MVI   ERROR,CDXMFERR      MISSING FIELD ERROR                          
         B     VALX                                                             
*                                  IF HAVE SEPARATOR, FIELD IS A REPEAT         
         ZIC   RE,CDXILEN          SEE IF HAVE VALUE FROM BEFORE                
         BCTR  RE,R0                                                            
         EX    RE,*+8                                                           
         B     *+10                                                             
         OC    0(0,R2),0(R2)                                                    
         BZ    V17                 NO VALUE TO REPEAT                           
         B     V50                                                              
*                                                                               
V18      DS    0H                  HAVE SOME VALID CHARACTERS                   
         CLM   R7,1,CDXMINL        TEST HAVE ENOUGH                             
         BNL   *+12                                                             
V19      DS    0H                                                               
         MVI   ERROR,CDXLNERR      BAD LENGTH                                   
         B     VALX                                                             
*                                  HAVE CHARACTERS IN WORK                      
         CLI   CDXFMT,C'A'         ALPHA                                        
         BE    V24                                                              
         CLI   CDXFMT,C'X'         ALPH-NUMERIC                                 
         BE    V24                                                              
*                                  MUST BE NUMERIC                              
         CLI   CDXIFMT,C'B'        TEST FORMAT                                  
         BNE   V22                                                              
*                                  BINARY                                       
         BCTR  R7,R0                                                            
         EX    R7,*+8                                                           
         B     *+10                                                             
         PACK  DUB,WORK(0)                                                      
         CVB   R0,DUB                                                           
         LTR   R0,R0                                                            
         BP    V21                                                              
*                                                                               
V20      DS    0H                                                               
         MVI   ERROR,CDXZVERR      ZERO VALUE IS ERROR                          
         B     VALX                                                             
*                                                                               
V21      DS    0H                                                               
         ZIC   RE,CDXILEN                                                       
         SLL   RE,2                                                             
         LA    RE,MAXTAB-4(RE)     MAXIMUM BINARY VALUES                        
         C     R0,0(RE)                                                         
         BNH   *+12                                                             
         MVI   ERROR,CDXMXERR      VALUE TOO LARGE TO FIT                       
         B     VALX                                                             
*                                                                               
         ZIC   RE,CDXILEN                                                       
         LA    RE,MSKTAB-1(RE)     MASKS FOR EXECUTED STCM                      
         IC    RE,0(RE)                                                         
         EX    RE,*+8                                                           
         B     *+8                                                              
         STCM  R0,0,0(R2)                                                       
         B     V50                                                              
*                                                                               
V22      DS    0H                                                               
         CLI   CDXIFMT,C'P'        PWOS                                         
         BNE   V23                                                              
         ZIC   RE,CDXILEN                                                       
         SLL   RE,4                                                             
         OR    RE,R7                                                            
         EX    RE,*+8                                                           
         B     *+10                                                             
         PACK  DUB(0),WORK(0)                                                   
         ZIC   RE,CDXILEN          ELSE PWOS                                    
         BCTR  RE,R0                                                            
         EX    RE,*+8                                                           
         B     *+10                                                             
         OC    DUB(0),DUB          ZERO VALUE IS ERROR                          
         BZ    V20                                                              
         EX    RE,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R2),DUB         SET PWOS IN INTERNAL CODE                    
         B     V50                                                              
*                                                                               
V23      DS    0H                  EBCDIC                                       
         BCTR  R7,R0                                                            
         EX    R7,*+8                                                           
         B     *+10                                                             
         PACK  DUB,WORK(0)                                                      
         CP    DUB,=P'0'           ZERO VALUE IS ERROR                          
         BNH   V20                                                              
         ZIC   RF,CDXILEN                                                       
         BCTR  RF,R0                                                            
         SLL   RF,4                                                             
         EX    RF,*+8                                                           
         B     *+10                                                             
         UNPK  0(0,R2),DUB                                                      
         B     V50                                                              
*                                                                               
V24      DS    0H                                                               
         CLI   CDXIFMT,C'C'        COMPRESSED ALPHA                             
         BE    V26                                                              
*                                  ELSE JUST MOVE TO INTERNAL                   
         ZIC   RE,CDXILEN                                                       
         BCTR  RE,R0                                                            
         EX    RE,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R2),WORK                                                     
         B     V50                                                              
*                                                                               
V26      DS    0H                  COMPRESSED ALPHA                             
         DC    H'0'                NO SUPPORTED                                 
*                                                                               
V50      DS    0H                                                               
         ZIC   RE,CDXILEN          BUMP POSITION IN INTERNAL CODE               
         AR    R2,RE                                                            
         C     R3,EOINPT           IF AT END OF INPUT                           
         BNL   VALX                DONE                                         
         LA    RA,CDXTABL(RA)      NEXT TABLE ENTRY                             
         B     V4                                                               
*                                                                               
V60      DS    0H                  END OF CONTROL TABLE                         
         C     R3,EOINPT           ARE WE AT END OF INPUT                       
         BE    *+8                                                              
         MVI   ERROR,CDXLVERR      TOO MANY LEVELS                              
*                                                                               
VALX     DS    0H                                                               
         STC   R4,LEVEL            RETURN LEVEL                                 
         ZIC   RE,INTRNL                                                        
         A     RE,AINTRN           CLEAR ANY REMAINING BYTES IN OUTPUT          
         SR    RE,R2                                                            
         BNP   VALX4                                                            
         BCTR  RE,R0                                                            
         EX    RE,*+8                                                           
         B     *+10                                                             
         XC    0(0,R2),0(R2)                                                    
*                                                                               
VALX4    DS    0H                                                               
         L     R1,SAVR1            RETURN VALUES                                
         MVC   0(1,R1),LEVEL                                                    
         MVC   4(1,R1),INTRNL                                                   
         MVC   8(1,R1),ERROR                                                    
         B     EXIT                                                             
         SPACE 3                                                                
*        TSTSEP - TEST SEPARATOR                                                
*                                                                               
TSTSEP   DS    0H                                                               
         MVI   HAVSEP,C'Y'                                                      
         LA    R1,SEPLST                                                        
         LA    R0,NSEPS                                                         
         CLC   0(1,R3),0(R1)                                                    
         BE    EQRET                                                            
         LA    R1,1(R1)                                                         
         BCT   R0,*-14                                                          
         MVI   HAVSEP,C'N'                                                      
         B     NEQRET                                                           
*                                                                               
SEPLST   DC    C' /,.-'                                                         
NSEPS    EQU   *-SEPLST            LIST OF SEPARATORS                           
         SPACE 3                                                                
*        TSTALPH - TEST ALPHA                                                   
*                                                                               
TSTALPH  DS    0H                                                               
         CLI   0(R3),C'Z'                                                       
         BH    NEQRET                                                           
         CLI   0(R3),C'A'                                                       
         BNL   EQRET                                                            
         CLI   0(R3),X'81'         ALLOW LOWER CASE                             
         BL    NEQRET                                                           
         CLI   0(R3),X'A9'                                                      
         BNH   EQRET                                                            
         B     NEQRET                                                           
         SPACE 3                                                                
*        TSTNUM - TEST NUMERIC                                                  
*                                                                               
TSTNUM   DS    0H                                                               
         CLI   0(R3),C'0'                                                       
         BL    NEQRET                                                           
         CLI   0(R3),C'9'                                                       
         BNH   EQRET                                                            
         B     NEQRET                                                           
         SPACE 3                                                                
*        TSTALN - TEST ALPHA-NUMERIC                                            
*                                                                               
TSTALN   DS    0H                                                               
         LR    R0,RE                                                            
         BAS   RE,TSTNUM                                                        
         BE    TSTALN4                                                          
         BAS   RE,TSTALPH                                                       
*                                                                               
TSTALN4  DS    0H                                                               
         LR    RE,R0                                                            
         BR    RE                                                               
         SPACE 3                                                                
*        DELSPC - SKIP LEADING SPACES                                           
*                                                                               
DELSPC   DS    0H                                                               
         C     R3,EOINPT                                                        
         BNLR  RE                                                               
         CLI   0(R3),C' '                                                       
         BHR   RE                                                               
         LA    R3,1(R3)                                                         
         B     DELSPC                                                           
         EJECT                                                                  
*        DISPLAY ROUTINE                                                        
*                                                                               
DISPLAY  DS    0H                                                               
         CLI   SEPTR,C' '          CHECK SEPARATOR GIVEN                        
         BH    *+8                                                              
         MVI   SEPTR,C' '                                                       
*                                                                               
         L     RA,ACXTAB           CONTROL TABLE                                
         SR    R4,R4               FOR LEVEL COUNTER                            
         L     R2,AINTRN           R2 FOR PLACE IN INTERNAL CODE                
         L     R3,AEXTRN           R3 FOR EXTERNAL                              
*                                                                               
D4       DS    0H                                                               
         CLI   CDXFMT,C' '         TEST END-OF-LIST                             
         BNH   D40                                                              
         ZIC   RF,CDXILEN                                                       
         BCTR  RF,R0                                                            
         EX    RF,*+8                                                           
         B     *+10                                                             
         OC    0(0,R2),0(R2)                                                    
         BZ    D40                 NO- DONE                                     
*                                                                               
         CLI   DSPLEV,C'L'         OPT TO DISPLAY ONLY LOWEST                   
         BNE   *+8                                                              
         L     R3,AEXTRN           YES, START OVER                              
*                                                                               
         CLI   CDXIFMT,C'A'        ALPHABETIC                                   
         BE    D8                                                               
         CLI   CDXIFMT,C'X'        ALPHA-NUMERIC                                
         BE    D8                                                               
         CLI   CDXIFMT,C'N'        NUMERIC (EBCDIC)                             
         BE    D9                                                               
         CLI   CDXIFMT,C'P'        PWOS                                         
         BE    D10                                                              
         CLI   CDXIFMT,C'B'        BINARY                                       
         BE    D12                                                              
         CLI   CDXIFMT,C'C'        COMPRESSED ALPHA                             
         BE    D14                                                              
         DC    H'0'                INVALID FORMAT                               
*                                                                               
D8       DS    0H                  A AND X FORMATS                              
         ZIC   RF,CDXILEN                                                       
         BCTR  RF,R0                                                            
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R3),0(R2)                                                    
*                                                                               
         LA    R3,0(R3,RF)         POINT TO LAST CHARACTER                      
         TM    CDXCNTL,X'80'       TEST TO LEAVE PADDED                         
         BNZ   D20                 YES                                          
         CLI   0(R3),C' '                                                       
         BH    *+8                                                              
         BCT   R3,*-8                                                           
         B     D20                                                              
*                                                                               
D9       DS    0H                  NUMERIC (EBCDIC)                             
         ZIC   RF,CDXILEN                                                       
         BCTR  RF,R0                                                            
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   WORK(0),0(R2)                                                    
         B     D11                                                              
*                                                                               
D10      DS    0H                  PWOS                                         
         ZIC   RF,CDXMAXL          MAX EXTERNAL LENGTH                          
         SLL   RF,4                                                             
         ZIC   RE,CDXILEN          INTERNAL LENGTH                              
         OR    RF,RE                                                            
         EX    RF,*+8                                                           
         B     *+10                                                             
         UNPK  WORK(0),0(0,R2)                                                  
*                                                                               
D11      DS    0H                  EBCDIC VALUE IN WORK FOR MAX LENGTH          
         ZIC   R5,CDXMAXL          MAX LENGTH                                   
         ZIC   R6,CDXMINL          MINIMUM LENGTH                               
         LA    R7,WORK                                                          
*                                                                               
D11D     DS    0H                                                               
         CR    R5,R6               HAVE WE REACHED MIN                          
         BNH   D11G                YES, PUT DIGIT                               
         CLI   0(R7),C'0'          ELSE, IS THIS A 0                            
         BE    D11J                YES, SKIP IT                                 
         LR    R6,R5               ELSE, FORCE TO INCLUDE REST                  
*                                                                               
D11G     DS    0H                                                               
         MVC   0(1,R3),0(R7)       SET DIGIT                                    
         LA    R3,1(R3)                                                         
*                                                                               
D11J     DS    0H                                                               
         LA    R7,1(R7)            NEXT DIGIT                                   
         BCT   R5,D11D                                                          
         B     D22                                                              
*                                                                               
D12      DS    0H                  BINARY                                       
         ZIC   RF,CDXILEN                                                       
         LA    RE,MSKTAB-1(RF)                                                  
         ZIC   RF,0(RE)                                                         
         EX    RF,*+8                                                           
         B     *+8                                                              
         ICM   R1,0,0(R2)                                                       
         CVD   R1,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         ZIC   RF,CDXMAXL          MAXIMUM LENGTH                               
         BCTR  RF,R0                                                            
         SLL   RF,4                                                             
         EX    RF,*+8                                                           
         B     *+10                                                             
         UNPK  WORK(0),DUB                                                      
         B     D11                                                              
*                                                                               
D14      DS    0H                  COMPRESSED ALPHA                             
         DC    H'0'                **NOT SUPPORTED**                            
*                                                                               
D20      DS    0H                                                               
         LA    R3,1(R3)            BUMP EXTERNAL POSITION                       
D22      DS    0H                                                               
         MVI   0(R3),C' '                                                       
         TM    CDXCNTL,X'40'       TEST NO SEPARTOR AFTER                       
         BNZ   D24                                                              
         MVC   0(1,R3),SEPTR       SET SEPARATOR                                
         LA    R3,1(R3)                                                         
         MVI   0(R3),X'FF'         SET FINAL SEPARATOR                          
*                                                                               
D24      DS    0H                                                               
         LA    R4,1(R4)            BUMP LEVEL COUNTER                           
         ZIC   RF,CDXILEN                                                       
         AR    R2,RF                                                            
         LA    RA,CDXTABL(RA)                                                   
         B     D4                  NEXT LEVEL                                   
*                                                                               
D40      DS    0H                                                               
         STC   R4,LEVEL            RETURN LEVEL                                 
         CLI   0(R3),X'FF'         TEST HAVE FINAL SEPARATOR                    
         BNE   D42                                                              
         MVI   0(R3),C' '                                                       
         BCTR  R3,R0                                                            
         MVI   0(R3),C' '                                                       
*                                                                               
D42      DS    0H                                                               
         L     R1,SAVR1            RETURN VALUES                                
         S     R3,AEXTRN                                                        
         STC   R3,8(R1)            SET DISPLAY LENGTH                           
         MVC   0(1,R1),LEVEL                                                    
         MVC   4(1,R1),INTRNL                                                   
         B     EXIT                                                             
*                                                                               
MSKTAB   DC    AL1(1,3,7,15)      ICM AND STCM MASKS                            
         DS    0F                                                               
MAXTAB   DC    AL4(255,65535,16777215,2147483647)   MAXIMUM BINARY VALS         
         EJECT                                                                  
*        COMPARE ROUTINE - COMPARE TWO INTERNAL CODES                           
*                                                                               
COMPARE  DS    0H                                                               
         L     R2,AINTRN           FIRST CODE                                   
         L     R3,AEXTRN           2ND CODE                                     
         L     RA,ACXTAB           CONTROL TABLE                                
         SR    R4,R4                                                            
*                                                                               
COMP4    DS    0H                                                               
         CLI   CDXFMT,C' '         TEST END-OF-LIST                             
         BNH   COMP20                                                           
         LA    R4,1(R4)            BUMP LEVEL COUNTER                           
         ZIC   RF,CDXILEN          LENGTH                                       
         BCTR  RF,R0                                                            
         EX    RF,*+8                                                           
         B     *+10                                                             
         CLC   0(0,R2),0(R3)                                                    
         BNE   COMP8                                                            
*                                  EQUAL SO FAR, TRY NEXT                       
         LA    R2,1(R2,RF)                                                      
         LA    R3,1(R3,RF)                                                      
         LA    RA,CDXTABL(RA)      NEXT LEVEL                                   
         B     COMP4                                                            
*                                                                               
COMP8    DS    0H                  HAVE UNEQUAL                                 
         STC   R4,LEVEL                                                         
         B     COMP22                                                           
*                                                                               
COMP20   DS    0H                  CODES ARE EQUAL                              
         MVI   LEVEL,0                                                          
COMP22   DS    0H                                                               
         L     R1,SAVR1            RETURN VALUES                                
         MVC   0(1,R1),LEVEL                                                    
         MVC   4(1,R1),INTRNL                                                   
         CLI   LEVEL,0             SET CC                                       
         B     EXIT                                                             
         EJECT                                                                  
*        SPLIT ROUTINE - SPLIT AN INTERNAL CODE INTO COMPONENTS                 
*                                                                               
SPLIT    DS    0H                                                               
         L     R2,AINTRN           INTERNAL CODE                                
         LR    R7,R2                                                            
         L     R3,AEXTRN           PLACE FOR NEW CODE LIST                      
         L     RA,ACXTAB           CONTROL TABLE                                
         ZIC   R6,INTRNL           INTERNAL CODE LENGTH                         
         BCTR  R6,R0               LESS 1                                       
         SR    R4,R4                                                            
         SR    R5,R5                                                            
*                                                                               
SPL4     DS    0H                                                               
         CLI   CDXFMT,C' '         TEST END-OF-LIST                             
         BNH   SPL20                                                            
         EX    R6,*+8                                                           
         B     *+10                                                             
         XC    0(0,R3),0(R3)       CLEAR SLOT                                   
         ZIC   RF,CDXILEN          LENGTH FOR THIS LEVEL                        
         BCTR  RF,R0                                                            
         EX    RF,*+8                                                           
         B     *+10                                                             
         OC    0(0,R7),0(R7)       TEST ANY VALUE                               
         BZ    SPL20               NO- DONE AT THIS LEVEL                       
         LA    R7,1(R7,RF)                                                      
*                                                                               
         AR    R5,RF               CUME LENGTH                                  
         EX    R5,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R3),0(R2)       MOVE PART OF CODE                            
         LA    R5,1(R5)                                                         
         LA    R4,1(R4)            BUMP LEVEL COUNTER                           
         LA    R3,1(R3,R6)         NEXT OUTPUT SLOT                             
         LA    RA,CDXTABL(RA)      NEXT LEVEL                                   
         B     SPL4                                                             
*                                                                               
SPL20    DS    0H                                                               
         L     R1,SAVR1            RETURN LEVEL COUNT                           
         STC   R4,0(R1)                                                         
         MVC   4(1,R1),INTRNL                                                   
*                                                                               
         B     EXIT                                                             
         EJECT                                                                  
*        SETCTLS - SET VARIOUS CONTROLS                                         
*                                                                               
SETCTLS  NTR1                                                                   
         MVC   COMMAND,PARAMS+0                                                 
         MVC   DSPLEV,PARAMS+4                                                  
         MVC   SEPTR,PARAMS+8                                                   
         MVC   EXTRNL,PARAMS+8                                                  
         MVI   LEVEL,0                                                          
         MVI   ERROR,0                                                          
*                                                                               
         L     RA,ACXTAB                                                        
         SR    R2,R2                                                            
*                                                                               
SC4      DS    0H                                                               
         CLI   CDXFMT,C' '         TEST END-OF-LIST                             
         BNH   SC20                                                             
*                                                                               
         CLI   COMMAND,C'T'        IF NOT SETTING TABLE VALUES                  
         BNE   SC18                JUST CUME LENGTH                             
*                                                                               
         CLI   CDXMAXL,0           IF MAX LEN IS 0                              
         BNE   *+10                                                             
         MVC   CDXMAXL,CDXMINL     SET IT TO MIN                                
*                                                                               
         CLI   CDXMINL,0           IF MIN LEN IS 0                              
         BNE   *+10                                                             
         MVC   CDXMINL,CDXMAXL     SET IT TO MAX                                
*                                                                               
         CLI   CDXFMT,C'N'         FOR NUMERICS                                 
         BNE   SC10                                                             
         CLI   CDXIFMT,C' '        DEFAULT FORMAT                               
         BH    *+8                                                              
         MVI   CDXIFMT,C'P'        IS PWOS                                      
         CLI   CDXIFMT,C'P'        FOR PWOS                                     
         BNE   SC6                                                              
         CLI   CDXILEN,0           TEST LENGTH GIVEN                            
         BNE   SC18                                                             
         ZIC   RF,CDXMAXL          INTERNAL LENGTH                              
         LA    RF,1(RF)            IS HALF OF EXTERNAL                          
         SRL   RF,1                                                             
         STC   RF,CDXILEN                                                       
         B     SC18                                                             
*                                                                               
SC6      DS    0H                                                               
         CLI   CDXIFMT,C'B'        BINARY                                       
         BNE   SC10                NO, TREAT AS NON-NUMERIC                     
         CLI   CDXILEN,0           ELSE, IF INTERNAL LENGTH IS SET              
         BNE   SC18                ACCEPT IT                                    
         MVI   CDXILEN,4           ELSE SET DEFAULT                             
         CLI   CDXMAXL,8           4 IF 8+ DIGITS                               
         BNL   SC18                                                             
         ZIC   RF,CDXMAXL          OR SET FROM TABLE                            
         LA    RF,BLENTAB(RF)                                                   
         MVC   CDXILEN,0(RF)                                                    
         B     SC18                                                             
*                                                                               
SC10     DS    0H                  NON-NUMERICS                                 
         CLI   CDXIFMT,C' '        INTERNAL FORMAT                              
         BH    *+10                                                             
         MVC   CDXIFMT,CDXFMT      DEFAULTS TO EXTERNAL                         
         CLI   CDXIFMT,C'C'        UNLESS COMPRESSED                            
         BE    SC12                                                             
         MVC   CDXILEN,CDXMAXL     INTERNAL LENGTH = EXTERNAL                   
         B     SC18                                                             
*                                                                               
SC12     DS    0H                  COMPRESSED FORMAT                            
         DC    H'0'                NOT SUPPORTED                                
*                                                                               
SC18     DS    0H                                                               
         ZIC   RF,CDXILEN          CUME INTERNAL LENGTH                         
         AR    R2,RF                                                            
         LA    RA,CDXTABL(RA)      NEXT EXTRY                                   
         B     SC4                                                              
*                                                                               
SC20     DS    0H                                                               
         STC   R2,INTRNL           SET TOTAL INTERNAL LENGTH                    
         CLI   COMMAND,C'T'        IF JUST SETTING TABLE                        
         BNE   SC22                                                             
         L     R1,SAVR1                                                         
         STC   R2,4(R1)            RETURN LENGTH                                
*                                                                               
SC22     DS    0H                                                               
         CLI   INTRNL,0            SET CC                                       
         BE    NEQXIT                                                           
         B     EQXIT                                                            
*                                                                               
BLENTAB  DC    AL1(0,1,1,2,2,3,3,3)   BINARY LENGTHS                            
         SPACE 3                                                                
         LTORG                                                                  
         SPACE 3                                                                
CXWRKD   DSECT                                                                  
DMCB     DS    6F                                                               
PARAMS   DS    6F                                                               
SAVR1    DS    F                                                                
ACXTAB   DS    F                   A(CONTROL TABLE)                             
AINTRN   DS    F                   A(INTERNAL CODE)                             
AEXTRN   DS    F                   A(EXTERNAL CODE)                             
*                                                                               
DUB      DS    D                                                                
FULL     DS    F                                                                
HALF     DS    H                                                                
BYTE     DS    X                                                                
HAVSEP   DS    C                                                                
*                                                                               
COMMAND  DS    CL1                 COMMAND                                      
SEPTR    DS    CL1                 SEPARATOR                                    
DSPLEV   DS    CL1                 LEVEL TO START DISPLAY                       
LEVEL    DS    CL1                 LEVEL                                        
INTRNL   DS    CL1                 INTERNAL LENGTH                              
EXTRNL   DS    CL1                 EXTERNAL LENGTH                              
ERROR    DS    CL1                 ERROR CODE                                   
*                                                                               
         DS    0F                                                               
WORK     DS    XL64                                                             
EOINPT   DS    F                                                                
EOOPT    DS    F                                                                
CXWRKL   EQU   *-CXWRKD                                                         
         EJECT                                                                  
*                                                                               
       ++INCLUDE DDCDXTAB                                                       
*                                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'010DDCODEX   05/01/02'                                      
         END                                                                    
