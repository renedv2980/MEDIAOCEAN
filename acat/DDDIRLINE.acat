*          DATA SET DDDIRLINE  AT LEVEL 017 AS OF 05/01/02                      
*CATALP DIRLINE                                                                 
         TITLE 'PROGRAM TO PRINT DIRECT LINE MEMOS FROM PAN'                    
DIRLINE  CSECT                                                                  
         PRINT NOGEN                                                            
         NBASE 0,**LINE**,=V(REGSAVE)                                           
         L     R2,=A(BOOKPOOL)                                                  
         LA   R3,640                                                            
         SPACE 2                                                                
CLEAR    MVC   0(100,R2),SPACES    CLEAR THE TEXT POOL                          
         LA    R2,100(R2)                                                       
         BCT   R3,CLEAR                                                         
         GOTO1 =V(PRINT),PARA,SPACES-1,=C'BC01'                                 
         EJECT                                                                  
*              READ CONTROL CARDS                                               
         SPACE 3                                                                
CARD     LA    R2,BOOKLIST                                                      
         LA    R3,10                                                            
         SPACE 2                                                                
CARD2    GOTO1 =V(CARDS),PARA,C,=C'RE00'                                        
         CLC   C(2),=C'/*'                                                      
         BE    BOOK                                                             
         CLC   LABLNAME,SPACES                                                  
         BNE   CARD4                                                            
         MVC   LABLNAME,C                                                       
         B     CARD2                                                            
         SPACE 2                                                                
CARD4    CLC   C(10),=C'LOWER CASE'                                             
         BNE   CARD5                                                            
         XC    MASK,MASK                                                        
         B     CARD2                                                            
         SPACE 2                                                                
CARD5    CLC   C(2),=C'F='         FILTER CARD                                  
         BNE   CARD6                                                            
         MVC   FILTERS,C+2                                                      
         B     CARD2                                                            
         SPACE 2                                                                
CARD6    MVC   0(10,R2),C          BOOK CARDS                                   
         LA    R2,20(R2)                                                        
         BCT   R3,CARD2                                                         
         B     ABORT2                                                           
         EJECT                                                                  
*              ROUTINE TO BUILD DIRECT LINE BOOKS                               
         SPACE 3                                                                
BOOK     LA    R2,BOOKLIST                                                      
         L     R3,=A(BOOKPOOL)                                                  
         SPACE 2                                                                
BOOK2    CLC   0(10,R2),SPACES                                                  
         BE    READ                                                             
         LR    R4,R3                                                            
         LA    R5,61                                                            
         SPACE 2                                                                
BOOK4    GOTO1 =V(PANIC),PARA,(X'80',=C'READ'),=C'PAN',(R2),(R4)                
         TM    PARA+8,X'80'                                                     
         BO    BOOK6                                                            
         CLI   PARA+8,0                                                         
         BNE   ABORT6                                                           
         CLC   0(2,R4),=C'*-'                                                   
         BNE   *+8                                                              
         MVI   0(R4),C'-'                                                       
         CLC   0(15,R4),=C'         SPACE '                                     
         BNE   BOOK5                                                            
         CLI   16(R4),C' '                                                      
         BNE   *+14                                                             
         MVC   16(1,R4),15(R4)                                                  
         MVI   15(R4),C'0'                                                      
         OC    15(2,R4),=X'F0F0'                                                
         PACK  DUB,15(2,R4)                                                     
         CVB   R1,DUB                                                           
         SPACE 2                                                                
BOOK4A   MVC   0(80,R4),SPACES                                                  
         LA    R4,80(R4)                                                        
         BCT   R5,*+8                                                           
         B     ABORT8                                                           
         BCT   R1,BOOK4A                                                        
         B     BOOK4                                                            
         SPACE 2                                                                
BOOK5    DS    0H                                                               
         LA    R4,80(R4)                                                        
         BCT   R5,BOOK4                                                         
         B     ABORT8                                                           
         SPACE 2                                                                
BOOK6    LA    R2,20(R2)                                                        
         MVC   0(2,R4),SPACES      DISPOSE OF /*                                
         A     R3,=F'6400'                                                      
         B     BOOK2                                                            
         EJECT                                                                  
*              NOW READ THE LABEL FILE                                          
         SPACE 3                                                                
READ     GOTO1 =V(PANIC),PARA,(X'80',=C'READ'),=C'PAN',LABLNAME,C               
         TM    PARA+8,X'80'                                                     
         BZ    READ2                                                            
         XBASE                                                                  
         SPACE 2                                                                
READ2    CLI   PARA+8,0                                                         
         BNE   ABORT4                                                           
         CLI   C,C'*'                                                           
         BNE   READ                                                             
         LA    R2,C+2                                                           
         LA    R3,FILTERS                                                       
         LA    R4,50                                                            
         SPACE 2                                                                
READ4    CLI   0(R3),C' '          CHECK FILTERS                                
         BE    READ6                                                            
         CLC   0(1,R2),0(R3)       OK IF WE HIT ON ANY CHARACTER                
         BE    READ8                                                            
         SPACE 2                                                                
READ6    LA    R2,1(R2)                                                         
         LA    R3,1(R3)                                                         
         BCT   R4,READ4                                                         
         B     READ                                                             
         SPACE 2                                                                
READ8    LA    R2,NAMADD           NOW FILL THE NAME & ADDRESS AREA             
         LA    R3,5                                                             
         LA    R4,1                NUMBER OF COPIES OPTION                      
         CLC   C+59(7),=C'COPIES='                                              
         BNE   READ10                                                           
         OI    C+66,X'F0'                                                       
         PACK  DUB,C+66(1)                                                      
         CVB   R4,DUB                                                           
         CLI   C+67,C' '                                                        
         BE    READ10                                                           
         OI    C+67,X'F0'                                                       
         PACK  DUB,C+66(2)                                                      
         CVB   R4,DUB                                                           
         SPACE 2                                                                
READ10   GOTO1 =V(PANIC),PARA,(X'80',=C'READ'),=C'PAN',LABLNAME,C               
         MVC   0(32,R2),C                                                       
         LA    R2,32(R2)                                                        
         BCT   R3,READ10                                                        
         SPACE 2                                                                
READ11   LA    R2,BOOKLIST         NOW WE'LL PRINT THE LETTER(S)                
         L     R3,=A(BOOKPOOL)                                                  
         SPACE 2                                                                
READ12   CLC   0(10,R2),SPACES                                                  
         BE    READ14                                                           
         BAS   RE,LETTER                                                        
         BAS   RE,LABEL                                                         
         LA    R2,20(R2)                                                        
         A     R3,=F'6400'                                                      
         B     READ12                                                           
         SPACE 2                                                                
READ14   BCT   R4,READ11                                                        
         B     READ                                                             
         EJECT                                                                  
*              ROUTINE TO PRINT A PAGE OF LETTER                                
         SPACE 3                                                                
LETTER   NTR1                                                                   
         MVC   P,SPACES                                                         
         LA    R4,60                                                            
         SPACE 2                                                                
LETTER2  MVC   P+4(80),0(R3)                                                    
         OC    P(132),MASK                                                      
         GOTO1 =V(PRINT),PARA,P-1,=C'BL01'                                      
         LA    R3,80(R3)                                                        
         BCT   R4,LETTER2                                                       
         GOTO1 =V(PRINT),PARA,SPACES-1,=C'BL03'                                 
         XIT1                                                                   
         SPACE 2                                                                
LABEL    NTR1                                                                   
         LA    R2,NAMADD                                                        
         LA    R3,5                                                             
         SPACE 2                                                                
LABEL2   MVC   P(132),SPACES                                                    
         MVC   P+50(32),0(R2)                                                   
         OC    P(132),MASK                                                      
         GOTO1 =V(PRINT),PARA,P-1,=C'BL01'                                      
         LA    R2,32(R2)                                                        
         BCT   R3,LABEL2                                                        
         GOTO1 =V(PRINT),PARA,SPACES-1,=C'BC01'                                 
         XIT1                                                                   
         EJECT                                                                  
*              ABORT CONDITION HANDLING                                         
         SPACE 3                                                                
ABORT2   MVC   P(37),=C'RUN ABORTED DUE TO MORE THAN 10 BOOKS'                  
         B     ABORTALL                                                         
         SPACE 2                                                                
ABORT4   MVC   P(32),=C'RUN ABORTED - LABEL BOOK MISSING'                       
         B     ABORTALL                                                         
         SPACE 2                                                                
ABORT6   MVC   P(38),=C'RUN ABORTED - DIRECT LINE BOOK MISSING'                 
         B     ABORTALL                                                         
         SPACE 2                                                                
ABORT8   MVC   P(40),=C'RUN ABORTED - MORE THAN 60 LINES IN BOOK'               
         SPACE 2                                                                
ABORTALL GOTO1 =V(PRINT),PARA,P-1,=C'BC01'                                      
         GOTO1 =V(LOGIO),PARA,1,(40,P)                                          
         XBASE                                                                  
         EJECT                                                                  
*              WORK SPACE FOR PROGRAM                                           
         SPACE 3                                                                
PARA     DS    6F                                                               
DUB      DS    D                                                                
C        DS    CL80                                                             
         DS    C                                                                
P        DS    CL132                                                            
         DS    C                                                                
SPACES   DC    CL132' '                                                         
MASK     DC    CL132' '                                                         
BOOKLIST DC    CL210' '                                                         
LABLNAME DC    CL10' '                                                          
         DC    CL10' '                                                          
FILTERS  DS    CL50                                                             
NAMADD   DS    CL160                                                            
         LTORG                                                                  
BOOKPOOL CSECT                                                                  
         DS    64000C                                                           
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'017DDDIRLINE 05/01/02'                                      
         END                                                                    
