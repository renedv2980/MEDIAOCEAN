*          DATA SET SPREPI2FR  AT LEVEL 124 AS OF 03/04/21                      
*CATALP SPI2FRN                                                                 
         TITLE 'SPI2 - FILM USAGE ANALYSIS REPORT'                              
         PRINT NOGEN                                                            
***********************************************************************         
* USER    JIRA       DATE                  CHANGE LOG                 *         
* ---- ----------  -------- ----------------------------------------- *         
* AKAT SPEC-46696  02/22/21 CARRY AND DISPLAY 4 BYTE DEMO VALUES      *         
***********************************************************************         
FILMTOT  CSECT                                                                  
         NMOD1 0,FILMTOT                                                        
         USING SPWORKD,RA,R9                                                    
         LA    RC,SPACEND                                                       
         USING IMRWORK,RC                                                       
         LA    R8,2048(RB)                                                      
         LA    R8,2048(R8)                                                      
         USING FILMTOT+4096,R8                                                  
*                                                                               
***      MVI   FILMDSCP,C'N'       SET NO DISCREPANCY                           
         MVI   AFRPTBUF,0          CLEAR HOB (OVERFLOW SW)                      
         L     RF,AFRPTBUF         PRINT LINE BUFFER                            
         MVI   0(RF),X'FF'         SET END                                      
         ST    RF,NXTLIN                                                        
*                                                                               
         MVC   SAVSPCNG,RCSPACNG   SINGLE SPACING FOR FILM REP                  
         MVI   RCSPACNG,1                                                       
*                                                                               
         MVC   APATLNS,=A(PATLINS)    AREA FOR PATTERN DESCRIPTION              
         MVC   AFLMRUN,=A(FLMRUN)     AREA FOR FILM RUN LIST                    
*                             SET COPY CODES IN AFFID TABLE                     
         SR    R3,R3                                                            
         SR    R4,R4                                                            
         L     R7,AFINV                                                         
         USING IELEMD,R7                                                        
         USING BYELEMD,R6                                                       
*                                                                               
IFT6     DS    0H                                                               
         C     R7,ALINV                                                         
         BH    IFT8B                                                            
         MVI   ICOPY,0                                                          
         CLI   PIGCTL,C'S'         IF DOING PIGS SEPARATELY                     
         BNE   IFT6D                                                            
         CLC   PIGFILT,IPRD2       ELSE MUST BE RIGHT PRD                       
         BNE   IFT8                                                             
*                                                                               
IFT6D    DS    0H                                                               
         SR    R6,R6                                                            
         ICM   R6,7,IBY            POINTER TO BUY ELEM                          
         BZ    IFT7                                                             
         A     R6,AFBY             POINTS TO BUY ENTRY + 1                      
         MVC   ICOPY,BYCOPY-1                                                   
         DROP  R6                                                               
*                                                                               
IFT7     DS    0H                                                               
         LA    R3,1(R3)            BUMP TOTAL AFFID COUNT                       
         CLI   IFILM,0                                                          
         BE    IFT8                                                             
         LA    R4,1(R4)            BUMP AFFID COUNT WITH FILMS                  
*                                                                               
IFT8     DS    0H                                                               
         AH    R7,IELMLEN                                                       
         B     IFT6                                                             
*                                                                               
IFT8B    DS    0H                                                               
         LTR   R3,R3               SKIP REPORT IF NO AFFIDS                     
         BZ    IFTX                                                             
         ST    R3,AFTCT            SET TOTAL AFFID COUNT                        
         ST    R4,AFRCT            SET AFFID FILM COUNT                         
*                                                                               
         CLI   RSETSW,C'Y'         IF HAVE HAD MULTIPLE RECORD SETS             
         BNE   IFT9                FOR ANY INVOICE NEED TO SORT ON DATE         
*                                                                               
         LH    R4,IELMLEN                                                       
         GOTO1 XSORT,DMCB,AFINV,(R3),(R4),2,0                                   
*                                                                               
IFT9     DS    0H                                                               
         L     R6,=A(GCPWKA)       AREA FOR GETCPAT DSECT                       
         ST    R6,AGCPW                                                         
         USING GCPD,R6             LINK-UP DSECT FOR GETCPAT                    
         LR    RE,R6               CLEAR LINK AREA                              
         LA    RF,GCPDL                                                         
         XCEF                                                                   
*                                                                               
         MVC   GCPIO,ADBUY         BUYREC FOR 1ST IOAREA                        
         MVC   GCPIO2,=A(IOAREA2)  2ND IOAREA                                   
         MVC   GCPT0PRF,T0PROF     T0 PROFILE                                   
         L     R2,=A(CMMLTAB)                                                   
         ST    R2,GCPCMTAB         CMML TABLE HERE                              
*                                                                               
         LA    R2,CMTBEL                                                        
         ST    R2,GCPCMTEL         CMML TAB ENTRY LENGTH                        
         LA    R2,CMTBELC                                                       
         ST    R2,GCPCMTCL         LENGTH TO BE CLEAR FOR NEW PATTERN           
         LH    RF,=Y(CMMLTABL)     CMML TAB LENGTH                              
         ST    RF,GCPCMTL                                                       
*                                                                               
         MVC   GCPPIGC,PIGCTL      SET PIGGY-BACK VALUES                        
         MVC   GCPPIGF,PIGFILT                                                  
         XC    THREE,THREE         USED TO PASS PAT REF TO GETCPAT              
         L     R2,APATREF                                                       
*                                                                               
IFT20    DS    0H                                                               
         CLI   NETPSW,C'Y'                                                      
         BNE   IFT30                                                            
*                                                                               
         OC    0(3,R2),0(R2)       ANY PATTERN REF NUMBER                       
         BNZ   IFT26                                                            
         XC    GCPNPRD,GCPNPRD     NO MEANS DONE                                
         MVC   THREE,=X'FFFFFF'    TELL GETCPAT DONE                            
         B     IFT30                                                            
IFT26    MVC   THREE,0(R2)         SET FOR GETCPAT                              
*                                                                               
IFT30    GOTO1 AGETCPAT,DMCB,AGCPW,SPWORKD                                      
*                                                                               
         CLI   GCPPPRD,0           TEST ANY PATTERN THIS TIME                   
         BE    IFT40                                                            
*                                                                               
         GOTO1 DATCON,DMCB,(3,GCPPST),(2,GAFST)   2 BYTE SEARCH DATES           
         GOTO1 (RF),(R1),(3,GCPPEND),(2,GAFEND)                                 
*                                                                               
         LLC   RF,GCPPSLN                                                       
         LLC   RE,GCPPSLN2                                                      
         AR    RF,RE                                                            
         STC   RF,GAFSLN           SET TOTAL SLN FOR SEARCH                     
*                                                                               
IFT40    DS    0H                                                               
         CLI   NETPSW,C'Y'                                                      
         BE    *+12                                                             
         BAS   RE,GAFFID           GET AFFIDS FOR PATTERN                       
         B     *+8                                                              
         BAS   RE,GAFFIDN          GET AFFIDS FOR PATTERN-NET                   
*                                                                               
         CLI   GCPPPRD,0           ONLY DO CATCH-ALL                            
         BNE   IFT45                                                            
         OC    GAFCNT,GAFCNT       IF HAVE FILMS IN IT                          
         BZ    IFT80                                                            
*                                                                               
IFT45    DS    0H                                                               
         BAS   RE,SETPAT           SET PATTERN DESCRIPTION                      
         MVC   LNEED,PDNLNS        LINES NEEDED FOR PATTERN DESC                
         MVC   ANXTPLN,APATLNS     1ST LINE                                     
*                                                                               
         L     R4,GCPCPARS+8                                                    
         L     R5,GCPCPARS+4                                                    
         A     R5,GCPCPARS+12      BYPASS TOTAL LINE FOR NOW                    
         SHI   R4,1                                                             
         BNP   IFT55                                                            
         USING CMTABD,R5                                                        
*                                                                               
IFT50    DS    0H                                                               
         BAS   RE,FMTFLM           FORMAT FILM DATA                             
*                                                                               
         A     R5,GCPCPARS+12      NEXT ENTRY                                   
         BCT   R4,IFT50                                                         
*                                                                               
IFT55    DS    0H                  NOW DO TOTAL LINE                            
         L     R5,GCPCPARS+4                                                    
         BAS   RE,FMTFLM                                                        
*                                  SEE IF ANY UNPRINTED DESC LINES              
         SR    R5,R5                                                            
         ICM   R5,1,PDNLNS                                                      
         BNP   IFT80               NO                                           
         L     R4,ANXTPLN                                                       
*                                                                               
IFT60    DS    0H                                                               
         MVC   P(PLL),0(R4)                                                     
         BAS   RE,RPRT                                                          
         LA    R4,PLL(R4)                                                       
         BCT   R5,IFT60                                                         
*                                                                               
IFT80    DS    0H                                                               
         CLI   GCPPPRD,0           IF DOING NO PATTERN                          
         BNE   IFT90                                                            
         BAS   RE,RPRT                                                          
         MVI   GCPPPRD,X'FF'       SET TO DO TOTALS                             
         CLI   NETPSW,C'Y'         IF NETPAK                                    
         BNE   IFT40                                                            
         MVC   GCPNPRD,=C'POL'     SET TO DO TOTALS                             
         B     IFT40                                                            
*                                                                               
IFT90    DS    0H                                                               
         CLI   NETPSW,C'Y'         IF NETPAK                                    
         BNE   IFT91                                                            
         CLC   GCPNPRD,=C'POL'     SET TO DO TOTALS                             
         B     *+8                                                              
*                                                                               
IFT91    CLI   GCPPPRD,X'FF'       FINISHED IF HAVE JUST DONE TOTALS            
         BE    IFTX                                                             
         BAS   RE,RPRT                                                          
         CLI   NETPSW,C'Y'         IF NETPAK                                    
         BNE   IFT20                                                            
         LA    R2,3(R2)            BUMP TO NEXT PATTERN REF NUMBER              
         B     IFT20                                                            
*                                                                               
IFTX     DS    0H                                                               
         MVC   RCSPACNG,SAVSPCNG                                                
         CLI   IRSKIP,C'Y'         IF HAVE NOT PRINTED                          
         BNE   IFTXX                                                            
         LA    R0,14               CLEAR ALL PLINES NOW                         
         LA    R1,P                                                             
*                                                                               
         MVC   0(132,R1),SPACES                                                 
         LA    R1,132(R1)                                                       
         BCT   R0,*-10                                                          
*                                                                               
IFTXX    DS    0H                                                               
EXIT     XIT1                                                                   
         EJECT                                                                  
*----------------------------------------------------------------               
*        FIND ALL AFFIDS FOR A PATTERN - SPOT                                   
*----------------------------------------------------------------               
GAFFID   NTR1                                                                   
*                                                                               
         XC    GAFCNT,GAFCNT       COUNT OF FILMS                               
         L     R7,AFINV                                                         
GAF2     DS    0H                                                               
         OC    AFTCT,AFTCT         TEST ANY UNUSED AFFIDS LEFT                  
         BZ    GAFX                                                             
GAF3     DS    0H                                                               
         C     R7,ALINV                                                         
         BNL   GAFX                                                             
         CLI   PIGCTL,C'S'         IF DOING PIGS SEPARATELY                     
         BNE   GAF3D                                                            
         CLC   PIGFILT,IPRD2       MUST BE RIGHT PRD                            
         BNE   GAF60                                                            
*                                                                               
GAF3D    DS    0H                                                               
         CLI   ICOPY,X'FF'         TEST AFF ALREADY USED                        
         BE    GAF60                                                            
*                                                                               
         OC    IBY,IBY             IF NOT MATCHED                               
         BNZ   GAF4                                                             
         CLC   =C'NO',QEST         AND RUNNING BY EST                           
         BE    GAF4                                                             
         CLC   IEST,BEST                                                        
         BNE   GAF60                                                            
*                                                                               
GAF4     DS    0H                                                               
         CLI   IFILM,0             IF NO FILM                                   
         BNE   GAF6                                                             
         CLI   I2CPROF+4,C'Y'      MISSING FILM (NONE) = FILM ERR?              
         BNE   GAF5                NO                                           
         CLI   TRAFIDSC,C'Y'       NO FILM DISCREPANT?                          
         BNE   GAF5                NO                                           
         MVI   FILMDSCP,C'Y'       YES - SET HAVE FILM ERROR                    
*                                                                               
GAF5     CLI   GCPPPRD,X'FF'       ACCEPT ONLY IF TOTAL PASS                    
         BE    GAF50                                                            
         B     GAF60                                                            
*                                                                               
GAF6     DS    0H                                                               
         CLI   GCPPPRD,0           END OF PATTERNS                              
         BE    GAF50               ACCEPT ALL REMAINING THAT HAVE FILMS         
*                                                                               
         CLC   IDAT,GAFST          TEST VS START                                
         BL    GAF60               SKIP IF LOW                                  
         CLC   IDAT,GAFEND         VS END                                       
         BH    GAF60               SKIP IF HIGH (CANNOT RELY ON                 
*                                  INVOICE BEING IN ORDER)                      
*                                                                               
GAF6AA   OC    GCPPSTIM(4),GCPPSTIM ANY PATTERN START/END TIME?                 
         BZ    GAF6B               NO                                           
*                                                                               
         MVC   HALF,ITIM           INVOICE TIME                                 
         TM    ISTAT2,X'08'        ITIM ALREADY CONVERTED?                      
         BNZ   GAF6AAA             YES                                          
         XR    R1,R1               CONVERT ITIM TO HHMM LIKE PAT TIMES          
         ICM   R1,3,ITIM                                                        
         XR    R0,R0                                                            
         D     R0,=F'60'           DIVIDE BY 60                                 
         MHI   R1,100              MULTIPLY BY 100                              
         AR    R0,R1               ADD THE REMAINDER                            
         CHI   R0,2400             GREATER THEN 2400?                           
         BL    *+8                 NO                                           
         SHI   R0,2400             YES - SUBTRACT 2400                          
         STCM  R0,3,HALF           HALF =  CONVERTED ITIM                       
*                                                                               
GAF6AAA  TM    GCPFLAGS,GCPDAILY   CHECK TIMES DAILY?                           
         BNZ   *+14                YES                                          
         CLC   IDAT,GAFST          INVOICE DATE = PATTERN START DATE?           
         BNE   *+14                NO                                           
         CLC   HALF,GCPPSTIM       INV TIME BEFORE PATTERN START?               
         BL    GAF60               YES - SKIP                                   
*                                                                               
         TM    GCPFLAGS,GCPDAILY   CHECK TIMES DAILY?                           
         BNZ   *+14                YES                                          
         CLC   IDAT,GAFEND         INVOICE DATE = PATTERN END DATE              
         BNE   GAF6B                                                            
         CLC   HALF,GCPPETIM       INVOICE TIME AFTER PATTERN END?              
         BH    GAF60               YES - SKIP                                   
*                                                                               
GAF6B    CLC   ILEN,GAFSLN         SLN                                          
         BNE   GAF60                                                            
*                                                                               
         CLI   GCPPCCE,C'Y'        IF COPY CODE=EST                             
         BNE   GAF6D               TEST EST = COPY CODE                         
         CLI   IEST,0                                                           
         BE    GAF6C                                                            
         CLC   IEST,GCPPCPY        YES- TEST = COPY                             
         BNE   GAF60                                                            
         B     GAF7                                                             
*                                                                               
GAF6C    DS    0H                                                               
         SR    RF,RF               USE BUY EST FOR CHECK                        
         ICM   RF,7,IBY                                                         
         BZ    GAF60                                                            
         A     RF,AFBY                                                          
         BCTR  RF,R0                                                            
         USING BYELEMD,RF                                                       
         CLC   BYEST,GCPPCPY                                                    
         BNE   GAF60                                                            
         B     GAF7                                                             
         DROP  RF                                                               
*                                                                               
GAF6D    DS    0H                                                               
         CLI   T0PROF+10,C'Y'      FOR COPY=DAYPART CLIENTS                     
         BE    GAF7                SKIP COPY CHECK                              
         CLI   T0PROF+10,C'D'                                                   
         BE    GAF7                                                             
         CLC   ICOPY,GCPPCPY       COPY                                         
         BNE   GAF60                                                            
*                                                                               
GAF7     DS    0H                  PRODUCT                                      
         CLI   IPRD,X'FF'          IF INV ITEM IS POL                           
         BNE   GAF8                                                             
         SR    RF,RF               USE BUY EST FOR CHECK                        
         ICM   RF,7,IBY                                                         
         BZ    GAF60                                                            
         A     RF,AFBY                                                          
         BCTR  RF,R0                                                            
         USING BYELEMD,RF                                                       
         CLC   BYPRD,GCPPPRD                                                    
         BNE   GAF60                                                            
         CLC   BYPRD2,GCPPPRD2                                                  
         BNE   GAF60                                                            
         B     GAF50                                                            
         DROP  RF                                                               
*                                                                               
GAF8     DS    0H                  ELSE USE INV PRODUCT                         
         CLC   IPRD,GCPPPRD        PRD1                                         
         BNE   GAF60                                                            
         MVI   BYTE,0              PRD2, ZERO                                   
         MVC   BYTE,IPRD2          ELSE PRD IS THERE                            
         CLC   BYTE,GCPPPRD2       PRD2                                         
         BNE   GAF60                                                            
***                                                                             
* ACCEPT THIS AFFID                                                             
***                                                                             
GAF50    BAS   RE,BUILDCML    BUILD CMML ENTRY IN WORK                          
*                                                                               
         LA    R5,WORK        WORK HAS CMML TABLE ENTRY!                        
         USING CMTABD,R5                                                        
*                                                                               
         GOTO1 BINSRCH,GCPCPARS,(1,CMTABD)                                      
         SR    RF,RF                                                            
         ICM   RF,7,1(R1)                                                       
         BNZ   *+6                                                              
         DC    H'0'                TABLE FULL                                   
         CLI   0(R1),1             TEST ALREADY THERE                           
         BE    GAF56                                                            
*                                                                               
GAF54    DS    0H                  ADD INTO EXISTING ENTRY                      
         MVC   CMADID-CMTABD(16,RF),CMADID  MOVE IN AD ID                       
         OC    CMACTV-CMTABD(1,RF),CMACTV   OR IN ACTV SW                       
         CLI   CMROT-CMTABD(RF),0                                               
         BNE   *+10                                                             
         MVC   CMROT-CMTABD(1,RF),CMROT   SET ROTATION LETTER                   
         LA    R4,CMSPTS-CMTABD(RF)                                             
         LA    R3,CMSPTS                                                        
         BRAS  RE,ADDEMUP                                                       
*                                                                               
GAF56    DS    0H                  ADD INTO TOTAL ENTRY                         
         MVC   BYTE,CMROT-CMTABD(RF)    SAVE ROTATION LETTER                    
         L     RF,GCPCMTAB         FIRST ENTRY IS FOR TOTALS                    
*                                                                               
         LA    R4,CMSPTS-CMTABD(RF)                                             
         LA    R3,CMSPTS                                                        
         BRAS  RE,ADDEMUP                                                       
         DROP  R5                                                               
*                                                                               
GAF56B   DS    0H                                                               
         L     RF,GAFCNT                                                        
         A     RF,AFLMRUN          POINT INTO AS RUN LIST                       
*                                                                               
         L     RE,=A(FLMRUNX)      AREA FOR FILM RUN LIST                       
         CR    RF,RE               DON'T BLOW PAST BUFFER                       
         BNL   GAF56C                                                           
*                                                                               
         MVC   0(1,RF),BYTE        SET ROTATION LETTER                          
         MVI   1(RF),X'FF'         SET EOL                                      
*                                                                               
GAF56C   L     RF,GAFCNT           BUMP INVOICE ITEM COUNT                      
         LA    RF,1(RF)                                                         
         ST    RF,GAFCNT                                                        
*                                                                               
GAF60    DS    0H                                                               
         AH    R7,IELMLEN                                                       
         B     GAF3                                                             
*                                                                               
GAFX     DS    0H                                                               
         B     EXIT                                                             
         EJECT                                                                  
*----------------------------------------------------------------               
*        FIND ALL AFFIDS FOR A PATTERN - NETPAK                                 
*----------------------------------------------------------------               
GAFFIDN  NTR1                                                                   
*                                                                               
         XC    GAFCNT,GAFCNT       COUNT OF FILMS                               
         L     R7,AFINV                                                         
GAN2     DS    0H                                                               
         OC    AFTCT,AFTCT         TEST ANY UNUSED AFFIDS LEFT                  
         BZ    GANX                                                             
GAN3     DS    0H                                                               
         C     R7,ALINV                                                         
         BNL   GANX                                                             
*        CLI   PIGCTL,C'S'         IF DOING PIGS SEPARATELY                     
*        BNE   GAN3D                                                            
*        CLC   PIGFILT,IPRD2       MUST BE RIGHT PRD                            
*        BNE   GAN60                                                            
*                                                                               
GAN3D    DS    0H                                                               
         CLI   ICOPY,X'FF'         TEST AFF ALREADY USED                        
         BE    GAN60                                                            
*                                                                               
         OC    IBY,IBY             IF NOT MATCHED                               
         BNZ   GAN4                                                             
         CLC   =C'NO',QEST         AND RUNNING BY EST                           
         BE    GAN4                                                             
         CLC   IEST,BEST                                                        
         BNE   GAN60                                                            
*                                                                               
GAN4     DS    0H                                                               
         CLI   IFILM,0             IF NO FILM                                   
         BNE   GAN6                                                             
         CLI   I2CPROF+4,C'Y'      MISSING FILM (NONE) = FILM ERR?              
         BNE   GAN5                NO                                           
         CLI   TRAFIDSC,C'Y'       NO FILM DISCREPANT?                          
         BNE   GAN5                NO                                           
         MVI   FILMDSCP,C'Y'       YES - SET HAVE FILM ERROR                    
*                                                                               
GAN5     CLC   GCPNPRD,=C'POL'     ACCEPT ONLY IF TOTAL PASS                    
         BE    GAN50                                                            
         B     GAN60                                                            
*                                                                               
GAN6     DS    0H                                                               
         CLI   GCPPPRD,0           END OF PATTERNS                              
         BE    GAN50               ACCEPT ALL REMAINING THAT HAVE FILMS         
*                                                                               
         OC    IBY,IBY             IF MATCHED                                   
         BZ    GAN6A                                                            
         SR    RF,RF               USE PAT REF FROM BUY                         
         ICM   RF,7,IBY                                                         
         BZ    GAN60                                                            
         A     RF,AFBY                                                          
         BCTR  RF,R0                                                            
         USING BYELEMD,RF                                                       
         CLC   BYPREF,THREE        HAVE PAT REF NUMBER ON UNIT                  
         BE    GAN50               ACCEPT IF MATCHES                            
         B     GAN60               SKIP IF NOT SAME REFERENCE NUM               
*                                                                               
GAN6A    CLC   IDAT,GAFST          TEST VS START                                
         BL    GAN60               SKIP IF LOW                                  
         CLC   IDAT,GAFEND         VS END                                       
         BH    GAN60               SKIP IF HIGH (CANNOT RELY ON                 
*                                  INVOICE BEING IN ORDER)                      
         OC    GCPPSTIM(4),GCPPSTIM ANY PATTERN START/END TIME?                 
         BZ    GAN6B               NO                                           
*                                                                               
         MVC   HALF,ITIM           INVOICE TIME                                 
         TM    ISTAT2,X'08'        ITIM ALREADY CONVERTED?                      
         BNZ   GAN6AA              YES                                          
         XR    R1,R1               CONVERT ITIM TO HHMM LIKE PAT TIMES          
         ICM   R1,3,ITIM                                                        
         XR    R0,R0                                                            
         D     R0,=F'60'           DIVIDE BY 60                                 
         MHI   R1,100              MULTIPLY BY 100                              
         AR    R0,R1               ADD THE REMAINDER                            
         CHI   R0,2400             GREATER THEN 2400?                           
         BL    *+8                 NO                                           
         SHI   R0,2400             YES - SUBTRACT 2400                          
         STCM  R0,3,HALF           HALF =  CONVERTED ITIM                       
*                                                                               
GAN6AA   CLC   IDAT,GAFST          INVOICE DATE = PATTERN START DATE?           
         BNE   *+14                NO                                           
         CLC   HALF,GCPPSTIM       INV TIME BEFORE PATTERN START?               
         BL    GAN60               YES - SKIP                                   
*                                                                               
         CLC   IDAT,GAFEND         INVOICE DATE = PATTERN END DATE              
         BNE   GAN6B               NO                                           
         CLC   HALF,GCPPETIM       INVOICE TIME AFTER PATTERN END?              
         BH    GAN60               YES - SKIP                                   
*                                                                               
GAN6B    CLC   ILEN,GAFSLN         SLN                                          
         BNE   GAN60                                                            
*                                                                               
*****    CLI   GCPPCCE,C'Y'        IF COPY CODE=EST                             
****     BNE   GAN6D               TEST EST = COPY CODE                         
****     CLI   IEST,0                                                           
****     BE    GAN6C                                                            
****     CLC   IEST,GCPPCPY        YES- TEST = COPY                             
****     BNE   GAN60                                                            
****     B     GAN7                                                             
*                                                                               
GAN6C    DS    0H                                                               
*****    SR    RF,RF               USE BUY EST FOR CHECK                        
******   ICM   RF,7,IBY                                                         
******   BZ    GAN60                                                            
*****    A     RF,AFBY                                                          
*****    BCTR  RF,R0                                                            
*****    USING BYELEMD,RF                                                       
****     CLC   BYEST,GCPPCPY                                                    
****     BNE   GAN60                                                            
****     B     GAN7                                                             
***      DROP  RF                                                               
*                                                                               
GAN6D    DS    0H                                                               
***      CLI   T0PROF+10,C'Y'      FOR COPY=DAYPART CLIENTS                     
***      BE    GAN7                SKIP COPY CHECK                              
***      CLI   T0PROF+10,C'D'                                                   
***      BE    GAN7                                                             
***      CLC   ICOPY,GCPPCPY       COPY                                         
***      BNE   GAN60                                                            
*                                                                               
GAN7     DS    0H                  PRODUCT                                      
         CLI   IPRD,X'FF'          IF INV ITEM IS POL                           
         BNE   GAN8                                                             
         SR    RF,RF               USE BUY EST FOR CHECK                        
         ICM   RF,7,IBY                                                         
         BZ    GAN60                                                            
         A     RF,AFBY                                                          
         BCTR  RF,R0                                                            
         USING BYELEMD,RF                                                       
         CLC   BYPRDNT,GCPNPRD                                                  
         BNE   GAN60                                                            
         CLC   BYPRD2NT,GCPNPRD2                                                
         BNE   GAN60                                                            
         B     GAN50                                                            
         DROP  RF                                                               
*                                                                               
GAN8     DS    0H                  ELSE USE INV PRODUCT                         
         CLC   IPRDNT,GCPNPRD      PRD1                                         
         BNE   GAN60                                                            
         CLC   IPRD2NT,GCPNPRD2    PRD2                                         
         BNE   GAN60                                                            
***                                                                             
* ACCEPT THIS AFFID                                                             
***                                                                             
GAN50    BAS   RE,BUILDCML    BUILD CMML ENTRY IN WORK                          
*                                                                               
         LA    R5,WORK        WORK HAS CMML TABLE ENTRY!                        
         USING CMTABD,R5                                                        
*                                                                               
         GOTO1 BINSRCH,GCPCPARS,(1,CMTABD)                                      
         SR    RF,RF                                                            
         ICM   RF,7,1(R1)                                                       
         BNZ   *+6                                                              
         DC    H'0'                TABLE FULL                                   
         CLI   0(R1),1             TEST ALREADY THERE                           
         BE    GAN56                                                            
*                                                                               
GAN54    DS    0H                  ADD INTO EXISTING ENTRY                      
         MVC   CMADID-CMTABD(16,RF),CMADID  MOVE IN AD ID                       
         OC    CMACTV-CMTABD(1,RF),CMACTV   OR IN ACTV SW                       
         CLI   CMROT-CMTABD(RF),0                                               
         BNE   *+10                                                             
         MVC   CMROT-CMTABD(1,RF),CMROT   SET ROTATION LETTER                   
         LA    R4,CMSPTS-CMTABD(RF)                                             
         LA    R3,CMSPTS                                                        
         BRAS  RE,ADDEMUP                                                       
*                                                                               
GAN56    DS    0H                  ADD INTO TOTAL ENTRY                         
         MVC   BYTE,CMROT-CMTABD(RF)    SAVE ROTATION LETTER                    
         L     RF,GCPCMTAB         FIRST ENTRY IS FOR TOTALS                    
*                                                                               
         LA    R4,CMSPTS-CMTABD(RF)                                             
         LA    R3,CMSPTS                                                        
         BRAS  RE,ADDEMUP                                                       
         DROP  R5                                                               
*                                                                               
         L     RF,GAFCNT                                                        
         A     RF,AFLMRUN          POINT INTO AS RUN LIST                       
*                                                                               
         L     RE,=A(FLMRUNX)      AREA FOR FILM RUN LIST                       
         CR    RF,RE               DON'T BLOW PAST BUFFER                       
         BNL   GAN56C                                                           
*                                                                               
         MVC   0(1,RF),BYTE        SET ROTATION LETTER                          
         MVI   1(RF),X'FF'         SET EOL                                      
*                                                                               
GAN56C   L     RF,GAFCNT           BUMP INVOICE ITEM COUNT                      
         LA    RF,1(RF)                                                         
         ST    RF,GAFCNT                                                        
*                                                                               
GAN60    DS    0H                                                               
         AH    R7,IELMLEN                                                       
         B     GAN3                                                             
*                                                                               
GANX     DS    0H                                                               
         B     EXIT                                                             
         SPACE 2                                                                
*                                                                               
BUILDCML NTR1                                                                   
         MVI   ICOPY,X'FF'    SET USED                                          
         L     RF,AFTCT       DECREMENT REMAINING COUNT                         
         BCTR  RF,R0                                                            
         ST    RF,AFTCT                                                         
*                             GET FILM CODE                                     
         LA    R5,WORK                                                          
         USING CMTABD,R5                                                        
         XC    WORK,WORK                                                        
*                                                                               
         LA    R3,IFILM                                                         
         LA    R4,CMFILM           CODE                                         
         MVI   BYTE,0                                                           
         BAS   RE,GAFGTFLM                                                      
*                                                                               
         CLI   IFILM2,0                                                         
         BE    BCML10                                                           
         LA    R3,IFILM2                                                        
         LA    R4,CMFILM+8                                                      
         MVI   BYTE,0                                                           
         BAS   RE,GAFGTFLM                                                      
*                                                                               
BCML10   MVI   CMROT,C'*'          ASSUME NO ROTATION LETTER                    
         OI    CMACTV,X'40'        FILM IS RUN                                  
*                                                                               
         MVC   CMSPTS,=F'1'                                                     
         ICM   RF,15,ICOST                                                      
         ST    RF,CMDOLS                                                        
         SR    RF,RF                                                            
         ICM   RF,15,IDEMVR                                                     
         ST    RF,CMDEMV                                                        
*                                                                               
         GOTO1 BINSRCH,GCPCPARS,(2,CMTABD)      READ HIGH FIRST!                
         CLI   0(R1),1             FOUND AN ENTRY?                              
         BE    BCML20              NO - WE BLEW PAST THE TABLE                  
         SR    RF,RF                                                            
         ICM   RF,7,1(R1)          HAVE A VALID ENTRY?                          
         BZ    BCML20              NO                                           
         CLC   CMFILM,0(RF)        DID WE REALLY FIND IT?                       
         BE    BCMLX               YES                                          
*                                                                               
BCML20   LA    R3,IFILM                                                         
         LA    R4,CMFILM           CODE                                         
         MVI   BYTE,1                                                           
         BAS   RE,GAFGTFLM                                                      
*                                                                               
         CLI   IFILM2,0                                                         
         BE    BCML30                                                           
         LA    R3,IFILM2                                                        
         LA    R4,CMFILM+8                                                      
         MVI   BYTE,1                                                           
         BAS   RE,GAFGTFLM                                                      
*                                                                               
BCML30   GOTO1 BINSRCH,GCPCPARS,(2,CMTABD)      READ HIGH FIRST!                
         CLI   0(R1),1             FOUND AN ENTRY?                              
         BE    BCML40              NO - WE BLEW PAST THE TABLE                  
         SR    RF,RF                                                            
         ICM   RF,7,1(R1)          HAVE A VALID ENTRY?                          
         BZ    BCML40              NO                                           
         CLC   CMFILM,0(RF)        DID WE REALLY FIND IT?                       
         BE    BCMLX               YES                                          
*                                                                               
BCML40   LA    R3,IFILM                                                         
         LA    R4,CMFILM           CODE                                         
         MVI   BYTE,0                                                           
         BAS   RE,GAFGTFLM                                                      
*                                                                               
         CLI   IFILM2,0                                                         
         BE    BCMLX                                                            
         LA    R3,IFILM2                                                        
         LA    R4,CMFILM+8                                                      
         MVI   BYTE,0                                                           
         BAS   RE,GAFGTFLM                                                      
*                                                                               
BCMLX    B     EXIT                                                             
*                                                                               
GAFGTFLM DS    0H                  FORMAT FILMS                                 
         ICM   R0,15,FLMTPARS+8                                                 
         BZ    GAFGF8                                                           
         L     RF,AFLMTAB                                                       
         USING FLMTABD,RF                                                       
*                                                                               
GAFGF4   DS    0H                                                               
         CLC   FLMIDNO,IINVID      TST RIGHT INVOICE REC                        
         BNE   GAFGF10                                                          
         CLC   FLMICOD,0(R3)       INTERNAL FILM NO.                            
         BNE   GAFGF10                                                          
         CLI   BYTE,0              LOOK UP REGULAR CMML ENTRY?                  
         BNE   GAFGF4A             NO - LOOK UP CENTERCUT ENTRY!                
         OC    FLMCPTR,FLMCPTR     CENTERCUT POINTER ENTRY?                     
         BNZ   GAFGF10             YES - DON'T PROCESS THIS ONE!                
         B     GAFGF5                                                           
*                                                                               
GAFGF4A  OC    FLMCPTR,FLMCPTR     CENTERCUT POINTER ENTRY?                     
         BZ    GAFGF10             NO - ONLY PROCESS CENTERCUT!                 
*                                                                               
GAFGF5   MVC   0(8,R4),FLMCOD      SET FILM CODE                                
*                                                                               
         TM    ISTAT3,IST3ADID+IST3HDEF   INVOICE COMML IS AD-ID                
         BZR   RE                                                               
         MVC   16(8,R4),FLMADID    YES, MOVE IN THE AD-ID                       
         CLC   0(8,R4),SPACES      HAVE FILMCODE?                               
         BH    *+10                YES                                          
         MVC   0(8,R4),FLMADID     NO - AD-ID IS KEY                            
         BR    RE                                                               
*                                                                               
GAFGF8   DS    0H                                                               
         MVC   0(8,R4),=C'*NO FILM'                                             
         BR    RE                                                               
*                                                                               
GAFGF10  DS    0H                                                               
         LA    RF,FLMTABEL(RF)                                                  
         BCT   R0,GAFGF4                                                        
         B     GAFGF8                                                           
         DROP  RF                                                               
         SPACE 3                                                                
*----------------------------------------------------------------               
*        SET PATTERN DESC                                                       
*----------------------------------------------------------------               
SETPAT   NTR1                                                                   
*                                                                               
         L     R6,AGCPW                                                         
         USING GCPD,R6                                                          
         L     R3,APATLNS          CLEAR THE AREA                               
         LA    R0,50                                                            
*                                                                               
SP3      DS    0H                                                               
         MVC   0(PLL,R3),SPACES                                                 
         LA    R3,PLL(R3)                                                       
         BCT   R0,SP3                                                           
*                                                                               
         L     R3,APATLNS                                                       
*                                                                               
         CLI   GCPPPRD,0           NO PATTERN                                   
         BNE   SP3B                                                             
         CLI   I2CPROF+2,C'Y'      SPOT NOT COVERED = FILM ERROR                
         BNE   *+8                 NO                                           
         MVI   FILMDSCP,C'Y'       YES - INDICATE FILM ERROR                    
         MVC   0(33,R3),=C'**  SPOTS RUN BUT NOT COVERED  **'                   
         MVC   PLL(33,R3),=C'**       BY ANY PATTERN        **'                 
         LA    R3,PLL*2(R3)                                                     
         B     SP90                                                             
*                                                                               
SP3B     DS    0H                                                               
         CLI   NETPSW,C'Y'         NETPAK?                                      
         BNE   SP3B1               NO                                           
         CLC   GCPNPRD,=C'POL'     TOTALS                                       
         B     *+8                                                              
*                                                                               
SP3B1    CLI   GCPPPRD,X'FF'       TOTALS                                       
         BNE   SP4                                                              
         MVC   0(18,R3),=C'**  FILM RECAP  **'                                  
         LA    R3,PLL(R3)                                                       
         B     SP90                                                             
*                                                                               
SP4      DS    0H                  1ST PRODUCT                                  
         MVC   7(PDL-7,R3),DOTS                                                 
         MVC   0(7,R3),=C'PRODUCT'                                              
         CLI   NETPSW,C'Y'         NETPAK?                                      
         BNE   SP5                 NO                                           
         MVC   PDL(3,R3),GCPNPRD                                                
         L     RF,PRDBUFF                                                       
         LH    RE,PRDBUFLN                                                      
         LA    R1,220                                                           
*                                                                               
SP4A     CLC   GCPNPRD,PTPRDA-PTBUFFD(RF)                                       
         BNE   SP4B                                                             
         MVC   PDL+4(20,R3),PTNAME-PTBUFFD(RF)                                  
         B     SP4C                                                             
*                                                                               
SP4B     AR    RF,RE                                                            
         BCT   R1,SP4A                                                          
*                                                                               
SP4C     OC    PDL(24,R3),SPACES                                                
         LA    R3,PLL(R3)                                                       
         CLI   GCPPPRD2,0          2ND PROD                                     
         BE    SP6                                                              
         MVC   PDL(3,R3),GCPNPRD                                                
         OC    PDL(24,R3),SPACES                                                
         LA    R3,PLL(R3)                                                       
         B     SP6                                                              
*                                                                               
SP5      LA    R1,GCPPPRD                                                       
         BAS   RE,SPFPRD                                                        
         LA    R3,PLL(R3)                                                       
*                                                                               
         CLI   GCPPPRD2,0          2ND PROD                                     
         BE    SP6                                                              
         LA    R1,GCPPPRD2                                                      
         BAS   RE,SPFPRD                                                        
         CLC   PDL(24,R3),SPACES                                                
         BE    SP6                                                              
         LA    R3,PLL(R3)                                                       
*                                                                               
SP6      DS    0H                  SPOT LENGTHS                                 
         MVC   11(PDL-11,R3),DOTS                                               
         MVC   0(11,R3),=C'SPOT LENGTH'                                         
         LLC   R0,GCPPSLN                                                       
         LA    R4,PDL(R3)                                                       
         BAS   RE,SPFSLN                                                        
         CLI   GCPPSLN2,0                                                       
         BE    SP8                                                              
         AR    R4,R0                                                            
         MVI   0(R4),C'-'                                                       
         LA    R4,1(R4)                                                         
         LLC   R0,GCPPSLN2                                                      
         BAS   RE,SPFSLN                                                        
*                                                                               
SP8      DS    0H                  COPY                                         
         AR    R4,R0                                                            
         CLI   GCPPCPY,0                                                        
         BE    SP9                                                              
         MVC   2(5,R4),=C'COPY='                                                
         CLI   GCPPCCE,C'Y'        TEST COPY COPE = EST                         
         BE    SP8B                                                             
         MVC   7(1,R4),GCPPCPY     NO JUST SHOW COPY CODE                       
         LA    R4,8(R4)                                                         
         B     SP9                                                              
*                                                                               
SP8B     DS    0H                  COPY CODE=EST                                
         EDIT  (B1,GCPPCPY),(3,7(R4)),FILL=0                                    
         LA    R4,10(R4)                                                        
*                                                                               
SP9      DS    0H                                                               
         MVC   2(4,R4),=C'REF='                                                 
         SR    R0,R0                                                            
         ICM   R0,7,GCPPREF                                                     
         EDIT  (R0),(7,6(R4)),ALIGN=LEFT,ZERO=NOBLANK                           
*                                                                               
         LA    R3,PLL(R3)                                                       
*                                                                               
         CLI   GCPPDESC,C' '       DESCRIPTION                                  
         BNH   SP10                                                             
         MVC   12(PDL-12,R3),DOTS                                               
         MVC   0(12,R3),=C'PATTERN DESC'                                        
         MVC   PDL(16,R3),GCPPDESC                                              
         LA    R3,PLL(R3)                                                       
*                                                                               
SP10     DS    0H                  START-END                                    
         MVC   9(PDL-9,R3),DOTS                                                 
         MVC   0(9,R3),=C'START-END'                                            
         GOTO1 DATCON,DMCB,(3,GCPPST),(5,PDL(R3))                               
         GOTO1 (RF),(R1),(3,GCPPEND),(5,PDL+9(R3))                              
         MVI   PDL+8(R3),C'-'                                                   
         LA    R3,PLL(R3)                                                       
*                                                                               
SP11     DS    0H                  PATTERN TIMES                                
         MVC   5(PDL-5,R3),DOTS                                                 
         MVC   0(5,R3),=C'TIMES'                                                
         OC    GCPPSTIM(4),GCPPSTIM ANY PATTERN START/END TIME?                 
         BZ    SP11A               NO - SKIP UNTIME CALL!                       
*                                                                               
         GOTO1 UNTIME,DMCB,GCPPSTIM,PDL(R3)                                     
*                                                                               
SP11A    LA    R3,PLL(R3)                                                       
*                                                                               
SP12     DS    0H                                                               
         CLI   GCPPTTNL,1          SKIP PATTERN ANALYSIS                        
         BNH   SP16                IF PATTERN LENGTH IS 1                       
         CLI   TRAFROT,C'Y'        OPTION TO PRINT ROTATIONS                    
         BNE   SP16                                                             
         MVC   8(PDL-8,R3),DOTS                                                 
         MVC   0(8,R3),=C'ROTATION'                                             
*                                                                               
         LA    R4,GCPPTTN                                                       
         LLC   R5,GCPPTTNL                                                      
         BAS   RE,SPFROT                                                        
*                                      AS RUN ROTATION                          
         OC    GAFCNT,GAFCNT                                                    
         BZ    SP16                                                             
         MVC   6(PDL-6,R3),DOTS                                                 
         MVC   0(6,R3),=C'AS RUN'                                               
         L     R4,AFLMRUN                                                       
         L     R5,GAFCNT                                                        
         CH    R5,=Y((PLL-PDL)*5)  ONLY DO 5 LINES WORTH                        
         BNH   SP13                                                             
         LH    R5,=Y((PLL-PDL)*5)                                               
         LA    R7,PLL*2(R3)                                                     
         MVC   1(6,R7),=C'(FIRST'                                               
         MVC   PLL+1(11,R7),=C'SPOTS ONLY)'                                     
         EDIT  (R5),(4,8(R7)),ALIGN=LEFT                                        
*                                                                               
SP13     DS    0H                                                               
         BAS   RE,SPFROT                                                        
*                                                                               
*                             COUNT PATTERN APPEARANCES IN RUN LIST             
         L     R5,GAFCNT           RUN COUNT                                    
         LLC   R4,GCPPTTNL         PATTERN LENGTH                               
*                                                                               
         GOTO1 ATSCAN,DMCB,AFLMRUN,(R5),((R4),GCPPTTN),((R4),GCPPTTN)           
*                                                                               
*                                  SHOW APPEARANCES/POSSIBLES                   
         LR    R5,R3                                                            
         MVI   0(R5),C'('                                                       
         LA    R5,1(R5)                                                         
         LLC   R0,DMCB+16                                                       
         EDIT  (R0),(4,0(R5)),ALIGN=LEFT,ZERO=NOBLANK                           
         AR    R5,R0                                                            
         MVC   1(18,R5),=C'OCCURRENCES OUT OF'                                  
         LA    R5,20(R5)                                                        
         L     R1,GAFCNT                                                        
         SR    R0,R0                                                            
         LLC   RF,GCPPTTNL                                                      
         DR    R0,RF                                                            
         EDIT  (R1),(4,0(R5)),ALIGN=LEFT                                        
         AR    R5,R0                                                            
         MVC   1(9,R5),=C'POSSIBLE)'                                            
         LA    R3,PLL(R3)                                                       
*                                                                               
SP16     DS    0H                                                               
*                                                                               
SP90     DS    0H                                                               
         MVI   0(R3),X'FF'         SET EOL                                      
*                                                                               
         LR    R1,R3                                                            
         SR    R0,R0                                                            
         S     R1,APATLNS                                                       
         D     R0,=A(PLL)                                                       
         STC   R1,PDNLNS           NO. OF LINES USED                            
*                                                                               
         B     EXIT                                                             
*                                                                               
*----------------------------------------------------------------               
*                                                                               
SPFPRD   DS    0H                  PRODUCTS                                     
         LLC   RF,0(R1)                                                         
         BCTR  RF,R0                                                            
         MH    RF,PRDBUFLN                                                      
         A     RF,PRDBUFF                                                       
         MVC   PDL(3,R3),PTPRDA-PTBUFFD(RF)                                     
         MVC   PDL+4(20,R3),PTNAME-PTBUFFD(RF)                                  
         OC    PDL(24,R3),SPACES                                                
         CLC   PDL(24,R3),SPACES   TEST PRODUCT FOUND                           
         BNER  RE                  YES - DONE                                   
*                                                                               
         MVC   BYTE,0(R1)          NO - GET CODE FROM CLT HDR                   
         L     R1,ADCLT                                                         
         LA    R1,CLIST-CLTHDR(R1)                                              
         LA    RF,220              220 PRODS IN CLIST                           
SPFP4    DS    0H                                                               
         CLI   3(R1),0                                                          
         BER   RE                                                               
         CLC   BYTE,3(R1)                                                       
         BE    SPFP6                                                            
         LA    R1,4(R1)                                                         
         BCT   RF,SPFP4                                                         
*                                                                               
         CLI   NETPSW,C'Y'         TEST NETPAK                                  
         BE    *+6                 ONLY CLIST2 FOR NET                          
         DC    H'0'                                                             
*                                                                               
         L     R1,ADCLT                                                         
         LA    R1,CLIST2-CLTHDR(R1)                                             
         LA    RF,35               35 PRODUCTS IN CLIST2                        
SPFP4A   CLI   3(R1),0                                                          
         BER   RE                                                               
         CLC   BYTE,3(R1)                                                       
         BE    SPFP6                                                            
         LA    R1,4(R1)                                                         
         BCT   RF,SPFP4A                                                        
         DC    H'0'                                                             
*                                                                               
SPFP6    DS    0H                                                               
         MVC   PDL(3,R3),0(R1)                                                  
         BR    RE                                                               
*                                                                               
SPFSLN   DS    0H                  SLN'S                                        
         EDIT  (R0),(3,0(R4)),ALIGN=LEFT                                        
         BR    RE                                                               
*----------------------------------------------------------------               
*        FORMAT ROTATIONS                                                       
*----------------------------------------------------------------               
SPFROT   DS    0H                                                               
         LTR   R5,R5                                                            
         BNPR  RE                                                               
*                                                                               
SPFROT2  DS    0H                                                               
         LR    RF,R5                                                            
         CH    RF,=Y(PLL-PDL)                                                   
         BNH   *+8                                                              
         LA    RF,PLL-PDL                                                       
         BCTR  RF,R0                                                            
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   PDL(0,R3),0(R4)                                                  
         LA    RF,1(RF)                                                         
*                                                                               
         LA    R3,PLL(R3)                                                       
         AR    R4,RF                                                            
         SR    R5,RF                                                            
         BP    SPFROT2                                                          
*                                                                               
         BR    RE                                                               
         EJECT                                                                  
*----------------------------------------------------------------               
*        FORMAT DATA FOR A FILM                                                 
*----------------------------------------------------------------               
FMTFLM   NTR1                                                                   
         USING CMTABD,R5                                                        
         LA    R2,P                                                             
         USING LNFD,R2                                                          
*                                                                               
         L     RF,ANXTPLN                                                       
         CLC   =C'**  FILM RECAP  **',0(RF)                                     
         BNE   FMF00                                                            
         MVI   0(R2),X'41'                                                      
         LA    R2,132(R2)                                                       
*                                                                               
FMF00    MVC   LNFFILM(8),=C'*TOTALS*'                                          
         CLI   CMFILM,0                                                         
         BE    FMF6                                                             
*                                                                               
         CLI   NETPSW,C'Y'         NETPAK?                                      
         BNE   FMF1                NO                                           
         CLC   GCPNPRD,=C'POL'     UNLESS DOINF MATCH TOTALS                    
         B     *+8                                                              
*                                                                               
FMF1     CLI   GCPPPRD,X'FF'       UNLESS DOING MATCH TOTALS                    
         BE    FMF3                                                             
         CLI   CMACTV,0            FILM MUST BE ACTIVE FOR PATTERN              
         BE    FMFX                                                             
*                                                                               
FMF3     DS    0H                                                               
         MVC   LNFFILM,CMFILM                                                   
         LA    R4,CMFILM           TEST REAL FILM                               
         MVI   BYTE,C'F'                                                        
         OC    CMADID(8),CMADID    IS THIS AN AD-ID?                            
         BZ    FMF3A               NO                                           
         LA    R4,CMADID           TEST READ AD-ID                              
         MVI   BYTE,C'A'                                                        
         GOTO1 VTRPACK,DMCB,(C'U',CMADID),LNFFILM                               
*                                                                               
FMF3A    BAS   RE,HDCNTR                                                        
         BAS   RE,TSTFLM           REAL FILM/AD-ID?                             
         BNZ   *+10                YES                                          
         MVC   LNFROT(3),=C'***'   NO- FLAG                                     
         LA    R4,0                                                             
         CLI   CMFILM+8,C' '       DO WE HAVE 2ND FILM                          
         BNH   FMF3B                                                            
         MVI   LNFFILM+132,C'-'                                                 
         MVC   LNFFILM+133(8),CMFILM+8 PUT IT ON 2ND LINE                       
         LA    R4,CMFILM+8                                                      
         MVI   BYTE,C'F'                                                        
*                                                                               
FMF3B    OC    CMADID+8(8),CMADID+8   IS THIS AN AD-ID?                         
         BZ    FMF3C               NO                                           
         GOTO1 VTRPACK,DMCB,(C'U',CMADID+8),LNFFILM+133                         
         LA    R4,CMADID+8                                                      
         MVI   BYTE,C'F'                                                        
*                                                                               
FMF3C    LTR   R4,R4               POINTING TO FILM/AD-ID?                      
         BZ    FMF4                NOPE                                         
         BAS   RE,TSTFLM                                                        
         BNZ   *+10                YES                                          
         MVC   LNFROT+132(3),=C'***'   NO- FLAG                                 
*                                                                               
FMF4     DS    0H                  ROTATION LETTER CODE                         
         CLI   CMROT,0                                                          
         BE    FMF6                                                             
         CLC   CMFILM(8),=C'*NO FILM'  SKIP FOR NO FILM ENTRY                   
         BE    FMF6                                                             
         CLI   CMROT,C'*'                                                       
         BNE   FMF5                                                             
         CLI   GCPPPRD,0           IF DOING 'NO PATN' BLOCK                     
         BE    FMF5                                                             
         CLI   NETPSW,C'Y'         NETPAK?                                      
         BNE   FMF4A               NO                                           
         CLC   GCPNPRD,=C'POL'     UNLESS DOINF MATCH TOTALS                    
         B     *+8                                                              
*                                                                               
FMF4A    CLI   GCPPPRD,X'FF'       OR TOTALS                                    
         BE    FMF5                SKIP STARS                                   
         MVC   LNFROT(3),=C'V**'                                                
         MVI   FILMDSCP,C'Y'       SET HAVE FILM ERROR                          
*                                                                               
         LA    R4,CMFILM           TEST REAL FILM                               
         MVI   BYTE,C'F'                                                        
         OC    CMADID(8),CMADID    IS THIS AN AD-ID?                            
         BZ    FMF4B               NO                                           
         LA    R4,CMADID           TEST READ AD-ID                              
         MVI   BYTE,C'A'                                                        
FMF4B    BAS   RE,TSTFLM                                                        
         BNZ   *+10                YES                                          
         MVC   LNFROT(3),=C'***'                                                
         B     FMF6                                                             
*                                                                               
FMF5     DS    0H                                                               
         CLI   TRAFROT,C'Y'        SKIP IF NOT SHOWING ROTATION                 
         BNE   FMF6                                                             
*                                                                               
         CLI   CMROT,X'01'         SPECIAL 2 CHAR ROTATIONS (AA-ZZ)             
         BL    FMF5R                 NO - DO REGULAR 1 CHAR ROTS                
         CLI   CMROT,X'1A'         SPECIAL 2 CHAR ROTATIONS (AA-ZZ)             
         BH    FMF5R                 NO - DO REGULAR 1 CHAR ROTS                
         LA    R3,SPTRRTAB         ROTATION TABLE                               
FMF5A    CLI   0(R3),0                                                          
         BNE   *+6                                                              
         DC    H'0'                MUST BE IN TABLE                             
         CLC   CMROT,2(R3)         MATCH ON HEX ROTATION                        
         BE    FMF5B                                                            
         LA    R3,3(R3)                                                         
         B     FMF5A                                                            
*                                                                               
FMF5B    MVI   LNFROT-1,C'('                                                    
         MVC   LNFROT(2),0(R3)     PRINT 2 CHAR ROTATION                        
         MVI   LNFROT+2,C')'                                                    
         B     FMF6                                                             
*                                                                               
FMF5R    MVI   LNFROT,C'('         REGULAR SINGLE LETTER ROTATION               
         MVC   LNFROT+1(1),CMROT                                                
         MVI   LNFROT+2,C')'                                                    
*                                                                               
FMF6     DS    0H                                                               
         CLC   CMFILM(8),=C'*NO FILM'  FOR NO FILM ENTRY                        
         BNE   FMF7                                                             
*                                                                               
         MVC   CMSPTST(12),CMSPTS  SET IN CROSS PATTN TOTS                      
         L     RF,GCPCPARS+4       AND ADD TO TOTAL ENTRY                       
         LA    R4,CMSPTST-CMTABD(RF)                                            
         LA    R3,CMSPTS                                                        
         BRAS  RE,ADDEMUP                                                       
*                                                                               
FMF7     DS    0H                                                               
         L     R4,GCPCPARS+4       POINT TO TOTAL ENTRY                         
         CLI   NETPSW,C'Y'         NETPAK?                                      
         BNE   FMF7A               NO                                           
         CLC   GCPNPRD,=C'POL'     UNLESS DOINF MATCH TOTALS                    
         B     *+8                                                              
*                                                                               
FMF7A    CLI   GCPPPRD,X'FF'       AT CROSS-PATTN TOTAL TIME                    
         BNE   FMF7B                                                            
*                                  DO ACTUAL PCT FOR TOTALS                     
         L     R1,CMSPTST             SPOTS FOR FILM                            
         M     R0,=F'100'                                                       
         L     RF,CMSPTST-CMTABD(R4)   TOTAL SPOTS                              
         BAS   RE,FMFDIV                                                        
         EDIT  (R1),(3,LNFAPCT),ZERO=NOBLANK                                    
*                                                                               
         MVC   FMFWK,CMSPTST       HAVE SHARE AND ACTUALS                       
         B     FMF10                                                            
*                                                                               
FMF7B    DS    0H                                                               
*                                  DO ACTUAL PCT  (NORMAL)                      
         L     R1,CMSPTS              SPOTS FOR FILM                            
         M     R0,=F'100'                                                       
         L     RF,CMSPTS-CMTABD(R4)   TOTAL SPOTS                               
         BAS   RE,FMFDIV                                                        
         EDIT  (R1),(3,LNFAPCT),ZERO=NOBLANK                                    
*                                                                               
         XC    FMFWK,FMFWK                                                      
         L     R1,CMPOCC           OCC'S                                        
         M     R0,=F'100'                                                       
         LLC   RF,GCPPTTNL         /PATT LENGTH                                 
         BAS   RE,FMFDIV           = PCT                                        
         EDIT  (R1),(3,LNFPCT),ZERO=NOBLANK                                     
*                                  NOW DO INDEX OF PCTS                         
         LLC   R1,GCPPTTNL         TOTAL PATT OCCS                              
         M     R0,CMSPTS           X FILM SPOTS                                 
         M     R0,=F'100'                                                       
         L     RF,CMSPTS-CMTABD(R4)   /TOTAL SPOTS                              
         M     RE,CMPOCC              X FILM PATT OCCS                          
         BAS   RE,FMFDIV                                                        
         EDIT  (R1),(4,LNFINDX)                                                 
*                                                                               
FMF7E    DS    0H                                                               
         ICM   R1,15,CMPOCC        SKIP SHARE CALCS IF                          
         BZ    FMF8                NO PATTERN OCCURENCES                        
*                                  SET SHARES                                   
*                                  SPOTS SHARE                                  
         L     R1,CMPOCC           PATTERN OCC'S                                
         M     R0,CMSPTS-CMTABD(R4)   X RUN SPOT TOTAL                          
         M     R0,=F'100'          CARRY TO 2 DECIMALS                          
         LLC   RF,GCPPTTNL                                                      
         BAS   RE,FMFDIV           /PATT LENGTH = SHARE                         
         ST    R1,FMFSPS                                                        
*                                                                               
*                                  DOLLARS SHARE                                
         L     R1,CMPOCC           PATTERN OCC'S                                
         M     R0,CMDOLS-CMTABD(R4)   X RUN DOLLAR TOTAL                        
         BAS   RE,FMFDIV           /PATT LENGTH = SHARE                         
         ST    R1,FMFDLS                                                        
*                                                                               
*                                  DEMOS SHARE                                  
         L     R1,CMPOCC           PATTERN OCC'S                                
         L     R0,CMDEMV-CMTABD(R4)   X RUN SPOT TOTAL                          
         N     R0,=X'3FFFFFFF'                                                  
         MR    R0,R0                                                            
         BAS   RE,FMFDIV                 /PATT LENGTH = SHARE                   
         TM    CMDEMV-CMTABD(R4),X'40'   TEST                                   
         BZ    *+8                                                              
         O     R1,=X'40000000'           SET 2-DEC FLAG                         
         ST    R1,FMFDMS                                                        
*                                  ADD TO FILM TOTALS SHARES                    
         LA    R3,FMFSPS                                                        
         LA    R4,CMSPTSTS                                                      
         BRAS  RE,ADDEMUP                                                       
*                                                                               
FMF8     DS    0H                                                               
         MVC   FMFSPA(12),CMSPTS   MOVE ACTUALS TO WORK AREA                    
*                                                                               
         LA    R3,CMSPTS                                                        
         LA    R4,CMSPTST                                                       
         BRAS  RE,ADDEMUP                                                       
*                                                                               
FMF10    DS    0H                  FORMAT PRINT LINE                            
*                                  SPOTS                                        
         L     R4,GCPCPARS+4       POINT TO TOTAL ENTRY                         
         CLI   NETPSW,C'Y'         NETPAK?                                      
         BNE   FMF10AA             NO                                           
         CLC   GCPNPRD,=C'POL'     UNLESS DOINF MATCH TOTALS                    
         B     *+8                                                              
*                                                                               
FMF10AA  CLI   GCPPPRD,X'FF'       NO SHARES FOR TOTALS                         
         BE    FMF10A                                                           
         CLI   CMFILM,0                                                         
         BE    FMF10A                                                           
         L     R1,FMFSPS           SPOTS SHARE                                  
         SR    R0,R0                                                            
         LA    RF,100              PRINT WHOLE NUMBERS                          
         BAS   RE,FMFDIV                                                        
         EDIT  (R1),(4,LNFSSHR),ZERO=NOBLANK                                    
FMF10A   DS    0H                                                               
         L     R0,FMFSPA           SPOTS ACTUAL                                 
         EDIT  (R0),(4,LNFSACT),ZERO=NOBLANK                                    
*                                  COSTS                                        
         LA    R3,LNFOTHR          OPTIONAL AREA                                
         CLI   TRAFCST,C'Y'        OPTION FOR COST                              
         BNE   FMF10C                                                           
         CLI   NETPSW,C'Y'         NETPAK?                                      
         BNE   FMF10BB             NO                                           
         CLC   GCPNPRD,=C'POL'     UNLESS DOINF MATCH TOTALS                    
         B     *+8                                                              
*                                                                               
FMF10BB  CLI   GCPPPRD,X'FF'       NO SHARES FOR TOTALS                         
         BE    FMF10B                                                           
         CLI   CMFILM,0                                                         
         BE    FMF10B                                                           
         L     R0,FMFDLS           DOLLARS SHARE                                
         EDIT  (R0),(10,0(R3)),2,FLOAT=$                                        
FMF10B   DS    0H                                                               
         CLI   PSEUDOPT,C'N'       FOR RESPONSE RUN                             
         BNE   FMF10B2             SKIP ACTUAL COST (IT IS FROM INVS)           
         L     R0,FMFDLA           DOLLARS ACTUAL                               
         EDIT  (R0),(10,10(R3)),2,FLOAT=$                                       
FMF10B2  DS    0H                                                               
         LA    R3,19(R3)           BUMP PAST COST                               
*                                                                               
FMF10C   DS    0H                                                               
         LA    R3,2(R3)                                                         
         CLI   TRAFDEM,C'Y'        OPTION FOR DEMOS                             
         BNE   FMF10G                                                           
         OC    DEMPARS+8(4),DEMPARS+8  NO DEMOS UNLESS DEMO REQ                 
         BZ    FMF10G                                                           
         CLI   NETPSW,C'Y'         NETPAK?                                      
         BNE   FMF10CC             NO                                           
         CLC   GCPNPRD,=C'POL'     UNLESS DOINF MATCH TOTALS                    
         B     *+8                                                              
*                                                                               
FMF10CC  CLI   GCPPPRD,X'FF'       NO SHARES FOR TOTALS                         
         BE    FMF10E                                                           
         CLI   CMFILM,0                                                         
         BE    FMF10E                                                           
         L     R0,FMFDMS           DEMO SHARE                                   
         N     R0,=X'3FFFFFFF'                                                  
         TM    FMFDMS,X'40'                                                     
         BO    FMF10D                                                           
         EDIT  (R0),(6,0(R3)),1                                                 
         B     FMF10E                                                           
FMF10D   EDIT  (R0),(7,0(R3)),2                                                 
*                                                                               
FMF10E   DS    0H                                                               
         L     R0,FMFDMA           DEMO ACTUAL                                  
         N     R0,=X'3FFFFFFF'     DROP 2-DEC FLAG                              
         TM    FMFDMA,X'40'                                                     
         BO    FMF10F                                                           
         EDIT  (R0),(6,8(R3)),1                                                 
         B     FMF10G                                                           
FMF10F   EDIT  (R0),(7,8(R3)),2                                                 
*                                                                               
FMF10G   DS    0H                                                               
         LA    R4,P                                                             
         LA    R3,14                                                            
*                                                                               
FMF14    DS    0H                                                               
         CLC   0(132,R4),SPACES                                                 
         BE    FMF16                                                            
         BAS   RE,FMFGPD           GET A LINE OF PAT DESC                       
*                                                                               
         LA    R4,132(R4)                                                       
         BCT   R3,FMF14                                                         
*                                                                               
FMF16    DS    0H                                                               
         BAS   RE,RPRT                                                          
*                                                                               
FMFX     DS    0H                                                               
         B     EXIT                                                             
*                                                                               
HDCNTR   NTR1                                                                   
         CLI   NETPSW,C'Y'         NETPAK?                                      
         BNE   *+14                NO                                           
         CLC   GCPNPRD,=C'POL'     TOTALS                                       
         B     *+8                                                              
         CLI   GCPPPRD,X'FF'       TOTALS                                       
         BNE   HDX                                                              
*                                                                               
         ICM   R0,15,FLMTPARS+8                                                 
         BZ    HDX                                                              
*                                                                               
         L     R5,AFLMTAB                                                       
         USING FLMTABD,R5                                                       
*                                                                               
HD00     OC    FLMSEQ,FLMSEQ       TEST HAVE CMML SQ (IE- TEST REAL)            
         BZ    HD20                CC ZERO = NOT REAL                           
         LA    R3,FLMCOD           FILMCODE                                     
         CLI   BYTE,C'F'           DOING FILMCODE?                              
         BE    *+8                 YES                                          
         LA    R3,FLMADID          NO - HAVE AN AD-ID                           
         CLC   0(8,R4),0(R3)       MATCH FILM CODE/AD-ID                        
         BNE   HD20                                                             
         OC    FLMCNTR,FLMCNTR     CENTERCUT?                                   
         BZ    HD10                NO                                           
         MVC   LNFDESC(4),=C'CTR='                                              
         GOTO1 VTRPACK,DMCB,(C'U',FLMCNTR),LNFDESC+4                            
*                                                                               
HD10     OC    FLMHDEF,FLMHDEF     HD?                                          
         BZ    HD20                NO                                           
         MVC   LNFDESC+20(3),=C'HD='                                            
         GOTO1 VTRPACK,DMCB,(C'U',FLMHDEF),LNFDESC+23                           
*                                                                               
HD20     LA    R5,FLMTABEL(R5)                                                  
         BCT   R0,HD00                                                          
*                                                                               
HDX      B     EXIT                                                             
         DROP  R5                                                               
*                                                                               
*----------------------------------------------------------------               
*                                                                               
FMFGPD   DS    0H                  GET A LINE FROM PAT DESC TABLE               
         SR    R6,R6                                                            
         ICM   R6,1,PDNLNS                                                      
         BZR   RE                  NONE                                         
*                                                                               
         L     RF,ANXTPLN                                                       
         MVC   0(PLL,R4),0(RF)     SET IN P LINE                                
         LA    RF,PLL(RF)                                                       
         ST    RF,ANXTPLN                                                       
         BCTR  R6,R0                                                            
         STC   R6,PDNLNS                                                        
         BR    RE                                                               
*                                                                               
*----------------------------------------------------------------               
*                                                                               
FMFDIV   DS    0H                                                               
         LTR   RF,RF               DON'T DIVIDE BY ZERO                         
         BNZ   *+8                                                              
         SR    R1,R1                                                            
         BR    RE                                                               
*                                                                               
         C     RF,=F'1'            IF DIVIDING BY 1                             
         BER   RE                  DON'T BOTHER                                 
*                                                                               
         SLDA  R0,1                                                             
         DR    R0,RF                                                            
         LTR   R1,R1                                                            
         BNP   *+8                                                              
         AHI   R1,1                                                             
         SRA   R1,1                                                             
         BR    RE                                                               
*                                                                               
*----------------------------------------------------------------               
*        RPRT - SET LINES IN BUFFER                                             
*----------------------------------------------------------------               
RPRT     NTR1                                                                   
         CLI   AFRPTBUF,0          TEST OVERFLOW                                
         BNE   RPRTX                                                            
         CLI   SPACING,0           IF SPACING ISN'T SPECIFIED                   
         BNE   *+8                                                              
         MVI   SPACING,1           SET TO 1                                     
         L     R6,NXTLIN           SPOT FOR NEXT LINE                           
         MVC   0(1,R6),SPACING     SAVE SPACING                                 
         MVC   1(1,R6),LNEED       AND LNEED                                    
*                                  COUNT LINES                                  
         LA    RF,1                ALWAYS COUNT P1                              
         LA    RE,P2                                                            
         LA    R0,13                                                            
*                                                                               
RPRT2    DS    0H                                                               
         CLC   0(132,RE),SPACES                                                 
         BE    RPRT3                                                            
         LA    RE,132(RE)                                                       
         LA    RF,1(RF)                                                         
         BCT   R0,RPRT2                                                         
*                                                                               
RPRT3    DS    0H                                                               
         STC   RF,2(R6)            SET NO OF LINES                              
*                                  NOW MOVE INDIVIDUAL LINES                    
         LA    R6,3(R6)                                                         
         LA    RE,P                                                             
         CLI   P,C' '              ENSURE FIRST LINE OK                         
         BNE   *+8                                                              
         MVI   P,0                                                              
*                                                                               
RPRT4    DS    0H                                                               
         LA    R1,132(RE)                                                       
         CLI   0(R1),C' '                                                       
         BNE   *+8                                                              
         BCT   R1,*-8                                                           
         SR    R1,RE                                                            
         LA    R0,2(R6,R1)         TEST WILL FIT                                
         C     R0,AFRPTBFX                                                      
         BNH   RPRT6                                                            
         MVI   AFRPTBUF,X'FF'      SET OVERFLOW                                 
         B     RPRTX                                                            
*                                                                               
RPRT6    DS    0H                                                               
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   1(0,R6),0(RE)                                                    
         LA    R1,2(R1)                                                         
         STC   R1,0(R6)                                                         
         AR    R6,R1                                                            
         MVC   0(132,RE),SPACES                                                 
         LA    RE,132(RE)                                                       
         BCT   RF,RPRT4                                                         
*                                                                               
         ST    R6,NXTLIN                                                        
         MVI   0(R6),X'FF'         SET END                                      
*                                                                               
         MVI   SPACING,1                                                        
         MVI   LNEED,0                                                          
*                                                                               
RPRTX    DS    0H                                                               
         B     EXIT                                                             
*                                                                               
*----------------------------------------------------------------               
*                                                                               
TSTFLM   DS    0H                  FORMAT FILMS                                 
         ICM   R0,15,FLMTPARS+8                                                 
         BZ    TSTF8                                                            
         L     RF,AFLMTAB                                                       
         USING FLMTABD,RF                                                       
*                                                                               
TSTF4    DS    0H                                                               
         LA    R3,FLMCOD           FILMCODE                                     
         CLI   BYTE,C'F'           DOING FILMCODE?                              
         BE    *+8                 YES                                          
         LA    R3,FLMADID          NO - HAVE AN AD-ID                           
         CLC   0(8,R4),0(R3)       MATCH FILM CODE/AD-ID                        
         BNE   TSTF10                                                           
         OC    FLMSEQ,FLMSEQ       TEST HAVE CMML SQ (IE- TEST REAL)            
         BR    RE                  CC ZERO = NOT REAL                           
*                                                                               
TSTF8    DS    0H                                                               
         LTR   RE,RE               FILM NOT FOUND-MAYBE FROM PATTN              
         BR    RE                  DONT TREAT AS NOT REAL                       
*                                                                               
TSTF10   DS    0H                                                               
         LA    RF,FLMTABEL(RF)                                                  
         BCT   R0,TSTF4                                                         
         B     TSTF8                                                            
         DROP  RF                                                               
*----------------------------------------------------------------               
*        LTORG                                                                  
*----------------------------------------------------------------               
         LTORG                                                                  
         EJECT                                                                  
       ++INCLUDE SPTRRTAB                                                       
         DC    X'0000'                                                          
         EJECT                                                                  
*============================================================                   
* SUBROUTINE TO ADD SPOTS,DOLLARS AND DEMOS                                     
* R3 POINTS TO NEW VALUES                                                       
* R4 POINTS TO ACCUMS                                                           
*============================================================                   
                                                                                
ADDEMUP  NTR1  BASE=*,LABEL=*                                                   
*                                                                               
         LHI   R0,2                DO SPOTS AND DOLLARS                         
*                                                                               
ADDEMUP2 L     RE,0(R3)                                                         
         A     RE,0(R4)                                                         
         ST    RE,0(R4)                                                         
*                                                                               
         AHI   R3,4                                                             
         AHI   R4,4                                                             
         BCT   R0,ADDEMUP2                                                      
                                                                                
* ADD DEMOS PRESERVING 2-DECIMAL FLAG                                           
                                                                                
         L     RE,0(R3)                                                         
         N     RE,=X'3FFFFFFF'                                                  
         L     RF,0(R4)                                                         
         N     RF,=X'3FFFFFFF'                                                  
         AR    RE,RF                                                            
         TM    0(R3),X'40'                                                      
         BZ    *+8                                                              
         O     RE,=X'40000000'                                                  
         ST    RE,0(R4)                                                         
ADDEMUPX J     EXIT                                                             
         LTORG                                                                  
*                                                                               
         DS    0D                                                               
GCPWKA   DS    0X                                                               
         ORG   *+GCPDL                                                          
         DC    X'0000'                                                          
*                                                                               
         DS    0D                                                               
IOAREA2  DS    0X                                                               
         ORG   *+4000                                                           
         DC    X'0000'                                                          
*                                                                               
         DS    0D                                                               
CMMLTAB  DS    0X                                                               
         ORG   *+20000                                                          
         DC    X'0000'                                                          
CMMLTABL EQU   20000                                                            
*                                                                               
         DS    0D                                                               
PATLINS  DS    0X                                                               
         ORG   *+(51*PLL)                                                       
         DC    X'0000'                                                          
*                                                                               
         DS    0D                                                               
FLMRUN   DS    0X                                                               
         ORG   *+5000                                                           
FLMRUNX  DS    0X                                                               
         DC    X'0000'                                                          
*                                                                               
       ++INCLUDE SPREPI2WKN                                                     
*                                                                               
       ++INCLUDE SPGCPD                                                         
*                                                                               
         PRINT ON                                                               
       ++INCLUDE SPREPWORKD                                                     
       ++INCLUDE SPGENCLT                                                       
*                                                                               
         PRINT ON                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'124SPREPI2FR 03/04/21'                                      
         END                                                                    
