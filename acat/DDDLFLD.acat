*          DATA SET DDDLFLD    AT LEVEL 012 AS OF 05/14/10                      
*CATALP DLFLD                                                                   
* RCRI 010 01DEC03 TRUNCATE DATE LONGER THAN LINE (STOPS LOOP)                  
* DCHA 008 06SEP96 REMOVE LVL 7 + SUPPORT SPACE FILLED NUM FLDS                 
* CBLA 007 18JUN96 TRAP BAD DLCBFLD'S @ PUTN2                                   
* RCRI 006 17JUN96 DLCBSDPC SUPPORT                                             
* DLOW 005 08MAY96 STRIP LEADING SPACES FROM NUM FLD FOR Â›MAD/PIANO             
* TGUT 004 13DEC95 FIX BUG IN DEFAULT SETTING OF DLCBFLG1 (OI NOT MVI)          
         PRINT NOGEN                                                            
         TITLE 'DLFLD - CONVERT DATA FIELD TO DOWN LOAD FORMAT'                 
DLFLD    CSECT                                                                  
         NMOD1 WORKX-WORKD,**DLFL**                                             
         USING WORKD,RC                                                         
         MVC   HOOKRD,4(RD)                                                     
         LR    R2,R1               R2=A(DOWN LOAD CONTROL BLOCK)                
         USING DLCBD,R2                                                         
         USING SPECD,R8                                                         
         LA    RE,*+6                                                           
         BSM   0,RE                FORCE 24 BIT MODE                            
*                                                                               
DLFLD1   LA    R8,MAXLINE          POINT TO STD/USER BLOCK                      
         TM    DLCBFLG1,DLCBFXTN                                                
         BZ    DLFLD2                                                           
         OC    DLCXTND,DLCXTND     IGNORE IF EXTENSION AREA ALL ZEROS           
         BZ    DLFLD2                                                           
         LA    R8,DLCXTND                                                       
*                                                                               
DLFLD2   MVI   DLCBRETC,0          SET OK RETURN CODE                           
         LM    R3,R4,DLCBAPR                                                    
         LA    R3,0(R3)                                                         
         LTR   R3,R3               R3=A(PRINT ROUTINE)                          
         BNZ   *+6                                                              
         DC    H'0'                DIE IF NO PRINT ROUTINE                      
         ST    R3,AHOOK                                                         
         LA    R4,0(R4)                                                         
         LTR   R4,R4               R4=A(PRINT LINE)                             
         BNZ   *+6                                                              
         DC    H'0'                DIE IF NO PRINT LINE                         
         SPACE 1                                                                
ACTION   CLI   DLCBACT,DLCBPUT     PUT FIELD                                    
         BE    PUT                                                              
         CLI   DLCBACT,DLCBEOL     END OF LINE                                  
         BE    EOL                                                              
         CLI   DLCBACT,DLCBSOR     START OF REPORT                              
         BE    SOR                                                              
         CLI   DLCBACT,DLCBINIT    INIT DATA AND START OF REPORT                
         BE    SOR                                                              
         CLI   DLCBACT,DLCBEOR     END OF REPORT                                
         BE    EOR                                                              
         CLI   DLCBACT,DLCBWRT     WRITE (NO EOL)                               
         BE    WRT                                                              
         DC    H'0'                INVALID ACTION                               
         SPACE 1                                                                
EXIT     MVI   DLCBACT,C' '        RESET ACTION                                 
         MVI   DLCBTYP,C' '        RESET DATA TYPE                              
         XC    DLCBLEN(4),DLCBLEN  RESET DATA LENGTH AND BIN INFO               
         LA    R3,L'DLCBFLD-2                                                   
         LA    R4,DLCBFLD                                                       
         MVI   0(R4),C' '                                                       
         EX    R3,*+8                                                           
         B     *+10                                                             
         MVC   1(0,R4),0(R4)       RESET DATA FIELD                             
         TM    DLCBFLG1,DLCBFXFL                                                
         BZ    EXITX                                                            
         LA    R3,L'DLCBFLX-2                                                   
         LA    R4,DLCBFLX                                                       
         MVI   0(R4),C' '                                                       
         EX    R3,*+8                                                           
         B     *+10                                                             
         MVC   1(0,R4),0(R4)       RESET EXTENDED DATA FIELD                    
EXITX    CLI   DLCBRETC,0                                                       
         XMOD1 1                                                                
         ORG   *-2                                                              
         BSM   0,RE                                                             
         EJECT                                                                  
SOR      XC    DLCBSAVE,DLCBSAVE   INITIALISE SAVE STORAGE                      
         CLI   DLCBACT,DLCBSOR     STANDARD INITIALISE                          
         BE    EXIT                                                             
         OI    DLCBFLG1,DLCBFXTN   SET EXTENDED BLOCK                           
         B     EXIT                                                             
         SPACE 1                                                                
EOR      OC    DLCBNUMC,DLCBNUMC   TEST IF ANY DATA IN LINE                     
         BZ    EOR2                NO                                           
         LH    RE,SPECMAXL                                                      
         CLI   SPECEOLC,0          TEST ANY END OF LINE CHAR                    
         BE    EOR1                JUST DO PRINT                                
         BCTR  RE,0                                                             
         CH    RE,DLCBNUMC         TEST IF SPACE FOR EOLCHR                     
         BNL   *+8                 YES                                          
         BRAS  R9,PLWRITE          NO WRITE THIS LINE                           
         BRAS  R9,PLEND            SET END OF LINE                              
EOR1     BRAS  R9,PLWRITE          WRITE PRINT LINE                             
*                                                                               
EOR2     CLI   SPECEORC,0          TEST END OF REPORT CHAR                      
         BE    EOR3                                                             
         MVC   0(1,R4),SPECEORC    SET END OF REPORT                            
         BRAS  RE,HOOK             WRITE PRINT LINE WITH USERS ROUT             
EOR3     L     R4,DLCBAPL                                                       
         LH    R3,SPECMAXL                                                      
         SH    R3,=H'2'                                                         
         MVI   0(R4),C' '                                                       
         EX    R3,*+8                                                           
         B     *+10                                                             
         MVC   1(0,R4),0(R4)                                                    
         OI    DLCBRETC,DLCBRCPR   RETURN PRINT ROUTINE INVOKED                 
         B     EXIT                                                             
         SPACE 1                                                                
EOL      OC    DLCBNUMC,DLCBNUMC   TEST IF ANY DATA IN LINE                     
         BZ    EXIT                NO EXIT                                      
         OC    DLCBPRVF,DLCBPRVF   TEST FIRST LINE                              
         BZ    EOL1                YES                                          
         CLC   DLCBNUMF,DLCBPRVF   TEST THIS WITH PREV NUM FLDS                 
         BE    EOL1                                                             
         OI    DLCBRETC,DLCBRCNF   SET RC TO SHOW NUM FLDS DIFFERS              
*                                                                               
EOL1     MVC   DLCBPRVF,DLCBNUMF   SAVE NUMBER OF FIELDS                        
         L     RE,DLCBTOTF                                                      
         AH    RE,DLCBNUMF                                                      
         ST    RE,DLCBTOTF         BUMP TOTAL NUMBER OF FIELDS                  
         XC    DLCBNUMF,DLCBNUMF                                                
*                                                                               
EOL2     LH    RE,SPECMAXL         YES TEST IF (EOLCHR)                         
         CLI   SPECEOLC,0          JUST DO WRITE                                
         BE    EOL3                                                             
         BCTR  RE,0                                                             
         CH    RE,DLCBNUMC         TEST IF SPACE FOR EOLCHR                     
         BNL   *+8                                                              
         BRAS  R9,PLWRITE          NO WRITE THIS LINE                           
         BRAS  R9,PLEND            SET END OF LINE                              
EOL3     BRAS  R9,PLWRITE          WRITE PRINT LINE                             
         B     EXIT                                                             
         SPACE 1                                                                
WRT      OC    DLCBNUMC,DLCBNUMC   TEST IF ANY DATA IN LINE                     
         BZ    EXIT                NO EXIT                                      
         BRAS  R9,PLWRITE          WRITE PRINT LINE                             
         B     EXIT                                                             
         SPACE 1                                                                
PUT      CLI   DLCBTYP,DLCBTXT     PUT FIELD TO PRINT LINE                      
         BE    PUTTXT                                                           
         TM    DLCBFLG1,DLCBFTRP+DLCBFTRM+DLCBFPRP+DLCBFPRM                     
         BNZ   *+8                                                              
         OI    DLCBFLG1,DLCBFPRM   DEFAULT TO PRECEDING MINUS SIGN              
         CLI   DLCBTYP,DLCBNUM                                                  
         BE    PUTNUM                                                           
         CLI   DLCBTYP,DLCBBIN                                                  
         BE    PUTBIN                                                           
         CLI   DLCBTYP,DLCBPACK                                                 
         BE    PUTPAC                                                           
         CLI   DLCBTYP,DLCBBINF                                                 
         BE    PUTBIN                                                           
         CLI   DLCBTYP,DLCBPACF                                                 
         BE    PUTPAC                                                           
         DC    H'0'                INVALID DATA TYPE                            
         EJECT                                                                  
PUTTXT   CLI   DLCBLEN,0           DID USER PASS LENGTH                         
         BE    PUTT1               NO                                           
         SR    R5,R5               R5=USER SUPPLIED LENGTH                      
         IC    R5,DLCBLEN                                                       
         LA    R0,L'DLCBFLD                                                     
         TM    DLCBFLG1,DLCBFXFL                                                
         BZ    *+8                                                              
         LA    R0,L'DLCBFLX                                                     
         CR    R5,R0               CANT BE GREATER THAN MAX                     
         BNH   *+6                                                              
         DC    H'0'                                                             
         B     PUTT1C                                                           
*                                                                               
PUTT1    LA    R5,DLCBFLD+L'DLCBFLD-1                                           
         LA    RF,DLCBFLD                                                       
         TM    DLCBFLG1,DLCBFXFL                                                
         BZ    PUTT1A                                                           
         LA    R5,DLCBFLX+L'DLCBFLX-1                                           
         LA    RF,DLCBFLX                                                       
PUTT1A   CLI   0(R5),C' '          FIND LENGTH OF TEXT FIELD                    
         BNE   PUTT1B                                                           
         BCTR  R5,0                                                             
         CR    R5,RF                                                            
         BNL   PUTT1A                                                           
         LR    R5,RF                                                            
         MVI   0(R5),C' '          SET DEFAULT NULL TEXT FIELD                  
PUTT1B   LA    R5,1(R5)                                                         
         SR    R5,RF               R5=SIGNIFICANT LENGTH OF TEXT                
*                                                                               
PUTT1C   LR    RE,R5               TEST IF BIGGER THAT ONE LINE                 
         SR    R0,R0                                                            
         CLI   SPECEOTC,0          ADJUST FOR END OF TEXT CHRS                  
         BE    *+8                                                              
         AHI   R0,2                                                             
         CLI   SPECDELC,0          ADJUST FOR DELIMITER CHR                     
         BE    *+8                                                              
         AHI   R0,1                                                             
         AR    RE,R0                                                            
         CH    RE,SPECMAXL                                                      
         BNH   PUTT1D                                                           
         LH    R5,SPECMAXL         TRUNCATE TEXT TO FIT ON A LINE               
         SR    R5,R0                                                            
         OI    DLCBRETC,DLCBRCDT   SET TRUNCATED DATA FLAG                      
*                                                                               
PUTT1D   STC   R5,DLCBRETL         RETURN ACTUAL TEXT LEN                       
         TM    DLCBFLG1,DLCBDD     TRANSLATION REQUIRED?                        
         BZ    PUTT2               NO                                           
         NI    DLCBFLG1,255-DLCBDD                                              
         LA    R1,DUB1                                                          
         MVC   0(4,R1),=C'T   '    SET TRANSLATE WHOLE FIELD                    
         ST    RF,4(R1)            SET ADR AND LEN OF FIELD                     
         STC   R5,4(R1)                                                         
         ICM   RF,15,DLCBADD                                                    
         BNZ   *+6                                                              
         DC    H'0'                                                             
         BASR  RE,RF                                                            
*                                                                               
PUTT2    LR    RE,R5                                                            
         CLI   SPECEOTC,0          ADJUST FOR END OF TEXT CHRS                  
         BE    *+8                                                              
         LA    RE,2(RE)                                                         
         CLI   SPECDELC,0          ADJUST FOR DELIMITER CHR                     
         BE    *+8                                                              
         LA    RE,1(RE)                                                         
*                                                                               
         TM    SPECCIND,DLCXEOTB   DO WE WANT TO IGNORE INITIAL EOT             
         BZ    *+6                                                              
         BCTR  RE,0                                                             
         TM    SPECCIND,DLCXEOTE   DO WE WANT TO IGNORE END EOT                 
         BZ    *+6                                                              
         BCTR  RE,0                                                             
*                                                                               
         AH    RE,DLCBNUMC                                                      
         CH    RE,SPECMAXL                                                      
         BNH   PUTT3                                                            
         BRAS  R9,PLWRITE          WRITE PRINT LINE                             
         B     PUTT2                                                            
*                                                                               
PUTT3    LR    RE,R4               MOVE TEXT FIELD                              
         AH    RE,DLCBNUMC         RE=A(NEXT AVAIL CHR IN PRINT LINE)           
         LA    RF,DLCBFLD          RF=A(NEXT CHR IN DATA FIELD)                 
         TM    DLCBFLG1,DLCBFXFL                                                
         BZ    *+8                                                              
         LA    RF,DLCBFLX                                                       
         CLI   SPECEOTC,0                                                       
         BE    PUTT3A                                                           
*                                                                               
         TM    SPECCIND,DLCXEOTB   DO WE WANT TO IGNORE INITIAL EOT             
         BO    PUTT3A                                                           
         MVC   0(1,RE),SPECEOTC                                                 
         LA    RE,1(RE)                                                         
*                                                                               
PUTT3A   MVC   0(1,RE),0(RF)                                                    
         CLI   SPECEOTC,0          TEST IF (EOTCHR) DEFINED                     
         BE    PUTT3B              NO                                           
         CLC   0(1,RE),SPECEOTC    YES REPLACE BY ALTERNATE CHR                 
         BNE   PUTT3B                                                           
         MVC   0(1,RE),SPECEOTA                                                 
PUTT3B   LA    RE,1(RE)                                                         
         LA    RF,1(RF)                                                         
         BCT   R5,PUTT3A                                                        
PUTT3C   CLI   SPECEOTC,0                                                       
         BE    PUTT3D                                                           
         LA    R5,2(R5)                                                         
*                                                                               
         TM    SPECCIND,DLCXEOTE   DO WE WANT TO IGNORE END EOT                 
         BO    PUTT3D                                                           
         MVC   0(1,RE),SPECEOTC    SET (EOT).......(EOT)                        
         LA    RE,1(RE)                                                         
*                                                                               
PUTT3D   CLI   SPECDELC,0                                                       
         BE    PUTT4                                                            
         LA    R5,1(R5)                                                         
         MVC   0(1,RE),SPECDELC    SET (DELIM)                                  
         LA    RE,1(RE)                                                         
*                                                                               
PUTT4    ZIC   RE,DLCBRETL         SET NUM CHRS THIS FIELD                      
         AR    R5,RE                                                            
         AH    R5,DLCBNUMC                                                      
         STH   R5,DLCBNUMC         BUMP NUM OF CHARS THIS LINE                  
         LH    RE,DLCBNUMF                                                      
         LA    RE,1(RE)                                                         
         STH   RE,DLCBNUMF         BUMP NUM OF FIELDS THIS LINE                 
         B     EXIT                                                             
         EJECT                                                                  
*                                                                               
PUTNUM   CLI   DLCBLEN,0           DID USER PASS LENGTH                         
         BE    PUTN1               NO                                           
         CLI   DLCBLEN,L'DLCBFLD   YES CANT BE GREATER THAN MAX                 
         BNH   *+6                                                              
         DC    H'0'                                                             
         SR    R5,R5               R5=USER SUPPLIED LENGTH                      
         IC    R5,DLCBLEN                                                       
         B     PUTN1C                                                           
*                                                                               
PUTN1    LA    R5,DLCBFLD+L'DLCBFLD-1                                           
         LA    RF,DLCBFLD                                                       
PUTN1A   CLI   0(R5),C' '          FIND LENGTH OF NUMERIC FIELD                 
         BNE   PUTN1B                                                           
         BCTR  R5,0                                                             
         CR    R5,RF                                                            
         BNL   PUTN1A                                                           
         LR    R5,RF                                                            
         MVI   0(R5),C'0'          SET DEFAULT NULL NUMERIC FIELD               
PUTN1B   LA    R5,1(R5)                                                         
         SR    R5,RF               R5=SIGNIFICANT LENGTH OF NUM FLD             
*                                  STRIP LEADING SPACES                         
PUTN1C   CLI   DLCBFLD,C' '        LEADING SPACES                               
         BNE   PUTN1F              NO                                           
         MVC   WORK(L'DLCBFLD),DLCBFLD                                          
         MVC   DLCBFLD+1(L'DLCBFLD-1),DLCBFLD                                   
         LA    R1,WORK                                                          
PUTN1D   CLI   0(R1),C' '                                                       
         BH    PUTN1E                                                           
         LA    R1,1(R1)                                                         
         BCT   R5,PUTN1D                                                        
         LA    R5,1                SET DEFAULT NUMERIC FIELD                    
         MVI   DLCBFLD,C'0'                                                     
         B     PUTN1F                                                           
PUTN1E   BCTR  R5,0                                                             
         EX    R5,*+8                                                           
         B     *+10                                                             
         MVC   DLCBFLD(0),0(R1)                                                 
         LA    R5,1(R5)                                                         
*                                                                               
PUTN1F   LR    RE,R5               TEST IF BIGGER THAT ONE LINE                 
         SR    R0,R0                                                            
         CLI   SPECDELC,0          ADJUST FOR DELIMITER CHR                     
         BE    *+8                                                              
         AHI   R0,1                                                             
         AR    RE,R0                                                            
         CH    RE,SPECMAXL                                                      
         BNH   PUTN2                                                            
         LH    R5,SPECMAXL         TRUNCATE TEXT TO FIT ON A LINE               
         SR    R5,R0                                                            
         OI    DLCBRETC,DLCBRCDT   SET TRUNCATED DATA FLAG                      
*                                                                               
PUTN2    STC   R5,DLCBRETL         RETURN ACTUAL NUMERIC FIELD LEN              
         LR    RE,R5                                                            
         CLI   SPECDELC,0                                                       
         BE    *+8                                                              
         LA    RE,1(RE)            NEED (NUMBER)(DELIM)                         
         AH    RE,DLCBNUMC                                                      
         CH    RE,SPECMAXL                                                      
         BNH   PUTN2A                                                           
         BRAS  R9,PLWRITE          WRITE PRINT LINE                             
         B     PUTN2                                                            
*                                                                               
PUTN2A   TM    DLCBFLG1,DLCBFPRM   PRECEDING MINUS SIGNS                        
         BNO   PUTN3                                                            
         LA    RE,DLCBFLD-1(R5)                                                 
         CLI   0(RE),C'-'          IS THERE A TRAILING MINUS                    
         BNE   PUTN3                                                            
         MVI   0(RE),C' '          IF SO DROP IT                                
         MVI   WORK,C' '           SHUFFLE DLCBFLD 1 CHAR TO THE RIGHT          
         MVC   WORK+1(L'DLCBFLD-1),DLCBFLD                                      
         MVC   DLCBFLD,WORK                                                     
         CLI   0(RE),C' '          LOOK BACK FOR 1ST NON-SPACE                  
         BNH   *+8                 (IE DONT ASSUME LEFT ALIGNED)                
         BCT   RE,*-8                                                           
         MVI   0(RE),C'-'          AND MOVE IN PRECEDING MINUS                  
*                                                                               
PUTN3    LR    RE,R4               MOVE NUMERIC FIELD                           
         AH    RE,DLCBNUMC         RE=A(NEXT AVAIL CHR IN PRINT LINE)           
         LA    RF,DLCBFLD          RF=A(NEXT CHR IN DATA FIELD)                 
PUTN3A   MVC   0(1,RE),0(RF)                                                    
         LA    RE,1(RE)                                                         
         LA    RF,1(RF)                                                         
         BCT   R5,PUTN3A                                                        
         CLI   SPECDELC,0                                                       
         BE    PUTN4                                                            
         MVC   0(1,RE),SPECDELC    SET (DELIM)                                  
         LA    R5,1(R5)                                                         
*                                                                               
PUTN4    ZIC   RE,DLCBRETL         SET NUM CHRS THIS FIELD                      
         AR    R5,RE                                                            
         AH    R5,DLCBNUMC                                                      
         STH   R5,DLCBNUMC         BUMP NUM OF CHARS THIS LINE                  
         LH    RE,DLCBNUMF                                                      
         LA    RE,1(RE)                                                         
         STH   RE,DLCBNUMF         BUMP NUM OF FIELDS THIS LINE                 
         B     EXIT                                                             
         EJECT                                                                  
PUTBIN   CLI   DLCBLEN,1           BINARY NUMBER                                
         BL    *+12                                                             
         CLI   DLCBLEN,4                                                        
         BNH   *+6                                                              
         DC    H'0'                DIE IF INVALID BINARY FLD LEN                
         XC    EBLOCK,EBLOCK                                                    
         MVI   EBTIN,C'B'          SET BINARY IN EDIT BLOCK                     
         CLI   DLCBTYP,DLCBBINF                                                 
         BNE   EDIT                                                             
         ICM   RF,15,DLCBFLD                                                    
         SR    RE,RE                                                            
         SR    R0,R0                                                            
         IC    R0,DLCBLEN                                                       
PUTBINF2 SLDL  RE,8                                                             
         BCT   R0,PUTBINF2                                                      
         CVD   RE,DUB2                                                          
         XC    DUB1,DUB1                                                        
         B     UNPACK                                                           
*                                                                               
PUTPAC   CLI   DLCBLEN,1           PACKED NUMBER                                
         BL    *+12                                                             
         CLI   DLCBLEN,16                                                       
         BNH   *+6                                                              
         DC    H'0'                DIE IF INVALID PACKED FLD LEN                
         XC    EBLOCK,EBLOCK                                                    
         MVI   EBTIN,C'P'          SET PACKED IN EDIT BLOCK                     
         CLI   DLCBTYP,DLCBPACF                                                 
         BE    PUTPACF                                                          
EDIT     MVC   FLD,DLCBFLD         EXTRACT INPUT NUMBER                         
         LA    RE,FLD              SET INPUT DATA INTO EDIT BLOCK               
         ST    RE,EBAIN                                                         
         MVC   EBLIN,DLCBLEN                                                    
         MVC   EBSCIN,DLCBSCI                                                   
         LA    RE,DLCBFLD          SET OUTPUT DATA INTO EDIT BLOCK              
         ST    RE,EBAOUT                                                        
         MVI   EBLOUT,20                                                        
         MVC   EBDECS,DLCBNDP                                                   
         MVI   EBALIGN,C'L'                                                     
         MVC   EBSCOUT,DLCBSCO                                                  
         MVI   EBOPT,EBOQMEY+EBOQZEN MINUS=Y/ZERO=NOBLANK                       
         TM    DLCBFLG1,DLCBFPRM                                                
         BNO   *+12                                                             
         MVI   EBOPT,EBOQZEN       FLOAT MINUS IF REQUESTED                     
         MVI   EBFLOAT,C'-'                                                     
         TM    DLCBFLG1,DLCBSDPC                                                
         BNO   *+8                                                              
         OI    EBOPT,EBOQSDPC      SWAP DECIMAL POINTS AND COMMAS               
*                                                                               
         LA    R3,L'DLCBFLD-2                                                   
         LA    RF,DLCBFLD                                                       
         MVI   0(RF),C' '                                                       
         EX    R3,*+8                                                           
         B     *+10                                                             
         MVC   1(0,RF),0(RF)                                                    
         SR    RF,RF               EDIT NUMBER BACK INTO INPUT AREA             
         ICM   RF,7,DLCBAED+1                                                   
         BNZ   *+6                                                              
         DC    H'0'                DIE IF A(EDITOR) NOT PASSED                  
         LA    R1,EBLOCK                                                        
         ST    R1,DUB1                                                          
         LA    R1,DUB1                                                          
         BASR  RE,RF                                                            
*                                                                               
         XC    DLCBLEN(4),DLCBLEN  CLEAR ALL INPUT FLAGS                        
         NI    DLCBFLG1,255-DLCBFTRP-DLCBFTRM-DLCBFPRP-DLCBFPRM                 
         NI    DLCBFLG1,255-DLCBSDPC                                            
         B     PUTNUM              TREAT AS REGULAR NUMBER                      
*                                                                               
PUTPACF  SR    RE,RE                                                            
         IC    RE,DLCBLEN                                                       
         BCTR  RE,R0                                                            
         EX    RE,*+8                                                           
         B     UNPACK                                                           
         ZAP   DUB1(16),DLCBFLD(0)                                              
UNPACK   SR    RE,RE                                                            
         IC    RE,DLCBRETL                                                      
         BCTR  RE,R0               -1 FOR UNPACK                                
         TM    DLCBFLG1,DLCBFTRP+DLCBFTRM                                       
         BZ    *+6                                                              
         BCTR  RE,R0               -1 FOR SIGN                                  
         SLL   RE,4                OPERATE ON DESTINATION FLD                   
         EX    RE,*+8                                                           
         B     *+10                                                             
         UNPK  DLCBFLD(0),DUB1(16)                                              
         SRL   RE,4                                                             
         LA    RE,DLCBFLD(RE)                                                   
         OI    0(RE),X'F0'                                                      
         TM    DLCBFLG1,DLCBFTRP+DLCBFTRM                                       
         BZ    UNPKEND                                                          
         CP    DUB2,=P'0'                                                       
         BL    UNPKMIN                                                          
         TM    DLCBFLG1,DLCBFTRP                                                
         BZ    UNPKEND                                                          
         MVI   1(RE),C'+'                                                       
         B     UNPKEND                                                          
UNPKMIN  TM    DLCBFLG1,DLCBFTRM                                                
         BZ    UNPKEND                                                          
         MVI   1(RE),C'-'                                                       
*                                                                               
UNPKEND  MVC   DLCBLEN,DLCBRETL                                                 
         NI    DLCBFLG1,255-DLCBFTRP-DLCBFTRM                                   
         B     PUTNUM                                                           
         EJECT                                                                  
PLEND    LR    RE,R4               SET END OF PRINT LINE                        
         AH    RE,DLCBNUMC                                                      
         MVC   0(1,RE),SPECEOLC                                                 
         LH    RE,DLCBNUMC         BUMP NUM OF CHRS IN LINE                     
         LA    RE,1(RE)                                                         
         STH   RE,DLCBNUMC                                                      
         BR    R9                                                               
         SPACE 2                                                                
PLWRITE  BRAS  RE,HOOK             WRITE PRINT LINE WITH USERS ROUT             
         L     R4,DLCBAPL                                                       
         LH    R3,SPECMAXL                                                      
         SH    R3,=H'2'                                                         
         MVI   0(R4),C' '                                                       
         EX    R3,*+8                                                           
         B     *+10                                                             
         MVC   1(0,R4),0(R4)       SET USERS PRINT LINE TO SPACES               
         OI    DLCBRETC,DLCBRCPR   RETURN PRINT ROUTINE INVOKED                 
*                                                                               
PLW1     L     RE,DLCBTOTC         BUMP TOTAL NUMBER OF CHARS                   
         AH    RE,DLCBNUMC                                                      
         ST    RE,DLCBTOTC                                                      
         L     RE,DLCBTOTL         BUMP TOTAL NUMBER OF LINES                   
         LA    RE,1(RE)                                                         
         ST    RE,DLCBTOTL                                                      
*                                                                               
         XC    DLCBNUMC,DLCBNUMC   CLEAR NUMBER OF CHARS IN LINE                
         BR    R9                                                               
         SPACE 2                                                                
HOOK     NTR1  ,                   SAVE MY REGISTERS                            
         L     RF,AHOOK                                                         
         L     RE,HOOKRD                                                        
         LM    R0,RC,20(RE)        RESTORE CALLER'S R0 THRU RC                  
         BASR  RE,RF                                                            
         XIT1  ,                   RESTORE MY REGISTERS AND RETURN              
         ORG   *-2                                                              
         BSM   0,RE                                                             
         EJECT                                                                  
         LTORG                                                                  
         SPACE 1                                                                
MAXLINE  DC    H'78'               MAX LINE WIDTH                               
DELIM    DC    C' '                FIELD DELIMITER CHR                          
EOTCHR   DC    C'"'                END OF TEXT FIELD DELIMITER                  
EOTALT   DC    C''''               END OF TEXT CHR ALTERNATE                    
EOLCHR   DC    C';'                END OF LINE CONTROL CHR                      
EORCHR   DC    C':'                END OF REPORT CONTROL CHR                    
         DC    X'00'               DEFAULT NO INDICATORS                        
         SPACE 1                                                                
         EJECT                                                                  
SPECD    DSECT                                                                  
SPECMAXL DS    H                   MAX LINE WIDTH                               
SPECDELC DS    C                   FIELD DELIMITER CHR                          
SPECEOTC DS    C                   END OF TEXT FIELD DELIMITER                  
SPECEOTA DS    C                   END OF TEXT FIELD ALTERNATE                  
SPECEOLC DS    C                   END OF LINE CHR                              
SPECEORC DS    C                   END OF REPORT CHR                            
SPECCIND DS    C                   CHR INDICATOR                                
         SPACE 2                                                                
WORKD    DSECT                                                                  
DUB1     DS    D                                                                
DUB2     DS    D                                                                
AHOOK    DS    A                                                                
HOOKRD   DS    A                                                                
SPECBASE DS    A                                                                
ATEXT    DS    A                                                                
FLD      DS    XL16                                                             
WORK     DS    XL(L'DLCBFLD)                                                    
*DDEBLOCK                                                                       
       ++INCLUDE DDEBLOCK                                                       
WORKX    EQU   *                                                                
         EJECT                                                                  
*DDDLCB                                                                         
       ++INCLUDE DDDLCB                                                         
         SPACE 1                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'012DDDLFLD   05/14/10'                                      
         END                                                                    
