*          DATA SET TASYSIOB   AT LEVEL 127 AS OF 01/21/16                      
*CATALP TASYSIOB                                                                
         TITLE 'TASYSIOB - UTILITIES FOR SYSIO'                                 
TASYSIOB CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 0,**SYIB*,RA,R9,R8,RR=R2,CLEAR=YES                               
         L     RC,0(R1)            P1=A(MYD)                                    
         USING MYD,RC                                                           
         L     R7,4(R1)            P2=A(SYSIOD)                                 
         USING TASYSIOD,R7                                                      
         ST    R2,RELOB                                                         
         ZIC   RF,0(R1)            P1/B1=ROUTINE NUMBER                         
         STC   RF,ROUTNUM                                                       
         SLL   RF,2                                                             
         L     RF,ROUTADDS(RF)                                                  
         A     RF,RELOB                                                         
         BASR  RE,RF                                                            
         B     XIT                                                              
         SPACE 1                                                                
ROUTADDS DS    0A                                                               
         DC    A(FILTKEY)                                                       
         DC    A(FILTREC)                                                       
         DC    A(FILTCOMM)                                                      
         DC    A(POSTREC)                                                       
         DC    A(SETLST)                                                        
         EJECT                                                                  
*              TEST FILTERS                                                     
         SPACE 3                                                                
FILTKEY  NTR1                                                                   
         OI    STATUS,FKEY         SET FILTERING KEY                            
         CLI   KEY,TLINCDQ         IF INVOICE RECORD                            
         BL    FK04                                                             
         CLI   KEY,TLINPCDQ                                                     
         BH    FK04                                                             
         TM    KEY+TLDRSTAT-TLDRD,TLINSDEL CHECK FOR DELETED KEY                
         BO    FK06                                                             
FK04     CLI   KEY,TLNXCDQ         IF HOLD RECORD                               
         BNE   FK05                                                             
         CLC   TIFUSE,SPACES       IS USE CODE FILTERED?                        
         BNH   FK04C               NO, CONITNUE                                 
         USING TLNXD,RF                                                         
         LA    RF,KEY                                                           
         CLC   TLNXUSE,TIFUSE      YES, MATCH IT                                
         BNE   NO                                                               
         MVC   TIUSE,TLNXUSE                                                    
         DROP  RF                                                               
FK04C    TM    KEY+TLDRSTAT-TLDRD,TLNXSDEL CHECK FOR DELETED KEY                
         BO    FK06                                                             
         B     FK10                                                             
FK05     TM    KEY+TLDRSTAT-TLDRD,X'80'  CHECK FOR DELETED KEY                  
         BZ    FK10                                                             
FK06     TM    TIQFLAGS,TIQFDEL    OK IF USER WANTS DELETES                     
         BZ    NO                                                               
*                                                                               
FK10     TM    TIFSTAT,TIFSPRG     CHECK PURGED RECORDS ONLY                    
         BZ    FK20                                                             
         TM    KEY+TLDRSTAT-TLDRD,TLCOSDEL                                      
         BZ    NO                                                               
         B     FILTALL             TRUE FOR CO AND CK ONLY                      
*                                                                               
FK20     TM    TIFSTAT,TIFSPRGE    CHECK PURGED ERROR RECORDS ONLY              
         BZ    FILTALL                                                          
         TM    KEY+TLDRSTAT-TLDRD,TLCOSERR                                      
         BZ    NO                                                               
         B     FILTALL             TRUE FOR CO ONLY                             
         SPACE 1                                                                
FILTREC  NTR1                                                                   
         NI    STATUS,ALL-FKEY     SET NOT FILTERING KEY                        
         TM    TIQFLAG3,TIQFLAST   IF ONLY WANT LATEST HISTORY                  
         BZ    *+12                                                             
         CLI   IO+TLRCCD-TLRCD,TLHCCDQ    SKIP HISTORY COMMENTS                 
         BE    NO                                                               
         SPACE 1                                                                
         TM    IO+TLRCSTAT-TLRCD,X'80'   CHECK FOR DELETED RECORD               
         BZ    FR10                                                             
         TM    TIQFLAGS,TIQFDEL    OK IF USER WANTS DELETES                     
         BO    FR10                                                             
         B     NO                                                               
         SPACE 1                                                                
FR10     BAS   RE,FILTFLAG         TEST FLAG FILTERS                            
         BNE   NO                                                               
         BRAS  RE,FILTCIHR                                                      
         BNE   NO                                                               
         BRAS  RE,FILTDATE         TEST DATE FILTERS                            
         BNE   NO                                                               
         BAS   RE,FILTSTAT         TEST STATUS FILTERS                          
         BNE   NO                                                               
         BAS   RE,FILTCUST         TEST CUSTOM FILTERS                          
         BNE   NO                                                               
         BAS   RE,FILTCLAN         TEST CLA NETWORK                             
         BNE   NO                                                               
         BAS   RE,FILTUNIT         TEST TAX UNIT                                
         BNE   NO                                                               
         BAS   RE,FILTOVRA         TEST GRT OVERAGE                             
         BNE   NO                                                               
         BAS   RE,FILTVERS         TEST VERSIONS                                
         BNE   NO                                                               
         BAS   RE,FILTAGY          TEST AGENCY                                  
         BNE   NO                                                               
         BAS   RE,FILTNHA          TEST NHA                                     
         BNE   NO                                                               
         BAS   RE,FILTMSC          TEST MSC                                     
         BNE   NO                                                               
         BAS   RE,FILTVITA         TEST VITA BETA                               
         BNE   NO                                                               
         BAS   RE,FILTMUS          TEST MUSIC                                   
         BNE   NO                                                               
         BAS   RE,FILTCSEQ         TEST CAST SEQUENCE                           
         BNE   NO                                                               
*                                                                               
FILTALL  DS    0H                                                               
         BAS   RE,NEEDDATA         MAKE SURE WE HAVE DATA FOR FILTERS           
         SPACE 1                                                                
         TM    STATUS,FKEY         IF NOT FILTERING ON KEY                      
         BO    *+12                                                             
         BAS   RE,FILTSTAT         RE-TEST STATUS FILTERS AFTR NEEDDATA         
         BNE   NO                                                               
         SPACE 1                                                                
         TM    TIQFLAG2,TIQFNLIM   TEST USER WANTS TO SKIP LIMIT CHECK          
         BO    *+12                                                             
         BAS   RE,CHKLIM                                                        
         BNE   NO                                                               
         SPACE 1                                                                
SETLST   DS    0H                  (CHECKS FOR LISTS TO READ)                   
         NI    STATUS,X'FF'-SSNFLIST                                            
         OC    TIFILTS,TIFILTS     ANY FILTERS AT ALL?                          
         BNZ   SETLST1                                                          
         OC    TIFILTS2,TIFILTS2                                                
         BZ    FILTX                                                            
         SPACE 1                                                                
SETLST1  LA    R2,TIFAGG           AGENCY GROUP                                 
         LA    R3,TIAGG                                                         
         LA    R4,L'TIAGG                                                       
         BAS   RE,TESTEM                                                        
         SPACE 1                                                                
         LA    R2,TIFOFF           TP OFFICE                                    
         LA    R3,TIOFF                                                         
         LA    R4,L'TIOFF                                                       
         BAS   RE,TESTEM                                                        
         SPACE 1                                                                
         CLI   ROUTNUM,0           AGENCY                                       
         BNE   FILT2                                                            
         CLC   TIAGY,=C'999999'    IF FILTERING ON KEY                          
         BE    FILT4                  LET AGENCY 999999 PASS FOR NOW            
*                                     REAL FILTERING IS ON RECORD               
         SPACE 1                                                                
FILT2    LA    R2,TIFAGY                                                        
         LA    R3,TIAGY                                                         
         LA    R4,L'TIAGY                                                       
         BAS   RE,TESTEM                                                        
         SPACE 1                                                                
FILT4    LA    R2,TIFCLG           CLIENT GROUP                                 
         LA    R3,TICLG                                                         
         LA    R4,L'TICLG                                                       
         BAS   RE,TESTEM                                                        
         SPACE 1                                                                
         LA    R2,TIFCLI           CLIENT                                       
         LA    R3,TICLI                                                         
         LA    R4,L'TICLI                                                       
         BAS   RE,TESTEM                                                        
         SPACE 1                                                                
         LA    R2,TIFPRG           PRODUCT GROUP                                
         LA    R3,TIPRG                                                         
         LA    R4,L'TIPRG                                                       
         BAS   RE,TESTEM                                                        
         SPACE 1                                                                
         LA    R2,TIFPRD           PRODUCT                                      
         LA    R3,TIPRD                                                         
         LA    R4,L'TIPRD                                                       
         BAS   RE,TESTEM                                                        
         SPACE 1                                                                
         TM    TISTAT,TISTSPCI                                                  
         BO    FILT8                                                            
         LA    R2,TIFISCI          ISCII CODE                                   
         LA    R3,TIISCI                                                        
         LA    R4,L'TIISCI                                                      
         BAS   RE,TESTEM                                                        
         SPACE 1                                                                
FILT8    LA    R2,TIFMUSIC         MUSIC CODE                                   
         LA    R3,TIMUSIC                                                       
         LA    R4,L'TIMUSIC                                                     
         BAS   RE,TESTEM                                                        
         SPACE 1                                                                
         OI    STATUS,SSNFLIST                                                  
         LA    R2,TIFSSN           SOCIAL SECURITY NUMBER                       
         LA    R3,TISSN                                                         
         LA    R4,L'TISSN                                                       
         BAS   RE,TESTEM                                                        
         NI    STATUS,X'FF'-SSNFLIST                                            
         SPACE 1                                                                
         LA    R2,TIFW4TY          W4 TYPE                                      
         LA    R3,TIW4TY                                                        
         LA    R4,L'TIW4TY                                                      
         BAS   RE,TESTEM                                                        
         SPACE 1                                                                
         LA    R2,TIFW4W4          W4 TYPE FROM W4 RECORD                       
         LA    R3,TIW4W4                                                        
         LA    R4,L'TIW4W4                                                      
         BAS   RE,TESTEM                                                        
         SPACE 1                                                                
         LA    R2,TIFCORP          CORPORATION CODE                             
         LA    R3,TICORP                                                        
         LA    R4,L'TICORP                                                      
         BAS   RE,TESTEM                                                        
         SPACE 1                                                                
         LA    R2,TIFAGT           AGENT CODE                                   
         LA    R3,TIAGT                                                         
         LA    R4,L'TIAGT                                                       
         BAS   RE,TESTEM                                                        
         SPACE 1                                                                
         LA    R2,TIFGUA           GUARANTEE CODE                               
         LA    R3,TIGUA                                                         
         LA    R4,L'TIGUA                                                       
         BAS   RE,TESTEM                                                        
         SPACE 1                                                                
         LA    R2,TIFEPI           EPISODE NUMBER                               
         LA    R3,TIEPI                                                         
         LA    R4,L'TIEPI                                                       
         BAS   RE,TESTEM                                                        
         SPACE 1                                                                
         LA    R2,TIFEST           ESTIMATE NUMBER                              
         LA    R3,TIEST                                                         
         LA    R4,L'TIEST                                                       
         BAS   RE,TESTEM                                                        
         SPACE 1                                                                
         LA    R2,TIFESPD          ESTIMATE PERIOD                              
         LA    R3,TIESPD                                                        
         LA    R4,L'TIESPD                                                      
         BAS   RE,TESTEM                                                        
         SPACE 1                                                                
         LA    R2,TIFAUPO          AUTH/PO NUMBER                               
         LA    R3,TIAUPO                                                        
         LA    R4,L'TIAUPO                                                      
         BAS   RE,TESTEM                                                        
         SPACE 1                                                                
         LA    R2,TIFAREA          AREA                                         
         LA    R3,TIAREA                                                        
         LA    R4,L'TIAREA                                                      
         BAS   RE,TESTEM                                                        
         SPACE 1                                                                
         LA    R2,TIFPUSE          PRINT USE                                    
         LA    R3,TIPUSE                                                        
         LA    R4,L'TIPUSE                                                      
         BAS   RE,TESTEM                                                        
         SPACE 1                                                                
         LA    R2,TIFCKID          CHECK ID                                     
         LA    R3,TICKID                                                        
         LA    R4,L'TICKID                                                      
         CLI   ANYINVEL,1                                                       
         BNE   *+8                                                              
         BAS   RE,TESTEM                                                        
         SPACE 1                                                                
         LA    R2,TIFCAT           CATEGORY CODE                                
         LA    R3,TICAT                                                         
         LA    R4,L'TICAT                                                       
         BAS   RE,TESTEM                                                        
         SPACE 1                                                                
         LA    R2,TIFONOF          ON/OFF CAMERA CODE                           
         LA    R3,TIONOF                                                        
         LA    R4,L'TIONOF                                                      
         BAS   RE,TESTEM                                                        
         SPACE 1                                                                
         LA    R2,TIFFILT1         FILTER 1                                     
         LA    R3,TIFILT1                                                       
         LA    R4,L'TIFILT1                                                     
         BAS   RE,TESTEM                                                        
         SPACE 1                                                                
         LA    R2,TIFFILT2         FILTER 2                                     
         LA    R3,TIFILT2                                                       
         LA    R4,L'TIFILT2                                                     
         BAS   RE,TESTEM                                                        
         SPACE 1                                                                
         LA    R2,TIFFILT3         FILTER 3                                     
         LA    R3,TIFILT3                                                       
         LA    R4,L'TIFILT3                                                     
         BAS   RE,TESTEM                                                        
         SPACE 1                                                                
         LA    R2,TIFFILT4         FILTER 4                                     
         LA    R3,TIFILT4                                                       
         LA    R4,L'TIFILT4                                                     
         BAS   RE,TESTEM                                                        
         SPACE 1                                                                
         LA    R2,TIFSTAFF         STAFF CODE                                   
         LA    R3,TISTAFF                                                       
         LA    R4,L'TISTAFF                                                     
         BAS   RE,TESTEM                                                        
         SPACE 1                                                                
         LA    R2,TIFTPC           TPC CODE                                     
         LA    R3,TITPC                                                         
         LA    R4,L'TITPC                                                       
         BAS   RE,TESTEM                                                        
         SPACE 1                                                                
         LA    R2,TIFMGR           MANAGER CODE                                 
         LA    R3,TIMGR                                                         
         LA    R4,L'TIMGR                                                       
         BAS   RE,TESTEM                                                        
         SPACE 1                                                                
         LA    R2,TIFLACT          LAST ACTIVE STAFF NUMBER                     
         LA    R3,TILACT                                                        
         LA    R4,L'TILACT                                                      
         BAS   RE,TESTEM                                                        
         SPACE 1                                                                
         LA    R2,TIFUN            UNION CODE                                   
         LA    R3,TIUN                                                          
         LA    R4,L'TIUN                                                        
         BAS   RE,TESTEM                                                        
         SPACE 1                                                                
         LA    R2,TIFLOCL          UNION LOCAL                                  
         LA    R3,TILOCL                                                        
         LA    R4,L'TILOCL                                                      
         BAS   RE,TESTEM                                                        
         SPACE 1                                                                
         LA    R2,TIFMED           MEDIA CODE                                   
         LA    R3,TIMED                                                         
         LA    R4,L'TIMED                                                       
         BAS   RE,TESTEM                                                        
         SPACE 1                                                                
         LA    R2,TIFSEC           SECONDS LENGTH                               
         LA    R3,TISEC                                                         
         LA    R4,L'TISEC                                                       
         BAS   RE,TESTEM                                                        
         SPACE 1                                                                
         LA    R2,TIFUNIT          TAX UNIT CODE                                
         LA    R3,TIUNIT                                                        
         LA    R4,L'TIUNIT                                                      
         BAS   RE,TESTEM                                                        
         SPACE 1                                                                
         LA    R2,TIFSEX           SEX CODE                                     
         LA    R3,TISEX                                                         
         LA    R4,L'TISEX                                                       
         BAS   RE,TESTEM                                                        
         SPACE 1                                                                
         LA    R2,TIFRACE          RACE CODE                                    
         LA    R3,TIRACE                                                        
         LA    R4,L'TIRACE                                                      
         BAS   RE,TESTEM                                                        
         SPACE 1                                                                
         LA    R2,TIFEMP           EMPLOYER CODE                                
         LA    R3,TIEMP                                                         
         LA    R4,L'TIEMP                                                       
         BAS   RE,TESTEM                                                        
         SPACE 1                                                                
         LA    R2,TIFUSE           USE CODE                                     
         LA    R3,TIUSE                                                         
         LA    R4,L'TIUSE                                                       
         BAS   RE,TESTEM                                                        
         SPACE 1                                                                
         LA    R2,TIFUTYPE         USE TYPE                                     
         LA    R3,TIUTYPE                                                       
         LA    R4,L'TIUTYPE                                                     
         BAS   RE,TESTEM                                                        
         SPACE 1                                                                
         LA    R2,TIFYEAR          YEAR                                         
         LA    R3,TIYEAR                                                        
         LA    R4,L'TIYEAR                                                      
         BAS   RE,TESTEM                                                        
         SPACE 1                                                                
         LA    R2,TIFCTYPE         COMMERCIAL TYPE                              
         LA    R3,TICTYPE                                                       
         LA    R4,L'TICTYPE                                                     
         BAS   RE,TESTEM                                                        
         SPACE 1                                                                
         LA    R2,TIFPTYPE         PAYMENT TYPE                                 
         LA    R3,TIPTYPE                                                       
         LA    R4,L'TIPTYPE                                                     
         BAS   RE,TESTEM                                                        
         SPACE 1                                                                
         LA    R2,TIFCUR           CURRENCY                                     
         LA    R3,TICUR                                                         
         LA    R4,L'TICUR                                                       
         BAS   RE,TESTEM                                                        
         SPACE 1                                                                
         LA    R2,TIFLOCK          LOCKED STATUS                                
         LA    R3,TILOCK                                                        
         LA    R4,L'TILOCK                                                      
         BAS   RE,TESTEM                                                        
         SPACE 1                                                                
         LA    R2,TIFZERO          ZERO BALANCE Y/N                             
         LA    R3,TIZERO                                                        
         LA    R4,L'TIZERO                                                      
         BAS   RE,TESTEM                                                        
         SPACE 1                                                                
         BAS   RE,FILTINV          INCLUDE/EXCLUDE CONV. INVOICES               
         SPACE 1                                                                
FILTX    CLI   ROUTNUM,4                                                        
         BNE   *+8                                                              
         BAS   RE,SETCLFL          SET UP COLUMN FILTER LISTS                   
         TM    STATUS,FKEY         IF FILTERING KEY                             
         BZ    YES                                                              
         TM    TIQFLAGS,TIQFDIR    TEST DIRECTORY HOOK REQUESTED                
         BZ    YES                                                              
         BAS   RE,GOHKDIR          HOOK TO APPLIC. - RETURNS CC                 
         BNE   NO                                                               
         B     YES                                                              
         EJECT                                                                  
*              SPECIAL FILTERING ON FLAG BITS                                   
         SPACE 2                                                                
FILTFLAG NTR1                                                                   
         CLI   ANYPDEL,1           NEED TO HAVE SEEN PAYMENT DTLS. EL.          
         BNE   FFX                                                              
         TM    PAYOPTS+2,TAPDODUM  IGNORE DUMMIES                               
         BZ    FF10                                                             
         TM    TIQFLAGS,TIQFPDUM   UNLESS REQUESTED TO PASS DUMMIES             
         BZ    NO                                                               
         MVC   TIBIDATE,TIPADATE   MAKE 'BILL' DATE = PAID DATE                 
         MVC   TICKDATE,TIPADATE   MAKE 'CHECK' DATE = PAID DATE                
         OI    INVSTAT,TAINSCHK+TAINSBIL   MARK BILLED AND CHECKS               
         SPACE 1                                                                
FF10     TM    PAYOPTS+2,TAPDOGRY  IGNORE GREY                                  
         BZ    *+12                                                             
         TM    TIQFLAG2,TIQFPGRY   UNLESS REQUESTED TO PASS THEM                
         BZ    NO                                                               
         SPACE 1                                                                
         TM    PAYSTAT+1,TAPDSPRM  IF PRIMARY INVOICE                           
         BNO   FF20                                                             
         TM    TIQFLAG2,TIQFPRI    REQUEST TO PASS PRIMARY                      
         BO    FF20                THEN OKAY                                    
         TM    TIQFLAG2,TIQFSUB    REQUEST TO PASS SUBS                         
         BO    NO                  THEN REJECT                                  
         B     FFX                 ELSE OKAY                                    
*                                                                               
FF20     TM    PAYSTAT+1,TAPDSSUB  IF SUBSIDIARY INVOICE                        
         BNO   FFX                                                              
         TM    TIQFLAG2,TIQFSUB    REQUEST TO PASS SUBS                         
         BO    FFX                 THEN OKAY                                    
         BNO   NO                  ELSE REJECT                                  
         SPACE 1                                                                
FFX      B     YES                                                              
         EJECT                                                                  
*              MAKE SURE WE HAVE DATA FOR FILTERING                             
         SPACE 2                                                                
NEEDDATA NTR1                                                                   
         CLI   TIFFTYPE,TIFFTYPA   IF FILTERS ARE AT AGENCY LEVEL               
         BE    NEEDD2                                                           
         TM    TIQCFILT,TIQCFAGY   OR IF AGENCY RELATED COLUMN FILTERS          
         BO    NEEDD2                                                           
         SPACE 1                                                                
         OC    TIFAGG,TIFAGG       OR AGENCY GROUP REQUESTED                    
         BZ    *+14                                                             
         OC    TIAGG,TIAGG         AND WE DON'T HAVE IT                         
         BZ    NEEDD2                                                           
         SPACE 1                                                                
         OC    TIFOFF,TIFOFF       OR TP OFFICE REQUESTED                       
         BZ    NEEDD4                                                           
         OC    TIOFF,TIOFF         AND WE DON'T HAVE IT                         
         BNZ   NEEDD4                                                           
         SPACE 1                                                                
NEEDD2   BAS   RE,NEEDAY           WE WILL NEED RELATED AGENCY                  
         SPACE 1                                                                
NEEDD4   CLI   TIFFTYPE,TIFFTYPC   IF FILTERS ARE AT CLIENT LEVEL               
         BE    NEEDD6                                                           
         TM    TIQCFILT,TIQCFCLI   OR IF CLIENT RELATED COLUMN FILTERS          
         BO    NEEDD6                                                           
         SPACE 1                                                                
         OC    TIFCLG,TIFCLG       OR CLIENT GROUP REQUESTED                    
         BZ    NEEDD8                                                           
         OC    TICLG,TICLG         AND WE DON'T HAVE IT                         
         BNZ   NEEDD8                                                           
         SPACE 1                                                                
NEEDD6   BAS   RE,NEEDCL           WE WILL NEED RELATED CLIENT                  
         SPACE 1                                                                
NEEDD8   TM    TIQCFILT,TIQCFW4    IF W4 RELATED COLUMN FILTERS                 
         BO    NEEDD10                                                          
         SPACE 1                                                                
         CLI   TIFW4W4,0           OR W4 FILTERING FROM W4                      
         BE    NEEDD9                                                           
         CLI   TIW4W4,0            AND WE DON'T HAVE IT                         
         BE    NEEDD10                                                          
         SPACE 1                                                                
NEEDD9   OC    TIFSEX(3),TIFSEX    OR SEX/RACE FILTERS                          
         BZ    NEEDD12                                                          
         OC    TISEX(3),TISEX      AND WE DON'T HAVE IT                         
         BNZ   NEEDD12                                                          
         SPACE 1                                                                
NEEDD10  BAS   RE,NEEDW4           WE WILL NEED RELATED W4                      
         SPACE 1                                                                
NEEDD12  OC    TIAGY,TIAGY         IF WE DON'T HAVE AGENCY                      
         BNZ   NEEDD20                                                          
         OC    TIFAGY,TIFAGY       AND WE'RE FILTERING ON AGENCY                
         BNZ   NEEDD14                                                          
         CLI   NLIMAGY,0           OR AGENCY LIMIT ACCESS IS DEFINED            
         BE    NEEDD20                                                          
         SPACE 1                                                                
NEEDD14  BAS   RE,NEEDCO           WE WILL NEED RELATED COMMERCIAL              
         SPACE 1                                                                
NEEDD20  CLI   TIFCOSTY,0          IF FILTERING ON COML STATUS YES/NO           
         BNE   *+12                                                             
         CLI   TIFCOSTN,0                                                       
         BE    NEEDD30                                                          
         CLI   ANYCOMEL,1          AND WE DON'T HAVE COMMERCIAL STATUS          
         BE    NEEDD30                                                          
         BAS   RE,NEEDCO           WE WILL NEED RELATED COMMERCIAL              
         SPACE 1                                                                
NEEDD30  OC    TIFMED,TIFMED       IF REQUESTING MEDIA                          
         BZ    NEEDDX                                                           
         OC    TIMED,TIMED         AND WE DON'T HAVE IT                         
         BNZ   NEEDDX                                                           
         BAS   RE,NEEDIN           WE WILL NEED RELATED INVOICE                 
         SPACE 1                                                                
NEEDDX   B     XIT                                                              
         EJECT                                                                  
*              NEED AGENCY CONTROL                                              
         SPACE 1                                                                
NEEDAY   NTR1                                                                   
         OC    TIAGY,TIAGY         GET AGENCY DATA IF WE HAVE THE CODE          
         BZ    XIT                                                              
         XC    BUFFAREA,BUFFAREA                                                
         MVI   BUFFKEY,TLAYCDQ                                                  
         MVC   BUFFKEY+1(6),TIAGY                                               
         LA    R1,BUFFDATA                                                      
         USING BUFFDATD,R1                                                      
         BAS   RE,LUPBUFF          IS IT IN BUFFER                              
         BE    NEEDAYX                                                          
         MVC   NEEDKEY,KEY         NO - SO WE'LL HAVE TO READ                   
         MVC   NEEDSAVE,KEYSAVE                                                 
         XC    KEY,KEY                                                          
         LA    R4,KEY                                                           
         USING TLAYD,R4                                                         
         MVI   TLAYCD,TLAYCDQ                                                   
         MVC   TLAYAGY,TIAGY                                                    
         BAS   RE,HIGH                                                          
         CLC   KEY(32),KEYSAVE                                                  
         BNE   NEEDAY2                                                          
*                                                                               
         BAS   RE,GETRECN          GET AGENCY RECORD                            
*                                                                               
         L     R6,TIANEED                                                       
         MVI   ELCODE,TAAYELQ                                                   
         BAS   RE,GETEL                                                         
         USING TAAYD,R6                                                         
*                                                                               
         MVC   BUFFAGRP,TAAYAGG    PASS BACK AGENCY GROUP                       
         CLI   BUFFAGRP,X'41'                                                   
         BH    *+10                                                             
         MVC   BUFFAGRP,=C'OTHERS' DEFAULT TO 'OTHERS'                          
         MVC   BUFFAOFF,TAAYTPOF                                                
         MVC   BUFFAFLT,=X'FFFFFFFF'                                            
         L     R6,TIANEED                                                       
         MVI   ELCODE,TAFLELQ                                                   
         BAS   RE,GETEL                                                         
         BNE   NEEDAY1                                                          
         USING TAFLD,R6                                                         
         MVC   BUFFAFLT,TAFLFLT1   PASS FILTERS 1-4                             
         SPACE 1                                                                
NEEDAY1  BAS   RE,PSTBUFF                                                       
NEEDAY2  MVC   KEY,NEEDKEY         TIDY UP                                      
         BAS   RE,HIGH                                                          
         MVC   KEYSAVE,NEEDSAVE                                                 
         SPACE 1                                                                
NEEDAYX  MVC   TIAGG,BUFFAGRP                                                   
         MVC   TIOFF,BUFFAOFF                                                   
         CLI   TIFFTYPE,TIFFTYPA   IF AGENCY FILTERS WERE REQUESTED             
         BNE   XIT                                                              
         MVC   TIFILT1(4),BUFFAFLT RETURN THESE                                 
         B     XIT                                                              
         DROP  R1                                                               
         EJECT                                                                  
*              NEED CLIENT CONTROL                                              
         SPACE 3                                                                
NEEDCL   NTR1                                                                   
         OC    TICLI,TICLI         GET CLIENT DATA IF WE HAVE THE CODE          
         BZ    XIT                                                              
         XC    BUFFAREA,BUFFAREA                                                
         MVI   BUFFKEY,TLCLCDQ                                                  
         MVC   BUFFKEY+1(6),TIAGY                                               
         MVC   BUFFKEY+7(6),TICLI                                               
         LA    R1,BUFFDATA                                                      
         USING BUFFDATD,R1                                                      
         BAS   RE,LUPBUFF          IS IT IN BUFFER                              
         BE    NEEDCLX                                                          
         MVC   NEEDKEY,KEY         NO - SO WE'LL HAVE TO READ                   
         MVC   NEEDSAVE,KEYSAVE                                                 
         MVC   NEEDFBIT,TISTAT                                                  
         NI    TISTAT,ALL-TISTRDCK ENSURE TALDIR                                
         SPACE 1                                                                
         MVC   BUFFCGRP,=C'OTHERS' SET DEFAULT                                  
         XC    KEY,KEY                                                          
         LA    R4,KEY                                                           
         USING TLCLD,R4                                                         
         MVI   TLCLCD,TLCLCDQ                                                   
         MVC   TLCLCLI,TICLI                                                    
         MVC   TLCLAGY,TIAGY                                                    
         BAS   RE,HIGHN                                                         
         CLC   KEY(32),KEYSAVE                                                  
         BE    NEEDCL1                                                          
         SPACE 1                                                                
         MVC   KEY,KEYSAVE                                                      
         XC    TLCLAGY,TLCLAGY                                                  
         XC    BUFFKEY+1(6),BUFFKEY+1                                           
         BAS   RE,HIGHN                                                         
         CLC   KEY(32),KEYSAVE                                                  
         BNE   NEEDCL2                                                          
         SPACE 1                                                                
NEEDCL1  BAS   RE,GETRECN                                                       
*                                                                               
         L     R6,TIANEED                                                       
         USING TACID,R6                                                         
         MVI   ELCODE,TACIELQ                                                   
         BAS   RE,GETEL                                                         
         BNE   NEEDCL1A                                                         
         CLC   TACICLG,SPACES      IF CLIENT GROUP                              
         BNH   NEEDCL1A                                                         
         MVC   BUFFCGRP,TACICLG    SET IT                                       
*                                                                               
NEEDCL1A MVC   BUFFCFLT,=X'FFFFFFFF'                                            
         L     R6,TIANEED                                                       
         USING TAFLD,R6                                                         
         MVI   ELCODE,TAFLELQ                                                   
         BAS   RE,GETEL                                                         
         BNE   *+10                                                             
         MVC   BUFFCFLT,TAFLFLT1   PASS FILTERS 1-4                             
         SPACE 1                                                                
         BAS   RE,PSTBUFF                                                       
         SPACE 1                                                                
NEEDCL2  MVC   KEY,NEEDKEY         TIDY UP                                      
         TM    NEEDFBIT,TISTRDCK   IF PREVIOUS READS FROM TALDIR                
         BO    *+8                                                              
         BAS   RE,HIGH             REESTABLISH TALDIR SEQ                       
         MVC   KEYSAVE,NEEDSAVE                                                 
         MVC   TISTAT,NEEDFBIT                                                  
         SPACE 1                                                                
NEEDCLX  MVC   TICLG,BUFFCGRP                                                   
         CLI   TIFFTYPE,TIFFTYPC   IF CLIENT FILTERS WERE REQUESTED             
         BNE   XIT                                                              
         MVC   TIFILT1(4),BUFFCFLT RETURN THESE                                 
         B     XIT                                                              
         DROP  R1                                                               
         EJECT                                                                  
*              NEED W4 CONTROL                                                  
         SPACE 3                                                                
NEEDW4   NTR1                                                                   
         OC    TISSN,TISSN         GET W4 DATA IF WE HAVE THE CODE              
         BZ    XIT                                                              
         XC    BUFFAREA,BUFFAREA                                                
         MVI   BUFFKEY,TLW4CDQ                                                  
         MVC   WORK(9),TISSN                                                    
         OC    WORK(9),=9C'0'      PROTECT V SS# WITH SPACES                    
         PACK  DUB,WORK(9)                                                      
         CVB   R1,DUB                                                           
         ST    R1,DUB                                                           
         MVC   BUFFKEY+1(4),DUB                                                 
         LA    R1,BUFFDATA                                                      
         USING BUFFDATD,R1                                                      
         CLI   LOOKUPSW,C'N'       MAY ASK NOT TO LOOK UP                       
         BE    NEEDW41                                                          
         BAS   RE,LUPBUFF          IS IT IN BUFFER                              
         BE    NEEDW4X                                                          
         SPACE 1                                                                
NEEDW41  MVC   NEEDKEY,KEY         NO - SO WE'LL HAVE TO READ                   
         MVC   NEEDSAVE,KEYSAVE                                                 
         MVC   NEEDFBIT,TISTAT                                                  
         NI    TISTAT,ALL-TISTRDCK ENSURE TALDIR                                
         SPACE 1                                                                
         XC    KEY,KEY                                                          
         LA    R4,KEY                                                           
         USING TLW4D,R4                                                         
         MVI   TLW4CD,TLW4CDQ                                                   
         MVC   TLW4SSN,TISSN                                                    
         BAS   RE,HIGH                                                          
         CLC   KEY(32),KEYSAVE                                                  
         BNE   NEEDW42                                                          
         SPACE 1                                                                
         BAS   RE,GETRECN                                                       
         L     R6,TIANEED                                                       
         MVI   ELCODE,TAW4ELQ                                                   
         BAS   RE,GETEL                                                         
         USING TAW4D,R6                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
         MVC   BUFFWSEX,TAW4SEX   PASS BACK SEX                                 
         CLI   BUFFWSEX,C' '                                                    
         BH    *+8                                                              
         MVI   BUFFWSEX,C'M'      (DEFAULT TO MALE)                             
         MVC   BUFFWRAC,TAW4RACE        AND RACE                                
         OC    BUFFWRAC,=C'  '                                                  
         CLC   BUFFWRAC,=C'  '                                                  
         BNE   *+8                                                              
         MVI   BUFFWRAC,C'C'      (DEFAULT TO CAUCASIAN)                        
         MVC   BUFFWTYP,TAW4TYPE       AND W4 TYPE                              
         OI    BUFFWTYP,C' '                                                    
         CLI   BUFFWTYP,C' '                                                    
         BNE   *+8                                                              
         MVI   BUFFWTYP,C'I'      (DEFAULT TO INDIVIDUAL)                       
         MVI   BUFFWNHA,C'N'      (DEFAULT TO NO)                               
         TM    TAW4STA3,TAW4SNHA                                                
         BZ    *+8                                                              
         MVI   BUFFWNHA,C'Y'      ACTIVE NEW HIRE                               
         TM    TAW4STA3,TAW4SNHP                                                
         BZ    *+8                                                              
         MVI   BUFFWNHA,C'P'      PENDING NEW HIRE                              
*                                                                               
         BAS   RE,PSTBUFF                                                       
         SPACE 1                                                                
NEEDW42  CLI   MAINREAD,TLW4CDQ    IF READING CHECKS THROUGH W4 RECORDS         
         BNE   NEEDW43                                                          
         CLI   SUBREAD,TLCKECDQ                                                 
         BNE   NEEDW43                                                          
         MVC   KEY,NEEDKEY         USE NEEDKEY INSTEAD OF PRIMEKEY              
         TM    NEEDFBIT,TISTRDCK                                                
         BO    *+8                                                              
         BAS   RE,HIGH             RESTABLISH TALDIR SEQ                        
         B     *+14                                                             
NEEDW43  MVC   KEY,PRIMEKEY        TIDY UP                                      
         BAS   RE,HIGH             RESTABLISH TALDIR SEQ                        
         SPACE 1                                                                
         MVC   TISTAT,NEEDFBIT                                                  
         MVC   KEY,NEEDKEY                                                      
         MVC   KEYSAVE,NEEDSAVE                                                 
         SPACE 1                                                                
NEEDW4X  MVC   TISEX,BUFFWSEX                                                   
         MVC   TIRACE,BUFFWRAC                                                  
         MVC   TIW4W4,BUFFWTYP                                                  
         MVC   TIW4TY,BUFFWTYP                                                  
         MVC   TINHA,BUFFWNHA                                                   
         B     XIT                                                              
         DROP  R1                                                               
         EJECT                                                                  
*              NEED COMMERCIAL CONTROL                                          
         SPACE 3                                                                
NEEDCO   NTR1                                                                   
         OC    TICOM,TICOM         GET COMML DATA IF WE HAVE THE CODE           
         BZ    XIT                                                              
         XC    BUFFAREA,BUFFAREA   SET FOR BUFFER LOOKUP                        
         MVI   BUFFKEY,TLCOCCDQ    KEY IS RECORD CODE                           
         MVC   BUFFKEY+1(4),TICOM  AND INTERNAL COMMERCIAL NUMBER               
         LA    R1,BUFFDATA                                                      
         USING BUFFDATD,R1                                                      
         BAS   RE,LUPBUFF          IS IT IN BUFFER                              
         BE    NEEDCOX                                                          
         SPACE 1                                                                
         MVC   NEEDKEY,KEY         NO - SO WE'LL HAVE TO READ                   
         MVC   NEEDSAVE,KEYSAVE                                                 
         SPACE 1                                                                
         XC    KEY,KEY             BUILD KEY FOR COMMERCIAL RECORD              
         LA    R4,KEY                                                           
         USING TLCOPD,R4                                                        
         MVI   TLCOPCD,TLCOCCDQ                                                 
         MVC   TLCOCCOM,TICOM                                                   
         BAS   RE,HIGH                                                          
         CLC   KEY(L'TLCOPKEY),KEYSAVE                                          
         BNE   NEEDCO2                                                          
         SPACE 1                                                                
         BAS   RE,GETRECN          GET THE RECORD                               
         L     R6,TIANEED                                                       
         USING TLCOD,R6                                                         
         MVC   BUFFCOAY,TLCOAGY  AGENCY IS IN ACTIVE KEY                        
         MVI   ELCODE,TACOELQ                                                   
         BAS   RE,GETEL          GET COMMERCIAL DETAILS ELEMENT                 
         BNE   NEEDCO1                                                          
         USING TACOD,R6                                                         
         MVC   BUFFCOST,TACOSTAT SAVE STATUS                                    
*                                                                               
NEEDCO1  BAS   RE,PSTBUFF                                                       
         SPACE 1                                                                
NEEDCO2  MVC   KEY,NEEDKEY         TIDY UP                                      
         BAS   RE,HIGH             RESTABLISH TALDIR SEQ                        
         MVC   KEYSAVE,NEEDSAVE                                                 
         SPACE 1                                                                
NEEDCOX  MVC   TIAGY,BUFFCOAY      EXTRACT AGENCY FROM BUFFDATA                 
         MVC   COMSTAT,BUFFCOST    AND EXTRACT COMMERCIAL STATUS                
         MVI   ANYCOMEL,1          FUDGE FOUND COMMERCIAL ELEMENT               
         B     XIT                                                              
         DROP  R1                                                               
         EJECT                                                                  
*              NEED INVOICE CONTROL                                             
         SPACE 3                                                                
NEEDIN   NTR1                                                                   
         OC    TIAGY,TIAGY         GET INVOICE DATA IF WE HAVE                  
         BZ    XIT                                                              
         OC    TIINV,TIINV         AGENCY AND INVOICE CODES                     
         BZ    XIT                                                              
         XC    BUFFAREA,BUFFAREA   SET FOR BUFFER LOOKUP                        
         MVI   BUFFKEY,TLINCDQ     KEY IS RECORD CODE                           
         MVC   BUFFKEY+1(6),TIAGY  AND AGENCY CODE                              
         MVC   BUFFKEY+7(6),TIINV  AND INVOICE CODE                             
         LA    R1,BUFFDATA                                                      
         USING BUFFDATD,R1                                                      
         BAS   RE,LUPBUFF          IS IT IN BUFFER                              
         BE    NEEDINX                                                          
         SPACE 1                                                                
         MVC   NEEDKEY,KEY         NO - SO WE'LL HAVE TO READ                   
         MVC   NEEDSAVE,KEYSAVE                                                 
         MVC   NEEDFBIT,TISTAT                                                  
         NI    TISTAT,ALL-TISTRDCK ENSURE TALDIR                                
         SPACE 1                                                                
         XC    KEY,KEY             BUILD KEY FOR INVOICE RECORD                 
         LA    R4,KEY                                                           
         USING TLIND,R4                                                         
         MVI   TLINCD,TLINCDQ                                                   
         MVC   TLINAGY,TIAGY                                                    
         MVC   TLININV,TIINV                                                    
         XC    TLININV,COMPLM                                                   
         BAS   RE,HIGH                                                          
         CLC   KEY(L'TLINKEY),KEYSAVE                                           
         BNE   NEEDIN2                                                          
         SPACE 1                                                                
         BAS   RE,GETRECN          GET THE RECORD                               
         L     R6,TIANEED                                                       
         USING TLCOD,R6                                                         
         MVI   ELCODE,TACOELQ                                                   
         BAS   RE,GETEL            GET COMMERCIAL DETAILS ELEMENT               
         BNE   NEEDIN1                                                          
         USING TACOD,R6                                                         
         MVC   BUFFIMED,TACOMED    SAVE MEDIA                                   
*                                                                               
NEEDIN1  BAS   RE,PSTBUFF                                                       
         SPACE 1                                                                
NEEDIN2  MVC   KEY,NEEDKEY         TIDY UP                                      
         MVC   KEYSAVE,NEEDSAVE                                                 
         TM    NEEDFBIT,TISTRDCK   IF PREVIOUS READS FROM TALDIR                
         BO    *+8                                                              
         BAS   RE,HIGH             RESTABLISH TALDIR SEQ                        
         MVC   TISTAT,NEEDFBIT                                                  
         SPACE 1                                                                
NEEDINX  MVC   TIMED,BUFFIMED      EXTRACT MEDIA                                
         B     XIT                                                              
         DROP  R1                                                               
         EJECT                                                                  
*              BUFFER HANDLING FOR NEED ROUTINES                                
         SPACE 3                                                                
*                                  TRY AND MATCH ON BUFFKEY                     
LUPBUFF  NTR1                                                                   
         OC    TIABUFF,TIABUFF                                                  
         BZ    NO                                                               
         L     R2,TIABUFF                                                       
         L     R0,0(R2)                                                         
         LA    R2,8(R2)                                                         
         LTR   R0,R0                                                            
         BZ    NO                                                               
         SPACE 1                                                                
LUPBUFF2 CLC   BUFFKEY,0(R2)       MATCH ON KEY                                 
         BE    LUPBUFF4                                                         
         LA    R2,L'BUFFAREA(R2)                                                
         BCT   R0,LUPBUFF2                                                      
         B     NO                                                               
         SPACE 1                                                                
LUPBUFF4 MVC   BUFFAREA,0(R2)      FOUND - RETURN KEY AND DATA                  
         B     YES                                                              
         SPACE 1                                                                
*                                  POST BUFFAREA INTO BUFFER                    
PSTBUFF  NTR1                                                                   
         OC    TIABUFF,TIABUFF     IF THERE IS ONE                              
         BZ    NO                                                               
         L     R2,TIABUFF                                                       
         L     R1,4(R2)            BUFF+4 HAS CURRENT POINTER #                 
         MH    R1,=Y(L'BUFFAREA)   (* 26 FOR DISPLACEMENT)                      
         LA    R1,8(R1,R2)         NOW ADDRESS INTO BUFFER SLOT                 
         MVC   0(L'BUFFAREA,R1),BUFFAREA   AND POST THE DATA                    
         L     R1,4(R2)            ADD 1 TO POINTER                             
         LA    R1,1(R1)                                                         
         ST    R1,4(R2)                                                         
         C     R1,0(R2)            IF THIS IS NOW AT MAX                        
         BNE   YES                                                              
         XC    4(4,R2),4(R2)          RESET POINTER TO START                    
         B     YES                                                              
         EJECT                                                                  
*              LITTLE ROUTINE TO TEST FILTERS                                   
         SPACE 3                                                                
*                                  R2=A(REQUESTED FILTER)                       
*                                  R3=A(DEDUCED VALUE)                          
*                                  R4=LENGTH                                    
         SPACE 1                                                                
TESTEM   LR    R1,R2               SAVE A(FIRST CHARACTER)                      
         CLI   ROUTNUM,4                                                        
         BE    SETLST2                                                          
         CLI   0(R2),X'40'                                                      
         BER   RE                                                               
         CLI   0(R2),X'00'                                                      
         BER   RE                                                               
         TM    TIQFLAG2,TIQFEXCT   IF EXACT MATCH ON FILTER REQUESTED           
         BZ    TESTEM2                                                          
         LR    R0,RE                                                            
         BAS   RE,TSTEXACT         TEST CORRECT LEVEL TO FILTER EXACTLY         
         LR    RE,R0                                                            
         BE    TESTEM4             YES                                          
TESTEM2  CLI   0(R3),X'41'         NO - BLANKS OKAY FOR NOW                     
         BLR   RE                                                               
         SPACE 1                                                                
TESTEM4  CLI   0(R2),X'5C'         UNLESS THIS IS A *                           
         BE    TSTF2                                                            
         CLI   0(R2),X'1C'                     OR A -*                          
         BE    TSTF2                                                            
         TM    0(R2),X'80'         MAY BE A LIST                                
         BNO   TSTLST                                                           
         SPACE 1                                                                
TSTF2    DS    0H                                                               
******** CLI   0(R2),X'41'         WE'LL TEST TRAILING SPACE NOW                
******** BL    TSTFNXT                                                          
         CLI   0(R2),C'*'          WILD CARD                                    
         BE    TSTFNXT                                                          
         CLI   0(R2),X'1C'         (NEGATIVE *)                                 
         BE    TSTFNXT                                                          
         TM    0(R1),X'40'         TEST FIRST BYTE HERE                         
         BNO   TSTFNEG                                                          
         CLC   0(1,R2),0(R3)                                                    
         BNE   NO                                                               
         B     TSTFNXT                                                          
         SPACE 1                                                                
TSTFNEG  MVC   BYTE,0(R2)          NEGATIVE TEST                                
         OI    BYTE,X'40'                                                       
         CLC   BYTE,0(R3)                                                       
         BNER  RE                  IF ANY BYTE DIFFERS, ITS OK                  
         SPACE 1                                                                
TSTFNXT  LA    R2,1(R2)                                                         
         LA    R3,1(R3)                                                         
         BCT   R4,TSTF2                                                         
         TM    0(R1),X'40'         IF WE GOT THIS FAR FOR NEGATIVE              
         BNO   NO                  THEN IT'S NO GOOD                            
         BR    RE                                                               
*                                                                               
TNO      LTR   RB,RB                                                            
         BR    RE                                                               
TYES     CR    RB,RB                                                            
         BR    RE                                                               
         EJECT                                                                  
*              TEST CORRECT LEVEL TO FILTER EXACTLY                             
         SPACE                                                                  
TSTEXACT NTR1                                                                   
         TM    STATUS,FKEY         IF FILTERING AT THE RECORD LEVEL             
         BO    NO                                                               
         CLI   SUBREAD,0           AND READING ONLY ONE RECORD                  
         BE    YES                 EXACT MATCH ALWAYS NEEDED                    
*                                                                               
         L     R1,TIAREC           R1=A(CURRENT RECORD)                         
         TM    TIFLTLVL,TIFLVLCK   IF FILTER FOR THE CHECK LEVEL                
         BZ    *+12                                                             
         CLI   0(R1),TLCKCDQ       AND HANDLING CHECK RECORD                    
         BE    YES                 MATCH EXACTLY ON FILTER                      
         TM    TIFLTLVL,TIFLVLIN   IF FILTER FOR THE INVOICE LEVEL              
         BZ    *+12                                                             
         CLI   0(R1),TLINCDQ       AND HANDLING INVOICE RECORD                  
         BE    YES                 MATCH EXACTLY ON FILTER                      
         TM    TIFLTLVL,TIFLVLCO   IF FILTER FOR THE COMMERCIAL LEVEL           
         BZ    *+12                                                             
         CLI   0(R1),TLCOCDQ       AND HANDLING COMMERCIAL RECORD               
         BE    YES                 MATCH EXACTLY ON FILTER                      
         TM    TIFLTLVL,TIFLVLCA   IF FILTER FOR THE CAST LEVEL                 
         BZ    *+12                                                             
         CLI   0(R1),TLCACDQ       AND HANDLING CAST RECORD                     
         BE    YES                 MATCH EXACTLY ON FILTER                      
         B     NO                  ELSE - BLANKS OKAY FOR NOW                   
         EJECT                                                                  
*              ROUTINE TO CHECK AGAINST A LIST                                  
         SPACE 3                                                                
*                                  R2=A(REQUESTED FILTER)                       
*                                  R3=A(DEDUCED VALUE)                          
*                                  R4=LENGTH                                    
         SPACE 1                                                                
TSTLST   MVC   WORK(8),SPACES                                                   
         STM   R2,R4,WORK+12       SAVE PARAMETERS AT WORK+12                   
         BCTR  R4,0                                                             
         EX    R4,*+8                                                           
         B     *+10                                                             
         MVC   WORK(0),0(R2)       GET REQUESTED FILTER                         
         OI    WORK,X'C0'          REMOVE CONTROL BITS                          
         MVC   WORK+8(1),0(R2)     BUT SAVE FIRST BYTE AT WORK+8                
         LA    R2,GLALST1                                                       
         CLC   GLCODE1,WORK        NOW FIND A MATCH ON LIST DETAILS             
         BE    TSTLST2                                                          
         LA    R2,GLALST2                                                       
         CLC   GLCODE2,WORK                                                     
         BE    TSTLST2                                                          
         LA    R2,GLALST3                                                       
         CLC   GLCODE3,WORK                                                     
         BE    TSTLST2                                                          
         DC    H'0'                TAKE A HIT - NONE FOUND                      
*                                                                               
         USING LISTD,R2                                                         
TSTLST2  ZIC   R0,LISTNENT         PICK UP N'ENTRIES IN THIS LIST               
         LTR   R0,R0               PROTECT AGAINST EMPTY LISTS                  
         BZ    TYES                                                             
         L     R1,LISTALST         AND THE A(ENTRIES)                           
*                                                                               
TSTLST4  LM    R3,R4,WORK+16       RESTORE R3/R4 TO VALUE/LENGTH                
         LR    RF,R1               RF=A(LIST ITEM)                              
*                                                                               
TSTLST6  CLI   0(RF),C'*'          CHECK WILD CARD                              
         BE    TSTLST8                                                          
         CLC   0(1,RF),0(R3)       ELSE MATCH ON CHARACTER                      
         BE    TSTLST8                                                          
*                                                                               
         LA    R1,16(R1)           FAILED TO MATCH - TRY NEXT IN LIST           
         BCT   R0,TSTLST4                                                       
*                                                                               
         TM    WORK+8,X'40'        FAILED TO MATCH ANY LIST ENTRY               
         BO    NO                  IF POSITIVE LIST, REJECT                     
         J     TYES                ELSE, IT'S OK                                
         SPACE 1                                                                
TSTLST8  LA    R3,1(R3)            ONE CHARACTER MATCHED - TRY NEXT             
         LA    RF,1(RF)                                                         
         BCT   R4,TSTLST6                                                       
*                                                                               
         TM    WORK+8,X'40'        GOT A MATCH ON LIST                          
         BNO   NO                  IF NEGATIVE LIST, REJECT                     
         J     TYES                ELSE, IT'S OK                                
         EJECT                                                                  
*              ROUTINE TO CHECK FOR LISTS IN COLUMN FILTER                      
         SPACE 3                                                                
SETCLFL  NTR1                                                                   
         L     R3,TIACOLFL                                                      
         LTR   R3,R3                                                            
         BZ    XIT                                                              
         LA    R0,32               (UP TO 32 SLOTS)                             
         SPACE 1                                                                
SETCLFL2 CLI   0(R3),0                                                          
         BE    XIT                                                              
         TM    3(R3),X'80'         ITS A LIST IF X'80' IS OFF                   
         BO    SETCLFL4                                                         
         LA    R2,4(R3)            R2=A(LIST CODE)                              
         LA    R4,8                                                             
         BAS   RE,SETLST3                                                       
         SPACE 1                                                                
SETCLFL4 LA    R3,16(R3)                                                        
         BCT   R0,SETCLFL2                                                      
         B     XIT                                                              
         EJECT                                                                  
*              ROUTINE TO CHECK FOR LIST FILTERS                                
         SPACE 3                                                                
SETLST2  NTR1                                                                   
         CLI   0(R2),0                                                          
         BE    YES                                                              
         CLI   0(R2),X'40'                                                      
         BE    YES                                                              
         CLI   0(R2),X'5C'         UNLESS THIS IS A *                           
         BE    YES                                                              
         CLI   0(R2),X'1C'                     OR A -*                          
         BE    YES                                                              
         TM    0(R2),X'80'         IF X'80' IS OFF, THIS IS A LIST              
         BO    YES                                                              
         B     SETLST3B                                                         
         SPACE 1                                                                
SETLST3  NTR1                      USED BY COLUMN FILTERS                       
         SPACE 1                                                                
SETLST3B MVC   WORK(8),SPACES      FIRST GET CODE INTO WORK                     
         BCTR  R4,0                                                             
         EX    R4,*+8                                                           
         B     *+10                                                             
         MVC   WORK(0),0(R2)                                                    
         OI    WORK,X'C0'          TURN LIST BIT (& NEG) BACK ON                
         SPACE 1                                                                
         LA    R2,GLALST1          NEXT FIND A SLOT                             
         USING LISTD,R2                                                         
         LA    R3,IO                                                            
         AHI   R3,L'IO+L'IO2+L'IO3 R3=A(GLIST1)                                 
         LA    R0,3                3 SLOTS SUPPORTED                            
         SPACE 1                                                                
SETLST4  OC    LISTALST,LISTALST   LOOK FOR EMPTY SLOT                          
         BZ    SETLST6                                                          
         CLC   LISTCODE,WORK       OR A MATCH ON CODE                           
         BE    XIT                                                              
         LA    R2,L'LISTENT(R2)                                                 
         LA    R3,1600(R3)         L'DATA ENTRIES * MAX ENTRIES                 
         BCT   R0,SETLST4                                                       
         B     XIT                                                              
         SPACE 1                                                                
SETLST6  ST    R3,LISTALST         SAVE DETAILS OF THIS LIST                    
         MVC   LISTCODE,WORK                                                    
         MVI   LISTLENT,16         L'DATA ENTRIES WILL BE 16                    
         SPACE 1                                                                
         LA    R4,KEY              NOW GO AND READ THIS LIST                    
         XC    KEY,KEY                                                          
         USING TLGLD,R4                                                         
         MVI   TLGLCD,TLGLCDQ                                                   
         MVI   TLGLTYPE,TLGLTYPF   (TYPE=FILTER)                                
         MVC   TLGLLST,LISTCODE                                                 
         BAS   RE,HIGH                                                          
         CLC   KEY(32),KEYSAVE                                                  
         BNE   XIT                                                              
         BAS   RE,GETREC                                                        
         LA    R6,IO                                                            
         MVI   ELCODE,TAGLELQ      LOOK FOR LIST DATA ELS.                      
         L     R3,LISTALST                                                      
         LA    R0,100              MAX 100 ENTRIES IN LIST                      
         SR    R4,R4                                                            
         DROP  R4                                                               
         BAS   RE,GETEL                                                         
         B     *+8                                                              
         SPACE 1                                                                
SETLST8  BAS   RE,NEXTEL                                                        
         BNE   XIT                                                              
         USING TAGLD,R6                                                         
         LA    R4,1(R4)            BUMP N'ELS                                   
         STC   R4,LISTNENT         SAVE THIS AS N'ENTRIES                       
         ZIC   R1,TAGLLEN                                                       
         SH    R1,=H'4'                                                         
         CH    R1,=H'15'                                                        
         BL    *+8                                                              
         LA    R1,15                                                            
         MVC   0(16,R3),SPACES                                                  
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R3),TAGLDATA                                                 
         BAS   RE,PIDTRANS                                                      
         LA    R3,16(R3)                                                        
         BCT   R0,SETLST8                                                       
         B     XIT                                                              
         SPACE 2                                                                
*              CUSTOM FILTERS                                                   
         SPACE 3                                                                
FILTCUST NTR1                                                                   
         CLI   TIFCUST,0           FCUST HAS ROUTINE NUMBER                     
         BE    YES                 NONE SPECIFIED                               
         L     R6,TIAREC                                                        
         ZIC   RF,TIFCUST          ELSE, PICK UP ROUTINE NUMBER                 
         BCTR  RF,0                                                             
         SLL   RF,2                                                             
         B     CUSTBRAN(RF)                                                     
         SPACE 1                                                                
CUSTBRAN DS    0H                                                               
         B     FILTCST1                                                         
         B     FILTCST2                                                         
         B     FILTCST3                                                         
         B     FILTCST4                                                         
         B     FILTCST5                                                         
         B     FILTCST6                                                         
         B     FILTCST7                                                         
         B     FILTCST8                                                         
         B     FILTCST9                                                         
         B     FCUST10                                                          
         B     FCUST11                                                          
         B     FCUST12                                                          
         B     FCUST13                                                          
         B     FCUST14                                                          
         B     YES                 15=CRSGUA (FILTERED LOCALLY)                 
         B     FCUST16                                                          
         B     FCUST17                                                          
         B     FCUST18                                                          
         B     FCUST19                                                          
         B     FCUST20                                                          
         B     FCUST21                                                          
         B     FCUST22                                                          
         B     FCUST23                                                          
         B     FCUST24                                                          
         B     FCUST25                                                          
         B     FCUST26             26=DIRECT (DIRECT DEPOSIT & WIRE )           
         B     FCUST27             27=ADVICES GENERATED THROUGH NETTAL          
         B     FCUST28             28=NETTAL ADVICES NOT PAID                   
         B     FCUST29             29=NETTAL HOLD RECORDS NOT MATCHED           
         B     FCUST30             30=SEE IF ANY CHECKS IN PERIOD               
         B     FCUST31             31=CHECKS WITH OVERSCALE                     
         B     FCUST32             32=RECORDS WITHOUT SRC RATES                 
         B     FCUST33             33=SKIP W4 TRUSTEE GENERATED CHKS            
         B     FCUST34             34=TRUSTEE PAYMENTS                          
         B     FCUST35             35=CHECKS > $10000                           
         B     FCUST36             36=STOPPED CHECKS                            
         B     FCUST37             37=EFT LIENS                                 
         SPACE 1                                                                
FILTCST1 MVI   ELCODE,TACDELQ      CHECKS > $5000                               
         BAS   RE,GETEL                                                         
         BNE   YES                 (MAY BE PASSED AN INVOICE)                   
         USING TACDD,R6                                                         
         L     R1,TACDNET                                                       
         C     R1,=F'500000'                                                    
         BL    NO                                                               
         B     YES                                                              
         SPACE 1                                                                
FILTCST2 MVI   ELCODE,TAPDELQ      BS* AND HLD, SHL AND ADH                     
         BAS   RE,GETEL                                                         
         BNE   NO                                                               
         USING TAPDD,R6                                                         
         CLC   TAPDUSE(2),=C'BS'                                                
         BE    YES                                                              
         CLC   TAPDUSE,=C'HLD'                                                  
         BE    YES                                                              
         CLC   TAPDUSE,=C'SHL'                                                  
         BE    YES                                                              
         CLC   TAPDUSE,=C'ADH'                                                  
         BE    YES                                                              
         B     NO                                                               
         SPACE 1                                                                
*                                  CUSTOM 3 - TV, APPLY 1/2, CLA OR CAB         
FILTCST3 CLI   0(R6),TLINCDQ       FILTER MEDIA FROM INVOICE                    
         BNE   FCST3CK                                                          
         MVI   ELCODE,TACOELQ      TV ONLY.....                                 
         BAS   RE,GETEL                                                         
         BNE   NO                                                               
         USING TACOD,R6                                                         
         CLI   TACOMED,TACOMEDT                                                 
         BE    YES                                                              
         CLI   TACOMED,TACOMEDI    OR INTERNET                                  
         BE    YES                                                              
         CLI   TACOMED,TACOMEDN    OR NEW MEDIA                                 
         BNE   NO                                                               
         B     YES                                                              
         SPACE 1                                                                
FCST3CK  MVI   ELCODE,TAPDELQ      OTHER TESTS FROM CHECKS                      
         BAS   RE,GETEL                                                         
         BNE   NO                                                               
         USING TAPDD,R6                                                         
         CLI   TAPDACDE,C'1'       MUST BE APPLY CODE OF 1 OR 2                 
         BL    NO                                                               
         CLI   TAPDACDE,C'2'                                                    
         BH    NO                                                               
         CLC   TAPDUSE,=C'CLA'     CLA OR CLB                                   
         BE    YES                                                              
         CLC   TAPDUSE,=C'CAB'                                                  
         BE    YES                                                              
         B     NO                                                               
         SPACE 1                                                                
FILTCST4 MVI   ELCODE,TACOELQ      FIRST AIR DATE WITHIN REQUEST                
         BAS   RE,GETEL                                                         
         BNE   NO                                                               
         USING TACOD,R6                                                         
         CLC   TACOAIR,TIQPSTR                                                  
         BL    NO                                                               
         CLC   TACOAIR,TIQPEND                                                  
         BH    NO                                                               
         B     YES                                                              
         SPACE 1                                                                
FILTCST5 MVI   ELCODE,TACDELQ      NO CHECKS GENERATED BY LIENS                 
         BAS   RE,GETEL                                                         
         BNE   YES                 (MAY BE PASSED AN INVOICE)                   
         USING TACDD,R6                                                         
         TM    TACDSTAT,TACDSLIN                                                
         BO    NO                                                               
         B     YES                                                              
         SPACE 1                                                                
FILTCST6 LA    R2,1                CYCLES STARTING 1 4 7 10                     
         B     FILTFFC                                                          
         SPACE 1                                                                
FILTCST7 LA    R2,2                CYCLES STARTING 2 5 8 11                     
         B     FILTFFC                                                          
         SPACE 1                                                                
FILTCST8 SR    R2,R2               CYCLES STARTING 3 6 9 12                     
         SPACE 1                                                                
FILTFFC  CLI   TIREAD,TLCACDQ      CHECK IF READING CAST OR COMM                
         BE    FILTFFC2                                                         
         MVI   ELCODE,TACOELQ      CHECK TACO                                   
         BAS   RE,GETEL                                                         
         BNE   YES                                                              
         USING TACOD,R6                                                         
         ZIC   R1,TACOFCYC+1       PICK UP FFC MONTH                            
         B     FILTFFC4                                                         
         SPACE 1                                                                
FILTFFC2 MVI   ELCODE,TACAELQ      CHECK TACA                                   
         BAS   RE,GETEL                                                         
         BNE   YES                                                              
         USING TACAD,R6                                                         
         ZIC   R1,TACAFCYC+1       PICK UP FFC MONTH                            
         SPACE 1                                                                
FILTFFC4 LTR   R1,R1               NOW HAVE MONTH IN R1                         
         BZ    NO                                                               
         SR    R0,R0                                                            
         D     R0,=F'3'            THEN GET 0, 1 OR 2 IN R0                     
         CR    R0,R2               CHECK FOR MATCH ON MONTH RANGE               
         BE    YES                                                              
         B     NO                                                               
         SPACE 1                                                                
FILTCST9 MVI   ELCODE,TACRELQ      UNUSED HOLDING FEES                          
         BAS   RE,GETEL                                                         
         BNE   YES                 (IF NONE, LOOKING AT COMMERCIAL)             
         B     *+8                                                              
         SPACE 1                                                                
CUST9B   BAS   RE,NEXTEL                                                        
         BNE   NO                                                               
         USING TACRD,R6                                                         
         CLC   TACREND,TIQPSTR     FIND TACR FOR THIS PERIOD                    
         BL    CUST9B                                                           
         CLC   TACREND,TIQPEND                                                  
         BH    CUST9B                                                           
         CLC   TACRUSE,=C'HLD'          FOR HOLDING FEE TYPES                   
         BE    CUST9F                                                           
         CLC   TACRUSE,=C'SHL'                                                  
         BE    CUST9F                                                           
         CLC   TACRUSE,=C'ADH'                                                  
         BNE   CUST9B                                                           
CUST9F   CLC   TACRAPPL,TACRBAL    IS APPLIED STILL = BALANCE?                  
         BE    YES                                                              
         B     NO                                                               
         SPACE 1                                                                
FCUST10  MVI   ELCODE,TAINELQ      COD PRINTED INVOICES                         
         BAS   RE,GETEL                                                         
         BNE   NO                                                               
         USING TAIND,R6                                                         
         ZIC   R0,TAINLEN          MUST HAVE PRINT DATE                         
         LA    R1,TAINHDTE-TAIND+3                                              
         CR    R0,R1                                                            
         BL    NO                                                               
         OC    TAINHDTE,TAINHDTE   MUST HAVE PRINT DATE                         
         BZ    NO                                                               
         OC    TIQPSTR,TIQPSTR     OPTIONAL DATE FILTERS                        
         BZ    YES                                                              
         CLC   TAINHDTE,TIQPSTR                                                 
         BL    NO                                                               
         CLC   TAINHDTE,TIQPEND                                                 
         BH    NO                                                               
         B     YES                                                              
         SPACE 1                                                                
FCUST11  LA    R3,SAGLIST          CHECK ADDRESS FOR SAG                        
         BAS   RE,SCANADD                                                       
         BE    YES                                                              
         B     NO                                                               
         SPACE 1                                                                
FCUST12  LA    R3,AFTLIST          CHECK ADDRESS FOR AFT                        
         BAS   RE,SCANADD                                                       
         BE    YES                                                              
         B     NO                                                               
         SPACE 1                                                                
FCUST13  MVI   ELCODE,TACMELQ      RETRO FTRACK RECORDS                         
         BAS   RE,GETEL                                                         
         BNE   NO                                                               
         USING TACMD,R6                                                         
         CLC   =C'RETROACTIVE RATE INCREASE',TACMCOMM                           
         BNE   NO                                                               
         B     YES                                                              
         SPACE 1                                                                
FCUST14  MVI   ELCODE,TADUELQ      DUE COMPANY RECORDS                          
         BAS   RE,GETEL                                                         
         BNE   NO                                                               
         USING TADUD,R6                                                         
         TM    TADUSTAT,TADUSHLD   IS RECORD ON HOLD                            
         BNO   NO                                                               
         B     YES                                                              
         SPACE 1                                                                
*                                  ACCRUED CUSTOM FILTER                        
FCUST16  MVI   ELCODE,TACDELQ      CHECKS DATED AFTER END DATE                  
         BAS   RE,GETEL                                                         
         BNE   YES                 (MAY BE PASSED AN INVOICE)                   
         USING TACDD,R6                                                         
         OC    TACDDTE,TACDDTE     OK IF NOT PROCESSED YET                      
         BZ    YES                                                              
         CLC   TACDDTE,TIQPEND     OR IF DATED AFTER END DATE                   
         BH    YES                                                              
         B     NO                                                               
         SPACE 1                                                                
FCUST17  LA    R3,ESTLIST          CHECK ADDRESS FOR ESTATES                    
         BAS   RE,SCANADD                                                       
         BE    YES                                                              
         B     NO                                                               
         SPACE 1                                                                
FCUST18  MVI   ELCODE,TAA2ELQ      CHECK W4 RES NOT EQU W/H                     
         BAS   RE,GETEL                                                         
         BNE   NO                                                               
         USING TAA2D,R6                                                         
         MVC   WORK(2),TAA2ST      FIRST GET STATE OF RES                       
         L     R6,TIAREC                                                        
         MVI   ELCODE,TAWHELQ      NOW LOOK FOR WITHHOLDING                     
         BAS   RE,GETEL                                                         
         B     *+8                                                              
         SPACE 1                                                                
FCUST182 BAS   RE,NEXTEL                                                        
         BNE   NO                                                               
         USING TAWHD,R6                                                         
         CLC   TAWHUNIT(2),=C'FD'  IGNORE STATE                                 
         BE    FCUST182                                                         
         CLI   TAWHUNIT+2,C' '     AND CITIES                                   
         BH    FCUST182                                                         
         CLC   TAWHUNIT(2),WORK    NOW CHECK STATES                             
         BNE   YES                 NEED TO BE UNEQUAL TO PASS                   
         B     NO                                                               
*                                  MULTIPLE CORPS                               
FCUST19  MVI   ELCODE,TATIELQ      GET TAX ID ELEMENT                           
         BAS   RE,GETEL                                                         
         BNE   NO                                                               
         BAS   RE,NEXTEL                                                        
         BNE   NO                  NEED AT LEAST 2                              
         B     YES                                                              
         SPACE 1                                                                
*                                  PAYMENTS TO CORPS DIRECTLY W/O INDIV         
FCUST20  CLI   0(R6),TLCKCDQ       ONLY WANT CHECKS                             
         BNE   YES                                                              
         MVI   ELCODE,TAPDELQ      GET PAYMENT DETAILS                          
         BAS   RE,GETEL                                                         
         BNE   NO                                                               
         USING TAPDD,R6                                                         
         CLI   TAPDW4TY,TAW4TYCO   TEST PAYMENT TO CORP                         
         BE    *+12                                                             
         CLI   TAPDW4TY,TAW4TYCA   OR CANADIAN                                  
         BNE   NO                                                               
         L     R6,TIAREC                                                        
         MVI   ELCODE,TATIELQ      GET TAX ID ELEMENT FOR CORP                  
         BAS   RE,GETEL                                                         
         BNE   YES                 IF NONE, PYMNT TO CORP DIRECTLY              
         B     NO                                                               
         EJECT                                                                  
*                                  CHECKS THAT WENT TO DEFAULT STATE            
FCUST21  CLI   0(R6),TLCKCDQ       ONLY WANT CHECKS                             
         BNE   YES                                                              
         MVI   ELCODE,TACWELQ      SCAN CHECK WITHHOLDING ELEMENTS              
         BAS   RE,GETEL                                                         
         B     *+8                                                              
FCST21B  BAS   RE,NEXTEL                                                        
         BNE   NO                                                               
         USING TACWD,R6                                                         
         TM    TACWSTAT,TACWSDEF   ENOUGH TO SIMPLY TEST FOR BIT                
         BO    YES                 (WILL ONLY BE ON FOR STATES)                 
         B     FCST21B                                                          
         EJECT                                                                  
         SPACE 1                                                                
FCUST22  LA    R3,AFMLIST          CHECK ADDRESS FOR AFM                        
         BAS   RE,SCANADD                                                       
         BE    YES                                                              
         B     NO                                                               
         SPACE 1                                                                
FCUST23  XR    R1,R1               WANT JUST RETURNED CHECKS                    
         B     FCUST25B                                                         
FCUST24  LA    R1,1                WANT RETURNED CHKS THAT WERE MAILED          
         B     FCUST25B                                                         
FCUST25  LA    R1,2                WANT RETURNED CHKS THAT WERE FILED           
*                                                                               
FCUST25B CLI   0(R6),TLCKCDQ       ONLY WANT CHECKS                             
         BNE   YES                                                              
         MVI   ELCODE,TARNELQ      LOOK FOR RETURNED CHECK DETAILS              
         BAS   RE,GETEL                                                         
         BNE   NO                                                               
         USING TARND,R6                                                         
         LTR   R1,R1               IF LOOKING FOR JUST RETURNED                 
         BNZ   *+16                                                             
         TM    TARNSTAT,TARNMAIL+TARNFILE  MAKE SURE NOT MAILED/FILED           
         BZ    YES                                                              
         B     NO                                                               
         CH    R1,=H'1'            ELSE, IF LOOKING FOR MAILED                  
         BNE   *+16                                                             
         TM    TARNSTAT,TARNMAIL   TEST MAILED                                  
         BO    YES                                                              
         B     NO                                                               
         TM    TARNSTAT,TARNFILE   ELSE, TEST FILED                             
         BO    YES                                                              
         B     NO                                                               
         EJECT                                                                  
FCUST26  CLI   0(R6),TLCKCDQ       ONLY WANT CHECKS THAT WENT DIRECT            
         BNE   YES                 OR THAT ARE WIRE TRANSFERS                   
         MVI   ELCODE,TAODELQ      LOOK FOR OTHER DEDUCTION ELEMENT             
         BAS   RE,GETEL                                                         
         B     *+8                                                              
FCUST26N BAS   RE,NEXTEL                                                        
         BNE   NO                                                               
         USING TAODD,R6                                                         
         CLI   TAODTYPE,TAODTYPD                                                
         BE    YES                                                              
         CLI   TAODTYPE,TAODTYPW                                                
         BE    YES                                                              
         B     FCUST26N                                                         
         EJECT                                                                  
FCUST27  CLI   0(R6),TLDVCDQ       ONLY WANT ADVICES GENERATED THROUGH          
         BNE   YES                 NETWORK-TALENT INTERFACE                     
         MVI   ELCODE,TANXELQ                                                   
         BAS   RE,GETEL                                                         
         BNE   NO                                                               
         B     YES                                                              
         SPACE 2                                                                
FCUST28  CLI   0(R6),TLDVCDQ       ONLY WANT PAID NETTAL ADVICES                
         BNE   YES                                                              
         MVI   ELCODE,TANXELQ                                                   
         BAS   RE,GETEL                                                         
         BNE   NO                                                               
         L     R6,TIAREC                                                        
         MVI   ELCODE,TADVELQ                                                   
         BAS   RE,GETEL                                                         
         BNE   NO                                                               
         USING TADVD,R6                                                         
         OC    TADVPDTE,TADVPDTE   CHECK PAID DATE                              
         BNZ   NO                                                               
         B     YES                                                              
         SPACE 2                                                                
FCUST29  CLI   0(R6),TLNXCDQ       ONLY WANT UNMATCHED HOLD RECORDS             
         BNE   NO                                                               
         MVI   ELCODE,TANXELQ                                                   
         BAS   RE,GETEL                                                         
         BNE   NO                                                               
         USING TANXD,R6                                                         
         OC    TANXMDTE,TANXMDTE   CHECK MATCHED DATE                           
         BNZ   NO                                                               
         B     YES                                                              
         EJECT                                                                  
FCUST30  OC    TISSN,TISSN         IF SSN AROUND                                
         BZ    YES                                                              
         BAS   RE,SCANCKE          SEE IF ANY CHECKS IN PERIOD                  
         BE    YES                                                              
         B     NO                                                               
         EJECT                                                                  
*              ROUTINE CHECKS IF ANY CHECKS FOR PERFORMER IN PERIOD             
         SPACE 1                                                                
SCANCKE  NTR1                                                                   
         MVC   NEEDKEY,KEY         SAVE OFF KEY                                 
         MVC   NEEDSAVE,KEYSAVE                                                 
         MVC   NEEDFBIT,TISTAT                                                  
         OI    TISTAT,TISTRDCK     ENSURE READING CHECKS                        
*                                                                               
         XC    KEY,KEY             READ HIGH FOR PERFORMER CHECKS               
         LA    R4,KEY                                                           
         USING TLCKPD,R4                                                        
         LA    R2,TLCKECUR-TLCKPD-1                                             
         MVI   TLCKPCD,TLCKECDQ                                                 
         MVC   TLCKESSN,TISSN                                                   
         CLC   TIFCUR,SPACES                                                    
         BNH   SCANCKE2                                                         
         MVC   TLCKECUR,TIFCUR                                                  
         LA    R2,TLCKEEMP-TLCKPD-1                                             
         CLC   TIFEMP,SPACES                                                    
         BNH   SCANCKE2                                                         
         MVC   TLCKEEMP,TIFEMP                                                  
         LA    R2,TLCKEDTE-TLCKPD-1                                             
*                                                                               
SCANCKE2 GOTO1 HIGH                                                             
         B     SCANCKE5                                                         
SCANCKE4 GOTO1 SEQ                                                              
*                                                                               
SCANCKE5 EX    R2,*+8                                                           
         B     *+10                                                             
         CLC   KEY(0),KEYSAVE                                                   
         BNE   SCANCKEN                                                         
         CLC   TIFEMP,SPACES       IF EMPLOYER SPECIFIED                        
         BNH   *+14                                                             
         CLC   TLCKEEMP,TIFEMP     MATCH ON IT                                  
         BNE   SCANCKE8                                                         
*                                                                               
         CLI   TLCKEDTE,0          IF NO CHECK DATE                             
         BE    SCANCKE4            SKIP TO NEXT CHECK RECORD                    
*                                                                               
         MVC   WORK(3),TLCKEDTE    ELSE, UNCOMPLEMENT DATE                      
         XC    WORK(3),COMPLM                                                   
         CLC   WORK(3),TIQPSTR     MATCH CHECK DATE AGAINST START DATE          
         BL    SCANCKE8            IF LOW, SKIP TO NEXT EMPLOYER                
         CLC   WORK(3),TIQPEND                                                  
         BH    SCANCKE4            IF HIGH, SKIP TO NEXT CHECK RECORD           
         B     SCANCKEY            ELSE, CHECK FOUND                            
*                                                                               
SCANCKE8 MVC   TLCKEDTE,COMPLM     SKIPPING TO NEXT EMPLOYER                    
         B     SCANCKE2                                                         
*                                                                               
SCANCKEN LA    R2,1                NO CHECKS FOUND IN PERIOD                    
         B     *+6                                                              
SCANCKEY XR    R2,R2               CHECKS FOUND IN PERIOD                       
         MVC   KEY,NEEDKEY         TIDY UP                                      
         TM    NEEDFBIT,TISTRDCK   IF PREVIOUS READS FROM CHKDIR                
         BZ    *+8                                                              
         BAS   RE,HIGH             REESTABLISH CHKDIR SEQ                       
         MVC   KEYSAVE,NEEDSAVE                                                 
         MVC   TISTAT,NEEDFBIT                                                  
         LTR   R2,R2               SET CC CODE                                  
         B     XIT                                                              
         EJECT                                                                  
FCUST31  L     R6,TIAREC           CHECKS WITH OVERSCALE ONLY                   
         CLI   0(R6),TLCKCDQ                                                    
         BNE   YES                                                              
         MVI   ELCODE,TAPDELQ                                                   
         BAS   RE,GETEL                                                         
         BNE   YES                                                              
         USING TAPDD,R6                                                         
         OC    TAPDOV1,TAPDOV1                                                  
         BZ    NO                                                               
         B     YES                                                              
         SPACE 2                                                                
FCUST32  L     R6,TIAREC           AGENCY & CLIENT WITHOUT SRC RATES            
         MVI   ELCODE,TABRELQ                                                   
         BAS   RE,GETEL                                                         
         BNE   NO                                                               
         USING TABRD,R6                                                         
         TM    TABRSTAT,TABRSSRC                                                
         BO    NO                                                               
         OC    TABRRATE,TABRRATE                                                
         BZ    NO                                                               
         B     YES                                                              
         SPACE 2                                                                
FCUST33  MVI   ELCODE,TACDELQ      NO CHECKS GENERATED BY W4 TRUSTEE            
         BAS   RE,GETEL                                                         
         BNE   YES                 (MAY BE PASSED AN INVOICE)                   
         USING TACDD,R6                                                         
         TM    TACDSTAT,TACDSTRS                                                
         BO    NO                                                               
         B     YES                                                              
         SPACE 1                                                                
*                                  PAYMENTS TO TRUSTEE W/O INDIV                
FCUST34  CLI   0(R6),TLCKCDQ       ONLY WANT CHECKS                             
         BNE   YES                                                              
         MVI   ELCODE,TAPDELQ      GET PAYMENT DETAILS                          
         BAS   RE,GETEL                                                         
         BNE   NO                                                               
         USING TAPDD,R6                                                         
         CLI   TAPDW4TY,TAW4TYTR   TEST PAYMENT TO TRUSTEE                      
         BNE   NO                                                               
         B     YES                                                              
*                                                                               
FCUST35  MVI   ELCODE,TACDELQ      CHECKS > $10000                              
         BAS   RE,GETEL                                                         
         BNE   YES                 (MAY BE PASSED AN INVOICE)                   
         USING TACDD,R6                                                         
         L     R1,TACDNET                                                       
         C     R1,=F'1000000'                                                   
         BL    NO                                                               
         B     YES                                                              
         SPACE 1                                                                
FCUST36  CLI   0(R6),TLCKCDQ       ONLY WANT CHECKS                             
         BNE   YES                                                              
         MVI   ELCODE,TAKPELQ      STOPPED CHECK ELEMENT                        
         BAS   RE,GETEL                                                         
         BNE   NO                  ONLY REPORTING STOPPED CHECKS                
*                                                                               
         L     R6,TIAREC           STOP ODER CAN'T BE CANCELLED                 
         MVI   ELCODE,TACNELQ      LOOK FOR CANCEL ELEMENT                      
         BAS   RE,GETEL                                                         
         BNE   YES                 STOP NOT CANCELLED                           
*                                                                               
         USING TACND,R6            ESTABLISH CANCEL ELEMENT                     
         TM    TACNTYPE,TACNTYPS   IF STOPPED CANCELLED                         
         BO    NO                     DROP CHECK FROM REPORT                    
*                                                                               
         B     YES                                                              
         SPACE 1                                                                
FCUST37  DS    0H                                                               
         L     R6,TIAREC           POINT TO CHECK RECORD                        
*                                                                               
         CLI   0(R6),TLCKCDQ       ONLY WANT CHECKS                             
         BNE   YES                                                              
         MVI   ELCODE,TACDELQ      GET PAYMENT DETAILS                          
         BAS   RE,GETEL                                                         
         BNE   NO                  ONLY REPORTING EFT CHECKS                    
*                                                                               
         USING TACDD,R6            ESTABLISH CHECK DETAILS ELEMENT              
         TM    TACDSTA2,TACDSELN   IF EFT LIEN CHECK                            
         BO    YES                    KEEP CHECK                                
*                                                                               
         B     NO                  ELSE REJECT                                  
         SPACE 1                                                                
         EJECT                                                                  
*              FILTER CLA NETWORK                                               
*                                                                               
FILTCLAN NTR1                                                                   
         CLI   TIFLTNET,C' '                                                    
         BL    YES                                                              
         L     R6,TIAREC           NETWORK / CLA PROGRAM DETAILS                
         MVI   ELCODE,TANPELQ                                                   
         BAS   RE,GETEL                                                         
FCLAN100 BNE   NO                                                               
         USING TANPD,R6                                                         
         CLC   TANPNWK,TIFLTNET    OK, IF ONE OF THEM IS PAX                    
         BE    YES                                                              
         BAS   RE,NEXTEL                                                        
         B     FCLAN100                                                         
         EJECT                                                                  
*              FILTER TAX UNIT                                                  
*                                                                               
FILTUNIT NTR1                                                                   
         L     R6,TIAREC                                                        
         MVI   ELCODE,TATUELQ      FIND TATU FIRST, THEN TACA                   
                                                                                
         TM    TIFLTSPC,TIFLTSCA                                                
         BO    FUNIT050                                                         
         TM    TIFTUNIT,TIFTWRK    WORK STATE                                   
         BO    FUNIT050                                                         
         TM    TIFTUNIT,TIFTTAX+TIFTRES                                         
         BZ    YES                                                              
FUNIT040 MVI   ELCODE,TAATELQ      LOOK AT ADDITIONAL TAXES                     
FUNIT050 CLI   0(R6),TLCKCDQ       FOR CALIFORNIA CHECKS ONLY                   
         BNE   YES                                                              
*                                                                               
FUNIT060 BAS   RE,GETEL                                                         
         B     FUNIT100                                                         
*                                                                               
FUNIT080 BAS   RE,NEXTEL                                                        
FUNIT100 BE    FUNIT150                                                         
         CLI   ELCODE,TACAELQ      2ND PASS WITH TACA                           
         BE    NO                  NOT FOUND WITH TACA TOO                      
         CLI   ELCODE,TAATELQ                                                   
         BE    FUNIT103                                                         
         CLI   ELCODE,TATUELQ                                                   
         BNE   FUNIT105                                                         
FUNIT103 MVI   ELCODE,TACWELQ                                                   
         B     FUNIT110                                                         
                                                                                
FUNIT105 MVI   ELCODE,TACAELQ      2ND PASS                                     
FUNIT110 L     R6,TIAREC                                                        
         B     FUNIT060                                                         
*                                                                               
FUNIT150 TM    TIFTUNIT,TIFTTAX+TIFTRES                                         
         BZ    FUNIT180                                                         
*        CLI   ELCODE,TACWELQ                                                   
*        BE    FUNIT500                                                         
*        CLI   ELCODE,TACAELQ                                                   
*        BE    FUNIT200                                                         
         USING TAATD,R6                                                         
         CLI   ELCODE,TAATELQ                                                   
         BNE   FUNIT180                                                         
         MVC   WORK(L'TAATUNIT),TAATUNIT                                        
         B     FUNIT270                                                         
*                                                                               
FUNIT180 CLI   ELCODE,TACAELQ                                                   
         BNE   FUNIT250                                                         
         USING TACAD,R6                                                         
         TM    TIFTUNIT,TIFTWRK    WORK STATE                                   
         BO    FUNIT200                                                         
         CLC   TACAUNIT,=C'CA '    CALIFORNIA SPECIFIC FILTER                   
         BE    YES                                                              
         CLC   TACAUNIT,=C'LAX'                                                 
         BE    YES                                                              
         B     FUNIT080                                                         
*                                                                               
FUNIT200 TM    TACASTA3,TACASPPL   PAYROLL PLUS EMPLOYEE?                       
         BO    FUNIT080            YES, SKIP                                    
         MVC   WORK(L'TACAUNIT),TACAUNIT                                        
         B     FUNIT270                                                         
*                                                                               
         USING TATUD,R6                                                         
FUNIT250 CLI   ELCODE,TATUELQ                                                   
         BNE   FUNIT500                                                         
         TM    TIFTUNIT,TIFTWRK    WORK STATE                                   
         BO    FUNIT260                                                         
         CLC   TATUUNIT,=C'CA '    CALIFORNIA SPECIFIC FILTER                   
         BE    YES                                                              
         CLC   TATUUNIT,=C'LAX'                                                 
         BE    YES                                                              
         B     FUNIT080                                                         
*                                                                               
FUNIT260 TM    TATUSTAT,TATUSRES   RESIDENT ONLY, NO WORK                       
         BO    FUNIT080            SKIP IT                                      
         MVC   WORK(L'TATUUNIT),TATUUNIT                                        
*                                                                               
FUNIT270 BAS   RE,CNVCTYST         CONVERT CITY TO STATE IN WORK                
         MVC   DUB,WORK            NEW WORKAREA                                 
         TM    TIFUNIT,X'80'                                                    
         BZ    FUNIT280                                                         
         CLC   TIFUNIT,DUB                                                      
         BE    YES                                                              
         B     FUNIT080                                                         
*                                                                               
FUNIT280 LA    R2,TIFUNIT          TAX UNIT CODE                                
         LA    R3,DUB              MATCH UNIT                                   
         LA    R4,L'TIUNIT                                                      
         BAS   RE,TESTEMRT                                                      
         BE    YES                                                              
         B     FUNIT080                                                         
*                                                                               
         USING TACWD,R6                                                         
FUNIT500 TM    TIFTUNIT,TIFTRES    LOOKING FOR RESIDENT                         
         BZ    FUNIT530                                                         
         TM    TACWSTAT,TACWSRES   THEN CHECK WITHHOLDING MUST BE RES           
         BZ    FUNIT080                                                         
*                                                                               
FUNIT530 TM    TIFTUNIT,TIFTTAX    LOOKING FOR TAXABLE                          
         BZ    FUNIT540                                                         
         TM    TACWSTAT,TACWSTAX   THEN CHECK WITHHOLDING MUST BE TAXD          
         BZ    FUNIT080                                                         
*                                                                               
FUNIT540 TM    TIFTUNIT,TIFTWRK    LOOKING FOR WORK                             
         BZ    FUNIT550                                                         
         TM    TACWSTAT,TACWSWRK   THEN CHECK WITHHOLDING MUST BE WORK          
         BZ    FUNIT080                                                         
*                                                                               
FUNIT550 MVC   WORK,TACWUNIT                                                    
         TM    TIFUNIT,X'80'       FLIST AND                                    
         BO    FUNIT570                                                         
         TM    TIFTUNIT,TIFTCITY   FILTERING CITY                               
         BO    FUNIT575            DON'T CONVERT                                
                                                                                
FUNIT570 BAS   RE,CNVCTYST         CONVERT CITY TO STATE IN WORK                
FUNIT575 MVC   DUB,WORK            NEW WORKAREA                                 
         TM    TIFUNIT,X'80'                                                    
         BZ    FUNIT580                                                         
         CLC   TIFUNIT,DUB                                                      
         BE    YES                                                              
         B     FUNIT080                                                         
*                                                                               
FUNIT580 LA    R2,TIFUNIT          TAX UNIT CODE                                
         LA    R3,DUB              MATCH UNIT                                   
         LA    R4,L'TIUNIT                                                      
         BAS   RE,TESTEMRT                                                      
         BE    YES                                                              
         B     FUNIT080                                                         
         SPACE 3                                                                
*                                                                               
TESTEMRT NTR1                                                                   
         BAS   RE,TESTEM                                                        
         J     XIT                                                              
         EJECT                                                                  
*                                                                               
CNVCTYST NTR1                                                                   
         CLI   TIFUNIT+2,C' '      NOT FILTERING CITY, DON'T CONVERT            
         BNE   XIT                                                              
         CLI   WORK+2,C' '         SEE IF 3RD CHAR IS NON-SPACE                 
         BE    XIT                 NOPE, IT'S A STATE, LEAVE                    
                                                                                
         LA    R1,CTYTAB                                                        
CCTYST5  CLI   0(R1),X'FF'         EOT                                          
         BE    XIT                 LEAVE                                        
         CLC   0(3,R1),WORK                                                     
         BE    CCTYST8                                                          
         AHI   R1,6                                                             
         B     CCTYST5                                                          
*                                                                               
CCTYST8  MVC   WORK(3),3(R1)                                                    
         B     XIT                                                              
*                                                                               
CTYTAB   DS    0C                                                               
         DC    C'CIN',C'OH '                                                    
         DC    C'CLV',C'OH '                                                    
         DC    C'DET',C'MI '                                                    
         DC    C'DNV',C'CO '                                                    
         DC    C'LAX',C'CA '                                                    
         DC    C'LJC',C'KY '                                                    
         DC    C'NYC',C'NY '                                                    
         DC    C'PHL',C'PA '                                                    
         DC    C'PIT',C'PA '                                                    
         DC    C'YON',C'NY '                                                    
         DC    X'FF'                                                            
*                                                                               
FILTOVRA NTR1                                                                   
         TM    TIFLTSPC,TIFLTSOY+TIFLTSON                                       
         BZ    YES                                                              
         L     R6,TIAREC                                                        
         CLI   0(R6),TLGUCDQ       FOR GUARENTEES ONLY                          
         BNE   YES                                                              
         MVI   ELCODE,TAGUELQ                                                   
         BAS   RE,GETEL                                                         
         BNE   NO                                                               
         USING TAGUD,R6                                                         
         TM    TIFLTSPC,TIFLTSOY   OVERAGE=Y ONLY                               
         BZ    FILTOVR5                                                         
         TM    TAGUSTAT,TAGUSOVR                                                
         BO    YES                                                              
         B     NO                                                               
*                                                                               
FILTOVR5 TM    TIFLTSPC,TIFLTSON    OVERAGE=N ONLY                              
         BZ    NO                                                               
         TM    TAGUSTAT,TAGUSOVR                                                
         BZ    YES                                                              
         B     NO                                                               
         EJECT                                                                  
*                                                                               
FILTVERS NTR1                                                                   
         CLI   TIREAD,TLCONCDQ                                                  
         BNE   YES                                                              
         L     R6,TIAREC                                                        
         CLI   0(R6),TLVRCDQ                                                    
         BNE   YES                                                              
         TM    TIQFLAG3,TIQFIVER                                                
         BZ    NO                                                               
         B     YES                                                              
         EJECT                                                                  
*                                                                               
*        FILTER ON AGENCY DATA                                                  
*                                                                               
FILTAGY  NTR1                                                                   
*                                                                               
         CLI   TIREAD,TLAYCDQ      SKIP IF NOT AGENCY RECORD                    
         BNE   FILTAGOK                                                         
*                                                                               
         OC    TIFSGNTR,TIFSGNTR   IF FILTERING ON SIGNATORY                    
         BZ    FILTAGOK                                                         
*                                                                               
*        FILTER ON SIGNATORY                                                    
*                                                                               
         L     R6,TIAREC           POINT TO FOUND RECORD                        
*                                                                               
         CLI   0(R6),TLAYCDQ       SKIP IF NOT AGENCY RECORD                    
         BNE   FILTAGOK                                                         
*                                                                               
         MVI   ELCODE,TANUELQ      FIND SIGNATORY ELEMENT                       
         BAS   RE,GETEL                                                         
*                                                                               
FILTAGSL DS    0H                                                               
*                                                                               
         BNE   FILTAGSD            NONE FOUND                                   
*                                                                               
         USING TANUD,R6            ESTABLISH ELEMENT                            
*                                                                               
         CLI   TANUTYPE,TANUTSIG   SKIP IF NOT SIGNATORY ELM                    
         BNE   FILTAGSC                                                         
*                                                                               
         LLC   RF,TANULEN          GET ELEMENT LENGTH                           
         SHI   RF,TANULNQ          DECREMENT BY HEADER LENGTH                   
         BNP   FILTAGSC            NO SIGNATORY DATA                            
*                                                                               
         BCTR  RF,0                DECREMENT FOR EXECUTE                        
         EX    RF,*+8                                                           
         B     *+10                                                             
         CLC   TIFSGNTR(0),TANUMBER   MATCH ON SIGNATORY FIELD                  
         BNE   FILTAGSC               NO MATCH                                  
*                                                                               
         LA    RE,TIFSGNTR+L'TIFSGNTR-1 FIND LENGTH OF FILTER                   
         LA    R0,L'TIFSGNTR       MAX LENGTH OF FILTER                         
*                                                                               
         CLI   0(RE),C' '          FIND LAST NO BLANK                           
         BH    *+14                                                             
         BCTR  RE,0                BACKUP  A POSITION                           
         BCT   R0,*-10                                                          
         B     FILTAGSC            NO FILTER                                    
*                                                                               
         LA    R1,TIFSGNTR         START OF FILTER                              
         SR    RE,R1               LENGTH OF FILTER MINUS 1                     
*                                                                               
         CR    RF,RE               LENGTHS MUST BE EQUAL                        
         BE    FILTAGSF                                                         
*                                                                               
FILTAGSC DS    0H                                                               
         BAS   RE,NEXTEL           NEXT ELM                                     
         B     FILTAGSL                                                         
*                                                                               
FILTAGSD DS    0H                                                               
         B     FILTAGNO            SKIP AGENCY                                  
*                                                                               
FILTAGSF DS    0H                  KEEP AGENCY                                  
*                                                                               
         B     FILTAGOK                                                         
*                                                                               
FILTAGNO DS    0H                                                               
         LTR   RB,RB               SET NEW CC                                   
         B     FILTAGYX                                                         
*                                                                               
FILTAGOK DS    0H                                                               
         CR    RB,RB               SET EQ CC                                    
*                                                                               
FILTAGYX DS    0H                                                               
         XIT1                                                                   
*                                                                               
         EJECT                                                                  
FILTNHA  NTR1                                                                   
         TM    TIFLTSP2,TIFLTS2A+TIFLTS2P                                       
         BZ    YES                                                              
         OC    TISSN,TISSN         MUST HAVE SS#                                
         BZ    YES                                                              
         MVI   TINHA,0             RESET NHA                                    
         BAS   RE,NEEDW4                                                        
         OC    PRIMEKEY,PRIMEKEY                                                
         BNZ   FNHA050                                                          
         GOTO1 HIGH                RESTORE READ SEQ                             
*                                                                               
FNHA050  TM    TIFLTSP2,TIFLTS2A   FILTER ON W4 ELIGIBLE NHA                    
         BZ    FNHA100                                                          
         CLI   TINHA,C'Y'                                                       
         BE    YES                                                              
         B     NO                                                               
FNHA100  CLI   TINHA,C'P'          FILTER ON W4 PENDING NHA                     
         BE    YES                                                              
         B     NO                                                               
         EJECT                                                                  
FILTMSC  NTR1                                                                   
         TM    TIFLTSP2,TIFLTS2M                                                
         BZ    YES                                                              
         L     R6,TIAREC           POINT TO FOUND RECORD                        
         CLI   0(R6),TLCKCDQ       MUST BE A CHECK RECORD                       
         BNE   YES                 NO, DON'T BOTHER FILTERING                   
         MVI   ELCODE,TACDELQ      R6=A(CHECK DETAILS ELEMENT)                  
         BAS   RE,GETEL                                                         
         BNE   NO                                                               
         USING TACDD,R6                                                         
         TM    TACDSTA2,TACDSMSC   MAKE SURE MSC IS ON                          
         BO    YES                                                              
         B     NO                                                               
         EJECT                                                                  
FILTVITA NTR1                      FILTER VITA BETA AGENCIES                    
         CLI   TIREAD,TLAYCDQ      SKIP IF NOT AGENCY RECORD                    
         BNE   YES                                                              
         TM    TIFLTSP2,TIFLTS2V                                                
         BZ    YES                                                              
         L     R6,TIAREC           POINT TO FOUND RECORD                        
         MVI   ELCODE,TAAYELQ      R6=A(AGENCY ELEMENT)                         
         BAS   RE,GETEL                                                         
         BNE   NO                                                               
         USING TAAYD,R6                                                         
         TM    TAAYSTA6,TAAYVIBE   MAKE SURE VITA BETA IS ON                    
         BO    YES                                                              
         B     NO                                                               
         EJECT                                                                  
*        FILTER ON MUSIC RECORD                                                 
*                                                                               
FILTMUS  NTR1                                                                   
         USING TLMUD,R6                                                         
         L     R6,TIAREC           POINT TO FOUND RECORD                        
         CLI   0(R6),TLMUCDQ       MUST BE A MUSIC RECORD                       
         BNE   YES                 NO, DON'T BOTHER FILTERING                   
         OC    TLMUAGY,TLMUAGY                                                  
         BNZ   YES                                                              
         OC    TIFAGY,TIFAGY                                                    
         BZ    YES                                                              
         DROP  R6                                                               
                                                                                
         MVI   ELCODE,TAPMELQ      R6=A(MUSIC INFO/HISTORY ELEMENT)             
         BAS   RE,GETEL                                                         
         BNE   NO                                                               
         USING TAPMD,R6                                                         
*                                                                               
         CLC   TAPMCAGY,TIFAGY     MATCH ON AGENCY?                             
         BE    YES                                                              
         B     NO                                                               
         DROP  R6                                                               
         EJECT                                                                  
*        FILTER ON CAST SEQUENCE                                                
*                                                                               
FILTCSEQ NTR1                      FILTER CAST SEQUENCE                         
         CLI   TIREAD,TLCANCDQ     MUST BE READING CAST BY NAME RECS            
         BNE   YES                                                              
         L     R6,TIAREC           POINT TO FOUND RECORD                        
         USING TLCKD,R6                                                         
         CLI   0(R6),TLCKCDQ       MUST BE A CHECK RECORD                       
         BNE   YES                                                              
         OC    TIFCSEQ,TIFCSEQ     ARE WE FILTERING ON CAST SEQ?                
         BZ    YES                                                              
         CLC   TIFCSEQ,TLCKSORT+4  MAKE SURE CAST SEQUENCE MATCHES              
         BE    YES                                                              
         B     NO                                                               
         DROP  R6                                                               
         EJECT                                                                  
*              CHECK ADDRESS FOR SPECIFIC TEXT                                  
         SPACE 1                                                                
*                                  R3=A(SCAN LIST)                              
SCANADD  NTR1                                                                   
         OC    TISSN,TISSN         NEED TO HAVE A PERSON AROUND                 
         BZ    YES                                                              
         MVI   LOOKUPSW,C'N'                                                    
         BAS   RE,NEEDW4                                                        
         MVI   LOOKUPSW,0                                                       
         L     R6,TIANEED                                                       
         MVI   ELCODE,TAA2ELQ      R6=A(NEW ADDRESS ELEMENT)                    
         BAS   RE,GETEL                                                         
         BNE   NO                                                               
         USING TAA2D,R6                                                         
         LA    R2,TAA2ADDR         R2=A(ADDRESS LINES)                          
         LA    R0,L'TAA2ADDR       R0=L'ADDRESS                                 
         SPACE 1                                                                
SCANADD2 LR    R4,R3               R4=A(SCAN LIST)                              
SCANADD4 CLI   0(R4),X'FF'         TEST END OF LIST                             
         BE    SCANADD6                                                         
         ZIC   R1,0(R4)            R1=L'SCAN EXPRESSION                         
         BCTR  R1,0                                                             
         EX    R1,*+8                                                           
         B     *+10                                                             
         CLC   0(0,R2),1(R4)                                                    
         BE    YES                                                              
         LA    R4,2(R1,R4)         BUMP TO NEXT SCAN LIST ENTRY                 
         B     SCANADD4                                                         
         SPACE 1                                                                
SCANADD6 LA    R2,1(R2)                                                         
         BCT   R0,SCANADD2                                                      
         B     NO                                                               
         SPACE 1                                                                
SAGLIST  DS    0H                                                               
         DC    AL1(4),C'SAG '                                                   
         DC    AL1(5),C'S.A.G'                                                  
         DC    AL1(10),C'SCREEN ACT'                                            
         DC    X'FF'                                                            
         SPACE 1                                                                
AFTLIST  DS    0H                                                               
         DC    AL1(5),C'AFTRA'                                                  
         DC    AL1(5),C'A.F.T'                                                  
         DC    X'FF'                                                            
         SPACE 1                                                                
AFMLIST  DS    0H                                                               
         DC    AL1(4),C'AFM '                                                   
         DC    AL1(5),C'A.F.M'                                                  
         DC    X'FF'                                                            
         SPACE 1                                                                
ESTLIST  DS    0H                                                               
         DC    AL1(6),C'ESTATE'                                                 
         DC    AL1(4),C'EST.'                                                   
         DC    X'FF'                                                            
         EJECT                                                                  
*              FILTER COPIED COMMERCIALS                                        
         SPACE 3                                                                
FILTCOMM NTR1                                                                   
         LA    R6,IO               FILTER OUT 'TO' COMMERCIALS                  
         MVI   ELCODE,TAOCELQ                                                   
         BAS   RE,GETEL                                                         
         BNE   YES                                                              
         SPACE 1                                                                
         USING TAOCD,R6                                                         
FILTC10  TM    TAOCSTAT,TAOCSTO                                                 
         BO    NO                  IF COMMERCIAL WAS COPIED - NO ACCESS         
         BAS   RE,NEXTEL           TO USER                                      
         BE    FILTC10                                                          
         B     YES                                                              
         EJECT                                                                  
*              FILTER ON INVOICE NUMBER CONVERSION STATUS                       
         SPACE 3                                                                
FILTINV  OC    TIINV,TIINV         NEED INVOICE NUMBER AROUND                   
         BZR   RE                                                               
         MVC   BYTE,TIINV+5                                                     
         NI    BYTE,X'7F'                                                       
         CLI   TIFINCVS,1          1=NO CONVERTED 2=ONLY CONVERTED              
         BLR   RE                                                               
         BE    FILTINV1                                                         
         B     FILTINV2                                                         
         SPACE 1                                                                
FILTINV1 CLI   BYTE,1              1=NO CONVERTED                               
         BE    NO                                                               
         CLI   BYTE,2                                                           
         BE    NO                                                               
         CLI   BYTE,3                                                           
         BE    NO                                                               
         BR    RE                                                               
         SPACE 1                                                                
FILTINV2 CLI   BYTE,1              2=CONVERTED ONLY                             
         BER   RE                                                               
         CLI   BYTE,2                                                           
         BER   RE                                                               
         CLI   BYTE,3                                                           
         BER   RE                                                               
         B     NO                                                               
         SPACE 1                                                                
         EJECT                                                                  
*              FILTER INVOICE AND PAYMENT STATUS                                
         SPACE 3                                                                
FILTSTAT NTR1                                                                   
         CLI   ANYINVEL,1          NEED TO HAVE SEEN INVOICE ELEMENT            
         BNE   FS20                                                             
         LA    R2,INVSTAT          INVOICE STATUS                               
         LA    R3,TIFINSTY                                                      
         BAS   RE,YESSTAT                                                       
         LA    R3,TIFINSTN                                                      
         BAS   RE,NOSTAT                                                        
         SPACE 1                                                                
         LA    R2,INVSTAT2                                                      
         LA    R3,TIFINS2Y                                                      
         BAS   RE,YESSTAT                                                       
         LA    R3,TIFINS2N                                                      
         BAS   RE,NOSTAT                                                        
         SPACE 1                                                                
FS20     CLI   ANYCOMEL,1          NEED TO HAVE SEEN COMMERCIAL ELEMENT         
         BNE   FS25                                                             
         LA    R2,COMSTAT                                                       
         LA    R3,TIFCOSTY                                                      
         BAS   RE,YESSTAT                                                       
         LA    R3,TIFCOSTN                                                      
         BAS   RE,NOSTAT                                                        
         SPACE 1                                                                
FS25     CLI   ANYDUCEL,1          NEED TO HAVE SEEN DUECOMP ELEMENT            
         BNE   FS30                                                             
         LA    R2,DUESTAT                                                       
         LA    R3,TIFDUSTY                                                      
         BAS   RE,YESSTAT                                                       
         LA    R3,TIFDUSTN                                                      
         BAS   RE,NOSTAT                                                        
         SPACE 1                                                                
FS30     CLI   ANYPDEL,1           NEED TO HAVE SEEN PAYMENT DTLS EL.           
         BNE   FS60                                                             
         LA    R2,PAYPSTAT         PAYMENT STATUS                               
         LA    R3,TIFPDPY                                                       
         BAS   RE,YESSTAT                                                       
         LA    R3,TIFPDPN                                                       
         BAS   RE,NOSTAT                                                        
         SPACE 1                                                                
         LA    R2,PAYSTAT                                                       
         LA    R3,TIFPDSY                                                       
         BAS   RE,YESSTAT                                                       
         LA    R3,TIFPDSN                                                       
         BAS   RE,NOSTAT                                                        
         SPACE 1                                                                
         LA    R2,PAYOPTS          PAYMENT OPTIONS                              
         LA    R3,TIFPO1Y                                                       
         BAS   RE,YESSTAT                                                       
         LA    R3,TIFPO1N                                                       
         BAS   RE,NOSTAT                                                        
         SPACE 1                                                                
         LA    R2,PAYOPTS+1                                                     
         LA    R3,TIFPO2Y                                                       
         BAS   RE,YESSTAT                                                       
         LA    R3,TIFPO2N                                                       
         BAS   RE,NOSTAT                                                        
         SPACE 1                                                                
         LA    R2,PAYOPTS+2                                                     
         LA    R3,TIFPO3Y                                                       
         BAS   RE,YESSTAT                                                       
         LA    R3,TIFPO3N                                                       
         BAS   RE,NOSTAT                                                        
         SPACE 1                                                                
         LA    R2,PAYAINDS         ADJUSTMENT INDICATORS                        
         LA    R3,TIFPAIY                                                       
         BAS   RE,YESSTAT                                                       
         LA    R3,TIFPAIN                                                       
         BAS   RE,NOSTAT                                                        
         SPACE 1                                                                
FS60     CLI   ANYCHKEL,1          NEED TO HAVE SEEN CHECK DETAILS EL.          
         BNE   FSX                                                              
         LA    R2,CDSTAT           CHECK DETAIL STATUS                          
         LA    R3,TIFCDSTY                                                      
         BAS   RE,YESSTAT                                                       
         LA    R3,TIFCDSTN                                                      
         BAS   RE,NOSTAT                                                        
         SPACE 1                                                                
FSX      B     YES                                                              
         SPACE 1                                                                
YESSTAT  CLI   0(R3),0                                                          
         BER   RE                                                               
         MVC   TESTBYTE,0(R3)                                                   
         NC    TESTBYTE,0(R2)                                                   
         CLC   TESTBYTE,0(R3)                                                   
         BER   RE                                                               
         B     NO                                                               
         SPACE 1                                                                
NOSTAT   CLI   0(R3),0                                                          
         BER   RE                                                               
         MVC   TESTBYTE,0(R3)                                                   
         NC    TESTBYTE,0(R2)                                                   
         CLI   TESTBYTE,0                                                       
         BER   RE                                                               
         B     NO                                                               
         EJECT                                                                  
***********************************************************************         
*        ROUTINE TO TEST STAFF2 LIMIT ACCESS                          *         
***********************************************************************         
                                                                                
CHKLIM   NTR1                                                                   
         OC    TIUSERID,TIUSERID   IF USER ID                                   
         BZ    YES                                                              
         CLI   TIQSTAFF,0          AND STAFF ID ARE NOT PROVIDED                
         BE    YES                 ALLOW ALL ACCESS                             
*                                                                               
         CLI   KEY,TLCLCDQ         IF LISTING CLIENTS,                          
         BE    CHKLIM10            ALWAYS CHECK ACCESS                          
         CLI   KEY,TLCLNCDQ                                                     
         BE    CHKLIM10                                                         
         CLI   TIAGY,X'41'         ELSE, IF AGENCY NOT SET YET                  
         BL    YES                 ALLOW ACCESS                                 
*                                                                               
         USING FACTSD,R1                                                        
CHKLIM10 GOTO1 GETFACT,DMCB,0                                                   
         L     R1,DMCB                                                          
         OC    FATMAXIO,FATMAXIO   DETERMINE IF CHECKING ONLINE                 
         BZ    CKLIMOFF            OR OFFLINE                                   
         DROP  R1                                                               
                                                                                
***********************************************************************         
                                                                                
CKLIMON  LHI   R2,1                                                             
*                                                                               
         USING COMFACSD,RF                                                      
CLON10   L     RF,TIACOMFC                                                      
*                                                                               
         USING FAWSSVRD,R1                                                      
         LA    R1,DMWORK                                                        
         MVC   FAWSTOKN(3),=C'STF'                                              
         STC   R2,FAWSTOKN+3                                                    
         MVI   FAWSACTN,FAWSARST   RECALL STAFF2 INFORMATION VIA WWSVR          
         XC    FAWSLEN,FAWSLEN                                                  
         MVC   FAWSADR,TIAACBLK                                                 
         GOTO1 CWSSVR,(R1)                                                      
         CLI   FAWSRTN,0           IF NOT FOUND, STAFF HAS NO ACCESS            
         JNE   NO                                                               
         DROP  R1,RF                                                            
*                                                                               
         AHI   R2,1                                                             
*                                                                               
         USING TAVAD,R1                                                         
         L     R1,TIAACBLK                                                      
         OC    TAVAAGY,TAVAAGY     IF STAFF HAS NO AGENCY LIMITS,               
         JZ    YES                 STAFF HAS ACCESS TO ALL RECORDS              
*                                                                               
CLON20   CLI   0(R1),0             RECALL NEXT RECORD FROM WSSVR                
         JE    CLON10                                                           
*                                                                               
         CLC   TIAGY,TAVAAGY       IF AGENCY IS FOUND IN STAFF LIMITS           
         JNE   CLON40                                                           
         CLI   KEY,TLAYCDQ         AND CHECKING AGENCY RECORD                   
         JE    YES                 ACCESS IS GRANTED                            
*                                                                               
         CLI   TAVALEN,TAVALNQ     IF NO CLIENT LIMITS ARE DEFINED              
         JE    YES                 ACCESS IS GRANTED                            
         OC    TICLI,TICLI                                                      
         JZ    YES                                                              
*                                                                               
         ZIC   RE,TAVALEN                                                       
         SHI   RE,TAVALNQ                                                       
         LA    RF,TAVACLI                                                       
CLON30   CLC   TICLI,0(RF)         IF CLIENT IS FOUND IN STAFF LIMITS           
         JE    YES                 ACCESS IS GRANTED                            
         LA    RF,L'TAVACLI(RF)                                                 
         SHI   RE,L'TAVACLI                                                     
         LTR   RE,RE                                                            
         JNZ   CLON30                                                           
*                                                                               
CLON40   ZIC   RE,TAVALEN          BUMP TO NEXT VALID AGENCY/CLIENT             
         AR    R1,RE               ELEMENT                                      
         J     CLON20                                                           
         DROP  R1                                                               
*                                                                               
***********************************************************************         
                                                                                
CKLIMOFF MVC   NEEDKEY,KEY         SAVE KEYS                                    
         MVC   NEEDSAVE,KEYSAVE                                                 
*                                                                               
         USING TLSTD,R3                                                         
         LA    R3,KEY              FIRST LOOK FOR STAFF RECORD                  
         XC    KEY,KEY             WITHOUT A USER ID                            
         MVI   TLSTCD,TLSTCDQ                                                   
         MVC   TLSTSTAF,TIQSTAFF                                                
******** MVC   KEYSAVE,KEY                                                      
******** GOTO1 DATAMGR,DMCB,=C'DMRDHI',=CL8'TALDIR',KEY,KEY                     
******** CLC   KEY(TLSSPR3-TLSTD),KEYSAVE                                       
******** BE    CLOFF10                                                          
********                                                                        
******** MVC   KEY,KEYSAVE         IF NOT FOUND, LOOK FOR STAFF                 
         MVC   TLSTUSER,TIUSERID   RECORD WITH USER ID                          
         MVC   KEYSAVE,KEY         IF NOT FOUND, DO NOT ALLOW ACCESS            
         GOTO1 DATAMGR,DMCB,=C'DMRDHI',=CL8'TALDIR',KEY,KEY                     
         CLC   KEY(TLSSPR3-TLSTD),KEYSAVE                                       
         BNE   CLOFFNO                                                          
         DROP  R3                                                               
*                                                                               
CLOFF10  LA    RE,IO                                                            
         AHI   RE,L'IO+L'IO2       SAVE ADDRESS FOR RECORD                      
         ST    RE,TIANEED                                                       
*                                                                               
         GOTO1 DATAMGR,DMCB,=C'GETREC',=CL8'TALFIL',KEY+TLDRDA-TLDRD,  +        
               TIANEED,DMWORK3                                                  
*                                                                               
         L     R6,TIANEED          READ AGENCY/CLIENT LIMIT ELEMENTS            
         MVI   ELCODE,TAVAELQ                                                   
         BAS   RE,GETEL            IF NONE EXIST                                
         BE    CLOFF40             STAFF HAS ACCESS TO ALL RECORDS              
         B     CLOFFYES                                                         
*                                                                               
CLOFF20  GOTO1 DATAMGR,DMCB,=C'DMRSEQ',=CL8'TALDIR',KEY,KEY                     
         CLC   KEY(TLSSPR3-TLSTD),KEYSAVE                                       
         BNE   CLOFFNO                                                          
*                                                                               
         GOTO1 DATAMGR,DMCB,=C'GETREC',=CL8'TALFIL',KEY+TLDRDA-TLDRD,  +        
               TIANEED,DMWORK3                                                  
*                                                                               
         USING TAVAD,R6                                                         
         L     R6,TIANEED          READ AGENCY/CLIENT LIMIT ELEMENTS            
         BAS   RE,GETEL                                                         
         B     *+8                                                              
CLOFF30  BAS   RE,NEXTEL                                                        
         BNE   CLOFF20                                                          
*                                                                               
CLOFF40  OC    TAVAAGY,TAVAAGY     IF STAFF HAS NO AGENCY LIMITS,               
         BZ    CLOFFYES            STAFF HAS ACCESS TO ALL RECORDS              
*                                                                               
         CLC   TIAGY,TAVAAGY       IF AGENCY IS FOUND IN STAFF LIMITS           
         BNE   CLOFF30                                                          
         CLI   KEY,TLAYCDQ         AND CHECKING AGENCY RECORD                   
         BE    CLOFFYES            ACCESS IS GRANTED                            
*                                                                               
         CLI   TAVALEN,TAVALNQ     IF NO CLIENT LIMITS ARE DEFINED              
         BE    CLOFFYES            ACCESS IS GRANTED                            
         CLI   TICLI,X'41'                                                      
         BL    CLOFFYES                                                         
*                                                                               
         ZIC   RE,TAVALEN                                                       
         SHI   RE,TAVALNQ                                                       
         LA    RF,TAVACLI                                                       
CLOFF50  CLC   TICLI,0(RF)         IF CLIENT IS FOUND IN STAFF LIMITS           
         BE    CLOFFYES            ACCESS IS GRANTED                            
         LA    RF,L'TAVACLI(RF)                                                 
         SHI   RE,L'TAVACLI                                                     
         LTR   RE,RE                                                            
         BNZ   CLOFF50                                                          
         B     CLOFF30                                                          
         DROP  R6                                                               
*                                                                               
CLOFFYES XR    R3,R3                                                            
CLOFFNO  MVC   KEY,NEEDKEY                                                      
         MVC   KEYSAVE,NEEDSAVE                                                 
         MVC   DIRNAME,=CL8'TALDIR'                                             
         MVC   FILENAME,=CL8'TALFIL'                                            
         TM    TISTAT,TISTRDCK                                                  
         BZ    *+16                                                             
         MVC   DIRNAME,=CL8'CHKDIR'                                             
         MVC   FILENAME,=CL8'CHKFIL'                                            
         GOTO1 DATAMGR,DMCB,=C'DMRDHI',DIRNAME,KEY,KEY                          
         TM    TIQFLAGS,TIQFUPRC                                                
         BZ    CLOFFX                                                           
         GOTO1 DATAMGR,DMCB,(X'80',=C'GETREC'),FILENAME,               +        
               KEY+TLDRDA-TLDRD,TIANEED,DMWORK3                                 
CLOFFX   LTR   R3,R3                                                            
         BZ    YES                                                              
         B     NO                                                               
         EJECT                                                                  
*              DATA MANAGER INTERFACE                                           
         SPACE 1                                                                
READ     MVC   COMMAND,=CL6'DMREAD'                                             
         MVC   KEYSAVE,KEY                                                      
         B     READDIR                                                          
         SPACE 1                                                                
SEQ      MVC   COMMAND,=CL6'DMRSEQ'                                             
         B     READDIR                                                          
         SPACE 1                                                                
FIRSTHI  OC    TIQSKEY,TIQSKEY     POSSIBLE OVERRIDING KEY                      
         BZ    HIGH                                                             
         MVC   KEY(32),TIQSKEY                                                  
         SPACE 1                                                                
HIGH     MVC   COMMAND,=CL6'DMRDHI'                                             
         MVC   KEYSAVE,KEY                                                      
         B     READDIR                                                          
         SPACE 1                                                                
READDIR  NTR1                                                                   
         TM    TISTAT,TISTRDCK                                                  
         BNO   *+10                                                             
         MVC   DIRNAME,=CL8'CHKDIR'                                             
         GOTO1 ,DMCB,(DMINBITS,COMMAND),DIRNAME,KEY,KEY                         
         TM    TIQFLAGS,TIQFUPDR   TEST READ FOR UPDATE REQUESTED               
         BZ    *+8                                                              
         OI    0(R1),X'80'         SET APPROPRIATE BIT IN HOB OF P1             
         GOTO1 DATAMGR                                                          
         MVC   DIRNAME,=CL8'TALDIR'                                             
         MVC   TIKEY,KEY                   PASS BACK KEY                        
         MVC   TIKEYST,KEY+TLDRSTAT-TLDRD  STATUS BYTES                         
         MVC   TIDSKADD,KEY+TLDRDA-TLDRD   AND DISK ADDRESS                     
         TM    DMCB+8,X'12'                                                     
         BNZ   NO                  RECORD NOT FOUND/DELETED                     
         B     DMCHECK                                                          
         SPACE 1                                                                
HIGHN    NTR1                       NEEDDATA ROUTINES DON'T SET TIFLDS          
         MVC   COMMAND,=CL6'DMRDHI'                                             
         MVC   KEYSAVE,KEY                                                      
         TM    TISTAT,TISTRDCK                                                  
         BNO   *+10                                                             
         MVC   DIRNAME,=CL8'CHKDIR'                                             
         GOTO1 ,DMCB,(DMINBITS,COMMAND),DIRNAME,KEY,KEY                         
         GOTO1 DATAMGR                                                          
         MVC   DIRNAME,=CL8'TALDIR'                                             
         TM    DMCB+8,X'12'                                                     
         BNZ   NO                  RECORD NOT FOUND/DELETED                     
         B     DMCHECK                                                          
         SPACE 2                                                                
GETREC   NTR1                                                                   
         LA    R2,IO                                                            
         ST    R2,TIAREC                                                        
         LA    R3,DMWORK                                                        
         MVC   COMMAND,=CL6'GETREC'                                             
         BAS   RE,READFILE                                                      
         B     XIT                                                              
         SPACE 1                                                                
GETRECN  NTR1                                                                   
         LA    R2,IO               NEEDDATA ROUTINES - USE IO3                  
         AHI   R2,L'IO+L'IO2                                                    
         ST    R2,TIANEED          RETURN A(RECORD) TO USER                     
         LA    R3,DMWORK3          USE SPECIAL DMWORK                           
         MVC   COMMAND,=CL6'GETREC'                                             
         BAS   RE,READFILE                                                      
         B     XIT                                                              
         SPACE 1                                                                
READFILE NTR1                                                                   
         TM    TISTAT,TISTRDCK                                                  
         BNO   *+10                                                             
         MVC   FILENAME,=CL8'CHKFIL'                                            
         GOTO1 ,DMCB,(DMINBITS,COMMAND),FILENAME,KEY+34,(R2),(R3)               
         TM    TIQFLAGS,TIQFUPRC   TEST READ FOR UPDATE REQUESTED               
         BZ    *+8                                                              
         OI    0(R1),X'80'         SET APPROPRIATE BIT IN HOB OF P1             
         GOTO1 DATAMGR                                                          
         MVC   FILENAME,=CL8'TALFIL'                                            
         SPACE 1                                                                
DMCHECK  CLI   DMCB+8,0                                                         
         BE    XIT                                                              
         DC    H'0'                                                             
         EJECT                                                                  
*              ASSORTED ROUTINES                                                
         SPACE 3                                                                
         GETEL R6,DATADISP,ELCODE                                               
         SPACE 1                                                                
ACBLKLNQ EQU   4000-(TLRCELEM-TLRCD)                                            
         SPACE 1                                                                
KEYCOMP  ZIC   R1,LKEY             VARAIABLE LENGTH FOR COMPARISON              
         EX    R1,*+6              SET CC                                       
         BR    RE                  AND EXIT                                     
         CLC   KEY(0),KEYSAVE                                                   
         SPACE 1                                                                
YES      SR    R1,R1                                                            
         B     *+8                                                              
         SPACE 1                                                                
NO       LA    R1,1                                                             
         LTR   R1,R1                                                            
         SPACE 1                                                                
XIT      XIT1                                                                   
         EJECT                                                                  
***********************************************************************         
*              BASIC POSTING FOR RECORD                                         
***********************************************************************         
         SPACE 1                                                                
POSTREC  NTR1                                                                   
         MVC   TINAME,SPACES                                                    
         MVC   TISHORT,SPACES                                                   
         MVC   TILACT,SPACES                                                    
         MVC   TIEST,SPACES                                                     
         CLI   MAINREAD,TLSTCDQ    IF STAFF, DON'T CLEAR IT                     
         BE    *+10                BECAUSE TISTAFF SHARES SAME AREA             
         MVC   TIAUPO,SPACES                                                    
         LA    R6,IO                                                            
         ST    R6,TIAREC                                                        
         TM    TIQFLAG3,TIQFFCOM   IF FORCING VALUES FROM COMML                 
         BZ    *+12                                                             
         CLI   TIKEY,TLINHCDQ      AND THIS IS INVOICE LEVEL                    
         BE    PREC1               DON'T CLEAR DATES                            
****     CLI   0(R6),TLCKCDQ       CLEAR DATES/INDICS FOR INVOICE               
         CLI   TIKEY,TLCKCDQ       IF READING CKS BY INVOICE DON'T              
         BE    *+16                CLEAR DATES                                  
         XC    TIDATES,TIDATES                                                  
         XC    ANYELIND,ANYELIND                                                
PREC1    CLI   0(R6),TLCKCDQ       LIMITED FOR CHECKS                           
         BNE   *+14                                                             
         XC    TICKDATE,TICKDATE                                                
         MVI   ANYCHKEL,0                                                       
         CLI   0(R6),TLVRCDQ                                                    
         BNE   *+10                                                             
         XC    TIPRD,TIPRD                                                      
         CLI   0(R6),TLCACDQ                                                    
         BNE   *+10                                                             
         MVC   TISSN,TLCASSN-TLCAD(R6)                                          
         MVI   ELCODE,0                                                         
         BRAS  RE,GETEL                                                         
         B     *+8                                                              
         SPACE 1                                                                
PREC2    BRAS  RE,NEXTEL                                                        
         BNE   PRECX                                                            
         CLI   0(R6),TANAELQ                                                    
         BE    PRECNAME                                                         
         CLI   0(R6),TASNELQ                                                    
         BE    PRECSHRT                                                         
         CLI   0(R6),TASTELQ                                                    
         BE    PRECSTAF                                                         
         CLI   0(R6),TAAYELQ                                                    
         BE    PRECAGY                                                          
         CLI   0(R6),TACIELQ                                                    
         BE    PRECCLI                                                          
         CLI   0(R6),TAPIELQ                                                    
         BE    PRECPROD                                                         
         CLI   0(R6),TADDELQ                                                    
         BE    PRECDUE                                                          
         CLI   0(R6),TAINELQ                                                    
         BE    PRECINV                                                          
         CLI   0(R6),TAACELQ                                                    
         BE    PRECLACT                                                         
         CLI   0(R6),TACOELQ                                                    
         BE    PRECTACO                                                         
         CLI   0(R6),TADUELQ                                                    
         BE    PRECDUD                                                          
         CLI   0(R6),TALNELQ                                                    
         BE    PRECLN                                                           
         CLI   0(R6),TACDELQ                                                    
         BE    PRECCHEK                                                         
         CLI   0(R6),TACAELQ                                                    
         BE    PRECCAST                                                         
         CLI   0(R6),TAPDELQ                                                    
         BE    PRECPAYD                                                         
         CLI   0(R6),TAGUELQ                                                    
         BE    PRECGUA                                                          
         CLI   0(R6),TASGELQ                                                    
         BE    PRECSG                                                           
         CLI   0(R6),TAGTELQ                                                    
         BE    PRECGTR                                                          
         CLI   0(R6),TAOIELQ                                                    
         BE    PRECOI                                                           
         CLI   0(R6),TADWELQ                                                    
         BE    PRECDW                                                           
         CLI   0(R6),TALWELQ                                                    
         BE    PRECLW                                                           
         CLI   0(R6),TAFLELQ                                                    
         BE    PRECFL                                                           
         CLI   0(R6),TACPELQ                                                    
         BE    PRECCP                                                           
         CLI   0(R6),TACWELQ                                                    
         BE    PRECCW                                                           
         CLI   0(R6),TAWHELQ                                                    
         BE    PRECWH                                                           
         CLI   0(R6),TAW4ELQ                                                    
         BE    PRECW4                                                           
         CLI   0(R6),TABYELQ                                                    
         BE    PRECBY                                                           
         CLI   0(R6),TANUELQ                                                    
         BE    PRECNU                                                           
         CLI   0(R6),TANXELQ                                                    
         BE    PRECNX                                                           
         CLI   0(R6),TAFNELQ                                                    
         BE    PRECFN                                                           
         CLI   0(R6),TAESELQ                                                    
         BE    PRECES                                                           
         CLI   0(R6),TATUELQ                                                    
         BE    PRECTU                                                           
         CLI   0(R6),TAVUELQ                                                    
         BE    PRECVU                                                           
         B     PREC2                                                            
         SPACE 1                                                                
         USING TANAD,R6                                                         
PRECNAME ZIC   R1,TANALEN                                                       
         SH    R1,=H'3'                                                         
         EX    R1,*+8                                                           
         B     PREC2                                                            
         MVC   TINAME(0),TANANAME                                               
         SPACE 1                                                                
         USING TASND,R6                                                         
PRECSHRT MVC   TISHORT,TASNAME                                                  
         B     PREC2                                                            
         SPACE 1                                                                
         USING TAIND,R6                                                         
PRECINV  MVC   TIPADATE,TAINPDTE       PAYMENT DATE                             
         MVC   TICKDATE,TAINCDTE       CHECK DATE                               
         MVC   TIBIDATE,TAINBDTE       BILLED DATE                              
         MVC   TICKID,TAINCKID         (CHECK) RUN ID                           
         MVC   TIRNDATE,TAINCKRN       (CHECK) RUN DATE                         
         MVC   INVSTAT,TAINSTAT        STATUS BYTES                             
         MVC   INVSTAT2,TAINSTA2                                                
         MVI   ANYINVEL,1                                                       
         B     PREC2                                                            
         SPACE 1                                                                
         USING TADDD,R6                                                         
PRECDUE  MVC   TIDUDATE,TADDDATE       DUE DATE                                 
         MVI   ANYDUEEL,1                                                       
         B     PREC2                                                            
         SPACE 1                                                                
         USING TABYD,R6                                                         
PRECBY   MVC   TIBIDATE,TABYDATE       BILL DATE                                
         MVI   ANYBYEL,1                                                        
         B     PREC2                                                            
         SPACE 1                                                                
         USING TACDD,R6                                                         
PRECCHEK MVC   TICKDATE,TACDDTE        CHECK DATE                               
         MVC   TIRNDATE,TACDRUN        RUN DATE                                 
         MVC   CDSTAT,TACDSTAT         STATUS                                   
         MVI   ANYCHKEL,1                                                       
         B     PREC2                                                            
         SPACE 1                                                                
         USING TAACD,R6                                                         
PRECLACT CLC   TILADATE,TAACCDTE       CHANGE ONLY IF MORE RECENT               
         BNL   PREC2                                                            
         MVC   TILACT,TAACSTAF         BY STAFF                                 
         MVC   TILADATE,TAACCDTE       ON THIS DATE                             
         MVI   ANYLACEL,1                                                       
         B     PREC2                                                            
         SPACE 1                                                                
         USING TASTD,R6                                                         
PRECSTAF MVC   TIMGR,TASTMGR           PICK UP MANAGER                          
         CLI   TASTTYPE,TASTTYP6                                                
         BE    *+12                                                             
         CLI   TASTTYPE,TASTTYP7                                                
         BNE   *+10                                                             
         MVC   TITPC,TISTAFF                   AND TPC FOR TYPE T               
         B     PREC2                                                            
         SPACE 1                                                                
         USING TAAYD,R6                                                         
PRECAGY  MVC   TIAGG,TAAYAGG           AGENCY GROUP                             
         MVC   TIOFF,TAAYTPOF          TP OFFICE                                
         MVI   TILOCK,C'N'         SET LOCK STATUS                              
         TM    TAAYSTA3,TAAYSLCK                                                
         BNO   *+8                                                              
         MVI   TILOCK,C'Y'                                                      
         B     PREC2                                                            
         SPACE 1                                                                
         USING TACID,R6                                                         
PRECCLI  MVC   TICLG,TACICLG           CLIENT GROUP                             
         B     PREC2                                                            
         SPACE 1                                                                
         USING TAPID,R6                                                         
PRECPROD MVC   TICLG,TAPICLG           PRODUCT GROUP                            
         MVC   TIPRG,TAPIPRG                                                    
         B     PREC2                                                            
         SPACE 1                                                                
         USING TACOD,R6                                                         
PRECTACO DS    0H                  NOW DONE IN TASYSIO (TIQFFCOM)               
**NO-OP**CLI   MAINREAD,TLCOCDQ    IF READING INVOICES VIA COMMERCIALS          
*        BNE   PRECTAC5                                                         
*        CLI   SUBREAD,TLINHCDQ                                                 
*        BNE   PRECTAC5                                                         
*        CLI   TIKEY,TLINHCDQ      DON'T SET TACO INFO FROM INVOICE REC         
**NO-OP**BE    PREC2                                                            
*                                                                               
PRECTAC5 MVC   TIMED,TACOMED                                                    
         MVC   TIISCI,TACOCID                                                   
         EDIT  (1,TACOSEC),(3,TISEC),ALIGN=LEFT                                 
* NO-OP  MVC   TIMUSIC,TACOMUS                                                  
         MVC   TICLG,TACOCLG                                                    
         MVC   TICTYPE,TACOTYPE                                                 
         CLI   TICTYPE,C' '                                                     
         BH    *+8                                                              
         MVI   TICTYPE,X'FE'                                                    
         MVI   TILOCK,C'N'         SET LOCK STATUS                              
         TM    TACOSTAT,TACOSTLO                                                
         BNO   *+8                                                              
         MVI   TILOCK,C'Y'                                                      
         MVC   COMSTAT,TACOSTAT                                                 
         MVI   ANYCOMEL,1                                                       
         B     PREC2                                                            
         SPACE 1                                                                
         USING TADUD,R6                                                         
PRECDUD  MVC   TIAGY,TADUAGY           DUE COMPANY DETAILS                      
         MVC   TIEMP,TADUEMP                                                    
         MVI   TIZERO,C'Y'                                                      
         CLC   TADUDUE,TADUCOL                                                  
         BE    *+8                                                              
         MVI   TIZERO,C'N'                                                      
         MVI   TICUR,C'U'              SET CURRENCY TO US                       
         TM    TADUSTAT,TADUSCAN           UNLESS IT'S CANADIAN                 
         BNO   *+8                                                              
         MVI   TICUR,C'C'                                                       
         TM    TADUSTAT,TADUSEUR           OR UNLESS IT EUROS                   
         BNO   *+8                                                              
         MVI   TICUR,C'E'                                                       
         MVC   DUESTAT,TADUSTAT                                                 
         MVI   ANYDUCEL,1                                                       
         B     PREC2                                                            
         SPACE 1                                                                
         USING TACAD,R6                CAST DETAILS                             
PRECCAST DS    0H                                                               
         GOTOR TRNSAGT,DMCB,(X'40',TACANCDE),TIAGT                              
         OC    TACANCDE,TACANCDE                                                
         BNZ   *+10                                                             
         MVC   TIAGT,=C'NONE'                                                   
         MVC   TIGUA,TACAGUA                                                    
         OC    TIGUA,TIGUA                                                      
         BNZ   *+10                                                             
         MVC   TIGUA,=C'NONE'                                                   
         MVC   TIONOF,TACAONOF         ON OFF CAMERA                            
         OC    TIONOF,TIONOF                                                    
         BNZ   *+10                                                             
         MVC   TIONOF,=C'OFF'                                                   
         MVC   TIUN,TACAUN             UNION                                    
         MVC   TILOCL,TACALOCL         LOCAL                                    
                                                                                
         OC    TIUNIT,TIUNIT       IF TIUNIT ALREADY SET                        
         BNZ   PRECCA2             YES, DON'T USE CAST ELEMENT'S UNIT           
         TM    TIFTUNIT,TIFTWRK    IF EXPLICITLY FILTERING ON WORK ST           
         BO    *+12                PRE-SET TO UNIT FROM CAST RECORD             
         TM    TISTAT,TISTRDCK         IF NOT READING CHECKS                    
         BO    *+10                    (WILL GET FROM TACW ELS OR KEY)          
         MVC   TIUNIT,TACAUNIT         TAX UNIT                                 
                                                                                
PRECCA2  OC    TIFUNIT,TIFUNIT     IF FILTERING ON UNIT                         
         BZ    PRECCA4                                                          
         CLI   TIFUNIT+2,C' '      AND FILTERING ON STATE                       
         BNE   PRECCA4                                                          
         CLC   TIUNIT,=C'NYC'      TRANSLATE CITIES TO STATE (YUK!)             
         BE    *+14                                                             
         CLC   TIUNIT,=C'YON'                                                   
         BNE   *+10                                                             
         MVC   TIUNIT,=C'NY '                                                   
         CLC   TIUNIT,=C'PHL'                                                   
         BE    *+14                                                             
         CLC   TIUNIT,=C'PIT'                                                   
         BNE   *+10                                                             
         MVC   TIUNIT,=C'PA '                                                   
         CLC   TIUNIT,=C'CIN'                                                   
         BE    *+14                                                             
         CLC   TIUNIT,=C'CLV'                                                   
         BNE   *+10                                                             
         MVC   TIUNIT,=C'OH '                                                   
         CLC   TIUNIT,=C'DNV'                                                   
         BNE   *+10                                                             
         MVC   TIUNIT,=C'CO '                                                   
         CLC   TIUNIT,=C'LJC'                                                   
         BNE   *+10                                                             
         MVC   TIUNIT,=C'KY '                                                   
         CLC   TIUNIT,=C'DET'                                                   
         BNE   *+10                                                             
         MVC   TIUNIT,=C'MI '                                                   
         CLC   TIUNIT,=C'LAX'                                                   
         BNE   *+10                                                             
         MVC   TIUNIT,=C'CA '                                                   
PRECCA4  CLI   TIREAD,TLCKYCDQ         IF NOT READING YTD CHECKS                
         BE    *+10                    (WILL GET FROM KEY)                      
         MVC   TIYEAR,TACAYEAR         YEAR                                     
                                                                                
PRECCA5  TM    TIQFLAG2,TIQFNLIM       SKIP LIMIT CHECK?                        
         BO    PREC2                                                            
         CLI   TIREAD,TLCACCDQ         CAST COMM'L                              
         BE    *+8                                                              
         CLI   TIREAD,TLCAGCDQ         GRT CAST COMM'L                          
         BNE   PREC2                                                            
                                                                                
         LA    R4,KEY                                                           
         MVC   NEEDKEY,KEY             NO - SO WE'LL HAVE TO READ               
         MVC   NEEDSAVE,KEYSAVE                                                 
         USING TLCAPD,R4                                                        
         ICM   RF,15,TLCAACOM          INTERNAL COMML NUMBER                    
                                                                                
         XC    KEY,KEY                                                          
         USING TLCOPD,R4                                                        
         MVI   TLCOPCD,TLCOCCDQ        PASSIVE BY INT COMM NUMBER               
         STCM  RF,15,TLCOCCOM                                                   
                                                                                
         BAS   RE,HIGH                                                          
         CLC   KEY(32),KEYSAVE                                                  
         BNE   PRECCA8                                                          
                                                                                
         BAS   RE,GETRECN              GET COMML RECORD                         
                                                                                
         L     R4,TIANEED                                                       
         USING TLCOD,R4                                                         
         MVC   TIAGY,TLCOAGY           AGENCY                                   
         MVC   TICLI,TLCOCLI           CLIENT FROM COMML RECORD                 
                                                                                
PRECCA8  MVC   KEY,NEEDKEY             TIDY UP                                  
         BAS   RE,HIGH                                                          
         MVC   KEYSAVE,NEEDSAVE                                                 
                                                                                
         B     PREC2                                                            
         SPACE 1                                                                
         USING TATUD,R6                CAST DETAILS                             
PRECTU   DS    0H                                                               
         OC    TIFUNIT,TIFUNIT                                                  
         BZ    PREC2                                                            
         TM    TIFUNIT,X'80'           CHARACTER / FLIST?                       
         BZ    PRECTU5                                                          
         TM    TIFTUNIT,TIFTWRK        LOOKING FOR WORKING                      
         BZ    PRECTU3                                                          
         TM    TATUSTAT,TATUSRES       RESIDENT ONLY, NO WORK                   
         BO    PREC2                                                            
PRECTU3  CLC   TIFUNIT,TATUUNIT                                                 
         BNE   PREC2                                                            
         MVC   TIUNIT,TATUUNIT                                                  
         B     PREC2                                                            
                                                                                
PRECTU5  LA    R2,TIFUNIT                                                       
         LA    R3,TATUUNIT                                                      
         LA    R4,L'TIUNIT                                                      
         BAS   RE,TESTEMRT                                                      
         BNE   PREC2                                                            
         MVC   TIUNIT,TATUUNIT                                                  
         B     PREC2                                                            
         SPACE 1                                                                
         USING TAVUD,R6                ADVICE DETAILS                           
PRECVU   DS    0H                                                               
         CLI   TAVUUSE,0                                                        
         BE    PREC2                                                            
                                                                                
         L     R1,TIATGLOB         LOOK UP REUSE IF A(TABLE OK)                 
         LTR   R1,R1                                                            
         BZ    PREC2                                                            
         USING TGD,R1                                                           
         L     R1,TGAUSES                                                       
                                                                                
         USING USETABD,R1                                                       
PRECVU2  CLI   0(R1),X'FF'         END OF USE TABLE?                            
         BE    PREC2               YES, LEAVE                                   
         CLC   TAVUUSE,USEEQU      MATCH ON EQUATE                              
         BE    PRECVU5                                                          
         LH    R0,USELEN                                                        
         AR    R1,R0                                                            
         B     PRECVU2                                                          
                                                                                
PRECVU5  MVC   TIUSE,USECDE                                                     
         B     PREC2                                                            
                                                                                
         USING TAATD,R6                ADDT'L TAX DETAILS                       
PRECAT   DS    0H                                                               
         OC    TIFUNIT,TIFUNIT                                                  
         BZ    PREC2                                                            
         TM    TIFUNIT,X'80'           CHARACTER / FLIST?                       
         BZ    PRECAT5                                                          
         CLC   TIFUNIT,TAATUNIT                                                 
         BNE   PREC2                                                            
         MVC   TIUNIT,TAATUNIT                                                  
         B     PREC2                                                            
                                                                                
PRECAT5  LA    R2,TIFUNIT                                                       
         LA    R3,TAATUNIT                                                      
         LA    R4,L'TIUNIT                                                      
         BAS   RE,TESTEMRT                                                      
         BNE   PREC2                                                            
         MVC   TIUNIT,TAATUNIT                                                  
         B     PREC2                                                            
         SPACE 1                                                                
         USING TAPDD,R6                PAYMENT DETAILS                          
PRECPAYD DS    0H                                                               
         MVI   ANYPDEL,1                                                        
         MVC   TIW4TY,TAPDW4TY         W4 TYPE                                  
         MVC   TIOFF,TAPDOFF           TP OFFICE                                
         CLI   TIOFF,C' '                                                       
         BH    *+8                                                              
         MVI   TIOFF,C'1'                 (DEFAULT TO 1)                        
         TM    TIQFLAG3,TIQFFCOM       IF FORCING COMML VALUES ONLY             
         BO    *+16                                                             
         MVC   TICLI,TAPDCLI           CLIENT AND                               
         MVC   TIPRD,TAPDPRD           PRODUCT                                  
         MVC   TIEMP,=C'TP '           DEFAULT EOR TO TP                        
         CLI   TAPDEMP,C'A'                                                     
         BL    *+10                                                             
         MVC   TIEMP,TAPDEMP           EMPLOYER                                 
         MVC   TICOM,TAPDCOM           INT COMM NO.                             
         MVC   TIINV,TAPDINV           INT INVOICE NUMBER                       
         MVC   TIUSE,TAPDUSE           USE CODE                                 
         CLI   TIUSE,C' '                                                       
         BH    *+10                                                             
         MVC   TIUSE,=C'OTH'              (DEFAULT TO OTH)                      
         MVC   TIUTYPE,TAPDTYPE        USE TYPE                                 
         MVC   PAYPSTAT,TAPDPSTS       PAYMENT PAYMENT STATUS                   
         MVC   PAYSTAT,TAPDSTAT        PAYMENT STATUS                           
         MVC   PAYOPTS,TAPDOPTS        PAYMENT OPTIONS                          
         MVC   PAYAINDS,TAPDADJS       ADJUSTMENT INDICATORS                    
         MVC   TICSDATE,TAPDCYCS       CYCLE START DATE                         
         MVC   TICEDATE,TAPDCYCE       CYCLE END DATE                           
         MVC   TIAREA,TAPDAREA         AREA                                     
         MVC   TIPUSE,TAPDPUSE         PRINT USE                                
         EDIT  TAPDESPD,(2,TIESPD),ALIGN=LEFT ESTIMATE PERIOD                   
         MVI   TICUR,C'U'              SET CURRENCY TO US                       
         TM    TAPDSTAT,TAPDSCAN           UNLESS IT'S CANADIAN                 
         BNO   *+8                                                              
         MVI   TICUR,C'C'                                                       
         TM    TAPDPST2,TAPDPEUR           OR EURO                              
         BNO   *+8                                                              
         MVI   TICUR,C'E'                                                       
         L     R1,TIATGLOB             LOOK UP REUSE IF A(TABLE OK)             
         LTR   R1,R1                                                            
         BZ    PREC2                                                            
         USING TGD,R1                                                           
         L     R1,TGAUSES                                                       
         USING USETABD,R1                                                       
         MVI   TIPTYPE,C'R'        PRESET TO REUSE                              
         SPACE 1                                                                
LUPUSE2  CLI   0(R1),X'FF'                                                      
         BE    PREC2                                                            
         CLC   TIUSE,USECDE                                                     
         BE    LUPUSE4                                                          
         LH    R0,USELEN                                                        
         AR    R1,R0                                                            
         B     LUPUSE2                                                          
         SPACE 1                                                                
LUPUSE4  TM    USESTAT,SESSION     UNLESS IT'S SESSION                          
         BNO   PREC2                                                            
         MVI   TIPTYPE,C'S'                                                     
         B     PREC2                                                            
         SPACE 1                                                                
         USING TAGUD,R6                GUARANTEE DETAILS                        
PRECGUA  DS    0H                                                               
         MVI   ANYPDEL,1               SIMULATE FINDING TAPDEL                  
         MVC   TIAGY,TAGUAGY           AGENCY AND                               
         MVC   TICLI,TAGUCLI           CLIENT                                   
         MVC   TICSDATE,TAGUSTRT       START DATE                               
         MVC   TICEDATE,TAGUEND        END DATE                                 
         MVI   TIZERO,C'Y'                                                      
         OC    TAGUBAL,TAGUBAL                                                  
         BZ    *+8                                                              
         MVI   TIZERO,C'N'                                                      
         B     PREC2                                                            
         SPACE 1                                                                
         USING TASGD,R6                SOAP GUARANTEE DETAILS                   
PRECSG   DS    0H                                                               
         MVC   TIAGY,TASGAGY           AGENCY                                   
         B     PREC2                                                            
         SPACE 1                                                                
         USING TAGTD,R6            GUARANTEE TRACKING DETAILS                   
PRECGTR  DS    0H                                                               
         MVI   ANYPDEL,1           SIMULATE FINDING TAPDEL                      
         MVC   TICID,TAGTCID           COMMERCIAL ID                            
         MVC   TICAT,TAGTCAT           CATEGORY                                 
         MVC   TIONOF,TAGTCAM          ON/OFF CAMERA                            
         MVC   TIYEAR,TAGTYEAR         UNION YEAR                               
         MVC   TIUN,TAGTUNI            UNION                                    
         MVC   TIUSE,TAGTUSE           USE CODE                                 
         MVC   TIUTYPE,TAGTTYPE        USE TYPE                                 
         MVC   TICSDATE,TAGTSTRT       CYCLE START DATE                         
         MVC   TICEDATE,TAGTEND        CYCLE END DATE                           
         MVC   PAYPSTAT,TAGTPST1       PAYMENT STATUS                           
         MVC   TIAGY,TAGTAGY           AGENCY (OPTIONAL)                        
         B     PREC2                                                            
         SPACE 1                                                                
         USING TALND,R6                                                         
PRECLN   DS    0H                      LIEN DETAILS                             
         MVI   TIZERO,C'Y'                                                      
         CLC   TALNDUE,TALNCOL                                                  
         BE    *+8                                                              
         MVI   TIZERO,C'N'                                                      
         MVC   TIEMP,TALNEMP                                                    
         B     PREC2                                                            
         SPACE 1                                                                
         USING TANUD,R6                                                         
PRECNU   CLI   TANUTYPE,TANUTEST       IF ESTIMATE NUMBER                       
         BNE   PRECNU2                                                          
*                                                                               
         ZIC   R1,TANULEN                                                       
         SH    R1,=Y(TANULNQ+1)                                                 
         EX    R1,*+8                                                           
         B     PRECNUX                                                          
         MVC   TIEST(0),TANUMBER       SET IT                                   
*                                                                               
PRECNU2  CLI   TANUTYPE,TANUTAUT       IF AUTH/PO NUMBER                        
         BNE   PRECNU3                                                          
*                                                                               
         ZIC   R1,TANULEN                                                       
         SH    R1,=Y(TANULNQ+1)                                                 
         EX    R1,*+8                                                           
         B     PRECNUX                                                          
         MVC   TIAUPO(0),TANUMBER      SET IT                                   
*                                                                               
PRECNU3  CLI   TANUTYPE,TANUTSIG       IF SIGNATORY                             
         BNE   PRECNUX                                                          
*                                                                               
         MVC   TISGNTR,SPACES      INIT SAVEAREA                                
*                                                                               
         ZIC   R1,TANULEN                                                       
         SH    R1,=Y(TANULNQ+1)                                                 
         EX    R1,*+8                                                           
         B     PRECNUX                                                          
         MVC   TISGNTR(0),TANUMBER        SET IT                                
*                                                                               
PRECNUX  DS    0H                                                               
*                                                                               
         B     PREC2                                                            
         SPACE 1                                                                
         USING TANXD,R6                                                         
PRECNX   L     RE,TIAREC               IF TRANSFER DETAILS ELEMENT              
         CLI   0(RE),TLNXCDQ           ON HOLD RECORD                           
         BNE   PREC2                                                            
         CLI   TANXLEN,TANXLN2Q        AND ELEMENT CONTAINS AD-ID               
         BL    PREC2                                                            
         OC    TANXADID,TANXADID       AND AD-ID IS SET                         
         BZ    PREC2                                                            
         MVC   TICID,TANXADID          MOVE AD-ID INTO COMMERCIAL               
         B     PREC2                                                            
         SPACE 1                                                                
         USING TAFND,R6                                                         
PRECFN   L     RE,TIAREC               IF FREE FORM NAME ELEMENT                
         CLI   0(RE),TLVRCDQ           ON VERSION RECORD                        
         BNE   PREC2                                                            
         CLI   TAFNTYPE,TAFNTPRD       IF ELEMENT CONTAINS PRODUCT              
         BNE   *+10                                                             
         MVC   TIPRD,TAFNNAME          SET IT                                   
         CLI   TAFNTYPE,TAFNTAGY       IF ELEMENT CONTAINS AGENCY               
         BNE   *+10                                                             
         MVC   TIAGY,TAFNNAME          SET IT                                   
         B     PREC2                                                            
         SPACE 1                                                                
         USING TAESD,R6                                                         
PRECES   CLI   TAESTYPE,TAESTCLI       CLIENT TYPE                              
         BNE   PREC2                                                            
         MVC   TICLI,TAESCLI           SET IT                                   
         B     PREC2                                                            
         SPACE 1                                                                
         USING TAOID,R6                                                         
PRECOI   DS    0H                      ORIGINAL AGENCY/INVOICE                  
         OC    TAOIAGY,TAOIAGY         IGNORE IF NO AGENCY AROUND               
         BZ    PREC2                                                            
**NOOP** CLC   TIFAGY,=C'999999'       IF FILTERING BY AGY 999999               
**NOOP** BE    PREC2                   DON'T EXTRACT OLD AGY/INV                
         MVC   TIAGY,TAOIAGY                                                    
         MVC   TIINV,TAOIINV                                                    
         B     PREC2                                                            
         SPACE 1                                                                
         USING TADWD,R6                                                         
PRECDW   DS    0H                      DUE COMPANY DETAILS                      
         MVC   TIDUC,TADWDUC                                                    
         B     PREC2                                                            
         SPACE 1                                                                
         USING TALWD,R6                                                         
PRECLW   DS    0H                      LIEN DETAILS                             
         MVC   TILNC,TALWLIN                                                    
         B     PREC2                                                            
*                                                                               
         USING TAFLD,R6                                                         
PRECFL   CLI   TIFFTYPE,0              FILTER DETAILS                           
         BNE   PREC2                   (UNLESS SPECIAL FILTER)                  
         MVC   TIFILT1(4),TAFLFLT1                                              
         B     PREC2                                                            
         SPACE 1                                                                
         USING TACPD,R6                                                         
PRECCP   CLI   TIMUSIC,0               NOTE (FIRST) MUSIC                       
         BNE   PREC2                   (UNLESS SPECIAL FILTER)                  
         MVC   TIMUSIC,TACPMUS                                                  
         B     PREC2                                                            
         EJECT                                                                  
         USING TACWD,R6                                                         
PRECCW   DS    0H                                                               
*        OC    TIFUNIT,TIFUNIT                                                  
*        BZ    PREC2                                                            
         OC    TIUNIT,TIUNIT       IF UNIT HAS NOT YET BEEN SET                 
         BNZ   PREC2                                                            
         CLC   TACWUNIT,=C'FD '                                                 
         BE    PREC2                                                            
         TM    TIFUNIT,X'80'           CHARACTER OR FLIST?                      
         BZ    PRECCW50                                                         
         B     PRECCW60                                                         
                                                                                
PRECCW50 LA    R2,TIFUNIT             TEST AGAINST FLIST                        
         LA    R3,TACWUNIT                                                      
         LA    R4,L'TIUNIT                                                      
         BAS   RE,TESTEMRT                                                      
         BNE   PREC2                                                            
                                                                                
PRECCW60 TM    TIFTUNIT,TIFTSTTE+TIFTCITY   BOTH STATE AND CITY?                
         BO    PRECCW94                                                         
                                                                                
PRECCW90 DS    0H                                                               
         TM    TIFTUNIT,TIFTCITY                                                
         BO    PRECCW92                                                         
         CLI   TACWUNIT+2,C' '        INTERESTED IN STATE                       
         BH    PREC2                                                            
         B     PRECCW94                                                         
         SPACE 1                                                                
PRECCW92 CLI   TACWUNIT+2,C' '        INTERESTED IN CITY                        
         BE    PREC2                                                            
                                                                                
PRECCW94 MVC   WORK(1),TIFTUNIT    WHAT TYPE OF STATE IS REQUIRED?              
         CLI   WORK,0              IF NONE SPECIFIED,                           
         BNE   *+8                                                              
         OI    WORK,TIFTTAX           DEFAULT TO TAXABLE                        
         NC    WORK(1),TACWSTAT    DOES THIS MATCH WITHHOLDING?                 
         CLI   WORK,0                                                           
         BE    PREC2                                                            
         MVC   TIUNIT,TACWUNIT                                                  
         B     PREC2                                                            
         EJECT                                                                  
         USING TAWHD,R6                                                         
PRECWH   DS    0H                  NOTE W/H EMPLOYER                            
**NO-OP**MVC   TIEMP,TAWHEMP       9/12/96 -BREAKS ACTIVEW4 FILTER              
         B     PREC2               AND NOT NECESSARY AS IT IS ALWAYS TP         
         SPACE 1                                                                
         USING TAW4D,R6                                                         
PRECW4   MVC   TIW4W4,TAW4TYPE     NOTE W4 TYPE                                 
         MVC   TIW4TY,TAW4TYPE     NOTE W4 TYPE                                 
         MVC   TISEX,TAW4SEX            AND SEX                                 
         MVI   TILOCK,C'N'         SET LOCK STATUS                              
         TM    TAW4STA2,TAW4SLCK                                                
         BNO   *+8                                                              
         MVI   TILOCK,C'Y'                                                      
         B     PREC2                                                            
                                                                                
PRECX    LA    R2,TIFILT1          SHOULD HAVE FILTERS BY NOW                   
         LA    R0,4                                                             
         SPACE 1                                                                
PRECX2   CLI   0(R2),C' '          SO SET IF MISSING                            
         BH    *+8                                                              
         MVI   0(R2),X'FE'         (USE A NON-PRINTABLE CHAR)                   
         LA    R2,1(R2)                                                         
         BCT   R0,PRECX2                                                        
         J     XIT                                                              
         EJECT                                                                  
*              ROUTINES CONTROL KEY SETTING AND I/O HOOKS TO CALLER             
         SPACE 1                                                                
GOHOOK   DS    0H                                                               
         MVI   TIMODE,PROCREC      HAVE A FILE RECORD                           
         B     IOHOOK                                                           
         SPACE 1                                                                
GOHKINV  DS    0H                                                               
         MVI   TIMODE,PROCINV      HAVE AN INVOICE RECORD                       
         B     IOHOOK                                                           
         SPACE 1                                                                
GOHKDIR  DS    0H                                                               
         MVI   TIMODE,PROCDIR      HAVE A DIRECTORY RECORD                      
         SPACE 1                                                                
IOHOOK   NTR1                                                                   
         OI    STATUS,FOUNDONE     SET WE PASSED A RECORD TO APPLIC.            
         L     RF,TIHOOK                                                        
         L     RE,USERRD                                                        
         LM    R0,RC,20(RE)                                                     
         BASR  RE,RF               MAY RETURN CC                                
         XIT1                                                                   
         EJECT                                                                  
*              EQUATES, CONSTANTS, TABLES, ETC....                              
         SPACE 1                                                                
DATADISP DC    H'40'                                                            
DMINBITS DC    X'08'                                                            
ALL      EQU   X'FF'                                                            
COMPLM   DC    X'FFFFFFFFFFFF'                                                  
         SPACE 1                                                                
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
*              ROUTINE TO TRANSLATE PIDS INTO SSNS                              
***********************************************************************         
         USING TAGLD,R6                                                         
PIDTRANS NTR1  BASE=*,LABEL=*                                                   
         TM    STATUS,SSNFLIST   IF CURRENT FLIST IS SETTING TIFSSN             
         JZ    XIT                                                              
         CLI   TAGLLEN,9         AND CURRENT ELEMENT HAS LENGTH OF 9            
         JNE   XIT               (ELCODE+LENGTH+SPARE+PID)                      
         GOTO1 SSNUNPK,DMCB,TAGLDATA,0(R3) TRANSLATE PID INTO SSN               
         J     XIT                                                              
         SPACE 2                                                                
SSNUNPK  NTR1                                                                   
         L     R3,4(R1)            CLEAR OUT SS#, IN CASE PID INVALID           
         MVC   0(9,R3),SPACES                                                   
         SPACE 1                                                                
         L     R2,0(R1)            PID #                                        
         LA    R3,6                6 DIGITS                                     
         LA    RF,BASE36TB                                                      
         XR    R0,R0                                                            
SSNUP10  CLI   0(RF),X'FF'         EOT?                                         
         JNE   SSNUP20                                                          
         LTR   RB,RB                                                            
         J     XIT                 LEAVE, WITH SS# AS BLANK                     
SSNUP20  BAS   RE,SSNUCALC         UNPACK CALCULATIONS                          
         JNE   XIT                                                              
         A     R0,FULL                                                          
         LA    RF,L'BASE36TB(RF)                                                
         LA    R2,1(R2)                                                         
         BCT   R3,SSNUP10                                                       
         L     R3,4(R1)                                                         
         EDIT  (R0),(9,(R3)),ZERO=NOBLANK,FILL=0                                
         CR    RB,RB                                                            
         J     XIT                                                              
         SPACE 2                                                                
SSNUCALC NTR1                                                                   
         LA    R3,BASE36T2                                                      
SCALU10  CLI   0(R3),X'FF'                                                      
         JE    SCALUNO                                                          
         CLC   0(1,R2),1(R3)       FIND MATCH OF BASE 36 NUM IN TABLE           
         JE    SCALU20                                                          
         LA    R3,L'BASE36T2(R3)                                                
         J     SCALU10                                                          
SCALU20  XC    HALF,HALF                                                        
         MVC   HALF+1(1),0(R3)     HEX EQUIVALENT NUMBER                        
         ZICM  RE,0(RF),4                                                       
         MH    RE,HALF                                                          
         STCM  RE,15,FULL                                                       
         SPACE 1                                                                
SCALUYES CR    RB,RB                                                            
         J     *+6                                                              
SCALUNO  LTR   RB,RB                                                            
         J     XIT                                                              
         SPACE 1                                                                
BASE36TB DS    0F                                                               
         DC    F'60466176'         36 TO THE 5TH POWER                          
         DC    F'1679616'          36 TO THE 4TH POWER                          
         DC    F'46656'            36 TO THE 3RD POWER                          
         DC    F'1296'             36 TO THE 2ND POWER                          
         DC    F'36'               36 TO THE 1ST POWER                          
         DC    F'1'                36 TO THE ZERO POWER                         
         DC    X'FFFFFFFF'                                                      
         SPACE                                                                  
BASE36T2 DS    0XL2                                                             
         DC    XL1'0',CL1'C'                                                    
         DC    XL1'1',CL1'U'                                                    
         DC    XL1'2',CL1'7'                                                    
         DC    XL1'3',CL1'N'                                                    
         DC    XL1'4',CL1'2'                                                    
         DC    XL1'5',CL1'A'                                                    
         DC    XL1'6',CL1'T'                                                    
         DC    XL1'7',CL1'K'                                                    
         DC    XL1'8',CL1'Z'                                                    
         DC    XL1'9',CL1'8'                                                    
         DC    XL1'A',CL1'R'                                                    
         DC    XL1'B',CL1'Q'                                                    
         DC    XL1'C',CL1'D'                                                    
         DC    XL1'D',CL1'0'                                                    
         DC    XL1'E',CL1'X'                                                    
         DC    XL1'F',CL1'B'                                                    
         DC    XL1'10',CL1'J'                                                   
         DC    XL1'11',CL1'V'                                                   
         DC    XL1'12',CL1'5'                                                   
         DC    XL1'13',CL1'S'                                                   
         DC    XL1'14',CL1'G'                                                   
         DC    XL1'15',CL1'P'                                                   
         DC    XL1'16',CL1'W'                                                   
         DC    XL1'17',CL1'L'                                                   
         DC    XL1'18',CL1'9'                                                   
         DC    XL1'19',CL1'6'                                                   
         DC    XL1'1A',CL1'Y'                                                   
         DC    XL1'1B',CL1'1'                                                   
         DC    XL1'1C',CL1'F'                                                   
         DC    XL1'1D',CL1'M'                                                   
         DC    XL1'1E',CL1'4'                                                   
         DC    XL1'1F',CL1'I'                                                   
         DC    XL1'20',CL1'3'                                                   
         DC    XL1'21',CL1'E'                                                   
         DC    XL1'22',CL1'H'                                                   
         DC    XL1'23',CL1'O'                                                   
         DC    X'FF'                                                            
         EJECT                                                                  
***********************************************************************         
*              CIHR FILTERING ROUTINE                                           
***********************************************************************         
         SPACE 2                                                                
FILTCIHR NTR1  BASE=*,LABEL=*                                                   
         TM    TIQFLAG4,TIQFCIHR   CHECK IF FILTER IS ON FOR CIHR               
         JZ    YES                                                              
                                                                                
         L     R6,TIAREC                                                        
         CLI   TIREAD,TLAYCDQ      AGENCY RECORD?                               
         JNE   FCIHR100                                                         
         CLI   0(R6),TLAYCDQ                                                    
         JE    FCIHR300                                                         
         J     YES                                                              
                                                                                
FCIHR100 CLI   TIREAD,TLCLCDQ      CLIENT RECORD                                
         JNE   YES                                                              
         CLI   0(R6),TLCLCDQ                                                    
         JE    FCIHR500                                                         
         J     YES                                                              
                                                                                
         USING TAAYD,R6                                                         
FCIHR300 MVI   ELCODE,TAAYELQ      AGENCY ELEMENT                               
         BRAS  RE,GETEL                                                         
         JNE   NO                                                               
         TM    TAAYSTA5,TAAYCIHR   CIHR IN AGENCY STATUS                        
         BO    YES                                                              
         J     NO                                                               
                                                                                
         USING TACID,R6                                                         
FCIHR500 MVI   ELCODE,TACIELQ      CLIENT INFO ELEMENT                          
         BRAS  RE,GETEL                                                         
         JNE   NO                                                               
         TM    TACISTA2,TACICIHR   CIHR IN CLIENT STATUS                        
         BO    YES                                                              
         J     NO                                                               
                                                                                
         DROP  R6                                                               
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
*        ROUTINE TO TRANSLATE AGENT CODE                              *         
*        INCLUDED BY TASYSVAL AND TALNK01                             *         
*        ON ENTRY ... P1 BYTE 0 = X'80' CHARACTER TO NUMERIC          *         
*                                 X'40' NUMERIC TO CHARACTER          *         
*                     P1        = A(INCOMING AGENT CODE)              *         
*                     P2        = A(TRANSLATED AGENT CODE)            *         
***********************************************************************         
                                                                                
TRNSAGT  NTR1  BASE=*,LABEL=*                                                   
       ++INCLUDE TATRNSAGT                                                      
***********************************************************************         
*              DATE FILTERING ROUTINE                                           
***********************************************************************         
         SPACE 2                                                                
FILTDATE NTR1  BASE=*,LABEL=*                                                   
         OC    TIQRDTES,TIQRDTES   POSSIBLE RUN DATE TEST                       
         BZ    FILTD1                                                           
         OC    TIRNDATE,TIRNDATE   TEST WE HAVE RUN DATE                        
         BZ    FILTD1                                                           
         CLC   TIRNDATE,TIQRSTR    START TEST                                   
         JL    NO                                                               
         OC    TIQREND,TIQREND     END TEST                                     
         BZ    FILTD1                                                           
         CLC   TIRNDATE,TIQREND                                                 
         JH    NO                                                               
         SPACE 1                                                                
FILTD1   CLI   TIQDTYPE,TIQDBILL   FILTER BY TYPE                               
         BE    FILTDBIL                                                         
         CLI   TIQDTYPE,TIQDPAY                                                 
         BE    FILTDPAY                                                         
         CLI   TIQDTYPE,TIQDCHK                                                 
         BE    FILTDCHK                                                         
         CLI   TIQDTYPE,TIQDDUE                                                 
         BE    FILTDDUE                                                         
         CLI   TIQDTYPE,TIQDLAST                                                
         BE    FILTDLAC                                                         
         CLI   TIQDTYPE,TIQDCYCS                                                
         BE    FILTDCYS                                                         
         CLI   TIQDTYPE,TIQDCYCE                                                
         BE    FILTDCYE                                                         
         CLI   TIQDTYPE,TIQDEXP                                                 
         BE    FILTDEXP                                                         
         CLI   TIQDTYPE,TIQDASS                                                 
         BE    FILTDASS                                                         
         CLI   TIQDTYPE,TIQDCOD                                                 
         BE    FILTDCOD                                                         
         CLI   TIQDTYPE,TIQDCFIL                                                
         BE    FILTDCFL                                                         
         CLI   TIQDTYPE,TIQDCREC                                                
         BE    FILTDCRC                                                         
         CLI   TIQDTYPE,TIQDCMUS                                                
         BE    FILTDCMS                                                         
         CLI   TIQDTYPE,TIQDPDTE                                                
         BE    FILTPDTE                                                         
         CLI   TIQDTYPE,TIQDRNR                                                 
         BE    FILTRNR                                                          
         CLI   TIQDTYPE,TIQDRND                                                 
         BE    FILTRND                                                          
         CLI   TIQDTYPE,TIQDRGDV                                                
         BE    FILTRGDV                                                         
         CLI   TIQDTYPE,TIQDAHLD                                                
         BE    FILTAHLD                                                         
         CLI   TIQDTYPE,TIQDFCYC                                                
         BE    FILTFCYC                                                         
         CLI   TIQDTYPE,TIQDVDTE   VERIFICATION DATE                            
         BE    FILTFVDT                                                         
         CLI   TIQDTYPE,TIQDCAST   CAST FIRST SERVICE DATE                      
         BE    FILTFCST                                                         
         L     R6,TIAREC           LOOK FOR ADVICE DATES                        
         MVI   ELCODE,TADVELQ                                                   
         BRAS  RE,GETEL                                                         
         JNE   YES                                                              
         USING TADVD,R6                                                         
         MVC   TIDATE,TADVVDTE     AND PICK OFF RELEVANT DATE                   
         CLI   TIQDTYPE,TIQDVADV                                                
         BE    FILTD2                                                           
         MVC   TIDATE,TADVRDTE                                                  
         CLI   TIQDTYPE,TIQDRADV                                                
         BE    FILTD2                                                           
         MVC   TIDATE,TADVPDTE                                                  
         CLI   TIQDTYPE,TIQDPADV                                                
         BE    FILTD2                                                           
         MVC   TIDATE,TADVSDTE                                                  
         CLI   TIQDTYPE,TIQDSADV                                                
         BE    FILTD2                                                           
         J     YES                                                              
         SPACE 1                                                                
FILTDBIL MVC   TIDATE,TIBIDATE     BILL DATE                                    
         CLI   ANYINVEL,1                                                       
         BE    FILTD2                                                           
         CLI   ANYBYEL,1                                                        
         BE    FILTD2                                                           
         J     YES                                                              
         SPACE 1                                                                
FILTDPAY MVC   TIDATE,TIPADATE     PAY DATE                                     
         CLI   ANYINVEL,1                                                       
         BE    FILTD2                                                           
         J     YES                                                              
         SPACE 1                                                                
FILTDCHK MVC   TIDATE,TICKDATE     CHECK DATE                                   
         CLI   ANYINVEL,1                                                       
         BE    FILTD2                                                           
         CLI   ANYCHKEL,1                                                       
         BE    FILTD2                                                           
         J     YES                                                              
         SPACE 1                                                                
FILTDLAC MVC   TIDATE,TILADATE     LAST ACTIVITY DATE                           
         CLI   ANYLACEL,1                                                       
         BE    FILTD2                                                           
         J     YES                                                              
         SPACE 1                                                                
FILTDDUE MVC   TIDATE,TIDUDATE     DUE DATE                                     
         CLI   ANYDUEEL,1                                                       
         BE    FILTD2                                                           
         J     YES                                                              
         SPACE 1                                                                
FILTDCYS MVC   TIDATE,TICSDATE     CYCLE START                                  
         CLI   ANYPDEL,1                                                        
         BE    FILTD2                                                           
         J     YES                                                              
         SPACE 1                                                                
FILTDCYE MVC   TIDATE,TICEDATE     CYCLE END DATE                               
         CLI   ANYPDEL,1                                                        
         BE    FILTD2                                                           
         J     YES                                                              
         SPACE 1                                                                
FILTDEXP XC    TIDATE,TIDATE       LOOK FOR EXPIRY DATES                        
         L     R6,TIAREC                                                        
         MVI   ELCODE,TACOELQ                                                   
         BRAS  RE,GETEL                                                         
         BNE   FILTDXP2                                                         
         USING TACOD,R6                                                         
         MVC   TIDATE,TACOEXP                                                   
         SPACE 1                                                                
FILTDXP2 L     R6,TIAREC           LOOK FOR EXPIRY DATES                        
         MVI   ELCODE,TACAELQ                                                   
         BRAS  RE,GETEL                                                         
         BNE   FILTDXP4                                                         
         USING TACAD,R6                                                         
         MVC   TIDATE,TACAEXP                                                   
         SPACE 1                                                                
FILTDXP4 OC    TIDATE,TIDATE                                                    
         JZ    YES                                                              
         B     FILTD2                                                           
         SPACE 1                                                                
FILTDASS L     R6,TIAREC           LOOK FOR ASSIGN DATES                        
         MVI   ELCODE,TAINELQ                                                   
         BRAS  RE,GETEL                                                         
         JNE   YES                                                              
         USING TAIND,R6                                                         
         MVC   TIDATE,TAINIDTE                                                  
         B     FILTD2                                                           
         SPACE 1                                                                
FILTDCOD L     R6,TIAREC           LOOK FOR COD PRINT DATE                      
         XC    TIDATE,TIDATE                                                    
         MVI   ELCODE,TAINELQ                                                   
         BRAS  RE,GETEL                                                         
         JNE   YES                                                              
         USING TAIND,R6                                                         
         ZIC   R0,TAINLEN                                                       
         LA    R1,TAINHDTE-TAIND+3                                              
         CR    R0,R1                                                            
         BL    FILTD2                                                           
         MVC   TIDATE,TAINHDTE                                                  
         B     FILTD2                                                           
         SPACE 1                                                                
FILTDCFL MVI   BYTE,TACSTYPF       LOOK FOR FILM DATE                           
         B     FILTDCST                                                         
FILTDCRC MVI   BYTE,TACSTYPR       LOOK FOR RECORD DATE                         
         B     FILTDCST                                                         
FILTDCMS MVI   BYTE,TACSTYPM       LOOK FOR MUSIC DATE                          
FILTDCST L     R6,TIAREC           ON COMMERCIAL STUDIO ELEMENT                 
         XC    TIDATE,TIDATE                                                    
         MVI   ELCODE,TACSELQ                                                   
         BRAS  RE,GETEL                                                         
         J     *+8                                                              
FILTDCS2 BRAS  RE,NEXTEL                                                        
         JNE   YES                                                              
         USING TACSD,R6                                                         
         CLC   TACSTYPE,BYTE       MATCH ON DATE TYPE                           
         BNE   FILTDCS2                                                         
         MVC   TIDATE,TACSDATE                                                  
         B     FILTD2                                                           
         SPACE 1                                                                
FILTPDTE DS    0H                                                               
         CLI   KEY,TLINPCDQ        PAY DATE PASSIVE POINTER?                    
         BE    FILTDPAY            JUST USE PAY DATE                            
FILTPDT5 XC    TIDATE,TIDATE       LOOK FOR LAST PAY DATE                       
         L     R6,TIAREC                                                        
         MVI   ELCODE,TACOELQ                                                   
         BRAS  RE,GETEL                                                         
         JNE   YES                                                              
         USING TACOD,R6                                                         
         MVC   TIDATE,TACOPDTE                                                  
         B     FILTD2                                                           
         SPACE 1                                                                
FILTRNR  L     R6,TIAREC           LOOK FOR RETURNED DATE (CHK RTN)             
         CLI   0(R6),TLCKCDQ       ON CHECK RECORDS ONLY                        
         JNE   YES                                                              
         MVI   ELCODE,TARNELQ                                                   
         BRAS  RE,GETEL                                                         
         JNE   YES                                                              
         USING TARND,R6                                                         
         MVC   TIDATE,TARNRDTE                                                  
         B     FILTD2                                                           
         SPACE 1                                                                
FILTRND  L     R6,TIAREC           LOOK FOR DISPOSITION DATE (CHK RTN)          
         CLI   0(R6),TLCKCDQ       ON CHECK RECORDS ONLY                        
         JNE   YES                                                              
         MVI   ELCODE,TARNELQ                                                   
         BRAS  RE,GETEL                                                         
         JNE   YES                                                              
         USING TARND,R6                                                         
         MVC   TIDATE,TARNDDTE                                                  
         B     FILTD2                                                           
         SPACE 1                                                                
FILTRGDV L     R6,TIAREC                                                        
         CLI   0(R6),TLDVCDQ                                                    
         JNE   NO                                                               
         MVI   ELCODE,TANXELQ                                                   
         BRAS  RE,GETEL                                                         
         JNE   NO                                                               
         USING TANXD,R6                                                         
         MVC   TIDATE,TANXUDTE                                                  
         B     FILTD2                                                           
         SPACE 1                                                                
FILTAHLD L     R6,TIAREC                                                        
         CLI   0(R6),TLNXCDQ                                                    
         JNE   NO                                                               
         MVI   ELCODE,TANXELQ                                                   
         BRAS  RE,GETEL                                                         
         JNE   NO                                                               
         USING TANXD,R6                                                         
         MVC   TIDATE,TANXADTE                                                  
         B     FILTD2                                                           
         SPACE 1                                                                
FILTFCYC XC    TIDATE,TIDATE       LOOK FOR FIRST FIXED CYCLE                   
         L     R6,TIAREC                                                        
         MVI   ELCODE,TACOELQ                                                   
         BRAS  RE,GETEL                                                         
         JNE   YES                                                              
         USING TACOD,R6                                                         
         MVC   TIDATE,TACOFCYC                                                  
         B     FILTD2                                                           
         SPACE 1                                                                
FILTFVDT DS    0H                  VERIFICATION DATE                            
         XC    TIDATE,TIDATE       LOOK FOR VERIFICATION DATE                   
         L     R6,TIAREC                                                        
         MVI   ELCODE,TACOELQ                                                   
         BRAS  RE,GETEL                                                         
         JNE   YES                                                              
         USING TACOD,R6                                                         
         MVC   TIDATE,TACOVDTE                                                  
         B     FILTD2                                                           
         SPACE 1                                                                
FILTFCST DS    0H                  VERIFICATION DATE                            
         XC    TIDATE,TIDATE       LOOK FOR CAST 1ST SERVICE DATE               
         L     R6,TIAREC                                                        
         MVI   ELCODE,TACAELQ                                                   
         BRAS  RE,GETEL                                                         
         JNE   YES                                                              
         USING TACAD,R6                                                         
         MVC   TIDATE,TACAFRST                                                  
         B     FILTD2                                                           
         SPACE 1                                                                
FILTD2   OC    TIQPSTR(6),TIQPSTR  POSSIBLE FILTERS                             
         JZ    YES                                                              
         CLC   TIQPSTR,=X'010000'  SPECIAL FOR NONE                             
         BE    FILTDNON                                                         
         CLC   TIQPSTR,=X'000001'          AND ANY                              
         BE    FILTDANY                                                         
         CLC   TIDATE,TIQPSTR      START TEST                                   
         JL    NO                                                               
         SPACE 1                                                                
FILTD4   OC    TIQPEND,TIQPEND     END TEST                                     
         JZ    YES                                                              
         CLC   TIDATE,TIQPEND                                                   
         JH    NO                                                               
         J     YES                                                              
         SPACE 1                                                                
FILTDNON OC    TIDATE,TIDATE       NONE TEST                                    
         JZ    YES                                                              
         J     NO                                                               
         SPACE 1                                                                
FILTDANY OC    TIDATE,TIDATE       ANY TEST                                     
         JNZ   YES                                                              
         J     NO                                                               
         LTORG                                                                  
         EJECT                                                                  
* TASYSWORKD                                                                    
* TASYSUSESD                                                                    
         PRINT OFF                                                              
TGLOBD   DSECT                                                                  
       ++INCLUDE TASYSWORKD                                                     
       ++INCLUDE TASYSUSESD                                                     
       ++INCLUDE FAFACTS                                                        
       ++INCLUDE FAWSSVRD                                                       
         PRINT ON                                                               
         SPACE 3                                                                
       ++INCLUDE TASYSIOMYD                                                     
         DSECT                                                                  
       ++INCLUDE TASYSIOD                                                       
         SPACE 1                                                                
LISTD    DSECT                                                                  
LISTENT  DS    0CL16                                                            
LISTALST DS    A                                                                
LISTCODE DS    CL8                                                              
LISTNENT DS    XL1                                                              
LISTLENT DS    XL1                                                              
         DS    XL1                                                              
         DS    XL1                                                              
*TAGENFILE                                                                      
*DDCOMFACS                                                                      
         PRINT OFF                                                              
       ++INCLUDE TAGENFILE                                                      
       ++INCLUDE DDCOMFACS                                                      
         PRINT ON                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'127TASYSIOB  01/21/16'                                      
         END                                                                    
