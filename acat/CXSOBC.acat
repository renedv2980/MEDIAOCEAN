*          DATA SET CXSOBC     AT LEVEL 023 AS OF 05/21/98                      
*CATALP CXSOBC                                                                  
         TITLE 'CXSOBC - OCONTROL USERID/GROUP LEVELS'                          
**********************************************************************          
* AL4 A(EXTRACT RECORD)                                              *          
* AL4 A(CONFILE RECORD)                                              *          
**********************************************************************          
         SPACE 1                                                                
         PRINT NOGEN                                                            
CXSOBC   CSECT                                                                  
         NMOD1 WORKX-WORKD,*CXSOBC*,RR=RA                                       
         USING WORKD,RC                                                         
         ST    R1,APARM                                                         
         ST    RA,RELO                                                          
         MVC   PARM,0(R1)          EXTRACT PARAMETER LIST                       
         LM    R2,R3,0(R1)                                                      
         USING CXSOBD,R2           R2=A(EXTRACT RECORD)                         
         USING SAOCREC,R3          R3=A(FCONTROL AGENCY RECORD)                 
*                                                                               
CXSLEN   XC    CXSOBLEN,CXSOBLEN   RECORD LENGTH/TYPE/ENDOFREC                  
         MVC   CXSOBLEN(2),=AL2(CXSOBDL)                                        
         MVC   CXSOBTYP,CXSOBDQ                                                 
         MVI   CXSOBDX,MXTRTQ                                                   
         MVI   CXSOBDX+1,MXTRTQ                                                 
*                                  GET KEY INFO                                 
CXSAID   MVI   CXSOBAID-1,MXTRTQ   AGENCY ALPHA ID                              
         MVC   CXSOBAID,SAOCAGY                                                 
*                                                                               
CXSUIN   MVI   CXSOBUIN-1,MXTRTQ   USER ID NUMBER                               
         SR    RF,RF                                                            
         ICM   RF,3,SAOCUID                                                     
         CVD   RF,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  CXSOBUIN,DUB+5(3)                                                
*                                                                               
CXSAGC   MVI   CXSOBAGC-1,MXTRTQ   ACCESS GROUP CODE                            
         GOTO1 XHEXOUT,DMCB,SAOCAGN,CXSOBAGC,2,=C'TOG'                          
*                                                                               
CXSSYS   MVI   CXSOBSYS-1,MXTRTQ   SYSTEM CODE                                  
         GOTO1 XHEXOUT,DMCB,SAOCOVS,CXSOBSYS,1,=C'TOG'                          
*                                                                               
CXSPGM   MVI   CXSOBPGM-1,MXTRTQ   PROGRAM CODE                                 
         GOTO1 XHEXOUT,DMCB,SAOCPGM,CXSOBPGM,1,=C'TOG'                          
*                                                                               
         CLI   SAVEACTN,C' '       TEST SAVED ACTION CODE                       
         BE    CXSACT1                                                          
         MVI   CXSOBACT,C'A'       GOTO ADD PROCESS                             
         B     CXSREC                                                           
CXSACT1  CLI   CXSOBACT,C'C'       IF CHANGE DO 'K' DELETE ALL                  
         BE    CXSACT2                                                          
         CLI   CXSOBACT,C'D'       IF DELETE DO 'K' DELETE ALL                  
         BE    CXSACT2                                                          
         B     CXSREC              OTHER ACTIONS GOTO ADD PROCESS               
*                                                                               
CXSACT2  MVC   SAVEACTN,CXSOBACT   SAVE THIS ACTION                             
         MVI   CXSOBACT,C'K'       OVERIDE WITH 'K' DELETE ALL                  
         MVI   CXSOBOPT-1,MXTRTQ   SET FIELD SEPS FOR NULL VALUES               
         MVI   CXSOBCOD-1,MXTRTQ                                                
         CLI   SAVEACTN,C'D'                                                    
         BNE   RETEXIT             IF NOT DELETE THEN RETURN FOR ADDS           
         B     DELEXIT             ELSE EXIT WITH JUST DELETE                   
*                                                                               
CXSREC   MVI   CXSOBOPT-1,MXTRTQ                                                
         MVI   CXSOBCOD-1,MXTRTQ                                                
*                                                                               
         USING SPATABD,R4                                                       
         ICM   R4,15,ASPATAB       GET ADDRESS SPATAB ENTRY                     
         BNZ   CXSOPT              FIRST TIME?                                  
         ICM   R4,15,=V(CXSSPAT)   R4=A(SYSTEM PROGRAM ACCESS TABLE)            
         BNZ   *+6                                                              
         DC    H'00'                                                            
*                                                                               
CXSPAT   CLI   SPATOVS,0           GET ADDRESS SPATAB ENTRY FOR                 
         BE    EOFEXIT                                                          
*        BNE   *+6                                                              
*        DC    H'00'                                                            
         CLC   SPATOVPG,SAOCOVPG                                                
         BE    *+12                                                             
         LA    R4,SPATLEN(R4)                                                   
         B     CXSPAT                                                           
         ST    R4,ASPATAB                                                       
         MVI   OPTNUM,0                                                         
         B     CXSO010                                                          
*                                                                               
CXSOPT   CLI   OPTNUM,X'FF'                                                     
         BE    EOFEXIT                                                          
         SR    R1,R1                                                            
         IC    R1,OPTNUM           R6=A(LAST FIELD NUMBER PROCESSED)            
         LA    R1,1(R1)                                                         
         STC   R1,OPTNUM                                                        
*                                                                               
CXSO010  SR    R0,R0                                                            
         IC    R0,OPTNUM                                                        
         GOTO1 TESTBIT,SPATOCL                                                  
         BZ    CXSOPT                                                           
*                                                                               
         GOTO1 XHEXOUT,DMCB,OPTNUM,CXSOBOPT,1,=C'TOG'                           
         LA    R5,SAOCDATA                                                      
CXSELM   CLI   0(R5),0             GET ELEMENT FROM RECORD                      
         BE    CXSOCN                                                           
         CLI   0(R5),SAOCTELQ      GET OCONTROL ELEMENT                         
         BE    CXSOCE                                                           
CXSNXT   SR    RF,RF               BUMP TO NEXT ELEMENT                         
         IC    RF,1(R5)                                                         
         CH    RF,=H'1'                                                         
         BNH   EXIT                EXIT IF FUNNY ELEMENT LENGTH                 
         AR    R5,RF                                                            
         B     CXSELM                                                           
*                                                                               
         USING SAOCTD,R5                                                        
CXSOCE   EQU   *                                                                
         SR    R0,R0                                                            
         IC    R0,OPTNUM                                                        
         XC    BITTABLX,BITTABLX   SET UP BIT TABLE                             
         CLI   SAOCTLN,SAOCTLNQ                                                 
         BE    CXSOCEA                                                          
         SR    RE,RE                                                            
         IC    RE,SAOCTLN          R2=L(BIT TABLE)-1                            
         SH    RE,=Y(SAOCTLNQ+1)                                                
         EX    RE,*+8                                                           
         B     *+10                                                             
         MVC   BITTABLX(0),SAOCTTAB                                             
CXSOCEA  GOTO1 TESTBIT,BITTABLX                                                 
         BZ    CXSOCN                                                           
         MVI   CXSOBCOD,C'Y'       CONTROL CODE = YES                           
         B     RETEXIT                                                          
*                                                                               
CXSOCN   MVI   CXSOBCOD,C'N'       CONTROL CODE = NO                            
         B     RETEXIT                                                          
*                                                                               
RETEXIT  L     R1,APARM                                                         
         OI    8(R1),X'40'         SET RECORD RETURNED                          
         B     EXIT                                                             
*                                                                               
EOFEXIT  XC    ASPATAB,ASPATAB                                                  
         MVI   OPTNUM,0                                                         
         MVI   SAVEACTN,C' '                                                    
         L     R1,APARM                                                         
         OI    8(R1),X'88'         SET EOF RETURN                               
         B     EXIT                                                             
*                                                                               
DELEXIT  XC    ASPATAB,ASPATAB                                                  
         MVI   OPTNUM,0                                                         
         MVI   SAVEACTN,C' '                                                    
         L     R1,APARM                                                         
         OI    8(R1),0             SET EOF AND WRITE 'K' TYPE RECORD            
         B     EXIT                                                             
*                                                                               
DELREC   L     R1,APARM            DELETE THIS RECORD                           
         MVI   8(R1),X'80'                                                      
         B     EXIT                                                             
*                                                                               
EXIT     XMOD1 1                                                                
         EJECT                                                                  
***********************************************************************         
* NTRY: R1=A(BIT TABLE), R0=CODE                                      *         
***********************************************************************         
         SPACE 1                                                                
TESTBIT  LR    RF,R0               R1=A(BYTE OF BIT)                            
         SRL   RF,3                                                             
         AR    R1,RF                                                            
         SPACE 1                                                                
         LA    RF,X'07'            RF=MASK                                      
         NR    RF,R0                                                            
         IC    RF,MASKS(RF)                                                     
         SPACE 1                                                                
         EX    RF,TESTBITM         EX A TM                                      
         BR    RE                                                               
         SPACE 1                                                                
TESTBITM TM    0(R1),0             A TM                                         
         EJECT                                                                  
         LTORG                                                                  
         SPACE 1                                                                
       ++INCLUDE MXTRT                                                          
       ++INCLUDE CXSRECID                                                       
         SPACE 1                                                                
         DS    0F                                                               
ASPATAB  DC    A(0)                                                             
OPTNUM   DC    AL1(0)                                                           
XHEXOUT  DC    V(HEXOUT)                                                        
         SPACE 1                                                                
MASKS    DC    X'8040201008040201'                                              
SAVEACTN DC    CL1' '                                                           
         SPACE 1                                                                
         EJECT                                                                  
* INCLUDE CXTRACT LOCAL DSECTS                                                  
         SPACE 1                                                                
       ++INCLUDE CXTRACTD                                                       
         EJECT                                                                  
         SPACE 2                                                                
WORKD    DSECT                                                                  
PARM     DS    0XL16               PARAMETER LIST                               
DMCB     DS    6F                  SUBROUTINE PARAMETER LIST                    
AXREC    DS    A                   A(EXTRACT RECORD)                            
CFREC    DS    A                   A(CONFILE RECORD)                            
         DS    A                                                                
         DS    A                                                                
*                                                                               
DUB      DS    D                                                                
APARM    DS    A                   A(PARAMETER LIST)                            
RELO     DS    A                                                                
*                                                                               
BITTABLE DS    0XL8                8 BYTE BIT TABLE                             
BITTABLX DS    XL32                32 BYTE BIT TABLE                            
*                                                                               
FACTBITT DS    XL8                 BIT TABLE RETURNED BY FINDACTS               
*                                                                               
WORKX    EQU   *                                                                
         EJECT                                                                  
       ++INCLUDE CXSOBD                                                         
         EJECT                                                                  
       ++INCLUDE SEACSFILE                                                      
         SPACE 1                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'023CXSOBC    05/21/98'                                      
         END                                                                    
