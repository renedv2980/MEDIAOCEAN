*          DATA SET ACAPEMAN   AT LEVEL 004 AS OF 09/04/07                      
*CATALP APEMAN                                                                  
APEMAN   TITLE '- APPROVER ELEMENT MAINTENANCE'                                 
*                                                                               
***********************************************************************         
* MAINTAINS APPEL APPROVER ELEMENTS ON BRANDOCEAN INVOICE LOG RECORDS           
*                                                                               
* PARMS                                                                         
* -----                                                                         
* NTRY - P1: BYTE 0: C'A' ADD AN APPROVER ELEMENT                               
*                    C'R' SET REJECT STATUS ON AN APPROVER ELEMENT              
*                    C'P' SET APPROVAL STATUS ON AN APPROVER ELEMENT            
*                    C'F' FILTER INVOICE AGAINST APPRVR/EDITOR (+ P2)           
*            BYTE 1-3: A(INVOICE RECORD)                                        
*      - P2: BYTE 0: C'A' FILTER INVOICES ON APPROVER                           
*                    C'E' FILTER INVOICES ON EDITOR                             
*            BYTE 1-3: A(APPROVER/EDITOR RECORD), ALL BUT ACTION A              
*      - P3: BYTE 0: C'A' ALL APPROVERS AT ALL LEVELS, ACTION F                 
*                    C'Q' QUALIFYING APPROVERS ONLY, ACTION F                   
*            BYTE 1-3: A(BLOCK) TO RETURN APPROVER PIDS, ACTION A/F             
*      - P4: BYTE 1-3: A(COMFACS)                                               
*                                                                               
* EXIT - CC: EQU: SUCCESS (ACTION A/R/P), RECORD QUALIFIES (ACTION F)           
*            NEQ: THE OPPOSITE                                                  
*      - P2: BYTE 0: ZERO/NON-ZERO RETURN STATUS (SEE 'RETSTS' BELOW)           
*                                                                               
***********************************************************************         
* LEVEL CHANGE COMMENTS                                                         
*----------------------                                                         
*JFOS 001 04MAY07 <DU01-5768> FIRST VERSION                                     
*JFOS 002 24MAY07 MINOR FIXES                                                   
*JFOS 003 06JUN07 FILT: RETURN APPROVERS EVEN IF REJ'D/ACTION REQ'D STS         
*JFOS 004 24JUL07 <LO01-6397> SUPPORT OFFICE-LEVEL APPROVERS                    
                                                                                
APEMAN   CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 WORKL,**APEM**,RA,CLEAR=YES,RR=RE                                
         USING WORKD,RC            RC=A(LOCAL W/S)                              
         ST    RE,RELO                                                          
         ST    R1,APARM            SAVE A(CALLER'S PARMS)                       
         MVC   AACTION,0(R1)       SAVE ACTION                                  
         CLI   4(R1),0             TEST FILTER A/Q SET                          
         BZ    INIT00                                                           
         MVI   AFLTSTS,EDITORQ                                                  
         CLI   4(R1),C'E'          TEST 'EDITOR' REQUIRED                       
         BE    *+10                                                             
         MVI   AFLTSTS,ANYAPPR     ELSE SET ANY 'APPROVER'                      
*                                  * PARM 1 *                                   
INIT00   SR    RF,RF                                                            
         ICM   RF,7,1(R1)                                                       
         BNZ   *+6                                                              
         DC    H'0'                A(INVOICE RECORD) MISSING                    
         ST    RF,AREC                                                          
*                                  * PARM 2 *                                   
         CLI   AACTION,ADD         TEST ACTION ADD                              
         BE    INIT02                                                           
         SR    RF,RF                                                            
         ICM   RF,7,5(R1)                                                       
         BNZ   *+6                                                              
         DC    H'0'                A(APPROVER RECORD) MISSING                   
         ST    RF,AAPPR                                                         
*                                  * PARM 4 *                                   
INIT02   SR    RF,RF                                                            
         ICM   RF,15,12(R1)                                                     
         BNZ   *+6                                                              
         DC    H'0'                A(COMFACS) MISSING                           
         L     RE,CDATAMGR-COMFACSD(RF)                                         
         ST    RE,VDATAMGR                                                      
         L     RE,CCALLOV-COMFACSD(RF)                                          
         ST    RE,VCALLOV                                                       
*                                  * PARM 3 *                                   
         XC    APIDBLK,APIDBLK     PID RETURN BLOCK                             
         CLI   8(R1),0             TEST QUALIFYING FILTER SET                   
         BE    *+10                                                             
         MVC   AFLTQUA,8(R1)       SAVE IT                                      
         SR    RE,RE                                                            
         ICM   RE,7,9(R1)                                                       
         BZ    INIT04              UNDEFINED - EMAILS NOT IN USE                
         ST    RE,APIDBLK                                                       
         LA    RF,APVTABN*PIDBLKL                                               
         SR    R0,R0                                                            
         SR    R1,R1                                                            
         MVCL  RE,R0               CLEAR THE BLOCK                              
                                                                                
*                                  SAVE CALLER'S I/O SEQUENCE                   
INIT04   GOTO1 VDATAMGR,DMCB,DMKEY,ACCDIR,KIOKEY,KIOKEY                         
                                                                                
         GOTO1 VCALLOV,DMCB,0,X'E3000A12',0                                     
         L     RF,0(R1)                                                         
         ST    RF,VXSORT                                                        
*                                  HANDLE THE ACTION:                           
INIT06   CLI   AACTION,ADD         ADD                                          
         BNE   INIT08                                                           
         BAS   RE,ADDE                                                          
         B     GOX                                                              
                                                                                
INIT08   CLI   AACTION,APPROVE     APPROVE                                      
         BNE   INIT10                                                           
         BAS   RE,APRV                                                          
         B     GOX                                                              
                                                                                
INIT10   CLI   AACTION,REJECT      REJECT                                       
         BNE   INIT12                                                           
         BAS   RE,APRV                                                          
***      BAS   RE,REJE             USE SAME ROUTINE AS APPROVE                  
         B     GOX                                                              
                                                                                
INIT12   CLI   AACTION,FILTER      FILTER                                       
         BNE   INIT14                                                           
         BAS   RE,FILT                                                          
         B     GOX                                                              
                                                                                
INIT14   DC    H'0'                UNKNOWN ACTION                               
                                                                                
GOX      GOTO1 VDATAMGR,DMCB,DMKEY,ACCDIR,IOKEY,IOKEY                           
         CLC   KIOKEY,IOKEY        TEST CALLER'S I/O SEQUENCE BROKEN            
         BE    GOXX                                                             
*                                  YES - RESTORE IT                             
         GOTO1 VDATAMGR,DMCB,(X'08',DMREAD),ACCDIR,KIOKEY,KIOKEY                
                                                                                
GOXX     L     R1,APARM                                                         
         MVC   4(1,R1),RETSTS      SET RETURN STATUS FOR CALLER                 
         CLI   RETSTS,RETSOK       AND CC                                       
                                                                                
EXIT     XIT1  ,                                                                
*                                                                               
***********************************************************************         
* LOOK UP APLIMITS AND APPROVERS. RETURN LIST OF DEFAULT APPROVER PIDS          
* IN PIDBLK. ADD AN APPEL TO CALLER'S INVREC                                    
***********************************************************************         
                                                                                
ADDE     NTR1  ,                                                                
         BAS   RE,INVDATA          EXTRACT APPROVER DATA FROM INVREC            
                                                                                
         BAS   RE,APLIMLU          LOOK UP APLIMIT RECORD                       
         BNE   ADDEX               ERROR - EXIT WITH RETSTS SET                 
                                                                                
* TEST ENOUGH APPROVERS AT EACH LVL, RETURN APPROVER PIDS                       
* FOR EACH LVL - CALLER FIGURES OUT SEQUENTIAL/CONCURRENT?                      
* IF NOT ENOUGH, EXIT WITH 'NOT ENOUGH APPROVERS' ERROR                         
*                                                                               
T        USING APLVAL,R4                                                        
         LA    R4,WORK             R4 =A(APPROVER N'/VALUE TABLE)               
         USING APPPASD,R2                                                       
ADDE24   LA    R2,IOKEY            READ APPROVER RECS VIA PASSIVES              
         XC    APPPAS,APPPAS                                                    
         MVI   APPPTYP,APPPTYPQ                                                 
         MVI   APPPSUB,APPPSUBQ                                                 
         MVC   APPPCPY,INCPY                                                    
         MVI   APPPCAT,0           NULL/GENERIC                                 
         ZAP   APPPVAL,T.APLVAL    APPROVAL VALUE FROM TABLE                    
         MVC   APPPOFFC,INOFF      OFFICE CODE OR FF'S                          
         MVC   SIOKEY(L'APPPAS),APPPAS                                          
         GOTO1 VDATAMGR,DMCB,DMRDHI,ACCDIR,APPPAS,APPPAS                        
         BNE   ADDEXNEA            NO APPROVERS?                                
         B     ADDE28                                                           
ADDE26   LA    R2,IOKEY                                                         
         MVC   APPPAS,SIOKEY       RESTORE KEY                                  
         GOTO1 VDATAMGR,DMCB,DMREAD,ACCDIR,APPPAS,APPPAS                        
         GOTO1 VDATAMGR,DMCB,DMRSEQ,ACCDIR,APPPAS,APPPAS                        
ADDE28   CLC   SIOKEY(APPPOFFC-APPPAS),APPPAS                                   
         BNE   ADDE34                                                           
         MVC   SIOKEY(L'APPPAS),APPPAS                                          
         TM    APPPSTA2,APPSINYQ   TEST INVOICE APPROVER (NOT EDITOR)           
         BZ    ADDE26              NO - NEXT                                    
         CP    APPPVAL,PZERO       TEST APPROVAL LIMIT = 0                      
         BZ    ADDE26              JUST IN CASE (SHOULD NEVER HAPPEN)           
         CLC   APPPOFFC,INOFF      TEST OFFICE MATCHES                          
         BE    ADDE30              OK                                           
         CLC   APPPOFFC,FFS        ELSE TEST COMPANY LEVEL APPROVER             
         BNE   ADDE26                                                           
ADDE30   GOTO1 VDATAMGR,DMCB,DMGETR,ACCMST,APPPDA,LIO,LIOWK                     
         BE    *+6                                                              
         DC    H'0'                                                             
         USING APPRECD,R2                                                       
         LA    R2,LIO                                                           
         BAS   RE,TSTAPRV          TEST VALID APPROVER FOR THIS INV             
         BNE   ADDE26              GET NEXT APPROVER                            
*                                                                               
         OC    APIDBLK,APIDBLK     TEST APPROVER PIDS REQUIRED                  
         BZ    ADDE32              NO                                           
*                                  TEST CONCURRENT APPROVAL                     
*        BE    *+12                YES - RETURN ALL APPROVER PIDS               
*                                  NO: TEST 1ST LEVEL APPROVALS                 
*        BNE   ADDE32              DON'T RETURN HIGHER-LVL PIDS                 
                                                                                
         LH    RE,APVTCNT          MAINTAIN APPROVERS INTERNAL TABLE            
         LA    R1,APVTABL                                                       
         MR    R0,RE                                                            
         USING APVTABD,RE                                                       
         LA    RE,APVTAB                                                        
         AR    RE,R1               DISPLACE TO NEXT FREE ENTRY                  
         XC    APVTYP(APVTABL),APVTYP                                           
         MVC   APVTYP,APTYPE                                                    
         NI    APVTYP,X'FF'-DEFAPPQ  STRIP OFF POSSIBLE DEFAULT IND             
         MVC   APVCODE,TEMP        RETURNED BY TSTAPRV                          
         MVI   APVDEF,APVDEFY                                                   
         TM    APTYPE,DEFAPPQ      TEST DEFAULT APPROVER                        
         BNZ   *+8                                                              
         MVI   APVDEF,APVDEFN      NO - FORCE HIGH IN SORT                      
         MVC   APVPID,APPKPIDB                                                  
         DROP  RE                                                               
         LH    R0,APVTCNT          BUMP THE COUNT                               
         AHI   R0,1                                                             
         STH   R0,APVTCNT                                                       
*                                                                               
ADDE32   SR    RF,RF                                                            
         ICM   RF,1,T.APLPREV      DECREMENT N'APPROVERS FOUND                  
         BZ    *+12                IF ALREADY 0, SKIP                           
         AHI   RF,-1                                                            
         STC   RF,T.APLPREV                                                     
         B     ADDE26              GET NEXT APPROVER FOR THIS LIMIT             
*                                                                               
ADDE34   CLI   T.APLPREV,0         TEST ENOUGH APPRVERS FOUND, THIS LVL         
         BH    ADDEXNEA            NOT ENOUGH APPROVERS                         
                                                                                
         LA    R4,APTABNTQ(R4)     BUMP TO NEXT APPROVAL LIMIT VALUE            
         LA    R0,WORK+L'WORK-1                                                 
         CR    R4,R0                                                            
         BNL   *+12                FINISHED                                     
         OC    T.APLVAL,T.APLVAL                                                
         BNZ   ADDE24              READ FOR FIRST APPROVER, NEXT LIMIT          
*                                  NO MORE APPROVAL LIMITS                      
                                                                                
         OC    APIDBLK,APIDBLK     TEST APPROVER PIDS REQUIRED                  
         BZ    ADDE40              NO                                           
         LH    R0,APVTCNT          SORT THE APPROVERS TABLE                     
         LA    RF,APVTABL                                                       
                                                                                
         GOTO1 VXSORT,DMCB,(0,APVTAB),(R0),(RF),(RF),0                          
                                                                                
         USING APVTABD,RF                                                       
         LA    RF,APVTAB                                                        
         LH    R0,APVTCNT                                                       
         USING PIDBLKD,RE                                                       
         L     RE,APIDBLK                                                       
         LA    R1,ADDE38                                                        
         CLI   APVDEF,APVDEFY      TEST 1ST ENTRY IS A DEFAULT APPROVER         
         BNE   ADDE38              THERE ARE NONE, SO RETURN ALL OTHERS         
         LA    R1,ADDE36           ELSE RETURN DEFAULT APPROVERS ONLY           
ADDE36   CLI   APVDEF,APVDEFY      TEST DEFAULT APPROVER                        
         BNE   ADDE40              NO - STOP NOW                                
ADDE38   MVC   PIDBIN,APVPID       SET APPROVER PID FROM TABLE                  
         MVC   PIDIND,APVTYP       SET APPROVER TYPE FROM TABLE                 
         AHI   RE,PIDBLKL                                                       
         AHI   RF,APVTABL                                                       
         BCTR  R0,R1               GET NEXT                                     
                                                                                
         DROP  R2                                                               
                                                                                
         USING REBRECD,R2                                                       
ADDE40   L     R2,AREC             CALLER'S INVOICE RECORD                      
         USING APPELD,R3                                                        
         LA    R3,REBRFST                                                       
         SR    R0,R0                                                            
ADDE42   CLI   APPEL,0             BUMP TO EOR/CHECK FOR EXISTNG APPEL          
         BE    ADDE44                                                           
         CLI   APPEL,APPELQ                                                     
         BE    ADDEXAAE            ALREADY GOT ONE - RETURN ERROR               
         IC    R0,APPLN                                                         
         AR    R3,R0                                                            
         B     ADDE42                                                           
                                                                                
ADDE44   XC    APPEL(APPLNSQ),APPEL ADD NEW ELEMENT                             
         MVI   APPEL,APPELQ                                                     
         MVC   APPALIC,APLCAT      SET CAT/SUBCAT FROM APLIMIT USED             
         MVC   APPALIS,APLSCAT     *NB. THESE WILL BE 00'S UFN                  
         SR    R0,R0                                                            
         LA    R4,WORK                                                          
         LA    RF,APPNTRY                                                       
         USING APPNTRY,RF                                                       
ADDE46   XC    APPNTRY(APPLNNQ),APPNTRY                                         
         ZAP   APPALIM,T.APLVAL                                                 
         OI    APPSTA1,APPS1WQ     1ST APPROVER STS 'AWAITS APPROVAL'           
         CLI   T.APLNUM,1          TEST > 1 APPROVER REQUIRED                   
         BH    *+12                                                             
         OI    APPSTA1,APPS2NQ     NO - 2ND APPROVER STS 'NOT REQ'D'            
         B     *+8                                                              
         OI    APPSTA1,APPS2WQ     2ND APPROVER STS 'AWAITS APPROVAL'           
         AHI   RF,APPLNNQ                                                       
         AHI   R4,APTABNTQ                                                      
         AHI   R0,1                                                             
         OC    T.APLVAL,T.APLVAL   TEST ANOTHER APPROVER LIMIT                  
         BNZ   ADDE46                                                           
                                                                                
         STC   R0,APPNTRS          N'ENTRIES                                    
         MVI   0(RF),0             SET NEW EOR                                  
         SR    RF,R3               CALC L'ELEMENT                               
         STC   RF,APPLN                                                         
         LH    R0,REBRLEN          SET NEW RECLEN                               
         AR    R0,RF                                                            
         STH   R0,REBRLEN                                                       
         B     ADDEX                                                            
                                                                                
ADDEXNEA OI    RETSTS,RETSNEA      NOT ENOUGH APPROVERS FOUND                   
         B     ADDEX                                                            
                                                                                
ADDEXAAE OI    RETSTS,RETSAAE      APPEL ALREADY EXISTS                         
                                                                                
ADDEX    B     EXIT                                                             
         DROP  R2,R3,RF                                                         
                                                                                
APTABNTQ EQU   L'APLVAL+L'APLNUM+L'APLPREV   L'WORK APLIMIT TABLE ENTRY         
         EJECT                                                                  
***********************************************************************         
* ADD AN APPROVER PID TO APPROVAL ELEMENT                             *         
*                                                                     *         
* NB. AACTION = APPROVE *OR* REJECT, APPROVERS ONLY                   *         
***********************************************************************         
                                                                                
APRV     NTR1  ,                                                                
                                                                                
         BAS   RE,INVDATA          EXTRACT APPROVER DATA FROM INVREC            
                                                                                
         TM    INREBSTA,REBSBSUB   SUBMITTED ONLY                               
         BZ    APRVX               ELSE NOT VALID FOR APPROVAL/REJECT'N         
         USING APPELD,R4                                                        
         ICM   R4,15,AAPPEL                                                     
         BZ    APRVXNOA            ERROR - NO APPEL ON INVOICE?                 
                                                                                
         USING APPRECD,R2                                                       
         L     R2,AAPPR            CALLER'S APPROVER RECORD                     
         BAS   RE,TSTAPRV                                                       
         BNE   APRVXNVA            NOT A VALID APPROVER FOR INVOICE             
         TM    APTYPE,EDITORQ      TEST EDITOR OR APPROVER                      
         BNZ   APRVXNVA            EDITOR - NOT VALID                           
         USING APLELD,R3                                                        
         ICM   R3,15,AAPLEL                                                     
         BZ    APRVXNVA            NO APPROVAL LIMIT EL ON APPROVER?            
*                                  PROCESS INVREC APPEL:                        
         SR    R0,R0                                                            
         IC    R0,APPNTRS          N'APPNTRYS                                   
         LA    RF,APPNTRY                                                       
         USING APPNTRY,RF                                                       
APRV02   TM    APPSTA1,APPS1WQ     ONLY WANT 'AWAITS APPROVAL' STATUS           
         BNZ   *+12                                                             
         TM    APPSTA1,APPS2WQ                                                  
         BZ    *+14                                                             
         CP    APPALIM,APLVAL      MATCH AGAINST APPROVER'S LIMIT               
         BE    APRV04              OK                                           
         AHI   RF,APPLNNQ                                                       
         BCT   R0,APRV02           TRY NEXT ENTRY                               
         B     APRVXCAP            NOTHING TO APPROVE/NO MATCH ON APLIM         
                                                                                
APRV04   CLC   APPPID1,APPKPIDB    TEST ALREADY APPROVED BY THIS PID            
         BE    APRVXCAP            CAN'T APPROVE IT TWICE                       
         TM    APPSTA1,APPS1WQ     TEST APPRVR PID TO GO IN SLOT 1 OR 2         
         BZ    APRV06              MUST BE 2                                    
         MVC   APPPID1,APPKPIDB                                                 
         CLI   AACTION,APPROVE     TEST APPROVE/REJECT ACTION                   
         BNE   *+12                                                             
         XI    APPSTA1,APPS1WQ+APPS1AQ  STS 'AWAITS'=OFF/'APPROVED'=ON          
         B     APRV08                                                           
         XI    APPSTA1,APPS1WQ+APPS1RQ  STS 'AWAITS'=OFF/'REJECTED'=ON          
         B     APRV11                                                           
APRV06   MVC   APPPID2,APPKPIDB                                                 
         CLI   AACTION,APPROVE     TEST APPROVE/REJECT ACTION                   
         BNE   *+12                                                             
         XI    APPSTA1,APPS2WQ+APPS2AQ  STS 'AWAITS'=OFF/'APPROVED'=ON          
         B     APRV08                                                           
         XI    APPSTA1,APPS2WQ+APPS2RQ  STS 'AWAITS'=OFF/'REJECTED'=ON          
         B     APRV11                                                           
*                                  TEST INVOICE IS NOW FULLY-APPROVED           
         DROP  R2,RF                                                            
APRV08   IC    R0,APPNTRS          N'APPNTRYS                                   
         LA    RF,APPNTRY                                                       
         USING APPNTRY,RF                                                       
APRV10   TM    APPSTA1,APPS1WQ     'AWAITS APPROVAL' SLOT 1                     
         BNZ   APRV12                                                           
         TM    APPSTA1,APPS2WQ     OR SLOT 2                                    
         BNZ   APRV12                                                           
         AHI   RF,APPLNNQ                                                       
         BCT   R0,APRV10           DO FOR N'APPNTRYS                            
         USING REBRECD,R2          NO APPROVALS OUTSTANDING...                  
APRV11   L     R2,AREC             ...SO UPDATE RECORD STATUS                   
         CLI   AACTION,REJECT      TEST ACTION IS REJECT                        
         BNE   *+12                                                             
         XI    REBRSTA2,REBSBSUB+REBSBREJ 'SUBMITTED' OFF/'REJECTED' ON         
         B     APRV12                                                           
         XI    REBRSTA2,REBSBSUB+REBSBAPP 'SUBMITTED' OFF/'APPROVED' ON         
APRV12   OI    RETSTS,RETSOK                                                    
         B     APRVX                                                            
                                                                                
APRVXNVA OI    RETSTS,RETSNVA      NOT A VALID APPROVER                         
         B     APRVX                                                            
                                                                                
APRVXCAP OI    RETSTS,RETSCAP      APPRVR CAN'T APPROVE/REJECT THIS INV         
         B     APRVX                                                            
                                                                                
APRVXNOA OI    RETSTS,RETSNOA      NO APPEL ON INVOICE                          
         B     APRVX                                                            
                                                                                
APRVX    B     EXIT                                                             
         DROP  R2,R4,RF                                                         
         EJECT                                                                  
***********************************************************************         
* SET REJECTED STATUS AND PID ON APPROVAL ELEMENT                     *         
***********************************************************************         
**** NOT USED - HANDLED IN APRV ROUTINE ****                                    
REJE     NTR1  ,                                                                
                                                                                
REJEX    B     EXIT                                                             
         EJECT                                                                  
***********************************************************************         
* FILTER AN INVOICE RECORD AGAINST SPECIFIED APPROVER/EDITOR.         *         
* OPTIONALLY, ALSO RETURN PID LIST OF ALL VALID APPROVERS FOR INVREC  *         
* IN WHICH CASE, A BRANCH TO 'FILTNO' DOES NOT EXIT ROUTINE.          *         
***********************************************************************         
                                                                                
FILT     NTR1  ,                                                                
         MVI   APTYPE,0                                                         
         BAS   RE,INVDATA          EXTRACT APPROVAL DATA FROM INVREC            
         USING APPRECD,R2                                                       
         L     R2,AAPPR            CALLER'S APPROVER RECORD                     
         TM    INREBSTA,REBSBARQ+REBSBREJ TEST INV STS: ACT REQ OR REJ          
         BZ    FILT02                                                           
         TM    APPRSTA2,APPSINEQ   YES - TEST APPREC HAS EDITOR STATUS          
         BZ    FILTEX              NO - EXIT NOW                                
**       CLI   AFLTSTS,EDITORQ     AND USER WANTS EDITOR                        
**       BNE   FILTEX              NO - EXIT NOW                                
FILT02   BAS   RE,TSTAPRV                                                       
         BNE   FILTNO              THIS APPROVER/EDITOR INVALID FOR INV         
         SR    RF,RF                                                            
         IC    RF,APTYPE           EDIT/APPROVAL TYPE SET BY TSTAPRV            
         TM    AFLTSTS,0           COMPARE WITH REQUESTED TYPE                  
         EX    RF,*-4                                                           
         BZ    FILTNO              DIFFERS - INVALID                            
         TM    APTYPE,EDITORQ      TEST EDITOR                                  
         BNZ   FILT08              YES - QUALIFIES                              
*                                  FOR APPROVERS, CHECK BASIS/LIMITS            
         SR    R4,R4                                                            
         ICM   R4,15,AAPPEL                                                     
         BZ    FILTNO              NO APPEL - SHOULDN'T BE HERE?                
         USING APPELD,R4                                                        
*        CLI   APPALIC,ALIKINV     MATCH APPROVER CAT/SUBCAT                    
*        BNE   FILTNO                                                           
*        CLC   APPALIS,ALIKSCAT    ?SUBCAT DOESN'T EXIST ON APPREC              
*        BNE   FILTNO                                                           
         ZAP   DUB,PZERO                                                        
         ICM   RE,15,AAPLEL        APPROVER'S APLIMIT IF SET                    
         BZ    *+10                                                             
         ZAP   DUB,APLVAL-APLELD(L'APLVAL,RE)                                   
         CP    DUB,PZERO                                                        
         BZ    FILT08              NOT FOUND/ZERO - SKIP APPEL TESTS            
         SR    R0,R0                                                            
         IC    R0,APPNTRS          N'APPEL LEVEL ENTRIES                        
         LA    R4,APPNTRY                                                       
         USING APPNTRY,R4                                                       
FILT04   CP    APPALIM,DUB                                                      
         BH    FILTNO              FINISHED WITH THIS APPROVER RECORD           
****     ALLOW HIGHER-LIMIT APPROVERS BY REMOVING NEXT INSTRUCTION              
         BL    FILT06                                                           
****     BUT MAY NEED A BIT MORE CODE TO HANDLE THEM                            
         TM    APPSTA1,APPS1NQ     TEST 1ST APPR NOT REQ'D (UNLIKELY)           
         BZ    FILT08                                                           
         TM    APPSTA1,APPS1AQ+APPS1RQ  TEST ALREADY APPROVED/REJECTED          
         BZ    *+14                                                             
         CLC   APPPID1,APPKPIDB    ... BY THIS APPROVER                         
         BE    FILTNO              YES - DISQUALIFIED                           
         TM    APPSTA1,APPS1WQ     TEST AWAITING APPROVAL                       
         BNZ   FILT08              QUALIFIES                                    
         TM    APPSTA1,APPS2NQ     TEST 2ND APPROVER NOT REQUIRED               
         BNZ   FILT06                                                           
         TM    APPSTA1,APPS2AQ+APPS2RQ  TEST ALREADY APPROVED/REJECTED          
         BZ    *+14                                                             
         CLC   APPPID2,APPKPIDB    ... BY THIS APPROVER                         
         BE    FILTNO              DISQUALIFIED                                 
         TM    APPSTA1,APPS2WQ     TEST AWAITING APPROVAL                       
         BNZ   FILT08              QUALIFIES                                    
FILT06   AHI   R4,APPLNNQ          BUMP TO NEXT APPEL LEVEL                     
         BCT   R0,FILT04                                                        
         DROP  R4                                                               
FILTNO   MVI   APTYPE,0            CALLER'S APPRVER REC DOESN'T QUALIFY         
*                                                                               
FILT08   OC    APIDBLK,APIDBLK     TEST ALL APPROVRS/EDITORS WANTED TOO         
         BZ    FILTEX              NO - FINISHED                                
         ZAP   DUB,PZERO                                                        
         SR    R0,R0                                                            
         CLI   AFLTSTS,EDITORQ     TEST LOOKING FOR EDITORS                     
         BE    FILT11              YES                                          
         USING APPELD,R3                                                        
         ICM   R3,15,AAPPEL        TEST/SET R3=A(APPEL)                         
         BZ    FILTEX              IF ACTN REQ'D, MAY NOT EXIST                 
         IC    R0,APPNTRS          N'APPEL LEVEL ENTRIES                        
         LA    R4,APPNTRY                                                       
         USING APPNTRY,R4                                                       
FILT10   TM    APPSTA1,APPS1NQ     TEST 1ST APPR NOT REQ'D (UNLIKELY)           
         BNZ   FILT18              NEXT APPNTRY                                 
         CLI   AFLTQUA,ALLQ        TEST WANT ALL (FOR APPROVER TABLE)           
         BE    FILT10A             YES - IGNORE APPNTRY STATUS                  
         TM    APPSTA1,APPS1WQ     TEST AWAITING APPROVAL                       
         BNZ   *+8                                                              
         TM    APPSTA1,APPS2WQ                                                  
         BZ    FILT18              NEXT APPNTRY                                 
FILT10A  ZAP   DUB,APPALIM                                                      
         USING APPPASD,R2                                                       
FILT11   LA    R2,IOKEY                                                         
         XC    APPPAS,APPPAS                                                    
         MVI   APPPTYP,APPPTYPQ                                                 
         MVI   APPPSUB,APPPSUBQ                                                 
         MVC   APPPCPY,INCPY                                                    
         MVI   APPPCAT,0           NULL/GENERIC                                 
         ZAP   APPPVAL,DUB         APPALIM(=APPROVER), ZERO(=EDITOR)            
         MVC   APPPOFFC,INOFF      OFFICE CODE OR FF'S                          
         MVC   SIOKEY(L'APPPAS),APPPAS                                          
         GOTO1 VDATAMGR,DMCB,DMRDHI,ACCDIR,APPPAS,APPPAS                        
         BE    FILT14                                                           
         DC    H'0'                                                             
FILT12   LA    R2,IOKEY                                                         
         MVC   APPPAS,SIOKEY       RESTORE KEY                                  
         GOTO1 VDATAMGR,DMCB,DMREAD,ACCDIR,APPPAS,APPPAS                        
         GOTO1 VDATAMGR,DMCB,DMRSEQ,ACCDIR,APPPAS,APPPAS                        
FILT14   CLC   SIOKEY(APPPOFFC-APPPAS),APPPAS   COMPARE DOWN TO OFFICE          
         BNE   FILT18              NO MORE MATCHES                              
         MVC   SIOKEY(L'APPPAS),APPPAS                                          
         CLC   APPPOFFC,INOFF      TEST OFFICE MATCHES                          
         BE    *+14                OK                                           
         CLC   APPPOFFC,FFS        ELSE TEST COMPANY-LEVEL APPROVER             
         BNE   FILT12              NO - TRY AGAIN                               
         GOTO1 VDATAMGR,DMCB,DMGETR,ACCMST,APPPDA,LIO,LIOWK                     
         BE    *+6                                                              
         DC    H'0'                                                             
         USING APPRECD,R2                                                       
         LA    R2,LIO                                                           
         BAS   RE,TSTAPRV          TEST APPROVER QUALIFIES                      
         BNE   FILT12                                                           
*                                  PRESERVE RF: DEFAULT IND AT 0(RF)            
         L     RE,APIDBLK                                                       
         USING PIDBLKD,RE                                                       
         LA    R1,PIDBLKN          FIND NEXT FREE SLOT                          
         OC    0(3,RE),0(RE)                                                    
         BZ    FILT16                                                           
         AHI   RE,PIDBLKL                                                       
         BCT   R1,*-14                                                          
         DC    H'0'                MORE THAN 680 APPROVERS...?                  
                                                                                
FILT16   SR    R1,R1                                                            
         IC    R1,APPNTRS                                                       
         AHI   R1,1                                                             
         SR    R1,R0               R1=APLIMIT LEVEL 1-4                         
         SLL   R1,4                                                             
         STC   R1,PIDIND           LEVEL IN HI NIBBLE                           
         LA    R1,5                N'POSSIBLE APPROVER TYPES                    
         LA    RF,B'00010000'      CONVERT BIT SETTINGS -> 1-5                  
         TM    APTYPE,0                                                         
         EX    RF,*-4                                                           
         BNZ   *+12                                                             
         SRL   RF,1                                                             
         BCT   R1,*-12                                                          
         MVI   BYTE1,0                                                          
         STC   R1,BYTE1                                                         
         OC    PIDIND,BYTE1        APPROVER TYPE 1-5 IN LO NIBBLE               
         TM    APTYPE,DEFAPPQ                                                   
         BZ    *+8                                                              
         OI    PIDIND,DEFAPPQ      DEFAULT IND IN HI NIBBLE                     
         MVC   PIDBIN,APPKPIDB                                                  
         B     FILT12              LOOK FOR ANOTHER APPROVER                    
                                                                                
FILT18   CLI   AFLTSTS,EDITORQ     TES LOOKING FOR EDITORS                      
         BE    FILT20              YES - FINISHED                               
         AHI   R4,APPLNNQ          BUMP TO NEXT APPEL LEVEL                     
         BCT   R0,FILT10                                                        
                                                                                
FILT20   B     FILTEXX                                                          
                                                                                
FILTEX   CLI   APTYPE,0            TEST VALID APPROVER OR EDITOR                
         BNE   *+8                                                              
         OI    RETSTS,RETSNEA      NO - SET ERROR STS FOR CALLER                
FILTEXX  B     EXIT                                                             
         EJECT                                                                  
***********************************************************************         
* EXTRACT DATA FROM CALLER'S INVOICE RECORD FOR APPROVAL LOOK-UP      *         
***********************************************************************         
                                                                                
INVDATA  NTR1  ,                                                                
         USING REBRECD,R2                                                       
         L     R2,AREC             R2=A(CALLER'S INVOICE RECORD)                
         MVC   INCPY,REBKCPY                                                    
         MVC   INREBSTA,REBRSTA2                                                
         USING RBDELD,R4                                                        
         LA    R4,REBRFST                                                       
         MVC   INCLI,SPACES        LOOK FOR CLIENT                              
         MVC   INPRO,SPACES                 PRODUCT                             
         MVC   INJOB,SPACES                 JOB                                 
         MVC   INSUP,SPACES                 SUPPLIER                            
         MVC   INETY,SPACES                 EXPENDITURE TYPE                    
         MVC   INDPT,SPACES                 DEPARTMENT                          
         ZAP   INTOT,PZERO                  AMOUNT                              
         MVC   INOFF,FFS           INIT OFFICE TO FF'S                          
         XC    AAPPEL,AAPPEL       A(APPEL)                                     
         SR    RF,RF                                                            
INVD02   CLI   RBDEL,0                                                          
         BE    INVD10                                                           
         CLI   RBDEL,APPELQ                                                     
         BNE   *+8                                                              
         ST    R4,AAPPEL                                                        
         CLI   RBDEL,RBDELQ                                                     
         BE    *+14                                                             
INVD04   IC    RF,RBDLN                                                         
         AR    R4,RF                                                            
         B     INVD02                                                           
                                                                                
         CLI   RBDTYP,RBDMAIN                                                   
         BNE   INVD06                                                           
         ZAP   INTOT,RBDMTAM       TOTAL IN AGENCY CURRENCY                     
         SRP   INTOT,64-2,5        ROUND TO $ FOR APLIMIT COMPARE               
         B     INVD04                                                           
                                                                                
INVD06   CLI   RBDTYP,RBDTSTA                                                   
         BNE   *+14                                                             
         MVC   AMTSTA1,RBDSTA1     SAVE STS FOR BILL/NON-BILL IND               
         B     INVD04                                                           
                                                                                
         LA    RE,INCLI                                                         
         CLI   RBDTYP,RBDTCLI                                                   
         BE    INVD08                                                           
         LA    RE,INPRO                                                         
         CLI   RBDTYP,RBDTPRO                                                   
         BE    INVD08                                                           
         LA    RE,INJOB                                                         
         CLI   RBDTYP,RBDTJOB                                                   
         BE    INVD08                                                           
         LA    RE,INSUP                                                         
         CLI   RBDTYP,RBDTSUP                                                   
         BE    INVD08                                                           
         LA    RE,INETY                                                         
         CLI   RBDTYP,RBDTETY                                                   
         BE    INVD08                                                           
         LA    RE,INDPT                                                         
         CLI   RBDTYP,RBDT2DA                                                   
         BE    INVD08                                                           
         LA    RE,INOFF                                                         
         CLI   RBDTYP,RBDTOFF                                                   
         BNE   INVD04                                                           
         MVC   INOFF,SPACES        RE-INIT TO SPACES                            
INVD08   IC    RF,RBDXLN                                                        
         EX    RF,*+8                                                           
         B     INVD04                                                           
         MVC   0(0,RE),RBDNTRY     SAVE THE CLIENT/SUPP/ETYP/DEPT/OFF           
                                                                                
INVD10   CLC   INCLI,SPACES        MERGE CLI/PRO/JOB CODE                       
         BNH   INVD12                                                           
         MVC   INCPJ(L'INCLI),INCLI                                             
         CLC   INPRO,SPACES                                                     
         BNH   INVD12                                                           
         LA    RF,INCPJ+L'INCLI-1                                               
         CLI   0(RF),C' '                                                       
         BH    *+12                                                             
         AHI   RF,-1                                                            
         B     *-12                                                             
         MVC   1(L'INPRO,RF),INPRO                                              
         CLC   INJOB,SPACES                                                     
         BNH   INVD12                                                           
         LA    RF,L'INPRO+1(RF)                                                 
         CLI   0(RF),C' '                                                       
         BH    *+12                                                             
         AHI   RF,-1                                                            
         B     *-12                                                             
         MVC   1(L'INJOB,RF),INJOB                                              
INVD12   B     EXIT                                                             
         DROP  R2                                                               
         EJECT                                                                  
**********************************************************************          
* LOOK UP APLIMIT RECORD USING APPROVER VALUES SET BY INVDATA ROUTINE*          
* BUILDS TABLE OF N'APPROVERS REQ'D + APPROVAL VALUES IN WORK        *          
* EXIT CC: EQU - OK, CC NEQ - RETSTS HAS ERROR BIT VALUE             *          
**********************************************************************          
                                                                                
APLIMLU  NTR1  ,                                                                
         USING ALIRECD,R2                                                       
         LA    R2,IOKEY            LOOK UP APLIMIT RECORD                       
         XC    ALIKEY,ALIKEY                                                    
         MVI   ALIKTYP,ALIKTYPQ                                                 
         MVI   ALIKSUB,ALIKSUBQ                                                 
         MVC   ALIKCPY,INCPY                                                    
         MVI   ALIKCAT,ALIKINV     'INVOICES' CATEGORY                          
                                                                                
         LA    R0,3                                                             
*                                  SET SUBCAT SEQUENCE FOR LOOKUP:              
         TM    AMTSTA1,RBDSBIL     TEST BILLABLE INVOICE                        
         BZ    APLI02                                                           
         MVI   APLMSC1,C'P'        1.BILLABLE SUBCAT                            
         MVI   APLMSC2,C'C'        2.ALL-CLIENT SUBCAT                          
         MVI   APLMSC3,C' '        3.DEFAULT SUBCAT                             
         B     APLI06                                                           
APLI02   MVI   APLMSC1,C'E'        1.HOUSE SUBCAT                               
         CLC   INCPJ,SPACES        TEST CLIENT FOUND                            
         BNH   APLI04                                                           
         MVI   APLMSC2,C'C'        2.ALL-CLIENT SUBCAT                          
         MVI   APLMSC3,C' '        3.DEFAULT SUBCAT                             
         B     APLI06                                                           
APLI04   MVI   APLMSC2,C' '        2.DEFAULT SUBTYPE                            
         MVI   APLMSC3,0           3.(NONE)                                     
*                                  *** 1ST SUB CATEGORY ***                     
APLI06   LNR   RF,R0                                                            
         LA    RF,APLMSC3+1(RF)    DISPLACE TO SUB CATEGORY                     
         CLI   0(RF),0                                                          
         BNH   APLIXNAA            NO MORE SUB CATEGORIES                       
         MVC   ALIKSCAT,0(RF)                                                   
         CLC   INCLI,SPACES        TEST CLIENT ON INVREC                        
         BNH   *+16                                                             
         MVC   ALIKCLIC(L'INCLI),INCLI     USE IT                               
         OC    ALIKCLIC,SPACES                                                  
         MVC   SIOKEY(L'ALIKEY),ALIKEY                                          
         GOTO1 VDATAMGR,DMCB,DMREAD,ACCDIR,ALIKEY,ALIKEY                        
         BE    APLI10                                                           
*                                                                               
         MVC   ALIKEY,SIOKEY       NOT FOUND - RESTORE KEY                      
         CLC   INCLI,SPACES        TEST CLIENT ON INVREC                        
         BNH   APLI08                                                           
         XC    ALIKCLIC,ALIKCLIC   TRY AGAIN WITHOUT                            
         MVC   SIOKEY(L'ALIKEY),ALIKEY                                          
         GOTO1 VDATAMGR,DMCB,DMREAD,ACCDIR,ALIKEY,ALIKEY                        
         BE    APLI10                                                           
         MVC   ALIKEY,SIOKEY       NOT FOUND - RESTORE KEY                      
APLI08   BCT   R0,APLI06           AND THE NEXT SUB CATEGORY                    
         B     APLIXNAA            NONE LEFT - ERROR                            
                                                                                
APLI10   GOTO1 VDATAMGR,DMCB,DMGETR,ACCMST,ALIKDA,LIO,LIOWK                     
         BE    *+6                                                              
         DC    H'0'                                                             
         MVC   APLCAT,ALIKCAT      SAVE THE CATEGORY USED FOR APLIMIT           
         MVC   APLSCAT,ALIKSCAT    SAVE THE SUBCAT USED FOR APLIMIT             
         LA    R4,WORK                                                          
         XC    WORK,WORK           HOLD APLEL VALUES IN WORK                    
         LA    R2,LIO                                                           
         LA    R3,ALIRFST                                                       
         USING APLELD,R3                                                        
APLI12   CLI   APLEL,0             END OF RECORD                                
         BE    APLIXIGA            INVOICE IS > ANY APPROVAL LIMIT              
         CLI   APLEL,APLELQ        FIND APPROVAL LIMIT ELEMENT(S)               
         BE    APLI16                                                           
APLI14   SR    R0,R0                                                            
         IC    R0,APLLN                                                         
         AR    R3,R0                                                            
         B     APLI12                                                           
                                                                                
T        USING APLVAL,R4                                                        
APLI16   OC    T.APLVAL,T.APLVAL   TEST FIRST TIME                              
         BZ    APLI18              YES, JUST ADD ENTRY                          
         CLI   APLPREV,0           TEST PREVIOUS LEVEL IS REQUIRED              
         BNH   APLI18              NO - OVERWRITE PREV LEVEL WITH THIS          
         LA    R4,APTABNTQ(R4)     ELSE BUMP TO NEXT FREE SLOT                  
APLI18   ZAP   T.APLVAL,APLVAL     SAVE APPROVAL VALUE                          
         MVC   T.APLNUM,APLNUM     N'APPROVERS *REQUIRED*, THIS LEVEL           
         MVC   T.APLPREV,APLNUM    DECREMENT AS APPROVERS ARE *FOUND*           
*                                                                               
         CP    APLVAL,INTOT        CONTINUE UNTIL ONE APPROVAL LIMIT...         
         BL    APLI14              ... => INVOICE VALUE HAS BEEN FOUND          
         B     APLIYES                                                          
                                                                                
APLIXNAA OI    RETSTS,RETSNAA      NO APPLICABLE APLIMIT REC FOUND              
         B     APLINO                                                           
                                                                                
APLIXIGA OI    RETSTS,RETSIGA      INVOICE GREATER THAN APLIMIT                 
         B     APLINO                                                           
                                                                                
APLINO   LTR   RE,RE                                                            
         B     *+6                                                              
APLIYES  CR    RE,RE                                                            
         B     EXIT                                                             
         DROP  R2,R3                                                            
         EJECT                                                                  
***********************************************************************         
* TEST APPROVER/EDITOR CAN SEE AN INVOICE FOR APPROVAL/ACTION REQ'D   *         
* BASED ON EXTRACTED INCPJ/INSUP/INETY/INDPT VALUES                   *         
* NTRY R2=A(APPROVER/EDITOR RECORD)                                   *         
* EXIT CC: EQU/NEQ - YES/NO,  0(RF) X'40' BIT OFF/ON - DEFAULT/NOT    *         
***********************************************************************         
                                                                                
TSTAPRV  NTR1  ,                                                                
         XC    ACLLIST,ACLLIST                                                  
         XC    ASULIST,ASULIST                                                  
         XC    AETLIST,AETLIST                                                  
         XC    ADPLIST,ADPLIST                                                  
         XC    AAPLEL,AAPLEL                                                    
         XC    TEMP,TEMP                                                        
         MVI   APTYPE,0            INITIALISE                                   
         USING APPRECD,R2          R2=A(APPROVER REC)                           
         LA    R3,APPRFST          LOOK FOR ALL LIST ELEMENTS                   
         USING LIDELD,R3                                                        
TSTA02   CLI   LIDEL,0             AT EOR                                       
         BE    TSTA06              CHECK WHAT WAS FOUND                         
         CLI   LIDEL,APLELQ        SAVE A(APPROVAL LIMIT) ELEMENT TOO           
         BNE   *+12                                                             
         ST    R3,AAPLEL                                                        
         B     TSTA04                                                           
         CLI   LIDEL,LIDELQ                                                     
         BNE   TSTA04                                                           
         CLI   LIDTYPE,LIDTCPJ                                                  
         BNE   *+12                                                             
         ST    R3,ACLLIST          SAVE A(CLIENT LIDEL)                         
         B     TSTA04                                                           
         CLI   LIDTYPE,LIDTDEPT                                                 
         BNE   *+12                                                             
         ST    R3,ADPLIST          SAVE A(DEPARTMENT LIDEL)                     
         B     TSTA04                                                           
         CLI   LIDTYPE,LIDTEXPD                                                 
         BNE   *+12                                                             
         ST    R3,AETLIST          SAVE A(EXPENDITURE TYPE LIDEL)               
         B     TSTA04                                                           
         CLI   LIDTYPE,LIDTSUPP                                                 
         BNE   *+8                                                              
         ST    R3,ASULIST          SAVE A(SUPPLIER LIDEL)                       
TSTA04   XR    R0,R0                                                            
         IC    R0,LIDLN                                                         
         AR    R3,R0                                                            
         B     TSTA02                                                           
***            PRECEDENCE = CLIENT/SUPPLIER/EXPENSE/DEPT   ***                  
TSTA06   XR    R0,R0                                                            
*        TM    INREBSTA,REBSBARQ+REBSBREJ TEST INV STS: ACTN REQ OR REJ         
*        BNZ   TSTA16              CAN ONLY MATCH ON SUPPLIERS                  
         CLI   AFLTSTS,EDITORQ     CAN ONLY MATCH ON SUPPLIERS                  
         BE    TSTA16                                                           
         OC    INCPJ,INCPJ         TEST CLIENT CODE ON INVOICE                  
         BZ    TSTA16                                                           
         ICM   R3,15,ACLLIST       TEST CLIENT LIST ON APPROVER REC             
         BZ    TSTA16                                                           
         IC    R0,LIDLN                                                         
         LR    RE,R3                                                            
         AR    RE,R0               RE=EOL                                       
         LA    RF,LIDDATA          LOOK FOR A MATCH ON CLIENT                   
         IC    R0,LIDITLN                                                       
TSTA08   MVC   TEMP(L'INCPJ),0(RF)                                              
         OI    TEMP,X'40'          ENSURE X'40' BIT ON FOR COMPARE              
         LA    R4,TEMP+L'INCPJ-1   GET NET L'APPROVER CPJ LIST ENTRY            
TSTA10   CLI   0(R4),C' '                                                       
         BH    TSTA12                                                           
         MVI   0(R4),X'FF'         FILL WITH X'FF' TO SORT C/P/J,C/P,C          
         BCT   R4,TSTA10                                                        
TSTA12   LA    R0,TEMP                                                          
         SR    R4,R0                                                            
         EX    R4,*+8              COMPARE FOR ACTUAL LENGTH                    
         BE    TSTA14              OK                                           
         CLC   INCPJ(0),TEMP                                                    
         SR    R0,R0                                                            
         IC    R0,LIDITLN                                                       
         AR    RF,R0               BUMP TO NEXT LIST ENTRY                      
         CR    RF,RE                                                            
         BL    TSTA08                                                           
         B     TSTANO              CLIENT NOT ON APPROVER                       
TSTA14   MVI   APTYPE,CLIENTQ      SAVE APPROVER TYPE                           
         TM    0(RF),X'40'                                                      
         BNZ   *+8                                                              
         OI    APTYPE,DEFAPPQ      SET DEFAULT APPROVER                         
         B     TSTAYES                                                          
*                                                                               
TSTA16   OC    INSUP,INSUP         TEST SUPPLIER ON INVOICE REC                 
         BZ    TSTA28                                                           
         ICM   R3,15,ASULIST       TEST SUPPLIER LIST ON APPROVER REC           
         BZ    TSTA28                                                           
         IC    R0,LIDLN                                                         
         LR    RE,R3                                                            
         AR    RE,R0               RE=EOL                                       
         LA    RF,LIDDATA          LOOK FOR A MATCH ON SUPPLIER                 
         IC    R0,LIDITLN                                                       
TSTA18   MVC   TEMP(L'INSUP),1(RF)                                              
         OI    TEMP,X'40'          ENSURE X'40' BIT ON FOR COMPARE              
         LA    R4,L'INSUP-1        INIT TO FULL EX L'LIST ENTRY                 
         CLI   0(RF),LIDAPSAP      TEST 'APPROVER' SUPPLIER ENTRY               
         BNE   TSTA20                                                           
         CLI   AFLTSTS,EDITORQ     TEST EDITORS WANTED                          
         BE    TSTA24              SKIP TO NEXT ENTRY                           
         TM    APPRSTA2,APPSINBQ   TEST EDITOR+APPROVER                         
         BO    TSTA22              COMPARE FOR FULL L'APPROVER                  
         B     *+12                ELSE COMPARE FOR EX L'APPROVER               
*                                  'EDITOR' SUPPLIER ENTRY                      
TSTA20   CLI   AFLTSTS,EDITORQ     TEST EDITORS WANTED                          
         BNE   TSTA24              NO - SKIP 'EDITOR' LIST ENTRY                
*                                  GET EX L'SUPPLIER LIST ENTRY                 
         LA    R4,TEMP+L'INSUP-1                                                
         CLI   0(R4),C' '                                                       
         BH    *+12                                                             
         MVI   0(R4),X'FF'         FILL X'FF' TO SORT BEST MATCH FIRST          
         BCT   R4,*-12                                                          
         LA    R0,TEMP                                                          
         SR    R4,R0                                                            
TSTA22   EX    R4,*+8              COMPARE FOR R4 LENGTH                        
         BE    TSTA26              OK                                           
         CLC   INSUP(0),TEMP                                                    
TSTA24   SR    R0,R0                                                            
         IC    R0,LIDITLN                                                       
         AR    RF,R0               BUMP TO NEXT LIST ENTRY                      
         CR    RF,RE                                                            
         BL    TSTA18                                                           
         B     TSTANO              SUPPLIER NOT ON APPROVER                     
TSTA26   MVI   APTYPE,SUPPLRQ      APPROVAL TYPE IS SUPPLIER                    
         TM    1(RF),X'40'                                                      
         BNZ   *+8                                                              
         OI    APTYPE,DEFAPPQ      SET DEFAULT APPROVER                         
         CLI   0(RF),LIDAPSAP      TEST 'APPROVER' SUPPLIER ENTRY               
         BE    *+8                                                              
         MVI   APTYPE,EDITORQ      ELSE APPROVAL TYPE IS EDITOR                 
         B     TSTAYES                                                          
*                                                                               
*STA28   TM    INREBSTA,REBSBARQ+REBSBREJ TEST INV STS: ACTN REQ OR REJ         
*        BNZ   TSTANO              CAN ONLY MATCH ON SUPPLIERS                  
TSTA28   CLI   AFLTSTS,EDITORQ     CAN ONLY MATCH ON SUPPLIERS                  
         BE    TSTANO                                                           
         OC    INETY,INETY         TEST EXP TYPE ON INVOICE REC                 
         BZ    TSTA34                                                           
         ICM   R3,15,AETLIST       TEST EXP TYPE LIST ON APPROVER REC           
         BZ    TSTA34                                                           
         IC    R0,LIDLN                                                         
         LR    RE,R3                                                            
         AR    RE,R0               RE=EOL                                       
         LA    RF,LIDDATA          LOOK FOR A MATCH ON EXPENDITURE TYPE         
         IC    R0,LIDITLN                                                       
TSTA30   MVC   TEMP(L'INETY),0(RF)                                              
         OI    TEMP,X'40'          ENSURE X'40' BIT ON FOR COMPARE              
         CLC   INETY,TEMP                                                       
         BE    TSTA32                                                           
         AR    RF,R0               BUMP TO NEXT LIST ENTRY                      
         CR    RF,RE                                                            
         BL    TSTA30                                                           
         B     TSTANO              EXP TYPE NOT ON APPROVER                     
TSTA32   MVI   APTYPE,EXPTYPQ      SET THIS IS DEFAULT APPROVER                 
         TM    0(RF),X'40'                                                      
         BNZ   *+8                                                              
         OI    APTYPE,DEFAPPQ      SET DEFAULT APPROVER                         
         B     TSTAYES                                                          
*                                  NO MATCH SO FAR:                             
TSTA34   OC    INDPT,INDPT         TEST DEPARTMENT ON INVOICE REC               
         BZ    TSTA40                                                           
         ICM   R3,15,ADPLIST       TEST DEPT LIST ON APPROVER REC               
         BZ    TSTA40                                                           
         IC    R0,LIDLN                                                         
         LR    RE,R3                                                            
         AR    RE,R0               RE=EOL                                       
         LA    RF,LIDDATA          LOOK FOR A MATCH ON DEPARTMENT               
         IC    R0,LIDITLN                                                       
TSTA36   MVC   TEMP(L'INDPT),0(RF)                                              
         OI    TEMP,X'40'          ENSURE X'40' BIT ON FOR COMPARE              
         CLC   INDPT,TEMP                                                       
         BE    TSTA38                                                           
         AR    RF,R0               BUMP TO NEXT LIST ENTRY                      
         CR    RF,RE                                                            
         BL    TSTA36                                                           
         B     TSTANO              DEPT NOT ON APPROVER                         
TSTA38   MVI   APTYPE,DEPTQ        APPROVAL TYPE IS DEPARTMENT                  
         TM    0(RF),X'40'                                                      
         BNZ   *+8                                                              
         OI    APTYPE,DEFAPPQ      SET DEFAULT APPROVER                         
         B     TSTAYES                                                          
*                                  NO CLI/SUP/DPT/EXP LISTS ON APPROVER         
TSTA40   MVI   APTYPE,0            ENSURE IND NOT SET                           
         B     TSTANO                                                           
                                                                                
TSTAYES  CR    RE,RE                                                            
         B     EXIT                                                             
                                                                                
TSTANO   B     TSTANOX             *** TESTING - PATCH TO DIE ***               
         CLC   APPKPIDB,=X'FFE8'                                                
         BNE   TSTANOX                                                          
         DC    H'0'                                                             
TSTANOX  LTR   RE,RE                                                            
         B     EXIT                                                             
         EJECT                                                                  
***********************************************************************         
* CONSTANTS, EQUATES, LITERAL POOL                                    *         
***********************************************************************         
                                                                                
ACCDIR   DC    C'ACCDIR '                                                       
ACCMST   DC    C'ACCMST '                                                       
ACCARC   DC    C'ACCARC '                                                       
DMRDHI   DC    C'DMRDHI '                                                       
DMREAD   DC    C'DMREAD '                                                       
DMRSEQ   DC    C'DMRSEQ '                                                       
DMWRT    DC    C'DMWRT  '                                                       
DMADD    DC    C'DMADD  '                                                       
DMGETR   DC    C'GETREC '                                                       
DMPUTR   DC    C'PUTREC '                                                       
DMADDR   DC    C'ADDREC '                                                       
DMKEY    DC    C'DMKEY '                                                        
                                                                                
SPACES   DC    64C' '                                                           
PZERO    DC    P'0'                                                             
                                                                                
SUPVUL   DC    C'SV'                                                            
SUPXUL   DC    C'SX'                                                            
                                                                                
FFS      DC    X'FFFF'                                                          
                                                                                
ADD      EQU   C'A'                                                             
APPROVE  EQU   C'P'                                                             
REJECT   EQU   C'R'                                                             
FILTER   EQU   C'F'                                                             
                                                                                
PIDBLKN  EQU   2048/3              * 3-BYTE ENTRY                               
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
* WORKING STORAGE                                                     *         
***********************************************************************         
         SPACE 1                                                                
WORKD    DSECT                                                                  
DUB      DS    D                                                                
VDATAMGR DS    V                                                                
VCALLOV  DS    V                                                                
VLDCPTR  DS    V                                                                
VXSORT   DS    V                                                                
AREC     DS    A                                                                
AAPPR    DS    A                                                                
APIDBLK  DS    A                                                                
DMCB     DS    6A                                                               
APARM    DS    A                                                                
RELO     DS    A                                                                
AAPPEL   DS    A                   A(APPROVAL EL) - INVOICE REC                 
AAPLEL   DS    A                   A(APPROVER LIMIT EL) - APPROVER REC          
APVTCNT  DS    H                                                                
DA       DS    XL4                                                              
LIOWK    DS    XL96                                                             
IOKEY    DS    XL64                                                             
SIOKEY   DS    XL64                                                             
KIOKEY   DS    XL64                RESERVED FOR DMKEY SAVE/RESTORE              
WORK     DS    XL128                                                            
TEMP     DS    XL64                                                             
BYTE1    DS    XL1                                                              
AACTION  DS    CL1                                                              
AFLTSTS  DS    XL1                                                              
AFLTQUA  DS    CL1                                                              
ALLQ     EQU   C'A'                ALL APPROVERS FOR ALL LEVELS ON INV          
QUALQ    EQU   C'Q'                APVRS WHO MATCH 'AWAIT APPRV' REQ'TS         
AMTSTA1  DS    XL1                 RBDSTA1 FOR BILLABLE/NON-BILL TEST           
APLCAT   DS    XL(L'ALIKCAT)       ALIREC CATEGORY USED TO BUILD APPEL          
APLSCAT  DS    XL(L'ALIKSCAT)      ALIREC SUBCAT USED TO BUILD APPEL            
APLMSC1  DS    XL1                 PRESET SUBCAT USED FOR LOOKUP 1              
APLMSC2  DS    XL1                 PRESET SUBCAT USED FOR LOOKUP 2              
APLMSC3  DS    XL1                 PRESET SUBCAT USED FOR LOOKUP 3              
ACLLIST  DS    A                   A(CLIENT LIDEL)                              
ASULIST  DS    A                   A(SUPPLIER LIDEL)                            
ADPLIST  DS    A                   A(DEPARTMENT LIDEL)                          
AETLIST  DS    A                   A(EXPENDITURE TYPE LIDEL)                    
SUPVHIXL DS    H                   EX L'HI-LEVEL SV AC                          
SUPXHIXL DS    H                   EX L'HI-LEVEL SX AC                          
APTYPE   DS    XL1                                                              
CLIENTQ  EQU   X'01'               CLIENT APPROVER                              
SUPPLRQ  EQU   X'02'               SUPPLIER APPROVER                            
EXPTYPQ  EQU   X'04'               EXPENDITURE TYPE APPROVER                    
DEPTQ    EQU   X'08'               DEPARTMENT APPROVER                          
ANYAPPR  EQU   CLIENTQ+SUPPLRQ+EXPTYPQ+DEPTQ                                    
EDITORQ  EQU   X'10'               EDITOR                                       
DEFAPPQ  EQU   X'80'               APPROVER IS DEFAULT                          
*                                  VALUES FROM CALLER'S INVOICE REC             
INCPY    DS    XL1                 HEX CPY                                      
INREBSTA DS    XL1                                                              
INCPJ    DS    CL(L'ACTKACT)                                                    
INCLI    DS    CL5                                                              
INPRO    DS    CL2                                                              
INJOB    DS    CL7                                                              
INSUP    DS    CL14                                                             
INDPT    DS    CL12                                                             
INETY    DS    CL3                                                              
INOFF    DS    CL2                                                              
INTOT    DS    PL6                                                              
                                                                                
RETSTS   DS    XL1                 RETURN STATUS TO CALLER                      
RETSOK   EQU   X'00'               OK                                           
*                                  *ADD* ERROR CODES                            
RETSNAA  EQU   X'80'               NO APPLICABLE APLIMIT RECORDS EXIST          
RETSNEA  EQU   X'40'               NO/NOT ENOUGH APPROVERS FOUND                
RETSIGA  EQU   X'20'               INVOICE VALUE > THAN APPROVAL LIMIT          
RETSAAE  EQU   X'10'               APPEL ALREADY EXISTS ON INVREC               
*                                  *APPROVE* ERROR CODES                        
RETSNVA  EQU   X'80'               NOT A VALID APPROVER                         
RETSCAP  EQU   X'40'               APPROVER CAN'T APPROVE THIS INVOICE          
RETSNOA  EQU   X'40'               NO APPEL ON INVOICE                          
                                                                                
LIO      DS    XL2000                                                           
APVTAB   DS    (APVTABN)XL(APVTABL)                                             
WORKL    EQU   *-WORKD                                                          
                                                                                
* INTERNAL TABLE TO SORT POTENTIAL APPROVERS BY 'BEST MATCH' CRITERIA           
                                                                                
APVTABD  DSECT                                                                  
APVTYP   DS    XL1                 AS APTYPE 1-4                                
APVDEF   DS    XL1                                                              
APVDEFY  EQU   X'00'               IS A DEFAULT APPROVER                        
APVDEFN  EQU   X'FF'               ISN'T A DEFAULT                              
APVCODE  DS    CL14                CPJ(PADDED WITH FF)/SUP/EXP/DPT CODE         
APVPID   DS    XL2                 APPROVER PID                                 
APVTABL  EQU   *-APVTABD                                                        
APVTABN  EQU   200                 N'APPROVERS WE CAN HANDLE                    
                                                                                
* COVERS PIDBLK ENTRY                                                           
PIDBLKD  DSECT                                                                  
PIDBIN   DS    XL2                 BINARY PID                                   
PIDIND   DS    XL1                 INDICATORS                                   
PIDDEFQ  EQU   X'80'               DEFAULT APPROVER                             
PIDAPLQ  EQU   X'70'               APLIMIT LEVEL 1-4                            
PIDAPTQ  EQU   X'07'               AS APTYPE 1-7                                
PIDBLKL  EQU   *-PIDBLKD                                                        
         EJECT                                                                  
***********************************************************************         
* INCLUDES                                                            *         
***********************************************************************         
                                                                                
* ACGENFILE                                                                     
         PRINT OFF                                                              
       ++INCLUDE ACGENFILE                                                      
         PRINT ON                                                               
* DDCOMFACS                                                                     
         PRINT OFF                                                              
       ++INCLUDE DDCOMFACS                                                      
         PRINT ON                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'004ACAPEMAN  09/04/07'                                      
         END                                                                    
