*          DATA SET TABIPRT    AT LEVEL 218 AS OF 07/10/17                      
*CATALP TABIPRT                                                                 
         TITLE 'TABIPRT - TALENT BILLING - PRINTING PROCEDURES'                 
TABIPRT  CSECT                                                                  
         ENTRY MYHOOK                                                           
         PRINT NOGEN                                                            
         NMOD1 0,TABIPRT,R7,R6,R5                                               
         L     RC,0(R1)                                                         
         USING GEND,RC                                                          
         L     R8,ASPOOLD                                                       
         USING SPOOLD,R8                                                        
         L     R9,ASUBSYSD         SYSTEM SPECIFIC WORK                         
         USING SUBSYSD,R9                                                       
         LA    RA,BUFF             BILL DSECT                                   
         LA    RA,8(RA)                                                         
         USING BILLD,RA                                                         
         EJECT                                                                  
*        PRINT OUT THE BILL/INVOICE                                             
*                                                                               
         CLI   LASTPG,C'Y'         IF THIS IS THE LAST PAGE                     
         BNE   PRN05                                                            
         GOTO1 =A(PREND),DMCB,(RC) PRINT OUT SUMMARY                            
         B     PRNX                                                             
*                                                                               
PRN05    TM    STAT2,STCLOSE       READY TO CLOSE DATASET?                      
         BNO   PRN08                                                            
         GOTOR CLOSTAPE                                                         
         B     PRNX                                                             
                                                                                
PRN08    XC    TBBNPPNH,TBBNPPNH   INIT INVOICE'S BNP PNH                       
**NO-OP  BRAS  RE,SETEFDT          SET EFFECTIVE DATE                           
         CLI   TBTPO,C'O'          OR OFFICE O                                  
         BE    *+12                                                             
         CLI   TBTPO,C'Q'          OR OFFICE Q                                  
         BNE   PRN09                                                            
         XC    EMSTTAX(8),EMSTTAX                                               
PRN09    MVI   EMSFLAG,0           RESET EMS FLAG                               
         MVI   EMSOVAPP,0          RESET EMS OVERRIDE APPLIED                   
         CLC   TGAGY,=CL6'8800'    IF AGENCY IS 8800, PUT TO TAPE               
         BNE   *+12                                                             
         BAS   RE,PUTEMS           PUT EMS REPORT TO TAPE                       
         BE    PRNX                                                             
*                                                                               
         BAS   RE,PRTOP            PRINT HEADLINES                              
         GOTOR PRTCMT,DMCB,STATPRT PRINT COMMENT                                
         BE    PRN15                                                            
*                                                                               
         USING TABCHKD,R3                                                       
PRN10    L     R3,ANXTCHK          CURRENT PERFORMER'S ENTRY                    
         BAS   RE,PRDET            PRINT PERFORMER DETAILS                      
         SR    R1,R1                                                            
         ICM   R1,3,CKREC2         IF THERE ARE NO PERFORMERS LEFT              
         AH    R1,CKREC                                                         
         BZ    PRN20               THEN CLOSE BOX                               
PRN15    MVI   FORCEHED,C'Y'       ELSE FORCE PAGE EJECT AND                    
         BAS   RE,PRTOP               CONTINUE ON NEXT PAGE                     
         B     PRN10                                                            
*                                                                               
PRN20    GOTO1 =A(SSURPT),DMCB,TBTAX,SUISURCA                                   
         GOTO1 (RF),DMCB,PRTTAX,SUISURCA                                        
                                                                                
         BRAS  RE,CHLSUR           ADD SURCHARGE FOR CHLOE                      
                                                                                
         CLI   TGBYTE4,C'Y'        IF WE ARE STARTING A NEW PAGE JUST           
         BNE   PRN30                                                            
         MVI   FORCEHED,C'Y'       FOR TOTALS - FORCE PAGE EJECT                
         BAS   RE,PRTOP            PRINT HEADLINES                              
*                                                                               
PRN30    BRAS  RE,GETACC           GET LOCAL ACCOUNT NUMBERS/NAMES              
         ZIC   R1,MAXLINES         LAST LINE ON PAGE                            
         ZIC   R2,LINE             CURRENT LINE                                 
         SR    R1,R2                                                            
         SH    R1,=Y(BBOXLEN)      # OF LINES NEEDED FOR BOT BOX                
*                                                                               
         BAS   RE,ADJLINE          RETURN # ADDL LINES TO ADJUST BY             
         ZIC   RF,TBBYTE           IN IN TBBYTE                                 
         SR    R1,RF                                                            
         LTR   R1,R1                                                            
         BNP   PRN40                                                            
         STC   R1,SPACING          SPACING = N'LINES NEEDED TO PRINT            
         BRAS  RE,PRTIT            BEFORE LEAVING 15 FOR BOTTOM BOX             
*                                                                               
PRN40    MVI   LSTAT,0                                                          
         BAS   RE,PRBOT            PRINT BOTTOM BOXES                           
*                                                                               
PRNX     B     XIT                                                              
         DROP  R3                                                               
         EJECT                                                                  
*                                                                               
*        PUT EMS BILLING TO TAPE INSTEAD OF PRINTING                            
*                                                                               
PUTEMS   NTR1                                                                   
         CLI   RECNUM,BI         ONLY CREATE TAPE FOR BILLING OR ACOPY          
         BE    *+12                                                             
         CLI   RECNUM,AC                                                        
         BNE   NO                                                               
         MVI   EMSFLAG,1                                                        
         TM    STAT2,STOPEN      IS DATASET ALREADY OPEN?                       
         BO    PEMS20                                                           
         GOTOR OPENTAPE          NO, OPEN IT                                    
PEMS20   GOTOR PUT1HD                                                           
         LA    R2,BLOCK          STORE HEADING FOR OT&TRAVEL IN BLOCK           
         BAS   RE,PRCKTOP                                                       
         GOTOR PUTBHD            HEADINGS FROM BOX RECORD                       
         GOTOR PUTCHD            MORE HEADINGS RECORD                           
         MVC   P,SPACES                                                         
         GOTOR PRTCMT,DMCB,STATEMS PUT INVOICE COMMENTS TO EMS FILE             
         MVC   P,SPACES                                                         
         BAS   RE,PRCENT         PRINT CLASS A DETAILS                          
PEMS30   MVC   P,SPACES                                                         
         MVC   P2,SPACES                                                        
         BAS   RE,PRDET          PRINT PERFORMER DETAILS                        
         SR    R1,R1                                                            
         ICM   R1,3,CKREC2       IF THERE ARE NO PERFORMERS LEFT                
         AH    R1,CKREC                                                         
         BNZ   PEMS30                                                           
*                                                                               
         BAS   RE,P5LINE                                                        
         CLI   RECNUM,AC         IF AGENCY COPY, EMS TOTALS WERE                
         BE    PEMS40            ALREADY CALCULATED IN BILLING RUN              
         MVC   P,SPACES                                                         
         MVC   BLOCK(L'P),SPACES                                                
         BAS   RE,P6LINE                                                        
         LA    R2,P                                                             
         BAS   RE,PRBITOT                                                       
PEMS40   GOTOR PUTTOTS           PUT INVOICE TOTALS TO DATASET                  
         CLI   RECNUM,AC         IF ACOPY, AMOUNTS ALREADY CALCULATED           
         BNE   PEMS45                                                           
         MVC   P,SPACES          CLEAR PRINT LINE                               
         MVC   P2,SPACES                                                        
         CLI   WHEN,X'20'        IF SOON, SET TO ALSO PRINT INVOICE             
         BNE   YES                                                              
         BAS   RE,SETACTOT       SET UP ACOPY TOTALS TO PRINT                   
         MVI   EMSFLAG,2         SET EMSFLAG FOR 2ND RUN ACOPY                  
         B     PEMS50                                                           
PEMS45   GOTOR CALCEMS           CALCULATE EMS AMOUNTS                          
         MVC   P,SPACES          CLEAR PRINT LINE                               
         MVC   P2,SPACES                                                        
***      CLI   WHEN,X'20'        IF SOON - SET TO ALSO PRINT INVOICE            
***      BNE   YES                                                              
         MVI   EMSFLAG,3         SET EMSFLAG FOR 2ND RUN BI                     
PEMS50   MVC   ANXTCHK,ACHKTAB   RESET ADDRESS CHECK TABLE                      
         MVC   CKREC,SVCKNUM     RESET NUMBER OF PERFORMERS                     
         MVC   CKREC2,SVCKNUM2                                                  
         MVC   CKREC3,SVCKNUM3                                                  
         XC    TBGROSS(AMTS2),TBGROSS   CLEAR AMOUNTS                           
         XC    TOTGRS(EAMTS),TOTGRS                                             
         XC    TBPSTU,TBPSTU                                                    
         B     NO                PRINT INVOICE                                  
*                                                                               
*        SET ACOPY EMS TOTALS TO PRINT                                          
SETACTOT NTR1                                                                   
         L     R4,AIO                                                           
         MVI   ELCODE,TABDELQ2                                                  
         BRAS  RE,GETEL                                                         
         BNE   SACTOTX                                                          
         USING TABDD,R4                                                         
         MVC   PRTTAX,TABDTAX      EMS PAYROLL TAXES                            
         MVC   PRTHAND,TABDHND     EMS HANDLING FEE                             
         MVC   PRTEMSF,TABDACOM    EMS FEE                                      
         MVC   PRTEMST,TABDTOT     EMS INVOICE TOTAL                            
         DROP  R4                                                               
SACTOTX  B     XIT                                                              
         EJECT                                                                  
*                                                                               
PRTOP    NTR1                                                                   
*        GOTO1 =A(PRHEAD),DMCB,(RC)   PRINT HEADLINES                           
         GOTOR PRHEAD                                                           
         CLI   CENTHED,0           ARE THERE NETWORK PGMS                       
         BE    PRT10                                                            
         BAS   RE,PRCENT           PRINT CENTER BOX                             
*                                                                               
PRT10    BRAS  RE,PRTIT                                                         
         BAS   RE,PRCKTOP          PRINT TOP OF PERFORMERS                      
         BRAS  RE,PRTIT                                                         
         B     XIT                                                              
         EJECT                                                                  
*        PRINT CENTER BOX                                                       
PRCENT   NTR1                                                                   
         CLI   EMSFLAG,1           IF AGROUP EMS, SKIP HEADINGS                 
         BE    PRC05                                                            
*                                                                               
         CLI   PGNUM,1             IF ON PAGE ONE                               
         BNE   PRC04                                                            
         GOTOR PRTCMT,DMCB,STATBOX AND A COMMENT MUST BE PRINTED                
         BE    XIT                 DON'T SET UP BOXES YET                       
*                                                                               
PRC04    BRAS  RE,PRTIT                LEAVE A BLANK LINE                       
         OI    LSTAT,CENT          SET FLAG COMING FROM CENTER TABLE            
         GOTO1 BXTOP,DMCB,0,131,2                                               
         MVI   LSTAT,0                                                          
         LA    R2,P                POINT TO PRINT LINE                          
         USING P8LINED,R2                                                       
         MVC   P8TITL(8),LTCLASS   CLASS A                                      
         MVC   P8INFO1,LTPGM                                                    
         MVC   P8INFO2,LTPGM                                                    
         MVC   P8INFO3,LTPGM                                                    
         MVC   P8INFO4,LTPGM                                                    
         MVI   SPACING,2                                                        
         BRAS  RE,PRTIT                                                         
         DROP  R2                                                               
PRC05    L     R3,=V(NETTABX)      END OF NETWORK TABLE                         
         ST    R3,ANXTNET                                                       
         L     R3,=V(NETTAB)       NETWORK TABLE                                
         USING NETTABD,R3                                                       
*                                                                               
PRC10    LA    R4,4                4 ENTRIES PER LINE                           
         LA    R2,P                RE-SET R2                                    
         LA    R2,L'P8TITL+2(R2)                                                
         USING P8INFD,R2                                                        
*                                                                               
PRC20    C     R3,ANXTNET          END OF TABLE                                 
         BNL   PRC50                                                            
         OC    NETDATE,NETDATE     ANY INFO LEFT IN TABLE                       
         BZ    PRC30                                                            
         MVC   P8DT(3),NETDATE                                                  
         CLC   =C'TBA',P8DT                                                     
         BE    PRC24                                                            
         GOTO1 DATCON,DMCB,(1,NETDATE),(4,P8DT)                                 
*                                                                               
PRC24    MVC   P8PGM,NETNAME                                                    
         MVC   P8LFT,NETLFT                                                     
         MVC   P8NWK,NETNWK                                                     
         OC    P8PGM(P8NWK-P8PGM+1),SPACES                                      
         LA    R3,NETLEN(R3)       BUMP TABLE                                   
         LA    R2,L'P8INFO1+1(R2)                                               
         BCT   R4,PRC20                                                         
         B     PRC40                                                            
*                                                                               
PRC30    CLC   P,SPACES                                                         
         BNH   PCX                                                              
*                                                                               
PRC40    CLI   EMSFLAG,1           IF AGROUP EMS                                
         BNE   PRC45                                                            
         GOTOR PUTCLA              PUT CLASS A INFO TO DATASET                  
         B     *+8                                                              
PRC45    BRAS  RE,PRTIT                                                         
         OC    NETDATE,NETDATE     INFO LEFT IN TABLE                           
         BNZ   PRC10               DO NEXT LINE                                 
*                                                                               
PRC50    CLI   EMSFLAG,1           IF AGROUP EMS, DON'T PRINT                   
         BE    PCX                                                              
         BRAS  RE,BXBOT            CLOSE BOX                                    
         BRAS  RE,PRTIT                                                         
*                                                                               
PCX      B     XIT                                                              
         EJECT                                                                  
*                                                                               
*        PRINT TOP OF PERFORMERS BOX                                            
*                                                                               
PRCKTOP  NTR1                                                                   
         CLI   EMSFLAG,1           IF EMS AGROUP                                
         BE    PTCK35                                                           
*                                                                               
         CLI   PGNUM,1             IF ON PAGE ONE                               
         BNE   PTCK04                                                           
         GOTOR PRTCMT,DMCB,STATBOX AND A COMMENT MUST BE PRINTED                
         BE    XIT                 DON'T SET UP BOXES YET                       
*                                                                               
PTCK04   OI    LSTAT,CHK           SET FLAG COMING FROM CHK TABLE               
         CLI   TBMED,TACOMEDE      IF MEDIA = EVENT,                            
         BE    *+12                                                             
         CLI   TBPRTINV,C'Y'                                                    
         BNE   PTCK05                                                           
         GOTO1 BXTOP,DMCB,0,131,3                                               
         B     PTCK10                                                           
*                                                                               
PTCK05   GOTO1 BXTOP,DMCB,0,131,4                                               
*                                                                               
PTCK10   MVI   LSTAT,0                                                          
         LA    R2,P                POINT TO PRINT LINE                          
         USING P4LINED,R2                                                       
         MVC   SSN,LTSSN           S/S NUMBER                                   
         TM    TGSYSTAT,TASYSPID   USING PID?                                   
         BZ    PTCK15                                                           
         CLC   TGAGY,=CL6'8800'    IF AGENCY IS 8800, SS#                       
         BE    PTCK15                                                           
         CLI   GTYPE,TABRTYE       EMS WANTS SS#                                
         BE    PTCK15                                                           
         CLI   UNICOPY,C'Y'        AND THIS IS NOT UNION COPY                   
         BE    PTCK15                                                           
         MVC   SSN,LTPID           PID NUMBER                                   
PTCK15   MVC   NAME,LTNAME         PERFORMER NAME                               
*                                                                               
         CLI   TBMED,TACOMEDE      IF MEDIA = EVENT                             
         BNE   PTCK18                                                           
         MVC   NAME,LTEMPNM        EMPLOYEE NAME                                
         MVC   PPTAXWG,LTTXWG      TAXABLE                                      
         MVC   PPTXWG2,LTTXWG2     WAGES                                        
         MVC   PPNTXWG,LTNTXWG     NON-TAXABLE                                  
         MVC   PPNTWG2,LTTXWG2     WAGES                                        
         MVC   PPTXNWG,LTTXWG      TAXABLE                                      
         MVC   PPTXNW2,LTREIMB     REIMB                                        
         MVC   PPNTXNW,LTNTXWG     NON-TAXABLE                                  
         MVC   PPNTNW2,LTREIMB     REIMB                                        
         B     PTCK90                                                           
*                                                                               
PTCK18   MVC   CAT,LTCAT           CATEGORY                                     
         MVC   AGENT,LTAGT         AGENT                                        
         MVC   STATE,LTSTATE       STATE                                        
*                                                                               
         CLI   UNICOPY,C'Y'        IF THIS IS A UNION COPY                      
         BNE   PTCK20                                                           
*        TM    TGUSXUNI,AFM        AND THIS IS MUSIC USE                        
         GOTO1 UNITEST,DMCB,TGUSXUNS,AFM,0,0,0                                  
         BO    PTCK20                                                           
         MVC   LOCL,LTLOCL         AFM                                          
         MVC   LOCL2,LTLOCL2       LOCL                                         
         MVC   LOCL3,LTLOCL3       ****                                         
*                                                                               
PTCK20   CLI   TBPRTINV,C'Y'       FOR PRINT INVOICES                           
         BNE   PTCK30                                                           
         MVC   PAYEE,LTPAYEE       PAYEE                                        
         MVC   SHDTE+1(5),LTSHOOT  SHOOT DATE                                   
         MVC   SHDTE2+1(4),LTDATE1                                              
         MVC   PUBDTE+2(3),LTPUB   PUB DATE                                     
         MVC   PUBDTE2+2(4),LTDATE1                                             
         MVC   EXPDTE+2(3),LTEXP   EXPIRATION DATE                              
         MVC   EXPDTE2+2(4),LTDATE1                                             
         MVC   AGTPCT(5),LTARATE   AGENT COMMISSION %                           
         MVI   AGTPCT2+2,C'%'                                                   
         B     PTCK80                                                           
*                                                                               
PTCK30   DS    0H                                                               
         MVC   OV,LTOV             OV%                                          
         MVC   CAM,LTCAM1          ON/OFF CAMERA                                
         MVC   CAM2,LTCAM2                                                      
         MVC   CAM3,LTCAM3                                                      
         MVI   LIFT,C'L'           LIFT                                         
         MVI   LIFT2,X'86'         = F                                          
         MVI   LIFT3,X'A3'         = T                                          
         MVI   DBL,C'D'            DOUBLES                                      
         MVI   DBL2,X'82'          = B                                          
         MVI   DBL3,X'93'          = L                                          
         MVC   FIRST,LTFIRST1      FIRST SERVE DATE                             
         MVC   FIRST2,LTFIRST2                                                  
         MVC   FIRST3,LTDATE1                                                   
*                                                                               
PTCK35   TM    TGUSSTAT,SESSION    IF SESSION                                   
         BNO   PTCK60                                                           
         CLI   TGUSEQU,UVNR        SKIP IF VERSION NON-RENDERED                 
         BE    PTCK60                                                           
         CLI   TGUSEQU,UVRE        OR IF VERSION RENDERED                       
         BE    PTCK60                                                           
         MVC   SPOT,LTSP1          NUM OF SPOTS                                 
         MVC   SPOT2,LTSP2                                                      
         TM    TGUSSTA2,BSSTYPE    IF TELEVISION SESSION                        
         BZ    PTCK40                                                           
         MVC   VARCOL,LTOT1        OVERTIME HOURS                               
         MVC   VARCOL2,LTOT2                                                    
         MVC   VARCOL3,LTOT3                                                    
         MVC   DY,LTDY1            NO DAYS                                      
         MVC   DY2,LTDY2                                                        
         B     PTCK70                                                           
*                                                                               
PTCK40   CLI   TGUSEQU,UBSM        IF MUSIC SESSION                             
         BE    PTCK50                                                           
         CLI   TGUSEQU,UIMS                                                     
         BE    PTCK50                                                           
         CLI   TGUSEQU,UBSR        OR IF RADIO SESSION                          
         BE    PTCK45                                                           
         CLI   TGUSEQU,URRR                                                     
         BE    PTCK45                                                           
         CLI   TGUSEQU,UADO                                                     
         BNE   PTCK70                                                           
PTCK45   MVC   DY,LTTAG            IF RADIO - SET TAG                           
*                                                                               
PTCK50   MVC   VARCOL,LTHR1        HOURS & MINUTES                              
         MVC   VARCOL2,LTHR2                                                    
         B     PTCK70                                                           
*                                                                               
PTCK60   TM    TGUSSTA3,NWKUSE     IF NETWORK USAGE (CLA,LNA,LNN,LNC)           
         BO    *+12                                                             
         CLI   TGUSEQU,UITN        OR ITN USE                                   
         BNE   PTCK65                                                           
         MVC   VARCOL(L'LTRANG1),LTRANG1  USE RANGES                            
         MVC   VARCOL2(L'LTRANG2),LTRANG2                                       
         B     PTCK70                                                           
*                                                                               
PTCK65   TM    STAT2,STAVERS       ELSE IF PAYING VERSION                       
         BZ    PTCK70                                                           
         TM    TGUSTYST,MAJORS     AND HAVE MAJORS                              
         BO    PTCK67                                                           
         TM    TGUSTYST,UNITS      OR UNITS                                     
         BZ    PTCK70                                                           
PTCK67   MVC   VARCOL(L'LTMAJUN1),LTMAJUN1   MAJORS                             
         MVC   VARCOL2(L'LTMAJUN2),LTMAJUN2    +                                
         MVC   VARCOL3(L'LTMAJUN3),LTMAJUN3  UNITS                              
*                                                                               
PTCK70   CLI   TGUSEQU,UVNR        VERSION NON-RENDERED                         
         BE    *+12                                                             
         CLI   TGUSEQU,UVRE        VERSION RENDERED                             
         BNE   PTCK75                                                           
         MVC   VARCOL(L'LTMAJUN3),LTMAJUN3   UNITS                              
*                                                                               
PTCK75   CLI   EMSFLAG,1           IF AGROUP EMS,                               
         BNE   *+12                                                             
         BRAS  RE,PUTCOL           GET TITLES OF LAST 2 COLUMNS                 
         B     XIT                                                              
         MVC   YEAR,LTYEAR         YEAR                                         
         MVI   APL,C'A'                                                         
         MVI   APL2,X'97'          = P                                          
         MVI   APL3,X'93'          = L                                          
         MVI   INC,C'I'                                                         
         MVI   INC2,X'95'          = N                                          
         MVI   INC3,X'83'          = C                                          
         MVC   AMTAPL,LTAMT1       AMOUNT APPLIED                               
         MVC   AMTAPL2,LTAMT2                                                   
*                                                                               
PTCK80   CLI   UNICOPY,C'Y'        IF THIS IS A UNION COPY                      
         BNE   PTCK85                                                           
         CLC   =C'ACT',CUNION      & UNION = ACTRA                              
         BNE   PTCK85                                                           
         MVC   GROSS,LTACTGRS      GROSS                                        
         MVC   DUES,LTDUES1        DUES/SVC FEES                                
         MVC   DUES2,LTDUES2                                                    
         B     PTCK90                                                           
PTCK85   MVC   GROSS,LTGROSS       GROSS WAGES                                  
         MVC   MISC,LTMISC1        MISC PAYMENTS                                
         MVC   MISC2,LTMISC2                                                    
PTCK90   SR    R4,R4               COUNT HOW MANY SUMS WERE PRINTED             
         BRAS  RE,PUTCOL           DECIDE WHICH COLUMNS WILL                    
         BRAS  RE,PRTIT                   BE PRINTED                            
         B     XIT                                                              
         DROP  R2                                                               
         EJECT                                                                  
*        PRINT PERFORMER/CHECK DETAILS                                          
*                                                                               
         USING TABCHKD,R3                                                       
PRDET    NTR1                                                                   
         L     R3,ANXTCHK                                                       
         LH    R4,CKREC            NUMBER OF ENTRIES IN TABLE                   
*                                                                               
         USING P4LINED,R2                                                       
PRD10    CLI   TBPRTINV,C'Y'       TEST NOT PRINT INVOICE                       
         BE    PRD12                                                            
         TM    TABSTAT2,TABSTACT   IF CHECK FOR ACTRAWORKS                      
         BZ    PRD12                                                            
         L     R1,TABPAYC                                                       
         A     R1,TBCACTWK                                                      
         ST    R1,TBCACTWK                                                      
         CLI   INTFACE,C'N'        IF ON INTERFACE - POST TO WORKER             
         BE    PRD100                 FILE                                      
         TM    TBSTA2,TAINSPRM     DON'T LET PRIMARY POST                       
         BZ    PRD85                                                            
         B     PRD100                                                           
*                                                                               
PRD12    LA    R2,P                POINT TO PRINT LINE                          
         MVI   PERFSTAT,0          CLEAR PERFORMER STATUS BYTE                  
         MVC   SSN,TABSSN          S/S NUMBER                                   
         TM    TGSYSTAT,TASYSPID   USING PID NUMBERS?                           
         BZ    PRD13                                                            
         CLC   TGAGY,=CL6'8800'    IF AGENCY IS 8800, SS#                       
         BE    PRD13                                                            
         CLI   GTYPE,TABRTYE       EMS WANTS SS#                                
         BE    PRD13                                                            
         CLI   UNICOPY,C'Y'        AND THIS IS NOT UNION COPY                   
         BE    PRD13                                                            
         XC    SSN,SSN                                                          
         GOTO1 SSNPACK,DMCB,TABSSN,SSN   CONVERT SSN TO PID                     
         OC    SSN,SPACES                                                       
PRD13    MVC   NAME,TABNAME        PERFORMER NAME                               
         CLI   TBMED,TACOMEDE      IF MEDIA = EVENT, SKIP                       
         BE    PRD67                                                            
         ICM   R1,15,TABOV1        OV%                                          
         BZ    PRD20                                                            
         SR    R0,R0                                                            
         TM    TABOV1,X'80'        PERCENT SCALE USED?                          
         BZ    PRD14                                                            
         N     R1,=X'7FFFFFFF'     STRIP OFF HOB                                
PRD14    D     R0,=F'100'                                                       
         TM    TABOV1,X'80'                                                     
         BZ    PRD16                                                            
         MVI   OV,C'%'             SHOW PERCENT SCALE USED                      
         TM    TBAYSTA5,TAAYSOTH   DON'T SHOW THESE?                            
         BO    PRD20                                                            
         EDIT  (R1),(2,OV+1)                                                    
         B     PRD20                                                            
PRD16    TM    TBAYSTA5,TAAYSOTH   DON'T SHOW THESE?                            
         BO    PRD20                                                            
         EDIT  (R1),(3,OV)                                                      
         B     PRD20                                                            
*                                                                               
PRD20    TM    TBAYSTA5,TAAYSOTH   DON'T SHOW THESE?                            
         BO    PRD21                                                            
         MVC   CAT,TABCAT          CATEGORY                                     
         GOTO1 CATVAL,DMCB,TABCAT  SET CATEGORY TYPE EQUATES                    
         MVC   CAM,TABONOFF        ON/OFF CAMERA                                
PRD21    GOTO1 YRVAL,DMCB,TABYR    SET GLOBAL YEAR VALUES                       
*                                                                               
         CLI   UNICOPY,C'Y'        IF THIS IS A UNION COPY                      
         BNE   PRD30                                                            
*        TM    TGUSXUNI,AFM        AND THIS IS MUSIC USE                        
         GOTO1 UNITEST,DMCB,TGUSXUNS,AFM,0,0,0                                  
         BO    PRD30                                                            
         CLI   TGYREQU,CN91        AND CONTRACT IS '91 OR LATER                 
         BL    PRD35                                                            
         MVC   LOCL(3),TABRLOCL    PRINT RESIDENT LOCAL                         
         OC    LOCL(3),LOCL                                                     
         BNZ   *+10                                                             
         MVC   LOCL(3),TABLOCL     ELSE PRINT WORK LOCAL                        
         B     PRD35                                                            
*                                                                               
PRD30    GOTO1 TRNSAGT,DMCB,(X'40',TABAGNT),AGENT                               
*                                                                               
PRD35    MVC   STATE,TABSTATE      STATE                                        
         CLI   TBPRTINV,C'Y'       FOR PRINT INVOICES                           
         BNE   *+12                                                             
         BRAS  RE,PRPRINT          ONLY PRINT CERTAIN FIELDS                    
         B     PRD65                                                            
*                                                                               
         MVC   LIFT,TABLIFT        LIFT                                         
         MVC   DBL,TABDBL          DOUBLES                                      
         OC    TABFSERV,TABFSERV                                                
         BZ    PRD40                                                            
         GOTO1 DATCON,DMCB,(1,TABFSERV),(X'20',TEMP)  FIRST SERV DATE           
         MVC   FIRST(2),TEMP+2     SWAP ORDER TO MMDDYY                         
         MVC   FIRST+2(2),TEMP+4                                                
         MVC   FIRST+4(2),TEMP                                                  
*                                                                               
PRD40    TM    TGUSSTAT,SESSION    IS THIS A SESSION PAYMENT                    
         BNO   PRD50                                                            
         BAS   RE,PRSESS           PRINT SESSION DETAILS                        
*                                                                               
PRD50    CLI   TGUSEQU,UITN        IF ITN USE                                   
         BE    *+12                                                             
         TM    TGUSSTA3,NWKUSE     OR NETWORK USAGE (CLA,LNA,LNN,LNC)           
         BNO   PRD55                                                            
         BAS   RE,PRCLASS          PRINT CLASS A DETAILS                        
*                                                                               
PRD55    TM    STAT2,STAVERS       IF PAYING VERSION                            
         BZ    PRD60                                                            
         TM    TGUSTYST,MAJORS     AND IF HAVE MAJORS                           
         BO    PRD57                                                            
         TM    TGUSTYST,UNITS      OR UNITS                                     
         BZ    PRD60                                                            
         LA    RF,VARCOL           SET RF TO WHERE TO PRINT UNITS               
         B     PRD59                                                            
PRD57    CLI   TABMAJ,NY+LA+CHI             TEST IF ALL MAJORS                  
         BNE   PRD57A                                                           
         MVC   VARCOL(3),=C'ALL'                                                
         B     PRD58                                                            
PRD57A   GOTO1 MAJVAL,DMCB,(X'80',TABMAJ)   VALIDATE MAJORS                     
         BNE   *+10                                                             
         MVC   VARCOL(L'TGMACHAR),TGMACHAR                                      
PRD58    OC    TABUNITS,TABUNITS            IF THERE ARE UNITS                  
         BZ    PRD60                                                            
         LA    RF,VARCOL+L'TGMACHAR-1       FIND END OF MAJORS                  
         CLI   0(RF),X'40'                                                      
         BH    *+8                                                              
         BCT   RF,*-8                                                           
         MVI   1(RF),C'+'                                                       
         LA    RF,2(RF)                                                         
PRD59    EDIT  TABUNITS,(4,(RF)),ALIGN=LEFT NUMBER OF UNITS                     
         B     PRD64                                                            
*                                                                               
PRD60    CLI   TGUSEQU,UVRE        VERSION RENDERED                             
         BNE   PRD62                                                            
         LA    RF,VARCOL           SET RF TO WHERE TO PRINT UNITS               
         EDIT  TABUNITS,(4,(RF)),ALIGN=LEFT  NUMBER OF UNITS                    
*                                                                               
PRD62    CLI   TGUSEQU,UVNR        VERSION NON-RENDERED                         
         BNE   PRD64                                                            
         LA    RF,VARCOL           SET RF TO WHERE TO PRINT UNITS               
         TM    TABSTAT2,TABSTSN1   PAID 1ST NON-RENDERED VERSION?               
         BZ    PRD63                                                            
         MVC   0(4,RF),=C'1st '                                                 
         LA    RF,4(RF)                                                         
         OC    TABUNITS,TABUNITS            IF THERE ARE UNITS                  
         BZ    PRD64                                                            
         MVI   0(RF),C'+'                                                       
         LA    RF,1(RF)                                                         
PRD63    EDIT  TABUNITS,(4,(RF)),ALIGN=LEFT  NUMBER OF UNITS                    
*                                                                               
PRD64    MVC   YEAR(2),TABYR       YEAR                                         
         MVC   APL,TABACDE                                                      
         MVC   INC,TABICDE                                                      
*                                                                               
PRD65    TM    TABSTAT,TAPDSMAN    MANUAL OVERRIDE                              
         BNO   PRD67                                                            
         CLI   TBPRTINV,C'Y'       FOR PRINT INVOICES                           
         BNE   PRD66                                                            
         MVI   POVRD,C'*'          DIFFERENT LOCATION                           
         B     PRD67                                                            
*                                                                               
PRD66    MVI   OVRIDE,C'*'                                                      
PRD67    BAS   RE,PRAMTS           PRINT AMOUNTS                                
         BAS   RE,PR2NDLN          PRINT 2ND LINE                               
*                                                                               
         TM    STATUS,STBNP        IF THIS IS A BNP PAYMENT                     
         BNO   PRD70                                                            
         TM    STATUS,STCOD        & NOT COD                                    
         BO    PRD70                                                            
         CLI   EMSFLAG,3           IF 2ND RUN BILLING EMS,                      
         BE    PRD70               TABLE WAS ALREADY UPDATED                    
         BRAS  RE,PUTBNP           THEN PUT P&H AMT IN TABLE TO POST            
*                                                                               
PRD70    TM    TBOPTS2,TBFIXINV    UNLESS FIXING INVOICES                       
         BO    PRD75                                                            
         TM    TBOPTS,TBRERUN      IF THIS IS RERUN DON'T CALC T&H-YTD          
         BO    PRD80                                                            
PRD75    LR    R0,R4               SAVE R4 (COUNTER)                            
         L     R4,ATALIM                                                        
         USING TMD,R4                                                           
         NI    EVNTSTAT,X'FF'-EVNTPSUI   RESET SUI FOR EACH UNIT?               
         XC    TMSUISUR,TMSUISUR                                                
         GOTO1 =A(SETLIMBK),DMCB,(RC)  SET BLOCK TO T&H CALL (TALIM)            
         GOTO1 =A(APPLYADD)        APPLY ADDITIONAL BILLING RATES?              
         L     RF,ATAXDSCT                                                      
         ST    RF,TMXTDSCT                                                      
         TM    TBSTA2,TAINSHLP     IF HOLD COD PRINTED                          
         BO    *+12                                                             
         TM    TBSTAT,TAINSCIN     OR IF CANCELLED INVOICE                      
         BZ    *+8                                                              
         OI    TMSTAT,TMSNTNH      DONT CALC T&H                                
PRD76    GOTO1 =A(GETPSTR),DMCB,TABSTATE   GET PST RATE AND HST INDICAT         
         BRAS  RE,SETBRATE         SET BRATES                                   
         TM    SVOPT1,TAPDOTAX     OVERRIDE TAX AMOUNT                          
         BZ    *+8                                                              
         OI    TMSTAT2,TMSOTAX     OVERRIDE TAX AMOUNT                          
         MVC   TMEMSFLG,EMSFLAG                                                 
*                                                                               
         CLI   TBMED,TACOMEDE      IF EVENT = MEDIA                             
         BNE   PRD76A70            NO, CONTINUE NORMALLY                        
         TM    TGSYSTA2,TASYSNCS   ALLOW TO CHARGE IT?                          
         BZ    PRD76A70            NO, LEAVE                                    
         GOTO1 =A(PRCEVSUI),DMCB,(RC)                                           
*                                                                               
PRD76A70 GOTO1 AMYTRACE,DMCB,(RC),=C'TO TALIM BLOCK',TMD,TMLNQ                  
         GOTO1 =V(TALIM),DMCB,TMD                                               
*                                                                               
         CLI   RECNUM,BI           FOR BI & PB                                  
         BE    *+8                                                              
         CLI   RECNUM,PB                                                        
         BNE   PRD79                                                            
         CLI   TBTPO,C'O'          OR OFFICE O                                  
         BE    *+12                                                             
         CLI   TBTPO,C'Q'          OR OFFICE Q                                  
         BNE   PRD79                                                            
         CLI   EMSFLAG,1           IF AGROUP EMS                                
         BNE   PRD79                                                            
         MVI   EMSFLAG,0                                                        
*                                                                               
         L     RF,EMSTHND          ACCUMULATE HANDLING AND TAXES                
         A     RF,TMOHAND                                                       
         A     RF,TMOCORP                                                       
         A     RF,TMOCAN                                                        
         ST    RF,EMSTHND                                                       
         TM    TBISTAT,TAPDSCAN    CANADIAN?                                    
         BNO   *+8                                                              
         BRAS  RE,CVTCNCHL         CONVERT CAN$ CHLOE HANDLING TO US$           
*                                                                               
         TM    SVOPT1,TAPDOTAX     OVERRIDE TAX AMOUNT                          
         BO    PRD76B                                                           
         L     RF,EMSTTAX                                                       
         A     RF,TMXTOTAL                                                      
         ST    RF,EMSTTAX                                                       
*                                                                               
PRD76B   CLI   EMSOVAPP,0          OVERRIDES APPLIED ALREADY?                   
         BH    PRD77                 YES, DON'T READD OVERRIDES                 
         MVI   EMSOVAPP,C'Y'                                                    
         TM    SVOPT4,TAPDOHDA     CHLOE HANDLING AMT OVERRIDE                  
         BZ    PRD76X                                                           
         MVI   WORK,TAPATCHA                                                    
         BRAS  RE,GETPAAMT         GET RATE FROM TAPA ELEMENT                   
         L     RF,EMSTHND                                                       
         A     RF,FULL                                                          
         ST    RF,EMSTHND                                                       
*                                                                               
PRD76X   TM    BOVRSTAT,BOVRHNDB+BOVRHNDP+BOVRHNDC BOVER HAND AMOUNT?           
         BZ    PRD76XX                                                          
         TM    TBISTAT,TAPDSCAN    CANADIAN?                                    
         BO    PRD76XA                                                          
         BRAS  RE,SETHANDI         SET NEW POSTING DETAILS                      
         B     *+8                                                              
PRD76XA  BRAS  RE,SETHANDA         SET NEW POSTING DETAILS                      
         L     RF,EMSTHND          ADD BOVER HANDLING TO TOTALS                 
         A     RF,FULL                                                          
         ST    RF,EMSTHND                                                       
         B     PRD77                                                            
*                                                                               
PRD76XX  TM    SVOPT1,TAPDOHND     HANDLING AMT OVERRIDE (INDIV & CORP)         
         BZ    PRD77                                                            
         MVI   WORK,TAPATHND                                                    
         BRAS  RE,GETPAAMT         GET RATE FROM TAPA ELEMENT                   
         L     RF,EMSTHND                                                       
         A     RF,FULL                                                          
         ST    RF,EMSTHND                                                       
*                                                                               
PRD77    GOTO1 AMYTRACE,DMCB,(RC),=C'EXTRA TAX+HAND',EMSTTAX,8                  
         MVC   TMBILTYP,GTYPE                                                   
         GOTO1 =A(SETRATES)        SET BILLING RATES                            
*                                                                               
         CLI   TBTPO,C'O'          OFFICE O                                     
         BE    *+12                                                             
         CLI   TBTPO,C'Q'          OFFICE Q                                     
         BNE   PRD78                                                            
         CLC   TABCAT,=C'ZZZ'      CAST CATEGORY = ZZZ                          
         BNE   PRD78                                                            
         XC    TMBRWCRP,TMBRWCRP   NO WC CORP CHARGE                            
         BRAS  RE,UPDNWC           UPDATE NEW WC CORP RATE                      
*                                                                               
PRD78    GOTO1 AMYTRACE,DMCB,(RC),=C'TO TALIM BLOCK',TMD,256                    
         B     PRD76                                                            
*                                                                               
PRD79    NI    TMSTAT,X'FF'-TMSNTNH                                             
         DROP  R4                                                               
         LR    R4,R0               RESTORE COUNTER                              
         BAS   RE,RETINFO          SET INFO IN CHKTAB FROM TALIM                
         CLI   TBMED,TACOMEDE      EVENT INVOICE?                               
         BE    PRD80               TOOK CARE OF SUI SURCHARGE BEFORE            
         GOTO1 =A(SUISUR),DMCB,ATALIM,SUISURCA     SURC FOR CA AND NY?          
*                                                                               
PRD80    BRAS  RE,SETOVRD          SET TAX LEVEL                                
         CLI   EMSFLAG,1           IF AGROUP EMS                                
         BNE   PRD83                                                            
         GOTOR PUTPERF             PUT PERFORMER DETAILS TO DATASET             
         B     *+8                                                              
PRD83    BRAS  RE,PRTIT            PRINT DETAIL LINE                            
*                                                                               
         CLI   INTFACE,C'N'        IF ON INTERFACE - POST TO WORKER             
         BE    PRD100                 FILE                                      
         TM    TBSTA2,TAINSPRM     DON'T LET PRIMARY POST                       
         BO    PRD100                                                           
*                                                                               
PRD85    BRAS  RE,TESTCAN          IF NEW-STYLE CAN$ PAYMENT                    
         BNE   PRD90                                                            
***      MVC   SVTAB,TABCHKD       SAVE CAN$ AMOUNTS                            
         LA    RE,TABCHKD          RE=A(DATA TO MOVE FROM)                      
         LA    R0,SVTAB            R0=A(DATA TO MOVE TO)                        
         LHI   R1,TABLEN           R1 AND RF = LENGTH TO MOVE                   
         LR    RF,R1                                                            
         MVCL  R0,RE               SAVE CAN$ AMOUNTS IN SVTAB                   
         GOTO1 ACVTTAB,DMCB,TABCHKD CONVERT CAN$ TO US$ IN TABLE                
*                                                                               
PRD90    GOTO1 =V(TABIPOST),DMCB,(RC),ECKPOST,(R3) BUFFER PROD POSTINGS         
*                                                                               
         BRAS  RE,TESTCAN          IF NEW-STYLE CAN$ PAYMENT                    
         BNE   PRD100                                                           
***      MVC   TABCHKD(TABLEN),SVTAB  RESTORE TABLE VALUES (CAN$)               
         LA    RE,SVTAB            RE=A(DATA TO MOVE FROM)                      
         LA    R0,TABCHKD          R0=A(DATA TO MOVE TO)                        
         LHI   R1,TABLEN           R1 AND RF = LENGTH TO MOVE                   
         LR    RF,R1                                                            
         MVCL  R0,RE               SAVE CAN$ AMOUNTS IN SVTAB                   
*                                                                               
PRD100   LA    R3,TABLEN(R3)       NEXT TABLE ENTRY                             
         LR    R1,R4               CHECKS LEFT IN TABLE                         
         BCTR  R1,0                     - 1 LINE ALREADY PRINTED                
         AH    R1,CKREC2                + EXTRA LINES (SUBTRACTED 1)            
         LA    R1,BBOXLEN(R1)           + ROOM FOR BOTTOM BOXES                 
         BAS   RE,ADJLINE          RETURN # ADDL LINES TO ADJUST BY             
         ZIC   RF,TBBYTE           IN TBBYTE                                    
         AR    R1,RF               R1=N'LINES NEEDED                            
*                                                                               
         ZIC   RF,LINE             CURRENT LINE                                 
         BCTR  RF,0                LINE IS POINTING TO NEXT LINE                
         ZIC   RE,MAXLINES         MAX N'LINES                                  
         SR    RE,RF               LINES LEFT                                   
         MVI   TGBYTE4,C'N'                                                     
         CR    R1,RE               N'LINES < = LINES LEFT                       
         BNH   PRD130                                                           
*                                                                               
         LA    R0,15                                                            
         ZIC   RF,TBBYTE                                                        
         AR    R0,RF                                                            
         CR    R1,R0               IF JUST THE BOTTOM BOX LEFT                  
         BH    PRD105                                                           
         CLC   PGNUM,NPAGES                                                     
         BNE   PRD104                                                           
         TM    TBISTAT,TAPDSCAN                                                 
         BNO   PRD103                                                           
         CLI   LINE,44                                                          
         BL    PRD105                                                           
         B     PRD104                                                           
PRD103   CLI   LINE,45                                                          
         BL    PRD105                                                           
PRD104   MVI   TGBYTE4,C'Y'                                                     
*                                                                               
PRD105   LA    R0,1                                                             
         CLI   TABXLN,0            IF THERE'S AN EXTRA LINE TO PRINT            
         BE    *+8                                                              
         LA    R0,2                NEED 2 MORE LINES NEXT TIME                  
         CR    RE,R0                                                            
         BNH   PRD110              IF AT END OF PAGE                            
         CH    R1,=H'15'           OR IF JUST THE BOTTOM BOX LEFT               
         BH    PRD130              CLOSE BOX & GO TO NEXT PAGE                  
         B     PRD120                                                           
*                                                                               
PRD110   BRAS  RE,BXBOT            CLOSE BOX                                    
*                                                                               
PRD120   BCTR  R4,0                                                             
         B     PRD140                                                           
*                                                                               
PRD130   BCT   R4,PRD10                                                         
*                                                                               
PRD140   ST    R3,ANXTCHK          ADDRESS OF NEXT ENTRY IN TABLE               
         STH   R4,CKREC            NUMBER OF ENTRIES LEFT IN TABLE              
         MVI   INV,C'N'            RESET FLAGS FOR NEXT PAGE                    
         MVC   INV+1(FLAGX-INV-3),INV                                           
         MVI   PARDYST,0                                                        
         B     XIT                                                              
         EJECT                                                                  
*--------------------------------------------------------------------           
* APPLY ADDITIONAL RATES TO GENERAL RATES, OFFICE O ONLY                        
*--------------------------------------------------------------------           
APPLYADD NTR1                                                                   
         L     R4,ATALIM                                                        
         USING TMD,R4                                                           
         CLI   RECNUM,BI           FOR BI & PB                                  
         BE    *+8                                                              
         CLI   RECNUM,PB                                                        
         BNE   APPADD90                                                         
         CLI   TBTPO,C'O'          OR OFFICE O                                  
         BE    *+12                                                             
         CLI   TBTPO,C'Q'          OR OFFICE Q                                  
         BNE   APPADD90                                                         
         MVI   EMSFLAG,1           SET TO RESET RATES LATER                     
*                                                                               
         MVC   TMBILTYP,ADRBTYPE                                                
*                                                                               
         TM    SVOPT1,TAPDOHND     HANDLING AMT OVERRIDE (INDIV & CORP)         
         BZ    APPADD15                                                         
         XC    TMBRCORP,TMBRCORP   NO RATE NECESSARY THEN                       
         XC    TMBRHAND,TMBRHAND                                                
*                                                                               
**NO-OP  2/14                                                                   
**BOVER OVERRIDES DO NOT APPLY TO CHLOE COPY                                    
*&&DO                                                                           
APPADD10 TM    BOVRSTAT,BOVRTAX    BOVER TAX OVERRIDE?                          
         BZ    APPADD15                                                         
         BRAS  RE,SETTAX           IF NEW WAY, SET OVERRIDE TAX                 
         L     R1,EMSTTAX          ADD OVERRIDE TAX TO TOTAL                    
         A     R1,FULL                                                          
         ST    R1,EMSTTAX                                                       
         L     RE,FULL                                                          
         GOTOR UPDHANDI,DMCB,(RE)  UPDATE IND. HANDLING IF NEEDED               
         BNE   APPADD20                                                         
         L     RF,EMSTHND          ADD BOVER HANDLING TO TOTALS                 
         A     RF,FULL                                                          
         ST    RF,EMSTHND                                                       
         B     APPADD20                                                         
*&&                                                                             
*                                                                               
APPADD15 TM    SVOPT1,TAPDOTAX     TAX AMOUNT OVERRIDE                          
         BZ    APPADD20                                                         
         MVI   WORK,TAPATTAX                                                    
         BRAS  RE,GETPAAMT         GET TAX AMT FROM TAPAEL IN FULL              
         MVC   EMSTTAX,FULL                                                     
*                                                                               
APPADD20 TM    SVOPT4,TAPDOHDP     CHLOE HANDLING RATE OVERRIDEN                
         BZ    APPADD25                                                         
         MVI   WORK,TAPATCHO                                                    
         BRAS  RE,GETPAAMT         GET RATE FROM TAPAEL IN FULL                 
*                                                                               
APPADD25 TM    SVOPT4,TAPDOHDA     CHLOE HANDLING AMT OVERRIDEN                 
         BZ    APPADD30                                                         
         XC    FULL,FULL                                                        
*                                                                               
*&&DO                                                                           
*        DO NOT USE - CHLOE COPY WILL USE OLD RATES AND IGNORE BRATE            
APPADD30 L     R4,ATALIM                                                        
         USING TMD,R4                                                           
         L     RE,TMACURRA         DO WE HAVE NEW RATES?                        
         LTR   RE,RE                                                            
         BZ    APPADD30                                                         
         BRAS  RE,ADDCHLOE         ADD CHLOE RATES                              
         B     APPADD90                                                         
         DROP  R4                                                               
*&&                                                                             
                                                                                
APPADD30 OC    ADRFUTA(L'TABRRATE),ADRFUTA  ANY RATES TO ACCUMULATE?            
         BNZ   APPADD35                     YES, CONTINUE                       
         OC    FULL,FULL                                                        
         BZ    APPADD90                     NO, LEAVE                           
APPADD35 LA    RE,TMRATEN          N'BILLING RATES                              
         LA    R3,TMBRATES                                                      
         LA    R4,ADRFUTA                                                       
         XR    R1,R1                                                            
APPADD50 L     RF,0(R3)            ACCUMULATE THE RATES                         
         ICM   R1,3,0(R4)                                                       
         CHI   RE,4                CHECK IF AT CORP HAND RATE                   
         BE    *+8                                                              
         CHI   RE,3                CHECK IF AT HANDLING RATE                    
         BNE   APPADD60                                                         
         TM    SVOPT4,TAPDOHDP+TAPDOHDA                                         
         BZ    APPADD60                                                         
         ICM   R1,15,FULL                                                       
APPADD60 AR    RF,R1                                                            
         ST    RF,0(R3)                                                         
                                                                                
         AHI   R3,4                BUMP TO NEXT RATES                           
         AHI   R4,2                                                             
         BCT   RE,APPADD50                                                      
*                                                                               
APPADD90 B     XIT                                                              
         EJECT                                                                  
*              ROUTINE CALCULATES # OF LINES NEEDED FOR ADJUSTMENT              
*                                  XIT = RETURNS TBBYTE                         
         SPACE 1                                                                
ADJLINE  NTR1                                                                   
         XR    R1,R1                                                            
*                                                                               
         BRAS  RE,TESTCAN          IF NEW CANADIAN STYLE PRINTING               
         BNE   ADJLINE1                                                         
         LHI   R1,2                NEED 2 EXTRA LINES                           
         OC    AOSAMT,AOSAMT       IF PRINTING AOS AMOUNT                       
         BZ    *+8                                                              
         AHI   R1,2                                                             
         B     ADJLINEX                                                         
*                                                                               
ADJLINE1 BAS   RE,TESTGSTU         TEST GST PAID ON US$ INVOICES                
         BNE   *+8                                                              
         LHI   R1,1                NEED ONE MORE EXTRA LINE                     
         OC    AOSAMT,AOSAMT       IF PRINTING AOS AMOUNT                       
         BZ    *+8                                                              
         AHI   R1,2                                                             
                                                                                
         BAS   RE,TESTLCLS         TEST MORE THAN 3 LOCALS                      
         BNE   *+8                                                              
         AHI   R1,1                & 1 MORE(MORE THAN 3 LOCALS)                 
                                                                                
         CLI   TBMED,TACOMEDE      IF MEDIA IS EVENT                            
         BNE   *+8                                                              
         AHI   R1,1                                                             
                                                                                
         CLI   TBMED,TACOMEDP      IF MEDIA IS PRINT                            
         BNE   *+8                                                              
         AHI   R1,2                                                             
*                                                                               
ADJLINEX STC   R1,TBBYTE                                                        
         B     XIT                                                              
         SPACE 2                                                                
*              TEST GST PAID ON US$ INVOICES                                    
*                                                                               
TESTGSTU NTR1                                                                   
         CLI   TGCUR,C'C'          IF NOT CANADIAN CURRENCY                     
         BO    NO                                                               
         OC    TBGSTU,TBGSTU                                                    
         BZ    NO                                                               
         B     YES                                                              
         SPACE 2                                                                
*        TEST EXTRA LINE NEEDED BECAUSE MORE THAN 3 LOCALS                      
*                                                                               
TESTLCLS NTR1                                                                   
         OC    TBGPAY4,TBGPAY4     OR IF MORE THAN THREE                        
         BNZ   YES                 UNION LOCALS                                 
         OC    TBPNH4,TBPNH4                                                    
         BNZ   YES                 NEED EXTRA LINE                              
         B     NO                                                               
         EJECT                                                                  
*                                                                               
*        DETAILS FOR SESSIONS                                                   
*                                                                               
         USING P4LINED,R2                                                       
PRSESS   NTR1                                                                   
         CLI   TBPRTINV,C'Y'       FOR PRINT INVOICES                           
         BE    PRSX                EXIT                                         
         ZIC   R1,TABSP                                                         
         LA    RF,SPOT             # OF SPOTS                                   
         BAS   RE,EDIT2A                                                        
         TM    TGUSSTA2,BSSTYPE    IF TELEVISION SESSION                        
         BZ    PRS10                                                            
         ZIC   R1,TABDAY           N'DAYS                                       
         LA    RF,DY                                                            
         BAS   RE,EDIT2A                                                        
*                                                                               
         ZIC   R1,TABOT            OVERTIME                                     
         ZIC   RE,TABDT            DOUBLE O/T                                   
         AR    R1,RE                                                            
         MH    R1,=H'100'          SHIFT TO ADD IN OTHER TIMES                  
*                                                                               
         LH    RE,TABTRV           TRAVEL TIME                                  
         AR    R1,RE                                                            
         LH    RE,TABPDW           PRIOR DAY WARDROBE                           
         AR    R1,RE                                                            
         LA    RF,VARCOL                                                        
         LTR   R1,R1               IF ANYTHING TO PRINT                         
         BZ    PRS05                                                            
         BAS   RE,EDIT5            PRINT IT                                     
         B     PRSX                                                             
*                                                                               
PRS05    ZIC   R1,TABTAG           IF THERE ARE ANY TAGS                        
         LTR   R1,R1                                                            
         BZ    PRSX                                                             
         MVC   VARCOL(3),LTTAG1    PRINT THEM                                   
         LA    RF,3(RF)                                                         
         BAS   RE,EDIT2                                                         
**NO-OP* MVI   TABTAG,0            CLEAR SO WON'T PRINT ON 2ND LINE             
         OI    PERFSTAT,PERFSTAG   SET PRINTED ON FIRST LINE                    
         B     PRSX                                                             
*                                                                               
PRS10    CLI   TGUSEQU,UBSM        IF MUSIC SESSION                             
         BE    PRS20                                                            
         CLI   TGUSEQU,UIMS                                                     
         BE    PRS20                                                            
         CLI   TGUSEQU,UBSR        OR IF RADIO SESSION                          
         BE    PRS15                                                            
         CLI   TGUSEQU,UADO                                                     
         BE    PRS15                                                            
         CLI   TGUSEQU,URRR                                                     
         BNE   PRSX                                                             
PRS15    ZIC   R1,TABDAY           TAG FOR RADIO SESSIONS                       
         LA    RF,DY                                                            
         BAS   RE,EDIT2A                                                        
*                                                                               
PRS20    LH    R1,TABTRV           HRS/MINS                                     
         LA    RF,VARCOL                                                        
         BAS   RE,EDIT5                                                         
*                                                                               
PRSX     B     XIT                                                              
         EJECT                                                                  
*                                                                               
*        DETAILS FOR CLASS A                                                    
*                                                                               
PRCLASS  NTR1                                                                   
         LH    R1,TABSTUS          ADD TO START USE NUMBER                      
         LH    RE,TABUSES          THE N'USES                                   
         LTR   RE,RE               & IF IT'S 0 - DON'T PRINT ANYTHING           
         BZ    PRCAX                                                            
         AR    RE,R1                                                            
         BCTR  RE,0                FENCE POST                                   
         ST    RE,FULL             SAVE LAST USE NUMBER                         
         LA    RF,CO11                                                          
         BAS   RE,EDIT4R           EDIT START USE #                             
*                                                                               
         MVI   VARCOL+3,C'-'          INSERT DASH                               
         LA    RF,VARCOL+4                                                      
         L     R1,FULL                                                          
         BAS   RE,EDIT4            & EDIT LAST USE NUMBER                       
*                                                                               
PRCAX    B     XIT                                                              
         EJECT                                                                  
*        PRINT AMOUNTS                                                          
*                                                                               
PRAMTS   NTR1                                                                   
         L     R1,TABAPPL          APPLIED CREDITS +                            
         A     R1,TABGUAR          + GUARANTEED CREDITS                         
         LA    RF,AMTAPL-1         = TOTAL AMOUNT APPLIED                       
         BAS   RE,EDIT10                                                        
         A     R1,TBAPPL           TOTAL AMOUNT APPLIED                         
         ST    R1,TBAPPL                                                        
*        BAS   RE,SPCLHAND         SPECIAL FOR BTYPE 7/18                       
*                                                                               
         CLI   UNICOPY,C'Y'        IF THIS IS A UNION COPY                      
         BNE   PRAM8                                                            
         CLC   =C'ACT',CUNION      & UNION = ACTRA                              
         BNE   PRAM8                                                            
         L     R1,TABPAYI          SHOW INDIV PAYMENT                           
         A     R1,TABPAYC             + CORP PAYMENT                            
         A     R1,TABREXP             + REIMBURSED EXPENSES                     
         LA    RF,GROSS-1          IN GROSS COLUMN                              
         BAS   RE,EDIT12                                                        
         OC    TABPAYI,TABPAYI     IF HAVE INDIV PAYMENT                        
         BZ    PRAM12                                                           
         L     R1,TABPAYI          RELOAD R1 - GETS CREAMED IN EDIT             
         A     R1,TABREXP          ADD REIMBURSED PAYMENT                       
         A     R1,TOTMISC          TO TOTAL MISC PAYMENTS                       
         ST    R1,TOTMISC                                                       
         B     PRAM30                                                           
*                                                                               
PRAM8    OC    TABPAYI,TABPAYI                                                  
         BZ    PRAM10                                                           
         L     R1,TABPAYI                                                       
         LA    RF,GROSS-1                                                       
         CLI   TBMED,TACOMEDE      IF EVENT = MEDIA,                            
         BNE   *+8                                                              
         LA    RF,PPTAXWG-1        USE TAXABLE WAGES                            
         BAS   RE,EDIT12                                                        
         L     R1,TABPAYI          RELOAD R1 - GETS CREAMED IN EDIT             
         A     R1,TOTGRS           SUM TOTAL GROSS OF ALL CHECKS                
         ST    R1,TOTGRS                                                        
         CLI   TBMED,TACOMEDE      IF EVENT = MEDIA,                            
         BE    PRAM30              SKIP THIS                                    
         L     R1,TABREXP          REIMBURSED EXPENSES                          
         LA    RF,MISC-1                                                        
         BAS   RE,EDIT11                                                        
*                                                                               
         L     R1,TABREXP          AMOUNT TO BE ADDED                           
         A     R1,TOTMISC          TO MISC PAYMENTS                             
         ST    R1,TOTMISC                                                       
         B     PRAM30                                                           
*                                                                               
PRAM10   CLI   TBMED,TACOMEDE      IF EVENT = MEDIA                             
         BNE   PRAM10A                                                          
         L     R1,TABPAYC          NO REIMB EXP. HERE                           
         LA    RF,PPNTXWG-1        USE NON-TAXABLE WAGES                        
         BAS   RE,EDIT12                                                        
         L     R1,TABPAYC          ADD TO MISC SUM OF ALL CHECKS                
         B     PRAM20                                                           
PRAM10A  L     R1,TABREXP          ADD REIMBURSED EXP TO MISC PAYMENT           
         A     R1,TABPAYC                                                       
         LA    RF,MISC-1                                                        
         BAS   RE,EDIT11                                                        
PRAM12   L     R1,TABREXP          ADD REIMBURSED EXP TO CORP PAYMENT           
         A     R1,TABPAYC                                                       
         BRAS  RE,CKCAN            IF I&R CHECK                                 
         BNE   PRAM20                                                           
         A     R1,TOTCINR          ACCUMULATE IN TOTCINR                        
         ST    R1,TOTCINR                                                       
         B     PRAM30                                                           
*                                                                               
PRAM20   A     R1,TOTMISC          ELSE ADD TO MISC SUM OF ALL CHECKS           
         ST    R1,TOTMISC                                                       
*                                                                               
PRAM30   CLI   TBMED,TACOMEDE      IF EVENT = MEDIA,                            
         BNE   PRAM35                                                           
         L     R1,TABTXNW                                                       
         LA    RF,PPTXNWG-1        TAXABLE NON-WAGES                            
         BAS   RE,EDIT12                                                        
         L     R1,TABNTNW                                                       
         LA    RF,PPNTXNW-1        NON-TAXABLE NON WAGES                        
         BAS   RE,EDIT12                                                        
         L     R1,TABTXNW                                                       
         A     R1,TOTTXNW                                                       
         ST    R1,TOTTXNW          SUM OF TOTAL TAXABLE NON WAGES               
         L     R1,TABNTNW                                                       
         A     R1,TOTNTXNW                                                      
         ST    R1,TOTNTXNW         SUM OF TOTAL NONTAX NON WAGES                
                                                                                
PRAM35   LA    RF,PH-1                                                          
         TM    PRCOLL,CPNH         IS P & H IN LEFT COLUMN                      
         BO    PRAM40                                                           
         LA    RF,HW-1                                                          
         TM    PRCOLL,CPNH         OR RIGHT COLUMN                              
         BNO   PRAM60                                                           
*                                                                               
PRAM40   L     R1,TABPNH           AMOUNT GETS CREAMED IN EDIT                  
         C     R1,=F'999999'       IF AMOUNT TOO BIG TO EDIT                    
         BH    PRAM50                                                           
         BAS   RE,EDIT8                                                         
         B     PRAM60                                                           
*                                                                               
PRAM50   SR    R0,R0               DROP PENNIES                                 
         D     R0,=F'100'                                                       
         BAS   RE,EDIT8A                                                        
*                                                                               
PRAM60   TM    PRCOLL,CHNW         IS H & W IN LEFT COLUMN                      
         BO    PRAM70                                                           
         TM    PRCOLR,CHNW            OR RIGHT COLUMN                           
         BNO   PRAM80                 OR NEITHER                                
         LA    RF,HW-1                                                          
*                                                                               
PRAM70   DS    0H                                                               
         L     R1,TABHNW                                                        
         TM    SVOPT5,TAPDONHW     IF NHW=Y, SUPPRESS PRINTING H&W              
         BO    PRAM75                                                           
         BAS   RE,EDIT8                                                         
PRAM75   CLI   EMSFLAG,1           IF FIRST TIME EMS BILLING,                   
         BNE   PRAM80                                                           
         GOTOR TOTHNW              KEEP TRACK OF HNW IF UNION HAS FUND          
*                                                                               
PRAM80   LA    RF,PH-1             IS THERE MISC/I & R                          
         TM    PRCOLL,CINR1                                                     
         BO    PRAM90                                                           
         LA    RF,HW-1                                                          
         TM    PRCOLR,CINR1                                                     
         BNO   PRAM100                                                          
*                                                                               
PRAM90   L     R1,TABMDED          ADD MISC DED AND DUES TO I & R               
         A     R1,TABDUES                                                       
         A     R1,TABINR                                                        
         BAS   RE,EDIT8                                                         
*                                                                               
PRAM100  LA    RF,PH-1             I & R                                        
         TM    PRCOLL,CINR2                                                     
         BO    PRAM110                                                          
         LA    RF,HW-1                                                          
         TM    PRCOLR,CINR2                                                     
         BNO   PRAM120                                                          
*                                                                               
PRAM110  L     R1,TABINR                                                        
         BAS   RE,EDIT8                                                         
*                                                                               
PRAM120  LA    RF,PH-1             MISC DEDUCTIONS                              
         TM    PRCOLL,CMDED                                                     
         BO    PRAM130                                                          
         LA    RF,HW-1                                                          
         TM    PRCOLR,CMDED                                                     
         BNO   PRAM140                                                          
*                                                                               
PRAM130  CLI   UNICOPY,C'Y'        IF THIS IS A UNION COPY                      
         BNE   PRAM135                                                          
         CLC   =C'ACT',CUNION      & UNION = ACTRA                              
         BNE   PRAM135                                                          
         L     R1,TABMDED          PRINT MISC DED ALONE                         
         BAS   RE,EDIT8                                                         
         LA    RF,DUES-1                                                        
         L     R1,TABDUES          PRINT UNION DUES ALONE                       
         BAS   RE,EDIT11                                                        
         B     PRAM140                                                          
PRAM135  L     R1,TABMDED                                                       
         A     R1,TABDUES                                                       
         BAS   RE,EDIT8                                                         
*                                                                               
PRAM140  LA    RF,PH-1                                                          
         TM    PRCOLL,CINR2        I & R                                        
         BO    PRAM150                                                          
         LA    RF,HW-1                                                          
         TM    PRCOLL,CINR2                                                     
         BNO   PRAMX                                                            
*                                                                               
PRAM150  L     R1,TABINR                                                        
         BAS   RE,EDIT8                                                         
*                                                                               
PRAMX    B     XIT                                                              
         EJECT                                                                  
*======================================================================         
*        SPECIAL CALCULATION FOR BILLING TYPE 7/18, OFFICE 7/G                  
*======================================================================         
*&&DO                                                                           
SPCLHAND NTR1                                                                   
         XC    TBXTRHND,TBXTRHND                                                
         CLI   TGOFF,C'7'          OFFICE 7 / G ONLY                            
         BE    *+8                                                              
         CLI   TGOFF,C'G'                                                       
         BNE   SPCLHNDZ                                                         
*                                                                               
         CLI   GTYPE,TABRTY7       BILLING TYPE 18 AND                          
         BE    *+8                                                              
         CLI   GTYPE,TABRTY18      BILLING TYPE 18 AND                          
         BNE   SPCLHNDZ                                                         
         CLI   TABACDE,APPLGUAR    FOR APPLIED GUARANTEE AND                    
         BNE   SPCLHNDZ                                                         
         OC    TABGUAR,TABGUAR     MUST NOT BE ZERO AMOUNT                      
         BZ    SPCLHNDZ                                                         
*                                                                               
         L     R1,TABGUAR                                                       
         LCR   R1,R1               REVERSE THE SIGN                             
         LR    R2,R1                                                            
         MVC   DUB(2),AYRHAND      AGENCY HANDLING RATE                         
         OC    CLRHAND,CLRHAND                                                  
         BZ    *+10                                                             
         MVC   DUB(2),CLRHAND      CLIENT HANDLING RATE                         
*                                                                               
         MH    R1,DUB                                                           
         CVD   R1,DUB              PACKED NUMBER                                
         SRP   DUB,60,5            DIVIDE BY 1000 AND ROUND                     
         CVB   R1,DUB                                                           
         LR    RF,R1                                                            
         A     R1,TBXTRHND         CHANGE HANDLING                              
         ST    R1,TBXTRHND                                                      
*                                                                               
*                                                                               
         CLI   GTYPE,TABRTY18      BILLING TYPE 18 ONLY                         
         BNE   SPCLHNDX                                                         
         OC    TABPNH,TABPNH       P&H ALREADY CALCULATED?                      
         BNZ   SPCLHNDX                                                         
         LR    R1,R2                                                            
         MVC   WORK(2),TABYR                                                    
         XC    WORK(2),=X'F0F0'    STRIP OFF HIGH NIBBLE                        
         SR    R2,R2               CALCULATE YEAR                               
         IC    R2,WORK                                                          
         MH    R2,=H'10'                                                        
         SR    RF,RF                                                            
         IC    RF,WORK+1                                                        
         AR    R2,RF                                                            
         CH    R2,=H'90'                                                        
         BH    *+8                                                              
         LA    R2,100(R2)                                                       
         STC   R2,WORK             SAVE IT IN WORK                              
*                                                                               
         LA    RF,YEARTAB                                                       
         USING YEARD,RF                                                         
SPCLHNDA CLI   0(RF),0             EOT, DON'T GET P&H CALC                      
         BE    SPCLHNDX                                                         
         CLC   YEARYR,WORK         MAKE SURE YEARS MATCH                        
         BE    SPCLHNDB                                                         
         LA    RF,YEARNEXT                                                      
         B     SPCLHNDA                                                         
*                                                                               
SPCLHNDB SR    R0,R0                                                            
         ICM   R0,3,YEARPNHT       P&H RATE                                     
         MR    R0,R0               PHANTOM P&H AMOUNT                           
         CVD   R1,DUB                                                           
         SRP   DUB,60,5                                                         
         CVB   R1,DUB                                                           
         MVC   DUB(2),AYRHAND      AGENCY HANDLING RATE                         
         OC    CLRHAND,CLRHAND                                                  
         BZ    *+10                                                             
         MVC   DUB(2),CLRHAND      CLIENT HANDLING RATE                         
*                                                                               
         MH    R1,DUB                                                           
         CVD   R1,DUB              PACKED NUMBER                                
         SRP   DUB,60,5            DIVIDE BY 1000 AND ROUND                     
         CVB   R1,DUB                                                           
         A     R1,TBXTRHND                                                      
         ST    R1,TBXTRHND                                                      
*                                                                               
SPCLHNDX L     R1,TBHANDI                                                       
         A     R1,TBXTRHND                                                      
         ST    R1,TBHANDI                                                       
         ST    R1,PRTHAND                                                       
         XC    TBXTRHND,TBXTRHND                                                
*                                                                               
SPCLHNDZ B     YES                                                              
*                                                                               
         DROP  RF                                                               
*&&                                                                             
         EJECT                                                                  
*                                                                               
*        PRINT 2 LINE IF NECESSARY                                              
*                                                                               
PR2NDLN  NTR1                                                                   
         CLI   TABXLN,0            IF THERE'S AN EXTRA LINE TO PRINT            
         BE    PR2LX                                                            
         LH    R1,CKREC2           DECREMENT # OF DOUBLE LINES                  
         BCTR  R1,0                                                             
         STH   R1,CKREC2                                                        
*                                                                               
         ICM   R1,15,TABOV2       IS THERE A 2ND OV%                            
         BZ    PR2L10                                                           
         SR    R0,R0                                                            
         D     R0,=F'100'                                                       
         LA    RF,P2+OV-P4LINED                                                 
         EDIT  (R1),(3,(RF))       PUT OV2% ON NEXT LINE                        
*                                                                               
PR2L10   CLI   TABCNUM,C' '        CORP OR NOT                                  
         BNH   PR2L20                                                           
         MVC   P2+SSN-P4LINED(L'TABCID),TABCID        CORP ID NUMBER            
         MVC   P2+NAME-P4LINED(L'TABCNAME),TABCNAME   CORP NAME                 
         MVC   P2+OV-P4LINED(3),=C'CRP'                                         
*                                                                               
         TM    TGSYSTAT,TASYSPID   USING PID NUMBERS?                           
         BZ    PR2L20                                                           
         CLC   TGAGY,=CL6'8800'    IF AGENCY IS 8800, SS#                       
         BE    PR2L20                                                           
         CLI   GTYPE,TABRTYE       EMS WANTS SS#                                
         BE    PR2L20                                                           
         CLI   UNICOPY,C'Y'        AND THIS IS NOT UNION COPY                   
         BE    PR2L20                                                           
         MVC   P2+SSN-P4LINED(L'TABCID),SPACES                                  
         LA    RF,P2+SSN-P4LINED                                                
         GOTO1 SSNPACK,DMCB,TABCID,(RF)  CONVERT SSN TO PID                     
*                                                                               
PR2L20   CLI   TABTAG,0            ARE THERE TAGS                               
         BE    PR2LX                                                            
         TM    PERFSTAT,PERFSTAG   IF ALREADY PRINTED ON FIRST LINE             
         BO    PR2LX               EXIT                                         
         CLI   TBPRTINV,C'Y'       FOR PRINT INVOICES                           
         BE    PR2LX               EXIT                                         
         MVC   P2+VARCOL-P4LINED(3),LTTAG1                                      
         ZIC   R1,TABTAG           # OF TAGS                                    
         LA    RF,P2+VARCOL-P4LINED+3                                           
         BAS   RE,EDIT2                                                         
*                                                                               
PR2LX    B     XIT                                                              
         EJECT                                                                  
*        PRINT BOTTOM BOX                                                       
*                                                                               
PRBOT    NTR1                                                                   
         LA    R2,P                POINT TO PRINT LINE                          
         MVI   SUBDONE,C'N'        DEFAULT FLAG-NOT ALL SUBTOTS PRINTED         
         L     R3,ABOX             NOW HANDLE BOXES                             
         USING BOXD,R3                                                          
         MVC   BOXCOLS,SPACES                                                   
         MVI   BOXCOLS,C'L'                                                     
         MVI   BOXCOLS+131,C'R'                                                 
         OI    LSTAT,PRB           SET FLAG - WHICH MIDDLE LINE                 
         BRAS  RE,BXCOL                                                         
         MVI   BOXREQ,C'B'                                                      
         BRAS  RE,PRTIT                                                         
         MVI   LSTAT,0                                                          
         DROP  R3                                                               
*                                                                               
         BAS   RE,P5LINE           PRINT OUT BOTTOM LINES                       
         BAS   RE,P6LINE                                                        
         BRAS  RE,BXBOT            CLOSE BOX                                    
         BRAS  RE,PRTIT               PRINT BOX BOTTOM                          
*                                                                               
         USING P10LINED,R2                                                      
         CLI   UNICOPY,C'Y'        IF THIS IS A UNION COPY                      
         BE    PRB05               PRINT BLANK LINE                             
         TM    SVOPT1,TAPDOCAN     ELSE, IF CANADIAN TAX WITHHELD               
         BNO   PRB05                                                            
         MVC   P10CTX,LTCANTAX     PRINT MESSAGE                                
PRB05    BRAS  RE,PRRET                                                         
         BRAS  RE,PRTIT                                                         
*                                                                               
         CLI   UNICOPY,C'Y'        IF THIS IS A UNION COPY                      
         BE    PBX                 EXIT                                         
*                                                                               
         USING P7LINED,R2                                                       
         TM    STATUS,STCOD        IF PUR INVOICE                               
         BNO   PRB08                                                            
         CLI   RECNUM,CC           CHLOE COPY                                   
         JE    PRB08                                                            
         MVC   P7COD(L'LTBOXC),LTBOXC   PRINT OUT REMIT TO: LINE                
         CLC   =C'PP ',TGEMP                                                    
         BNE   PRB08                                                            
         MVC   P7COD2(L'LTBOXP),LTBOXP  OFFICE A - Z, PRINT PAYROLL             
**NO-OP  MVC   P7COD(L'LTBOXP),LTBOXP   OFFICE A - Z, PRINT PAYROLL             
*                                                                               
PRB08    OI    LSTAT,ITOT          SET FLAG COMING FROM INV TOTAL LINE          
         TM    TBISTAT,TAPDSCAN    IF THIS IS A CANADIAN PAYMENT                
         BO    PRB09                                                            
         CLI   RECNUM,AC           AND THIS IS AGENCY COPY                      
         BE    *+12                                                             
         CLI   RECNUM,EB           OR E-BILLING                                 
         BNE   PRB10                                                            
         TM    TBCLSTA2,TACISTCA   DOES CLIENT HAVE CAN STATUS?                 
         BO    *+12                                                             
         TM    TBAYSTA6,TAAYSTCA   DOES AGENCY HAVE CAN STATUS?                 
         BZ    PRB10                                                            
PRB09    GOTO1 BXTOP,DMCB,81,123,0     OPEN BOX                                 
         B     PRB20                                                            
*                                                                               
PRB10    GOTO1 BXTOP,DMCB,81,115,0     OPEN BOX                                 
PRB20    MVI   LSTAT,0                                                          
*                                                                               
         BAS   RE,PRBITOT          PRINTS INVOICE TOTAL LINE                    
*                                                                               
         BRAS  RE,PRBLINE          PRINTS BOTTOM LINE OF BOX                    
*                                                                               
         BRAS  RE,PRBAOSL          PRINTS BOTTOM AOS LINE                       
*                                                                               
PBX      MVI   FORCEHED,C'Y'       FORCE PAGE EJECT                             
         B     XIT                                                              
         EJECT                                                                  
*              INVOICE TOTAL LINE OF BOTTOM BOX                                 
*                                                                               
         USING P7LINED,R2          R2=A(PRINT LINE)                             
PRBITOT  NTR1                                                                   
         TM    STATUS,STCOD        IF COD INVOICE                               
         BNO   PRBITOT3                                                         
         CLC   =C'PP ',TGEMP                                                    
         BE    PRBITOT2                                                         
         MVC   P7COD(L'LTCOD1),LTCOD1 PRINT COD PART 1                          
         B     PRBITOT5                                                         
PRBITOT2 MVC   P7COD2(L'LTCOD1),LTCOD1 PRINT COD PART 1                         
         B     PRBITOT5                                                         
*                                                                               
******** CLI   TBTPO,C'7'          OFFICE 7 / G                                 
******** BE    *+8                                                              
******** CLI   TBTPO,C'G'                                                       
******** BNE   PRBITOT3                                                         
******** MVC   P7COD,LTOFF7G                                                    
******** B     PRBITOT5                                                         
*                                                                               
PRBITOT3 TM    TBSTA2,TAINSPRM     IF NOT PRIMARY INVOICE                       
         BO    PRBITOT5                                                         
         TM    STATUS,STCRDT       AND IF NOT CREDIT INVOICE                    
         BO    PRBITOT5                                                         
         CLI   TBAYDAYS,X'FF'      AND DAYS DUE                                 
         BE    PRBITOT5                                                         
*                                                                               
         CLI   RECNUM,CC           CCOPY?  DON'T PRINT DUE NN DAYS              
         BE    PRBITOT5                                                         
*                                                                               
         CLI   TBAYDAYS,99         IF DUE NOW                                   
         BNE   *+14                                                             
         MVC   P7NOW,LTDAYNOW      PRINT 'DUE UPON RECEIPT'                     
         B     PRBITOT5                                                         
         LA    RE,P7DAYS           ELSE, PRINT PAYMENT DUE NN DAYS              
         MVC   0(L'LTDAY1,RE),LTDAY1                                            
         LA    RE,L'LTDAY1+1(RE)                                                
         EDIT  TBAYDAYS,(3,0(RE)),ALIGN=LEFT                                    
         AR    RE,R0                                                            
         LA    RE,1(RE)                                                         
         MVC   0(L'LTDAY2,RE),LTDAY2                                            
*                                                                               
PRBITOT5 MVC   P7INVT,LTITOT       INVOICE TOTAL LITERAL                        
                                                                                
         TM    TBIPST2,TAPDPEUR    IF INVOICE IS FOR A EUROS-BASED              
         BZ    PRBITOT6            PAYMENT                                      
         CLI   RECNUM,AC           AND THIS IS AGENCY COPY                      
         BNE   PRBITOT6                                                         
         MVC   P7INVT,LTITOTE      INVOICE TOTAL EUROS LITERAL                  
                                                                                
PRBITOT6 L     R1,TBTOT                                                         
         CLI   EMSFLAG,2           AGROUP EMS, ACOPY - 2ND RUN                  
         BNE   *+8                                                              
         L     R1,PRTEMST          TOTAL STORED FROM TABD2 ELEMENT              
         CLI   RECNUM,AC           IF THIS IS AGENCY COPY                       
         BE    *+12                                                             
         CLI   RECNUM,EB           OR E-BILLING                                 
         BNE   PRBITOT7                                                         
         TM    TBCLSTA2,TACISTCA   DOES CLIENT HAVE CAN STATUS?                 
         BO    *+12                                                             
         TM    TBAYSTA6,TAAYSTCA   DOES AGENCY HAVE CAN STATUS?                 
         BZ    PRBITOT7                                                         
         MVC   P7US(3),=C'CAN'     PRINT TOTAL IN CAN$                          
         TM    TBISTAT,TAPDSCAN    IF THIS IS A CANADIAN PAYMENT                
         BZ    *+8                                                              
         L     R1,TBCSTOT          PRINT CANADIAN SUBTOTAL                      
         LA    RF,P7TOT-1                                                       
         BAS   RE,EDIT11N                                                       
         B     PRBITOT9                                                         
PRBITOT7 LA    RF,P7TOT-1                                                       
         BAS   RE,EDIT11N                                                       
         TM    TBISTAT,TAPDSCAN    IF THIS IS A CANADIAN PAYMENT                
         BNO   *+10                                                             
         MVC   P7US(4),=C'U.S.'                                                 
PRBITOT9 CLI   EMSFLAG,1           IF AGROUP IS EMS, 1ST RUN                    
         BE    *+12                                                             
         BRAS  RE,PRTIT                                                         
         B     XIT                                                              
         LA    RE,BLOCK                                                         
         USING RECD,RE                                                          
         MVC   RECITOT,P7TOT-1      STORE INVOICE TOTAL IN BLOCK                
         DROP  RE                                                               
         B     XIT                                                              
         EJECT                                                                  
*        PRINT OUT 1ST BOTTOM LINE                                              
*                                                                               
P5LINE   NTR1                                                                   
         LA    R2,P                POINT TO PRINT LINE                          
         USING P5LINED,R2                                                       
         CLI   TBPRTINV,C'Y'       IF PRINT INVOICE                             
         BE    P5L10               SKIP                                         
         CLI   TBMED,TACOMEDE      IF MEDIA = EVENT, SKIP                       
         BE    P5L10                                                            
         MVC   P5PHR,LTPHR         PENSION & HEALTH REMITTANCE                  
         MVC   P5APLEG,LTAPLEG     APPLY LEGEND                                 
         MVC   P5INCL,LTINCL       INCLUDE LEGEND                               
*                                                                               
P5L10    CLI   TBMED,TACOMEDE      IF MEDIA = EVENT,                            
         BNE   P5L15                                                            
         MVC   P5TOT2(6),LTTOT     TOTALS                                       
         L     R1,TOTGRS                                                        
         LA    RF,P5TAXWG-1                                                     
         B     P5L17                                                            
*                                                                               
P5L15    MVC   P5TOT(6),LTTOT      TOTALS                                       
         L     R1,TOTGRS                                                        
         LA    RF,P5GROSS-1                                                     
P5L17    CLI   UNICOPY,C'Y'        IF THIS IS A UNION COPY                      
         BNE   P5L20                                                            
         CLC   =C'ACT',CUNION      & UNION = ACTRA                              
         BNE   P5L20                                                            
         A     R1,TOTMISC          ADD IN MISC TOTALS                           
         BAS   RE,EDIT12N                                                       
         B     P5L30                                                            
P5L20    BAS   RE,EDIT12N                                                       
         L     R1,TOTMISC                                                       
         LA    RF,P5MISC-1                                                      
         CLI   TBMED,TACOMEDE      IF MEDIA = EVENT,                            
         BNE   P5L25                                                            
         LA    RF,P5NTXWG-1                                                     
         BAS   RE,EDIT12N          NON TAXABLE WAGES                            
         L     R1,TOTTXNW                                                       
         LA    RF,P5TXNWG-1                                                     
         BAS   RE,EDIT12N          TAXABLE NON WAGES                            
         L     R1,TOTNTXNW                                                      
         LA    RF,P5NTNWG-1                                                     
         BAS   RE,EDIT12N          NON TAXABLE NON WAGES                        
         B     P5L30                                                            
P5L25    BAS   RE,EDIT11N                                                       
*                                                                               
P5L30    LA    RF,P5PNH-1                                                       
         TM    PRCOLL,CPNH         IS PNH IN LEFT TITLE                         
         BNO   P5L60                                                            
         L     R1,TBPNH1           P & H CONTRIBUTIONS                          
         A     R1,TBPNH2                                                        
         A     R1,TBPNH3                                                        
         A     R1,TBPNH4                                                        
*                                                                               
P5L50    BAS   RE,EDAMT            EDIT AMOUNT                                  
*                                                                               
P5L60    BAS   RE,PRBCOL           PRINT RIGHT HAND COLUMNS                     
         CLI   EMSFLAG,1           IF AGROUP IS EMS, DON'T PRINT                
         BNE   P5L70                                                            
         GOTOR GETTOTS             GET TOTALS FOR DATASET                       
         B     P5LX                                                             
P5L70    BRAS  RE,PRTIT                                                         
         BAS   RE,PRBUNI           PRINT UNION INFO                             
         BAS   RE,SUBTMAST                                                      
P5LX     B     XIT                                                              
         DROP  R2                                                               
         EJECT                                                                  
*                                                                               
*        PRINT OUT BOTTOM LINES                                                 
*                                                                               
P6LINE   NTR1                                                                   
         XC    PRTSIGNF,PRTSIGNF                                                
         XC    PRTCSF,PRTCSF                                                    
*                                                                               
         USING P6LINED,R2                                                       
         LA    R2,P                POINT TO PRINT LINE                          
         CLI   TBPRTINV,C'Y'       IF PRINT INVOICE                             
         BE    P6L02                  SKIP                                      
         CLI   TBMED,TACOMEDE      IF MEDIA = EVENT, SKIP                       
         BE    P6L02                                                            
         MVC   P6APLEG(9),LTSESS   SESSION                                      
         MVC   P6INCL(14),LTWARD   WARDROBE                                     
*                                                                               
P6L02    MVC   P6TOT,LTGNM         GROSS & MISC PAYMENTS                        
         L     R1,TOTGRS                                                        
         A     R1,TOTMISC                                                       
         CLI   TBMED,TACOMEDE      IF MEDIA = EVENT,                            
         BNE   *+12                                                             
         A     R1,TOTTXNW          ADD TAXABLE NON WAGES                        
         A     R1,TOTNTXNW         ADD NON TAXABLE NON WAGES                    
         ST    R1,TBGM                                                          
         LA    RF,P6MISC-1                                                      
         BAS   RE,EDIT11N                                                       
*                                                                               
*&&DO                                                                           
         BRAS  RE,CHEMTEST         IF AGENCY IS CHEMISTRI                       
         BNE   P6L02A                                                           
         MVC   P6WCDEC,CH10712     WORK CODE IS 10-712                          
         CLI   TGUSEQU,UGRT        IF USE IS GRT                                
         BE    P6L02A                                                           
         MVC   P6WCDEC,CH10696     WORK CODE IS 10-696                          
         TM    TGUSSTAT,SESSION    IF USE IS SESSION                            
         BO    P6L02A                                                           
         MVC   P6WCDEC,CH10704     ELSE, WORK CODE IS 10-704                    
*&&                                                                             
*                                                                               
P6L02A   BRAS  RE,GMWKCTST         IF PRINTING GM WORKCODES                     
         BNE   *+10                                                             
         MVC   P6GMWCDE,RTPGMP49   WORK CODE IS RTPGM-P49                       
*                                                                               
         BRAS  RE,GMWKCT2          IF PRINTING NEW GM WORKCODES                 
         BNE   P6L02AA                                                          
         MVC   P6GMWCDE,RTP10696   SESSION WORK CODE IS 10-696                  
         TM    TGUSSTAT,SESSION                                                 
         BO    P6L02AA                                                          
         MVC   P6GMWCDE,RTP10704   REUSE WORK CODE IS 10-704                    
*                                                                               
P6L02AA  CLI   EMSFLAG,1           AGROUP EMS                                   
         BNE   *+14                                                             
         LA    RE,BLOCK                                                         
         USING RECD,RE                                                          
         MVC   RECWAGM,P6MISC-1    STORE GROSS & MISC IN BLOCK                  
         DROP  RE                                                               
*                                                                               
         MVI   BYTE,C'W'                                                        
         BRAS  RE,CHKIF            CHECK IF WANT WORK CODES ON BILL             
*                                                                               
         L     R1,TBGM                                                          
         BRAS  RE,SUMTOT           SUM TOTALS                                   
         CLI   EMSFLAG,1           AGROUP EMS - DON'T PRINT                     
         BE    *+8                                                              
         BRAS  RE,PRTIT                                                         
*                                                                               
         CLI   TBPRTINV,C'Y'       IF NOT A PRINT INVOICE                       
         BE    P6L03                                                            
         CLI   TBMED,TACOMEDE      IF MEDIA = EVENT, SKIP                       
         BE    P6L03                                                            
         CLI   GTYPE,TABRTY20      AND BILLING TYPE 20                          
         BNE   P6L02C                                                           
         CLI   UNICOPY,C'Y'        AND NOT UNION COPY                           
         BE    P6L02C                                                           
         MVC   P6COMP1,LTCOMP      COMPANY                                      
         MVC   P6COMP2,TBEMNAME    COMPANY NAME                                 
         MVC   P6UNI1,LTUNION      UNION                                        
         MVC   P6UNI2(L'TBUNI1),TBUNI1                                          
         OC    TBUNI2,TBUNI2       IS THERE A 2ND UNION                         
         BZ    P6L02B                                                           
         CLC   TBUNI2,P6UNI2       SAME (WITH DIFF LOCAL)                       
         BE    P6L02B                                                           
         MVC   P6UNI2+L'TBUNI1+1(L'TBUNI2),TBUNI2                               
         B     P6L02C                                                           
P6L02B   OC    TBUNI3,TBUNI3       IS THERE A 3RD UNION                         
         BZ    P6L02C                                                           
         CLC   TBUNI3,P6UNI2       SAME (WITH DIFF LOCAL)                       
         BE    P6L02C                                                           
         MVI   P6UNI2+L'TBUNI1,C'*'                                             
*                                                                               
P6L02C   MVC   P6APLEG(13),LTHOLD  HOLDING FEE                                  
         CLI   TBMED,TACOMEDR      IF MEDIA = RADIO                             
         BNE   *+10                                                             
         MVC   P6APLEG(13),LTRGRT  CYCLE GUAARANTEE                             
         MVC   P6INCL(14),LTMEAL   MEAL PENALTY                                 
*                                                                               
P6L03    BAS   RE,SUBT             WHICH SUBTOTAL COMES NEXT                    
         CLI   EMSFLAG,1           AGROUP EMS - DON'T PRINT                     
         BE    *+8                                                              
         BRAS  RE,PRTIT                                                         
*                                                                               
         CLI   TBPRTINV,C'Y'       IF PRINT INVOICE                             
         BE    P6L05                  SKIP                                      
         CLI   TBMED,TACOMEDE      IF MEDIA = EVENT, SKIP                       
         BE    P6L05                                                            
         MVC   P6COMP1,LTACC       ACCOUNT                                      
         MVC   P6GPAY,LTGPAY       GROSS PAYMENT                                
         MVC   P6UNI1(12),LTCONT   CONTRIBUTION                                 
         MVC   P6APLEG(12),LTWSU   WS UPGRADE                                   
         MVC   P6INCL(9),LTCART    CARTAGE                                      
*                                                                               
P6L05    BAS   RE,SUBT             WHICH SUBTOTAL COMES NEXT                    
         CLI   EMSFLAG,1           AGROUP EMS - DON'T PRINT                     
         BE    *+8                                                              
         BRAS  RE,PRTIT                                                         
*                                                                               
         CLI   TBPRTINV,C'Y'       IF PRINT INVOICE                             
         BE    P6L65                  SKIP                                      
         CLI   TBMED,TACOMEDE      IF MEDIA = EVENT, SKIP                       
         BE    P6L65                                                            
         CLI   UNICOPY,C'Y'        IS THIS A UNION COPY                         
         BNE   P6L50               THEN ONLY WRITE THE CURRENT UNION            
         LA    R1,TBUNI1                                                        
         SR    R4,R4               INDEX TO GROSS PAY                           
         CLI   UNIFILT,C'N'        IF REQUESTING NON-TURNAROUNDS                
         BNE   P6L10                                                            
         CLC   =C'AFT',CUNION      & UNION = AFTRA                              
         BE    P6L20                                                            
         CLC   =C'SAG',CUNION      OR SAG                                       
         BE    P6L20               EVERYHTING IS IN 1ST UNION                   
*                                                                               
P6L10    CH    R4,=H'3'            LAST UNION - 'OTHERS'                        
         BE    P6L80                                                            
         CLC   CUNION(6),0(R1)          UNION                                   
         BE    P6L20                                                            
         LA    R4,1(R4)                                                         
         LA    R1,LOCINFLN(R1)                                                  
         B     P6L10                                                            
*                                                                               
P6L20    MVC   P6COMP1(L'TBACC1),L'TBUNI1+L'TBLOC1(R1)                          
         LA    R3,TBGPAY1                                                       
         LTR   R4,R4               BUMP TO CORRECT GROSS PAYMENT                
         BZ    P6L40                                                            
*                                                                               
P6L30    LA    R3,4(R3)                                                         
         BCT   R4,P6L30                                                         
*                                                                               
P6L40    L     R1,0(R3)                                                         
         LA    RF,P6GPAY-1                                                      
         BAS   RE,EDIT12N                                                       
*                                                                               
         L     R1,16(R3)           PNH BELONGING TO CURRENT UNION               
         LA    RF,P6UNI1-1                                                      
         BAS   RE,EDIT12                                                        
         B     P6L60                                                            
*                                                                               
P6L50    MVC   P6COMP1(L'TBACC1),TBACC1                                         
         L     R1,TBGPAY1          AGENCY COPY                                  
         LA    RF,P6GPAY-1                                                      
         BAS   RE,EDIT12                                                        
         L     R1,TBPNH1                                                        
         LA    RF,P6UNI1-1                                                      
         BAS   RE,EDIT12                                                        
*                                                                               
P6L60    MVC   P6APLEG(11),LTGUAR  GUARANTEE                                    
         MVC   P6INCL(7),LTOTH1    OTHER                                        
*                                                                               
P6L65    BAS   RE,SUBT             WHICH SUBTOTAL COMES NEXT                    
         CLI   EMSFLAG,1           AGROUP EMS - DON'T PRINT                     
         BE    *+8                                                              
         BRAS  RE,PRTIT                                                         
*                                                                               
         CLI   TBPRTINV,C'Y'       IF PRINT INVOICE                             
         BE    P6L85                  SKIP                                      
         CLI   TBMED,TACOMEDE      IF MEDIA = EVENT, SKIP                       
         BE    P6L85                                                            
         OC    TBGPAY2,TBGPAY2                                                  
         BNZ   P6L70                                                            
         OC    TBPNH2,TBPNH2                                                    
         BZ    P6L80                                                            
*                                                                               
P6L70    MVC   P6COMP1(L'TBACC2),TBACC2                                         
         L     R1,TBGPAY2                                                       
         LA    RF,P6GPAY-1                                                      
         BAS   RE,EDIT12                                                        
         L     R1,TBPNH2                                                        
         LA    RF,P6UNI1-1                                                      
         BAS   RE,EDIT12                                                        
*                                                                               
P6L80    MVC   P6APLEG(7),LTOTH2   OTHER                                        
         MVC   P6INCL(10),LTSUPP   SUPPLIES                                     
*                                                                               
P6L85    BAS   RE,SUBT             WHICH SUBTOTAL COMES NEXT                    
         CLI   EMSFLAG,1           AGROUP EMS - DON'T PRINT                     
         BE    *+8                                                              
         BRAS  RE,PRTIT                                                         
*                                                                               
         CLI   TBPRTINV,C'Y'       IF PRINT INVOICE                             
         BE    P6L105                 SKIP                                      
         CLI   TBMED,TACOMEDE      IF MEDIA = EVENT, SKIP                       
         BE    P6L105                                                           
         OC    TBGPAY3,TBGPAY3                                                  
         BNZ   P6L90                                                            
         OC    TBPNH2,TBPNH2                                                    
         BZ    P6L100                                                           
*                                                                               
P6L90    MVC   P6COMP1(L'TBACC3),TBACC3                                         
         L     R1,TBGPAY3                                                       
         LA    RF,P6GPAY-1                                                      
         BAS   RE,EDIT12                                                        
         L     R1,TBPNH3                                                        
         LA    RF,P6UNI1-1                                                      
         BAS   RE,EDIT12                                                        
*                                                                               
P6L100   MVC   P6APLEG(7),LTCABL   CABLE                                        
*                                                                               
P6L105   BAS   RE,SUBT             WHICH SUBTOTAL COMES NEXT                    
         CLI   EMSFLAG,1           AGROUP EMS - DON'T PRINT                     
         BE    *+8                                                              
         BRAS  RE,PRTIT                                                         
*                                                                               
         CLI   TBPRTINV,C'Y'       IF PRINT INVOICE                             
         BE    P6L115                 SKIP                                      
         CLI   TBMED,TACOMEDE      IF MEDIA = EVENT, SKIP                       
         BE    P6L115                                                           
         OC    TBGPAY4,TBGPAY4                                                  
         BNZ   P6L110                                                           
         OC    TBPNH4,TBPNH4                                                    
         BZ    P6L120                                                           
*                                                                               
P6L110   MVC   P6COMP1(6),LTLOC4   OTHERS                                       
         L     R1,TBGPAY4                                                       
         LA    RF,P6GPAY-1                                                      
         BAS   RE,EDIT12                                                        
         L     R1,TBPNH4                                                        
         LA    RF,P6UNI1-1                                                      
         BAS   RE,EDIT12                                                        
*                                                                               
P6L115   BAS   RE,SUBT             WHICH SUBTOTAL COMES NEXT                    
         CLI   EMSFLAG,1           AGROUP EMS - DON'T PRINT                     
         BE    *+8                                                              
         BRAS  RE,PRTIT                                                         
*                                                                               
P6L120   BAS   RE,SUBT             FINISH PRINTING ALL SUBTOTALS                
         CLI   SUBDONE,C'Y'                                                     
         BE    P6LX                                                             
         CLI   EMSFLAG,1           AGROUP EMS - DON'T PRINT                     
         BE    *+8                                                              
         BRAS  RE,PRTIT                                                         
         B     P6L120                                                           
*                                                                               
P6LX     B     XIT                                                              
         DROP  R2                                                               
         EJECT                                                                  
SESSWK   NTR1                                                                   
         BRAS  RE,SESSWC                                                        
         B     XIT                                                              
*                                                                               
*        PRINT COLUMNS                                                          
*                                                                               
         USING P5LINED,R2                                                       
PRBCOL   NTR1                                                                   
         L     R1,THNW                                                          
         TM    PRCOLL,CHNW         IS THERE H & W                               
         BNO   PRBC10                                                           
         LA    RF,P5PNH-1                                                       
         B     PRBC20                                                           
*                                                                               
PRBC10   TM    PRCOLR,CHNW                                                      
         BNO   PRBC30                                                           
         LA    RF,P5HNW-1                                                       
*                                                                               
PRBC20   DS    0H                                                               
         TM    SVOPT5,TAPDONHW     SKIP PRINTING H&W COLUMN?                    
         BO    PRBC30                                                           
         BAS   RE,EDAMT            EDIT AMOUNT                                  
*                                                                               
PRBC30   L     R1,TMDED                                                         
         A     R1,TDUES                                                         
         TM    PRCOLL,CMDED        IS THERE MDED                                
         BNO   PRBC40                                                           
         LA    RF,P5PNH-1                                                       
         B     PRBC50                                                           
*                                                                               
PRBC40   TM    PRCOLR,CMDED                                                     
         BNO   PRBC60                                                           
         LA    RF,P5HNW-1                                                       
*                                                                               
PRBC50   BAS   RE,EDAMT            EDIT AMOUNT                                  
*                                                                               
PRBC60   L     R1,TINR                                                          
*        BRAS  RE,SUMTOT                                                        
         TM    PRCOLL,CINR1        IS THERE INR                                 
         BNO   PRBC70                                                           
         LA    RF,P5PNH-1                                                       
         B     PRBC80                                                           
*                                                                               
PRBC70   TM    PRCOLR,CINR1                                                     
         BNO   PRBCX                                                            
         LA    RF,P5HNW-1                                                       
*                                                                               
PRBC80   BAS   RE,EDAMT            EDIT AMOUNT                                  
*                                                                               
PRBCX    B     XIT                                                              
         EJECT                                                                  
*                                                                               
*        PRINT UNION INFO                                                       
*                                                                               
PRBUNI   NTR1                                                                   
         L     R3,ABOX             NOW HANDLE BOXES                             
         USING BOXD,R3                                                          
         OI    LSTAT,PRB2          SET FLAG - WHICH MIDDLE LINE                 
         MVC   BOXCOLS,SPACES                                                   
         MVI   BOXCOLS,C'L'        SET COLUMNS TO OUTLINE ACCUMS                
         MVI   BOXCOLS+131,C'R'                                                 
         BRAS  RE,BXCOL            RESET COLUMNS                                
         MVI   BOXREQ,C'B'         & INSERT MIDDLE LINE                         
         BRAS  RE,PRTIT                                                         
         MVI   LSTAT,0                                                          
*                                                                               
         USING P6LINED,R2                                                       
         CLI   TBPRTINV,C'Y'       IF PRINT INVOICE                             
         BE    PRUX                   DONE                                      
         CLI   TBMED,TACOMEDE      IF MEDIA = EVENT, SKIP                       
         BE    PRUX                                                             
         MVC   P6COMP1,LTCOMP      COMPANY                                      
         MVC   P6COMP2,TBEMNAME    COMPANY NAME                                 
*                                                                               
         CLC   =C'DM ',TGEMP       IF EMPLOYER DM                               
         BNE   PRU05                                                            
         CLC   TGAGY,=CL6'0601'    AND IN AGENCY RANGE                          
         BL    PRU05                                                            
         CLC   TGAGY,=CL6'0699'                                                 
         BH    PRU05                                                            
         MVC   P6COMP2(L'LTDMBBS),LTDMBBS  SET D'ARCY MASIUS B&B                
*                                                                               
PRU05    CLI   UNICOPY,C'Y'                                                     
         BE    *+12                                                             
         CLI   GTYPE,TABRTY20                                                   
         BE    PRUX                                                             
         MVC   P6UNI1,LTUNION      UNION                                        
         CLI   UNICOPY,C'Y'        IS THIS A UNION COPY                         
         BNE   PRU10               THEN ONLY WRITE THE CURRENT UNION            
         MVC   P6UNI2(L'CUNION),CUNION                                          
         MVC   P6UNI2+L'TBUNI1+1(L'CLOCAL),CLOCAL  AND LOCAL                    
         B     PRUX                                                             
*                                                                               
PRU10    MVC   P6UNI2(L'TBUNI1),TBUNI1                                          
PRU11    OC    TBUNI2,TBUNI2       IS THERE A 2ND UNION                         
         BZ    PRU20                                                            
         CLC   TBUNI2,P6UNI2       SAME (WITH DIFF LOCAL)                       
         BE    PRU20                                                            
         MVC   P6UNI2+L'TBUNI1+1(L'TBUNI2),TBUNI2                               
         B     PRUX                                                             
*                                                                               
PRU20    OC    TBUNI3,TBUNI3       IS THERE A 3RD UNION                         
         BZ    PRUX                                                             
         CLC   TBUNI3,P6UNI2       SAME (WITH DIFF LOCAL)                       
         BE    PRUX                                                             
         MVI   P6UNI2+L'TBUNI1,C'*'                                             
*                                                                               
PRUX     B     XIT                                                              
         DROP  R2,R3                                                            
         EJECT                                                                  
*                                                                               
*                                                                               
*        IF AMOUNT TOO LARGE, DROP PENNIED                                      
*                                  R1 - AMOUNT TO EDIT                          
*                                  RF - WHERE TO EDIT                           
*                                                                               
EDAMT    NTR1                                                                   
         C     R1,=F'999999'       IF AMOUNT TOO BIG TO EDIT                    
         BH    EDA10                                                            
         BAS   RE,EDIT8                                                         
         B     EDAX                                                             
*                                                                               
EDA10    SR    R0,R0               DROP PENNIES                                 
         D     R0,=F'100'                                                       
         BAS   RE,EDIT8A                                                        
*                                                                               
EDAX     B     XIT                                                              
         EJECT                                                                  
*                                                                               
*        DETERMINE WHICH SUBTOTAL TO PRINT                                      
*                                                                               
         SPACE 1                                                                
         USING P6LINED,R2                                                       
SUBT     NTR1                                                                   
         CLI   SUBDONE,C'Y'        ALL SUBTOTALS FINISHED                       
         BE    SUBX                                                             
         TM    TBISTAT,TAPDSCAN    IF THIS IS A CANADIAN PAYMENT                
         BNO   ST20                                                             
         BAS   RE,PRCAN            PRINT CANADIAN INFO                          
         BE    SUBX                                                             
         B     ST900                                                            
*                                                                               
ST20     CLI   UNICOPY,C'Y'        IF THIS IS A UNION COPY                      
         BE    ST30                   DON'T PRINT TAXES                         
         BAS   RE,SUBTAX           PRINT TAXES                                  
         BE    SUBX                                                             
*                                                                               
ST30     BAS   RE,SUBPNH           PRINT PNH                                    
         BE    SUBX                                                             
         CLI   UNICOPY,C'Y'        IF THIS IS A UNION COPY                      
         BE    ST40                   DON'T PRINT FICA CREDIT                   
         BAS   RE,SUBFICA          PRINT FICR                                   
         BE    SUBX                                                             
*                                                                               
ST40     CLI   SUB,C'Y'            WAS IT PRINTED                               
         BE    ST60                                                             
         MVI   SUB,C'Y'            FLAG IT PRINTED                              
         CLI   GTYPE,TABRTY6       ONLY PRINT SUBTOTAL FOR                      
         BE    ST50                     TYPES 6, 8, AND 21                      
         CLI   GTYPE,TABRTY8                                                    
         BE    ST50                                                             
         CLI   GTYPE,TABRTY21                                                   
         BNE   ST60                                                             
*                                                                               
ST50     ICM   R1,15,TBSTOT        SUBTOTAL                                     
         BZ    ST60                                                             
         MVC   P6TOT(9),LTSTOT                                                  
         L     R1,TBSTOT                                                        
         LA    RF,P6MISC-1                                                      
         BAS   RE,EDIT11                                                        
         B     SUBX                                                             
*                                                                               
ST60     BAS   RE,SUBGSTU          GST PAID ON US$ INVOICES                     
         BE    SUBX                                                             
*                                                                               
         CLI   UNICOPY,C'Y'        IF THIS IS A UNION COPY                      
         BE    ST100                  DON'T PRINT HANDLING FEE                  
         CLI   HND,C'Y'            WAS IT PRINTED                               
         BE    ST95                                                             
*                                                                               
         CLI   TBTPO,C'O'          OFFICE O?                                    
         BE    *+12                                                             
         CLI   TBTPO,C'Q'          OFFICE Q?                                    
         BNE   ST70                                                             
         CLI   RECNUM,CC           CCOPY                                        
         BNE   ST70                                                             
         TM    TBAYSTA6,TAAYSPLH   SEPARATE HANDLING?                           
         BO    *+8                                                              
         TM    TBCLSTA2,TACISPLH                                                
         BZ    ST70                                                             
*                                                                               
         USING TABDD,R4                                                         
         CLI   HND,C'N'                                                         
         BNE   ST63                                                             
         GOTOR GETBDEL,DMCB,AIO1                                                
         BE    ST64                                                             
         B     ST70                                                             
*                                                                               
ST63     L     R4,AIO1                                                          
         MVI   ELCODE,TABDELQ2                                                  
         BAS   RE,GETEL                                                         
         BNE   ST70                                                             
*                                                                               
ST64     L     RF,TABDHND                                                       
         CLI   HND,C'N'                                                         
         BE    ST65                                                             
         S     RF,PRTHAND                                                       
ST65     ST    RF,PRTHAND                                                       
*                                                                               
         CLI   HND,C'S'            TP HANDLING PRINTED ALREADY?                 
         BE    ST70                FINISH HANDLING                              
*                                                                               
         MVI   HND,C'S'            TP HANDLING MODE                             
         B     ST73                                                             
*                                                                               
ST70     MVI   HND,C'Y'            FLAG IT PRINTED                              
ST73     TM    STAT2,STCORP        IF THERE IS A CORP ON INVOICE                
         BO    ST75                PRINT HANDLING                               
         ICM   R1,15,PRTHAND       ELSE DON'T PRINT IF HANDLING = 0             
         BZ    ST95                                                             
*                                                                               
ST75     MVI   BYTE,C'H'                                                        
         BRAS  RE,CHKIF            CHECK IF MANT WORK CODES ON BILL             
         CLI   RECNUM,CC           CCOPY                                        
         BNE   ST76                                                             
         BRAS  RE,GETBTYP2         IF CHLOE COPY, GET 2ND BILLING TYPE          
         BE    ST95                TYPES 9 & 13 ALREADY COUNTED                 
         B     ST77                                                             
*                                                                               
ST76     CLI   GTYPE,TABRTY9       TYPES 9 & 13 ALREADY COUNTED                 
         BE    ST95                                                             
         CLI   GTYPE,TABRTY13      HANDLING IN TAX/HANDLING FIELD               
         BE    ST95                                                             
*                                                                               
ST77     L     R1,TBTOT            INVOICE TOTAL ONLY                           
         A     R1,PRTHAND                                                       
         ST    R1,TBTOT                                                         
*                                                                               
         CLI   HND,C'S'                                                         
         BE    ST78                                                             
         CLI   GTYPE,TABRTY11                                                   
         BNE   ST80                                                             
ST78     MVC   P6TOT(15),LTTALENT  TALENT PARTNERS                              
         B     ST90                                                             
*                                                                               
ST80     CLI   GTYPE,TABRTY20                                                   
         BE    ST95                                                             
         MVC   P6TOT(8),LTHAND     HANDLING                                     
*                                                                               
ST90     L     R1,PRTHAND                                                       
         LA    RF,P6MISC-1                                                      
         BAS   RE,EDIT11                                                        
*                                                                               
******** BAS   RE,CHEMWC           PRINT OUT CHEMISTRI WORK CODES               
*                                                                               
         BRAS  RE,GMWKCTST         IF PRINTING GM WORKCODES                     
         BNE   *+10                                                             
         MVC   P6GMWCDE,RTPGMP52   WORK CODE IS RTPGM-P52                       
*                                                                               
         BRAS  RE,GMWKCT2          IF PRINTING NEW GM WORKCODES                 
         BNE   ST91                                                             
         MVC   P6GMWCDE,RTP10696   SESSION WORK CODE IS 10-696                  
         TM    TGUSSTAT,SESSION                                                 
         BO    ST91                                                             
         MVC   P6GMWCDE,RTP10704   REUSE WORK CODE IS 10-704                    
*                                                                               
ST91     CLI   EMSFLAG,1              AGROUP EMS ONLY - 1ST RUN                 
         BNE   SUBX                                                             
         LA    RE,BLOCK                                                         
         USING RECD,RE                                                          
         MVC   RECHAND,P6MISC-1       STORE HANDLING IN BLOCK                   
         B     SUBX                                                             
         DROP  RE                                                               
*                                                                               
ST95     CLI   EMSFLAG,2           AGROUP EMS, ACOPY - 2ND RUN                  
         BNE   ST100                                                            
         CLI   EMSF,C'Y'            WAS IT PRINTED                              
         BE    ST100                                                            
         MVI   EMSF,C'Y'            FLAG IT PRINTED                             
         MVC   P6TOT(7),LTEMSF     EMS FEE                                      
         L     R1,PRTEMSF                                                       
         LA    RF,P6MISC-1                                                      
         BAS   RE,EDIT11                                                        
         B     SUBX                                                             
*                                                                               
ST100    CLI   ACTWRK,C'Y'         WAS IT PRINTED                               
         BE    ST110                                                            
         MVI   ACTWRK,C'Y'         FLAG IT PRINTED                              
         OC    TBCACTWK,TBCACTWK   DON'T PRINT IF AMOUNT=0                      
         BZ    ST110                                                            
         MVC   P6TOT(10),LTACTWRK  ACTRAWORKS                                   
         L     R1,TBCACTWK                                                      
         LA    RF,P6MISC-1                                                      
         BAS   RE,EDIT11                                                        
         L     R1,TBCACTWK                                                      
         A     R1,TBTOT            ADD TO INVOICE TOTAL                         
         ST    R1,TBTOT                                                         
         MVC   TBACTWKU,TBCACTWK   SAVE IN US$ AREA FOR POSTING                 
         B     SUBX                                                             
*                                                                               
ST110    TM    PARDYST,PARDYACM    AGENCY COMMISSION PRINTED?                   
         BO    ST120                                                            
         OI    PARDYST,PARDYACM                                                 
         CLI   TBMED,TACOMEDE      IF MEDIA = EVENT, PRINT AGY COMM             
         BE    ST115                                                            
*                                                                               
         CLC   TGAGY,=CL6'1094'    IF AGENCY IS 1094, PRINT BBF                 
         BE    ST115               OR                                           
         CLI   GTYPE,TABRTY16      BILLING TYPE 16 ONLY                         
         BNE   ST120                                                            
*                                                                               
ST115    BAS   RE,SUBAGYCM                                                      
         BE    SUBX                                                             
*                                                                               
ST120    TM    PARDYST,PARDYSGF    SIGNATORY FEE PRINTED?                       
         BO    ST130                                                            
         OI    PARDYST,PARDYSGF                                                 
         BRAS  RE,SUBSIGNF                                                      
         BE    SUBX                                                             
*                                                                               
ST130    TM    PARDYST,PARDYCSF    CONTRACT SERVICE FEE PRINTED?                
         BO    ST900                                                            
         OI    PARDYST,PARDYCSF                                                 
         BAS   RE,SUBCSF                                                        
         BE    SUBX                                                             
*                                                                               
ST900    MVI   SUBDONE,C'Y'        ALL SUBTOTALS FINISHED                       
*                                                                               
SUBX     B     XIT                                                              
         EJECT                                                                  
*                                                                               
*        PRINT TAXES                                                            
*                                                                               
SUBTAX   NTR1                                                                   
         CLI   TAX,C'Y'            WAS IT PRINTED                               
         BE    NO                                                               
         MVI   TAX,C'Y'            FLAG IT PRINTED                              
         CLI   EMSFLAG,2           EMS ACOPY - 2ND RUN?                         
         BE    STAX70                                                           
         CLI   RECNUM,CC           CCOPY AND OFFICE O OR Q ONLY                 
         BNE   STAX05                                                           
         TM    TBISTAT,TAPDSCAN    AND CANADIAN PAYMENT                         
         BO    STAX70                                                           
*                                                                               
STAX05   GOTO1 EXTRCT,DMCB,(RC)    GENERAL EXTRACT TEST                         
         BNE   STAX10                                                           
         USING TABDD,R4                                                         
         GOTOR GETBDEL,DMCB,AIO1   IF THERE IS A BILL DETAILS ELEMENT           
         BNE   STAX10              USE IT'S INFO                                
         MVC   PRTTAX,TABDTAX                                                   
         MVC   TBTAX,TABDTAX                                                    
         MVC   FICR,TABDFICR                                                    
         MVC   TBHANDI,TABDHND     IF AMOUNTS SET IN TABD                       
         MVC   TBHANDC,TABDHNDC                                                 
         MVC   PRTHAND,TABDHND     EXTRACT THEM                                 
         MVC   PRTHANDI,TABDHND                                                 
         MVC   PRTHANDC,TABDHNDC                                                
         MVC   PRTSIGNF,TABDSIGN                                                
         MVC   PRTCSF,TABDCSF                                                   
         DROP  R4                                                               
*                                                                               
*&&DO                                                                           
         TM    TBISTA2,TAPDSSUB                                                 
         BZ    STAX65                                                           
*                                                                               
         USING TAPAD,R4                                                         
         MVI   ELCODE,TAPAELQ                                                   
         GOTO1 GETL,DMCB,(1,=AL1(TAPATFEE))                                     
         BNE   STAX65                                                           
         L     R4,TGELEM                                                        
         ICM   RE,15,TAPADATA                                                   
         A     RE,PRTHANDC                                                      
         ST    RE,PRTHANDC                                                      
         DROP  R4                                                               
*&&                                                                             
         B     STAX65                                                           
*                                                                               
STAX10   TM    BOVRSTAT,BOVRTAX    BOVER TAX AMOUNT?                            
         BO    STAX15                                                           
         TM    BOVRSTAT,BOVRHNDB+BOVRHNDP+BOVRHNDC BOVER HAND AMOUNT?           
         BNZ   STAX11                                                           
         TM    SVOPT1,TAPDOTAX     OVERRIDE TAX AMOUNT                          
         BO    STAX15                                                           
         TM    SVOPT1,TAPDOHND     IF NO TAX, BUT HANDLING                      
         BNO   STAX30                                                           
STAX11   CLI   GTYPE,TABRTY1       IF BILLING TYPE 1, 2, 16, OR 20              
         BE    STAX12                                                           
         CLI   GTYPE,TABRTY2                                                    
         BE    STAX12                                                           
         CLI   GTYPE,TABRTY16                                                   
         BE    STAX12                                                           
         CLI   GTYPE,TABRTY20      AND THERE IS A TAX OVERRIDE                  
         BNE   STAX30                                                           
STAX12   TM    STAT2,STCORP        & PAYING AN INDIVIDUAL                       
         BO    STAX30                                                           
         CLC   SVCKNUM,=H'1'       & IF ONLY 1 PERFORMER PAID                   
         BH    STAX30                SET FIELD FROM TABD ELEMENT                
                                                                                
         TM    BOVRSTAT,BOVRHNDB+BOVRHNDP+BOVRHNDC BOVER HAND AMOUNT?           
         BZ    STAX13                                                           
         BRAS  RE,SETHANDI         SET NEW POSTING DETAILS                      
         L     R1,PRTHAND          ADD BOVER HANDLING TO TOTALS                 
         A     R1,FULL                                                          
         ST    R1,PRTHAND                                                       
         L     R1,PRTHANDI                                                      
         A     R1,FULL                                                          
         ST    R1,PRTHANDI                                                      
         B     STAX30              SKIP OLD HANDLING ADJUSTMENT                 
                                                                                
STAX13   MVI   WORK,TAPATHND                                                    
         BRAS  RE,GETPAAMT         GET AMOUNT FROM TAPA ELEMENT                 
         MVC   TBHANDI,FULL                                                     
         MVC   PRTHAND,FULL                                                     
         MVC   PRTHANDI,FULL       SET INDIVIDUAL HANDLING                      
         XC    TBHANDC,TBHANDC                                                  
         XC    PRTHANDC,PRTHANDC   & CLEAR OTHER HANDLING FIELDS                
                                                                                
STAX14   CLI   GTYPE,TABRTY2       ONLY BILLING TYPES 1 AND 20,                 
         BE    STAX30                                                           
         CLI   GTYPE,TABRTY16                                                   
         BE    STAX30                                                           
         L     R1,PRTTAX                                                        
         S     R1,PRTHAND          DEDUCT HANDLING FROM TAX                     
         ST    R1,PRTTAX                                                        
         ST    R1,TBTAX                                                         
         B     STAX30                                                           
*                                                                               
STAX15   TM    BOVRSTAT,BOVRTAX    BOVER TAX OVERRIDE?                          
         BZ    STAX17                                                           
         BRAS  RE,SETTAX           IF NEW WAY, SET OVERRIDE TAX                 
         L     R1,PRTTAX           ADD OVERRIDE TAX TO TOTAL                    
         A     R1,FULL                                                          
         ST    R1,PRTTAX                                                        
         L     RE,FULL                                                          
         GOTOR UPDHANDI,DMCB,(RE)  UPDATE IND. HANDLING IF NEEDED               
         BNE   STAX19                                                           
         TM    BOVRSTAT,BOVRHNDB+BOVRHNDP  IF ONE HANDLING OVERRIDE?            
         BZ    STAX16                                                           
         TM    STAT2,STCORP        IF THERE IS A CORP ON INVOICE,               
         BZ    STAX16                                                           
         L     R1,PRTHANDC         ADD HANDLING OVERRIDE TO CORP HAND           
         A     R1,FULL                                                          
         ST    R1,PRTHANDC                                                      
         B     STAX19                                                           
STAX16   L     R1,PRTHAND          ADD OVERRIDE TAX TO TOTAL                    
         A     R1,FULL                                                          
         ST    R1,PRTHAND                                                       
         L     R1,PRTHANDI                                                      
         A     R1,FULL                                                          
         ST    R1,PRTHANDI                                                      
         B     STAX19                                                           
                                                                                
STAX17   MVI   WORK,TAPATTAX                                                    
         BRAS  RE,GETPAAMT         GET AMOUNT FROM TAPA ELEMENT                 
         MVC   PRTTAX,FULL                                                      
         MVC   TBTAX,FULL                                                       
*                                                                               
STAX19   CLI   GTYPE,TABRTY1       IF BILLING TYPE 1 OR 2                       
         BE    STAX20                                                           
         CLI   GTYPE,TABRTY2       AND THERE IS A TAX OVERRIDE                  
         BE    STAX20                                                           
         CLI   GTYPE,TABRTY16                                                   
         BE    STAX20                                                           
         CLI   GTYPE,TABRTY20                                                   
         BNE   STAX30                                                           
*                                                                               
STAX20   TM    BOVRSTAT,BOVRHNDB+BOVRHNDP+BOVRHNDC BOVER HAND AMOUNT?           
         BNZ   STAX23                                                           
         TM    SVOPT1,TAPDOHND     ELSE IF THERE'S HANDLING OVERRIDE            
         BNO   STAX30                                                           
STAX23   TM    STAT2,STCORP        & PAYING AN INDIVIDUAL                       
         BO    STAX30                                                           
         CLC   SVCKNUM,=H'1'       & IF MORE THAN 1 PERFORMER PAID              
         BH    STAX30                SET FIELD FROM TABD ELEMENT                
*                                                                               
         TM    BOVRSTAT,BOVRHNDB+BOVRHNDP+BOVRHNDC BOVER HAND AMOUNT?           
         BZ    STAX25                                                           
         BRAS  RE,SETHANDI         SET NEW POSTING DETAILS                      
         L     R1,PRTHAND          ADD BOVER HANDLING TO TOTALS                 
         A     R1,FULL                                                          
         ST    R1,PRTHAND                                                       
         L     R1,PRTHANDI                                                      
         A     R1,FULL                                                          
         ST    R1,PRTHANDI                                                      
         B     STAX30                                                           
                                                                                
STAX25   MVI   WORK,TAPATHND                                                    
         BRAS  RE,GETPAAMT         GET AMOUNT FROM TAPA ELEMENT                 
**NO-OP  L     R1,FULL                                                          
**SEP/09 A     R1,TBTAX            ADD IT TO THE TAX OVERRIDE                   
**       ST    R1,PRTTAX                                                        
**NO-OP  ST    R1,TBTAX                                                         
         MVC   TBHANDI,FULL                                                     
         MVC   PRTHAND,FULL                                                     
         MVC   PRTHANDI,FULL       SET INDIVIDUAL HANDLING                      
         XC    TBHANDC,TBHANDC                                                  
         XC    PRTHANDC,PRTHANDC   & CLEAR OTHER HANDLING FIELDS                
*                                                                               
STAX30   TM    SVOPT2,TAPDOFCR     OVERRIDE FICA CREDIT                         
         BNO   STAX50                                                           
         MVI   WORK,TAPATFIC                                                    
         BRAS  RE,GETPAAMT         GET AMOUNT FROM TAPA ELEMENT                 
         MVC   FICR,FULL                                                        
*                                                                               
STAX50   BRAS  RE,CKHAND           SET OVERRIDE HANDLING FEES                   
         BRAS  RE,CKFEE            ADD IN PAYMENT OPTION FEE                    
*                                                                               
STAX65   TM    TBISTAT,TAPDSCAN    IF THIS IS A CANADIAN PAYMENT                
         BO    NO                  DON'T SET PRINT FIELDS                       
         BRAS  RE,SETPRT           SET PRINT FIELDS                             
         BNE   NO                                                               
*                                                                               
         TM    BOVRSTAT,BOVRTAX    IF BOVER TAX OVERRIDE, PRINT IT              
         BO    STAX70                                                           
         TM    SVOPT1,TAPDOTAX     IF OVERRIDE = ZERO THEN PRINT IT             
         BO    STAX70                                                           
         CLI   GTYPE,TABRTY20                                                   
         BE    STAX70                                                           
         CLI   RECNUM,CC           CCOPY AND OFFICE O OR Q ONLY                 
         BNE   STAX68                                                           
         TM    SVOPT4,TAPDOHDP     CHLOE HANDLING RATE OVERRIDE                 
         BO    STAX70                                                           
         TM    SVOPT4,TAPDOHDA     CHLOE HANDLING AMOUNT OVERRIDE               
         BO    STAX70                                                           
                                                                                
STAX68   ICM   R1,15,PRTTAX        PAYROLL TAXES                                
         BNZ   STAX70                                                           
         CLI   RECNUM,CC           CCOPY AND OFFICE O OR Q ONLY                 
         BNE   NO                                                               
**NO-OP  OC    PRTHAND,PRTHAND     NO TAX BUT THERE'S HANDLING                  
**AUG/16 BZ    NO                  (COULD BE CHLOE HANDLING)                    
*                                                                               
STAX70   CLI   RECNUM,CC           CCOPY AND OFFICE O OR Q ONLY                 
         BNE   STAX75                                                           
         CLI   TBTPO,C'O'                                                       
         BE    *+12                                                             
         CLI   TBTPO,C'Q'                                                       
         BNE   STAX75                                                           
         BAS   RE,SETACTOT                                                      
         TM    TBISTAT,TAPDSCAN    AND CANADIAN PAYMENT                         
         BO    NO                                                               
         OC    PRTTAX,PRTTAX                                                    
         BNZ   STAX75                                                           
         OC    PRTHAND,PRTHAND     NO CHLOE TAX BUT THERE'S HANDLING            
         BZ    NO                                                               
*                                                                               
STAX75   L     R1,PRTTAX           PAYROLL TAXES                                
         BRAS  RE,SUMTOT                                                        
         MVC   P6TOT(12),LTTAX13                                                
         CLI   GTYPE,TABRTY13                                                   
         BE    STAX80                                                           
         MVC   P6TOT(13),LTTAX     PAYROLL TAXES                                
         CLI   GTYPE,TABRTY20                                                   
         BNE   STAX80                                                           
         MVC   P6TOT(L'LTTAX20),LTTAX20                                         
         L     R1,PRTTAX                                                        
         A     R1,PRTHAND                                                       
         A     R1,PRTAGYCM                                                      
         B     STAX81                                                           
*                                                                               
STAX80   L     R1,PRTTAX                                                        
STAX81   LA    RF,P6MISC-1                                                      
         BAS   RE,EDIT11                                                        
*                                                                               
******** BAS   RE,CHEMWC           PRINT OUT CHEMISTRI WORK CODES               
*                                                                               
         BRAS  RE,GMWKCTST         IF PRINTING GM WORKCODES                     
         BNE   *+10                                                             
         MVC   P6GMWCDE,RTPGMP52   WORK CODE IS RTPGM-P52                       
*                                                                               
         BRAS  RE,GMWKCT2          IF PRINTING NEW GM WORKCODES                 
         BNE   STAX84                                                           
         MVC   P6GMWCDE,RTP10696   SESSION WORK CODE IS 10-696                  
         TM    TGUSSTAT,SESSION                                                 
         BO    STAX84                                                           
         MVC   P6GMWCDE,RTP10704   REUSE WORK CODE IS 10-704                    
*                                                                               
STAX84   CLI   EMSFLAG,1           AGROUP EMS ONLY - 1ST RUN                    
         BNE   STAX85                                                           
         LA    RE,BLOCK                                                         
         USING RECD,RE                                                          
         MVC   RECPTAX,P6MISC-1    STORE PAYROLL TAXES IN BLOCK                 
         DROP  RE                                                               
STAX85   MVI   BYTE,C'T'                                                        
         BRAS  RE,CHKIF            CHECK IF WANT WORK CODES ON BILL             
         B     YES                                                              
         EJECT                                                                  
*        PRINT PNH                                                              
*                                                                               
SUBPNH   NTR1                                                                   
         CLI   PNH,C'Y'            WAS IT PRINTED                               
         BE    NO                                                               
         MVI   PNH,C'Y'            FLAG IT PRINTED                              
         L     R1,TBPNH1           P & H CONTRIBUTIONS                          
         A     R1,TBPNH2                                                        
         A     R1,TBPNH3                                                        
         A     R1,TBPNH4                                                        
         BZ    NO                                                               
         ST    R1,TFULL                                                         
         BRAS  RE,SUMTOT                                                        
*                                                                               
SPNH60   TM    TBISTAT,TAPDSCAN    IF THIS IS A CANADIAN PAYMENT                
         BNO   SPNH70                                                           
         ST    R1,TBCANPNH         SAVE CANADIAN $ PNH                          
         BAS   RE,SETCVT           SET CONV RATE INTO FULL (BINARY)             
         L     R3,TBCANPNH         CANADIAN$ PNH                                
         GOTO1 MULT,DMCB,(R3),FULL                                              
         MVC   TBCPNHU,0(R1)       = US$ PNH                                    
                                                                                
         TM    STATUS,STBNP        IF THIS IS A BNP PAYMENT                     
         BNO   SPNH69                                                           
         TM    STATUS,STCOD        & NOT COD                                    
         BO    SPNH69                                                           
         CLI   EMSFLAG,3           IF 2ND RUN BILLING EMS,                      
         BE    SPNH69              TABLE WAS ALREADY UPDATED                    
         CLC   TBCPNHU,TBBNPPNH                                                 
         BE    SPNH69                                                           
         L     RF,TBBNPPNH                                                      
         S     RF,TBCPNHU                                                       
         GOTO1 =A(ADJBNP),DMCB,(RF)    ADJUST BNP PNH FOR US$                   
                                                                                
SPNH69   L     R1,TBCANPNH         PRINT CAN$ P&H                               
*                                                                               
SPNH70   MVC   P6TOT(18),LTPNH     P & H CONTRIBUTION                           
         LA    RF,P6MISC-1                                                      
         BAS   RE,EDIT11                                                        
*                                                                               
******** BAS   RE,CHEMWC           PRINT OUT CHEMISTRI WORK CODES               
*                                                                               
         BRAS  RE,GMWKCTST         IF PRINTING GM WORKCODES                     
         BNE   *+10                                                             
         MVC   P6GMWCDE,RTPGMP51   WORK CODE IS RTPGM-P51                       
*                                                                               
         BRAS  RE,GMWKCT2          IF PRINTING NEW GM WORKCODES                 
         BNE   SPNH71                                                           
         MVC   P6GMWCDE,RTP10696   SESSION WORK CODE IS 10-696                  
         TM    TGUSSTAT,SESSION                                                 
         BO    SPNH71                                                           
         MVC   P6GMWCDE,RTP10704   REUSE WORK CODE IS 10-704                    
*                                                                               
SPNH71   CLI   EMSFLAG,1           AGROUP EMS ONLY - 1ST RUN                    
         BNE   SPNH75                                                           
         LA    RE,BLOCK                                                         
         USING RECD,RE                                                          
         MVC   RECPNHC,P6MISC-1    STORE P&H IN BLOCK                           
         DROP  RE                                                               
SPNH75   MVI   BYTE,C'P'                                                        
         BRAS  RE,CHKIF            CHECK IF MANT WORK CODES ON BILL             
         B     YES                                                              
         EJECT                                                                  
*----------------------------------------------------------------------         
*        PRINT AGENCY COMMISSION                                                
*----------------------------------------------------------------------         
SUBAGYCM NTR1                                                                   
*                                                                               
         CLI   TBMED,TACOMEDE      IF MEDIA = EVENT,                            
         BNE   SUBACM10                                                         
         USING TABDD,R4                                                         
         GOTOR GETBDCN,DMCB,AIO1   GET TABD ELEMENT                             
         BNE   SUBACMNO                                                         
         L     R1,TABDACOM                                                      
         LTR   R1,R1                                                            
         BZ    SUBACMNO                                                         
         ST    R1,PRTAGYCM                                                      
         LR    RE,R1                                                            
         LCR   RE,RE               COMPLEMENT POSTING AMOUNT                    
         ST    RE,TACOMM                                                        
         MVC   P6TOT(L'LTAGYCM2),LTAGYCM2                                       
         B     SUBACM85                                                         
*                                                                               
**NO-OP  TM    TBSTA2,TAINSHLR     PUR RELEASED, USED WHAT WAS CALC'D           
SUBACM10 TM    TBSTA2,TAINSHLP     PUR PRINTED, USED WHAT WAS CALC'D            
         BZ    SUBACM30                                                         
*                                                                               
         USING TABDD,R4                                                         
         GOTOR GETBDCN,DMCB,AIO1   GET TABD ELEMENT                             
         BNE   SUBACM30                                                         
         L     R1,TABDACOM                                                      
         B     SUBACM80                                                         
*                                                                               
         USING TMD,R4                                                           
SUBACM30 L     R4,ATALIM                                                        
         USING TARAD,RE                                                         
         L     RE,TMACURRA         GET TARA ELEMENT                             
         LTR   RE,RE               CHECK FOR NEW RATES                          
         BZ    SUBACM40                                                         
         MVC   BYTE,TARACOMM       AGY COMMISSION CALCULATION BASIS             
         MVC   HALF,TARAAACO       AGENCY COMMISSION RATE                       
         B     SUBACM45                                                         
         DROP  R4                                                               
                                                                                
         USING TAEPD,R4                                                         
SUBACM40 MVC   AIO,AIO2                                                         
         GOTO1 RECVAL,DMCB,TLCTCDQ,(X'20',0)                                    
         L     R4,AIO              CONTROL RECORD HAS COMMISSION RATE           
         MVI   ELCODE,TAEPELQ                                                   
         BAS   RE,GETEL                                                         
         BNE   SUBACMNO                                                         
         MVC   BYTE,TAEPCOMM       AGY COMMISSION CALCULATION BASIS             
         MVC   HALF,TAEPRATE       AGENCY COMMISSION RATE                       
         DROP  R4                                                               
                                                                                
SUBACM45 CLI   BYTE,TARACNO                                                     
         BE    SUBACMNO                                                         
         CLI   BYTE,TARACCLI                                                    
         BE    SUBACMNO                                                         
*                                                                               
         TM    TBISTAT,TAPDSCAN    IF THIS IS A CANADIAN PAYMENT                
         BNO   SUBACM50                                                         
         L     R1,TBCSTOT          CALCULATE ON SUBTOTAL W/O GST+PST            
         S     R1,TBCGST                                                        
         S     R1,TBCPST                                                        
         ICM   RF,15,AOSPST                                                     
         SR    R1,RF               SUBTRACT AOSPST                              
         B     SUBACM70                                                         
*                                                                               
SUBACM50 L     R1,TOTGRS           ( GROSS +                                    
         A     R1,TOTMISC          MISC  +                                      
         CLI   BYTE,TARACPAY   ON PAY ONLY?                                     
         BE    SUBACM60                                                         
         A     R1,TBPNH1           P & H )                                      
         A     R1,TBPNH2                                                        
         A     R1,TBPNH3                                                        
         A     R1,TBPNH4                                                        
*                                                                               
SUBACM60 CLI   BYTE,TARACHND       + HANDLING?                                  
         BE    *+16                                                             
         CLI   BYTE,TARACTNH       + TAX AND HANDLING?                          
         BNE   *+16                                                             
         A     R1,TBTAX                                                         
         A     R1,TBHANDC                                                       
         A     R1,TBHANDI                                                       
*                                                                               
SUBACM70 XR    R0,R0                                                            
         LH    RE,HALF                                                          
         MR    R0,RE                                                            
         LHI   RF,5000             4 DECIMAL PLACES                             
         DR    R0,RF                                                            
         LTR   R1,R1               R1=QUOTIENT                                  
         BM    *+8                                                              
         AHI   R1,1                ROUND                                        
         SRA   R1,1                                                             
SUBACM80 ST    R1,PRTAGYCM                                                      
         ST    R1,TACOMM                                                        
*                                                                               
         MVC   P6TOT(L'LTAGYCOM),LTAGYCOM                                       
SUBACM85 LA    RF,P6MISC-1                                                      
         BAS   RE,EDIT11                                                        
*                                                                               
         TM    TBISTAT,TAPDSCAN    IF THIS IS A CANADIAN PAYMENT                
         BNO   SUBACM90                                                         
         L     R1,TBCSTOT                                                       
         A     R1,TACOMM           ADD COMMISSION TOTAL                         
         ST    R1,TBCSTOT          TO CANADIAN $ SUBTOTAL                       
*                                                                               
SUBACM90 MVC   AIO,AIO1                                                         
         L     R1,PRTAGYCM                                                      
         A     R1,TBTOT            ADD TO INVOICE TOTAL                         
         ST    R1,TBTOT                                                         
         B     YES                                                              
*                                                                               
SUBACMNO MVC   AIO,AIO1                                                         
         B     NO                                                               
         EJECT                                                                  
*                                                                               
*----------------------------------------------------------------------         
*        PRINT TALENT MASTERS                                                   
*----------------------------------------------------------------------         
SUBTMAST NTR1                                                                   
         CLI   GTYPE,TABRTY20      IF BILLING TYPE 20                           
         BNE   NO                                                               
         CLI   UNICOPY,C'Y'        AND NOT UNION COPY                           
         BE    NO                                                               
*                                                                               
         XC    PRTAGYCM,PRTAGYCM                                                
                                                                                
         TM    TBSTA2,TAINSHLP     PUR PRINTED, USED WHAT WAS CALC'D            
         BZ    SUBTM05                                                          
*                                                                               
         USING TABDD,R4                                                         
         GOTOR GETBDCN,DMCB,AIO1   GET TABD ELEMENT                             
         BNE   SUBTM05                                                          
         L     R1,TABDACOM                                                      
         B     SUBTM20                                                          
*                                                                               
         USING TMD,R4                                                           
SUBTM05  L     R4,ATALIM                                                        
         USING TARAD,RE                                                         
         L     RE,TMACURRA         GET TARA ELEMENT                             
         LTR   RE,RE               CHECK FOR NEW RATES                          
         BZ    SUBTM10                                                          
         MVC   BYTE,TARACOMM       AGY COMMISSION CALCULATION BASIS             
         B     SUBTM15                                                          
         DROP  R4                                                               
*                                                                               
SUBTM10  MVC   AIO,AIO2                                                         
         GOTO1 RECVAL,DMCB,TLCTCDQ,(X'20',0)                                    
         USING TAEPD,R4                                                         
         L     R4,AIO              & CONTROL RECORD HAS COMMISSION RATE         
         MVI   ELCODE,TAEPELQ                                                   
         BAS   RE,GETEL                                                         
         BNE   SUBTMNO                                                          
         MVC   BYTE,TAEPCOMM                                                    
         DROP  R4                                                               
*                                                                               
SUBTM15  CLI   BYTE,TARACNO                                                     
         BE    SUBTMNO                                                          
         CLI   BYTE,TARACCLI                                                    
         BE    SUBTMNO                                                          
*                                                                               
         L     R1,TPAYI            CALCULATE ON GROSS                           
         A     R1,TPAYC                                                         
         XR    R0,R0                                                            
                                                                                
         USING TLMSTBD,RF                                                       
         LA    RF,TMTAB1                                                        
**NO-OP  LHI   RE,750              TAKE 7.5% ON REUSE PAYMENTS                  
         CLI   TGUSEQU,UGRT                                                     
         BNE   *+8                                                              
         LA    RF,TMTABGRT                                                      
**NO-OP  LHI   RE,500              5.0% ON GRT PAYMENTS                         
                                                                                
                                                                                
SUBTM16  CLI   0(RF),X'FF'                                                      
         JE    XIT                                                              
         CLC   TLMSEFST,TBEFFDTE   FIND EFFECTIVE DATE RANGE                    
         BH    SUBTM18                                                          
         CLC   TLMSEFED,TBEFFDTE                                                
         BNL   SUBTM19                                                          
SUBTM18  AHI   RF,TLMSLNQ                                                       
         J     SUBTM16                                                          
                                                                                
SUBTM19  XR    RE,RE                                                            
         ICM   RE,3,TLMSRATE                                                    
         MR    R0,RE                                                            
         LHI   RF,5000             4 DECIMAL PLACES                             
         DR    R0,RF                                                            
         LTR   R1,R1               R1=QUOTIENT                                  
         BM    *+8                                                              
         AHI   R1,1                ROUND                                        
         SRA   R1,1                                                             
SUBTM20  ST    R1,PRTAGYCM                                                      
         ST    R1,TACOMM                                                        
*                                                                               
         MVC   P6COMP1(30),SPACES                                               
         MVC   P6COMP1(L'LTTLMAST),LTTLMAST                                     
         CLC   TGAGY,=CL6'0585'    IF AGENCY IS 0585 - GRUPO GALLEGOS           
         BNE   SUBTM30                                                          
         MVC   P6COMP1(L'LTLUNAPR),LTLUNAPR  PRINT LUNA PRODUCTIONS             
         LA    RF,P6UNI1                                                        
         B     *+8                                                              
SUBTM30  LA    RF,P6GPAY                                                        
         BAS   RE,EDIT11                                                        
*                                                                               
         TM    TBISTAT,TAPDSCAN    IF THIS IS A CANADIAN PAYMENT                
         BNO   SUBTM50                                                          
         L     R1,TBCSTOT                                                       
         A     R1,TACOMM           ADD COMMISSION TOTAL                         
         ST    R1,TBCSTOT          TO CANADIAN $ SUBTOTAL                       
*                                                                               
SUBTM50  MVC   AIO,AIO1                                                         
         L     R1,PRTAGYCM                                                      
         A     R1,TBTOT            ADD TO INVOICE TOTAL                         
         ST    R1,TBTOT                                                         
         B     YES                                                              
*                                                                               
SUBTMNO  MVC   AIO,AIO1                                                         
         B     NO                                                               
         EJECT                                                                  
*                                                                               
*        TALENT MASTERS RATE TABLE FOR ALL PAY TYPES EXCEPT GRT                 
**DUMMY  DC    H'700',XL6'000000B40131'   7.000%                                
**RATES  DC    H'750',XL6'B40201FFFFFF'   7.500%  LATEST RATE                   
*                                                                               
TMTAB1   DS    0F                                                               
         DC    H'750',XL6'000000FFFFFF'   7.500%  LATEST RATE                   
         DC    X'FF'                                                            
         SPACE 3                                                                
*                                                                               
*        TALENT MASTERS RATE TABLE FOR GRT ONLY                                 
**DUMMY  DC    H'400',XL6'000000B40131'   4.000%                                
**RATES  DC    H'500',XL6'B40201FFFFFF'   5.000%  LATEST RATE                   
*                                                                               
TMTABGRT DS    0F                                                               
         DC    H'500',XL6'000000FFFFFF'   5.000%  LATEST RATE                   
         DC    X'FF'                                                            
         SPACE 3                                                                
*                                                                               
*----------------------------------------------------------------------         
*        PRINT CONTRACT SERVICE FEE                                             
*----------------------------------------------------------------------         
SUBCSF   NTR1                                                                   
         USING TABDD,R4                                                         
         L     R4,AIO              INVOICE MAY HAVE IT CALCULATED               
         GOTOR GETBDCN,DMCB,AIO1   GET TABD ELEMENT                             
         BNE   SCSF03                                                           
         OC    TABDCSF,TABDCSF                                                  
         BZ    SCSF03                                                           
         L     R1,TABDCSF                                                       
         B     SCSF08                                                           
*                                                                               
SCSF03   L     R1,PRTCSF           IF SIGNATORY FEE ALREADY CALCULATED          
         LTR   R1,R1               NO NEED TO CALCULATE AGAIN                   
         BNZ   SCSF10                                                           
*                                                                               
         TM    STAT3,STCSFCR       IF CREDITING CSF FEE,                        
         BZ    SCSF05                                                           
         SR    R1,R1                                                            
         SHI   R1,CSFRATE          NEGATIVE CSF RATE                            
         B     SCSF08                                                           
*                                                                               
SCSF05   TM    STAT2,STCSF         IF CHARGING CSF FEE                          
         BNO   NO                                                               
*                                                                               
         LHI   R1,CSFRATE          CSF RATE                                     
*                                                                               
SCSF08   ST    R1,PRTCSF           SAVE CSF                                     
SCSF10   ST    R1,TCSF                                                          
         OC    TCSF,TCSF                                                        
         BZ    NO                                                               
*                                                                               
         MVC   P6TOT(L'LTCSF),LTCSF                                             
         LA    RF,P6MISC-1                                                      
         BAS   RE,EDIT11                                                        
*                                                                               
         BRAS  RE,GMWKCTST         IF PRINTING GM WORKCODES                     
         BNE   *+10                                                             
         MVC   P6GMWCDE,RTPGMP50   WORK CODE IS RTPGM-P50                       
*                                                                               
         CLI   EMSFLAG,1           AGROUP EMS ONLY                              
         BNE   SCSF20                                                           
         LA    RE,BLOCK                                                         
         USING RECD,RE                                                          
         MVC   RECCSF,P6MISC-1    STORE CSF IN BLOCK                            
*                                                                               
SCSF20   L     R1,PRTCSF                                                        
         A     R1,TBTOT            ADD TO INVOICE TOTAL                         
         ST    R1,TBTOT                                                         
         B     YES                                                              
*                                                                               
         DROP  RE                                                               
         EJECT                                                                  
*                                                                               
*        PRINT FICA CREDITS                                                     
*                                                                               
SUBFICA  NTR1                                                                   
         CLI   FCR,C'Y'            WAS IT PRINTED                               
         BE    NO                                                               
         MVI   FCR,C'Y'            FLAG IT PRINTED                              
*                                                                               
         CLI   GTYPE,TABRTY2       ONLY PRINT FOR TYPE 2                        
         BNE   NO                                                               
         TM    SVOPT2,TAPDOFCR     OVERRIDE = ZERO THEN PRINT                   
         BO    SFIC10                                                           
         ICM   R1,15,FICR          FICA CREDITS                                 
         BZ    NO                                                               
*                                                                               
SFIC10   MVC   P6TOT(12),LTFICR    FICA CREDITS                                 
         L     R1,FICR                                                          
         TM    TBOPTS,TBRERUN      IF THIS IS A RERUN                           
         BO    SFIC20                                                           
         TM    SVOPT2,TAPDOFCR     OR OVERRIDE FICA CREDITS                     
         BO    SFIC20              CORRECT AMOUNT IS IN TABDFICR                
         TM    STATUS,STCRDT       IF CREDIT                                    
         BO    SFIC20              DON'T COMPLEMENT AMOUNT                      
         LCR   R1,R1                                                            
         ST    R1,FICR                                                          
*                                                                               
SFIC20   LA    RF,P6MISC-1                                                      
         BAS   RE,EDIT11N                                                       
         L     R1,FICR                                                          
         BRAS  RE,SUMTOT                                                        
         B     YES                                                              
         EJECT                                                                  
*        PRINT GST FOR US$ INVOICE                                              
*                                                                               
SUBGSTU  NTR1                                                                   
         CLI   GST,C'Y'            WAS IT PRINTED                               
         BE    NO                                                               
         MVI   GST,C'Y'            FLAG IT PRINTED                              
         GOTO1 EXTRCT,DMCB,(RC)    GENERAL EXTRACT TEST                         
         BNE   SGSTU05                                                          
*                                                                               
         USING TABDD,R4                                                         
         GOTOR GETBDEL,DMCB,AIO1                                                
         BNE   SGST05              GET AMOUNT FROM TABD ELEMENT                 
         MVC   TBGSTU,TABDGST                                                   
         MVC   TBPSTU,TABDPST                                                   
*                                                                               
         ICM   RF,15,TBPSTU        SUBTRACT IT FROM PST TOTAL                   
         S     RF,AOSPST                                                        
         STCM  RF,15,TBPSTU                                                     
*                                                                               
         B     SGSTU10                                                          
*                                                                               
SGSTU05  DS    0H                                                               
         TM    SVOPT1,TAPDOGST     OR IF OVERRIDE GST                           
         BZ    SGSTU10                                                          
         MVI   WORK,TAPATGST                                                    
         BRAS  RE,GETPAAMT         GET AMOUNT FROM TAPA ELEMENT                 
         MVC   TBGSTU,FULL                                                      
*                                                                               
SGSTU10  L     R1,TBGSTU           IF GST + PST PAID                            
         A     R1,TBPSTU                                                        
         ICM   RF,15,AOSPST                                                     
         AR    R1,RF                                                            
SGSTU15  LTR   R1,R1                                                            
         BZ    NO                                                               
         A     R1,TBTOT            ADD TO INVOICE TOTAL                         
         ST    R1,TBTOT                                                         
*                                                                               
         L     R1,TBGSTU                                                        
         A     R1,TBPSTU                                                        
         MVC   P6TOT(13),LTGST     GST/PST (HST)                                
         ICM   RF,15,AOSPST                                                     
         AR    R1,RF                                                            
SGSTU18  MVC   P6TOT(13),LTGSTAOS  GST/PST (HST)                                
SGSTU20  LA    RF,P6MISC-1                                                      
         BAS   RE,EDIT11                                                        
*                                                                               
*        MVC   TBPSTAOS,AOSPST     AOS PST IN US$ (FOR POSTING)                 
         LHI   RE,TBPSTAOS-BILLD                                                
         AR    RE,RA                                                            
         MVC   0(4,RE),AOSPST      AOS PST IN US$ (FOR POSTING)                 
*                                                                               
         BRAS  RE,GMWKCTST         IF PRINTING GM WORKCODES                     
         BNE   *+10                                                             
         MVC   P6GMWCDE,RTPGMP52   WORK CODE IS RTPGM-P52                       
                                                                                
         BRAS  RE,GMWKCT2          IF PRINTING NEW GM WORKCODES                 
         BNE   SGSTU30                                                          
         MVC   P6GMWCDE,RTP10696   SESSION WORK CODE IS 10-696                  
         TM    TGUSSTAT,SESSION                                                 
         BO    SGSTU30                                                          
         MVC   P6GMWCDE,RTP10704   REUSE WORK CODE IS 10-704                    
*                                                                               
SGSTU30  CLI   EMSFLAG,1           AGROUP EMS ONLY                              
         BNE   YES                                                              
         LA    RE,BLOCK                                                         
         USING RECD,RE                                                          
         MVC   RECUGST,P6MISC-1    STORE GST IN BLOCK                           
         B     YES                                                              
         DROP  RE                                                               
         EJECT                                                                  
*                                                                               
*        PRINT CANADIAN INFO                                                    
*                                                                               
         USING P6LINED,R2                                                       
PRCAN    NTR1                                                                   
         BAS   RE,SUBTAX            SET OVERRIDES & AMTS FOR RERUNS             
         BE    SUBX                                                             
         BAS   RE,SUBPNH            PRINT P & H CONTRIBUTION                    
         BE    PRCX                                                             
         BAS   RE,SUBINR            PRINT I & R CONTRIBUTION                    
         BE    PRCX                                                             
*                                                                               
         TM    TBOPTS,TBRERUN       IF A NEW INVOICE OR                         
         BNO   PRCN10                                                           
         TM    TBBDSTAT,TABDSCNW    IF A RERUN & BILLED THE NEW WAY             
         BO    PRCN10               GO PRINT THE NEW WAY                        
*                                                                               
         BAS   RE,SUBCAN            PRINT CANADIAN SUBTOTAL                     
         BE    PRCX                                                             
         BRAS  RE,SUBCVT            PRINT CANADIAN CONVERSION RATE              
         BE    PRCX                                                             
         BAS   RE,SUBTOTUS          PRINT US SUBTOTAL                           
         BE    PRCX                                                             
         BAS   RE,SUBCHAND          PRINT HANDLING PERCENT                      
         BNE   NO                                                               
         B     PRCX                                                             
*                                                                               
PRCN10   BAS   RE,SUBCHAND          PRINT HANDLING PERCENT                      
         BE    PRCX                                                             
         BAS   RE,SUBCAN            PRINT SUBTOTAL                              
         BE    PRCX                                                             
         BAS   RE,SUBGST            PRINT CANADIAN GST                          
         BE    PRCX                                                             
         BAS   RE,SUBACTWK          PRINT ACTRAWORKS AMOUNT                     
         BE    PRCX                                                             
*                                                                               
         TM    PARDYST,PARDYACM    AGENCY COMMISSION PRINTED?                   
         BO    PRCN20                                                           
         OI    PARDYST,PARDYACM                                                 
         CLC   TGAGY,=CL6'1094'    IF AGENCY IS 1094, PRINT BBF                 
         BE    PRCN15              OR                                           
         CLI   GTYPE,TABRTY16      BILLING TYPE 16 ONLY                         
         BNE   PRCN20                                                           
PRCN15   BAS   RE,SUBAGYCM                                                      
         BE    PRCX                                                             
*                                                                               
PRCN20   TM    PARDYST,PARDYSGF    SIGNATORY FEE PRINTED?                       
         BO    PRCN30                                                           
         OI    PARDYST,PARDYSGF                                                 
         BRAS  RE,SUBSIGNF                                                      
         BE    PRCX                                                             
*                                                                               
PRCN30   BAS   RE,SUBNCAN           PRINT CANADIAN SUBTOTAL                     
         BE    PRCX                                                             
         BRAS  RE,SUBCVT            PRINT CANADIAN CONVERSION RATE              
         BE    PRCX                                                             
         BAS   RE,SUBTOTUS          CALUCLATE US$ TOTAL                         
         BNE   NO                                                               
*                                                                               
PRCX     B     YES                                                              
         EJECT                                                                  
*                                                                               
*        PRINT CANADIAN I & R                                                   
*                                                                               
SUBINR   NTR1                                                                   
         CLI   INR,C'Y'            WAS IT PRINTED                               
         BE    NO                                                               
         MVI   INR,C'Y'            FLAG IT PRINTED                              
         OC    TOTCINR,TOTCINR                                                  
         BZ    NO                                                               
         MVC   P6TOT(18),LTCINR    CANADIAN I&R CONTRIBUTION                    
         L     R1,TOTCINR          I&R CONTRIBUTION                             
         LA    RF,P6MISC-1                                                      
         BAS   RE,EDIT11                                                        
*                                                                               
         BRAS  RE,GMWKCTST         IF PRINTING GM WORKCODES                     
         BNE   *+10                                                             
         MVC   P6GMWCDE,RTPGMP51   WORK CODE IS RTPGM-P51                       
*                                                                               
         BRAS  RE,GMWKCT2          IF PRINTING NEW GM WORKCODES                 
         BNE   YES                                                              
         MVC   P6GMWCDE,RTP10696   SESSION WORK CODE IS 10-696                  
         TM    TGUSSTAT,SESSION                                                 
         BO    YES                                                              
         MVC   P6GMWCDE,RTP10704   REUSE WORK CODE IS 10-704                    
         B     YES                                                              
         SPACE 3                                                                
*                                                                               
*        PRINT CANADIAN SUBTOTAL                                                
*                                                                               
SUBCAN   NTR1                                                                   
         CLI   SUB,C'Y'            WAS IT PRINTED                               
         BE    NO                                                               
         MVI   SUB,C'Y'            FLAG IT PRINTED                              
         MVC   P6TOT(17),LTCSTOT   CANADIAN SUBTOTAL                            
         L     R1,TOTCINR          I&R CONTRIBUTION                             
         A     R1,TBCANPNH         P&H CONTRIBUTION                             
         A     R1,TBGM             CANADIAN GROSS & MISC (IN CAN$)              
*                                                                               
         TM    TBOPTS,TBRERUN      IF THIS IS A RERUN                           
         BNO   SC10                                                             
         TM    TBBDSTAT,TABDSCNW   & BILLED THE OLD WAY                         
         BNO   SC15                DON'T ADD IN HANDLING                        
*                                                                               
SC10     A     R1,TBHANDC          ADD IN TOTAL HANDLING                        
         MVC   P6TOT(17),SPACES                                                 
         MVC   P6TOT(8),LTSUBTOT   & CHANGE WORDING                             
*                                                                               
SC15     CLI   GTYPE,TABRTY20                                                   
         BNE   *+8                                                              
         A     R1,TBCSTOT                                                       
         ST    R1,TBCSTOT          STORE IN SUBTOTAL                            
         LA    RF,P6MISC-1                                                      
         BAS   RE,EDIT11                                                        
*                                                                               
         L     R3,TOTCINR          I&R CONTRIBUTION                             
         A     R3,TBGM             CANADIAN GROSS & MISC (IN CAN$)              
*                                                                               
         BAS   RE,SETCVT           SET CONV RATE INTO FULL (BINARY)             
         GOTO1 MULT,DMCB,(R3),FULL     * (WAGES + INR)                          
         L     R3,0(R1)                                                         
         ST    R3,TBGMINR          STORE FOR POSTING (US$)                      
         B     YES                                                              
         EJECT                                                                  
*                                                                               
*        PRINT CANADIAN SUBTOTAL                                                
*                                                                               
SUBNCAN  NTR1                                                                   
         CLI   RECNUM,AC           IF THIS IS AGENCY COPY                       
         BE    *+12                                                             
         CLI   RECNUM,EB           OR E-BILLING                                 
         BNE   SNCAN10                                                          
         TM    TBCLSTA2,TACISTCA   AND CLIENT HAS CAN STAT,                     
         BO    NO                                                               
         TM    TBAYSTA6,TAAYSTCA   OR AGENCY HAS CAN STAT,                      
         BO    NO                  DO NOT PRINT THIS LINE                       
SNCAN10  CLI   SBT,C'Y'            WAS IT PRINTED                               
         BE    NO                                                               
         MVI   SBT,C'Y'            FLAG IT PRINTED                              
         MVC   P6TOT(17),LTCSTOT   CANADIAN SUBTOTAL                            
         L     R1,TBCSTOT          CAN$ SUBTOTAL                                
         LA    RF,P6MISC-1                                                      
         BAS   RE,EDIT11                                                        
         B     YES                                                              
         SPACE 3                                                                
*        PRINT CANADIAN GST                                                     
*                                                                               
SUBGST   NTR1                                                                   
         CLI   GST,C'Y'            WAS IT PRINTED                               
         BE    NO                                                               
         MVI   GST,C'Y'            FLAG IT PRINTED                              
         GOTO1 EXTRCT,DMCB,(RC)    GENERAL EXTRACT TEST                         
         BNE   SGST05                                                           
         USING TABDD,R4                                                         
         GOTOR GETBDEL,DMCB,AIO1                                                
         BNE   SGST05              GET AMOUNT FROM TABD ELEMENT                 
         MVC   TBCGST,TABDGST                                                   
         MVC   TBCPST,TABDPST                                                   
*                                                                               
         ICM   RF,15,TBCPST        SUBTRACT IT FROM PST TOTAL                   
         S     RF,AOSPST                                                        
         STCM  RF,15,TBCPST                                                     
*                                                                               
         B     SGST10                                                           
*                                                                               
SGST05   TM    SVOPT1,TAPDOGST     OR IF OVERRIDE GST                           
         BZ    SGST10                                                           
         MVI   WORK,TAPATGST                                                    
         BRAS  RE,GETPAAMT         GET AMOUNT FROM TAPA ELEMENT                 
         MVC   TBCGST,FULL                                                      
*                                                                               
SGST10   L     R1,TBCSTOT          CAN$ SUBTOTAL                                
         A     R1,TBCGST           + GST                                        
         ICM   RF,15,TBCPST                                                     
         AR    R1,RF                                                            
*                                                                               
         ICM   RF,15,AOSPST                                                     
         AR    R1,RF               + PST                                        
SGST15   ST    R1,TBCSTOT          = CAN$ SUBTOTAL                              
*                                                                               
         MVC   P6TOT(13),LTGST                                                  
         L     R1,TBCGST           GST IN CAN$                                  
         ICM   RF,15,TBCPST                                                     
         AR    R1,RF                                                            
         MVC   P6TOT(13),LTGSTAOS                                               
         ICM   RF,15,AOSPST                                                     
         AR    R1,RF                                                            
SGST20   LA    RF,P6MISC-1                                                      
         BAS   RE,EDIT11G                                                       
*                                                                               
         BRAS  RE,GMWKCTST         IF PRINTING GM WORKCODES                     
         BNE   *+10                                                             
         MVC   P6GMWCDE,RTPGMP52   WORK CODE IS RTPGM-P52                       
*                                                                               
         BRAS  RE,GMWKCT2          IF PRINTING NEW GM WORKCODES                 
         BNE   SGST21                                                           
         MVC   P6GMWCDE,RTP10696   SESSION WORK CODE IS 10-696                  
         TM    TGUSSTAT,SESSION                                                 
         BO    SGST21                                                           
         MVC   P6GMWCDE,RTP10704   REUSE WORK CODE IS 10-704                    
*                                                                               
SGST21   L     R3,TBCGST           GST                                          
         BAS   RE,SETCVT           SET CONV RATE INTO FULL (BINARY)             
         GOTO1 MULT,DMCB,(R3),FULL                                              
         MVC   TBGSTU,0(R1)        GST IN US$                                   
*                                                                               
         L     R3,TBCPST           PST                                          
         BAS   RE,SETCVT           SET CONV RATE INTO FULL (BINARY)             
         GOTO1 MULT,DMCB,(R3),FULL                                              
         MVC   TBPSTU,0(R1)        PST IN US$                                   
*                                                                               
         L     R3,AOSPST           AOS PST CAN$                                 
         BAS   RE,SETCVT           SET CONV RATE INTO FULL (BINARY)             
         GOTO1 MULT,DMCB,(R3),FULL                                              
*        MVC   TBPSTAOS,0(R1)      AOS PST IN US$                               
         LHI   RE,TBPSTAOS-BILLD                                                
         AR    RE,RA                                                            
         MVC   0(4,RE),0(R1)       AOS PST IN US$                               
*                                                                               
         L     R3,TBCHAND          TAKE GST FOR HANDLING                        
         A     R3,TOTCINR          & I&R                                        
         TM    STATUS,STCRDT       OR, CREDITED INVOICE?                        
         BNO   SGST25                                                           
         BRAS  RE,GETCRD           CHECK CREDITED INVOICE BILLED DATE           
         BNE   SGST25              IF < JUL01/06, USE OLD GST RATE              
         GOTO1 MULT,DMCB,(R3),=AL4(GSTOLDRT)  * OLD GST RATE                    
         B     SGST30                                                           
SGST25   GOTO1 MULT,DMCB,(R3),TGGSTRAT      * GST RATE                          
SGST30   MVC   TBCGST2,0(R1)       CAN$                                         
*                                                                               
         L     R3,TBCGST2                                                       
         GOTO1 MULT,DMCB,(R3),FULL    * CANADIAN GST                            
         MVC   TBGSTU2,0(R1)       STORE FOR POSTING (US$)                      
*                                                                               
         L     R1,TBCGST2          CANADIAN GST                                 
         S     R1,TBGSTU2          - US GST                                     
         ST    R1,TBDIFGST         IS DIFFERENCE                                
                                                                                
         USING TMD,R3                                                           
         L     R3,ATALIM                                                        
         USING TAPGD,R1                                                         
         L     R1,TMACURPG         GET TAPG ELEMENT                             
         LTR   R1,R1               IF NEW POSTING ELEMENT EXISTS,               
         BZ    YES                                                              
         MVC   TAPGCGST,TBCGST2    SAVE CANADIAN GST TAX                        
         MVC   TAPGUGST,TBGSTU2    US GST TAX                                   
         DROP  R1,R3                                                            
         B     YES                                                              
         EJECT                                                                  
*                                                                               
*        PRINT ACTRAWORKS AMOUNT                                                
*                                                                               
SUBACTWK NTR1                                                                   
         CLI   ACTWRK,C'Y'                                                      
         BE    NO                                                               
         MVI   ACTWRK,C'Y'         FLAG IT PRINTED                              
         OC    TBCACTWK,TBCACTWK   DON'T PRINT IF AMOUNT=0                      
         BZ    NO                                                               
         MVC   P6TOT(10),LTACTWRK  ACTRAWORKS                                   
         L     R1,TBCACTWK                                                      
         LA    RF,P6MISC-1                                                      
         BAS   RE,EDIT11                                                        
         L     R1,TBCACTWK                                                      
         A     R1,TBCSTOT          ADD TO CAN$ SUBTOTAL                         
         ST    R1,TBCSTOT                                                       
*                                                                               
         L     R3,TBCACTWK                                                      
         BAS   RE,SETCVT           SET CONV RATE INTO FULL (BINARY)             
         GOTO1 MULT,DMCB,(R3),FULL                                              
         L     R3,0(R1)                                                         
         ST    R3,TBACTWKU         STORE FOR POSTING (US$)                      
         B     YES                                                              
         SPACE 3                                                                
*                                                                               
*        PRINT U.S. SUBTOTAL                                                    
*                                                                               
SUBTOTUS NTR1                                                                   
         CLI   USUB,C'Y'                                                        
         BE    NO                                                               
         MVI   USUB,C'Y'           FLAG IT PRINTED                              
*&&DO                                                                           
         OC    TASIGN,TASIGN       IF SIGNATORY FEE PRESENT                     
         BZ    STU10                                                            
         BAS   RE,SETCVT           CONVERT IT TO US DOLLARS                     
         L     R3,TASIGN           FOR POSTING                                  
         GOTO1 MULT,DMCB,(R3),FULL                                              
         L     R1,0(R1)                                                         
         ST    R1,TASIGN                                                        
*                                                                               
*TU10    CLI   GTYPE,TABRTY20      IF BILLING TYPE 20                           
*        BNE   STU20                                                            
STU10    OC    TACOMM,TACOMM       IF TALENT MASTERS FEE OR BROADCAST           
         BZ    STU20               BUSINESS FEE PRESENT                         
         BAS   RE,SETCVT           CONVERT IT TO US DOLLARS                     
         L     R3,TACOMM           FOR POSTING                                  
         GOTO1 MULT,DMCB,(R3),FULL                                              
         L     R1,0(R1)                                                         
         ST    R1,TACOMM                                                        
*&&                                                                             
STU20    BAS   RE,SETCVT           SET CONV RATE INTO FULL (BINARY)             
         L     R3,TBCSTOT                                                       
         GOTO1 MULT,DMCB,(R3),FULL      CANADIAN SUBTOTAL                       
         L     R1,0(R1)                 = US SUBTOTAL                           
         ST    R1,TBTOT                                                         
*                                                                               
         TM    TBOPTS,TBRERUN      IF THIS IS A RERUN                           
         BNO   NO                                                               
         TM    TBBDSTAT,TABDSCNW   & BILLED THE OLD WAY                         
         BO    STU30                                                            
*                                  PRINT US SUBTOTAL                            
         MVC   P6TOT(13),LTUSSTOT  US SUBTOTAL                                  
         LA    RF,P6MISC-1                                                      
         BAS   RE,EDIT11                                                        
         B     YES                                                              
*                                                                               
STU30    TM    TBISTA2,TAPDSSUB    IF SUBSIDIARY INVOICE,                       
         BZ    NO                                                               
         CLI   RECNUM,CC           AND NOT CHLOE COPY                           
         BE    NO                                                               
         CLI   GTYPE,TABRTYE       AND NOT EMS                                  
         BE    NO                                                               
         USING TABDD,R4                                                         
         GOTOR GETBDCN,DMCB,AIO1   GET TABD ELEMENT                             
         BNE   NO                                                               
         MVC   TBTOT,TABDTOT       USE TOTAL ALREADY CALCULATED                 
         B     NO                                                               
         EJECT                                                                  
*                                                                               
*        PRINT CANADIAN HANDLING PERCENT & AMOUNT                               
*                                                                               
SUBCHAND NTR1                                                                   
         CLI   HND,C'Y'            USE US FLAGS                                 
         BE    NO                                                               
         MVI   HND,C'Y'            FLAG IT PRINTED                              
*                                                                               
         CLI   RECNUM,CC           CCOPY AND OFFICE O OR Q ONLY                 
         BNE   SCH01                                                            
         CLI   TBTPO,C'O'                                                       
         BE    *+12                                                             
         CLI   TBTPO,C'Q'                                                       
         BNE   SCH01                                                            
         MVC   TBHANDC,PRTHAND                                                  
*                                                                               
SCH01    CLI   GTYPE,TABRTY24      BILL TYPE 24 ALWAYS HAS GRCAN=0              
         BE    SCH02                                                            
*                                                                               
         BRAS  RE,CKCNHND          CHECK FOR CANADIAN HANDLING RATE             
         BNE   SCH05                                                            
*&&DO                                                                           
         SR    R1,R1                                                            
         ICM   R1,3,GRCAN          CANADIAN HANDLING PERCENT                    
         EDIT  (R1),(5,P6TOT),2,ALIGN=LEFT                                      
         MVI   P6TOT+5,C'%'                                                     
         MVC   P6TOT+7(8),LTCHAND                                               
*&&                                                                             
SCH02    MVC   P6TOT(8),LTCHAND                                                 
*                                                                               
SCH04    L     R1,TBHANDC          TOTAL HANDLING (ACCUMULATED)                 
         L     R3,TBTOT            ADD HANDLING TO INVOICE TOTAL                
         AR    R3,R1                                                            
         ST    R3,TBTOT                                                         
*                                                                               
         CLI   GTYPE,TABRTY20      IF BILLING TYPE 20                           
         BNE   SCH04A                                                           
         CLI   UNICOPY,C'Y'        AND NOT UNION COPY                           
         BE    SCH04A                                                           
         A     R1,TACOMM           ADD TALENT MASTERS FEE TO HANDLING           
*                                                                               
SCH04A   LA    RF,P6MISC-1                                                      
         BAS   RE,EDIT11N                                                       
*                                                                               
SCH05    MVC   TBCHANDU,TBHANDC    SET FOR OLD INVOICES - TO POST               
*                                                                               
         BRAS  RE,GMWKCTST         IF PRINTING GM WORKCODES                     
         BNE   *+10                                                             
         MVC   P6GMWCDE,RTPGMP52   WORK CODE IS RTPGM-P52                       
*                                                                               
         BRAS  RE,GMWKCT2          IF PRINTING NEW GM WORKCODES                 
         BNE   SCH06                                                            
         MVC   P6GMWCDE,RTP10696   SESSION WORK CODE IS 10-696                  
         TM    TGUSSTAT,SESSION                                                 
         BO    SCH06                                                            
         MVC   P6GMWCDE,RTP10704   REUSE WORK CODE IS 10-704                    
*                                                                               
SCH06    TM    TBOPTS,TBRERUN      IF THIS IS A NEW INVOICE                     
         BNO   SCH10                                                            
         TM    TBBDSTAT,TABDSCNW   OR IT WAS BILLED THE NEW WAY                 
         BNO   SCHX                                                             
*                                                                               
SCH10    BAS   RE,SETCVT           SET CONV RATE INTO FULL (BINARY)             
         L     R3,TBHANDC                                                       
         ST    R3,TBCHAND          (KEEP HANDLING IN CAN$)                      
         GOTO1 MULT,DMCB,(R3),FULL    * HANDLING CAN$                           
         MVC   TBCHANDU,0(R1)      HANDLING US$ FOR POSTING                     
*                                                                               
SCHX     CLI   GTYPE,TABRTY24      BILL TYPE 24 ALWAYS HAS GRCAN=0              
         BE    *+12                                                             
         BRAS  RE,CKCNHND          CHECK FOR CANADIAN HANDLING RATE             
         BNE   NO                                                               
         B     YES                                                              
         DROP  R2                                                               
         EJECT                                                                  
*&&DO                                                                           
*              ROUTINE TO PRINT OUT WORK CODES FOR CHEMISTRI                    
*              ON ENTRY ... R2=A(PRINT LINE)                                    
         SPACE 1                                                                
         USING P6LINED,R2                                                       
CHEMWC   NTR1                                                                   
         BRAS  RE,CHEMTEST         IF AGENCY IS CHEMISTRI                       
         BNE   XIT                                                              
         MVC   P6WCDEC,CH10716     WORK CODE IS 10-716                          
         CLI   TGUSEQU,UGRT        IF USE IS GRT                                
         BE    XIT                                                              
         MVC   P6WCDEC,CH10700     WORK CODE IS 10-700                          
         TM    TGUSSTAT,SESSION    IF USE IS SESSION                            
         BO    XIT                                                              
         MVC   P6WCDEC,CH10708     ELSE, WORK CODE IS 10-708                    
         B     XIT                                                              
         DROP  R2                                                               
         EJECT                                                                  
*&&                                                                             
*=====================================================================          
*        SEE IF INVOICE WAS BILLED BEFORE                                       
*=====================================================================          
         USING TAIND,R4                                                         
BILLDB4  NTR1                                                                   
         L     R4,AIO1                                                          
         MVI   ELCODE,TAINELQ                                                   
         BAS   RE,GETEL                                                         
         BNE   NO                                                               
         OC    TAINHDTE,TAINHDTE   HAS BEEN PRINTED OR                          
         BNZ   YES                                                              
         TM    TAINSTAT,TAINSCIN   IS THE CANCELLER                             
         BO    YES                                                              
         OC    TAINBDTE,TAINBDTE   HAS BEEN BILLED                              
         BZ    NO                                                               
         TM    TBOPTS2,TBFIXINV    FIXING INVOICES                              
         BO    NO                                                               
         B     YES                                                              
*                                                                               
         DROP  R4                                                               
         EJECT                                                                  
*                                                                               
*        GET INFORMATION BACK FROM TNH INTERFACE BLOCK                          
*                                                                               
RETINFO  NTR1                                                                   
         USING TABCHKD,R3                                                       
         L     R2,ATALIM                                                        
         USING TMD,R2                                                           
         BRAS  RE,UPDYTD           UPDATE YTD TABLE                             
*                                                                               
         TM    TBSTA2,TAINSHLR     IF THIS IS A RELEASED INVOICE                
         BNO   RET02                                                            
         TM    TBSTA2,TAINSHLP     THAT WAS ALREADY PRINTED - CONTINUE          
         BO    RET05                                                            
*                                                                               
RET02    GOTO1 EXTRCT,DMCB,(RC)    IF EXTRACTING SKIP                           
         BE    *+10                                                             
         MVC   TABLEVL,TMLEVEL     SETTING RATE LEVEL A - E                     
         MVC   TABXYTD,TMTTOTAL    TOTAL AMOUNT THAT'S TAXABLE                  
         MVC   TABXFUI,TMTFUI      THIS CHECK'S FUI TAXABLE AMOUNT              
         MVC   TABXSUI,TMTSUI                   SUI                             
         MVC   TABXFICA,TMTFICA                 FICA                            
         MVC   TABXMED,TMTMED                   MEDICARE                        
         MVC   TABXOMED,TMTOMED                 OVER MEDICARE                   
*                                                                               
RET05    MVC   TABNYTD,TMNYEARN    NEW YTD EARNINGS                             
         MVC   TABNFUI,TMNYFUI             FUI                                  
         MVC   TABNSUI,TMNYSUI             SUI                                  
         MVC   TABNFICA,TMNYFICA           FICA                                 
         MVC   TABNMED,TMNYMED             MEDICARE                             
         MVC   TABNTAX,TMNYNTAX            NON TAXABLE                          
*                                                                               
         CLI   TGCUR,C'C'          IF NOT CANADIAN CURRENCY                     
         BE    RET6                                                             
         L     R1,TBGSTU           ACCUMULATE GST PAID IN US$                   
         A     R1,TMOGSTU                                                       
         ST    R1,TBGSTU                                                        
         L     R1,TBPSTU           ACCUMULATE PST PAID IN US$                   
         A     R1,TMOPSTU                                                       
         ST    R1,TBPSTU                                                        
         B     RET7                                                             
*                                                                               
RET6     L     R1,TBCGST                                                        
         A     R1,TMOGST           SET GST                                      
         ST    R1,TBCGST                                                        
         L     R1,TBCPST                                                        
         A     R1,TMOPST           SET PST                                      
         ST    R1,TBCPST                                                        
*                                                                               
RET7     L     R1,FICR                                                          
         A     R1,TMXFICR          SET FICA CREDITS                             
         ST    R1,FICR                                                          
*                                                                               
         L     R1,TMXTOTAL                                                      
*                                                                               
         CLI   TMBILTYP,TABRTY1     IF BILLING TYPE 1,6,7, OR 8                 
         BE    RET8                                                             
         CLI   TMBILTYP,TABRTY6                                                 
         BE    RET8                                                             
         CLI   TMBILTYP,TABRTY8                                                 
         BE    RET8                                                             
         CLI   TMBILTYP,TABRTY16                                                
         BE    RET8                                                             
         CLI   TMBILTYP,TABRTY18                                                
         BE    RET8                                                             
         CLI   TMBILTYP,TABRTY23                                                
         BE    RET8                                                             
         CLI   TMBILTYP,TABRTY20                                                
         BE    RET8                                                             
         CLI   TMBILTYP,TABRTY7                                                 
         BE    RET8                                                             
         CLI   TMBILTYP,TABRTY21                                                
         BE    RET8                                                             
         CLI   TMBILTYP,TABRTY11                                                
         BE    RET8                                                             
         CLI   TMBILTYP,TABRTY25                                                
         BE    RET8                                                             
         CLI   TMBILTYP,TABRTY26                                                
         BE    RET8                                                             
         BNE   RET10                                                            
*                                                                               
RET8     S     R1,TMXFICR          FICA CREDITS ARE TAKEN OFF OF TAX            
RET8A    ST    R1,TMXTOTAL                                                      
         XC    FICR,FICR                                                        
         XC    TMXFICR,TMXFICR                                                  
*                                                                               
RET10    L     R1,TBTAX                                                         
         A     R1,TMXTOTAL                                                      
         ST    R1,TBTAX            INVOICE ACCUMULATED TAX                      
         ST    R1,PRTTAX                   PRINTED TOTAL TAX                    
*                                                                               
         CLI   RECNUM,PB           IF PRINT BILLING                             
         BNE   RET12                                                            
         TM    TBAYSTA4,TAAYSNHD   AND AGY NOT CHARGING HANDLING ON AGT         
         BZ    RET12                                                            
         TM    TABPSTAT,TABPSAGT   AND THIS IS AGENT CHECK                      
         BZ    RET12                                                            
         XC    TMOCORP,TMOCORP     CLEAR OUT CALCULATED HANDLING FEE            
*                                                                               
RET12    CLI   TMBILTYP,TABRTY14   IF BILLING TYPE 14 OR                        
         BE    RET14                                                            
         CLI   TMBILTYP,TABRTY15   IF BILLING TYPE 15 OR                        
         BE    RET14                                                            
         CLI   TMBILTYP,TABRTY10   IF BILLING TYPE 10                           
         BNE   RET15                                                            
RET14    TM    SVOPT1,TAPDOTAX     & IF OVERRIDE TAX                            
         BZ    RET15                                                            
         XC    TMOHAND,TMOHAND     CLEAR OUT CALCULATED HANDLING FEE            
*                                                                               
RET15    TM    BOVRSTAT,BOVRHNDB+BOVRHNDP  BAS OR PREM HAND OVERRIDE?           
         BZ    RET18                                                            
         TM    TBISTAT,TAPDSCAN    US$ ONLY,                                    
         BO    RET18                                                            
         CLI   TMBILTYP,TABRTY14   IF BILLING TYPE 14 OR                        
         BE    RET16                                                            
         CLI   TMBILTYP,TABRTY15   IF BILLING TYPE 15 OR                        
         BE    RET16                                                            
         CLI   TMBILTYP,TABRTY10   OR TYPE 10, ALWAYS GOES IN CORP HAND         
         BE    RET16                                                            
*                                                                               
         TM    STAT2,STCORP        IS THERE A CORP ON THE INVOICE?              
         BNO   RET17                                                            
RET16    OC    TMOHAND,TMOHAND     ANY IND. HANDLING?                           
         BZ    RET18                                                            
         L     R1,TMOCORP          COMBINE IND HAND WITH CORP HANDLING          
         A     R1,TMOHAND                                                       
         ST    R1,TMOCORP          PUT IT ALL IN CORP HANDLING                  
         XC    TMOHAND,TMOHAND     CLEAR IT OUT OF IND. HANDLING                
         B     RET18                                                            
RET17    OC    TMOCORP,TMOCORP     IF NO CORPS, ANY CORP HANDLING?              
         BZ    RET18                                                            
         L     R1,TMOHAND          COMBINE CORP HAND WITH IND HANDLING          
         A     R1,TMOCORP                                                       
         ST    R1,TMOHAND          PUT IT ALL IN IND. HANDLING                  
         XC    TMOCORP,TMOCORP     CLEAR IT OUT OF CORP HANDLING                
*                                                                               
RET18    L     R1,TBHANDI                                                       
         A     R1,TMOHAND                                                       
         ST    R1,TBHANDI          INVOICE ACCUMULATED IND HANDLING             
         ST    R1,PRTHANDI                 PRINTED IND HANDLING                 
*                                                                               
**NO-OP  BAS   RE,INRHND18         CALC HANDLING ON I&R FOR BTYPE 18            
**FEB12                                                                         
*                                                                               
         L     R1,TBHANDC                                                       
         A     R1,TMOCORP                                                       
         A     R1,TMOCAN                                                        
         ST    R1,TBHANDC          INVOICE ACCUMULATED CORP HANDLING            
         ST    R1,PRTHANDC                 PRINTED CORP HANDLING                
*                                                                               
         MVC   TABTAX,TMXTOTAL     CAST MEMBER TAX                              
         MVC   TABGST,TMOGST       CAST MEMBER GST                              
         L     R1,TMXFICR                      FICA CREDITS                     
         TM    STATUS,STCRDT       IF CREDIT                                    
         BO    *+6                 DON'T COMPLEMENT AMOUNT                      
         LCR   R1,R1                                                            
         ST    R1,TABFICR                                                       
         L     R1,TMOHAND          INDIVIDUAL HANDLING                          
         ST    R1,TABHND                                                        
         L     R1,TMOCORP          CORP HANDLING                                
         A     R1,TMOCAN           CANADIAN                                     
         ST    R1,TABHNDC                                                       
*                                                                               
         TM    SVOPT1,TAPDOTAX     OVERRIDE TAX AMOUNT                          
         BNO   *+10                                                             
         XC    TABTAX,TABTAX       DON'T POST INDIVIDUALLY                      
*                                                                               
         TM    SVOPT1,TAPDOHND     OVERRIDE HANDLING FEE                        
         BNO   RET20                                                            
         XC    TABHND,TABHND                                                    
         XC    TABHNDC,TABHNDC                                                  
*                                                                               
RET20    TM    SVOPT1,TAPDOGST     OVERRIDE GST                                 
         BNO   *+10                                                             
         XC    TABGST,TABGST                                                    
*                                                                               
         TM    SVOPT2,TAPDOFCR     OVERRIDE FICA CREDITS                        
         BNO   *+10                                                             
         XC    TABFICR,TABFICR                                                  
*                                                                               
         GOTO1 AMYTRACE,DMCB,(RC),=C'FROM TALIM BLOCK',TMD,TMLNQ                
         B     XIT                                                              
         EJECT                                                                  
*&&DO                                                                           
***********************************************************************         
*        ROUTINE, FOR BILLING TYPE 18, TO CALCULATE HANDLING          *         
*        ALSO FOR BILLING TYPE 23                                     *         
*        AMOUNT ON I&R                                                *         
***********************************************************************         
                                                                                
INRHND18 NTR1                                                                   
         CLI   GTYPE,TABRTY18      IF BILLING TYPE 18                           
         BE    INRH1810                                                         
         CLI   GTYPE,TABRTY23      OR IF BILLING TYPE 23                        
         BNE   XIT                                                              
INRH1810 BAS   RE,BILLDB4          AND INVOICE WAS NOT BILLED BEFORE            
         BE    XIT                                                              
         BRAS  RE,CKCAN            AND THIS IS AN I&R CHECK                     
         BNE   XIT                                                              
*                                                                               
         L     R1,TABGRS           TAKE CANADIAN HANDLING RATE                  
         MVC   DUB(2),GRCAN        ON THE GROSS                                 
         MH    R1,DUB                                                           
         CVD   R1,DUB                                                           
         SRP   DUB,60,5                                                         
         CVB   R1,DUB                                                           
         A     R1,TMOCORP          AND ADD IT TO CORP HANDLING                  
         ST    R1,TMOCORP                                                       
         B     XIT                                                              
         DROP  R2,R3                                                            
         EJECT                                                                  
*&&                                                                             
*                                                                               
*              ROUTINE TO POP IN A BOXES TOP                                    
*                                                                               
*              P1 = BOXCOLS + X LHS                                             
*              P2 = BOXCOLS + X RHS                                             
*              P3 = MIDLNS                                                      
*                                                                               
         SPACE 1                                                                
BXTOP    NTR1                                                                   
         L     R3,ABOX                                                          
         USING BOXD,R3                                                          
         L     R4,0(R1)            LHS                                          
         L     RE,4(R1)            RHS                                          
         L     R2,8(R1)            MID LINES                                    
         MVC   BOXROWS,SPACES                                                   
         MVC   BOXCOLS,SPACES                                                   
         ZIC   RF,LINE                                                          
         LA    RF,BOXROWS-1(RF)                                                 
         MVI   0(RF),C'T'                                                       
         LTR   R2,R2               DO WE WANT A MIDDLE LINE                     
         BZ    BXT10                                                            
         AR    RF,R2               NUMBER OF LINES TO MIDDLE LINE               
         MVI   0(RF),C'M'                                                       
*                                                                               
BXT10    LA    R4,BOXCOLS(R4)                                                   
         MVI   0(R4),C'L'          LEFT CORNER                                  
         LA    RE,BOXCOLS(RE)                                                   
         MVI   0(RE),C'R'          RIGHT CORNER                                 
*                                                                               
BXT30    BRAS  RE,BXCOL                                                         
         MVI   BOXINIT,0                                                        
         BRAS  RE,PRTIT                                                         
         B     XIT                                                              
         DROP  R3                                                               
         SPACE 2                                                                
*                                                                               
*        DO AN EXECUTED MOVE FOR VARIABLE LENGTH ELEMENTS                       
*                            P1(0) - OVERRIDE BIT (X'80')                       
*                            P1    - STRING FOR GETL                            
*                            P2(0) - LEN OF DESTINATION                         
*                            P2    - A(DESTINATION)                             
*                            P3(0) - ELCODE                                     
*                                                                               
         SPACE 1                                                                
MVCTST1  NTR1                                                                   
         ZIC   R2,11(R1)           ELCODE                                       
         STC   R2,ELCODE                                                        
         MVC   BYTE,3(R1)          SEARCH STRING FOR GETL                       
         L     R4,4(R1)            DESTINATION                                  
         ZIC   R3,4(R1)            LEN OF DESTINATION                           
         BCTR  R3,0                SUB 1 FOR EXECUTED MVC                       
         TM    0(R1),X'80'         TEST IF OVERRIDE                             
         BO    MV10                YES - ALREADY HAVE INFO                      
         EX    R3,*+8                    DON'T ERASE                            
         B     *+10                                                             
         MVC   0(0,R4),SPACES      ELSE - INIT WITH SPACES                      
*                                                                               
MV10     SR    R2,R2                                                            
         CLI   BYTE,0              IF THERE'S SEARCH STRING                     
         BE    MV20                                                             
         LA    R2,1                   SET LENGTH OF IT                          
*                                                                               
MV20     GOTO1 GETL,DMCB,((R2),BYTE)                                            
         BNE   MVX                 ELEM NOT FOUND - EXIT                        
         L     R3,TGELEM                                                        
         ZIC   R1,1(R3)            LENGTH OF ELEMENT                            
         AHI   R2,2                R2 = 2 OR 3 - LENGTH BEFORE DATA             
         CLI   ELCODE,TAADELQ      IF ADDRESS ELEMENT                           
         BNE   MV30                                                             
         LA    R2,1(R2)            PASS BYTE CONTAINING N'ADDDR LNS             
*                                                                               
MV30     SR    R1,R2               - ELCODE, LEN & (TYPE IF EXISTS)             
         AR    R3,R2               POSITION R3 TO START OF DATA                 
         BCTR  R1,0                SUBTRACT 1 FOR MOVE                          
*                                                                               
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R4),0(R3)       EXECUTED                                     
*                                                                               
MVX      B     XIT                                                              
         EJECT                                                                  
*                                                                               
*              HEADLINE/BOX ROUTINES (HEADHOOK)                                 
*                                                                               
MYHOOK   NTR1                                                                   
         CLI   LASTPG,C'Y'                                                      
         BE    HKX                                                              
*                                                                               
         USING BOXD,R3                                                          
HK10     L     R3,ABOX             NOW HANDLE BOXES                             
         MVC   BOXCOLS,SPACES                                                   
         MVC   BOXROWS,SPACES                                                   
*                                                                               
         MVI   BOXROWS,C'T'        SET TOP                                      
         MVI   BOXROWS+2,C'M'          MIDDLE                                   
         MVI   BOXCOLS+91,C'L'         LEFT                                     
         MVI   BOXCOLS+131,C'R'        RIGHT                                    
*                                                                               
         MVI   BOXYORN,C'Y'                                                     
         MVI   BOXWT,1                                                          
         MVI   BOXINIT,0                                                        
         MVI   BOXOFF,0                                                         
         MVI   BOXREQ,C' '                                                      
*                                                                               
HKX      B     XIT                                                              
         SPACE 3                                                                
*                                                                               
*        SET TBGCVT (CONVERSION RATE) IN FULL (BINARY)                          
*                                                                               
SETCVT   ZAP   DUB,TBGCVT                                                       
         CVB   R1,DUB                                                           
         ST    R1,FULL                                                          
         BR    RE                                                               
         EJECT                                                                  
*                                                                               
*        EDIT A LINE - INPUT (R1)                                               
*                      OUTPUT LENGTH = 2                                        
*                      OUTPUT ADDR   = RF                                       
*                                                                               
         SPACE 1                                                                
EDIT2    EDIT  (R1),(2,(RF)),ZERO=BLANK,ALIGN=LEFT                              
         BR    RE                                                               
         SPACE 2                                                                
*                                                                               
*        EDIT A LINE - INPUT (R1)                                               
*                      OUTPUT LENGTH = 2                                        
*                      OUTPUT ADDR   = RF                                       
*                                                                               
         SPACE 1                                                                
EDIT2A   EDIT  (R1),(2,(RF)),ZERO=BLANK                                         
         BR    RE                                                               
         SPACE 2                                                                
*                                                                               
*        EDIT A LINE - INPUT (R1)                                               
*                      OUTPUT LENGTH = 3                                        
*                      OUTPUT ADDR   = RF                                       
*                                                                               
         SPACE 1                                                                
EDIT3    EDIT  (R1),(3,(RF)),ZERO=BLANK                                         
         BR    RE                                                               
         SPACE 2                                                                
*                                                                               
*        EDIT A LINE - INPUT (R1)                                               
*                      OUTPUT LENGTH = 4                                        
*                      OUTPUT ADDR   = RF                                       
*                                                                               
         SPACE 1                                                                
EDIT4    EDIT  (R1),(4,(RF)),ZERO=BLANK,ALIGN=LEFT                              
         BR    RE                                                               
         SPACE 2                                                                
*                                                                               
*        EDIT A LINE - INPUT (R1)                                               
*                      OUTPUT LENGTH = 4                                        
*                      OUTPUT ADDR   = RF                                       
*                                                                               
         SPACE 1                                                                
EDIT4R   EDIT  (R1),(4,(RF)),ZERO=BLANK                                         
         BR    RE                                                               
         EJECT                                                                  
*                                                                               
*        EDIT A LINE - INPUT (R1)                                               
*                      OUTPUT LENGTH = 5                                        
*                      OUTPUT ADDR   = RF                                       
*                                                                               
         SPACE 1                                                                
EDIT5    LTR   R1,R1               DON'T EDIT 0                                 
         BZR   RE                                                               
         EDIT  (R1),(5,(RF)),2,ZERO=BLANK                                       
         BR    RE                                                               
         SPACE 2                                                                
*                                                                               
*        EDIT A LINE - INPUT (R1)                                               
*                      OUTPUT LENGTH = 8                                        
*                      OUTPUT ADDR   = RF                                       
*                                                                               
         SPACE 1                                                                
EDIT8    LTR   R1,R1               DON'T EDIT 0                                 
         BZR   RE                                                               
         EDIT  (R1),(8,(RF)),2,FLOAT=-,ZERO=BLANK                               
         BR    RE                                                               
         SPACE 2                                                                
*                                                                               
*        EDIT A LINE - INPUT (R1)                                               
*                      OUTPUT LENGTH = 8                                        
*                      OUTPUT ADDR   = RF                                       
*                                                                               
         SPACE 1                                                                
EDIT8A   LTR   R1,R1               DON'T EDIT 0                                 
         BZR   RE                                                               
         EDIT  (R1),(8,(RF)),FLOAT=-,ZERO=BLANK                                 
         BR    RE                                                               
         SPACE 2                                                                
*                                                                               
*        EDIT A LINE - INPUT (R1)                                               
*                      OUTPUT LENGTH = 10                                       
*                      OUTPUT ADDR   = RF                                       
*                      NO MINUS SIGN                                            
*                                                                               
         SPACE 1                                                                
EDIT10   LTR   R1,R1               DON'T EDIT 0                                 
         BZR   RE                                                               
         EDIT  (R1),(10,(RF)),2,ZERO=BLANK                                      
         BR    RE                                                               
         EJECT                                                                  
*                                                                               
*        EDIT A LINE - INPUT (R1)                                               
*                      OUTPUT LENGTH = 11                                       
*                      OUTPUT ADDR   = RF                                       
*                                                                               
         SPACE 1                                                                
EDIT11N  EDIT  (R1),(11,(RF)),2,FLOAT=-,ZERO=NOBLANK                            
         BR    RE                                                               
         SPACE 2                                                                
*                                                                               
*        EDIT A LINE - INPUT (R1)                                               
*                      OUTPUT LENGTH = 11                                       
*                      OUTPUT ADDR   = RF                                       
*                                                                               
         SPACE 1                                                                
EDIT11   LTR   R1,R1               DON'T EDIT 0                                 
         BZR   RE                                                               
EDIT11G  EDIT  (R1),(11,(RF)),2,FLOAT=-,ZERO=BLANK                              
         BR    RE                                                               
         SPACE 2                                                                
*                                                                               
*        EDIT A LINE - INPUT (R1)                                               
*                      OUTPUT LENGTH = 12                                       
*                      OUTPUT ADDR   = RF                                       
*                                                                               
         SPACE 1                                                                
EDIT12N  EDIT  (R1),(12,(RF)),2,FLOAT=-,ZERO=NOBLANK                            
         BR    RE                                                               
*                                                                               
*        EDIT A LINE - INPUT (R1)                                               
*                      OUTPUT LENGTH = 12                                       
*                      OUTPUT ADDR   = RF                                       
*                                                                               
         SPACE 1                                                                
EDIT12   LTR   R1,R1               DON'T EDIT 0                                 
         BZR   RE                                                               
         EDIT  (R1),(12,(RF)),2,FLOAT=-,ZERO=BLANK                              
         BR    RE                                                               
         EJECT                                                                  
*                                                                               
YES      SR    RC,RC               SET CONDITION CODE                           
NO       LTR   RC,RC                                                            
*                                                                               
XIT      XIT1                                                                   
         SPACE 3                                                                
*                                                                               
         GETEL R4,DATADISP,ELCODE                                               
         EJECT                                                                  
*                                                                               
*  CONSTANTS                                                                    
*                                                                               
COMPLM   DC    X'FFFFFFFFFFFF'                                                  
*                                                                               
LTCLASS  DC    C' Class A'                                                      
LTPGM    DC    C'Date  Program Name   Lft Net'                                  
LTCANTAX DC    C'* Canadian tax withheld *'                                     
LTDATE1  DC    C'Date  '                                                        
*                                                                               
         SPACE 1                                                                
LTSSN    DC    C'Soc Sec #'                                                     
LTPID    DC    C'  Pid #  '                                                     
LTNAME   DC    C' Performer Name '                                              
LTEMPNM  DC    C'  Employee Name '                                              
LTOV     DC    C'Ov%'                                                           
LTCAT    DC    C'Cat'                                                           
LTCAM1   DC    C'On/'                                                           
LTAGT    DC    C'Agnt'                                                          
LTLOCL   DC    C'AFM '                                                          
LTLOCL2  DC    C'Locl'                                                          
LTLOCL3  DC    C'****'                                                          
LTSTATE  DC    C'St'                                                            
LTFIRST1 DC    C'First '                                                        
LTOT1    DC    C'OT && '                                                        
LTHR1    DC    C'Hrs &&'                                                        
LTTAG    DC    C'Tg'                                                            
LTTAG1   DC    C'Tg='                                                           
LTRANG1  DC    C'  Use'                                                         
LTMAJUN1 DC    C'  Majors'                                                      
LTDY1    DC    C'No'                                                            
LTSP1    DC    C'No'                                                            
LTYEAR   DC    C'Yr'                                                            
LTAMT1   DC    C'  Amount  '                                                    
LTGROSS  DC    C'Gross Wages'                                                   
LTACTGRS DC    C'   Gross   '                                                   
LTTXWG   DC    C'  Taxable  '                                                   
LTTXWG2  DC    C'   Wages   '                                                   
LTNTXWG  DC    C'  Non-Tax  '                                                   
LTREIMB  DC    C'   Reimb.  '                                                   
LTMISC1  DC    C'   Misc.  '                                                    
LTDUES1  DC    C'Dues && Svc'                                                   
LTSHOOT  DC    C'Shoot'                                                         
LTPUB    DC    C'Pub'                                                           
LTPAYEE  DC    C'Pay'                                                           
LTARATE  DC    C'Agent'                                                         
LTEXP    DC    C'Exp'                                                           
         SPACE 1                                                                
LTCAM2   DC    C'Off'                                                           
LTFIRST2 DC    C'Serv. '                                                        
LTOT2    DC    C'Travl'                                                         
LTHR2    DC    C'Mins.'                                                         
LTRANG2  DC    C' Range'                                                        
LTMAJUN2 DC    C'    +'                                                         
LTDY2    DC    C'Dy'                                                            
LTSP2    DC    C'Sp'                                                            
LTAMT2   DC    C' Applied  '                                                    
LTMISC2  DC    C' Payment  '                                                    
LTDUES2  DC    C'Fees/GST  '                                                    
         SPACE 1                                                                
LTCAM3   DC    C'Cam'                                                           
LTOT3    DC    C'Hr.Mn'                                                         
LTMAJUN3 DC    C'  Units'                                                       
         SPACE 1                                                                
*                                                                               
LTPHR    DC    C'Pension && Health Remittance'                                  
LTAPLEG  DC    C'Apply Legend'                                                  
LTINCL   DC    C'Include Legend'                                                
LTTOT    DC    C'Totals'                                                        
         SPACE 1                                                                
LTCOMP   DC    C'Company'                                                       
LTDMBBS  DC    CL18'D''ARCY MASIUS B&&B'                                        
LTUNION  DC    C'Union'                                                         
LTACC    DC    C'Account'                                                       
LTGPAY   DC    C'Gross Payment'                                                 
LTCONT   DC    C'Contribution'                                                  
LTGNM    DC    C'Wages && Misc Payments '                                       
LTTAX    DC    C'Payroll Taxes'                                                 
LTTAX13  DC    C'Tax/Handling'                                                  
LTTAX20  DC    C'Payroll Tax && Handling'                                       
LTPNH    DC    C'P && H Contribution'                                           
LTINR    DC    C'I && R Contribution'                                           
LTFICR   DC    C'Fica Credits'                                                  
LTSTOT   DC    C'Sub Total'                                                     
LTHAND   DC    C'Handling'                                                      
LTTALENT DC    C'Talent Partners'                                               
LTAGYCOM DC    C'Broadcast Business Fee'                                        
LTAGYCM2 DC    C'Agency Commission'                                             
LTEMSF   DC    C'EMS Fee'                                                       
LTTLMAST DC    C'Talent Masters'                                                
LTLUNAPR DC    C'Luna Productions && Talent'                                    
LTSIGNF  DC    C'Signatory Fee'                                                 
LTCSF    DC    C'Commercial Service Fee'                                        
         SPACE 1                                                                
LTCINR   DC    C'I && R Contribution'                                           
LTCSTOT  DC    C'Subtotal Canadian'                                             
LTCCVT   DC    C'Conversion Rate'                                               
LTUSSTOT DC    C'Subtotal U.S.'                                                 
LTSUBTOT DC    C'Subtotal'                                                      
LTCHAND  DC    C'Handling'                                                      
LTGST    DC    C'GST/PST (HST)'                                                 
LTGSTAOS DC    C'GST/PST (HST)'                                                 
LTACTWRK DC    C'Actraworks'                                                    
         SPACE 1                                                                
*                                                                               
LTSESS   DC    C'1 Session'                                                     
LTWARD   DC    C'1 Wardrobe Fee'                                                
LTHOLD   DC    C'2 Holding Fee'                                                 
LTRGRT   DC    C'2 Cycle Grt  '                                                 
LTMEAL   DC    C'2 Meal Penalty'                                                
LTWSU    DC    C'3 WS Upgrade'                                                  
LTCART   DC    C'3 Cartage'                                                     
LTGUAR   DC    C'4 Guarantee'                                                   
LTOTH1   DC    C'4 Other'                                                       
LTOTH2   DC    C'5 Other'                                                       
LTSUPP   DC    C'5 Supplies'                                                    
LTCABL   DC    C'6 Cable'                                                       
LTLOC4   DC    C'Others'                                                        
LTITOT   DC    C'Invoice Total  '                                               
LTITOTE  DC    C'Inv Tot (Euros)'                                               
*TOFF7G  DC    C'** TERMS: PAYABLE ON RECEIPT **                       X        
                               '                                                
LTCOD1   DC    C'** PUR - PAYABLE UPON RECEIPT **'                              
LTDAY1   DC    C'** Payment due in'                                             
LTDAY2   DC    C'days **'                                                       
LTDAYNOW DC    C'** PAYMENT DUE UPON RECEIPT OF INVOICE **'                     
LTSUML   DC    C'** SUMMARY LISTING - DO NOT PAY **'                            
*                                                                               
CH10712  DC    CL(L'P6WCDEC)'10-712'                                            
CH10696  DC    CL(L'P6WCDEC)'10-696'                                            
CH10704  DC    CL(L'P6WCDEC)'10-704'                                            
CH10716  DC    CL(L'P6WCDEC)'10-716'                                            
CH10700  DC    CL(L'P6WCDEC)'10-700'                                            
CH10708  DC    CL(L'P6WCDEC)'10-708'                                            
*                                                                               
RTPGMP49 DC    CL(L'P6GMWCDE)'RTPGM-P49'                                        
RTPGMP52 DC    CL(L'P6GMWCDE)'RTPGM-P52'                                        
RTPGMP51 DC    CL(L'P6GMWCDE)'RTPGM-P51'                                        
RTPGMP50 DC    CL(L'P6GMWCDE)'RTPGM-P50'                                        
*                                                                               
RTPP51   DC    CL(L'P6GMWCDE)'P51'                                              
RTPP52   DC    CL(L'P6GMWCDE)'P52'                                              
RTP10704 DC    CL(L'P6GMWCDE)'10-704'                                           
RTP10696 DC    CL(L'P6GMWCDE)'10-696'                                           
*                                                                               
         LTORG                                                                  
         EJECT                                                                  
*                                                                               
PRTTAX   DS    F                   TOTAL PAYROLL TAX TO BE PRINTED              
PRTHAND  DS    F                   TOTAL HANDLING TO BE PRINTED                 
PRTHANDI DS    F                   INDIVIDUAL HANDLING TO BE PRINTED            
PRTHANDC DS    F                   CORP HANDLING TO BE PRINTED                  
PRTAGYCM DS    F                   AGENCY COMMISSION TO BE PRINTED              
PRTSIGNF DS    F                   SIGNATORY FEE TO BE PRINTED                  
PRTCSF   DS    F                   CONTRACT SERVICE FEE TO BE PRINTED           
*                                                                               
PRTEMSF  DS    F                   EMS FEE TO BE PRINTED                        
PRTEMST  DS    F                   EMS INVOICE TOTAL TO BE PRINTED              
EMSFLAG  DS    CL1                 1- AGROUP=EMS, RECNUM = BI OR AC             
*                                  2- 2ND RUN OF ACOPY (PRINT INV)              
*                                  3- 2ND RUN OF BILLING (PRINT INV)            
EMSOVAPP DS    CL1                 OVERRIDES APPLIED                            
*                                                                               
         SPACE 2                                                                
         EJECT                                                                  
*              TABLE OF LOCK BOX CODES/MESSAGES                                 
*                                                                               
         DS    0D                                                               
LBOXTAB  DS    0CL8                                                             
         DC    AL1(TAAYLCHI),AL1(L'LTBOXC),AL2(P11LBOXC-P11LINED)               
         DC    AL4(LTBOXC)                                                      
         DC    AL1(TAAYLNY),AL1(L'LTBOXN),AL2(P11LBOXN-P11LINED)                
         DC    AL4(LTBOXN)                                                      
         DC    AL1(TAAYLDDS),AL1(L'LTBOXD),AL2(P11LBOXD-P11LINED)               
         DC    AL4(LTBOXD)                                                      
         DC    AL1(TAAYLP),AL1(L'LTBOXP),AL2(P11LBOXP-P11LINED)                 
         DC    AL4(LTBOXP)                                                      
         DC    AL1(TAAYLT),AL1(L'LTBOXT),AL2(P11LBOXT-P11LINED)                 
         DC    AL4(LTBOXT)                                                      
         DC    X'FF'                                                            
*              EXTRA STUFF                                                      
         SPACE 1                                                                
*&&DO                                                                           
LTBOXC   DC    CL80'Remit to: Talent Partners, 3603 Paysphere Circle, CX        
               hicago, IL.  60674'                                              
LTBOXP   DC    CL80'Remit to: Print Payroll Services, 541 N. Fairbanks X        
               Ct., Chicago, IL 60611'                                          
LTBOXT   DC    CL80'Remit to: Talent Partners, 541 N. Fairbanks Ct., ChX        
               icago, IL 60611'                                                 
LTBOXD   DC    CL65'Remit to: Talent Partners, 115 W. 18th St, New YorkX        
               ,NY 10011-4113'                                                  
LTBOXP   DC    CL80'Remit to: Print Payroll Services, 400 N. Michigan AX        
               ve, 14 Fl, Chicago, IL 60611'                                    
LTBOXT   DC    CL80'Remit to: Talent Partners, 400 N. Michigan Ave, 14 X        
               Fl, Chicago, IL 60611'                                           
*&&                                                                             
LTBOXP   DC    CL80'Remit to: Print Payroll Services, 111 W Jackson BlvX        
               d Ste 1525, Chicago, IL 60604'                                   
LTBOXC   DC    CL80'Remit to: Talent Partners, Department 6095, Carol SX        
               tream, IL 60122-6095'                                            
*                                                                               
LTBOXN   DC    CL65'Remit to: P.O. Box 10129, Church Street Station, NYX        
               ,NY 10259-0129'                                                  
LTBOXD   DC    CL80'Remit to: Talent Partners, 475 Park Avenue South, NX        
               ew York, NY 10016'                                               
LTBOXT   DC    CL80'Remit to: Talent Partners, 111 W. Jackson Blvd, SteX        
                1525, Chicago, IL 60604'                                        
*                                                                               
         EJECT                                                                  
*&&DO                                                                           
YEARTAB  DS    0C                                                               
         DC    AL1(ALL,CN90),X'69',AL2(00001000,1150,1150,1000,0950)            
         DC    AL1(ALL,CN91),X'6A',AL2(00001200,1150,1250,1000,0950)            
         DC    AL1(ALL,CN92),X'6A',AL2(00001200,1250,1250,1000,0950)            
         DC    AL1(ALL,CN93),X'6C',AL2(00001200,1250,1250,1000,1000)            
         DC    AL1(ALL,CN94),X'6D',AL2(00001200,1265,1265,1000,1000)            
         DC    AL1(ALL,CN95),X'6D',AL2(00001200,1265,1265,1000,1000)            
         DC    AL1(ALL,CN96),X'6D',AL2(00001200,1265,1265,1000,1000)            
         DC    AL1(ALL,CN97),X'8F',AL2(00001300,1265,1265,1000,1000)            
         DC    AL1(ALL,CN98),X'8F',AL2(00001300,1265,1265,1000,1000)            
         DC    AL1(ALL,CN99),X'8F',AL2(00001300,1265,1265,1000,1000)            
         DC    AL1(ALL,CN00),X'8E',AL2(00001300,1330,1330,1000,1000)            
         DC    AL1(ALL,CN01),X'8E',AL2(00001300,1330,1330,1000,1000)            
         DC    X'00'                                                            
         DROP  RB,R7,R6,R5                                                      
*&&                                                                             
         EJECT                                                                  
*                                                                               
*                                                                               
*        PRINT CANADIAN CONVERSION RATE                                         
*                                                                               
         USING P6LINED,R2                                                       
SUBCVT   NTR1  BASE=*,LABEL=*                                                   
         CLI   RECNUM,AC           IF THIS IS AGENCY COPY                       
         BE    *+12                                                             
         CLI   RECNUM,EB           OR E-BILLING                                 
         BNE   SBCVT10                                                          
         TM    TBCLSTA2,TACISTCA   AND CLIENT HAS CAN STAT,                     
         JO    NO                                                               
         TM    TBAYSTA6,TAAYSTCA   OR AGENCY HAS CAN STAT,                      
         JO    NO                  DO NOT PRINT THIS LINE                       
SBCVT10  CLI   FCR,C'Y'            USE US FLAGS                                 
         JE    NO                                                               
         MVI   FCR,C'Y'            FLAG IT PRINTED                              
         MVC   P6TOT(15),LTCCVT    CANADIAN CONVERSION RATE                     
         LA    RF,P6MISC+4                                                      
         EDIT  TBGCVT,(6,(RF)),2                                                
         J     YES                                                              
         DROP  R2                                                               
         LTORG                                                                  
         EJECT                                                                  
*=====================================================================          
*        CONVERT CAN$ CHLOE HANDLING TO US$                                     
*=====================================================================          
CVTCNCHL NTR1  BASE=*,LABEL=*                                                   
         ZAP   DUB,TBGCVT                                                       
         CVB   R1,DUB                                                           
         ST    R1,FULL                                                          
         L     R2,EMSTHND                                                       
         GOTO1 MULT,DMCB,(R2),FULL                                              
         L     R1,0(R1)                                                         
         ST    R1,EMSTHNDU         EMS CAN HANDLING IN US$                      
         J     XIT                                                              
         LTORG                                                                  
         EJECT                                                                  
*=====================================================================          
*        ADJ BNP AMOUNT FOR CANADIAN ROUNDING ERROR                             
*        P1 = DIFFERENCE                                                        
*=====================================================================          
ADJBNP   NTR1  BASE=*,LABEL=*                                                   
         L     R3,0(R1)            R3=DIFFRENCE                                 
                                                                                
         XR    RF,RF               TOTAL AMOUNT IN TABLE                        
                                                                                
         USING BNPD,R2                                                          
         L     R2,=V(BNPTAB)       TABLE OF PNH FOR BNP INVOICES                
         LR    R4,R2               POINTS TO HIGHEST AMOUNT IN TABLE            
         OC    0(BNPLEN,R2),0(R2)  NO ENTRIES?                                  
         JZ    ADJBNPX                                                          
                                                                                
ADJBNP10 C     R2,=V(BNPTABX)      IF WE REACHED THE END OF TABLE               
         JNL   ADJBNP90                                                         
         OC    0(BNPLEN,R2),0(R2)  OR END OF ENTRIES                            
         JZ    ADJBNP90                                                         
                                                                                
BNPTB    USING BNPD,R4                                                          
                                                                                
         OC    BNPAMT,BNPAMT       ANY AMOUNT?                                  
         JZ    ADJBNP80                                                         
         A     RF,BNPAMT                                                        
                                                                                
         CLC   BNPTB.BNPAMT,BNPAMT                                              
         JNL   ADJBNP80                                                         
         LR    R4,R2                                                            
                                                                                
ADJBNP80 AHI   R2,BNPLEN           BUMP TO NEXT ENTRY                           
         J     ADJBNP10                                                         
                                                                                
ADJBNP90 L     R1,BNPTB.BNPAMT                                                  
         SR    R1,R3                                                            
         ST    R1,BNPTB.BNPAMT                                                  
                                                                                
ADJBNPX  J     XIT                                                              
                                                                                
         LTORG                                                                  
         EJECT                                                                  
*&&DO                                                                           
*                                                                               
*              ROUTINE TO SET EFFECTIVE DATE FOR INVOICE                        
*              USE TODAY'S DATE IF NOT A CREDIT                                 
*              IF NEW CREDITING ENABLED, AND A CREDIT, USE PUR PRINT DT         
*              IF NO PUR PRINT DATE, USE BILLED DATE OF ORIGINAL INV            
*                                                                               
SETEFDT  NTR1  BASE=*,LABEL=*                                                   
         MVC   SVEFDTE1,TGTODAY1   SET AS TODAY'S DATE                          
                                                                                
         TM    TGSYSTAT,TASYSCRD   IF NEW CREDITING RULES ENABLED,              
         JZ    XIT                                                              
         TM    TBIPST2,TAPDPCIB    AND IF CREDIT INVOICE HAS SAVED              
         JZ    XIT                 BILLING RATES, USE PUR/BILL DATE             
                                                                                
         BRAS  RE,GETCRD           GET CREDITED INVOICE IN AIO2                 
         L     R4,AIO2                                                          
         MVI   ELCODE,TAINELQ      GET INVOICE ELEMENT                          
         BRAS  RE,GETEL                                                         
         JNE   XIT                                                              
                                                                                
         USING TAIND,R4                                                         
         OC    TAINHDTE,TAINHDTE   PUR PRINT DATE                               
         BZ    SEFDT10                                                          
         MVC   SVEFDTE1,TAINHDTE                                                
         J     XIT                                                              
*                                                                               
SEFDT10  OC    TAINBDTE,TAINBDTE   IF NO PUR PRINT DATE, GET BILLED DT          
         JZ    XIT                                                              
         MVC   SVEFDTE1,TAINBDTE                                                
         J     XIT                                                              
         DROP  R4                                                               
         LTORG                                                                  
         EJECT                                                                  
*&&                                                                             
*=====================================================================*         
* GET SYSTEM RECORD                                                             
* IF INVOICE IS A NEW STYLE CREDIT, GET SYSTEM RECORD FROM EFFECTIVE            
* DATE OF ORIGINAL INVOICE BEING CREDITED                                       
*=====================================================================*         
GETSYS   NTR1  BASE=*,LABEL=*                                                   
         TM    TGSYSTAT,TASYSCRD   IF NEW CREDITING RULES ENABLED,              
         BZ    GSYS50                                                           
         TM    TBIPST2,TAPDPCIB   AND IF CREDIT INVOICE HAS SAVED               
         BZ    GSYS50             BILLING RATES, USE EFFECTIVE DATE             
                                                                                
         USING TLSYD,R4                                                         
         LA    R4,KEY                                                           
         XC    KEY,KEY                                                          
         MVI   TLSYCD,TLSYCDQ          READ SYSTEM RECORD                       
         MVI   TLSYTYPE,TLSYSAVE       SAVED SYSTEM RECORD                      
         MVC   TLSYSVDT,TBEFFDTE       EFFECTIVE DATE                           
         GOTO1 HIGH                                                             
         CLC   KEY(TLSYLEN-TLSYD),KEYSAVE  IF IT DOES NOT EXIST, READ           
         BNE   GSYS50                      TODAY'S SYSTEM RECORD                
         MVC   AIO,AIO2                                                         
         GOTO1 GETREC                                                           
         B     GSYSX                                                            
         DROP  R4                                                               
                                                                                
GSYS50   MVC   AIO,AIO2                                                         
         GOTO1 RECVAL,DMCB,TLSYCDQ,(X'20',0)  GET TODAY'S SYSTEM RECORD         
         BE    *+6                                                              
         DC    H'0'                                                             
                                                                                
GSYSX    MVC   AIO,AIO1                                                         
         J     XIT                                                              
         LTORG                                                                  
         EJECT                                                                  
*=====================================================================          
*              IF CHLOE, ADD SUI SURCHARGE FOR CA AND NY                        
*              TO PAYROLL TAXES AND TOTAL FOR CHLOE COPY                        
*=====================================================================          
*                                                                               
CHLSUR   NTR1  BASE=*,LABEL=*                                                   
         CLI   TBTPO,C'O'          OR OFFICE O                                  
         BE    *+12                                                             
         CLI   TBTPO,C'Q'          OR OFFICE Q                                  
         JNE   XIT                                                              
         GOTOR SSURPT,DMCB,EMSTTAX,SUISURCA  ADD CA/NY SURCHARGE                
         GOTOR SSURPT,DMCB,EMSTOT,SUISURCA   TO P/R TAX AND TOTAL               
         J     XIT                                                              
         LTORG                                                                  
         EJECT                                                                  
*=====================================================================          
*              SUI SURCHARGE FOR CA AND NY                                      
*                  P1 = A(TALIM BLOCK)                                          
*                  P2 = A(FIRST SUI SURCHARGE ACCUM)                            
*=====================================================================          
*                                                                               
         USING TMD,R2                                                           
         USING TABCHKD,R3                                                       
SUISUR   NTR1  BASE=*,LABEL=*                                                   
         TM    TGSYSTA2,TASYSNCS   ALLOW TO CHARGE IT?                          
         JZ    XIT                 NO, LEAVE                                    
                                                                                
         CLI   EMSFLAG,3           IF EMS AND ON 2ND PASS,                      
         JNL   XIT                 DON'T NEED TO STORE IT TWICE                 
                                                                                
         CLI   TBTPO,C'O'          IF OFFICE O                                  
         JE    *+12                                                             
         CLI   TBTPO,C'Q'          OR OFFICE Q                                  
         JNE   SUISUR05                                                         
         CLI   EMSFLAG,1           AND CHLOE CALCULATION PASS                   
         JE    XIT                 DON'T NEED TO STORE IT TWICE                 
                                                                                
SUISUR05 L     R2,0(R1)                                                         
         L     RE,4(R1)                                                         
                                                                                
         LA    RF,SUISURTB                                                      
SUISUR10 CLI   0(RF),X'FF'                                                      
         JE    XIT                                                              
         CLC   TMUNIT,0(RF)        MATCH ON UNIT                                
         JE    SUISUR30                                                         
         AHI   RE,4                BUMP                                         
         AHI   RF,2                                                             
         J     SUISUR10                                                         
*                                                                               
SUISUR30 L     R1,TMSUISUR                                                      
         L     R0,0(RE)                                                         
         AR    R0,R1               ADD TO ACCUM                                 
         ST    R0,0(RE)                                                         
*                                                                               
         J     XIT                                                              
*                                                                               
         DS    0H                                                               
SUISURTB DC    CL2'CA'                                                          
         DC    CL2'NY'                                                          
         DC    X'FF'                                                            
         LTORG                                                                  
                                                                                
         DROP  R2,R3                                                            
         EJECT                                                                  
*=====================================================================          
*              ADD SUI SURCHARGE TO PAYROLL TAXES                               
*                  P1 = PAYROLL TAX                                             
*                  P2 = A(FIRST SUI SURCHARGE ACCUM)                            
*=====================================================================          
*                                                                               
SSURPT   NTR1  BASE=*,LABEL=*                                                   
         TM    TGSYSTA2,TASYSNCS   ALLOW TO CHARGE IT?                          
         JZ    XIT                 NO, LEAVE                                    
                                                                                
         L     RF,0(R1)            PAYROLL TAX                                  
         ICM   R4,15,0(RF)                                                      
                                                                                
         L     R2,4(R1)                                                         
         LA    R3,SSURPTTB                                                      
SSURPT10 CLI   0(R3),X'FF'                                                      
         JE    SSURPT90                                                         
         ICM   RF,15,0(R2)                                                      
         AR    R4,RF                                                            
         AHI   R3,2                                                             
         AHI   R2,4                                                             
         J     SSURPT10                                                         
                                                                                
SSURPT90 L     RF,0(R1)                                                         
         STCM  R4,15,0(RF)                                                      
         J     XIT                                                              
                                                                                
         DS    0H                                                               
SSURPTTB DC    CL2'CA'                                                          
         DC    CL2'NY'                                                          
         DC    X'FF'                                                            
         LTORG                                                                  
         EJECT                                                                  
*              BOTTOM LINE OF INVOICE TOTAL BOX                                 
*                                                                               
         USING P11LINED,R2         R2=A(PRINT LINE)                             
PRBLINE  NTR1  BASE=*,LABEL=*                                                   
         TM    STATUS,STCOD        IF COD INVOICE                               
         BO    PRBLINE5            SKIP AHEAD                                   
*                                                                               
         TM    TBSTA2,TAINSPRM     IF PRIMARY INVOICE                           
         BNO   *+14                                                             
         MVC   P11SUML(L'LTSUML),LTSUML  PRINT SUMMARY LISTING MESSAGE          
         B     PRBLINE5                                                         
*                                                                               
         CLI   TBAYLBOX,0          IF LOCK BOX CODE                             
         BE    PRBLINE5                                                         
         CLI   RECNUM,CC           CHLOE COPY                                   
         JE    PRBLINE5                                                         
         LA    RE,LBOXTAB          RE=A(LOCK BOX LITERAL TABLE)                 
         USING LOCKTABD,RE                                                      
PRBLINE3 CLI   LOCKCD,X'FF'        TEST END OF TABLE                            
         BE    PRBLINE5                                                         
         CLC   TBAYLBOX,LOCKCD     MATCH ON LOCK BOX CODE                       
         BE    *+12                                                             
         LA    RE,L'LBOXTAB(RE)                                                 
         B     PRBLINE3                                                         
         AH    R2,LOCKPDSP         R2=A(P) + DISPLACEMENT                       
         L     R1,LOCKDATA         R1=DATA TO PRINT                             
         ZIC   RF,LOCKLLEN         RF=LENGTH OF DATA                            
         BCTR  RF,0                                                             
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R2),0(R1)       MOVE DATA TO PRINT LINE                      
         DROP  RE                                                               
*                                                                               
PRBLINE5 BRAS  RE,BXBOT            CLOSE BOX                                    
         BRAS  RE,PRTIT                                                         
         J     XIT                                                              
         DROP  R2                                                               
         LTORG                                                                  
         EJECT                                                                  
*                                                                               
*              ROUTINE TO POP IN A BOTTOM AT END OF REPORT                      
*                                                                               
         SPACE 1                                                                
BXBOT    NTR1   BASE=*,LABEL=*                                                  
         L     R3,ABOX                                                          
         USING BOXD,R3                                                          
         MVC   BOXROWS,SPACES                                                   
         ZIC   RF,LINE                                                          
         LA    RF,BOXROWS-1(RF)                                                 
         MVI   0(RF),C'B'                                                       
         MVI   BOXINIT,0                                                        
         J     XIT                                                              
         DROP  R3                                                               
         LTORG                                                                  
         EJECT                                                                  
*                                                                               
*        CHECK IF OVERRIDE HANDLING FEE                                         
*        IF SO OVERRIDE WITH AMOUNT IN TAPA ELEMENT                             
*                                                                               
CKHAND   NTR1  BASE=*,LABEL=*                                                   
         TM    BOVRSTAT,BOVRHNDB+BOVRHNDP+BOVRHNDC  BOVER HAND AMOUNT?          
         BNZ   CKH05                                                            
         TM    SVOPT1,TAPDOHND     IF OVERRIDE HANDLING FEE                     
         BNO   CKXIT                                                            
         MVI   WORK,TAPATHND                                                    
         BRAS  RE,GETPAAMT         GET AMOUNT FROM TAPA ELEMENT                 
                                                                                
CKH05    TM    TBISTAT,TAPDSCAN    IF THIS IS A CANADIAN PAYMENT                
         BNO   CKH10                                                            
         TM    BOVRSTAT,BOVRHNDB+BOVRHNDP+BOVRHNDC  BOVER HAND AMOUNT?          
         BZ    CKH07                                                            
         BRAS  RE,SETHANDA         SET POSTING DETAILS                          
         B     CKXIT                                                            
CKH07    MVC   TBHANDC,FULL        SET OVERRIDE HANDLING INTO CORP HND          
         B     CKXIT                                                            
*                                                                               
CKH10    CLI   GTYPE,TABRTY10      FOR TYPE 10'S                                
         BE    CKH15                                                            
         CLI   GTYPE,TABRTY15      AND FOR TYPE 15'S                            
         BE    CKH15                                                            
         CLI   GTYPE,TABRTY14      AND FOR TYPE 14'S                            
         BE    CKH15                                                            
         TM    STAT2,STCORP        OR IF THERE IS A CORP ON INVOICE             
         BNO   CKH20                                                            
*                                                                               
CKH15    TM    BOVRSTAT,BOVRHNDB+BOVRHNDP+BOVRHNDC  BOVER HAND AMOUNT?          
         BZ    CKH18                                                            
         BRAS  RE,SETHANDC         SET NEW POSTING DETAILS                      
         L     R1,PRTHANDC         ADD BOVER HANDLING TO TOTAL                  
         A     R1,FULL                                                          
         ST    R1,PRTHANDC                                                      
         B     CKXIT               DONE                                         
                                                                                
CKH18    MVC   TBHANDC,FULL                                                     
         MVC   PRTHANDC,FULL       PUT AMOUNT IN CORP FIELDS                    
         XC    TBHANDI,TBHANDI                                                  
         XC    PRTHANDI,PRTHANDI   & CLEAR OTHER HANDLING FIELDS                
         XC    PRTHAND,PRTHAND                                                  
         B     CKXIT                                                            
*                                                                               
CKH20    CLI   GTYPE,TABRTY1       IF BILLING TYPE 1                            
         BE    CKH25                                                            
         CLI   GTYPE,TABRTY2       OR 2                                         
         BE    CKH25                                                            
         CLI   GTYPE,TABRTY16      OR 16                                        
         BE    CKH25                                                            
         CLI   GTYPE,TABRTY20      OR 20                                        
         BNE   CKH30                                                            
*                                                                               
CKH25    CLC   SVCKNUM,=H'1'       & ONLY 1 PERFORMER PAID                      
         BNH   CKXIT               DONE                                         
**NO-OP  L     R1,FULL                                                          
**SEP/09 A     R1,TBTAX            ELSE ADD IT TO THE TAX                       
**       ST    R1,PRTTAX                                                        
**NO-OP  ST    R1,TBTAX                                                         
*                                                                               
CKH30    TM    BOVRSTAT,BOVRHNDB+BOVRHNDP+BOVRHNDC  BOVER HAND AMOUNT?          
         BZ    CKH35                                                            
         BRAS  RE,SETHANDI         IF BOVER, SET NEW POSTING DETAILS            
         L     R1,PRTHAND          ADD BOVER HANDLING TO TOTALS                 
         A     R1,FULL                                                          
         ST    R1,PRTHAND                                                       
         L     R1,PRTHANDI                                                      
         A     R1,FULL                                                          
         ST    R1,PRTHANDI                                                      
         J     XIT                                                              
                                                                                
CKH35    MVC   TBHANDI,FULL        ELSE                                         
         MVC   PRTHAND,FULL                                                     
         MVC   PRTHANDI,FULL       SET INDIVIDUAL HANDLING                      
         XC    TBHANDC,TBHANDC                                                  
         XC    PRTHANDC,PRTHANDC   & CLEAR OTHER HANDLING FIELDS                
*                                                                               
CKXIT    J     XIT                                                              
         LTORG                                                                  
         EJECT                                                                  
*                                                                               
*        CHECK IF PAYMENT OPTION FEE                                            
*        IF SO ADD TO HANDLING                                                  
*                                                                               
CKFEE    NTR1  BASE=*,LABEL=*                                                   
         USING TAPAD,R4                                                         
         MVI   ELCODE,TAPAELQ                                                   
         GOTO1 GETL,DMCB,(1,=AL1(TAPATFEE))                                     
         BNE   CFEEXIT                                                          
         L     R4,TGELEM                                                        
         ICM   RE,15,TAPADATA                                                   
         DROP  R4                                                               
*                                                                               
         USING TMD,R3                                                           
         L     R3,ATALIM                                                        
         USING TAPGD,R1                                                         
         L     R1,TMACURPG         GET TAPG ELEMENT                             
         LTR   R1,R1               IF NEW POSTING ELEMENT EXISTS,               
         BZ    CFEE05                                                           
         ST    RE,TAPGWFEE         STORE IN WIRE FEE                            
         DROP  R1,R3                                                            
*                                                                               
CFEE05   TM    TBISTAT,TAPDSCAN    IF THIS IS A CANADIAN PAYMENT                
         BNO   CFEE10                                                           
         A     RE,TBHANDC          ADD THE FEE TO CANADIAN HANDLING             
         ST    RE,TBHANDC                                                       
         B     CFEEXIT                                                          
*                                                                               
CFEE10   CLI   GTYPE,TABRTY10      FOR TYPE 10'S                                
         BE    CFEE20                                                           
         CLI   GTYPE,TABRTY15      AND FOR TYPE 15'S                            
         BE    CFEE20                                                           
         CLI   GTYPE,TABRTY14      AND FOR TYPE 14'S                            
         BE    CFEE20                                                           
         TM    STAT2,STCORP        OR IF THERE IS A CORP ON INVOICE             
         BNO   CFEE30                                                           
CFEE20   A     RE,TBHANDC                                                       
         ST    RE,TBHANDC                                                       
         ST    RE,PRTHANDC         PUT AMOUNT IN CORP FIELDS                    
         B     CFEEXIT             DONE                                         
*                                                                               
CFEE30   CLI   GTYPE,TABRTY1       IF BILLING TYPE 1                            
         BE    CFEE40                                                           
         CLI   GTYPE,TABRTY2       OR 2                                         
         BE    CFEE40                                                           
         CLI   GTYPE,TABRTY16      OR 16                                        
         BE    CFEE40                                                           
         CLI   GTYPE,TABRTY20      OR 20                                        
         BNE   CFEE50                                                           
CFEE40   CLC   SVCKNUM,=H'1'       AND MORE THAN 1 PERFORMER PAID               
         BNH   CFEE50                                                           
         A     RE,TBTAX            ADD IT TO THE TAX                            
         ST    RE,TBTAX                                                         
         ST    RE,PRTTAX                                                        
         B     CFEEXIT                                                          
*                                                                               
CFEE50   A     RE,TBHANDI                                                       
         ST    RE,TBHANDI          ELSE                                         
         ST    RE,PRTHAND                                                       
         ST    RE,PRTHANDI         SET INDIVIDUAL HANDLING                      
CFEEXIT  J     XIT                                                              
         SPACE 2                                                                
         LTORG                                                                  
         EJECT                                                                  
*        PRINT TAXES FIELD                                                      
*                                                                               
SETPRT   NTR1  BASE=*,LABEL=*                                                   
         CLI   GTYPE,TABRTY9                                                    
         BE    SP20                                                             
         CLI   GTYPE,TABRTY13                                                   
         BE    SP20                                                             
         CLI   GTYPE,TABRTY15      FOR TYPE 15 OR                               
         BE    SP05                                                             
         CLI   GTYPE,TABRTY14      FOR TYPE 14 OR TYPE 10                       
         BE    SP05                                                             
         CLI   GTYPE,TABRTY10                                                   
         BNE   SP10                                                             
SP05     L     R1,PRTTAX           TAX + IND HANDLIN => TAX                     
         A     R1,PRTHANDI                                                      
         ST    R1,PRTTAX                   CORP HAND => HANDLING                
         MVC   PRTHAND,PRTHANDC                                                 
         B     SP30                                                             
*                                                                               
SP10     DS    0H                  FOR ALL OTHER TYPES                          
         L     R1,PRTHANDC         CORP + HANDLING => HAND                      
         A     R1,PRTHANDI                                                      
         ST    R1,PRTHAND                                                       
         B     SP30                                                             
*                                                                               
SP20     L     R1,PRTTAX           TYPE 9,13 - TAX + HANDLING => TAX            
         A     R1,PRTHANDI                                                      
         A     R1,PRTHANDC                                                      
         ST    R1,PRTTAX                                                        
         XC    PRTHAND,PRTHAND     CLEAR PRINTABLE HANDLING FIELD               
*                                                                               
SP30     CLI   GTYPE,TABRTY11                                                   
         BNE   SPX                                                              
         L     R1,PRTTAX                                                        
         A     R1,PRTHAND           TYPE 11 ALL AMTS IN HANDLING                
         ST    R1,PRTHAND                                                       
         XC    PRTTAX,PRTTAX                                                    
         J     NO                                                               
*                                                                               
SPX      J     YES                                                              
         LTORG                                                                  
         EJECT                                                                  
*                                                                               
*                                                                               
*        GET UNION/LOCAL ACCOUNT NUMBER                                         
*                                                                               
         SPACE 1                                                                
GETACC   NTR1  BASE=*,LABEL=*                                                   
         LA    R3,TBUNI1           1ST UNION                                    
         LA    R2,3                MAX 3 UNIONS                                 
*                                                                               
GA10     XC    KEY,KEY                                                          
         LA    R4,KEY                                                           
         USING TLLOD,R4            GET UNION LOCAL RECORD                       
         MVI   TLLOCD,TLLOCDQ                                                   
         MVC   TLLOUN,0(R3)        UNION                                        
         MVC   TLLOLCL,3(R3)       LOCAL                                        
         GOTO1 HIGH                                                             
         CLC   KEY(L'TLLOKEY),KEYSAVE                                           
         BNE   GA20                                                             
         DROP  R4                                                               
*                                                                               
         MVC   AIO,AIO2                                                         
         GOTO1 GETREC                                                           
         GOTO1 MVCTST1,DMCB,0,(L'TBLCNAM1,WORK),TANAELQ                         
         MVC   18(L'TBLCNAM1,R3),WORK                                           
         GOTO1 MVCTST1,DMCB,TANUTLOC,(L'TBACC1,WORK),TANUELQ                    
         MVC   6(L'TBACC1,R3),WORK                                              
*                                                                               
         L     R4,AIO              GET LOCAL ELEMENT                            
         USING TALOD,R4                FOR SS NUMBER                            
         MVI   ELCODE,TALOELQ                                                   
         BRAS  RE,GETEL                                                         
         BNE   GA20                                                             
         TM    TALOSTAT,TALOSHNW   IS THERE AN H&W FUND                         
         BNO   GA20                                                             
         MVC   34(L'TALOSSN,R3),TALOSSN                                         
*                                                                               
GA20     LA    R3,LOCINFLN(R3)                                                  
         BCT   R2,GA10                                                          
*                                                                               
         MVC   AIO,AIO1                                                         
         J     XIT                                                              
         DROP R4                                                                
         LTORG                                                                  
         EJECT                                                                  
*                                                                               
*        UPDATE YTD TABLE WITH THIS CHECKS AMOUNTS                              
*                                                                               
UPDYTD   NTR1  BASE=*,LABEL=*                                                   
         TM    STATUS,STCOD        IF THIS IS A COD                             
         JO    XIT                 DON'T UPDATE YTD TABLE                       
         CLI   EMSFLAG,1           IF FIRST RUN OF EMS BILLING,                 
         JE    XIT                 DON'T UPDATE YTD TABLE                       
         L     R2,ATALIM                                                        
         USING TMD,R2              R2=A(TALIM BLOCK)                            
         L     R1,ATHISYTD                                                      
         USING TYTDTABD,R1                                                      
         MVC   TYTDEARN,TMNYEARN                                                
         MVC   TYTDFUI,TMNYFUI                                                  
         MVC   TYTDSUI,TMNYSUI                                                  
         MVC   TYTDFICA,TMNYFICA                                                
         MVC   TYTDMED,TMNYMED                                                  
         MVC   TYTDNTAX,TMNYNTAX                                                
         J     XIT                                                              
         DROP  R2,R1                                                            
         EJECT                                                                  
*&&DO                                                                           
*                                                                               
*                                                                               
*        ADD CHLOE RATES ON TOP OF REGULAR BILLING RATES                        
*                                                                               
         USING TMD,R4                                                           
ADDCHLOE NTR1  BASE=*,LABEL=*                                                   
         L     RE,TMACURRA         NEW RATES                                    
         USING TARAD,RE                                                         
                                                                                
         XR    R1,R1                                                            
         ICM   RF,15,TARATFU       ADD CHLOE FUTA TO FUTA                       
         ICM   R1,3,ADRFUTA                                                     
         AR    RF,R1                                                            
         STCM  RF,15,TARATFU                                                    
                                                                                
         XR    R1,R1                                                            
         ICM   RF,15,TARATSU       ADD CHLOE SUTA TO SUTA                       
         ICM   R1,3,ADRSUTA                                                     
         AR    RF,R1                                                            
         STCM  RF,15,TARATSU                                                    
                                                                                
         XR    R1,R1                                                            
         ICM   RF,15,TARATFI       ADD CHLOE FICA TO FICA                       
         ICM   R1,3,ADRFICA                                                     
         AR    RF,R1                                                            
         STCM  RF,15,TARATFI                                                    
                                                                                
         XR    R1,R1                                                            
         ICM   RF,15,TARATM        ADD CHLOE MED TO MEDICARE                    
         ICM   R1,3,ADROVER                                                     
         AR    RF,R1                                                            
         STCM  RF,15,TARATM                                                     
                                                                                
         XR    R1,R1                                                            
         ICM   RF,15,TARATWC       ADD CHLOE WC TO WC                           
         ICM   R1,3,ADRWCRP                                                     
         AR    RF,R1                                                            
         STCM  RF,15,TARATWC                                                    
                                                                                
         XR    R1,R1                                                            
         ICM   RF,15,TARAHFB       ADD CHLOE HANDLING TO HANDLING               
         ICM   R1,3,ADRHAND                                                     
         AR    RF,R1                                                            
         STCM  RF,15,TARAHFB                                                    
                                                                                
         CLI   TARATYPE,TABRTY21   IF BILLING TYPES 21, 23, OR 24               
         BE    ADCHL10                                                          
         CLI   TARATYPE,TABRTY23                                                
         BE    ADCHL10                                                          
         CLI   TARATYPE,TABRTY24                                                
         BNE   ADCHL20                                                          
ADCHL10  XR    R1,R1                                                            
         ICM   RF,15,TARARFB       ADD CHLOE HANDLING TO REUSE HAND             
         ICM   R1,3,ADRHAND                                                     
         AR    RF,R1                                                            
         STCM  RF,15,TARARFB                                                    
                                                                                
ADCHL20  TM    TARASTA1,TARASPRE   IF AGENCY IS PREMIUM,                        
         BZ    ADCHL40                                                          
         XR    R1,R1                                                            
         ICM   RF,15,TARAHFP       ADD CHLOE HANDLING TO PREM HAND              
         ICM   R1,3,ADRHAND                                                     
         AR    RF,R1                                                            
         STCM  RF,15,TARAHFP                                                    
                                                                                
         CLI   TARATYPE,TABRTY21   IF BILLING TYPES 21, 23, OR 24               
         BE    ADCHL30                                                          
         CLI   TARATYPE,TABRTY23                                                
         BE    ADCHL30                                                          
         CLI   TARATYPE,TABRTY24                                                
         BNE   ADCHL40                                                          
ADCHL30  XR    R1,R1                                                            
         ICM   RF,15,TARARFP       ADD CHLOE HANDLING TO PRM REUSE HND          
         ICM   R1,3,ADRHAND                                                     
         AR    RF,R1                                                            
         STCM  RF,15,TARARFP                                                    
                                                                                
ADCHL40  XR    R1,R1                                                            
         ICM   RF,15,TARAHFBC      ADD CHLOE CAN HAND TO CAN HAND               
         ICM   R1,3,ADRCAN                                                      
         AR    RF,R1                                                            
         STCM  RF,15,TARHFBC                                                    
                                                                                
         CLI   TARATYPE,TABRTY21   IF BILLING TYPES 21, 23, OR 24               
         BE    ADCHL50                                                          
         CLI   TARATYPE,TABRTY23                                                
         BE    ADCHL50                                                          
         CLI   TARATYPE,TABRTY24                                                
         BNE   ADCHLX                                                           
ADCHL50  XR    R1,R1                                                            
         ICM   RF,15,TARARFBC      ADD CHLOE CAN HAND TO CAN REUSE HND          
         ICM   R1,3,ADRCAN                                                      
         AR    RF,R1                                                            
         STCM  RF,15,TARHFBC                                                    
                                                                                
***      TM    SVOPT4,TAPDOHDP+TAPDOHDA                                         
***      BZ    APPADD60                                                         
***      ICM   R1,15,FULL                                                       
                                                                                
ADCHLX0  J     XIT                                                              
         DROP  R4,RE                                                            
         EJECT                                                                  
*&&                                                                             
*======================================================================         
*        SET BRATE RATES                                                        
*======================================================================         
SETBRATE NTR1  BASE=*,LABEL=*                                                   
         L     R4,ATALIM                                                        
         USING TMD,R4                                                           
         XC    TMACURRA,TMACURRA   CURRENT BILLING RATES ELEMENT                
         XC    TMACURPG,TMACURPG   CURRENT POSTING DETAILS ELEMENT              
                                                                                
         CLI   TBTPO,C'O'          OR OFFICE O                                  
         BE    *+12                                                             
         CLI   TBTPO,C'Q'          OR OFFICE Q                                  
         BNE   SBRATE10                                                         
         CLI   EMSFLAG,1           IF CHLOE PASS IN BILLING,                    
         BNE   SBRATE10            IGNORE BRATES                                
         XC    TMACPYRA,TMACPYRA   CLEAR COPY OF BILLING RATES ELEMENT          
         J     XIT                                                              
                                                                                
         USING TARAD,RE                                                         
SBRATE10 L     RE,ACURTARA         IF WE ARE CAPTURING POSTINGS AT              
         CLI   TARAEL,TARAELQ      THE NEW LOWER LEVEL ...                      
         JNE   XIT                                                              
         ST    RE,TMACURRA                                                      
         L     RE,ACPYTARA                                                      
         ST    RE,TMACPYRA                                                      
         DROP  RE                                                               
                                                                                
         USING TAPGD,RE                                                         
         L     RE,ACURTAPG                                                      
         MVI   TAPGEL,TAPGELQ      INITIALIZE POSTING DETAILS                   
         MVI   TAPGLEN,TAPGLNQ     ELEMENT CODE AND LENGTH                      
         ST    RE,TMACURPG                                                      
         J     XIT                                                              
         DROP R4,RE                                                             
         LTORG                                                                  
         EJECT                                                                  
*----------------------------------------------------------------------         
*        PRINT SIGNATORY FEE                                                    
*----------------------------------------------------------------------         
         USING P6LINED,R2                                                       
         USING TABDD,R4                                                         
SUBSIGNF NTR1  BASE=*,LABEL=*                                                   
         TM    TBSTAT,TAINSBIL     IF ALREADY BILLED                            
         JO    SUBSIG00                                                         
         TM    TBSTA2,TAINSHLP     OR ALREADY PRINTED (PUR)                     
         JO    SUBSIG00                                                         
         TM    TBSTAT,TAINSCIN     OR IF INVOICE IS CANCELLED                   
         JZ    SUBSIG10                                                         
SUBSIG00 L     R4,AIO                                                           
         GOTOR GETBDCN,DMCB,AIO1   GET TABD ELEMENT                             
         JNE   SUBSIG10                                                         
         L     R1,TABDSIGN         USE CALCULATED AMOUNT                        
         J     SUBSIG70                                                         
         DROP  R4                                                               
*                                                                               
SUBSIG10 OC    SIGNINFO,SIGNINFO   IF AGENCY HAS SIGNATORY FEE INFO             
         JZ    NO                                                               
*                                                                               
         L     R1,PRTSIGNF         IF SIGNATORY FEE ALREADY CALCULATED          
         LTR   R1,R1               NO NEED TO CALCULATE AGAIN                   
         JNZ   SUBSIG80                                                         
*                                                                               
         TM    TBISTAT,TAPDSCAN                                                 
         JO    SUBSIG40                                                         
         L     R1,TBTOT            R1=INVOICE SUBTOTAL                          
         ICM   RF,15,AOSPST                                                     
         A     RF,TBGSTU                                                        
         A     RF,TBPSTU                                                        
         ST    RF,WORK             SAVE TAX IN WORK TEMPORARY                   
         CLI   AYSTSIG,0           FULL CALC                                    
         JE    SUBSIG60                                                         
         CLI   AYSTSIG,7           WAGES + TAXES + HANDLING                     
         JNE   SUBSIG13                                                         
         S     R1,TBPNH            DEDUCT PNH                                   
         S     R1,TBHNW            DEDUCT HNW                                   
         S     R1,TBINR            DEDUCT INR                                   
         J     SUBSIG60                                                         
SUBSIG13 CLI   AYSTSIG,6           WAGES + PNH + HANDLING                       
         JNE   SUBSIG15                                                         
         S     R1,TBTAX                                                         
         S     R1,WORK             DEDUCT GST/PST                               
         J     SUBSIG60                                                         
SUBSIG15 CLI   AYSTSIG,5           WAGES + TAXES + PNH                          
         JNE   SUBSIG18                                                         
         S     R1,TBHANDI                                                       
         S     R1,TBHANDC                                                       
         J     SUBSIG60                                                         
SUBSIG18 L     R1,TBGM             WAGES ONLY                                   
         S     R1,TBHNW            DEDUCT HNW                                   
         S     R1,TBINR            DEDUCT INR                                   
         CLI   AYSTSIG,1                                                        
         JE    SUBSIG60                                                         
         CLI   AYSTSIG,2           WAGES + PAYROLL TAX                          
         JNE   SUBSIG20                                                         
         A     R1,TBTAX                                                         
         A     R1,WORK             US$ GST/PST                                  
         J     SUBSIG60                                                         
SUBSIG20 CLI   AYSTSIG,3           WAGES + PNH                                  
         JNE   SUBSIG23                                                         
         A     R1,TBPNH            ADD PNH                                      
         A     R1,TBHNW            ADD HNW                                      
         A     R1,TBINR            ADD INR                                      
         J     SUBSIG60                                                         
SUBSIG23 CLI   AYSTSIG,4           WAGES + HANDLING                             
         JNE   SUBSIG60                                                         
         A     R1,TBHANDI                                                       
         A     R1,TBHANDC                                                       
         J     SUBSIG60                                                         
*                                                                               
SUBSIG40 L     R1,TBCSTOT                                                       
         S     R1,TBREXP           DEDUCT REIMBURSED EXPENSES                   
         ICM   RF,15,AOSPST                                                     
         A     RF,TBCGST                                                        
         A     RF,TBCPST                                                        
         ST    RF,WORK             SAVE TAX IN WORK TEMPORARY                   
                                                                                
         CLI   AYSTSIG,0           FULL CALC                                    
         JE    SUBSIG60                                                         
         CLI   AYSTSIG,7           WAGES + TAXES + HANDLING                     
         JNE   SUBSIG43                                                         
         S     R1,TBINR                                                         
         J     SUBSIG60                                                         
SUBSIG43 CLI   AYSTSIG,6           WAGES + PNH + HANDLING                       
         JNE   SUBSIG45                                                         
         S     R1,WORK                                                          
         J     SUBSIG60                                                         
SUBSIG45 CLI   AYSTSIG,5           WAGES + TAXES + PNH                          
         JNE   SUBSIG48                                                         
         S     R1,TBCHAND                                                       
         J     SUBSIG60                                                         
SUBSIG48 L     R1,TBGRS            WAGES ONLY                                   
         S     R1,TBINR                                                         
         CLI   AYSTSIG,1                                                        
         JE    SUBSIG60                                                         
         CLI   AYSTSIG,2           WAGES + PAYROLL TAX                          
         JNE   SUBSIG50                                                         
         A     R1,WORK                                                          
         J     SUBSIG60                                                         
SUBSIG50 CLI   AYSTSIG,3           WAGES + PNH                                  
         JNE   SUBSIG53                                                         
         A     R1,TBINR                                                         
         J     SUBSIG60                                                         
SUBSIG53 CLI   AYSTSIG,4           WAGES + HANDLING                             
         JNE   SUBSIG60                                                         
         A     R1,TBCHAND                                                       
*                                                                               
SUBSIG60 TM    TBISTAT,TAPDSCAN    US$ INVOICES,                                
         JO    *+8                                                              
         S     R1,TBREXP           DEDUCT REIMBURSED EXPENSES                   
         XR    R0,R0                                                            
         LH    RE,SIGNRATE                                                      
         MR    R0,RE                                                            
         LHI   RF,5000             4 DECIMAL PLACES                             
         DR    R0,RF                                                            
         LTR   R1,R1               R1=QUOTIENT                                  
         JM    *+8                                                              
         AHI   R1,1                ROUND                                        
         SRA   R1,1                                                             
*                                                                               
         TM    STATUS,STCRDT                                                    
         BZ    SUBSIG65                                                         
         L     R0,SIGNCAP                                                       
         LNR   R0,R0                                                            
         CR    R1,R0                                                            
         JNL   SUBSIG70                                                         
         LR    R1,R0                                                            
         B     SUBSIG70                                                         
                                                                                
SUBSIG65 C     R1,SIGNCAP          IF LARGER THAN CAP                           
         JNH   *+8                                                              
         L     R1,SIGNCAP          RESET TO CAP INSTEAD                         
*                                                                               
SUBSIG70 ST    R1,PRTSIGNF         SAVE SIGNATORY FEE                           
SUBSIG80 ST    R1,TASIGN                                                        
         OC    TASIGN,TASIGN                                                    
         JZ    NO                                                               
*                                                                               
         MVC   P6TOT(L'LTSIGNF),LTSIGNF                                         
         LA    RF,P6MISC-1                                                      
         BAS   RE,EDIT11                                                        
*                                                                               
         TM    TBISTAT,TAPDSCAN    IF THIS IS A CANADIAN PAYMENT                
         JNO   SUBSIG90                                                         
         L     R1,TBCSTOT                                                       
         A     R1,TASIGN           ADD COMMISSION TOTAL                         
         ST    R1,TBCSTOT          TO CANADIAN $ SUBTOTAL                       
*                                                                               
SUBSIG90 L     R1,PRTSIGNF                                                      
         A     R1,TBTOT            ADD TO INVOICE TOTAL                         
         ST    R1,TBTOT                                                         
         J     YES                                                              
                                                                                
         DROP  R2                                                               
         LTORG                                                                  
         EJECT                                                                  
*=====================================================================          
*              PROCESS EVENT SUI SURCHARGE                                      
*=====================================================================          
*                                                                               
         USING TMD,R2                                                           
PRCEVSUI NMOD1 TMLNQ,*EVSU*                                                     
         LR    R2,RC               TEMP STORAGE FOR TALIM                       
         L     RC,0(R1)                                                         
         USING GEND,RC                                                          
                                                                                
         TM    EVNTSTAT,EVNTPSUI   PROCESSED SUI FOR EACH UNIT?                 
         JO    PESUIXIT            YES, LEAVE                                   
                                                                                
         L     RF,ATALIM           COPY TALIM BLOCK                             
         LA    RE,0(RF)            RE=A(DATA TO MOVE FROM)                      
         LR    R0,R2               R0=A(DATA TO MOVE TO)                        
         LHI   R1,TMLNQ            R1 AND RF = LENGTH TO MOVE                   
         LR    RF,R1                                                            
         MVCL  R0,RE               SAVE ENTRY CONTENTS IN SVTAB                 
                                                                                
         OC    TMACHK,TMACHK                                                    
         BZ    PESUIXIT                                                         
                                                                                
         MVC   SYSFIL,=CL8'CHKFIL'    GET CHECK RECORD                          
         MVC   SYSDIR,=CL8'CHKDIR'                                              
         MVC   TGFULL,AIO                                                       
         MVC   AIO,AIO3                                                         
                                                                                
         USING TLDRD,R3                                                         
         LA    R3,KEY                                                           
         MVC   TLDRDA,TMACHK          READ CHECK RECORD                         
         GOTO1 GETREC                                                           
         DROP  R3                                                               
                                                                                
         MVC   SYSFIL,=CL8'TALFIL'                                              
         MVC   SYSDIR,=CL8'TALDIR'                                              
         MVC   AIO,TGFULL                                                       
                                                                                
         USING TATUD,R4                                                         
         L     R4,AIO3                                                          
         MVI   ELCODE,TATUELQ                                                   
         BRAS  RE,GETEL                                                         
         B     PESUI15                                                          
PESUI10  BRAS  RE,NEXTEL                                                        
PESUI15  BNE   PESUIXIT                                                         
         CLC   TATUUNIT,=C'FD '    SKIP FEDERAL                                 
         BE    PESUI10                                                          
         CLC   TATUUNIT,=C'CN '    SKIP CANADA                                  
         BE    PESUI10                                                          
         CLC   TATUUNIT,=C'NYC'    SKIP CITIES EXCEPT NYC                       
         BE    PESUI20                                                          
         CLI   TATUUNIT+2,C' '                                                  
         BH    PESUI10                                                          
                                                                                
PESUI20  MVC   TMUNIT,TATUUNIT                                                  
         MVC   TMTXEARN,TATUWAGE                                                
                                                                                
         OI    TMSTAT2,TMEVTSUI    GOING TO CALC SUI SUR FOR EVENTS             
         GOTO1 AMYTRACE,DMCB,(RC),=C'EVSUI: TO TALIM BLOCK',TMD,TMLNQ           
         GOTO1 =V(TALIM),DMCB,TMD                                               
         GOTO1 AMYTRACE,DMCB,(RC),=C'EVSUI: FROM TALIM BLOCK',TMD,TMLNQ         
                                                                                
         GOTO1 =A(SUISUR),DMCB,TMD,SUISURCA     SURC FOR CA AND NY?             
         MVC   TMYTD(TMNYLN),TMNYTD         UPDATE YTD FOR NEXT UNIT            
                                                                                
         XC    TMRTBLK1(TMRTLNQ1+TMRTLNQ2+TMRTLNQ3),TMRTBLK1                    
         XC    TMRTBLK4(TMRTLNQ4),TMRTBLK4                                      
         XC    TMSUISUR,TMSUISUR                                                
         B     PESUI10                                                          
                                                                                
PESUIXIT NI    TMSTAT2,X'FF'-TMEVTSUI                                           
         OI    EVNTSTAT,EVNTPSUI   PROCESSED                                    
         XIT1                                                                   
         LTORG                                                                  
         DROP  R2                                                               
         EJECT                                                                  
*                                                                               
*        SET INTERFACE BLOCK TO T & H CALCULATIONS (TALIM)                      
*        R3 - CURRENT CHECK RECORD                                              
*                                                                               
         DS    0D                                                               
         USING TABCHKD,R3                                                       
SETLIMBK NMOD1 0,*SLIM*                                                         
         L     RC,0(R1)                                                         
         USING GEND,RC                                                          
         L     R4,ATALIM                                                        
         USING TMD,R4                                                           
*                                                                               
         ST    RC,TMRC                                                          
*                                                                               
         MVI   TMSTAT3,0                                                        
                                                                                
         LA    RE,SYSCOMM                                                       
         ST    RE,TMASYCOM         A(SYSTEM ROUTINES)                           
*                                                                               
         TM    TBOPTS,TBTRACE      REQUESTING TRACE                             
         BNO   *+8                                                              
         MVI   TMTRACE,C'Y'                                                     
**NO-OP  MVC   TMEFDTE0,TGTODAY0   SET EFFECTIVE DATE                           
         GOTO1 DATCON,DMCB,(1,TBEFFDTE),(0,TMEFDTE0)  EFFECTIVE DATE            
                                                                                
SB03     MVC   TMEMP,TGEMP         EMPLOYER                                     
         MVC   TMOFCDE,TBTPO       OFFICE CODE                                  
         MVC   TMUNIT,TABTAXST     TAXABLE STATE                                
         MVC   TMWUNIT,TABSTATE    WORK UNIT                                    
         OI    TMSTAT,TMSBIL       USE BILLING RATES                            
         LA    R2,NSURTAB          TABLE OF AGENCIES THAT DON'T                 
         USING NSURTABD,R2         GET SURCHARGE ADDED                          
*                                                                               
SB05     CLI   0(R2),X'FF'         END OF TABLE                                 
         BE    SB10                                                             
         CLC   TGAGY,NSURAGY       FOUND AN AGENCY                              
         BNE   SB06                                                             
         OI    TMSTAT,TMSNSUR      SET BIT - NO SURCHARGE                       
         B     SB10                                                             
*                                                                               
SB06     LA    R2,NSURLNQ(R2)      BUMP TO NEXT AGENCY                          
         B     SB05                                                             
*                                                                               
SB10     MVC   TMW4TYPE,TABW4TY    W4 TYPE                                      
         MVC   TMSSN,TABSSN        SS NUMBER OF PERFORMER                       
         OC    TABCID,TABCID       IF THERE IS A CORP                           
         BZ    *+10                                                             
         MVC   TMSSN,TABCID        USE THAT SS NUMBER                           
         MVC   TMCSEQ,TABSEQ       CAST SEQUENCE NUMBER                         
         MVC   TMAGY,TGAGY         AGENCY                                       
         MVC   TMCOM,TGCOM         INTERNAL COMMERCIAL NUMBER                   
         MVC   TMSTATUS,STATUS     STATUS                                       
         MVC   TMCURR,TGCUR        CURRENCY                                     
         MVC   TMBILTYP,GTYPE      SET GLOBAL BILLING RULES INFO                
         MVC   TMHNDRLS,GRHRULE    SET GLOBAL HANDLING RULES INFO               
         GOTO1 =A(SETRATES)        SET BILLING RATES                            
         MVI   TMSTAT2,0                                                        
*                                                                               
         TM    STAT2,STCORP        IF THERE IS A CORP ON THE INVOICE            
         BNO   *+8                                                              
         OI    TMSTAT2,TMSCORP     SET CORP ON INVOICE STATUS                   
*                                                                               
*        TM    TBISTA2,TAPDSPRI    FIXED CYCLE PMT ON GRT PRI COMML?            
*        BNZ   SB13                                                             
         CLI   TMHNDRLS,3          IF GRT APPLY RULE 3                          
         BE    SB12                                                             
         CLI   TMHNDRLS,4          OR 4, TAKE BOTH INDS AND CORPS               
         BE    SB12                                                             
         OC    TABCID,TABCID       ELSE, CORPS ONLY FOR HANDLING                
         BZ    SB13                                                             
SB12     OC    TABGRTCD,SPACES                                                  
         CLC   TABGRTCD,SPACES     IS THERE A GRT CODE?                         
         BNH   SB15                                                             
         GOTO1 =A(ISTYPE6)         SEE IF THIS IS A TYPE 6 GRT?                 
         BNE   SB15                                                             
SB13     MVI   TMHNDRLS,X'FF'      DISABLE ADDITIONAL GRT HANDLING              
*                                                                               
SB15     CLI   TMCURR,C'C'         IF CANADIAN                                  
         BE    SB16                                                             
         CLI   TMCURR,C'E'         OR EUROS ALL AMOUNTS GO INTO                 
         BNE   SB20                                                             
SB16     L     R1,TABPAYI          NON TAXABLE EARNINGS                         
         A     R1,TABPAYC                                                       
         A     R1,TABREXP                                                       
         ST    R1,TABMPAY                                                       
*2/10/06 CLI   TBPRTINV,C'Y'       IF NOT PRINT INVOICE                         
*2/10/06 BE    SB50                                                             
         CLI   TMCURR,C'E'                                                      
         BE    SB50                                                             
         BRAS  RE,PENTEST          AND IF NOT A PENALTY PAYMENT                 
         BE    SB50                                                             
         TM    TABSTAT2,TABSTGST   AND GST ON W4                                
         BZ    SB50                                                             
         BRAS  RE,TAKEGST          AND NOT AN AUTOMATIC                         
         BNE   SB50                                                             
         OI    TMSTAT2,TMSGSTU     SET TO CALCULATE GST                         
         B     SB50                                                             
*                                                                               
SB20     L     R1,TABPAYI          INDIV. PAYMENT                               
         CLI   TBMED,TACOMEDE      IF MEDIA = EVENT                             
         BNE   *+8                                                              
         A     R1,TABTXNW          ADD TAXABLE REIMBURSEMENTS                   
         ST    R1,TMTXEARN         TAXABLE EARNINGS                             
*NO-OP   MVC   TMTXEARN,TABPAYI    TAXABLE EARNINGS                             
*2/10/06 CLI   TBPRTINV,C'Y'       IF NOT PRINT INVOICE                         
*2/10/06 BE    SB30                                                             
         BRAS  RE,PENTEST          AND IF NOT A PENALTY PAYMENT                 
         BE    SB30                                                             
         TM    TABSTAT2,TABSTGST   AND GST ON W4                                
         BZ    SB30                                                             
*2/10/06 CLC   TABUNI,=C'ACT'      FOR ACTRA PAYMENTS ONLY                      
*2/10/06 BE    SB25                                                             
*2/10/06 CLC   TABUNI,=C'UDA'      OR UDA                                       
*2/10/06 BE    SB25                                                             
*2/10/06 CLC   TABUNI,=C'NON'      OR NON UNION AND                             
*2/10/06 BNE   SB30                                                             
*2/10/06 CLC   TABLOCL,=C'CAN'     CANADA LOCAL                                 
*2/10/06 BNE   SB30                                                             
SB25     BRAS  RE,TAKEGST          AND NOT AN AUTOMATIC                         
         BNE   SB30                                                             
         OI    TMSTAT2,TMSGSTU     SET TO CALCULATE GST PAID                    
*                                                                               
SB30     CLI   TMW4TYPE,TAW4TYIN   IF THIS IS AN INDIVIDUAL OR ESTATE           
         BE    *+12                                                             
         CLI   TMW4TYPE,TAW4TYES                                                
         BNE   *+14                                                             
         MVC   TABMPAY,TABREXP     MUST CALC HANDLING ON REIMBURSED             
         B     SB50                                                             
*                                                                               
**NO-OP  CLI   TBMED,TACOMEDE      IF MEDIA=E, TAKE WC IF CANADIAN WORK         
**FUTURE BE    SB45                                                             
         CLI   TMW4TYPE,TAW4TYCA   IF CANADIAN                                  
         BE    *+12                                                             
         CLI   TMW4TYPE,TAW4TYCO   OR CORPORATION                               
         BNE   SB45                                                             
         CLC   TABSTATE,=C'CN '    AND CANADIAN WORK                            
         BE    SB40                                                             
         MVC   TGCTRY,=C'CA'                                                    
         GOTO1 TAXVAL,DMCB,(X'FF',TABSTATE)                                     
         BE    SB40                                                             
SB35     CLC   TABCAT,=C'ZZZ'      OR CATEGORY IS ZZZ                           
         BNE   SB45                                                             
SB40     XC    TMBRWCRP,TMBRWCRP   DON'T CALCULATE WORKMEN'S COMP               
         BRAS  RE,UPDNWC           UPDATE NEW WC CORP RATE                      
*                                                                               
SB45     L     R1,TABREXP          ADD REIMBURSED EXP TO MISC PAYMENT           
         A     R1,TABPAYC                                                       
         ST    R1,TABMPAY          CALC TAXES/HANDLING ON ENTIRE AMT            
*                                                                               
SB50     CLI   TBMED,TACOMEDE      IF MEDIA = EVENT                             
         BNE   SB55                                                             
         L     R1,TABMPAY          MISC PAYMENTS                                
         S     R1,TABTXNW          SUBTRACT TAXABLE REIMBURSEMENTS.             
         ST    R1,TABMPAY          MISC PAYMENTS LESS TAXABLE REIMB.            
*&&DO                                                                           
**NO-OP                                                                         
**05/03                                                                         
         LTR   R1,R1               IF DIFFERENCE IS NEGATIVE,                   
         BP    SB55                                                             
         XC    TABMPAY,TABMPAY     SET MISC PYMTS TO 0 SO NOT NEGATIVE          
         L     RE,TABADVR                                                       
         LCR   RE,RE               COMPLEMENT ADV. REC                          
         AR    R1,RE               AND ADD TO MISC PAYMENTS                     
         LTR   R1,R1               IF AMOUNT IS POSITIVE,                       
         BNP   SB54                                                             
         LCR   R1,R1               COMPLEMENT THIS AMOUNT                       
         ST    R1,TABADVR          AND SAVE AS THE NEW ADV. REC.                
         B     SB55                                                             
SB54     XC    TABADVR,TABADVR     IF NEGATIVE, NO HANLDING ON ADV REC          
*&&                                                                             
*                                                                               
SB55     MVC   TMNTEARN,TABMPAY    NON TAXABLE EARNINGS                         
         MVC   TMPNH,TABPNH        P & H                                        
         MVC   TMAPPLG,TABGUAR     GUARANTEE CREDITS                            
         OC    TABINR,TABINR       IF THERE IS I & R                            
         BZ    *+10                                                             
         MVC   TMINR,TABINR        SET IT                                       
         MVC   TMHNW,TABHNW        H&W                                          
*        XC    TMREXP,TMREXP                                                    
*        CLI   TGUSEQU,UBSC                                                     
*        BNE   *+8                                                              
         L     R1,TABREXP                                                       
         CLI   TBMED,TACOMEDE      IF MEDIA = EVENT                             
         BNE   *+8                                                              
         S     R1,TABTXNW          SUBTRACT TAXABLE REIMBURSEMENTS              
         ST    R1,TMREXP                                                        
*NO-OP   MVC   TMREXP,TABREXP                                                   
         L     R1,TABMDED          MISC DEDUCTIONS                              
         A     R1,TABDUES          ADD IN DUES                                  
         ST    R1,TMMDED                                                        
         BAS   RE,GETYTD           GET YTD AMOUNTS                              
         TM    TBSTA2,TAINSHLR     IF THIS IS A RELEASED INVOICE                
         BNO   SB60                                                             
         TM    TBSTA2,TAINSHLP     THAT WAS ALREADY PRINTED                     
         BNO   SB60                                                             
         OI    TMSTAT,TMSPAMT      PASS TAXABLE AMOUNTS                         
         MVC   TMTAXBLE(TMTNAMT*4),TABXAMT  THAT WERE CALC'ED AT PRINT          
*                                                TIME                           
SB60     BAS   RE,CKOV1N2          CHECK OVERIDE FOR TYPE 1 & 2                 
         BNE   *+10                                                             
         MVC   TMOHAND,FULL        SET AMOUNT OF OVERIDE                        
*                                                                               
         CLI   TBMED,TACOMEDE      IF MEDIA = EVENT                             
         BNE   SB70                                                             
         TM    TGSYSTA2,TASYSROI                                                
         BO    *+8                                                              
         OI    TMSTAT2,TMOLDSUI    USE OLD SUI CALCULATION                      
         MVC   TMACHK,TABDA        SET A(CHECK RECORD)                          
         MVC   TMCNTR,TABCNTR      TAXABLE REIMB. FOR NON-IND                   
         XC    TMADVR,TMADVR       ADV. RECONC.                                 
         OC    TABADVR,TABADVR     IF NOT ZERO,                                 
         BZ    SB70                                                             
         L     RE,TABADVR                                                       
         LCR   RE,RE               TAKE COMPLEMENT                              
         ST    RE,TMADVR           ADVANCED RECONCILIATION                      
*                                                                               
SB70     TM    STATUS,STCRDT       CREDITED INVOICE?                            
         BNO   SBX                                                              
         BRAS  RE,GETCRD           IF BILLED DATE < JUL01/06,                   
         BNE   SB75                                                             
         OI    TMSTAT2,TMSGSTO     USE OLD GST RATE                             
*                                                                               
SB75     TM    TGSYSTAT,TASYSCRD   IF NEW CREDITING RULES ENABLED,              
         BZ    SBX                                                              
         TM    TBIPST2,TAPDPCIB    CREDITED INV HAS SAVED BILLING RATES         
         BZ    SBX                                                              
         OI    TMSTAT3,TMSCRBR     PASS IT TO TALIM                             
*                                                                               
SBX      B     SXIT                                                             
         DROP  R4                                                               
*                                                                               
TAKEGST  NTR1                                                                   
         LA    RE,NOGSTTAB                                                      
TGST10   CLI   0(RE),X'FF'                                                      
         BE    SYES                                                             
         CLC   TABSSN,0(RE)                                                     
         BE    SNO                                                              
         LA    RE,L'NOGSTTAB(RE)                                                
         B     TGST10                                                           
         SPACE 2                                                                
NOGSTTAB DS    0CL9                                                             
         DC    CL9'000000055'                                                   
         DC    CL9'000003755'                                                   
         DC    CL9'000003855'                                                   
         DC    CL9'000003876'                                                   
         DC    CL9'000007106'                                                   
         DC    CL9'000008213'                                                   
         DC    CL9'000000066'                                                   
*        DC    CL9'000002533'                                                   
         DC    X'FF'                                                            
         EJECT                                                                  
*                                                                               
*        CHECK BILLING TYPES 1 & 2 FOR HANDLING OVERRIDE                        
*        RETURN - FULL OVERIDE AMOUNT                                           
*                                                                               
CKOV1N2  NTR1                                                                   
         L     R2,ATALIM                                                        
         USING TMD,R2                                                           
         XC    FULL,FULL                                                        
         TM    SVOPT1,TAPDOHND     IF THERE'S AN OVERRIDE HANDLING FEE          
         BNO   SNO                                                              
         CLI   TMBILTYP,TABRTY1    & IF BILLING TYPE 1,2, 16 OR 20              
         BE    CKO10                                                            
         CLI   TMBILTYP,TABRTY20                                                
         BE    CKO10                                                            
         CLI   TMBILTYP,TABRTY2                                                 
         BNE   SNO                                                              
         CLI   TMBILTYP,TABRTY16                                                
         BNE   SNO                                                              
*                                                                               
CKO10    CLC   SVCKNUM,=H'1'       & IF ONLY 1 PERFORMER PAID                   
         BH    SNO                                                              
         OI    TMSTAT,TMSOHAND     SET OVERIDE HANDLING FEE BIT                 
         MVC   AIO,AIO1            GET TAPA ELEMENT                             
         MVI   ELCODE,TAPAELQ                                                   
         MVI   WORK,TAPATHND                                                    
         GOTO1 GETL,DMCB,(1,WORK)                                               
         BNE   CKO20                                                            
         L     R4,TGELEM                                                        
         USING TAPAD,R4                                                         
         MVC   FULL,TAPADATA       AND SET THE OVERIDE AMOUNT                   
         B     CKOX                                                             
         DROP  R4                                                               
*                                                                               
         USING TABDD,R4                                                         
CKO20    GOTOR GETBDEL,DMCB,AIO    IF OVERRIDE HANDLING IS IN TABD              
         BNE   CKOX                GET TABD ELEMENT                             
         MVC   FULL,TABDHND                                                     
         CLI   GTYPE,TABRTY15      IF BILLING TYPE 15 OR                        
         BE    CKO30                                                            
         CLI   GTYPE,TABRTY14      IF BILLING TYPE 14 OR                        
         BE    CKO30                                                            
         CLI   GTYPE,TABRTY10      IF BILLING TYPE 10                           
         BNE   CKOX                                                             
CKO30    MVC   FULL,TABDHNDC       OVERRIDE IS IN CORP HANDLING                 
*                                                                               
CKOX     B     SYES                                                             
         DROP  R2,R4                                                            
         EJECT                                                                  
*                                                                               
*        GET YTD                                                                
*              R3 - CURRENT CHECK ENTRY                                         
*                                                                               
         SPACE 1                                                                
         USING TABCHKD,R3                                                       
GETYTD   NTR1                                                                   
         L     R2,ATALIM           TALIM PARM BLOCK                             
         USING TMD,R2                                                           
         L     R4,ATAYTD           TAYTD TABLE                                  
         USING YTDD,R4                                                          
         L     R5,AYTDTAB          ACCUMULATIVE (RUN) YTD TABLE                 
         USING TYTDTABD,R5                                                      
         LHI   R6,YTDBLK-BILLD     TGTAYTD PARM BLOCK                           
         AR    R6,RA                                                            
         USING TYD,R6                                                           
         XC    TMYTD(TYBYLNQ),TMYTD                                             
         MVC   TGSSN,TABSSN                                                     
         OC    TABCID,TABCID       IF THERE IS A CORP                           
         BZ    *+10                                                             
         MVC   TGSSN,TABCID        USE IT                                       
*                                                                               
GET10    OC    0(TYTDLEN,R5),0(R5) SSN NOT IN TABLE YET                         
         BZ    GET30                                                            
         CLC   TYTDSSN,TGSSN       IF CORRECT SSN FOUND                         
         BNE   GET20                                                            
         CLC   TYTDCURR,TGCUR         & CORRECT CURRENCY                        
         BNE   GET20                                                            
         CLC   TYTDEMP,TGEMP          & CORRECT EMPLOYER                        
         BNE   GET20                                                            
         MVC   TMYTD(TYBYLNQ),TYTDAMTS  SET AMOUNTS                             
         B     GET60                                                            
*                                                                               
GET20    LA    R5,TYTDLEN(R5)      BUMP TO NEXT ENTRY IN TABLE                  
         C     R5,AYTDTABX                                                      
         BL    GET10               IF WE REACHED END OF TABLE                   
         DC    H'0'                THEN DIE                                     
*                                                                               
GET30    XC    TYD(TYLNQ),TYD      CLEAR TAYTD PARAMETER BLOCK                  
         MVC   TYASYSIO,TASYSIO    A(SYSIO)                                     
         ST    R4,TYATAB           A(YTD TABLE)                                 
         MVC   TYPEND,TGTODAY1                                                  
         MVC   TYEMP,TGEMP         EMPLOYER                                     
         MVC   TYSSN,TGSSN         SOCIAL SECURITY NUMBER *** CHECK CID         
         TM    TBOPTS,TBTRACE      TRACE OPTION                                 
         BNO   *+8                                                              
         MVI   TYTRACE,C'Y'                                                     
         MVC   TYCUR,TGCUR         SET CURRENCY                                 
         OI    TYSTAT,TYSTBILL     REQUEST BILLING YTD                          
*                                                                               
         BAS   RE,SETCHK                                                        
         GOTO1 TGTAYTD,DMCB,(RC),SYSCOMM,(R6)  BUILD YTD TABLE                  
         BAS   RE,SETTAL                                                        
         TM    TYRETURN,TYFNDPTR   WAS A POINTER FOUND                          
         BNO   *+10                                                             
         MVC   TMYTD(TYBYLNQ),TYBYTD   SET BILLING YTD AMOUNTS                  
         MVC   TYTDSSN,TGSSN       SET PERFORMER IN TABLE                       
         MVC   TYTDCURR,TGCUR                                                   
         MVC   TYTDEMP,TGEMP                                                    
         MVC   TYTDAMTS(TYBYLNQ),TMYTD                                          
*                                                                               
GET60    ST    R5,ATHISYTD         RETURN LOCATION OF SSN ENTRY                 
*                                                                               
GYX      B     SXIT                                                             
         EJECT                                                                  
*                                                                               
*        SET SYSFIL/DIR TO CHECK FILE                                           
*                                                                               
         SPACE 1                                                                
SETCHK   MVC   SYSFIL,=C'CHKFIL'                                                
         MVC   SYSDIR,=C'CHKDIR'                                                
         BR    RE                                                               
         SPACE 2                                                                
*                                                                               
*        SET SYSFIL/DIR TO TALENT FILE                                          
*                                                                               
         SPACE 1                                                                
SETTAL   MVC   SYSFIL,=C'TALFIL'                                                
         MVC   SYSDIR,=C'TALDIR'                                                
         BR    RE                                                               
         SPACE 3                                                                
*                                                                               
SYES     SR    RC,RC               SET CONDITION CODE                           
SNO      LTR   RC,RC                                                            
*                                                                               
SXIT     XIT1                                                                   
         SPACE 2                                                                
         GETEL2 R4,DATADISP,ELCODE                                              
         EJECT                                                                  
         SPACE 1                                                                
GSTAMTS  DC    F'700'                                                           
         SPACE 1                                                                
         LTORG                                                                  
         SPACE                                                                  
*                                                                               
*        TABLE AGENCIES THAT DONT HAVE SURCHARGE ADDED                          
*                                                                               
NSURTAB  DS    0CL6                                                             
* NO-OP  DC    CL6'0057  '                                                      
* NO-OP  DC    CL6'0257  '                                                      
         DC    X'FF'                                                            
         SPACE 2                                                                
         DROP  R2,R3,R4,R5,R6,RB                                                
         EJECT                                                                  
*                                                                               
*        TEST NEW CANADIAN PRINTING                                             
*                                                                               
TESTCAN  NTR1  BASE=*,LABEL=*                                                   
         TM    TBISTAT,TAPDSCAN    IF THIS IS A CANADIAN PAYMENT                
         JNO   NO                                                               
         TM    TBOPTS,TBRERUN      & THIS IS A RERUN                            
         JNO   YES                                                              
         TM    TBBDSTAT,TABDSCNW   & BILLED THE NEW WAY                         
         JNO   NO                                                               
         J     YES                 RETURN CC EQ - NEED 2 EXTRA LINES            
         LTORG                                                                  
         EJECT                                                                  
         SPACE 2                                                                
*======================================================================         
*              ROUTINE TO DETERMINE WORK CODE FOR SESSION WAGES & MISC.         
*======================================================================         
         SPACE                                                                  
         USING P6LINED,R2                                                       
SESSWC   NTR1  BASE=*,LABEL=*                                                   
         L     R3,ACHKTAB          START AT BEGINING OF CHECK TABLE             
         ST    R3,ANXTCHK                                                       
         USING TABCHKD,R3                                                       
*                                                                               
         LA    R1,ZETAB                                                         
         BAS   RE,FINDWC           FIND WORK CODE IN TABLE                      
         JNE   SWC70                                                            
         MVC   P6WCDE,=C'ZE '                                                   
         J     SWCX                                                             
*                                                                               
SWC70    CLC   TABONOFF,=C'ON '    IF ON CAMERA                                 
         JNE   SWC80                                                            
         MVC   P6WCDE,=C'ZD '                                                   
         J     SWCX                                                             
*                                                                               
SWC80    CLI   TGUSEQU,UBSM        IF BSM PAYMENT                               
         JE    SWC85                                                            
         CLI   TGUSEQU,UIMS        OR IMS PAYMENT                               
         JNE   SWC90                                                            
*                                                                               
SWC85    MVC   P6WCDE,=C'ZB '                                                   
         J     SWCX                                                             
*                                                                               
SWC90    LA    R1,ZCTAB                                                         
         BAS   RE,FINDWC           FIND WORK CODE IN TABLE                      
         JNE   SWC100                                                           
         MVC   P6WCDE,=C'ZC '                                                   
         J     SWCX                                                             
*                                                                               
SWC100   LA    R1,ZFTAB                                                         
         BAS   RE,FINDWC           FIND WORK CODE IN TABLE                      
         JNE   SWCX                                                             
         MVC   P6WCDE,=C'ZF '                                                   
SWCX     J     XIT                                                              
*                                                                               
FINDWC   NTR1                                                                   
FC10     CLI   0(R1),X'FF'                                                      
         JE    NO                                                               
         CLC   TABCAT,0(R1)        IS FIRST CAST MEMBER FOUND                   
         JE    YES                                                              
         LA    R1,L'TABCAT(R1)                                                  
         J     FC10                                                             
                                                                                
*                                                                               
*        TABLE OF CATEGORIES FOR WORK CODE ZE                                   
ZETAB    DS    0CL3                                                             
         DC    CL3'EX '                                                         
         DC    CL3'EXB'                                                         
         DC    CL3'HM '                                                         
         DC    CL3'HMB'                                                         
         DC    CL3'ZZZ'                                                         
         DC    CL3'ZZ '                                                         
         DC    X'FF'                                                            
*                                                                               
*        TABLE OF CATEGORIES FOR WORK CODE ZF                                   
ZFTAB    DS    0CL3                                                             
         DC    CL3'ACR'                                                         
         DC    CL3'ANR'                                                         
         DC    CL3'ANN'                                                         
         DC    CL3'P  '                                                         
         DC    CL3'CV '                                                         
         DC    X'FF'                                                            
*                                                                               
*        TABLE OF CATEGORIES FOR WORK CODE ZC                                   
ZCTAB    DS    0CL3                                                             
         DC    CL3'S  '                                                         
         DC    CL3'D  '                                                         
         DC    CL3'C3 '                                                         
         DC    CL3'C6 '                                                         
         DC    CL3'C9 '                                                         
         DC    CL3'G3 '                                                         
         DC    CL3'G6 '                                                         
         DC    CL3'G9 '                                                         
         DC    CL3'G3C'                                                         
         DC    CL3'G6C'                                                         
         DC    CL3'G9C'                                                         
         DC    CL3'G3M'                                                         
         DC    CL3'G6M'                                                         
         DC    CL3'G9M'                                                         
         DC    CL3'DC '                                                         
         DC    CL3'SFX'                                                         
         DC    X'FF'                                                            
                                                                                
         LTORG                                                                  
         EJECT                                                                  
*----------------------------------------------------------------------         
*        CHECK IF WISH TO PRINT WORK CODES ON INVOICE                           
*----------------------------------------------------------------------         
         USING P6LINED,R2                                                       
CHKIF    NTR1  BASE=*,LABEL=*                                                   
         CLI   RECNUM,BI           FOR BI & PB & AC ONLY                        
         BE    CI5                                                              
         CLI   RECNUM,PB                                                        
         BE    CI5                                                              
         CLI   RECNUM,AC                                                        
         BE    CI5                                                              
         CLI   RECNUM,EB                                                        
         BNE   CIX                                                              
*                                                                               
         USING IFTABD,R3                                                        
CI5      LA    R3,IFTAB            A(TABLE OF AGENCIES WHO WANT WC)             
*                                                                               
CI10     CLI   0(R3),X'FF'                                                      
         BE    CIX                                                              
         CLC   IFTAGY,TGAGY        IS AGENCY IN TABLE                           
         BE    CI20                                                             
         LA    R3,IFTNEXT          BUMP TO NEXT ENTRY                           
         B     CI10                                                             
*                                                                               
CI20     CLI   BYTE,C'T'           TAXES                                        
         BNE   CI30                                                             
         LA    R4,IFTTAX                                                        
         B     CI70                                                             
*                                                                               
CI30     CLI   BYTE,C'H'           HANDLING                                     
         BNE   CI40                                                             
         LA    R4,IFTHAND                                                       
         B     CI70                                                             
*                                                                               
CI40     CLI   BYTE,C'P'           P & H                                        
         BNE   CI50                                                             
         LA    R4,IFTPNH                                                        
         B     CI70                                                             
*                                                                               
CI50     CLI   BYTE,C'W'           WAGES & MISC                                 
         BNE   CIX                                                              
         TM    TGUSSTAT,SESSION                                                 
         BO    CI60                                                             
         LA    R4,IFTREUSE         REUSE                                        
         B     CI70                                                             
CI60     LA    R4,IFTSESS          SESSION                                      
*                                                                               
CI70     TM    0(R4),X'80'         TEST ENTRY IS WORK CODE                      
         BZ    CI80                                                             
         MVC   P6WCDE,0(R4)        SAVE IT                                      
         B     CIX                                                              
*                                                                               
CI80     XR    RF,RF               ELSE SET RF=DISP. TO ROUTINE                 
         ICM   RF,3,0(R4)                                                       
         AR    RF,RB               ADD START OF PROGRAM                         
         BASR  RE,RF                                                            
CIX      XIT1                                                                   
*                                                                               
*        TABLE OF AGENCIES AND WORK CODES THEY WANT ON INVOICES                 
IFTAB    DS    0CL21                                                            
         DC    CL6'0425  ',C'ZO ZT ZW ZX ',AL2(SESSWK-TABIPRT),X'00'            
         DC    CL6'0426  ',C'ZO ZT ZW ZX ',AL2(SESSWK-TABIPRT),X'00'            
         DC    CL6'0427  ',C'ZO ZT ZW ZX ',AL2(SESSWK-TABIPRT),X'00'            
         DC    CL6'1367  ',C'03 03 02 63 63 '                                   
         DC    CL6'0475  ',C'76 76 75 74 61 '                                   
         DC    CL6'1150  ',C'764764700696696'                                   
         DC    CL6'0050  ',C'764764700696696'                                   
         DC    X'FF'                                                            
*                                                                               
         LTORG                                                                  
         EJECT                                                                  
*----------------------------------------------------------------------         
*        SETUP FOR HST CALCULATIONS                                             
*----------------------------------------------------------------------         
GETPSTR  NTR1  BASE=*,LABEL=*                                                   
         L     R5,0(R1)            CANADIAN PROVINCE                            
         NI    TGPRVST,X'FF'-TGPSTHST-TGPSTQUE                                  
         XC    TGPSTRAT,TGPSTRAT         NO PST                                 
*                                                                               
         CLC   0(3,R5),=C'   '                                                  
         BNH   GETPX                                                            
*                                                                               
         MVC   AIO,AIO2            GET SYSTEM RECORD FOR CANADIAN PROV          
         GOTO1 RECVAL,DMCB,TLSYCDQ,(X'20',0)                                    
         BE    *+6                 RATE                                         
         DC    H'0'                                                             
                                                                                
         TM    TGSYSTAT,TASYSCRD   IF NEW CREDITING RULES ENABLED,              
         JZ    GETP30                                                           
         TM    TBIPST2,TAPDPCIB    AND IF CREDIT INVOICE HAS SAVED              
         JZ    GETP30              BILLING RATES, USE PUR/BILL DATE             
         BRAS  RE,GETSYS           GET SYSTEM REC FROM DATE OF ORIG INV         
*                                                                               
GETP30   MVC   AIO,AIO1                                                         
         L     R4,AIO2                                                          
         USING TAPCD,R4                                                         
         MVI   ELCODE,TAPCELQ         GET CANADIAN PROVINCE ELEMENT             
         BRAS  RE,GETEL                                                         
         BNE   GETPX                                                            
         ZIC   R1,TAPCNSUB                                                      
         LA    R3,TAPCPROV                                                      
E1       USING TAPCPROV,R3                                                      
GETP100  CLC   E1.TAPCPROV,0(R5)      MATCH ON PROVINCE                         
         BE    GETP200                                                          
         AHI   R3,TAPCSLNQ            BUMP TO NEXT PROVINCE                     
         BCT   R1,GETP100                                                       
         B     GETPX                  PROVINCE NOT IN TABLE                     
*                                                                               
GETP200  MVC   TGPRVST,E1.TAPCSTAT    SET HST INDICATOR                         
         TM    TGPRVST,TGPSTHST       FOR NOW, ONLY CALC PST WHEN HST           
         BZ    GETPX                                                            
         MVC   TGPSTRAT,E1.TAPCPSTR   SET PST RATE                              
         CLC   0(3,R5),=C'QC '        QUEBEC                                    
         BE    GETP210                                                          
         CLC   0(3,R5),=C'PE '        OR PRINCE EDWARD ISLAND                   
         BNE   GETPX                                                            
GETP210  OI    TGPRVST,TGPSTQUE       USE QUEBEC CALCULATION                    
*                                                                               
GETPX    XIT1                                                                   
         DROP  R4                                                               
*                                                                               
         LTORG                                                                  
         EJECT                                                                  
*----------------------------------------------------------------------         
*        CHECK IF CANADIAN I&R ACCOUNT CHECK                                    
*                R3 = CHECK TABLE                                               
*----------------------------------------------------------------------         
         USING TABCHKD,R3                                                       
CKCAN    NTR1  BASE=*,LABEL=*                                                   
         CLI   TBACTYPE,CCTY04A    IF THIS IS ACTRA TYPE 2404A                  
         BE    CCAN05                                                           
         CLI   TBACTYPE,CCTY2404   OR ACTRA TYPE 2404                           
         BE    CCAN05                                                           
*                                                                               
         TM    TBISTAT,TAPDSCAN    OR IF THIS IS A CANADIAN PAYMENT             
         JNO   NO                                                               
CCAN05   LA    R4,IRCAN            TABLE OF SSN'S FOR I&R CANADA                
*                                                                               
CCAN10   CLI   0(R4),X'FF'         END OF TABLE                                 
         JE    NO                                                               
         CLC   TABSSN,0(R4)        IF THIS IS I&R ACCOUNT                       
         JE    YES                                                              
         LA    R4,L'IRCAN(R4)      BUMP TABLE                                   
         B     CCAN10                                                           
*                                                                               
*        TABLE OF I&R SSN'S                                                     
IRCAN    DS    0CL9                                                             
         DC    CL9'000000055'                                                   
         DC    CL9'000003755'                                                   
         DC    CL9'000003855'                                                   
         DC    CL9'000003876'                                                   
         DC    CL9'000007106'                                                   
         DC    CL9'000008213'                                                   
         DC    CL9'000000066'                                                   
         DC    X'FF'                                                            
*                                                                               
         LTORG                                                                  
         EJECT                                                                  
*======================================================================         
*              BOTTOM LINE OF AOS BREAKOUT                                      
*======================================================================         
         USING P7LINED,R2          R2=A(PRINT LINE)                             
PRBAOSL  NTR1  BASE=*,LABEL=*                                                   
         OC    AOSAMT,AOSAMT       ONLY IF THERE WAS AOS CHARGED                
         BZ    PRBAOSLX                                                         
*                                                                               
         MVC   P7COD(L'LTAOS1A),LTAOS1A  PRINT OUT AOS LINE                     
         LA    R3,P7COD+L'LTAOS1A                                               
*                                                                               
         EDIT  AOSBASE,(8,0(R3)),2,ALIGN=LEFT,ZERO=NOBLANK                      
         AR    R3,R0                                                            
         AHI   R3,1                                                             
         MVC   0(L'LTAOS1B,R3),LTAOS1B  PRINT OUT AOS LINE                      
         AHI   R3,L'LTAOS1B                                                     
         EDIT  AOSAMT,(8,0(R3)),2,ALIGN=LEFT,ZERO=NOBLANK                       
         AR    R3,R0                                                            
         AHI   R3,1                                                             
         MVC   0(L'LTAOS1C,R3),LTAOS1C  PRINT OUT AOS LINE                      
         BRAS  RE,PRTIT                                                         
*                                                                               
         MVC   P7COD(L'LTAOS2A),LTAOS2A  PRINT OUT AOS LINE                     
         LA    R3,P7COD+L'LTAOS2A                                               
         L     RF,TBCGST                                                        
         A     RF,TBCPST                                                        
         CLI   TGCUR,C'C'          IF CANADIAN CURRENCY                         
         BE    PRBAOSL5            YES, CONTINUE                                
         A     RF,TBGSTU           NO, ADD IN GST FOR US                        
         A     RF,TBPSTU           NO, ADD IN GST FOR US                        
PRBAOSL5 EDIT  (RF),(8,0(R3)),2,ALIGN=LEFT,ZERO=NOBLANK                         
         AR    R3,R0                                                            
         AHI   R3,1                                                             
         OC    AOSPST,AOSPST       ANY PST?                                     
         BZ    PRBAOSL8            NO, JUST SO GST                              
         MVC   0(L'LTAOS2B,R3),LTAOS2B  PRINT OUT AOS LINE                      
         AHI   R3,L'LTAOS2B                                                     
         EDIT  AOSAMT,(8,0(R3)),2,ALIGN=LEFT,ZERO=NOBLANK                       
         AR    R3,R0                                                            
         AHI   R3,1                                                             
         MVC   0(L'LTAOS2C,R3),LTAOS2C  PRINT OUT AOS LINE                      
         AHI   R3,L'LTAOS2C                                                     
         EDIT  AOSPST,(8,0(R3)),2,ALIGN=LEFT,ZERO=NOBLANK                       
         BRAS  RE,PRTIT                                                         
*                                                                               
         MVC   P7COD(L'LTAOS3),LTAOS3    PRINT OUT AOS LINE                     
         LA    R3,P7COD+L'LTAOS3                                                
         L     R4,TBCGST                                                        
         A     R4,TBCPST                                                        
         CLI   TGCUR,C'C'          IF CANADIAN CURRENCY                         
         BE    PRBAOSL6            YES, CONTINUE                                
         A     R4,TBGSTU                                                        
         A     R4,TBPSTU                                                        
PRBAOSL6 ICM   RF,15,AOSPST                                                     
         AR    R4,RF                                                            
PRBAOSL7 EDIT  (R4),(8,0(R3)),2,ALIGN=LEFT,ZERO=NOBLANK                         
PRBAOSL8 BRAS  RE,PRTIT                                                         
PRBAOSLX XIT1                                                                   
         EJECT                                                                  
LTAOS1A  DC    CL47'This invoice includes 1% AOS on Misc Wages of $'            
LTAOS1B  DC    CL3'= $'                                                         
LTAOS1C  DC    CL18'Check to ACTRA FBS'                                         
LTAOS2A  DC    CL31'GST/PST (HST) includes HST of $'                            
LTAOS2B  DC    CL17'plus PST/QST on $'                                          
LTAOS2C  DC    CL7'AOS = $'                                                     
LTAOS3   DC    CL7'Total $'                                                     
         LTORG                                                                  
*                                                                               
         DROP  R2                                                               
         EJECT                                                                  
*                                                                               
*        DETAILS FOR PRINT INVOICES                                             
*                                                                               
         USING P4LINED,R2                                                       
         USING TABCHKD,R3                                                       
PRPRINT  NTR1  BASE=*,LABEL=*                                                   
         MVC   PAYEE+1(1),TABPAYEE                                              
         EDIT  TABARATE,(5,AGTPCT),2                                            
         OC    TABSDATE,TABSDATE                                                
         BZ    PRP10                                                            
         GOTO1 DATCON,DMCB,(1,TABSDATE),(5,SHDTE) SHOOT DATE                    
*                                                                               
PRP10    OC    TABPUBDT,TABPUBDT   PUB DATE                                     
         BZ    PRP20                                                            
         GOTO1 DATCON,DMCB,(1,TABPUBDT),(5,PUBDTE)                              
*                                                                               
PRP20    OC    TABEXPDT,TABEXPDT   EXPIRATION DATE                              
         BZ    PRPX                                                             
         GOTO1 DATCON,DMCB,(1,TABEXPDT),(5,EXPDTE)                              
*                                                                               
PRPX     XIT1                                                                   
         DROP  R2,R3                                                            
         LTORG                                                                  
         EJECT                                                                  
         SPACE 2                                                                
*                                                                               
*        PRINTING OF TAX LEVEL                                                  
         USING P4LINED,R2                                                       
         USING TABCHKD,R3                                                       
SETOVRD  NTR1  BASE=*,LABEL=*                                                   
         CLI   TBMED,TACOMEDE      IF MEDIA = E, SKIP                           
         BE    SETOVRDX                                                         
         CLI   GTYPE,TABRTY6       IF THIS IS TYPE 1,2,9,10,12 OR 13            
         BE    SETOVRDX                                                         
         CLI   GTYPE,TABRTY7                                                    
         BE    SETOVRDX                                                         
         CLI   GTYPE,TABRTY8                                                    
         BE    SETOVRDX                                                         
         CLI   GTYPE,TABRTY21                                                   
         BE    SETOVRDX                                                         
         CLI   GTYPE,TABRTY11                                                   
         BE    SETOVRDX                                                         
         CLI   GTYPE,TABRTY23                                                   
         BE    SETOVRDX                                                         
         CLI   GTYPE,TABRTY18                                                   
         BE    SETOVRDX            SET TAX LEVEL                                
         CLI   TBPRTINV,C'Y'       FOR PRINT INVOICES                           
         BNE   SETOVRD5                                                         
         MVC   POVRD+1(1),TABLEVL  PUT TAX LEVEL AT DIFF SPOT                   
         BE    SETOVRDX                                                         
*                                                                               
SETOVRD5 MVC   OVRIDE+1(1),TABLEVL                                              
SETOVRDX XIT1                                                                   
         DROP  R2,R3                                                            
         LTORG                                                                  
         EJECT                                                                  
         SPACE 2                                                                
*                                                                               
*        PUT P&H AMOUNT IN TABLE TO POST LATER                                  
*                                                                               
         USING TABCHKD,R3                                                       
PUTBNP   NTR1  BASE=*,LABEL=*                                                   
         L     R2,=V(BNPTAB)       FIND ENTRY IN BNP TABLE                      
         USING BNPD,R2                                                          
         CLC   TGEMP,=C'DM '       IF THIS EMP = DM                             
         BNE   PUTB10                                                           
         TM    TBISTAT,TAPDSCAN    AND THIS IS NOT A $CAN INVOICE               
         BNO   PUTBX               DON'T POST PNH FOR BNP                       
*                                                                               
PUTB10   C     R2,=V(BNPTABX)      REACHED END OF TABLE                         
         BL    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         OC    BNPUNI(L'BNPUNI+L'BNPLCL),BNPUNI                                 
         BZ    PUTB20              SET NEW ENTRY                                
         CLC   TABUNI(L'TABUNI+L'TABLOCL),BNPUNI                                
         BE    PUTB30              FOUND CORRECT ENTRY                          
         LA    R2,BNPLEN(R2)       BUMP TO NEXT ENTRY                           
         B     PUTB10                                                           
*                                                                               
PUTB20   MVC   BNPUNI(L'TABUNI+L'TABLOCL),TABUNI                                
*                                                                               
PUTB30   L     R4,BNPAMT                                                        
*=====================================================================          
*        TM    TBOPTS2,TBFIXINV    IF FIXING INVOICES                           
*        BO    PUTB35              IGNORE RERUN                                 
*        TM    TBOPTS,TBRERUN      IF RERUN                                     
*        BNO   PUTB40                                                           
PUTB35   TM    TBISTAT,TAPDSCAN    & IF THIS IS A CANADIAN PAYMENT              
         BNO   PUTB40                                                           
         ZAP   DUB,TBGCVT          SET CONV RATE INTO FULL (BINARY)             
         CVB   R1,DUB                                                           
         ST    R1,FULL                                                          
         LR    R0,R4               (SAVE R4)                                    
         L     R4,TABPNH                PNH CAN$                                
         GOTO1 MULT,DMCB,(R4),FULL                                              
         L     R1,0(R1)                 = US$ PNH                               
         LR    RF,R1                    = US$ PNH                               
         LR    R4,R0               (RESTORE R4)                                 
         AR    R4,R1                                                            
         B     PUTB50                                                           
*=====================================================================          
PUTB40   L     RF,TABPNH                                                        
         A     R4,TABPNH           ADD PNH TO TOTAL PNH FOR BNP                 
*                                                                               
PUTB50   ST    R4,BNPAMT                                                        
                                                                                
         L     RE,TBBNPPNH                                                      
         AR    RE,RF                                                            
         ST    RE,TBBNPPNH                                                      
*                                                                               
PUTBX    XIT1                                                                   
         DROP  R2,R3                                                            
         LTORG                                                                  
         EJECT                                                                  
*=====================================================================          
*        GET OVERRIDE HANDLING FROM TAPA ELEMENT                                
*        IF THERE IS NO TAPA - TRY TABD (OLD WAY)                               
*        INPUT  - WORK = DATA TYPE                                              
*        RETURN - AMOUNT IN FULL                                                
*=====================================================================          
GETPAAMT NTR1  BASE=*,LABEL=*                                                   
         XC    FULL,FULL           CLEAR RETURN VALUE                           
         MVI   ELCODE,TAPAELQ      GET TAPA ELEMENT                             
         GOTO1 GETL,DMCB,(1,WORK)                                               
         BNE   GPA10                                                            
         USING TAPAD,R4                                                         
         L     R4,TGELEM                                                        
         MVC   FULL,TAPADATA       AND SET THE OVERIDE AMOUNT                   
         B     GPAX                                                             
*                                                                               
         USING TABDD,R4                                                         
GPA10    GOTOR GETBDEL,DMCB,AIO    GET TABD ELEMENT                             
         BNE   GPAX                                                             
         CLI   WORK,TAPATHND       REQUESTING HANDLING                          
         BNE   GPA20                                                            
         MVC   FULL,TABDHND                                                     
         CLI   GTYPE,TABRTY15      IF BILLING TYPE 15 OR                        
         BE    GPA15                                                            
         CLI   GTYPE,TABRTY14      IF BILLING TYPE 14 OR                        
         BE    GPA15                                                            
         CLI   GTYPE,TABRTY10      IF BILLING TYPE 10                           
         BNE   GPAX                                                             
GPA15    MVC   FULL,TABDHNDC       OVERRIDE IS IN CORP HANDLING                 
*                                                                               
GPA20    CLI   WORK,TAPATTAX       REQUESTING TAX                               
         BNE   *+10                                                             
         MVC   FULL,TABDTAX                                                     
*                                                                               
         CLI   WORK,TAPATFIC       REQUESTING FICA                              
         BNE   *+10                                                             
         MVC   FULL,TABDFICR                                                    
*                                                                               
         CLI   WORK,TAPATGST       REQUESTING GST                               
         BNE   *+10                                                             
         MVC   FULL,TABDGST                                                     
                                                                                
GPAX     XIT1                                                                   
         DROP  R4                                                               
         EJECT                                                                  
*=====================================================================          
*        IF CALCULATING WC CORP THE NEW WAY,                                    
*        CLEAR WC CORP RATE                                                     
*=====================================================================          
UPDNWC   NTR1  BASE=*,LABEL=*                                                   
         USING TMD,R4                                                           
         L     R4,ATALIM                                                        
         USING TARAD,RE                                                         
         L     RE,TMACURRA                                                      
         LTR   RE,RE                                                            
         JZ    XIT                                                              
         TM    TARASTA2,TARASWCD   IS WC IN DOLLARS?                            
         BZ    UPNWC10                                                          
         ICM   R2,15,TARATWC                                                    
         C     R2,=XL4'FFFFFFFF'   IS RATE = ZERO?                              
         JNE   XIT                                                              
**NO-OP  XC    TARATWC,TARATWC                                                  
UPNWC10  OI    TMSTAT3,TMSNOWC     DON'T CALCULATE WC                           
         J     XIT                                                              
         LTORG                                                                  
         DROP  R4,RE                                                            
         EJECT                                                                  
*=====================================================================          
*        CHECK IF CALCULATING CAN HANDLING OLD WAY OR NEW WAY.                  
*        RETURN CC NEQ IF CAN HANDLING RATE = 0                                 
*=====================================================================          
CKCNHND  NTR1  BASE=*,LABEL=*                                                   
         USING TMD,R4                                                           
         L     R4,ATALIM                                                        
         USING TARAD,RE                                                         
         L     RE,TMACURRA         GET TARA ELEMENT                             
         LTR   RE,RE               CHECK FOR NEW RATES                          
         JNZ   CKCNH10                                                          
         CLI   RECNUM,CC           CHLOE COPY                                   
         BNE   CKCNH05                                                          
         OC    TBHANDC,TBHANDC     PRINT IF CAN$ HANDLING                       
         JNZ   YES                                                              
CKCNH05  OC    GRCAN,GRCAN         IF NO HANDLING DON'T PRINT                   
         JZ    NO                                                               
         J     YES                                                              
                                                                                
CKCNH10  CLI   GTYPE,TABRTY21      IF BILLING TYPES 21,                         
         BE    CKCNH20                                                          
         CLI   GTYPE,TABRTY23      23,                                          
         BE    CKCNH20                                                          
         CLI   GTYPE,TABRTY24      OR 24                                        
         BNE   CKCNH30                                                          
CKCNH20  TM    TGUSSTAT,SESSION    CHECK SESSION VS. REUSE RATES                
         BO    CKCNH30                                                          
         OC    TARARFBC,TARARFBC   REUSE BASIC                                  
         JZ    CKCNH25                                                          
         CLC   TARARFBC,=X'FFFFFFFF' MARKED AS ZEROS?                           
         JNE   YES                                                              
CKCNH25  OC    TARARFP,TARARFP     REUSE PREMIUM                                
         JZ    NO                                                               
         CLC   TARARFP,=X'FFFFFFFF'  MARKED AS ZEROS?                           
         JE    NO                                                               
         J     YES                                                              
                                                                                
CKCNH30  OC    TARAHFBC,TARAHFBC   SESSION BASIC                                
         JZ    CKCNH35                                                          
         CLC   TARAHFBC,=X'FFFFFFFF' MARKED AS ZEROS?                           
         JNE   YES                                                              
CKCNH35  OC    TARAHFP,TARAHFP     SESSION PREMIUM                              
         JZ    NO                                                               
         CLC   TARAHFP,=X'FFFFFFFF'  MARKED AS ZEROS?                           
         JE    NO                                                               
         J     YES                                                              
                                                                                
         DROP  R4,RE                                                            
         LTORG                                                                  
         EJECT                                                                  
*=====================================================================          
*        IF CALCULATING HANDLING THE NEW WAY,                                   
*        STORE INDIVIDUAL HANDLING OVERRIDE IN NEW POSTING ELEMENT              
*        RETURN - FULL = HANDLING OVERRIDE                                      
*=====================================================================          
SETHANDI NTR1  BASE=*,LABEL=*                                                   
         XC    FULL,FULL                                                        
         USING TMD,R3                                                           
         L     R3,ATALIM                                                        
         USING TAPGD,RE                                                         
         L     RE,TMACURPG         GET TAPG ELEMENT                             
         LTR   RE,RE               IF NEW POSTING ELEMENT EXISTS,               
         JZ    XIT                                                              
         TM    BOVRSTAT,BOVRHNDB+BOVRHNDP  BASIC OR PREM HAND OVERRIDE?         
         JZ    XIT                                                              
         TM    TBISTAT,TAPDSCAN    IF THIS IS A CANADIAN PAYMENT,               
         JO    XIT                 IGNORE IND OVERRIDES                         
                                                                                
         USING TARAD,R4                                                         
         L     R4,TMACURRA         GET TARA ELEMENT                             
         LTR   R4,R4                                                            
         JZ    XIT                                                              
                                                                                
         TM    BOVRSTAT,BOVRHNDB   BASIC HAND OVERRIDE?                         
         BZ    SHDI10                                                           
         ICM   R2,15,TARAHFB       BASIC HANDLING IN DOLLARS                    
         C     R2,=XL4'FFFFFFFF'   IS RATE = ZERO?                              
         BNE   *+6                                                              
         XR    R2,R2                                                            
         ST    R2,TAPGHNDI         IND HANDLING OVERRIDE                        
                                                                                
         L     R1,TBHANDI          ADD BASIC HANDLING TO TOTAL                  
         AR    R1,R2                                                            
         ST    R1,TBHANDI                                                       
         ST    R2,FULL             SAVE OVERRIDE FOR ADDING TO TOTAL            
                                                                                
SHDI10   TM    BOVRSTAT,BOVRHNDP   PREM HAND OVERRIDE?                          
         JZ    XIT                                                              
         ICM   R2,15,TARAHFP       PREM HANDLING IN DOLLARS                     
         C     R2,=XL4'FFFFFFFF'   IS RATE = ZERO?                              
         BNE   *+6                                                              
         XR    R2,R2                                                            
         ST    R2,TAPGHPRI         IND PREM HAND OVERRRIDE                      
                                                                                
         L     R1,TBHANDI          ADD BASIC HANDLING TO TOTAL                  
         AR    R1,R2                                                            
         ST    R1,TBHANDI                                                       
         L     R1,FULL             SAVE OVERRIDE FOR ADDING TO TOTAL            
         AR    R1,R2                                                            
         ST    R1,FULL                                                          
                                                                                
         J     XIT                                                              
         DROP  R3,R4,RE                                                         
         LTORG                                                                  
         EJECT                                                                  
*=====================================================================          
*        IF CALCULATING HANDLING THE NEW WAY,                                   
*        STORE CORP HANDLING OVERRIDE IN NEW POSTING ELEMENT                    
*        RETURN - FULL = HANDLING OVERRIDE                                      
*=====================================================================          
SETHANDC NTR1  BASE=*,LABEL=*                                                   
         XC    FULL,FULL                                                        
         USING TMD,R3                                                           
         L     R3,ATALIM                                                        
         USING TAPGD,RE                                                         
         L     RE,TMACURPG         GET TAPG ELEMENT                             
         LTR   RE,RE               IF NEW POSTING ELEMENT EXISTS,               
         JZ    XIT                                                              
         TM    BOVRSTAT,BOVRHNDB+BOVRHNDP  BASIC OR PREM HAND OVERRIDE?         
         JZ    XIT                                                              
         TM    TBISTAT,TAPDSCAN    IF THIS IS A CANADIAN PAYMENT,               
         JO    XIT                 IGNORE CORP OVERRIDES                        
                                                                                
         USING TARAD,R4                                                         
         L     R4,TMACURRA         GET TARA ELEMENT                             
         LTR   R4,R4                                                            
         JZ    XIT                                                              
                                                                                
         TM    BOVRSTAT,BOVRHNDB   BASIC HAND OVERRIDE?                         
         BZ    SHDC10                                                           
         ICM   R2,15,TARAHFB       BASIC HANDLING IN DOLLARS                    
         C     R2,=XL4'FFFFFFFF'   IS RATE = ZERO?                              
         BNE   *+6                                                              
         XR    R2,R2                                                            
         ST    R2,TAPGHNDC         CORP HANDLING OVERRIDE                       
                                                                                
         L     R1,TBHANDC          ADD BASIC HANDLING TO TOTAL                  
         AR    R1,R2                                                            
         ST    R1,TBHANDC                                                       
         ST    R2,FULL             SAVE OVERRIDE FOR ADDING TO TOTAL            
                                                                                
SHDC10   TM    BOVRSTAT,BOVRHNDP   PREM HAND OVERRIDE?                          
         JZ    XIT                                                              
         ICM   R2,15,TARAHFP       PREM HANDLING IN DOLLARS                     
         C     R2,=XL4'FFFFFFFF'   IS RATE = ZERO?                              
         BNE   *+6                                                              
         XR    R2,R2                                                            
         ST    R2,TAPGHPRC         CORP PREM HAND OVERRIDE                      
                                                                                
         L     R1,TBHANDC          ADD PREM HANDLING TO TOTAL                   
         AR    R1,R2                                                            
         ST    R1,TBHANDC                                                       
         L     R1,FULL             SAVE OVERRIDE FOR ADDING TO TOTAL            
         AR    R1,R2                                                            
         ST    R1,FULL                                                          
                                                                                
         J     XIT                                                              
         DROP  R3,R4,RE                                                         
                                                                                
         LTORG                                                                  
         EJECT                                                                  
*=====================================================================          
*        IF CALCULATING HANDLING THE NEW WAY,                                   
*        STORE CANADIAN HANDLING OVERRIDE IN NEW POSTING ELEMENT                
*        RETURN - FULL = HANDLING OVERRIDE                                      
*=====================================================================          
SETHANDA NTR1  BASE=*,LABEL=*                                                   
         USING TMD,R3                                                           
         XC    FULL,FULL                                                        
         L     R3,ATALIM                                                        
         USING TAPGD,RE                                                         
         L     RE,TMACURPG         GET TAPG ELEMENT                             
         LTR   RE,RE               IF NEW POSTING ELEMENT EXISTS,               
         JZ    XIT                                                              
         TM    BOVRSTAT,BOVRHNDC+BOVRHNDP  CAN OR PREM HAND OVERRIDE?           
         JZ    XIT                                                              
         TM    TBISTAT,TAPDSCAN    THIS MUST BE A CANADIAN PAYMENT              
         JZ    XIT                                                              
                                                                                
         USING TARAD,R4                                                         
         L     R4,TMACURRA         GET TARA ELEMENT                             
         LTR   R4,R4                                                            
         JZ    XIT                                                              
                                                                                
         TM    BOVRSTAT,BOVRHNDC   CAN HAND OVERRIDE?                           
         BZ    SHDA10                                                           
         ICM   R2,15,TARAHFBC      BASIC CAN HANDLING IN DOLLARS                
         C     R2,=XL4'FFFFFFFF'   IS RATE = ZERO?                              
         BNE   *+6                                                              
         XR    R2,R2                                                            
         ST    R2,TAPGHNDC         CORP HANDLING OVERRIDE                       
                                                                                
         L     R1,TBHANDC          ADD BASIC CAN HANDLING TO TOTAL              
         AR    R1,R2                                                            
         ST    R1,TBHANDC                                                       
         ST    R2,FULL             SAVE OVERRIDE                                
                                                                                
SHDA10   TM    BOVRSTAT,BOVRHNDP   PREM HAND OVERRIDE?                          
         JZ    XIT                                                              
         ICM   R2,15,TARAHFP       PREM HANDLING IN DOLLARS                     
         C     R2,=XL4'FFFFFFFF'   IS RATE = ZERO?                              
         BNE   *+6                                                              
         XR    R2,R2                                                            
         ST    R2,TAPGHPRC         IND PREM HAND OVERRIDE                       
                                                                                
         L     R1,TBHANDC          ADD PREM CAN HANDLING TO TOTAL               
         AR    R1,R2                                                            
         ST    R1,TBHANDC                                                       
                                                                                
         L     R1,FULL             SAVE OVERRIDE                                
         AR    R1,R2                                                            
         ST    R1,FULL                                                          
                                                                                
         J     XIT                                                              
         DROP  R3,R4,RE                                                         
         LTORG                                                                  
         EJECT                                                                  
*=====================================================================          
*        IF CALCULATING PAYROLL TAX THE NEW WAY,                                
*        STORE PAYROLL TAX OVERRIDE IN NEW POSTING ELEMENT                      
*        RETURN - FULL = PAYROLL TAX OVERRIDE                                   
*=====================================================================          
SETTAX   NTR1  BASE=*,LABEL=*                                                   
         XC    FULL,FULL                                                        
         USING TMD,R3                                                           
         L     R3,ATALIM                                                        
         USING TAPGD,RE                                                         
         L     RE,TMACURPG         GET TAPG ELEMENT                             
         LTR   RE,RE               IF NEW POSTING ELEMENT EXISTS,               
         JZ    XIT                                                              
         TM    TBISTAT,TAPDSCAN    IF THIS IS A CANADIAN PAYMENT,               
         JO    XIT                 IGNORE TAX OVERRIDES                         
                                                                                
         USING TARAD,R4                                                         
         L     R4,TMACURRA         GET TARA ELEMENT                             
         LTR   R4,R4                                                            
         JZ    XIT                                                              
                                                                                
         TM    TARASTA2,TARASFUD   FUTA IN DOLLARS?                             
         BZ    STTAX10                                                          
         ICM   R2,15,TARATFU                                                    
         C     R2,=XL4'FFFFFFFF'   IS RATE = ZERO?                              
         BNE   *+6                                                              
         XR    R2,R2                                                            
         ST    R2,TAPGFUTA         FUTA                                         
                                                                                
         L     R1,TBTAX            ADD FUTA TO TAX TOTAL                        
         AR    R1,R2                                                            
         ST    R1,TBTAX                                                         
         ST    R2,FULL             ADD UP TOTAL OF OVERRIDES                    
                                                                                
STTAX10  TM    TARASTA2,TARASSUD   SUTA IN DOLLARS?                             
         BZ    STTAX20                                                          
         ICM   R2,15,TARATSU                                                    
         C     R2,=XL4'FFFFFFFF'   IS RATE = ZERO?                              
         BNE   *+6                                                              
         XR    R2,R2                                                            
         ST    R2,TAPGSUTA         SUTA                                         
                                                                                
         L     R1,TBTAX            ADD SUTA TO TAX TOTAL                        
         AR    R1,R2                                                            
         ST    R1,TBTAX                                                         
                                                                                
         L     R1,FULL             ADD SUTA TO TAX TOTAL                        
         AR    R1,R2                                                            
         ST    R1,FULL                                                          
                                                                                
STTAX20  TM    TARASTA2,TARASFID   FICA IN DOLLARS?                             
         BZ    STTAX30                                                          
         ICM   R2,15,TARATFI                                                    
         C     R2,=XL4'FFFFFFFF'   IS RATE = ZERO?                              
         BNE   *+6                                                              
         XR    R2,R2                                                            
         ST    R2,TAPGFICA         FICA                                         
                                                                                
         L     R1,TBTAX            ADD FICA TO TAX TOTAL                        
         AR    R1,R2                                                            
         ST    R1,TBTAX                                                         
                                                                                
         L     R1,FULL             ADD FICA TO TAX TOTAL                        
         AR    R1,R2                                                            
         ST    R1,FULL                                                          
                                                                                
STTAX30  TM    TARASTA2,TARASMED   MEDICARE IN DOLLARS?                         
         BZ    STTAX40                                                          
         ICM   R2,15,TARATM                                                     
         C     R2,=XL4'FFFFFFFF'   IS RATE = ZERO?                              
         BNE   *+6                                                              
         XR    R2,R2                                                            
         ST    R2,TAPGMED          MEDICARE                                     
                                                                                
         L     R1,TBTAX            ADD MEDICARE TO TAX TOTAL                    
         AR    R1,R2                                                            
         ST    R1,TBTAX                                                         
                                                                                
         L     R1,FULL             ADD MEDICARE TO TAX TOTAL                    
         AR    R1,R2                                                            
         ST    R1,FULL                                                          
                                                                                
STTAX40  TM    TARASTA2,TARASWCD   WC IN DOLLARS?                               
         BZ    STTAXX                                                           
         XC    TAPGWCN,TAPGWCN     CLEAR WC INDIVIDUAL                          
         ICM   R2,15,TARATWC                                                    
         C     R2,=XL4'FFFFFFFF'   IS RATE = ZERO?                              
         BNE   *+6                                                              
         XR    R2,R2                                                            
         ST    R2,TAPGWCC          WC CORP                                      
                                                                                
         L     R1,TBTAX            ADD WC TO TAX TOTAL                          
         AR    R1,R2                                                            
         ST    R1,TBTAX                                                         
                                                                                
         L     R1,FULL             ADD WC TO TAX TOTAL                          
         AR    R1,R2                                                            
         ST    R1,FULL                                                          
                                                                                
STTAXX   J     XIT                                                              
         DROP  R3,R4,RE                                                         
         LTORG                                                                  
         EJECT                                                                  
*=====================================================================          
*        UPDATES IND OR CORP HANDLING, IF BILLING TYPES 6, 8, 21, 26            
*        AND THERE IS A PAYROLL TAX OVERRIDE                                    
*        HANDLING INCLUDES PAYROLL TAX IN CALCULATION FOR THESE TYPES           
*        UPDATES TAPG ELEMENT                                                   
*        ON ENTRY - P1 = PAYROLL TAX OVERRIDE AMOUNT                            
*        RETURN - FULL = EXTRA PAYROLL TAX TO ADD TO HANDLING                   
*=====================================================================          
UPDHANDI NTR1  BASE=*,LABEL=*                                                   
         XC    FULL,FULL                                                        
         L     R5,0(R1)            PAYROLL TAX OVERRIDE AMOUNT                  
                                                                                
         USING TMD,R3                                                           
         L     R3,ATALIM                                                        
         USING TAPGD,RE                                                         
         L     RE,TMACURPG         GET TAPG ELEMENT                             
         LTR   RE,RE               IF NEW POSTING ELEMENT EXISTS,               
         JZ    NO                                                               
         TM    BOVRSTAT,BOVRHNDB+BOVRHNDP  BASIC & PREM HAND OVERRIDE?          
         JO    NO                  IF BOTH, EXIT                                
         TM    TBISTAT,TAPDSCAN    IF THIS IS A CANADIAN PAYMENT,               
         JO    NO                  IGNORE IND OVERRIDES                         
         CLI   GTYPE,TABRTY6       MUST BE BILLING TYPES 6, 8, 21, 26           
         BE    UPDHI10                                                          
         CLI   GTYPE,TABRTY8                                                    
         BE    UPDHI10                                                          
         CLI   GTYPE,TABRTY21                                                   
         BE    UPDHI10                                                          
         CLI   GTYPE,TABRTY26                                                   
         JNE   NO                                                               
                                                                                
         USING TARAD,R4                                                         
UPDHI10  L     R4,TMACURRA         GET TARA ELEMENT                             
         LTR   R4,R4                                                            
         JZ    NO                                                               
                                                                                
         TM    BOVRSTAT,BOVRHNDB   BASIC HAND OVERRIDE? IF YES, SKIP            
         BNZ   UPDHI50                                                          
         ICM   R2,15,TARAHFB       BASIC HANDLING RATE                          
         CLI   GTYPE,TABRTY21      IF BILLING TYPE 21,                          
         BNE   UPDHI20                                                          
         TM    TGUSSTAT,SESSION    AND REUSE,                                   
         BO    UPDHI20                                                          
         ICM   R2,15,TARARFB       USE REUSE BASIC HANDLING RATE                
UPDHI20  C     R2,=XL4'FFFFFFFF'   IS RATE = ZERO?                              
         BE    UPDHI50                                                          
         GOTOR MULTP2,DMCB,(R5),(R2)                                            
         L     RE,TMACURPG         RESTORE ADDRESS OF TAPG ELEMENT              
         L     R2,TAPGHNDI                                                      
         A     R2,TGFULL                                                        
         ST    R2,TAPGHNDI         IND HANDLING OVERRIDE                        
                                                                                
         TM    BOVRSTAT,BOVRHNDP   PREM HAND OVERRIDE?                          
         BZ    UPDHI30                                                          
         TM    STAT2,STCORP        IF THERE IS A CORP ON INVOICE,               
         BZ    UPDHI30                                                          
         L     R1,TBHANDC          ADD BASIC HANDLING TO CORP HAND TOT          
         A     R1,TGFULL                                                        
         ST    R1,TBHANDC                                                       
         B     UPDHI50                                                          
UPDHI30  L     R1,TBHANDI          ADD BASIC HANDLING TO IND HAND TOT           
         A     R1,TGFULL                                                        
         ST    R1,TBHANDI                                                       
                                                                                
UPDHI50  TM    BOVRSTAT,BOVRHNDP   PREM HAND OVERRIDE? IF YES, SKIP             
         BNZ   UPDHIX                                                           
         ICM   R2,15,TARAHFP       PREM HANDLING RATE                           
         CLI   GTYPE,TABRTY21      IF BILLING TYPE 21,                          
         BNE   UPDHI60                                                          
         TM    TGUSSTAT,SESSION    AND REUSE,                                   
         BO    UPDHI60                                                          
         ICM   R2,15,TARARFP       USE REUSE PREMIUM HANDLING RATE              
UPDHI60  C     R2,=XL4'FFFFFFFF'   IS RATE = ZERO?                              
         JE    UPDHIX                                                           
         GOTOR MULTP2,DMCB,(R5),(R2)                                            
         L     RE,TMACURPG         RESTORE ADDRESS OF TAPG ELEMENT              
         L     R2,TAPGHPRI                                                      
         A     R2,TGFULL                                                        
         ST    R2,TAPGHPRI         IND PREM HAND OVERRRIDE                      
                                                                                
         TM    BOVRSTAT,BOVRHNDB   BASIC HAND OVERRIDE?                         
         BZ    UPDHI70                                                          
         TM    STAT2,STCORP        IF THERE IS A CORP ON INVOICE,               
         BZ    UPDHI70                                                          
         L     R1,TBHANDC          ADD PREM HANDLING TO CORP HAND TOT           
         A     R1,TGFULL                                                        
         ST    R1,TBHANDC                                                       
         B     UPDHIX                                                           
UPDHI70  L     R1,TBHANDI          ADD PREM HANDLING TO IND HAND TOT            
         A     R1,TGFULL                                                        
         ST    R1,TBHANDI                                                       
                                                                                
UPDHIX   J     YES                                                              
         DROP  R3,R4,RE                                                         
         LTORG                                                                  
         EJECT                                                                  
*=====================================================================          
*        ROUTINE MULTIPLIES PAYROLL TAX OVERRIDE BY HANDLING RATE               
*        RETURNS AMOUNT IN TGFULL AND ADDS AMOUNT TO FULL                       
*=====================================================================          
         SPACE 1                                                                
MULTP2   NTR1  BASE=*,LABEL=*                                                   
         L     R3,0(R1)            PAYROLL TAX OVERRIDE                         
         L     RE,4(R1)            HANDLING RATE                                
*                                                                               
         XR    R2,R2                                                            
*                                                                               
         MR    R2,RE               R2,R3 = RE * R3                              
         LHI   RF,5000                                                          
         DR    R2,RF                                                            
         LTR   R3,R3               R3 = QUOTIENT                                
         BM    *+8                                                              
         AHI   R3,1                ROUND                                        
         SRA   R3,1                                                             
         ST    R3,TGFULL                                                        
*                                                                               
         A     R3,FULL             ADD TO HAND TOTAL                            
         ST    R3,FULL                                                          
         XIT1                                                                   
         LTORG                                                                  
         EJECT                                                                  
*=====================================================================          
*        DECIDE WHICH COLUMNS SHOULD BE PRINTED                                 
*=====================================================================          
         USING P4LINED,R2                                                       
PUTCOL   NTR1  BASE=*,LABEL=*                                                   
         CLI   TBMED,TACOMEDE      IF MEDIA = EVENT, SKIP                       
         BNE   PCOL05                                                           
         MVI   PRCOLL,0                                                         
         B     PCOLX                                                            
PCOL05   TM    PRCOLL,CPNH         SET PNH IN LEFT TITLE                        
         BNO   PCOL10                                                           
         MVC   PH,LTPNH1                                                        
         LA    R4,1(R4)            SET 1 SUM PRINTED                            
         B     PCOL20                                                           
*                                                                               
PCOL10   TM    PRCOLL,CHNW         IS THERE H & W                               
         BNO   PCOL20                                                           
         MVC   PH,LTHNW1                                                        
         B     PCOL30                                                           
*                                                                               
PCOL20   TM    PRCOLR,CHNW                                                      
         BNO   PCOL40                                                           
         MVC   HW,LTHNW1                                                        
*                                                                               
PCOL30   LA    R4,1(R4)                                                         
*                                                                               
PCOL40   OC    TMDED,TMDED         MISC DEDUCTIONS?                             
         BZ    *+8                                                              
         L     R1,TMDED            ADD MISC DEDUCTIONS TO SUB/TOTAL             
         TM    PRCOLL,CMDED        CHECK WHERE IT SHOULD BE PRINTED             
         BNO   PCOL50                                                           
         MVC   PH,LTMDED1                                                       
         MVC   PH2,LTMDED2                                                      
         CLI   UNICOPY,C'Y'        IF THIS IS A UNION COPY                      
         BNE   PCOL60                                                           
         CLC   =C'ACT',CUNION      & UNION = ACTRA                              
         BNE   PCOL60                                                           
         MVC   PH,LTINRD1          CHANGE 1ST LINE OF HEADING                   
         B     PCOL60                                                           
*                                                                               
PCOL50   TM    PRCOLR,CMDED                                                     
         BNO   PCOL70                                                           
         MVC   HW,LTMDED1                                                       
         MVC   HW2,LTMDED2                                                      
*                                                                               
PCOL60   LA    R4,1(R4)                                                         
         B     PCOL100                                                          
*                                                                               
PCOL70   TM    PRCOLL,CINR1                                                     
         BNO   PCOL80                                                           
         MVC   PH,LTINR1                                                        
         B     PCOL90                                                           
*                                                                               
PCOL80   TM    PRCOLR,CINR1                                                     
         BNO   PCOL100                                                          
         MVC   HW,LTINR1                                                        
*                                                                               
PCOL90   LA    R4,1(R4)                                                         
*                                                                               
PCOL100  TM    PRCOLL,CINR2        INSURANCE & RETIREMENT                       
         BNO   PCOL110                                                          
         MVC   PH,LTINR2                                                        
         B     PCOLX                                                            
*                                                                               
PCOL110  TM    PRCOLR,CINR2                                                     
         BNO   PCOLX                                                            
         MVC   HW,LTINR2                                                        
*                                                                               
PCOLX    XIT1                                                                   
*                                                                               
LTPNH1   DC    C' P && H '                                                      
LTHNW1   DC    C' H && W '                                                      
LTINRD1  DC    C'  I&&R  '                                                      
LTMDED1  DC    C'  Misc '                                                       
LTMDED2  DC    C'  Ded  '                                                       
LTINR1   DC    C'Misc/IR'                                                       
LTINR2   DC    C' I && R '                                                      
         LTORG                                                                  
         EJECT                                                                  
*=====================================================================          
*        GET CREDITED INVOICE                                                   
*        RETURNS CC EQ IF BILLING DATE IS BEFORE 7/1/06                         
*=====================================================================          
GETCRD   NTR1  BASE=*,LABEL=*                                                   
         MVC   AIO,AIO1                                                         
         MVI   ELCODE,TANUELQ                                                   
         MVI   WORK,TANUTINV                                                    
         GOTO1 GETL,DMCB,(1,WORK)                                               
         BNE   GCRDNO                                                           
         USING TANUD,R4          GET CREDIT OF INVOICE ELEMENT                  
         L     R4,TGELEM         ADDRESS OF ELEMENT                             
         MVC   WORK(6),TANUMBER  CREDITED INVOICE NUMBER                        
         XC    WORK(6),=6X'FF'   COMPLEMENT                                     
         MVC   TEMP(L'TGINV),TGINV   SAVE ACTUAL INVOICE NUMBER                 
         DROP  R4                                                               
*                                                                               
         MVC   AIO,AIO2                                                         
         GOTO1 RECVAL,DMCB,TLINCDQ,(X'A0',WORK)                                 
         MVC   TGINV,TEMP        RESTORE ACTUAL INVOICE NUMBER                  
         MVC   AIO,AIO1                                                         
         BNE   GCRDNO                                                           
         L     R4,AIO2                                                          
         MVI   ELCODE,TAINELQ    GET INVOICE STATUS ELEMENT                     
         BRAS  RE,GETEL                                                         
         BNE   GCRDNO                                                           
         USING TAIND,R4                                                         
         OC    TAINBDTE,TAINBDTE  IF NO BILLING DATE,                           
         BZ    GCRDNO             USE NEW GST RATE                              
         CLC   TAINBDTE,=X'A60701'   IF BILLING DATE < JULY01/06,               
         BL    GCRDYES           USE OLD GST RATE                               
         B     GCRDNO                                                           
         DROP  R4                                                               
*                                                                               
GCRDYES  SR    RC,RC                                                            
GCRDNO   LTR   RC,RC                                                            
         XIT1                                                                   
         LTORG                                                                  
         EJECT                                                                  
*              ROUTINE TO TO TEST IF PAYMENT TYPE IS A PENALTY                  
         SPACE 1                                                                
PENTEST  NTR1  BASE=*,LABEL=*                                                   
         CLI   TGUSEQU,UPEN        IF USE IS PEN                                
         BE    PTYES                                                            
         CLI   TGUSEQU,USPN        OR SPN                                       
         BE    PTYES                                                            
         CLI   TGUSEQU,UCPN        OR CPN                                       
         BE    PTYES                                                            
         CLI   TGUSEQU,UPEM        OR PEM                                       
         BNE   PTNO                                                             
PTYES    XR    RC,RC               RETURN POSITIVE CONDITION CODE               
PTNO     LTR   RC,RC                                                            
         XIT1                                                                   
         SPACE 2                                                                
         LTORG                                                                  
         EJECT                                                                  
*        ROUTINE SETS CCEQ IF PRINTING ACTRA EXPIRY DATE INSTEAD OF             
*        REGULAR EXPIRY DATE                                                    
*                                                                               
CHKEXP   NTR1  BASE=*,LABEL=*                                                   
         L     R4,AIO1                                                          
         USING TACOD,R4                                                         
         MVI   ELCODE,TACOELQ                                                   
         BRAS  RE,GETEL                                                         
         BNE   CEXPNO                                                           
         CLI   TACOCTYP,CCTY04A    IF ACTRA TYPE IS 2404A                       
         BE    CEXP10                                                           
         CLI   TACOCTYP,CCTY2404   OR ACTRA TYPE IS 2404                        
         BNE   CEXPNO                                                           
CEXP10   TM    TACOSTAT,TACOSCAN   AND CAN$ INVOICE                             
         BNO   CEXPNO                                                           
*                                                                               
CEXPYES  SR    RC,RC               SET CONDITION CODE                           
CEXPNO   LTR   RC,RC                                                            
         XIT1                                                                   
         DROP  R4                                                               
         LTORG                                                                  
         EJECT                                                                  
*&&DO                                                                           
*              ROUTINE TO TEST IF AGENCY IS CHEMISTRI                           
         SPACE 1                                                                
CHEMTEST NTR1  BASE=*,LABEL=*                                                   
         CLI   RECNUM,AC                                                        
         BNE   CHEMNO                                                           
         CLC   TGAGY,=CL6'0163'                                                 
         BE    CHEMYES                                                          
         CLC   TGAGY,=CL6'0162'                                                 
         BNE   CHEMNO                                                           
         CLC   TGCLI,=CL6'GMSPO'                                                
         BE    CHEMNO                                                           
CHEMYES  XR    RC,RC                                                            
CHEMNO   LTR   RC,RC                                                            
         XIT1                                                                   
         SPACE 2                                                                
         LTORG                                                                  
         EJECT                                                                  
*&&                                                                             
*              ROUTINE TO TEST IF GM WORKCODE SHOULD BE PRINTING                
*              FOR CURRENT AGENCY/CLIENT                                        
         SPACE 1                                                                
GMWKCTST NTR1  BASE=*,LABEL=*                                                   
         CLI   RECNUM,AC           NEVER PRINT IF NOT ACOPY                     
         BNE   GMWKCNO                                                          
*                                                                               
         CLC   TGAGY,=CL6'0162'    IF AGENCY IS 0162 OR 0163 ONLY               
******** BE    GMWKC010                                                         
******** CLC   TGAGY,=CL6'0163'                                                 
         BNE   GMWKCNO                                                          
*                                                                               
GMWKC010 DS    0H                                                               
         MVC   AIO,AIO2                                                         
         GOTO1 RECVAL,DMCB,TLCLCDQ,(X'20',0)                                    
         L     R4,AIO              CONTROL RECORD HAS COMMISSION RATE           
         USING TACID,R4                                                         
         MVI   ELCODE,TACIELQ                                                   
         BRAS  RE,GETEL                                                         
         BNE   GMWKCNO                                                          
         CLC   TACICLG,=CL6'GM'    HAS TO BE CLI GROUP GM                       
         BNE   GMWKCNO                                                          
******** CLC   TGCLI,=CL6'GMLLC'   EXCEPT FOR THESE CLIENTS                     
******** BE    GMWKCNO                                                          
***NO-OP CLC   TGCLI,=CL6'GMCL'                                                 
***08/14 BE    GMWKCNO                                                          
*                                                                               
*                                                                               
GMWKCYES XR    RF,RF                                                            
GMWKCNO  LTR   RF,RF                                                            
         MVC   AIO,AIO1                                                         
         XIT1                                                                   
         SPACE 2                                                                
         LTORG                                                                  
         EJECT                                                                  
*              ROUTINE TO TEST IF GM WORKCODE SHOULD BE PRINTING                
*              FOR CURRENT AGENCY/CLIENT                                        
         SPACE 1                                                                
GMWKCT2  NTR1  BASE=*,LABEL=*                                                   
         CLI   RECNUM,AC           NEVER PRINT IF NOT ACOPY                     
         JNE   NO                                                               
*                                                                               
         CLC   TGAGY,=CL6'0162'    IF AGENCY IS 0162                            
         JNE   NO                                                               
*                                                                               
******** CLC   TGCLI,=CL6'GMLLC'   CLIENT MUST BE GMLLC,                        
******** JE    YES                                                              
***NO-OP CLC   TGCLI,=CL6'GMCL'    GMCL,                                        
***08/14 JE    YES                                                              
         CLC   TGCLI,=CL6'NCAA'    OR NCAA                                      
         JE    YES                                                              
         J     NO                                                               
*                                                                               
         SPACE 2                                                                
         LTORG                                                                  
         EJECT                                                                  
*              ROUTINE TO PRINT OUT CLIENT COMMENT RECORD                       
*              CALLED WITH PARAMETER:                                           
*              STATPRT = PRINT COMMENT                                          
*              STATEMS = PUT COMMENT TO EMS FILE                                
*              STATPAG = INCREMENT TOTAL INVOICE PAGES BY ONE                   
*              STATBOX = DETERMINE WHETHER TO PRINT BOXES ON PAGE               
         SPACE 2                                                                
PRTCMT   NTR1  BASE=*,LABEL=*                                                   
         MVC   TGBYTE,3(R1)                                                     
         SPACE 1                                                                
         USING TLIND,R4                                                         
         L     R4,AIO              EXIT IF AIO DOES NOT CONTAIN                 
         CLI   TLINCD,TLINCDQ      AN INVOICE                                   
         BNE   PCMTNO                                                           
         DROP  R4                                                               
         SPACE 1                                                                
         USING TAMTD,R4                                                         
         L     R4,AIO              IF THE INVOICE RECORD                        
         MVI   ELCODE,TAMTELQ      HAS MKTS/CNETS ON IT                         
         BRAS  RE,GETEL            WE WILL PRINT THE MKTS/CNETS,                
         BNE   PCMT100             NOT THE COMMENT                              
         TM    TGBYTE,STATPRT+STATEMS                                           
         BZ    PCMTYES                                                          
         SPACE 1                                                                
         USING PHEADS,R2                                                        
         LA    R2,P+3              PRINT 2 MKT/CNETS HEADERS ON                 
         LHI   R3,2                FIRST LINE                                   
         SPACE 1                                                                
         LA    RE,CNETHEAD         USE HEADING 'CNET' FOR CBL                   
         CLI   TGUSEQU,UCBL        AND SCB                                      
         BE    PCMT10                                                           
         CLI   TGUSEQU,USCB                                                     
         BE    PCMT10                                                           
         LA    RE,CSYSHEAD         USE HEADING 'CSYS' FOR LCB                   
         CLI   TGUSEQU,ULCB                                                     
         BE    PCMT10                                                           
         LA    RE,MKTHEAD          ELSE USE 'MKT'                               
         SPACE 1                                                                
PCMT10   MVC   PDATE,DATEHEAD                                                   
         MVC   PNAME,0(RE)         PUT HEADINGS TO PRINT LINE                   
         LA    R2,PHEADLEN(R2)                                                  
         BCT   R3,PCMT10                                                        
         SPACE 1                                                                
         TM    TGBYTE,STATEMS      FOR EMS PUT TO FILE                          
         BNO   PCMT20                                                           
         GOTOR PUTCMT                                                           
         B     PCMT30                                                           
PCMT20   BRAS  RE,PRTIT            FOR ALL OTHERS, PRINT                        
         SPACE 1                                                                
PCMT30   LA    R2,P+3              PRINT 2 MKTS/CNETS PER LINE                  
         LHI   R3,2                                                             
         B     PCMT50                                                           
         SPACE 1                                                                
PCMT40   BRAS  RE,NEXTEL                                                        
         BNE   PCMT80                                                           
         SPACE 1                                                                
PCMT50   ZIC   R0,TGBYTE                                                        
         BAS   RE,OUTTAMT          OUTPUT CNET/CSYS/MARKET DETAILS              
         STC   R0,TGBYTE                                                        
         SPACE 1                                                                
         LA    R2,PHEADLEN(R2)                                                  
         BCT   R3,PCMT40                                                        
         SPACE 1                                                                
         TM    TGBYTE,STATEMS      WHEN LINE IS FULL                            
         BNO   PCMT60                                                           
         GOTOR PUTCMT              PUT COMMENT TO DATASET                       
         B     PCMT70                                                           
PCMT60   BRAS  RE,PRTIT            OR PRINT IT                                  
         SPACE 1                                                                
PCMT70   LA    R2,P+3              GET NEXT MKT/CNET ELEMENT                    
         LHI   R3,2                                                             
         B     PCMT40                                                           
         SPACE 1                                                                
PCMT80   TM    TGBYTE,STATEMS                                                   
         BNO   PCMT90                                                           
         GOTOR PUTCMT              PUT LAST LINE TO DATASET                     
         B     PCMTYES                                                          
PCMT90   BRAS  RE,PRTIT            OR PRINT                                     
         B     PCMTYES                                                          
         DROP  R4                                                               
         SPACE 1                                                                
         USING TAMDD,R4                                                         
PCMT100  L     R4,AIO              IF THE INVOICE RECORD                        
         MVI   ELCODE,TAMDELQ      HAS INTERNET/NEW MEDIA ON IT                 
         BRAS  RE,GETEL            WE WILL PRINT INTERNET/NEW MEDIA             
         BNE   PCMT200             NOT THE COMMENT                              
         TM    TGBYTE,STATPRT+STATEMS                                           
         BZ    PCMTYES                                                          
         SPACE 1                                                                
         L     RE,AIO              SAVE INVOICE KEY                             
         XC    PCKEY,PCKEY                                                      
         MVC   PCKEY(L'TLINKEY),0(RE)                                           
         SPACE 1                                                                
         MVC   PCMEEQU,TGMEEQU     SAVE MEDIA                                   
         MVC   TGMEEQU,TAMDTYPE    AND RESET TO INTERNET/NEW MEDIA              
         SPACE 1                                                                
         LA    R2,P+3              PRINT 2 INTERNET/NEW MEDIA HEADERS           
         LHI   R3,2                ON FIRST LINE                                
         SPACE 1                                                                
         LA    RE,IRNHEAD          USE HEADING 'INTERNET'                       
         CLI   TAMDTYPE,INTERNET                                                
         BE    PCMT110                                                          
         LA    RE,NMRHEAD          OR "NEW MEDIA"                               
         SPACE 1                                                                
PCMT110  MVC   PCODE2,CODEHEAD                                                  
         MVC   PNAME2,0(RE)        PUT HEADINGS TO PRINT LINE                   
         LA    R2,PHEA2LEN(R2)                                                  
         BCT   R3,PCMT110                                                       
         SPACE 1                                                                
         TM    TGBYTE,STATEMS      FOR EMS PUT TO FILE                          
         BNO   PCMT120                                                          
         GOTOR PUTCMT                                                           
         B     PCMT130                                                          
PCMT120  BRAS  RE,PRTIT            FOR ALL OTHERS, PRINT                        
         SPACE 1                                                                
PCMT130  LA    R2,P+3              PRINT 2 INTERNET/NEW MEDIA PER LINE          
         LHI   R3,2                                                             
         B     PCMT150                                                          
         SPACE 1                                                                
PCMT140  BRAS  RE,NEXTEL                                                        
         BNE   PCMT180                                                          
         SPACE 1                                                                
PCMT150  MVC   PCODE2,TAMDCODE     PUT CODE TO LINE                             
         OC    PCODE2,SPACES       AND PAD WITH SPACES                          
         SPACE 1                                                                
         ST    R4,PCAELEM          SAVE A(INTERNET/NEW MEDIA ELEMENT)           
         SPACE 1                                                                
         MVC   AIO,AIO2                                                         
         GOTO1 RECVAL,DMCB,TLMDCDQ,(X'A4',TAMDCODE)                             
         BNE   PCMT155                                                          
         DROP  R4                                                               
         SPACE 1                                                                
         USING TANAD,R4                                                         
         L     R4,AIO                                                           
         MVI   ELCODE,TANAELQ                                                   
         BRAS  RE,GETEL                                                         
         BNE   PCMT155                                                          
         ZIC   RE,TANALEN                                                       
         SHI   RE,3                                                             
         EX    RE,*+8                                                           
         B     *+10                                                             
         MVC   PNAME2(0),TANANAME   COPY INTERNET/NEW MEDIA NAME                
         OC    PNAME2,SPACES        TO LINE                                     
         DROP  R4                                                               
         SPACE 1                                                                
PCMT155  MVC   KEY,PCKEY            RESTORE INVOICE RECORD                      
         GOTO1 HIGH                                                             
         CLC   KEY(L'TLINKEY),KEYSAVE                                           
         BE    *+6                                                              
         DC    H'00'                                                            
         GOTO1 GETREC                                                           
         SPACE 1                                                                
         MVC   AIO,AIO1                                                         
         SPACE 1                                                                
         USING TAMDD,R4                                                         
         L     R4,PCAELEM                                                       
         MVI   ELCODE,TAMDELQ       RESTORE ELEMENT POINTER                     
         SPACE 1                                                                
         LA    R2,PHEA2LEN(R2)                                                  
         BCT   R3,PCMT140                                                       
         SPACE 1                                                                
         TM    TGBYTE,STATEMS      WHEN LINE IS FULL                            
         BNO   PCMT160                                                          
         GOTOR PUTCMT              PUT COMMENT TO DATASET                       
         B     PCMT170                                                          
PCMT160  BRAS  RE,PRTIT            OR PRINT IT                                  
         SPACE 1                                                                
PCMT170  LA    R2,P+3              GET NEXT INTERNET/NEW MEDIA ELEMENT          
         LHI   R3,2                                                             
         B     PCMT140                                                          
         SPACE 1                                                                
PCMT180  TM    TGBYTE,STATEMS                                                   
         BNO   PCMT190                                                          
         GOTOR PUTCMT              PUT LAST LINE TO DATASET                     
         B     PCMTYES                                                          
PCMT190  BRAS  RE,PRTIT            OR PRINT                                     
         SPACE 1                                                                
         MVC   TGMEEQU,PCMEEQU     RESTORE MEDIA                                
         B     PCMTYES                                                          
         DROP  R4                                                               
         SPACE 1                                                                
         USING TLIND,R4                                                         
         USING TLCMD,R3                                                         
PCMT200  L     R4,AIO                                                           
         LA    R3,KEY              BUILD KEY TO SEE IF TP                       
         XC    TLCMKEY,TLCMKEY     COMMENT EXISTS FOR THIS                      
         MVI   TLCMCD,TLCMCDQ      INVOICE                                      
         MVC   TLCMAGY,TLINAGY                                                  
         MVI   TLCMTYP,TLCMTINV                                                 
         MVC   TLCMINV,TLININV                                                  
         MVI   TLCMLEV,TLCMTPC                                                  
         GOTO1 HIGH                                                             
         CLC   KEY(L'TLCMKEY),KEYSAVE                                           
         BE    PCMT210                                                          
         SPACE 1                                                                
         LA    R3,KEY              IF NOT, BUILD KEY TO SEE                     
         XC    TLCMKEY,TLCMKEY     IF CLIENT COMMENT EXISTS                     
         MVI   TLCMCD,TLCMCDQ      FOR THIS INVOICE                             
         MVC   TLCMAGY,TLINAGY                                                  
         MVI   TLCMTYP,TLCMTINV                                                 
         MVC   TLCMINV,TLININV                                                  
         GOTO1 HIGH                                                             
         CLC   KEY(L'TLCMKEY),KEYSAVE                                           
         BNE   PCMTNO              IF NOT, EXIT                                 
         DROP  R3,R4                                                            
         SPACE 1                                                                
PCMT210  MVC   AIO,AIO2                                                         
         GOTO1 GETREC                                                           
         SPACE 1                                                                
**NO-OP  TM    TGBYTE,STATPRT+STATEMS  PRINTING OR PUTTING TO EMS FILE          
**06/14  BZ    PCMT290                                                          
         SPACE 1                                                                
         L     R4,AIO                                                           
         LA    R2,P+3              R2=PRINT LINE                                
         MVI   TGBYTE2,1           TGBYTE2=COMMENT FIELD COUNTER                
         MVI   ELCODE,TAXCELQ                                                   
         BRAS  RE,GETEL            R4=A(COMMENT ELEMENT)                        
         BNE   PCMT290                                                          
         TM    TGBYTE,STATBOX+STATPAG                                           
         BZ    PCMT218                                                          
         USING TAXCD,R4                                                         
         CLC   TAXCCMNT(14),=C'DELETE REASON:'   NO BOXES FOR DELETES           
         BNE   PCMT290                                                          
         B     PCMT219                                                          
PCMT218  LA    RF,17               RF=# OF COMMENT LINES + 1                    
*                                                                               
         CLC   TAXCCMNT(14),=C'DELETE REASON:'   SKIP DELETE COMMENTS           
         BNE   PCMT230                                                          
         DROP  R4                                                               
         USING TLIND,R3                                                         
         USING TLCMD,R4                                                         
PCMT219  LA    R3,KEY              RESTORE AIO1 TO INVOICE                      
         L     R4,AIO              RECORD                                       
         XC    TLINKEY,TLINKEY                                                  
         MVI   TLINCD,TLINCDQ                                                   
         MVC   TLINAGY,TLCMAGY                                                  
         MVC   TLININV,TLCMINV                                                  
         GOTO1 HIGH                                                             
         CLC   KEY(L'TLINKEY),KEYSAVE                                           
         BE    *+6                                                              
         DC    H'00'                                                            
         GOTO1 GETREC                                                           
         MVC   AIO,AIO1                                                         
         B     PCMTNO              DON'T MAKE A NEW PAGE                        
         DROP  R3,R4                                                            
                                                                                
PCMT220  BRAS  RE,NEXTEL                                                        
         BNE   PCMT290                                                          
         USING TAXCD,R4                                                         
PCMT230  AHI   RF,-1                                                            
         STC   RF,TGBYTE3                                                       
         ZIC   RE,TAXCSEQ          MATCH UP SEQUENCE # IN ELEMENT               
         ZIC   RF,TGBYTE2                                                       
         CR    RF,RE               TO SEQUENCE # OF COMMENT FIELD               
         BNE   PCMT260                                                          
         ZIC   RE,TAXCLEN                                                       
         SHI   RE,4                                                             
         BCTR  RE,0                                                             
         EX    RE,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R2),TAXCCMNT    DISPLAY COMMENT                              
         SPACE 1                                                                
         TM    TGBYTE,STATEMS      1ST RUN EMS BILLING OR ACOPY                 
         BNO   PCMT240                                                          
         GOTOR PUTCMT              PUT COMMENT TO DATASET                       
         B     PCMT250             DON'T PRINT                                  
PCMT240  BRAS  RE,PRTIT                                                         
PCMT250  ZIC   RF,TGBYTE2          INCREMENT COMMENT FIELD COUNTER              
         AHI   RF,1                                                             
         STC   RF,TGBYTE2                                                       
         ZIC   RF,TGBYTE3                                                       
         BCT   RF,PCMT220                                                       
         B     PCMT290                                                          
         SPACE 1                                                                
PCMT260  TM    TGBYTE,STATEMS      1ST RUN EMS BILLING OR ACOPY                 
         BNO   PCMT270                                                          
         GOTOR PUTCMT              PUT COMMENT TO DATASET                       
         B     PCMT280             DON'T PRINT                                  
PCMT270  BRAS  RE,PRTIT                                                         
PCMT280  ZIC   RF,TGBYTE2          INCREMENT COMMENT FIELD COUNTER              
         AHI   RF,1                                                             
         STC   RF,TGBYTE2                                                       
         ZIC   RF,TGBYTE3                                                       
         BCT   RF,PCMT230                                                       
         SPACE 1                                                                
         USING TLIND,R3                                                         
         USING TLCMD,R4                                                         
PCMT290  LA    R3,KEY              RESTORE AIO1 TO INVOICE                      
         L     R4,AIO              RECORD                                       
         XC    TLINKEY,TLINKEY                                                  
         MVI   TLINCD,TLINCDQ                                                   
         MVC   TLINAGY,TLCMAGY                                                  
         MVC   TLININV,TLCMINV                                                  
         GOTO1 HIGH                                                             
         CLC   KEY(L'TLINKEY),KEYSAVE                                           
         BE    *+6                                                              
         DC    H'00'                                                            
         GOTO1 GETREC                                                           
         MVC   AIO,AIO1                                                         
         DROP  R3,R4                                                            
         SPACE 1                                                                
PCMTYES  SR    RC,RC                                                            
PCMTNO   LTR   RC,RC                                                            
PCMTX    XIT1                                                                   
         SPACE 2                                                                
STATPRT  EQU   X'80'                                                            
STATPAG  EQU   X'40'                                                            
STATBOX  EQU   X'20'                                                            
STATEMS  EQU   X'10'                                                            
         SPACE 2                                                                
DATEHEAD DC    CL(L'PDATE)'Date'                                                
CNETHEAD DC    CL(L'PNAME)'Cable Network/System'                                
CSYSHEAD DC    CL(L'PNAME)'Cable System'                                        
MKTHEAD  DC    CL(L'PNAME)'Market'                                              
         SPACE 1                                                                
CODEHEAD DC    CL(L'PCODE2)'Code'                                               
IRNHEAD  DC    CL(L'PNAME2)'Internet'                                           
NMRHEAD  DC    CL(L'PNAME2)'New Media'                                          
         SPACE 1                                                                
PCKEY    DC    XL(L'KEY)'00'                                                    
PCMEEQU  DC    X'00'                                                            
PCAELEM  DC    A(0)                                                             
         SPACE 1                                                                
*              ROUTINE TO OUTPUT TAMTD ELEMENT DETAILS                          
*              ON ENTRY ... R2=A(PRINT LINE AREA)                               
*                           R4=A(CNET/CSYS/MARKET ELEMENT)                      
         SPACE 2                                                                
         USING PHEADS,R2                                                        
         USING TAMTD,R4                                                         
OUTTAMT  NTR1                                                                   
         OC    TAMTCYCS,TAMTCYCS   IF DATE IS PRESENT                           
         BZ    OTAMT10             PUT IT TO PRINT LINE                         
         GOTO1 DATCON,DMCB,(1,TAMTCYCS),(8,PDATE)                               
         SPACE 1                                                                
OTAMT10  MVC   PNAME(6),TAMTCODE   INITIALLY PUT CODE TO PRINT LINE             
         SPACE 1                                                                
         MVI   TGMTTYPE,TANPNET    SET TO READ CNET RECORDS                     
         TM    TGUSSTA3,CBLUSE     FOR CABLE                                    
         BO    OTAMT20             OR SPANISH CABLE                             
         SPACE 1                                                                
         MVI   TGMTTYPE,C'S'       SET TO READ CSYS RECORDS                     
         CLI   TGUSEQU,ULCB        FOR LOCAL CABLE                              
         BE    OTAMT20                                                          
         SPACE 1                                                                
         MVI   TGMTTYPE,C'T'       ELSE, SET TO READ TMKT RECORDS               
         CLI   TGMEEQU,RADIO       UNLESS MEDIA IS RADIO                        
         BNE   OTAMT20             THEN SET TO READ RMKT RECORDS                
         MVI   TGMTTYPE,C'R'                                                    
         SPACE 1                                                                
OTAMT20  LHI   RF,TLMTCDQ          DETERMINE WHICH KEY TO READ                  
         TM    TGMEEQU,LIKETV                                                   
         BZ    OTAMT30                                                          
         TM    TGUSSTA3,CBLUSE                                                  
         BO    OTAMT30                                                          
         CLI   TGUSEQU,ULCB                                                     
         BE    OTAMT30                                                          
         LHI   RF,TLMTALDQ                                                      
         SPACE 1                                                                
OTAMT30  MVC   AIO,AIO2            READ FOR CODE'S ASSOCIATED RECORD            
         GOTO1 RECVAL,DMCB,(RF),(X'A4',TAMTCODE)                                
         BE    OTAMT40                                                          
         SPACE 1                                                                
         TM    TGUSSTA3,CBLUSE     IF CABLE OR SPANISH CABLE INVOICE            
         BZ    OTAMT50             AND CNET NOT FOUND                           
         MVI   TGMTTYPE,C'S'       CHECK IF CODE EXISTS AS A CSYS               
         GOTO1 RECVAL,DMCB,TLMTCDQ,(X'A4',TAMTCODE)                             
         BNE   OTAMT50                                                          
         DROP  R4                                                               
         SPACE 1                                                                
         USING TANAD,R4                                                         
OTAMT40  L     R4,AIO                                                           
         MVI   ELCODE,TANAELQ                                                   
         BRAS  RE,GETEL                                                         
         BNE   OTAMT50                                                          
         ZIC   RE,TANALEN                                                       
         SHI   RE,3                                                             
         EX    RE,*+8                                                           
         B     *+10                                                             
         MVC   PNAME(0),TANANAME   PUT ASSOCIATED NAME TO PRINT LINE            
         DROP  R4                                                               
         SPACE 1                                                                
OTAMT50  OC    PNAME,SPACES        PAD WITH SPACES                              
         MVC   AIO,AIO1            RESTORE I/O AREA                             
         MVI   ELCODE,TAMTELQ      AND RESTORE ELCODE                           
         B     PCMTX                                                            
         DROP  R2                                                               
         SPACE 1                                                                
         LTORG                                                                  
         EJECT                                                                  
*======================================================================         
*        SEE IF GUARANTEE IS A TYPE 6 GUARANTEE                                 
*======================================================================         
ISTYPE6  NTR1  BASE=*,LABEL=*                                                   
         MVC   AIO,AIO2                                                         
         XC    KEY,KEY                                                          
         LA    R4,KEY                                                           
         USING TABCHKD,R3                                                       
         USING TLGUD,R4                                                         
         MVI   TLGUCD,TLGUCDQ            GUARANTEE RECORD                       
         MVC   TLGUSSN,TABSSN            SOCIAL SECURITY NUMBER                 
         MVC   TLGUGUA,TABGRTCD          GRT CODE                               
         XC    TLGUGUA,=X'FFFFFFFF'      COMPLEMENT IT                          
         GOTO1 HIGH                                                             
         GOTO1 GETREC                                                           
         L     R4,AIO                                                           
         USING TAGUD,R4                                                         
         MVI   ELCODE,TAGUELQ      GRT ELEMENT                                  
         BRAS  RE,GETEL                                                         
         BNE   ISTYPE6N                                                         
         OC    TAGUCOM,TAGUCOM     COMMERCIAL SET, TYPE 6 GRT                   
         BZ    ISTYPE6N                                                         
*                                                                               
         CR    RB,RB                                                            
         B     *+6                                                              
ISTYPE6N LTR   RB,RB                                                            
ISTYPE6X MVC   AIO,AIO1                                                         
         XIT1                                                                   
*                                                                               
         LTORG                                                                  
         DROP   R3,R4                                                           
         EJECT                                                                  
*======================================================================         
*        SET BILLING RATES FOR TALIM                                            
*======================================================================         
SETRATES NTR1  BASE=*,LABEL=*                                                   
         L     R4,ATALIM                                                        
         USING TMD,R4                                                           
*                                                                               
         LA    R1,GRFUTA           BILLING RATES                                
         LA    R2,TMBRATES                                                      
         LA    R3,TMRATEN          N'BILLING RATES                              
*                                                                               
SR10     LH    R0,0(R1)                                                         
         ST    R0,0(R2)                                                         
         LA    R1,2(R1)            BUMP TO NEXT RATE                            
         LA    R2,4(R2)                                                         
         BCT   R3,SR10                                                          
*                                  *** 1/8/03 as per Tom ***                    
*        CLC   TMUNIT,=C'CA'       CALIFORNIA WORK,                             
*        BNE   SR20                                                             
*        CLI   TBTPO,C'7'          OFFICE 7 / G,                                
*        BE    *+8                                                              
*        CLI   TBTPO,C'G'                                                       
*        BNE   SR20                                                             
*        MVC   TMBRFUI,=F'1643'    SET FUTA RATE TO 16.43%                      
*                                                                               
SR20     XIT1                                                                   
         DROP R4                                                                
         LTORG                                                                  
         EJECT                                                                  
*                                                                               
*        CALCULATE EMS PAYROLL TAX, HANDLING, EMS FEE AND TOTAL                 
*                                                                               
CALCEMS  NTR1  BASE=*,LABEL=*                                                   
         L     R1,TOTGRS         PAYROLL TAX IS TAKEN FROM TOTAL                
         A     R1,TOTMISC        WAGES, AND MISC                                
         ST    R1,EMSTOT         STORE IN EMS TOTAL                             
         MVC   HALF,EMSSESS      PAYROLL TAX RATE FOR SESSIONS                  
         TM    TGUSSTAT,SESSION  SESSION                                        
         BO    *+10                                                             
         MVC   HALF,EMSRESI      PAYROLL TAX RATE FOR RESIDUALS                 
         GOTOR MULTP             MULT PERCENTAGE AND ADD TO TOTAL               
         MVC   EMSTTAX,FULL      TOTAL PAYROLL TAX                              
         GOTOR SSURPT,DMCB,EMSTTAX,SUISURCA      ADD CA/NY SURCHARGE            
         GOTOR SSURPT,DMCB,EMSTOT,SUISURCA       TO P/R TAX AND TOTAL           
*                                                                               
         L     R1,EMSTOT         HANDLING IS TAKEN FROM WAGES, MISC,            
         A     R1,TBPNH1         PAYROLL TAX, P&H, H&W, I&R, AND GST            
         A     R1,TBPNH2                                                        
         A     R1,TBPNH3                                                        
         A     R1,TBPNH4                                                        
         A     R1,TBHNW1                                                        
         A     R1,TBHNW2                                                        
         A     R1,TBHNW3                                                        
         A     R1,TBHNW4                                                        
         TM    TBISTAT,TAPDSCAN   IF THIS IS A CANADIAN PAYMENT                 
         BNO   CEMS10                                                           
         A     R1,TBCGST          GST CAN$                                      
         A     R1,TBCPST          PST CAN$                                      
         A     R1,AOSPST          AOSPST                                        
         B     CEMS20                                                           
CEMS10   A     R1,TBGSTU          GST                                           
         A     R1,TBPSTU          PST                                           
CEMS20   A     R1,TBINR           I&R                                           
         S     R1,HNWFUND         SUBTRACT ANY H&W IF UNI/LOC HAS FUND          
         ST    R1,EMSTOT                                                        
         MVC   HALF,EMSHAND       HANDLING RATE                                 
         GOTOR MULTP              MULTIPLY PERCENTAGE AND ADD TO TOTAL          
         MVC   EMSTHND,FULL       TOTAL HANDLING                                
*                                                                               
         MVC   HALF,EMSEMSF       EMS FEE RATE                                  
         GOTOR MULTP              MULT PERCENTAGE AND ADD TO TOTAL              
         MVC   EMSTEMS,FULL       TOTAL EMS FEE                                 
         XIT1                                                                   
         LTORG                                                                  
*                                                                               
*        ROUTINE MULTIPLIES EMSTOT BY PERCENTAGE IN HALF                        
*        RETURNS AMOUNT IN FULL AND ADDS AMOUNT TO EMSTOT                       
*                                                                               
         SPACE 1                                                                
MULTP    NTR1  BASE=*,LABEL=*                                                   
         LH    RE,HALF             RATE                                         
*                                                                               
         XR    R2,R2                                                            
         L     R3,EMSTOT                                                        
*                                                                               
         MR    R2,RE               R2,R3 = RE * R3                              
         LHI   RF,5000                                                          
         DR    R2,RF                                                            
         LTR   R3,R3               R3 = QUOTIENT                                
         BM    *+8                                                              
         AHI   R3,1                ROUND                                        
         SRA   R3,1                                                             
         ST    R3,FULL             RETURN CALC FOR 1                            
*                                                                               
         A     R3,EMSTOT           ADD TO EMS TOTAL                             
         ST    R3,EMSTOT                                                        
         XIT1                                                                   
         LTORG                                                                  
         EJECT                                                                  
*======================================================================         
*        PUT HEADLINES TO DATASET                                               
*======================================================================         
PUT1HD   NTR1  BASE=*,LABEL=*                                                   
         GOTOR CLRREC              PUT SPACES IN TAPEREC                        
*                                                                               
         LA    R3,TAPEREC                                                       
         USING RECD,R3                                                          
         MVI   RECTYP,RECTYPH      HEADING RECORD                               
         MVC   RECAGYN,TBAYNAME    AGENCY NAME                                  
         MVC   RECGST,SPACES                                                    
         TM    TBISTAT,TAPDSCAN    IF CANADIAN -PRINT GST NUMBER                
         BNO   *+10                                                             
         MVC   RECGST,TBEMGST#                                                  
*                                                                               
         MVC   RECADD1,TBADD1      ADDRESS LINE 1                               
         OC    RECADD1,SPACES                                                   
         MVC   RECADD2,TBADD2      ADDRESS - 2ND LINE                           
         OC    RECADD2,SPACES                                                   
         MVC   RECADD3,TBADD3      ADDRESS - 3RD LINE                           
         OC    RECADD3,SPACES                                                   
         MVC   RECADD4,TBADD4      ADDRESS - 4TH LINE                           
         OC    RECADD4,SPACES                                                   
*                                                                               
         CLC   TBATTN,SPACES                                                    
         BNH   *+10                                                             
         MVC   RECATTN,TBATTN      ATTN NAME                                    
*                                                                               
         MVC   RECCLIN,TBCLNAME    CLIENT NAME                                  
         GOTO1 DATCON,DMCB,(1,TBAIR),(5,RECFAIR)  FIRST AIR DATE                
         MVC   RECPRDN,TBPRNAME    PRODUCT NAME                                 
         GOTO1 DATCON,DMCB,(1,TBFCYC),(5,RECFFCY)  FIRST FIXED CYCLE            
         MVC   RECCOM,TBCONAME     COMMERCIAL TITLE                             
*                                                                               
         BRAS  RE,CHKEXP           PRINT EXPIRY OR ACTRA EXPIRY DATE?           
         BNE   P1H10                                                            
*                                                                               
         OC    TBACTEXP,TBACTEXP   IF WE HAVE ACTRA EXPIRY DATE, PRINT          
         BZ    P1H10               THIS INSTEAD OF EXPIRY DATE                  
         GOTO1 DATCON,DMCB,(1,TBACTEXP),(5,RECEXP) ACTRA EXPIRY DATE            
         B     P1H20                                                            
P1H10    GOTO1 DATCON,DMCB,(1,TBCOEXP),(5,RECEXP) EXP DATE                      
*                                                                               
P1H20    MVC   RECCID,TGCID        COMMERCIAL ID                                
         OC    TBCID2,TBCID2       IF HAVE 2ND CID                              
         BZ    *+10                                                             
         MVC   RECCID2,TBCID2                                                   
*                                                                               
         TM    STAT2,STAVERS          IF HAVE VERSION CODE                      
         BZ    *+10                                                             
         MVC   RECVER,TBVERS          VERSION CODE                              
         TM    STAT2,STAVERS                                                    
         BZ    *+10                                                             
         MVC   RECVID,TBVERSID        VERSION ID                                
*                                                                               
         TM    STAT2,STAVERS          IF VERSION, DON'T PRINT LIFT              
         BO    P1HX                                                             
         CLC   TBLID,SPACES                                                     
         BNH   *+10                                                             
         MVC   RECLFT,TBLID           LIFT NUMBER                               
         DROP  R3                                                               
*                                                                               
P1HX     GOTOR PUTTAPE                PUT RECORD TO TAPE                        
         XIT1                                                                   
         LTORG                                                                  
         EJECT                                                                  
*                                                                               
*        PUT HEADLINES IN BOX TO DATASET                                        
*                                                                               
PUTBHD   NTR1  BASE=*,LABEL=*                                                   
         GOTOR CLRREC              PUT SPACES IN TAPEREC                        
*                                                                               
         LA    R3,TAPEREC                                                       
         USING RECD,R3                                                          
         MVI   RECTYP,RECTYPB          BOX HEADING RECORD                       
         MVC   RECCLI,TGCLI            AGENCY CODE                              
*                                                                               
         GOTOR PUTHEAD          PUT HEADINGS FOR OT&TRAVEL, P&H, H&W            
*                                                                               
PBH15    MVC   TEMP(L'TGINV),TGINV                                              
         XC    TEMP(L'TGINV),COMPLM2  COMPLEMENT INV NUM                        
         GOTO1 TINVCON,DMCB,(X'80',TEMP),TBINV,DATCON                           
         MVC   RECINV,TBINV           INVOICE NUMBER                            
*                                                                               
         TM    STATUS,STBNP            IF BNP                                   
         BNO   *+10                                                             
         MVC   RECSPEC+7(3),=C'BNP'    SPECIAL INVOICE CODES                    
         TM    TBSTAT,TAINSCIN         IF INVOICE WAS CANCELLED                 
         BNO   *+10                                                             
         MVC   RECSPEC(3),=C'CXL'                                               
         TM    STATUS,STCRDT           IF CREDIT                                
         BNO   *+10                                                             
         MVC   RECSPEC+4(2),=C'CR'                                              
*                                                                               
         MVC   RECBDAT,TBRUNDTE        BILLING DATE                             
*                                                                               
         GOTO1 MEDVAL,DMCB,TBMED                                                
         CLI   TBCOTYPE,CTYIND     DON'T PRINT MEDIA FOR INDUST COMTYPE         
         BE    *+10                                                             
         MVC   RECMED,TGMENAME     MEDIA                                        
*                                                                               
         CLC   TBPO,SPACES                                                      
         BNH   *+10                                                             
         MVC   RECPONM,TBPO        PO NUMBER                                    
*                                                                               
         CLC   TBEST,SPACES                                                     
         BNH   *+10                                                             
         MVC   RECESTN,TBEST       ESTIMATE NUMBER                              
*                                                                               
         CLC   TBPER,SPACES                                                     
         BNH   *+10                                                             
         MVC   RECESTP,TBPER       ESTIMATE PERIOD                              
*                                                                               
         CLC   TBVEN,SPACES                                                     
         BNH   *+10                                                             
         MVC   RECVEN,TBVEN        VENDOR                                       
*                                                                               
         ZICM  R1,TBSEC,1          COMMERCIAL LENGTH                            
         BZ    PBH20                                                            
         EDIT  (R1),(3,RECCOLN),ZERO=BLANK,ALIGN=LEFT                           
*                                                                               
PBH20    OC    TBLID,TBLID         IF HAVE LIFT                                 
         BZ    PBH40                                                            
         OC    TBLSEC,TBLSEC       AND LIFT LENGTH                              
         BZ    PBH40                                                            
         TM    STAT2,STAVERS       NOT VERSION                                  
         BO    PBH40                                                            
         EDIT  TBLSEC,(2,RECLFLN),ZERO=BLANK,ALIGN=LEFT       LIFT LEN          
*                                                                               
         USING P1LINED,R2                                                       
PBH40    LA    R2,P                                                             
*                                                                               
         MVC   P,SPACES                                                         
         GOTOR PRPTYP              GET PAY TYPE                                 
         MVC   RECPTYP,RI11        PAY TYPE                                     
*                                                                               
         MVC   P,SPACES                                                         
         GOTOR PRUSE               GET USE TYPE                                 
         MVC   RECUTYP,RI11        USE TYPE                                     
         CLI   TGUSEQU,UITN        IF ITN,                                      
         BNE   *+10                                                             
         MVC   RECUNUM,RI11+12     USE NUMBERS OR AUTO                          
*                                                                               
         MVC   P,SPACES                                                         
         GOTOR PRCYC               CYCLE WEEKS                                  
         MVC   RECCWKS,RI11        CYCLE WEEKS                                  
*                                                                               
         MVC   P,SPACES                                                         
         GOTOR PRCST               CYCLE START AND END                          
         MVC   RECCSTE,RI11        CYCLE START AND END                          
*                                                                               
         MVC   P,SPACES                                                         
         GOTOR PRMAIN              MAIN USES                                    
         MVC   RECMUSE,RI11        MAIN USES                                    
*                                                                               
         MVC   P,SPACES                                                         
         GOTOR PRLUSE              LIFT USES                                    
         MVC   RECLUSE,RI11        LIFT USES                                    
*                                                                               
         MVC   P,SPACES                                                         
         GOTOR PRMAJ               MAJORS/UNITS/INSERTS/TAGS/DEMOS              
         CLC   RI11,SPACES                                                      
         BNH   *+10                IS THERE A MAJOR/UNIT, ETC?                  
         MVC   RECLBL,R11          LABEL                                        
         MVC   RECMAJ,RI11                                                      
*                                                                               
         MVC   P,SPACES                                                         
         GOTOR PRMAJUP             UPGRADE MAJORS/UNITS/INSERTS                 
         MVC   RECUMAJ,RI11  UPGRADE                                            
*                                                                               
         MVC   P,SPACES                                                         
         GOTOR PRPHOTO             PHOTOGRAPHER (PRINT ONLY)                    
         MVC   RECPHOT,RI11        PHOTOGRAPHER                                 
*                                                                               
         MVC   P,SPACES                                                         
         GOTOR PRAREA              AREA (PRINT ONLY)                            
         MVC   RECAREA,RI11        AREA                                         
*                                                                               
         MVC   P,SPACES                                                         
         GOTOR PRPUSE              USE (PRINT ONLY)                             
         MVC   RECUSE,RI11         USE                                          
*                                                                               
         OC    TBUNI1,TBUNI1                                                    
         BZ    *+10                                                             
         MVC   RECUNI1,TBUNI1      UNION1                                       
         OC    TBUNI2,TBUNI2                                                    
         BZ    *+10                                                             
         MVC   RECUNI2,TBUNI2      UNION2                                       
         OC    TBUNI3,TBUNI3                                                    
         BZ    *+10                                                             
         MVC   RECUNI3,TBUNI3      UNION3                                       
         GOTOR PUTTAPE             PUT RECORD TO TAPE                           
*                                                                               
         DROP  R2,R3                                                            
         XIT1                                                                   
*                                                                               
         LTORG                                                                  
         EJECT                                                                  
*                                                                               
*        PUT MORE HEADLINES IN DATASET                                          
*                                                                               
PUTCHD   NTR1  BASE=*,LABEL=*                                                   
         GOTOR CLRREC              PUT SPACES IN TAPEREC                        
*                                                                               
         LA    R3,TAPEREC                                                       
         USING RECD,R3                                                          
         MVI   RECTYP,RECTYPC    MORE HEADINGS RECORD                           
         OC    TBFDAT,TBFDAT                                                    
         BZ    PCHD10                                                           
         CLI   TBPRTINV,C'Y'     PRINT INVOICE?                                 
         BNE   PCHD05                                                           
         GOTO1 DATCON,DMCB,(1,TBFDAT),(5,RECSDAT)   SHOOT DATE                  
         MVC   RECSSTU,TBFSTUD   SHOOOT STUDIO                                  
         MVC   RECSCTY,TBFCITY   SHOOT CITY                                     
         B     PCHD10                                                           
*                                                                               
PCHD05   DS    0H                                                               
         GOTO1 DATCON,DMCB,(1,TBFDAT),(5,RECFDAT)   FILM DATE                   
         MVC   RECFSTU,TBFSTUD   FILM STUDIO                                    
         MVC   RECFCTY,TBFCITY   FILM CITY                                      
*                                                                               
PCHD10   OC    TBRDAT,TBRDAT                                                    
         BZ    PCHD20                                                           
         GOTO1 DATCON,DMCB,(1,TBRDAT),(5,RECRDAT)   RECORDING DATE              
         MVC   RECRSTU,TBRSTUD   REC. STUDIO                                    
         MVC   RECRCTY,TBRCITY   REC. CITY                                      
*                                                                               
PCHD20   OC    TBMDAT,TBMDAT                                                    
         BZ    PCHD30                                                           
         GOTO1 DATCON,DMCB,(1,TBMDAT),(5,RECMDAT)   MUSIC DATE                  
         MVC   RECMSTU,TBMSTUD   MUSIC STUDIO                                   
         MVC   RECMCTY,TBMCITY   MUSIC CITY                                     
*                                                                               
PCHD30   CLC   TBCONT,SPACES                                                    
         BNH   *+16                                                             
         MVC   RECCONT,TBCONT    CONTRACTS 1,2,3,4                              
         OC    RECCONT,SPACES                                                   
*                                                                               
         CLC   TBCOM,SPACES                                                     
         BNH   *+10                                                             
         MVC   RECCOM1,TBCOM     COMMENT 1                                      
         CLC   TBCOM2,SPACES                                                    
         BNH   *+10                                                             
         MVC   RECCOM2,TBCOM2    COMMENT 2                                      
*                                                                               
         MVC   RECEMP,TBEMNAME   EMPLOYER OF RECORD                             
         MVC   RECTPC,TBTPC      TPC USER ID                                    
*                                                                               
         GOTO1 CTYPVAL,DMCB,TBCOTYPE                                            
         BNE   *+10                                                             
         MVC   RECCTYP,TGCTNAME  COMM'L TYPE NAME                               
         DROP  R3                                                               
*                                                                               
         GOTOR PUTTAPE           PUT RECORD TO TAPE                             
         XIT1                                                                   
*                                                                               
         LTORG                                                                  
         EJECT                                                                  
*                                                                               
*        PUT PERFORMER DETAILS TO DATASET                                       
*                                                                               
PUTPERF  NTR1  BASE=*,LABEL=*                                                   
         GOTOR CLRREC              PUT SPACES IN TAPEREC                        
*                                                                               
         LA    R3,TAPEREC                                                       
         USING RECD,R3                                                          
         MVI   RECTYP,RECTYPP     PERFORMER RECORD                              
         MVC   RECPER1,P+1        STORE PRINT LINE IN TAPEREC                   
         MVC   RECPER2,P2         STORE SECOND PRINT LINE                       
*                                                                               
         CLI   TBPRTINV,C'Y'      FOR PRINT INVOICES                            
         BNE   PPERF10                                                          
         MVI   RECTYP,RECTYPR     PRINT PERFORMER RECORD                        
         LA    R2,P                                                             
         USING P4LINED,R2                                                       
         MVC   RECPPRNT,SHDTE     PUT PRINT DETAILS AFTER PERF DETAILS          
         DROP  R2,R3                                                            
*                                                                               
PPERF10  LA    RE,480             CHANGE ANY ZEROS TO SPACES                    
         LA    R3,TAPEREC                                                       
PPERF20  CLI   0(R3),0                                                          
         BNE   *+8                                                              
         MVI   0(R3),C' '                                                       
         LA    R3,1(R3)                                                         
         BCT   RE,PPERF20                                                       
*                                                                               
         LA    R3,TAPEREC                                                       
         GOTOR PUTTAPE            PUT RECORD TO TAPE                            
         MVC   P,SPACES           CLEAR PRINT LINE                              
         MVC   P2,SPACES                                                        
         XIT1                                                                   
         LTORG                                                                  
         EJECT                                                                  
*                                                                               
*        GET TOTALS FOR AGROUP EMS                                              
*                                                                               
GETTOTS  NTR1  BASE=*,LABEL=*                                                   
         GOTOR CLRREC              PUT SPACES IN TAPEREC                        
*                                                                               
         LA    R3,TAPEREC                                                       
         USING RECD,R3                                                          
         MVI   RECTYP,RECTYPT    TOTALS RECORD                                  
*                                                                               
         LA    R2,P                                                             
         USING P5LINED,R2                                                       
         MVC   RECGROS,P5GROSS                                                  
         MVC   RECMISC,P5MISC                                                   
         MVC   RECPNH,P5PNH                                                     
         MVC   RECHNW,P5HNW                                                     
         DROP  R2,R3                                                            
         XIT1                                                                   
         LTORG                                                                  
         EJECT                                                                  
*                                                                               
*        PUT INVOICE TOTALS TO DATASET                                          
*                                                                               
PUTTOTS  NTR1  BASE=*,LABEL=*                                                   
         USING RECD,R3                                                          
         LA    R3,TAPEREC                                                       
*                                                                               
         CLI   RECNUM,AC        IF AGENCY COPY, GET TOTALS FROM                 
         BE    PTOT10           NEW BILLING DETAILS ELEMENT                     
*                                                                               
         LA    R4,BLOCK                                                         
         MVC   RECWAGM(88),BLOCK+(RECWAGM-RECTYP)                               
         B     PTOTX                                                            
*                                                                               
PTOT10   L     R1,TOTGRS                                                        
         A     R1,TOTMISC                                                       
         EDIT  (R1),RECWAGM,2,FLOAT=-,ZERO=NOBLANK     WAGES AND MISC           
*                                                                               
         L     R1,TBPNH1                                                        
         A     R1,TBPNH2                                                        
         A     R1,TBPNH3                                                        
         A     R1,TBPNH4                                                        
         EDIT  (R1),RECPNHC,2,FLOAT=-,ZERO=NOBLANK     P&H                      
*                                                                               
         L     R4,AIO                                                           
         MVI   ELCODE,TABDELQ2                                                  
         BRAS  RE,GETEL                                                         
         BNE   PTOTX                                                            
         USING TABDD,R4                                                         
         EDIT  TABDTAX,RECPTAX,2,FLOAT=-,ZERO=NOBLANK  PAYROLL TAXES            
         EDIT  TABDHND,RECHAND,2,FLOAT=-,ZERO=NOBLANK  HANDLING                 
         EDIT  TABDACOM,RECEMS,2,FLOAT=-,ZERO=NOBLANK  EMS FEE                  
         EDIT  TABDTOT,RECITOT,2,FLOAT=-,ZERO=NOBLANK  TOTAL                    
         L     RF,TABDGST                                                       
         ICM   R1,15,TABDPST                                                    
         AR    RF,R1                                                            
         EDIT  (RF),RECUGST,2,FLOAT=-,ZERO=NOBLANK  GST                         
*        EDIT  TABDGST,RECUGST,2,FLOAT=-,ZERO=NOBLANK  GST                      
         EDIT  TABDCSF,RECCSF,2,FLOAT=-,ZERO=NOBLANK   CSF                      
         DROP  R4                                                               
*                                                                               
PTOTX    GOTOR PUTTAPE           PUT RECORD TO TAPE                             
         XIT1                                                                   
         LTORG                                                                  
         EJECT                                                                  
*                                                                               
*        PUT CLASS A INFO TO DATASET                                            
*                                                                               
PUTCLA   NTR1  BASE=*,LABEL=*                                                   
         GOTOR CLRREC              PUT SPACES IN TAPEREC                        
*                                                                               
         LA    R3,TAPEREC                                                       
         USING RECD,R3                                                          
         MVI   RECTYP,RECTYPA      CLASS A RECORD                               
         LA    R2,P                                                             
         USING P8LINED,R2                                                       
         MVC   RECCLA,P8INFO1                                                   
         GOTOR PUTTAPE             PUT RECORD TO TAPE                           
         MVC   P,SPACES                                                         
         DROP  R2,R3                                                            
         XIT1                                                                   
         LTORG                                                                  
         EJECT                                                                  
*                                                                               
*        PUT 1 LINE OF COMMENT TO DATASET                                       
*                                                                               
PUTCMT   NTR1  BASE=*,LABEL=*                                                   
         GOTOR CLRREC              PUT SPACES IN TAPEREC                        
*                                                                               
         LA    R3,TAPEREC                                                       
         USING RECD,R3                                                          
         MVI   RECTYP,RECTYPM      COMMENT TYPE RECORD                          
         MVC   RECCMT,P+3                                                       
         GOTOR PUTTAPE             PUT RECORD TO TAPE                           
         MVC   P,SPACES                                                         
         DROP  R3                                                               
         XIT1                                                                   
         LTORG                                                                  
         EJECT                                                                  
*                                                                               
*        KEEP TRACK OF TOTAL HNW WHEN UNIONLOCAL HAS AN HNW FUND                
*                                                                               
         USING TABCHKD,R3                                                       
TOTHNW   NTR1  BASE=*,LABEL=*                                                   
         XC    KEY,KEY                                                          
         LA    R4,KEY                                                           
         USING TLLOD,R4              GET UNION LOCAL RECORD                     
         MVI   TLLOCD,TLLOCDQ                                                   
         MVC   TLLOUN,TABUNI         UNION                                      
         MVC   TLLOLCL,TABLOCL       LOCAL                                      
         GOTO1 HIGH                                                             
         CLC   KEY(L'TLLOKEY),KEYSAVE                                           
         BNE   THNWX                                                            
         MVC   AIO,AIO2                                                         
         GOTO1 GETREC                                                           
*                                                                               
         L     R4,AIO                GET LOCAL ELEMENT                          
         USING TALOD,R4                                                         
         MVI   ELCODE,TALOELQ                                                   
         BRAS  RE,GETEL                                                         
         BNE   THNWX                                                            
         TM    TALOSTAT,TALOSHNW     IF THERE IS AN H&W FUND,                   
         BNO   THNWX                                                            
         L     R1,HNWFUND            ADD H&W AMOUNT TO TOTAL H&W THAT           
         A     R1,TABHNW             WE WILL DEDUCT FROM EMS HANDLING           
         ST    R1,HNWFUND                                                       
THNWX    MVC   AIO,AIO1                                                         
         XIT1                                                                   
         DROP  R3,R4                                                            
         LTORG                                                                  
         EJECT                                                                  
*                                                                               
*        PUT COLUMN HEADINGS FOR OT&T, P&H, H&W                                 
*        R3--> TAPEREC                                                          
*                                                                               
         USING RECD,R3                                                          
PUTHEAD  NTR1  BASE=*,LABEL=*                                                   
         LA    R2,BLOCK         HEADING FOR OT&TRAVEL/HR.MN/USE RANGE           
         USING P4LINED,R2                                                       
         MVI   RECHOTT,C'U'                                                     
         CLC   =CL5'  Use',VARCOL      USE RANGE                                
         BE    PHD10                                                            
         MVI   RECHOTT,C'H'                                                     
         CLC   =CL3'Hrs',VARCOL        HRS. AND MINS./TAGS                      
         BE    PHD10                                                            
         MVI   RECHOTT,C'O'                                                     
         CLC   =CL2'OT',VARCOL         OT AND TRAVEL HR.MN./DAYS                
         BE    PHD10                                                            
         MVI   RECHOTT,C'M'                                                     
         CLC   =CL5'  Maj',VARCOL      MAJORS AND UNITS                         
         BE    PHD10                                                            
         MVI   RECHOTT,C' '                                                     
*                                                                               
PHD10    LA    RE,PNHTAB               HEADING FOR P&H COLUMN                   
PHD30    CLI   0(RE),X'FF'             CAN BE P&H, H&W, MISC, MISC/IR,          
         BE    PHD50                   I&R, OR NOTHING                          
         CLC   0(3,RE),PH                                                       
         BE    PHD40                                                            
         LA    RE,L'PNHTAB(RE)                                                  
         B     PHD30                                                            
PHD40    MVC   RECHPNH,3(RE)                                                    
*                                                                               
PHD50    LA    RE,PNHTAB              HEADING FOR H&W COLUMN                    
PHD60    CLI   0(RE),X'FF'                                                      
         BE    PHDX                                                             
         CLC   0(3,RE),HW                                                       
         BE    PHD70                                                            
         LA    RE,L'PNHTAB(RE)                                                  
         B     PHD60                                                            
PHD70    MVC   RECHHNW,3(RE)                                                    
PHDX     XIT1                                                                   
         DROP  R2,R3                                                            
*                                                                               
PNHTAB   DS    0CL4                                                             
         DC    CL3' P ',C'P'                                                    
         DC    CL3' H ',C'H'                                                    
         DC    CL3'  M',C'M'                                                    
         DC    CL3'Mis',C'B'                                                    
         DC    CL3' I ',C'I'                                                    
         DC    X'FF'                                                            
*                                                                               
         LTORG                                                                  
         EJECT                                                                  
*======================================================================         
*        PUT RECORD TO TAPE                                                     
*======================================================================         
PUTTAPE  NTR1 BASE=*,LABEL=*                                                    
         CLI   WHEN,X'20'        IF SOON, DON'T WRITE TO TAPE                   
         BE    PTAPEX                                                           
         LA    R2,BIDISK                                                        
         PUT   (R2),(R3)         PUT IT TO TAPE/DATASET                         
PTAPEX   XIT1                                                                   
         LTORG                                                                  
         EJECT                                                                  
*======================================================================         
*        OPEN TAPE                                                              
*======================================================================         
OPENTAPE NTR1  BASE=*,LABEL=*                                                   
         OI    STAT2,STOPEN      SET STATUS FOR DATASET OPEN                    
         CLI   WHEN,X'20'        IF SOON, DON'T WRITE TO TAPE                   
         BE    OPENTX                                                           
         L     RE,ATWA                                                          
         USING CONHEADH-64,RE                                                   
         L     R1,TWADCONS                                                      
         USING TWADCOND,R1                                                      
         MVC   DYNALLOC,TDYNALLO                                                
         CLC   TWAAGY,=CL2'D2'  IF AGENCY DPS2 CREATE DIFF. DATASET             
         BNE   OPNTAP10                                                         
         DROP  R1,RE                                                            
         MVC   WORK(20),=CL20'TALDISK.TA0EMS2'     TEST SYSTEM  BILLING         
         CLI   RECNUM,BI                                                        
         BE    OPNTAP20                                                         
         MVC   WORK(20),=CL20'TALDISK.TA0EMS3'     TEST SYSTEM  ACOPY           
         B     OPNTAP20                                                         
OPNTAP10 MVC   WORK(20),=CL20'TALDISK.TA0EMSBI'    BILLING                      
         CLI   RECNUM,BI                                                        
         BE    *+10                                                             
         MVC   WORK(20),=CL20'TALDISK.TA0EMSAC'    ACOPY                        
OPNTAP20 GOTO1 DYNALLOC,DMCB,(0,=CL8'BIDISK'),(0,WORK)                          
         LA    R2,BIDISK           OPEN TAPE                                    
         OPEN  ((2),OUTPUT)                                                     
         LTR   RF,RF                                                            
         BZ    *+6                                                              
         DC    H'00'                                                            
*        GOTO1 DATCON,DMCB,(5,0),(11,EMSDATE)                                   
*        MVC   EMSNAME,WORK                                                     
*        GOTO1 DATAMGR,DMCB,=C'OPMSG',(=AL1(EMSFILLN),EMSFILE)                  
*                                                                               
OPENTX   XIT1                                                                   
EMSFILE  DC    C'AUTONOTE*GHOA,MZEI,JBAS,SCHT:'                                 
EMSDATE  DC    C'JAN01/01 EMS FILE CREATED '                                    
EMSNAME  DS    CL20                                                             
EMSFILLN EQU   *-EMSFILE                                                        
         LTORG                                                                  
         EJECT                                                                  
*======================================================================         
*        CLOSE TAPE                                                             
*======================================================================         
CLOSTAPE NTR1  BASE=*,LABEL=*                                                   
         CLI   WHEN,X'20'        IF SOON, DON'T WRITE TO TAPE                   
         BE    CLOSTX                                                           
         LA    R2,BIDISK         CLOSE TAPE                                     
         CLOSE ((2))                                                            
         LTR   RF,RF                                                            
         BZ    *+6                                                              
         DC    H'00'                                                            
CLOSTX   XIT1                                                                   
         LTORG                                                                  
         EJECT                                                                  
*                                                                               
*======================================================================         
*        PUT SPACES IN 480 BYTES OF TAPEREC                                     
*======================================================================         
CLRREC   NTR1  BASE=*,LABEL=*                                                   
         LA    R3,TAPEREC          PUT SPACES IN 480 BYTES                      
         LA    RE,3                                                             
         MVC   0(132,R3),SPACES                                                 
         LA    R3,132(R3)                                                       
         BCT   RE,*-10                                                          
         MVC   0(84,R3),SPACES                                                  
         XIT1                                                                   
         LTORG                                                                  
         EJECT                                                                  
*                                                                               
TAPEREC  DS    CL(RECLNQ)          TAPE RECORD AREA                             
DYNALLOC DS    A                   DYNAMIC ALLOCATION OF DATASET                
*                                                                               
COMPLM2  DC    X'FFFFFFFFFFFF'                                                  
*                                                                               
BIDISK   DCB   DDNAME=BIDISK,DSORG=PS,RECFM=FB,LRECL=480,BLKSIZE=0,    X        
               MACRF=PM                                                         
         EJECT                                                                  
         LTORG                                                                  
         EJECT                                                                  
*                                                                               
*        THIS ROUTINE WILL PRINT OUT THE INVOICES IN ERROR                      
*        AND THE ACCOUNTS OF HOW MANY DIFFERENT INVOICES WERE                   
*        PROCESSED                                                              
*                                                                               
         DS    0D                                                               
PREND    NMOD1 0,*PREN*                                                         
         L     RC,0(R1)                                                         
         USING GEND,RC                                                          
         MVC   P,SPACES            IF EMS BILLING, P MIGHT NOT BE CLEAR         
         MVC   P2,SPACES                                                        
         MVC   P3,SPACES                                                        
         MVC   P4,SPACES                                                        
         LA    R2,P                                                             
         USING P9LINED,R2                                                       
         MVC   P9AGY(5),=C'DATE '                                               
         MVC   P9AGY+6(8),TGTODAY8                                              
         MVI   SPACING,2                                                        
         BRAS  RE,PRTIT                                                         
         MVC   P9AGY(14),DASHES                                                 
         BRAS  RE,PRTIT                                                         
*                                                                               
         L     R3,=V(ERRTAB)       START AT BEGINING OF ERROR TABLE             
         USING ERRINVD,R3                                                       
         LA    R2,P                                                             
         USING P9LINED,R2                                                       
         MVI   SPACING,2                                                        
         BRAS  RE,PRTIT                                                         
*                                                                               
         OC    0(ERRLEN,R3),0(R3)  ANY MORE INVOICES IN ERROR                   
         BZ    PREND20                                                          
         MVC   P9AGY,=C'AGENCY'    HEADLINES                                    
         MVC   P9INV(7),=C'INVOICE'                                             
         MVC   P9ER1(5),=C'ERROR'                                               
         MVC   P9DESC(11),=C'DESCRIPTION'                                       
         BRAS  RE,PRTIT                                                         
*                                                                               
         MVC   P9AGY,DASHES                                                     
         MVC   P9INV(7),DASHES                                                  
         MVC   P9ER1(5),DASHES                                                  
         MVC   P9DESC(11),DASHES                                                
         MVI   SPACING,2                                                        
         BRAS  RE,PRTIT                  LEAVE BLANKS AT TOP OF LINE            
*                                                                               
PREND10  MVC   P9AGY,ERRAGY        AGENCY                                       
         XC    ERRINV,=X'FFFFFFFFFFFF'   COMPLEMENT INV NUM                     
         GOTO1 TINVCON,DMCB,(X'80',ERRINV),P9INV,DATCON                         
         LA    RF,P9ERR                                                         
         ZIC   R1,ERRNUM                                                        
         BAS   RE,EDT3             ERROR NUMBER                                 
*                                                                               
         GOTO1 ERROUT,DMCB,(ERRNUM,P9DESC)                                      
         CLI   ERRNUM,128          MARK ALL WARNINGS                            
         BL    PREND15                                                          
         MVC   P9WARN,=C'**WARNING**'                                           
*                                                                               
PREND15  BRAS  RE,PRTIT                  PRINT THE INVOICE IN ERROR             
         LA    R3,ERRLEN(R3)                                                    
         OC    0(ERRLEN,R3),0(R3)  ANY MORE INVOICES IN ERROR                   
         BNZ   PREND10                                                          
*                                                                               
PREND20  MVI   SPACING,4                                                        
         BRAS  RE,PRTIT                                                         
         DROP  R3                                                               
*                                                                               
         LA    R1,COUNTS           ADDRESS OF COUNTERS                          
         LA    R3,NCOUNTS          NUMBER OF COUNTERS                           
         LA    R4,LTINVTAB                                                      
*                                                                               
PREND30  OI    3(R1),X'0F'         TURN SIGN BIT                                
         UNPK  P+25(7),0(4,R1)     UNPACK THE NUMBER INTO PRINT LINE            
         MVC   P(L'LTINVTAB),0(R4)                                              
         MVI   SPACING,2                                                        
         BRAS  RE,PRTIT                                                         
         LA    R1,L'COUNTS(R1)     BUMP TO NEXT COUNTER                         
         LA    R4,L'LTINVTAB(R4)                                                
         BCT   R3,PREND30                                                       
*                                                                               
         MVI   SPACING,2                                                        
         BRAS  RE,PRTIT                                                         
*                                                                               
         LA    R2,CTAMTS           ADDRESS OF TOTALS                            
         LA    R3,NCTAMTS          NUMBER OF TOTALS                             
         L     R4,=A(LTTOTTAB)                                                  
*                                                                               
PREND40  L     R1,0(R2)                                                         
         LA    RF,P+25             EDIT OUT AMOUNT                              
         BAS   RE,EDT11                                                         
         MVC   P(L'LTINVTAB),0(R4) PUT LABLE                                    
         MVI   SPACING,2                                                        
         BRAS  RE,PRTIT                                                         
         LA    R2,4(R2)            BUMP TO NEXT COUNTER                         
         LA    R4,L'LTINVTAB(R4)                                                
         BCT   R3,PREND40                                                       
*                                                                               
         MVI   FORCEHED,C'Y'                                                    
         L     R4,ALOGOC           A(LOGOC)                                     
         USING LOGOD,R4                                                         
         MVI   LOGOTYPE,C'E'       END TALENT LOGOS                             
         GOTO1 ALOGO,DMCB,(R4)                                                  
*                                                                               
         TM    TBOPTS,TBNOPOST     IF NOT POSTING                               
         BO    PREX                DON'T PUT OUT CONTROL SHEETS                 
         MVI   FORCEHED,C'Y'                                                    
         L     R4,ALOGOC           A(LOGOC)                                     
         USING LOGOD,R4                                                         
         MVI   LOGOTYPE,C'S'       START CONTROL LOGOS                          
         MVC   LOGO1,=C'CONTROL'                                                
         MVC   LOGO2,SPACES                                                     
         MVC   LOGONAME,CNTNAME                                                 
         MVC   LOGOADD,CNTADDR                                                  
         MVC   LOGOADD2,SPACES                                                  
         MVC   LOGOADD3,SPACES                                                  
         GOTO1 ALOGO,DMCB,(R4)                                                  
         MVI   FORCEHED,C'Y'                                                    
         MVI   PGNUM,0                                                          
*                                                                               
         LA    R2,HEAD1                                                         
         MVC   2(6,R2),=C'RUN ON'                                               
         GOTO1 DATCON,DMCB,(5,0),(8,9(R2))                                      
         MVC   50(7,R2),=C'BILLING'                                             
         MVC   90(11,R2),=C'REPORT TABI'                                        
         LA    R2,HEAD2                                                         
         MVC   50(7,R2),DASHES                                                  
         BRAS  RE,PRTIT                                                         
*                                                                               
         TM    TBOPTS,TBPRWK       POSTING ONLY PRODUCTION                      
         BO    PREX                                                             
         MVC   P+2(19),=C'TOTAL POSTINGS FOR '                                  
         MVC   P+22(6),TPUSER                                                   
         L     R1,CTRCVBL          TOTAL RECEIVABLE AMOUNT                      
         LA    RF,P+30             EDIT OUT AMOUNT                              
         BAS   RE,EDT11                                                         
         MVI   SPACING,2                                                        
         BRAS  RE,PRTIT                                                         
*                                                                               
PREX     XIT1                                                                   
         DROP  R2,R4                                                            
         EJECT                                                                  
*                                                                               
*        EDIT A LINE - INPUT (R1)                                               
*                      OUTPUT LENGTH = 3                                        
*                      OUTPUT ADDR   = RF                                       
*                                                                               
         SPACE 1                                                                
EDT3     EDIT  (R1),(3,(RF)),ZERO=BLANK                                         
         BR    RE                                                               
         SPACE 2                                                                
*                                                                               
*        EDIT A LINE - INPUT (R1)                                               
*                      OUTPUT LENGTH = 11                                       
*                      OUTPUT ADDR   = RF                                       
*                                                                               
         SPACE 1                                                                
EDT11    LTR   R1,R1               DON'T EDIT 0                                 
         BZR   RE                                                               
         EDIT  (R1),(11,(RF)),2,FLOAT=-,ZERO=BLANK                              
         BR    RE                                                               
         SPACE 2                                                                
         DROP  RB                                                               
         EJECT                                                                  
         SPACE 1                                                                
CNTNAME  DC    CL33'******** INTERNAL CONTROL *******'                          
CNTADDR  DC    CL33'******** DO NOT SEND OUT  *******'                          
         SPACE 1                                                                
DASHES   DC    14C'-'                                                           
         SPACE 1                                                                
*                                                                               
*        TABLE OF COUNTERS                                                      
*                                                                               
LTINVTAB DS    0CL21                                                            
         DC    C'Regular Invoices     '                                         
         DC    C'PUR Invoices posted  '                                         
         DC    C'BNP Invoices         '                                         
         DC    C'PUR Invoices printed '                                         
         DC    C'Urgent Invoices      '                                         
         DC    C'Not Yet Paid Invoices'                                         
         DC    C'Invoices In Error    '                                         
*                                                                               
LTTOTTAB DS    0CL21                                                            
         DC    C'Gross Wages          '                                         
         DC    C'Misc. Payments       '                                         
         DC    C'Total                '                                         
         DC    C'Total Receivables    '                                         
         LTORG                                                                  
         EJECT                                                                  
*                                                                               
*        PRINT THE HEADLINES                                                    
*                                                                               
         DS    0D                                                               
PRHEAD   NTR1  BASE=*,LABEL=*                                                   
*        L     RC,0(R1)                                                         
*        USING GEND,RC                                                          
         ZIC   R1,PGNUM            INCREMENT PAGE NUMBER                        
         LA    R1,1(R1)                                                         
         STC   R1,PGNUM                                                         
         MVI   PRATTN,C'N'         SET FLAG - ATTN NAME NOT YET PRINTED         
         MVI   DONE,C'N'           DEFAULT - MORE RHS TO PRINT                  
         MVI   INV,C'N'            RESET FLAGS FOR PAGE                         
         MVC   INV+1(FLAGX-INV-3),INV                                           
*                                                                               
         LA    R2,P                POINT TO PRINT LINE                          
         BAS   RE,PR1HEAD                                                       
         BAS   RE,PR2HEAD                                                       
         B     PRXIT                                                            
         EJECT                                                                  
*        PRINT HEADLINES                                                        
*                                                                               
PR1HEAD  NTR1                                                                   
         USING P1LINED,R2                                                       
         CLC   =C'DM ',TGEMP       IF EMPLOYER IS DM OR PP                      
         BE    PR1H0                                                            
         CLC   =C'PP ',TGEMP                                                    
         BNE   PR1H2                                                            
         CLI   TBTPO,C'O'          OR OFFICE O                                  
         BE    PR1H2                                                            
         CLI   TBTPO,C'Q'          OR OFFICE Q                                  
         BE    PR1H2                                                            
*                                                                               
PR1H0    MVC   LI11(L'TBEMNAME),TBEMNAME     EMPLOYER NAME                      
         CLC   =C'DM ',TGEMP       IF EMPLOYER DM                               
         BNE   PR1H1                                                            
         CLC   TGAGY,=CL6'0601'    AND IN AGENCY RANGE                          
         BL    PR1H1                                                            
         CLC   TGAGY,=CL6'0699'                                                 
         BH    PR1H1                                                            
         MVC   LI11(L'LTDMBB),LTDMBB  SET D'ARCY MASIUS BENTON & BOWLES         
PR1H1    BRAS  RE,PRTIT                                                         
*                                                                               
         MVC   LI11(L'TBEMADDR),TBEMADDR     AND ADDRESS                        
         BAS   RE,PRIGHT                                                        
***      BRAS  RE,PRTIT                                                         
         B     PR1H3                                                            
*                                                                               
PR1H2    MVC   LI11(L'TBOFNAME),TBOFNAME     ELSE TP OFFICE NAME                
         BRAS  RE,PRTIT                                                         
         MVC   LI11(L'TBOFADDR),TBOFADDR          AND ADDRESS                   
         BAS   RE,PRIGHT                                                        
*                                                                               
PR1H3    MVC   AIO,AEMPIO       SEARCH EMPLOYER RECORD FOR FDID#                
         MVI   ELCODE,TATIELQ                                                   
         MVI   FULL,TATITYUN                                                    
         MVC   FULL+1(3),=C'FD '                                                
         GOTO1 GETL,DMCB,(4,FULL)                                               
         MVC   AIO,AIO1                                                         
         BNE   PR1H4                                                            
         L     R4,TGELEM                                                        
         USING TATID,R4                                                         
         MVC   C11(L'LTTFDID#),LTTFDID#    TP FED ID                            
         CLC   =C'PP ',TGEMP                                                    
         BNE   *+10                                                             
         MVC   C11(L'LTTFDID#),LTPFDID#    PP FED ID                            
         MVC   CI11(L'TATIID),TATIID                                            
         DROP  R4                                                               
PR1H4    BRAS  RE,PRTIT                                                         
*                                                                               
         MVC   LI11(L'TBAYNAME),TBAYNAME  AGENCY NAME                           
         MVC   C11(18),LTEMP                                                    
         MVC   CI11(17),TBEMNAME EMPLOYER OF RECORD (DON'T GO INTO BOX)         
         BAS   RE,PRIGHT                                                        
*                                                                               
         TM    TBISTAT,TAPDSCAN           IF CANADIAN -PRINT GST NUMBER         
         BNO   PR1H5                                                            
         MVC   C11(L'LTGST#),LTGST#                                             
         MVC   CI11(L'TBEMGST#),TBEMGST#                                        
*                                                                               
PR1H5    MVC   L11(11),LTINV                                                    
         MVC   LI11(ADDLINE),TBADD1  ADDRESS                                    
         BAS   RE,PRIGHT                                                        
*                                                                               
         CLC   TBADD2,SPACES    IF THERE'S A 2ND ADDDR LINE                     
         BH    PR1H10              PRINT IT                                     
         CLI   PRATTN,C'Y'      SET FLAG - NAME PRINTED                         
         BE    PR1H20                                                           
         BAS   RE,CKATTN        ELSE CHECK IF ATTN NAME                         
         B     PR1H20                                                           
*                                                                               
PR1H10   MVC   LI11(ADDLINE),TBADD2       ADDRESS - 2ND LINE                    
*                                                                               
PR1H20   MVC   C11(15),LTINT                                                    
         MVC   CI11(L'TGCLI),TGCLI        CLIENT CODE                           
         LA    R1,CI11+L'CI11             END OF DATA                           
         BAS   RE,SHUFFLE                 FIND 1ST NON BLANK                    
         MVC   0(L'TBPRD,R1),TBPRD        PRODUCT CODE                          
         BAS   RE,PRIGHT                                                        
*                                                                               
         CLC   TBADD3,SPACES    IF THERE'S A 3RD ADDDR LINE                     
         BH    PR1H30              PRINT IT                                     
         CLI   PRATTN,C'Y'      ELSE WAS ATTN NAME PRINTED ALREADY              
         BE    PR1H40                                                           
         BAS   RE,CKATTN        NO - CHECK IF ATTN NAME EXISTS                  
         B     PR1H40                                                           
*                                                                               
PR1H30   MVC   LI11(ADDLINE),TBADD3       AGENCY ADDRESS - 3RD LINE             
*                                                                               
PR1H40   CLC   TBQC,TBTPC                 IF PAYER = QC THEN ONLY               
         BNE   PR1H50                                                           
         MVC   CI11(L'TBQC),TBQC          PRINT NAME 1 TIME                     
         LA    R1,CI11+L'TBQC+1           SET R1 - WHERE TO PRINT               
         B     PR1H60                                                           
*                                                                               
PR1H50   MVC   CI11(4),TBTPC              TALENT PARTNERS PAYER                 
         MVC   CI11+5(4),TBQC             QUALITY CONTROL                       
         LA    R1,CI11+10                 SET R1 - WHERE TO PRINT               
*                                                                               
PR1H60   GOTOR RUNTYPE             PRINT OUT RUN INFORMATION                    
         BAS   RE,PRIGHT                                                        
*                                                                               
PR1H65   CLC   TBADD4,SPACES       IF THERE'S A 4TH ADDDR LINE                  
         BNH   PR1H70              PRINT IT                                     
         MVC   LI11(ADDLINE),TBADD4      ADDRESS - 4TH LINE                     
         BAS   RE,PRIGHT                                                        
*                                                                               
PR1H70   CLI   PRATTN,C'Y'      WAS ATTN NAME PRINTED ALREADY                   
         BE    PR1H80                                                           
         BAS   RE,CKATTN        NO - CHECK IF ATTN NAME EXISTS                  
         BAS   RE,PRIGHT                                                        
*                                                                               
PR1H80   GOTO1 CTYPVAL,DMCB,TBCOTYPE      IS THERE A COMM'L TYPE?               
         BNE   PR1H85                                                           
         MVC   C11(15),LTCTYP             COMM'L TYPE                           
         MVC   CI11(7),TGCTNAME           COMM'L TYPE NAME                      
*        CLI   TBCOTYPE,CTYICAT1                                                
*        BNE   *+10                                                             
*        MVC   CI11(13),=C'INDUSTR CAT 1'                                       
*        CLI   TBCOTYPE,CTYICAT2                                                
*        BNE   PR1H85                                                           
*        MVC   CI11(13),=C'INDUSTR CAT 2'                                       
*                                                                               
PR1H85   CLI   TBMED,TACOMEDE  IF MEDIA=EVENT,                                  
         BNE   PR1H88                                                           
         CLI   CWS,C'Y'        WAS PERIOD PRINTED?                              
         BE    PR1H88                                                           
         BAS   RE,PRIGHT       NO, PRINT IT                                     
         B     *+8                                                              
*                                                                               
PR1H88   BRAS  RE,PRTIT        IF NO COMM'L TYPE, PRINT BLANK LINE              
*                                                                               
         MVC   L11(6),LTCLI               CLIENT'S NAME                         
         MVC   LI11(L'TBCLNAME),TBCLNAME                                        
         CLI   TBMED,TACOMEDE       IF MEDIA = EVENT, SKIP                      
         BE    PR1H90                                                           
         OC    TBAIR,TBAIR                FIRST AIR DATE                        
         BZ    PR1H90                                                           
         MVC   C11(14),LTAIR                                                    
         GOTO1 DATCON,DMCB,(1,TBAIR),(5,CI11)                                   
*                                                                               
PR1H90   BAS   RE,PRIGHT           MUST PRINT CLIENT NAME                       
         CLI   TBMED,TACOMEDE  IF MEDIA = EVENT,                                
         BNE   PR1H95                                                           
         CLC   TBPRNAME,SPACES     AND NO PRODUCT,                              
         BNH   PR1H98              PRINT BLANK LINE                             
PR1H95   CLC   TBPRNAME,SPACES            PRODUCT NAME                          
         BNH   PR1H100                                                          
         MVC   L11(7),LTPRD               PRODUCT'S NAME                        
         CLI   TBMED,TACOMEDE  IF MEDIA = EVENT,                                
         BNE   *+10                                                             
         MVC   L11(7),LTPRDRG   PRODUCT/REGION                                  
         MVC   LI11(L'TBPRNAME),TBPRNAME                                        
PR1H98   MVI   DONE,C'N'           FAKE OUT TO FORCE PRINT                      
*                                                                               
PR1H100  OC    TBFCYC,TBFCYC              FIRST FIXED CYCLE                     
         BZ    PR1H110                                                          
         CLI   TBMED,TACOMEDE      IF MEDIA = EVENT, SKIP                       
         BE    PR1H110                                                          
         MVC   C11(17),LTFIX                                                    
         TM    TBAYSTA5,TAAYSOTH   DON'T SHOW THESE?                            
         BO    PR1H120                                                          
         GOTO1 DATCON,DMCB,(1,TBFCYC),(5,CI11)                                  
         B     PR1H120                                                          
*                                                                               
PR1H110  CLI   DONE,C'Y'           DON'T BOTHER IF NOTHING                      
         BE    PR1H130                   LEFT TO PRINT                          
*                                                                               
PR1H120  BAS   RE,PRIGHT                                                        
*                                                                               
PR1H130  MVC   L11(5),LTTITLE      PRINT TITLE                                  
         CLI   TBPRTINV,C'Y'                                                    
         BE    *+10                                                             
         MVC   L11(10),LTCOM              COMMERCIAL'S TITLE                    
         CLI   TBMED,TACOMEDE      IF MEDIA = EVENT                             
         BNE   *+10                                                             
         MVC   L11(10),LTEVTNM     EVENT NAME                                   
         MVC   LI11(L'TBCONAME),TBCONAME                                        
*                                                                               
         CLI   TBMED,TACOMEDE      IF MEDIA = EVENT, SKIP                       
         BE    PR1H140                                                          
         BRAS  RE,CHKEXP           PRINT EXPIRY OR ACTRA EXPIRY DATE?           
         BNE   PR1H135                                                          
         OC    TBACTEXP,TBACTEXP          ACTRA EXPIRY DATE                     
         BZ    PR1H135                                                          
         MVC   C11(15),LTCEXP                                                   
         GOTO1 DATCON,DMCB,(1,TBACTEXP),(5,CI11)                                
         B     PR1H140                                                          
PR1H135  OC    TBCOEXP,TBCOEXP            EXP DATE                              
         BZ    PR1H140                                                          
         MVC   C11(15),LTCEXP                                                   
         GOTO1 DATCON,DMCB,(1,TBCOEXP),(5,CI11)                                 
*                                                                               
PR1H140  BAS   RE,PRIGHT                                                        
*                                                                               
         MVC   L11(8),LTPRTID             PRINT ID                              
         CLI   TBMED,TACOMEDE      IF MEDIA = EVENT                             
         BNE   PR1H141                                                          
         MVC   L11(8),LTEVTID             EVENT ID                              
         B     PR1H141A                                                         
PR1H141  CLI   TBPRTINV,C'Y'                                                    
         BE    *+10                                                             
         MVC   L11(12),LTISC              COMMERCIAL'S ID                       
PR1H141A MVC   LI11(L'TGCID),TGCID                                              
*                                                                               
         CLI   TBMED,TACOMEDE      IF MEDIA = EVENT, SKIP                       
         BE    PR1H150                                                          
         OC    TBCID2,TBCID2       IF HAVE 2ND CID                              
         BNZ   PR1H142                                                          
         TM    STAT2,STAVERS       IF HAVE VERSION CODE                         
         BZ    PR1H145                                                          
PR1H142  LA    RE,LI11+L'TGCID-1   START AT END OF FIRST CID                    
         CLI   0(RE),C' '                                                       
         BNE   *+8                                                              
         BCT   RE,*-8              KEEP GOING BACK TILL FIRST NON-SPACE         
*                                                                               
         LA    RE,2(RE)               SKIP A SPACE                              
         TM    STAT2,STAVERS          IF HAVE VERSION CODE                      
         BZ    PR1H143                                                          
         EDIT  TBVERS,(3,0(RE)),ALIGN=LEFT                                      
         MVC   C11(L'LTVER),LTVER     AND VERSION ID                            
         MVC   CI11(L'TBVERSID),TBVERSID                                        
         B     PR1H150                                                          
PR1H143  MVC   0(3,RE),=C'AND'        ELSE                                      
         LA    RE,4(RE)                                                         
         MVC   0(L'TBCID2,RE),TBCID2  DISPLAY 2ND CID                           
*                                                                               
PR1H145  CLC   TBLID,SPACES                                                     
         BNH   PR1H150                                                          
         MVC   C11(11),LTLID              LIFT NUMBER                           
         MVC   CI11(L'TBLID),TBLID                                              
*                                                                               
PR1H150  BAS   RE,PRIGHT                                                        
         CLI   TBMED,TACOMEDE      IF MEDIA = EVENT, SKIP                       
         BE    PRXIT                                                            
         BAS   RE,PRIGHT           LEAVE A BLANK LINE ON LHS                    
         B     PRXIT                                                            
         DROP  R2                                                               
         EJECT                                                                  
*                                                                               
*        NEW LINE LAYOUT                                                        
*                                                                               
PR2HEAD  NTR1                                                                   
         SPACE 1                                                                
         USING P2LINED,R2                                                       
         MVI   FLAG,C'N'           DEFAULT NO FILM/RECD/MUS INFO                
         CLI   TBMED,TACOMEDE      IF MEDIA = EVENT, SKIP                       
         BE    PR2H90                                                           
         OC    TBFDAT,TBFDAT              FILM DATE                             
         BZ    PR2H10                                                           
         MVI   FLAG,C'Y'                                                        
         MVC   L12(9),LTFDT                                                     
         MVC   L22(11),LTFST       IF DATE -MUST HAVE STUDIO                    
         MVC   L32(9),LTFCT        AND CITY                                     
         CLI   TBPRTINV,C'Y'       PRINT INVOICES                               
         BNE   PR2H05                                                           
         MVC   L12(10),LTSDT       SHOOT INFO LABLES                            
         MVC   L22(12),LTSST                                                    
         MVC   L32(10),LTSCT       SHOOT INFO IS ORG'ED OVER FILM INFO          
*                                                                               
PR2H05   TM    TBAYSTA5,TAAYSOTH                                                
         BO    PR2H06                                                           
         GOTO1 DATCON,DMCB,(1,TBFDAT),(5,LI12)                                  
         MVC   LI22(L'TBFSTUD),TBFSTUD                                          
         MVC   LI32(L'TBFCITY),TBFCITY                                          
         GOTOR PRFRMSTA,DMCB,TBFSTAT                                            
PR2H06   BAS   RE,PRIGHT                                                        
*                                                                               
PR2H10   OC    TBRDAT,TBRDAT              RECORDING DATE                        
         BZ    PR2H20                                                           
         MVI   FLAG,C'Y'                                                        
         MVC   L12(11),LTRDT                                                    
         MVC   L22(13),LTRST              IF DATE-MUST HAVE STUDIO              
         MVC   L32(11),LTRCT                                                    
         TM    TBAYSTA5,TAAYSOTH                                                
         BO    PR2H13                                                           
         GOTO1 DATCON,DMCB,(1,TBRDAT),(5,LI12)                                  
         MVC   LI22(L'TBRSTUD),TBRSTUD       AND CITY                           
         MVC   LI32(L'TBRCITY),TBRCITY                                          
         GOTOR PRFRMSTA,DMCB,TBRSTAT                                            
PR2H13   BAS   RE,PRIGHT                                                        
*                                                                               
PR2H20   OC    TBMDAT,TBMDAT              MUSIC DATE                            
         BZ    PR2H30                                                           
         MVI   FLAG,C'Y'                                                        
         MVC   L12(10),LTMDT                                                    
         MVC   L22(12),LTMST              IF DATE-MUST HAVE STUDIO              
         MVC   L32(10),LTMCT                                                    
         TM    TBAYSTA5,TAAYSOTH                                                
         BO    PR2H23                                                           
         GOTO1 DATCON,DMCB,(1,TBMDAT),(5,LI12)                                  
         MVC   LI22(L'TBMSTUD),TBMSTUD       AND CITY                           
         MVC   LI32(L'TBMCITY),TBMCITY                                          
         GOTOR PRFRMSTA,DMCB,TBMSTAT                                            
PR2H23   BAS   RE,PRIGHT                                                        
*                                                                               
PR2H30   LA    R1,TBCONT                  CONTRACT                              
         LA    R4,LI12                                                          
         MVI   BYTE,C'N'                                                        
         CLC   0(L'TBCON1,R1),SPACES                                            
         BNH   PR2H80                                                           
         MVI   FLAG,C'Y'                                                        
         MVC   L12(12),LTAFM                                                    
         LA    R3,TBCOM                                                         
*                                                                               
PR2H40   MVC   0(L'TBCON1,R4),0(R1)                                             
         CLI   BYTE,C'Y'                                                        
         BE    PR2H50                                                           
         MVI   BYTE,C'Y'                                                        
         BAS   RE,PRTID                 PRINT TRACK ID ONCE                     
         B     PR2H60                                                           
*                                                                               
PR2H50   LA    R4,L'TBCON1+2(R4)        NEXT CONTRACT                           
*                                                                               
PR2H60   LA    R1,L'TBCON1(R1)          LEAVE 2 SPACES                          
         CR    R1,R3                                                            
         BNL   PR2H70                                                           
         CLC   0(L'TBCON1,R1),SPACES                                            
         BH    PR2H40                                                           
*                                                                               
PR2H70   BAS   RE,PRIGHT                                                        
*                                                                               
PR2H80   CLI   BYTE,C'Y'           WENT TO PRTID ALREADY                        
         BE    PR2H90                                                           
         BAS   RE,PRTID            PRINT TRACK ID ONCE                          
         CLI   TGBYTE,C'N'         IF NOTHING WAS PRINTED                       
         BE    PR2H90              DON'T PRINT RIGHT HAND SIDE                  
         BAS   RE,PRIGHT                                                        
*                                                                               
PR2H90   CLC   TBCOM,SPACES        COMMENT                                      
         BNH   PR2H110                                                          
         CLI   FLAG,C'N'           IF THERE WAS NO FILM/RECD/MUS/CONT           
         BE    PR2H100             DON'T LEAVE AN EXTRA BLANK LINE              
         BAS   RE,PRIGHT                                                        
*                                                                               
PR2H100  MVC   L12(8),LTCMT                                                     
         MVC   LI12(L'TBCOM),TBCOM                                              
         BAS   RE,PRIGHT                                                        
*                                                                               
PR2H110  CLC   TBCOM2,SPACES         IF THERE IS AN EXTRA COMMENT               
         BNH   PR2H140                                                          
         CLC   TBCOM,SPACES          CHECK IF ALREADY PRINTED THE               
         BH    PR2H130                     WORD 'COMMENT'                       
         CLI   FLAG,C'N'             IF THERE WAS NO FILM/RECD/MUS/CONT         
         BE    PR2H120                                                          
         BAS   RE,PRIGHT             DON'T LEAVE AN EXTRA BLANK LINE            
*                                                                               
PR2H120  MVC   L12(8),LTCMT                                                     
*                                                                               
PR2H130  MVC   LI12(L'TBCOM2),TBCOM2                                            
         BAS   RE,PRIGHT                                                        
*                                                                               
PR2H140  CLI   DONE,C'Y'                                                        
         BE    PR2HX                                                            
         BAS   RE,PRIGHT                  FINISH PRINTING RHS                   
         B     PR2H140                                                          
*                                                                               
PR2HX    B     PRXIT                                                            
         EJECT                                                                  
*        PRINT TRACK ID                                                         
*                                                                               
PRTID    NTR1                                                                   
         MVI   TGBYTE,C'N'         NOTHING TO PRINT                             
         CLC   TBCONT,SPACES                                                    
         BNH   *+8                                                              
         LA    R4,L'TBCON1+1(R4)   SET R4 TO NEXT PRINT LOCATION                
*                                                                               
         CLC   TBTID,SPACES        IF THERE IS A TRACK ID                       
         BNH   PT10                                                             
         MVC   0(L'TBTID,R4),TBTID    PRINT IT                                  
         LA    R4,L'TBTID+1(R4)                                                 
         MVI   FLAG,C'Y'                                                        
         MVI   TGBYTE,C'Y'         SOMETHING PRINTED                            
*                                                                               
PT10     SR    R1,R1                                                            
         ICM   R1,1,TBTLN          PRINT TRACK LENGTH                           
         BZ    PT20                                                             
         LR    RF,R4                                                            
         CLC   TBTID,SPACES                                                     
         BNH   *+8                                                              
         LA    RF,1(RF)                                                         
         BAS   RE,PEDIT3A                                                       
         MVI   FLAG,C'Y'                                                        
         MVI   TGBYTE,C'Y'         SOMETHING PRINTED                            
         LA    R4,5(R4)                                                         
*                                                                               
PT20     LA    R4,1(R4)                                                         
*                                                                               
PTX      XIT1  REGS=(R4)                                                        
         DROP  R2                                                               
         EJECT                                                                  
*                                                                               
*        DETERMINE WHICH ITEM TO PRINT ON UPPER RIGHT HAND SIDE                 
*                  OF INVOICE                                                   
*                                                                               
         USING P1LINED,R2                                                       
PRIGHT   NTR1                                                                   
         LA    R2,P                POINT TO PRINT LINE                          
         CLI   DONE,C'Y'           DON'T BOTHER IF NOTHING                      
         BE    PRTX                      LEFT TO PRINT                          
         GOTOR PTINV               PRINT INVOICE                                
         BE    PRTX                                                             
         CLI   DAT,C'Y'                                                         
         BE    PR2                                                              
         BAS   RE,PRDAT            PRINT DATE                                   
         B     PRTX                                                             
*                                                                               
PR2      CLI   CDAT,C'Y'                                                        
         BE    PR10                                                             
         BRAS  RE,PRCDAT           PRINT CHECK DATE                             
         BE    PRTX                                                             
*                                                                               
PR10     CLI   TBMED,TACOMEDE      IF MEDIA = EVENT, SKIP ALL THIS              
         BE    PR20                                                             
         CLI   MED,C'Y'                                                         
         BE    PR20                                                             
         BAS   RE,PRMED            PRINT MEDIA                                  
         B     PRTX                                                             
*                                                                               
PR20     CLI   PO,C'Y'                                                          
         BE    PR30                                                             
         BAS   RE,PRPO             PRINT PO NUMBER                              
         BE    PRTX                                                             
*                                                                               
PR30     CLI   EST,C'Y'                                                         
         BE    PR40                                                             
         BRAS  RE,PREST            PRINT ESTIMATE NUMBER                        
         BE    PRTX                                                             
*                                                                               
PR40     CLI   VEN,C'Y'                                                         
         BE    PR50                                                             
         BAS   RE,PRVEN            PRINT VENDOR                                 
         BE    PRTX                                                             
*                                                                               
PR50     CLI   LEN,C'Y'                                                         
         BE    PR60                                                             
         BAS   RE,PRLEN            PRINT LENGTH                                 
         BE    PRTX                                                             
*                                                                               
PR60     CLI   PAY,C'Y'                                                         
         BE    PR67                                                             
         GOTOR PRPTYP              PRINT PAY TYPE                               
         B     PRTX                                                             
*                                                                               
PR67     CLI   TBMED,TACOMEDE      IF MEDIA = EVENT, SKIP ALL THIS              
         BE    PR80                                                             
         CLI   USET,C'Y'                                                        
         BE    PR70                                                             
         GOTOR PRUSE               PRINT USE TYPE                               
         B     PRTX                                                             
*                                                                               
PR70     CLI   CWK,C'Y'                                                         
         BE    PR80                                                             
         GOTOR PRCYC               PRINT CYCLE WEEKS                            
         BE    PRTX                                                             
*                                                                               
PR80     CLI   CWS,C'Y'                                                         
         BE    PR90                                                             
         GOTOR PRCST               PRINT CYCLE START END DATES                  
         BE    PRTX                                                             
*                                                                               
PR90     CLI   TBMED,TACOMEDE      IF MEDIA = EVENT, SKIP ALL THIS              
         BE    PR170                                                            
         CLI   MAIN,C'Y'                                                        
         BE    PR100                                                            
         GOTOR PRMAIN              PRINT MAIN USES                              
         BE    PRTX                                                             
*                                                                               
PR100    CLI   LUSE,C'Y'                                                        
         BE    PR110                                                            
         GOTOR PRLUSE              PRINT LIFT USES                              
         BE    PRTX                                                             
*                                                                               
PR110    TM    TGUSTYST,MAJORS+UNITS+INSERTS      IF MAJORS, UNITS, OR          
         BNZ   PR115                              INSERTS                       
         CLI   TGUSEQU,UTAG                       OR TAG                        
         BE    PR115                                                            
         CLI   TGUSEQU,UDEM                       IF DEMO                       
         BE    PR111                                                            
         CLI   TGUSEQU,USNA                       OR SNA                        
         BE    PR111                                                            
         CLI   TGUSEQU,UCDM                       OR CDM                        
         BNE   PR130                                                            
PR111    OC    TBUNITS,TBUNITS     IF DEM, MUST BE NEW STYLE                    
         BZ    PR130                                                            
*                                                                               
PR115    CLI   MAJ,C'Y'                                                         
         BE    PR120                                                            
         GOTOR PRMAJ              PRINT MAJORS/UNITS/INSERTS/TAGS/DEMOS         
         BE    PRTX                                                             
*                                                                               
PR120    CLI   UPMAJ,C'Y'                                                       
         BE    PR130                                                            
         GOTOR PRMAJUP             PRINT UPGRADE MAJORS/UNITS/INSERTS           
         BE    PRTX                                                             
*                                                                               
PR130    CLI   PHOTO,C'Y'                                                       
         BE    PR140                                                            
         GOTOR PRPHOTO             PRINT PHOTOGRAPHER                           
         BE    PRTX                                                             
*                                                                               
PR140    CLI   PAREA,C'Y'                                                       
         BE    PR150                                                            
         GOTOR PRAREA              PRINT AREA                                   
         BE    PRTX                                                             
*                                                                               
PR150    CLI   USE,C'Y'                                                         
         BE    PR160                                                            
         GOTOR PRPUSE              PRINT USE                                    
         BE    PRTX                                                             
*                                                                               
PR160    CLI   SIGN,C'Y'                                                        
         BE    PR170                                                            
         BAS   RE,PRSIGN           PRINT SIGNATORY                              
         BE    PRTX                                                             
*                                                                               
PR170    DS    0H                                                               
*                                                                               
PR200    MVI   DONE,C'Y'           ALL ITEMS PRINTED                            
         BAS   RE,PBXBOT           SET BOTTOM OF BOX - UPPER RHS                
*                                                                               
PRTX     BRAS  RE,PRTIT                 PRINT CURRENT LINE                      
*                                                                               
PRTX10   B     PRXIT                                                            
         EJECT                                                                  
*        PRINT DATE                                                             
*                                                                               
PRDAT    NTR1                                                                   
         MVI   DAT,C'Y'                                                         
         MVC   R11(4),LTDATE                                                    
***      CLI   TBMED,TACOMEDE      IF MEDIA = EVENT,                            
***      BNE   PRDT10                                                           
***      MVC   RI11+1(8),TBRUNDTE  BILLING DATE SHIFT 1 TO RIGHT                
***      B     *+10                                                             
PRDT10   MVC   RI11(8),TBRUNDTE    BILLING DATE                                 
*                                                                               
         MVC   R21(4),LTPG                                                      
         LA    RF,R21+5            LEAVE 1 BLANK                                
         ZIC   R1,PGNUM                                                         
         BAS   RE,PEDIT2                                                        
*                                                                               
         LA    R1,RI21+L'RI21      END OF DATA                                  
         BAS   RE,SHUFFLE          FIND 1ST NON BLANK                           
         MVC   0(2,R1),LTOF                                                     
         LA    RF,3(R1)            LEAVE BLANKS                                 
         ZIC   R3,NPAGES                                                        
         GOTOR PRTCMT,DMCB,STATPAG IF THERE IS A COMMENT,                       
         BNE   PRDAT10             INCREMENT NUMBER OF PAGES BY 1               
         ZIC   R3,NPAGES                                                        
         AHI   R3,1                                                             
PRDAT10  EDIT  (R3),(2,(RF)),ZERO=BLANK,ALIGN=LEFT                              
         B     PRXIT                                                            
         SPACE 5                                                                
*                                                                               
*        PRINT MEDIA                                                            
*                                                                               
PRMED    NTR1                                                                   
         MVI   MED,C'Y'                                                         
         CLI   TBCOTYPE,CTYIND           DON'T PRINT MEDIA FOR INDUST           
*        BE    PMED10                          COMMERCIAL TYPES                 
*        CLI   TBCOTYPE,CTYICAT1         DON'T PRINT MEDIA FOR INDUST           
*        BE    PMED10                          COMMERCIAL TYPES                 
*        CLI   TBCOTYPE,CTYICAT2         DON'T PRINT MEDIA FOR INDUST           
         BE    PMED10                          COMMERCIAL TYPES                 
         MVC   R11(5),LTMED              MEDIA                                  
         GOTO1 MEDVAL,DMCB,TBMED                                                
         TM    TBAYSTA5,TAAYSOTH                                                
         BO    PMED10                                                           
         MVC   RI11(L'TGMENAME),TGMENAME                                        
*                                                                               
PMED10   GOTOR PUTCPY                    PUT OUT COPY                           
         B     PRXIT                                                            
         SPACE 5                                                                
*                                                                               
*        PRINT PO NUMBER                                                        
*                                                                               
PRPO     NTR1                                                                   
         MVI   PO,C'Y'                                                          
         CLI   TBMED,TACOMEDE      IF MEDIA = EVENT, DO DIFFERENTLY             
         BE    PRPO10                                                           
         CLC   TBPO,SPACES                PO NUMBER                             
         BNH   PNO                                                              
         MVC   R11(9),LTPO                                                      
         MVC   RI11(L'TBPO),TBPO                                                
         B     PYES                                                             
                                                                                
PRPO10   CLC   TBPO,SPACES         MEDIA = EVENT                                
         BNH   PRPO20                                                           
         MVC   R11(3),LTPO2                                                     
         MVC   RI11(L'TBPO),TBPO                                                
PRPO20   GOTOR PUTCPY                    PUT OUT COPY                           
         B     PYES                                                             
                                                                                
         SPACE 3                                                                
*                                                                               
*        PRINT VENDOR                                                           
*                                                                               
PRVEN    NTR1                                                                   
         MVI   VEN,C'Y'                                                         
         CLI   TBMED,TACOMEDE      IF MEDIA = EVENT, PRINT BLANK RIGHT          
         BE    PYES                SIDE                                         
         CLC   TBVEN,SPACES               VENDOR                                
         BNH   PNO                                                              
         MVC   R11(6),LTVEN                                                     
         MVC   RI11(L'TBVEN),TBVEN                                              
         B     PYES                                                             
         SPACE 3                                                                
*                                                                               
*        PRINT COMMERCIAL LENGTH                                                
*                                                                               
PRLEN    NTR1                                                                   
         MVI   LEN,C'Y'                                                         
         CLI   TBMED,TACOMEDE      IF MEDIA = EVENT, PRINT BLANK RIGHT          
         BE    PRLENX              SIDE                                         
         TM    TBAYSTA5,TAAYSOTH                                                
         BO    PRLENX                                                           
         SR    R1,R1                                                            
         ICM   R1,1,TBSEC          ONLY REQUIRED FOR TV                         
         BZ    PNO                                                              
         MVC   R11(9),LTLEN        COMMERCIAL LENGTH                            
         LA    RF,RI11                                                          
         BAS   RE,PEDIT3A                                                       
         OC    TBLID,TBLID         IF HAVE LIFT                                 
         BZ    PRLENX                                                           
         OC    TBLSEC,TBLSEC       AND LIFT LENGTH                              
         BZ    PRLENX                                                           
         TM    STAT2,STAVERS       NOT VERSION                                  
         BO    PRLENX                                                           
         MVC   R21(8),LTLLEN       PRINT IT                                     
         ZIC   R1,TBLSEC                                                        
         LA    RF,RI21+4                                                        
         BAS   RE,PEDIT2                                                        
*                                                                               
PRLENX   B     PYES                                                             
         EJECT                                                                  
*                                                                               
*        PRINT SIGNATORY ON UNION COPIES                                        
*                                                                               
PRSIGN   NTR1                                                                   
         MVI   SIGN,C'Y'                                                        
         CLI   UNICOPY,C'Y'        ONLY PRINT ON UNION COPIES                   
         BNE   PNO                                                              
*                                                                               
         MVC   R11(L'LTSIGN),LTSIGN                                             
*                                                                               
         CLC   TBSIGN,SPACES       HAVE WE GOT A SIGNATORY?                     
         BH    *+10                YES                                          
         MVC   TBSIGN,TGAGY        NO, USE AGENCY                               
*                                                                               
         CLI   TBCOTYPE,C'I'       ONLY INDUSTRIAL COMM'L TYPES                 
         BE    PRSIGN1                                                          
         CLI   TBCOTYPE,C'1'                                                    
         BE    PRSIGN1                                                          
         CLI   TBCOTYPE,C'2'                                                    
         BNE   PRSIGN4                                                          
PRSIGN1  CLI   TGUSEQU,UISS        ALL THESE TYPES HAVE 0101 SIGNATORY          
         BE    PRSIGN2                                                          
         CLI   TGUSEQU,UINS                                                     
         BE    PRSIGN2                                                          
         CLI   TGUSEQU,UIDS                                                     
         BNE   PRSIGN4                                                          
**NO-OP  CLI   TGUSEQU,UINA                                                     
**01/13  BE    PRSIGN2                                                          
**       CLI   TGUSEQU,UINR                                                     
**NO-OP  BNE   PRSIGN4                                                          
*                                                                               
PRSIGN2  MVC   TBSIGN,SPACES                                                    
         MVC   TBSIGN(4),=C'0101'                                               
*                                                                               
PRSIGN4  MVC   RI11(L'TBSIGN),TBSIGN                                            
*                                                                               
         MVC   AIO,AIO2                                                         
         GOTO1 RECVAL,DMCB,TLAYCDQ,(X'AC',TBSIGN),0                             
         MVC   AIO,AIO1                                                         
         BNE   *+10                                                             
         MVC   RI11+L'TBSIGN+1(28-L'TBSIGN-1),TGNAME                            
         MVC   TGAGY,LAGY          RESET AGENCY CODE FOR NEXT COPY              
         B     PYES                                                             
         DROP  R2                                                               
         EJECT                                                                  
*                                                                               
*        SHUFFLE BACK TO 1ST NON BLANK                                          
*                ENTRY - R1 = END OF DATA                                       
*                EXIT  - R1 = 1ST NON BLANK + 1                                 
*                                                                               
         SPACE 1                                                                
SHUFFLE  CLI   0(R1),C' '          STOP AFTER 1ST BLANK                         
         BNE   SHX                                                              
         BCT   R1,SHUFFLE          BUMP TO NEXT CHAR                            
*                                                                               
SHX      LA    R1,2(R1)            LEAVE 1 BLANK                                
         BR    RE                                                               
         SPACE 3                                                                
*                                                                               
*        CHECK IF ATTN NAME & PRINT IT                                          
*                                                                               
         SPACE 1                                                                
         USING P1LINED,R2                                                       
CKATTN   NTR1                                                                   
*                                                                               
         MVI   PRATTN,C'Y'                                                      
         CLC   TBATTN,SPACES     ELSE PRINT ATTN NAME                           
         BNH   PNO                                                              
         MVC   LI11(6),=C'ATTN: '                                               
         MVC   LI11+6(L'TBATTN),TBATTN                                          
         B     PYES                                                             
         DROP  R2                                                               
         SPACE 2                                                                
*                                                                               
*              ROUTINE TO POP IN A BOTTOM AT END OF REPORT                      
*                                                                               
         SPACE 1                                                                
PBXBOT   NTR1                                                                   
         L     R3,ABOX                                                          
         USING BOXD,R3                                                          
         MVC   BOXROWS,SPACES                                                   
         ZIC   RF,LINE                                                          
         LA    RF,BOXROWS-1(RF)                                                 
         MVI   0(RF),C'B'                                                       
         MVI   BOXINIT,0                                                        
         B     PRXIT                                                            
         EJECT                                                                  
*                                                                               
*        EDIT A LINE - INPUT (R1)                                               
*                      OUTPUT LENGTH = 2                                        
*                      OUTPUT ADDR   = RF                                       
*                                                                               
         SPACE 1                                                                
PEDIT2   EDIT  (R1),(2,(RF)),ZERO=BLANK,ALIGN=LEFT                              
         BR    RE                                                               
         SPACE 2                                                                
*                                                                               
*        EDIT A LINE - INPUT (R1)                                               
*                      OUTPUT LENGTH = 3                                        
*                      OUTPUT ADDR   = RF                                       
*                                                                               
PEDIT3A  EDIT  (R1),(3,(RF)),ZERO=BLANK,ALIGN=LEFT                              
         BR    RE                                                               
         EJECT                                                                  
*                                                                               
PYES     SR    RC,RC               SET CONDITION CODE                           
PNO      LTR   RC,RC                                                            
*                                                                               
PRXIT    XIT1                                                                   
         EJECT                                                                  
LTINV    DC    C'Invoice For'                                                   
LTEMP    DC    C'Employer of Record'                                            
LTPG     DC    C'Page'                                                          
LTOF     DC    C'of'                                                            
LTDATE   DC    C'Date  '                                                        
LTINT    DC    C'Internal Coding'                                               
LTCTYP   DC    C'Commercial Type'                                               
LTPO     DC    C'PO Number'                                                     
LTPO2    DC    C'PO#'                                                           
LTVEN    DC    C'Vendor'                                                        
LTCLI    DC    C'Client'                                                        
LTAIR    DC    C'First Air Date'                                                
LTMED    DC    C'Media'                                                         
LTPRD    DC    C'Product'                                                       
LTPRDRG  DC    C'Prd/Reg'                                                       
LTGST#   DC    CL10'GST Number'                                                 
LTTFDID# DC    C'TP FED ID'                                                     
LTPFDID# DC    C'PP FED ID'                                                     
LTFIX    DC    C'First Fixed Cycle'                                             
LTCEXP   DC    C'Expiration Date'                                               
LTLEN    DC    C'Comml Len'                                                     
LTLLEN   DC    C'Lift Len'                                                      
LTCOM    DC    C'Commercial'                                                    
LTTITLE  DC    C'Title'                                                         
LTISC    DC    C'ISCII Number'                                                  
LTPRTID  DC    C'Print ID'                                                      
LTEVTNM  DC    C'Event Name  '                                                  
LTEVTID  DC    C'Event ID'                                                      
LTLID    DC    C'Lift Number'                                                   
LTVER    DC    C'Version ID'                                                    
LTFDT    DC    C'Film Date'                                                     
LTFST    DC    C'Film Studio'                                                   
LTFCT    DC    C'Film City'                                                     
LTRDT    DC    C'Record Date'                                                   
LTRST    DC    C'Record Studio'                                                 
LTRCT    DC    C'Record City'                                                   
LTMDT    DC    C'Music Date'                                                    
LTMST    DC    C'Music Studio'                                                  
LTMCT    DC    C'Music City'                                                    
LTSDT    DC    C'Shoot Date'                                                    
LTSST    DC    C'Shoot Studio'                                                  
LTSCT    DC    C'Shoot City'                                                    
LTAFM    DC    C'AFM Contract'                                                  
LTCMT    DC    C'Comments'                                                      
LTSIGN   DC    C'Signatory'                                                     
*                                                                               
LTDMBB   DC    CL36'D''ARCY MASIUS BENTON && BOWLES'                            
         LTORG                                                                  
         EJECT                                                                  
*                                                                               
*        PRINT ESTIMATE                                                         
*                                                                               
         USING P1LINED,R2                                                       
PREST    NTR1  BASE=*,LABEL=*                                                   
         MVI   EST,C'Y'                                                         
         CLI   TBMED,TACOMEDE      IF MEDIA = EVENT, DO DIFFERENTLY             
         BNE   PREST05                                                          
         CLC   TBEST,SPACES        ESTIMATE #                                   
         JNH   PRESTX                                                           
         MVC   R11(4),LTJOB                                                     
         MVC   RI11(L'TBEST),TBEST                                              
         B     PRESTX                                                           
                                                                                
PREST05  CLC   TBEST,SPACES        ESTIMATE #                                   
         BNH   PREST10                                                          
         MVC   R11(8),LTEST                                                     
         MVC   RI11(L'TBEST),TBEST                                              
*                                                                               
PREST10  CLI   TBPER,0             PERIOD                                       
         BNE   PREST20                                                          
         CLC   TBEST,SPACES                                                     
         JNH   PNO                                                              
         B     PRESTX                                                           
*                                                                               
PREST20  MVC   R21(6),LTPER                                                     
         LA    RF,RI21+2                                                        
         ZIC   R1,TBPER                                                         
         EDIT  (R1),(2,(RF)),ZERO=BLANK,ALIGN=LEFT                              
*                                                                               
PRESTX   J     PYES                                                             
         DROP  R2                                                               
LTEST    DC    C'Estimate'                                                      
LTPER    DC    C'Period'                                                        
LTJOB    DC    C'Job#'                                                          
         LTORG                                                                  
         EJECT                                                                  
*                                                                               
*        PRINT CHECK DATE                                                       
*                                                                               
         USING P1LINED,R2                                                       
PRCDAT   NTR1  BASE=*,LABEL=*                                                   
         MVI   CDAT,C'Y'                                                        
         CLI   UNICOPY,C'Y'        IF THIS IS A UNION COPY                      
         JNE   PNO                                                              
         CLC   TBUNI1,=C'AFT'       & IT'S UNION AFTRA                          
         JNE   PNO                                                              
         MVC   R11(8),LTCKDTE      THEN PRINT CHECK DATE                        
         GOTO1 DATCON,DMCB,(1,TBCKDTE),(5,RI11)                                 
         J     PYES                                                             
         DROP  R2                                                               
LTCKDTE  DC    C'Chk Date'                                                      
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
*        PRINT FILM/RECORD/MUSIC CITY/STATE                           *         
*        ON ENTRY ... P1 = A(TABILLD FILM/MUSIC/RECORD STATE FIELD)   *         
*                     R2 = A(PRINT LINE)                              *         
***********************************************************************         
                                                                                
         USING P2LINED,R2                                                       
PRFRMSTA NTR1  BASE=*,LABEL=*                                                   
         MVC   L32(L'LTCST),LTCST  CHANGE CITY HEAD TO CITY/STATE               
                                                                                
         L     R1,0(R1)            IF FILM/MUSIC/RECORD STATE IS SET            
         OC    0(L'TBFSTAT,R1),SPACES                                           
         CLC   0(L'TBFSTAT,R1),SPACES                                           
         BE    PFRMSX                                                           
                                                                                
         LA    RE,LI32+L'TBFCITY+1                                              
PFRMS10  CLI   0(RE),C' '          FIND LAST SIGNIFICANT CHARACTER              
         BNE   PFRMS20                                                          
         SHI   RE,1                                                             
         B     PFRMS10                                                          
                                                                                
PFRMS20  MVI   1(RE),C','          THEN ADD COMMA AND STATE                     
         MVC   3(L'TBFSTAT,RE),0(R1)                                            
PFRMSX   XIT1                                                                   
         DROP R2                                                                
                                                                                
LTCST    DC    C'City/State '                                                   
                                                                                
         LTORG                                                                  
         EJECT                                                                  
*        DETERMINE WHICH RUN TYPE THIS IS, IF BILLED, IF RERUN                  
*        R1 - WHERE TO PRINT                                                    
*                                                                               
RUNTYPE  NTR1  BASE=*,LABEL=*                                                   
         L     R3,ATWA             WRITE <> NO                                  
         USING T703FFD,R3                                                       
*                                                                               
         MVC   0(2,R1),RCPROG+2                                                 
         CLI   RECNUM,BI           IF BI                                        
         BE    *+12                                                             
         CLI   RECNUM,PB           OR PB &                                      
         BNE   RT10                                                             
         CLI   TWAWRITE,C'N'       THEN IT'S NIGHTLY BILLING RUN -              
         BNE   RT30                                                             
*                                                                               
RT10     TM    TBSTAT,TAINSBIL     ELSE IF THIS INVOICE WASN'T MARKED           
         BO    RT20                                                             
         MVI   2(R1),C'U'          BILLED - PUT OUT 'U'                         
         B     RT30                                                             
*                                                                               
RT20     CLI   RECNUM,BI           IF BI                                        
         BE    *+12                                                             
         CLI   RECNUM,PB           OR PB &                                      
         BNE   *+12                                                             
         CLI   TWAWRITE,C'N'       RERUN - INDICATE SO                          
         BE    *+14                                                             
         CLC   TGTODAY1,TBTODAY    IF BILLING DATE <> TODAY                     
         BE    RT30                                                             
         MVI   2(R1),C'R'          THEN THIS IS A RERUN                         
*                                                                               
RT30     CLI   RECNUM,AC           IF AC                                        
         BNE   RTX                                                              
         MVI   3(R1),C'H'          SET DELIVERY TYPE                            
         TM    TBAYSTA2,TAAYSHND   HAND                                         
         BO    RTX                                                              
         MVI   3(R1),C'O'                                                       
         TM    TBAYSTA2,TAAYSOVN   OVERNIGHT                                    
         BO    RTX                                                              
         MVI   3(R1),C'P'                                                       
         TM    TBAYSTA2,TAAYSPCH   POUCH                                        
         BO    RTX                                                              
         MVI   3(R1),C'C'                                                       
         TM    TBAYSTA2,TAAYSCHI   CHICAGO POUCH                                
         BO    RTX                                                              
         MVI   3(R1),C'I'                                                       
         TM    TBAYSTA3,TAAYSPRI   PRIORITY MAIL                                
         BO    RTX                                                              
         MVI   3(R1),C'M'          MAIL                                         
*                                                                               
RTX      XIT1                                                                   
         DROP  R3                                                               
         LTORG                                                                  
         EJECT                                                                  
*                                                                               
*        PRINT PAY TYPE                                                         
*                                                                               
         USING P1LINED,R2                                                       
PRPTYP   NTR1  BASE=*,LABEL=*                                                   
         MVI   PAY,C'Y'                                                         
         GOTO1 USEVAL,DMCB,(X'20',TBUSE),TBTYPE                                 
         MVC   R11(8),LTPAYT               PAY TYPE                             
         TM    TBAYSTA5,TAAYSOTH                                                
         BO    PRPTYX                                                           
         CLI   TBMED,TACOMEDE            IF MEDIA = EVENT,                      
         BE    PRPTYX                                                           
         MVC   RI11(7),=C'SESSION'                                              
         TM    TGUSSTAT,SESSION                                                 
         BO    PRPTYX                                                           
         MVC   RI11(7),=C'REUSE  '                                              
*                                                                               
PRPTYX   CLI   TBMED,TACOMEDE            IF MEDIA = EVENT,                      
         BNE   PRPTYXX                                                          
         MVC   RI11(5),=C'EVENT'                                                
*                                                                               
PRPTYXX  XIT1                                                                   
         LTORG                                                                  
         SPACE 3                                                                
         EJECT                                                                  
*                                                                               
*        PRINT USE TYPE                                                         
*                                                                               
PRUSE    NTR1  BASE=*,LABEL=*                                                   
         MVI   USET,C'Y'                                                        
         MVC   RI21+3(3),TGUSCDE                                                
         MVC   R11(8),LTUSET               USE TYPE                             
         TM    TBAYSTA5,TAAYSOTH                                                
         BO    *+10                                                             
         MVC   RI11(L'TGUSNAME),TGUSNAME                                        
*                                                                               
         CLI   TGUSEQU,UITN                                                     
         BNE   PRUSEX                                                 I         
         L     R4,AIO                                                           
         MVI   ELCODE,TAUHELQ      IF USAGE HISTORY ELEMENT                     
         BRAS  RE,GETEL            IS NOT PRESENT                               
         BE    *+14                MUST BE AUTO PAYMENT                         
         MVC   RI11+12(4),=C'AUTO'                                              
         B     PRUSEX                                                           
         SPACE                                                                  
         USING TAUHD,R4                                                         
         LA    R5,RI11+12           OTHERWISE PRINT OUT USE #'S                 
         EDIT  TAUHIFUS,(3,(R5)),ALIGN=LEFT                                     
         CLI   0(R5),C' '                                                       
         BE    *+12                                                             
         LA    R5,1(R5)                                                         
         B     *-12                                                             
         MVI   0(R5),C'-'                                                       
         LH    RE,TAUHIFUS                                                      
         AH    RE,TAUHINUS                                                      
         SHI   RE,1                                                             
         EDIT  (RE),(3,1(R5)),ALIGN=LEFT                                        
PRUSEX   XIT1                                                                   
         DROP  R4                                                               
         LTORG                                                                  
         SPACE 3                                                                
*                                                                               
*        PRINT INV NUMBER & SPECIAL CODES                                       
*                                                                               
PTINV    NTR1  BASE=*,LABEL=*                                                   
         CLI   INV,C'Y'                                                         
         BE    PINVNO                                                           
         MVI   INV,C'Y'                                                         
         MVC   R11(9),LTINUM       INVOICE NUMBER                               
         MVC   RI11(6),TGAGY                                                    
*                                                                               
         MVC   TEMP(L'TGINV),TGINV                                              
         XC    TEMP(L'TGINV),COMPLM1 COMPLEMENT INV NUM                         
*                                                                               
         CLI   RECNUM,EB           IF ECOPY,                                    
         BNE   PTINV10                                                          
         TM    TBSTAT,TAINSCIN     AND CANCELLER                                
         BZ    PTINV10                                                          
         GOTO1 TINVCON,DMCB,TEMP,TBINV,DATCON   PRINT CANCELLER INV#            
         B     PTINV20                                                          
*                                                                               
PTINV10  GOTO1 TINVCON,DMCB,(X'80',TEMP),TBINV,DATCON                           
PTINV20  LA    R1,RI11+L'RI11                                                   
         GOTOR SHUFFLE2                                                         
         MVC   0(L'TBINV,R1),TBINV                                              
         GOTOR SPCLCDES            PRINT SPECIAL CODES (CXL/BNP/CR)             
         B     PINVYES                                                          
*                                                                               
PINVYES  SR    RC,RC               SET CONDITION CODE                           
PINVNO   LTR   RC,RC                                                            
         XIT1                                                                   
         LTORG                                                                  
         EJECT                                                                  
*                                                                               
*        PRINT CYCLE WEEKS                                                      
*                                                                               
PRCYC    NTR1  BASE=*,LABEL=*                                                   
         MVI   CWK,C'Y'                                                         
         OC    TGUSWKS,TGUSWKS                                                  
         BZ    PRCYCNO                                                          
         MVC   R11(9),LTCWK                CYCLE WEEKS                          
         LA    RF,RI11                                                          
*                                                                               
         CLI   TGUSEQU,UDOR                                                     
         BNE   PRCYC05                                                          
         MVC   0(16,RF),=C'26 Weeks + 1 Day'                                    
         B     PRCYCYES                                                         
*                                                                               
PRCYC05  ZIC   R1,TGUSWKS                                                       
         TM    TGUSWKS,X'80'               TEST IF MONTHS                       
         BO    PRCYC10                                                          
         TM    TGUSWKS,X'40'               OR DAYS                              
         BO    PRCYC20                                                          
*        BAS   RE,PEDIT2B                                                       
         EDIT  (R1),(2,(RF)),ZERO=BLANK,ALIGN=LEFT                              
         B     PRCYCYES                                                         
*                                                                               
PRCYC10  NI    TGUSWKS,X'7F'               TURN OFF BIT                         
         ZIC   R1,TGUSWKS                                                       
*        BAS   RE,PEDIT2B                                                       
         EDIT  (R1),(2,(RF)),ZERO=BLANK,ALIGN=LEFT                              
         LA    R1,RI11+L'RI11              SHUFFLE BACK TO NON BLANK            
         GOTOR SHUFFLE2                                                         
         MVC   0(6,R1),=C'Months'                                               
         B     PRCYCYES                                                         
*                                                                               
PRCYC20  NI    TGUSWKS,X'BF'               TURN OFF BIT                         
         ZIC   R1,TGUSWKS                                                       
*        BAS   RE,PEDIT2B                                                       
         EDIT  (R1),(2,(RF)),ZERO=BLANK,ALIGN=LEFT                              
         LA    R1,RI11+L'RI11              SHUFFLE BACK TO NON BLANK            
         GOTOR SHUFFLE2                                                         
         MVC   0(4,R1),=C'Days'                                                 
PRCYCX   B     PRCYCYES                                                         
*                                                                               
PRCYCYES SR    RC,RC               SET CONDITION CODE                           
PRCYCNO  LTR   RC,RC                                                            
         XIT1                                                                   
         LTORG                                                                  
         SPACE 5                                                                
*                                                                               
*        PRINT CYCLE START/END DATES                                            
*                                                                               
PRCST    NTR1  BASE=*,LABEL=*                                                   
         MVI   CWS,C'Y'                                                         
         OC    TBCDTS,TBCDTS                  CYCLE START/END DATES             
         BZ    PRCSTNO                                                          
         MVC   R11(5),LTCYC                                                     
                                                                                
         CLI   TBMED,TACOMEDE      IF MEDIA = EVENT,                            
         BNE   PRCST10                                                          
         MVC   R11(6),LTPAYPR      PERIOD                                       
*&&DO                                                                           
         GOTO1 DATCON,DMCB,(1,TBCDTS),(8,RI11+1)                                
         OC    TBCDTS+3(3),TBCDTS+3                                             
         BZ    PRCSTYES                                                         
         MVI   RI11+9,C'-'                                                      
         GOTO1 DATCON,DMCB,(1,TBCDTS+3),(8,RI11+10) END CYCLE                   
         B     PRCSTYES                                                         
*&&                                                                             
                                                                                
PRCST10  GOTO1 DATCON,DMCB,(1,TBCDTS),(8,RI11)                                  
         OC    TBCDTS+3(3),TBCDTS+3                                             
         BZ    PRCSTYES                                                         
         MVI   RI11+8,C'-'                                                      
         GOTO1 DATCON,DMCB,(1,TBCDTS+3),(8,RI11+9) END CYCLE                    
PRCSTYES SR    RC,RC               SET CONDITION CODE                           
PRCSTNO  LTR   RC,RC                                                            
         XIT1                                                                   
         LTORG                                                                  
         EJECT                                                                  
*                                                                               
*        PRINT MAIN USES                                                        
*                                                                               
PRMAIN   NTR1  BASE=*,LABEL=*                                                   
         MVI   MAIN,C'Y'                                                        
         OC    TBSTUS,TBSTUS                  MAIN USES                         
         BZ    PRMNO                                                            
         MVC   R11(9),LTMAIN                                                    
         LH    R1,TBSTUS                      START USE NUMBER                  
         LA    RF,RI11                                                          
*        BAS   RE,PEDIT4                                                        
         EDIT  (R1),(4,(RF)),ZERO=BLANK,ALIGN=LEFT                              
*                                                                               
         LA    R1,RI11+L'RI11                                                   
         GOTOR SHUFFLE2                                                         
         MVI   0(R1),C'-'                                                       
         LA    R1,2(R1)                                                         
         LR    RF,R1                                                            
         LH    R1,TBSTUS                      START USE                         
         AH    R1,TBUSES                      + TOTAL NUMBER OF USES            
         BCTR  R1,0                           - 1                               
*        BAS   RE,PEDIT4                      = END TOTAL USE NUMBER            
         EDIT  (R1),(4,(RF)),ZERO=BLANK,ALIGN=LEFT                              
*                                                                               
         LH    RE,=Y(USEDET1-BILLD)                                             
         AR    RE,RA                                                            
         MVC   0(L'USEDET1,RE),RI11   SAVE USE DETAILS FOR TRNSNARR             
         B     PRMNYES                                                          
*                                                                               
PRMNYES  SR    RC,RC               SET CONDITION CODE                           
PRMNNO   LTR   RC,RC                                                            
         XIT1                                                                   
         LTORG                                                                  
         SPACE 5                                                                
*                                                                               
*        PRINT LIFT USES                                                        
*                                                                               
PRLUSE   NTR1  BASE=*,LABEL=*                                                   
         MVI   LUSE,C'Y'                                                        
         LH    R1,TBUSEL                      IF NO LIFT USES                   
         LTR   R1,R1                                                            
         BZ    PRLUNO              CONTINUE                                     
         MVC   R11(9),LTLUSES                                                   
         LH    R1,TBSTUL                      ELSE PRINT                        
         LA    RF,RI11                                                          
*        BAS   RE,PEDIT4A                     STARTING LIFT USE NUMBER          
         EDIT  (R1),(4,(RF)),ALIGN=LEFT                                         
*                                                                               
         LA    R1,RI11+L'RI11                                                   
         GOTOR SHUFFLE2                       POSITION TO EDIT                  
         MVI   0(R1),C'-'                                                       
         LA    R1,2(R1)                       LAST LIFT USE NUMBER              
         LR    RF,R1                                                            
         LH    R1,TBSTUL                      STARTING LIFT USE NUMBER          
         AH    R1,TBUSEL                      + NUMBER OF USES                  
         BCTR  R1,0                           - 1                               
*        BAS   RE,PEDIT4A                     = LAST USE NUMBER                 
         EDIT  (R1),(4,(RF)),ALIGN=LEFT                                         
*                                                                               
         LH    RE,=Y(USEDET2-BILLD)                                             
         AR    RE,RA                                                            
         MVC   0(L'USEDET2,RE),RI11   SAVE USE DETAILS FOR TRNSNARR             
         B     PRLUYES                                                          
*                                                                               
PRLUYES  SR    RC,RC               SET CONDITION CODE                           
PRLUNO   LTR   RC,RC                                                            
         XIT1                                                                   
         LTORG                                                                  
         EJECT                                                                  
*                                                                               
*        PRINT MAJORS/UNITS/INSERTS/TAGS/DEMOS                                  
*                                                                               
PRMAJ    NTR1  BASE=*,LABEL=*                                                   
         MVI   MAJ,C'Y'                                                         
         MVC   BYTE,TBMAJ          EQUATE                                       
         MVC   HALF,TBUNITS        UNITS                                        
         GOTOR PRMAJUN             PRINT MAJORS/UNITS/UPGRADES/INSERTS          
         MVC   R11(8),LTORIG       USE WORD = 'ORIGINAL'                        
         TM    TGUSTYST,UPGRADE    IF THERE ARE UPGRADES                        
         BO    PMYES                                                            
         MVC   R11(8),LTMAJ1       ELSE WORD = 'MAJORS'                         
         TM    TGUSTYST,MAJORS     IF THERE WERE MAJORS                         
         BZ    PRMAJ5                                                           
         CLI   TGUSEQU,UFGR        IF USE IS FGR                                
         BNE   PMYES                                                            
         MVC   R11(8),LTAREAS      USE WORD = 'AREAS'                           
         B     PMYES                                                            
PRMAJ5   MVC   R11(8),LTUNI        ELSE WORD =  'UNITS'                         
         TM    TGUSTYST,UNITS                                                   
         BO    PMYES                                                            
         MVC   R11(8),LTINSERT     ELSE WORD =  'INSERTS'                       
         TM    TGUSTYST,INSERTS                                                 
         BO    PMYES                                                            
         MVC   R11(8),LTTAGS       ELSE WORD = 'TAGS'                           
         CLI   TGUSEQU,UTAG                                                     
         BE    PMYES                                                            
         MVC   R11(8),LTDEMS       ELSE MUST BE DEMOS                           
*                                                                               
PMYES    LH    RE,=Y(USEDET1-BILLD)                                             
         AR    RE,RA                                                            
         MVC   0(L'USEDET1,RE),RI11   SAVE USE DETAILS FOR TRNSNARR             
         B     PRMYES                                                           
*                                                                               
PRMYES   SR    RC,RC               SET CONDITION CODE                           
PRMNO    LTR   RC,RC                                                            
         XIT1                                                                   
         LTORG                                                                  
         SPACE 3                                                                
*                                                                               
*        PRINT UPGRADE MAJORS/UNITS/INSERTS                                     
*                                                                               
PRMAJUP  NTR1  BASE=*,LABEL=*                                                   
         TM    TGUSTYST,UPGRADE    IF THERE ARE UPGRADES                        
         BNO   PRMJNO                                                           
         MVI   UPMAJ,C'Y'                                                       
         MVC   BYTE,TBUPMAJ                                                     
         MVC   HALF,TBUPUNIT                                                    
         GOTOR PRMAJUN             PRINT THEM OUT                               
         MVC   R11(8),LTUPGR       WORD = 'UPGRADE'                             
*                                                                               
         LH    RE,=Y(USEDET2-BILLD)                                             
         AR    RE,RA                                                            
         MVC   0(L'USEDET2,RE),RI11   SAVE USE DETAILS FOR TRNSNARR             
         B     PRMJYES                                                          
*                                                                               
PRMJYES  SR    RC,RC               SET CONDITION CODE                           
PRMJNO   LTR   RC,RC                                                            
         XIT1                                                                   
         LTORG                                                                  
         EJECT                                                                  
*                                                                               
*        PRINT OUT MAJORS                                                       
*              BYTE - EQUATE                                                    
*              HALF - UNITS                                                     
*                                                                               
PRMAJUN  NTR1  BASE=*,LABEL=*                                                   
         TM    TGUSTYST,MAJORS        IF MAJORS/UPGRADE                         
         BNO   PRM10                                                            
         GOTO1 MAJVAL,DMCB,(X'80',BYTE)                                         
         BNE   PRM10                                                            
         MVC   RI11(L'TGMACHAR),TGMACHAR      MOVE THEM TO PRINT LINE           
*                                                                               
PRM10    TM    TGUSTYST,UNITS      IF ONLY UNITS                                
         BO    PRM15                                                            
         CLI   TGUSEQU,UTAG        OR TAG                                       
         BE    PRM17                                                            
         CLI   TGUSEQU,UDEM        OR DEM                                       
         BE    PRM17                                                            
         CLI   TGUSEQU,USNA        OR SNA                                       
         BE    PRM17                                                            
         CLI   TGUSEQU,UCDM        OR CDM                                       
         BE    PRM17                                                            
         TM    TGUSTYST,INSERTS    (OR INSERTS)                                 
         BNO   PRMX                                                             
*                                                                               
PRM15    TM    TGUSTYST,MAJORS+UPGRADE                                          
         BNZ   PRM20                                                            
*                                                                               
PRM17    LA    RF,RI11             ONLY PRINT THEM                              
         LH    R1,HALF                                                          
*        BAS   RE,PEDIT4NZ                                                      
         EDIT  (R1),(4,(RF)),ZERO=NOBLANK,ALIGN=LEFT                            
         B     PRMX                                                             
*                                                                               
PRM20    TM    TGUSTYST,MAJORS     IF NO MAJORS/UPGRADE                         
         BO    PRM25                                                            
         LA    R1,RI11             POINT TO BEGINING OF FIELD                   
         B     PRM30                                                            
*                                                                               
PRM25    LA    R1,RI11+L'RI11      ELSE IF MAJ/UPG GO TO END OF FIELD           
         GOTOR SHUFFLE2            SHUFFLE BACK                                 
         MVI   0(R1),C'+'                                                       
         LA    R1,2(R1)                                                         
*                                                                               
PRM30    LR    RF,R1                                                            
         LH    R1,HALF             OUTPUT NUMBER OF UNITS                       
*        BAS   RE,PEDIT4NZ                                                      
         EDIT  (R1),(4,(RF)),ZERO=NOBLANK,ALIGN=LEFT                            
*                                                                               
         LA    R1,RI11+L'RI11      AND THE WORD 'UNITS' AFTERWARDS              
         GOTOR SHUFFLE2                                                         
         MVC   0(5,R1),LTUNI                                                    
         TM    TGUSTYST,INSERTS    IF INSERTS - USE CORRECT OWRDING             
         BZ    PRMX                                                             
         MVC   0(8,R1),LTINSERT                                                 
*                                                                               
PRMX     XIT1                                                                   
         LTORG                                                                  
         EJECT                                                                  
*                                                                               
*        DECIDE WHERE TO PUT 'COPY'                                             
*                                                                               
PUTCPY   NTR1  BASE=*,LABEL=*                                                   
         MVI   CPY,C'Y'                                                         
         MVC   R21(4),LTCOPY                                                    
         ZIC   R1,COPY                                                          
         LA    RF,RI21                                                          
*        BAS   RE,PEDIT2B                                                       
         EDIT  (R1),(2,(RF)),ZERO=BLANK,ALIGN=LEFT                              
         XIT1                                                                   
         LTORG                                                                  
         SPACE 2                                                                
*                                                                               
*        PRINT PHOTOGRAPHER FOR PRINT INVOICES                                  
*                                                                               
PRPHOTO  NTR1  BASE=*,LABEL=*                                                   
         MVI   PHOTO,C'Y'                                                       
         CLI   TBPRTINV,C'Y'       ONLY PRINT FOR PRINT INVOICES                
         BNE   PPHNO                                                            
         MVC   TGNAME,SPACES                                                    
         MVC   TGNAME(L'TBPHFFNM),TBPHFFNM                                      
         CLC   TGNAME,SPACES       IF THERE'S A FREE FORM NAME                  
         BH    PPH10               USE IT                                       
         OC    TBPHOTO,TBPHOTO                                                  
         BZ    PPHYES                                                           
         MVC   AIO,AIO2                                                         
         GOTO1 RECVAL,DMCB,TLW4CDQ,(X'AC',TBPHOTO),0                            
         MVC   AIO,AIO1                                                         
         BNE   PPHYES                                                           
*                                                                               
PPH10    MVC   R11(8),LTPHOTO                                                   
         MVC   RI11(29),TGNAME                                                  
PHX      B     PPHYES                                                           
*                                                                               
PPHYES   SR    RC,RC               SET CONDITION CODE                           
PPHNO    LTR   RC,RC                                                            
         XIT1                                                                   
         LTORG                                                                  
         EJECT                                                                  
*                                                                               
*        PRINT AREA FOR PRINT INVOICES                                          
*                                                                               
PRAREA   NTR1  BASE=*,LABEL=*                                                   
         MVI   PAREA,C'Y'                                                       
         CLI   TBPRTINV,C'Y'       ONLY PRINT FOR PRINT INVOICES                
         BNE   PARNO                                                            
         MVC   R11(4),LTAREA                                                    
         MVC   AIO,AIO2                                                         
         GOTO1 RECVAL,DMCB,TLARCDQ,(X'AC',TBAREA),0                             
         MVC   AIO,AIO1                                                         
         BNE   PARYES                                                           
         MVC   RI11(28),TGNAME                                                  
         B     PARYES                                                           
*                                                                               
PARYES   SR    RC,RC               SET CONDITION CODE                           
PARNO    LTR   RC,RC                                                            
         XIT1                                                                   
         LTORG                                                                  
         SPACE 2                                                                
*                                                                               
*        PRINT USE FOR PRINT INVOICES                                           
*                                                                               
PRPUSE   NTR1  BASE=*,LABEL=*                                                   
         MVI   USE,C'Y'                                                         
         CLI   TBPRTINV,C'Y'       ONLY PRINT FOR PRINT INVOICES                
         BNE   PPUNO                                                            
         MVC   R11(3),LTUSE                                                     
         MVC   AIO,AIO2                                                         
         GOTO1 RECVAL,DMCB,TLUSCDQ,(X'AC',TBPUSE),0                             
         MVC   AIO,AIO1                                                         
         BNE   PPUYES                                                           
         MVC   RI11(28),TGNAME                                                  
         B     PPUYES                                                           
*                                                                               
PPUYES   SR    RC,RC               SET CONDITION CODE                           
PPUNO    LTR   RC,RC                                                            
         XIT1                                                                   
         LTORG                                                                  
         EJECT                                                                  
*                                                                               
*        PRINT SPECIAL CODES CXL/BNP/CR                                         
*                                                                               
         USING P1LINED,R2                                                       
SPCLCDES NTR1  BASE=*,LABEL=*                                                   
         TM    STATUS,STBNP        IF BNP                                       
         BNO   SPCL10                                                           
         MVC   R21+8(3),LTBNP      PRINT BNP                                    
*                                                                               
SPCL10   TM    TBSTAT,TAINSCIN     IF INVOICE IS CANCELLED                      
         BNO   SPCL20                                                           
         MVC   R21+1(3),LTCXL        PRINT CANCELLED                            
*                                                                               
SPCL20   TM    STATUS,STCRDT       IF CREDIT                                    
         BNO   SPCLX                                                            
         MVC   R21+5(2),LTCR       PRINT CR                                     
*                                                                               
SPCLX    XIT1                                                                   
         DROP  R2                                                               
         LTORG                                                                  
         EJECT                                                                  
*                                                                               
*        SHUFFLE BACK TO 1ST NON BLANK                                          
*                ENTRY - R1 = END OF DATA                                       
*                EXIT  - R1 = 1ST NON BLANK + 1                                 
*                                                                               
         SPACE 1                                                                
SHUFFLE2 NTR1  BASE=*,LABEL=*                                                   
SHX1     CLI   0(R1),C' '          STOP AFTER 1ST BLANK                         
         BNE   SHX2                                                             
         BCT   R1,SHX1             BUMP TO NEXT CHAR                            
*                                                                               
SHX2     LA    R1,2(R1)            LEAVE 1 BLANK                                
         XIT1  REGS=(R1)                                                        
         LTORG                                                                  
         SPACE 3                                                                
*                                                                               
*                                                                               
*        EDIT A LINE - INPUT (R1)                                               
*                      OUTPUT LENGTH = 2                                        
*                      OUTPUT ADDR   = RF                                       
*                                                                               
         SPACE 1                                                                
PEDIT2B  EDIT  (R1),(2,(RF)),ZERO=BLANK,ALIGN=LEFT                              
         BR    RE                                                               
         SPACE 2                                                                
*                                                                               
*        EDIT A LINE - INPUT (R1)                                               
*                      OUTPUT LENGTH = 4                                        
*                      OUTPUT ADDR   = RF                                       
*                                                                               
         SPACE 1                                                                
PEDIT4   EDIT  (R1),(4,(RF)),ZERO=BLANK,ALIGN=LEFT                              
         BR    RE                                                               
         SPACE 2                                                                
*                                                                               
*        EDIT A LINE - INPUT (R1)                                               
*                      OUTPUT LENGTH = 4                                        
*                      OUTPUT ADDR   = RF                                       
*                                                                               
         SPACE 1                                                                
PEDIT4NZ EDIT  (R1),(4,(RF)),ZERO=NOBLANK,ALIGN=LEFT                            
         BR    RE                                                               
         SPACE 2                                                                
*                                                                               
*        EDIT A LINE - INPUT (R1)                                               
*                      OUTPUT LENGTH = 4                                        
*                      OUTPUT ADDR   = RF                                       
*                                                                               
         SPACE 1                                                                
PEDIT4A  EDIT  (R1),(4,(RF)),ALIGN=LEFT                                         
         BR    RE                                                               
         SPACE 2                                                                
COMPLM1  DC    X'FFFFFFFFFFFF'                                                  
LTPAYT   DC    C'Pay Type'                                                      
LTUSET   DC    C'Use Type'                                                      
LTINUM   DC    C'Invoice #'                                                     
LTCWK    DC    C'Cycle Wks'                                                     
LTCYC    DC    C'Cycle'                                                         
LTPAYPR  DC    C'Period'                                                        
LTMAIN   DC    C'Totl Uses'                                                     
LTLUSES  DC    C'Lift Uses'                                                     
LTORIG   DC    C'Original'                                                      
LTMAJ1   DC    C'Majors  '                                                      
LTAREAS  DC    C'Areas   '                                                      
LTUPGR   DC    C'Upgrade '                                                      
LTUNI    DC    C'Units   '                                                      
LTINSERT DC    C'Inserts '                                                      
LTTAGS   DC    C'Tags    '                                                      
LTDEMS   DC    C'Demos   '                                                      
LTCOPY   DC    C'Copy'                                                          
LTPHOTO  DC    C'Phtgrphr'                                                      
LTUSE    DC    C'Use'                                                           
LTAREA   DC    C'Area'                                                          
LTBNP    DC    C'BNP'                                                           
LTCXL    DC    C'CXL'                                                           
LTCR     DC    C'CR'                                                            
*                                                                               
         EJECT                                                                  
         LTORG                                                                  
         DROP  RB,R7                                                            
         EJECT                                                                  
*                                                                               
*        PRINT A LINE                                                           
*                                                                               
         DS    0D                                                               
PRINTIT  NMOD1 0,*PRIN*                                                         
         L     RC,0(R1)                                                         
         USING GEND,RC                                                          
*                                                                               
         CLI   LASTPG,C'Y'         IF LAST PAGE                                 
         BE    PRNT30              PRINT IT                                     
         OC    TBVINV,TBVINV       IF SPECIFICALLY REQUESTED                    
         BNZ   PRNT30              PRINT IT                                     
*                                                                               
         TM    TBISTA2,TAPDSSUB    IF SUBSIDIARY INVOICE                        
         BNO   PRNT05                                                           
         CLI   RECNUM,BI           ON BILLING COPY                              
         BE    *+12                                                             
         CLI   RECNUM,PB           AND PRINT BILLING                            
         BNE   PRNT05                                                           
         TM    TBOPTS,TBRERUN      RERUN                                        
         BO    PRNT20              DON'T PRINT IT                               
         TM    TBSTAT,TAINSCIN     OR CANCEL(LER) INVOICE                       
         BO    PRNT20              DON'T PRINT IT                               
         TM    TBSTA2,TAINSHLR     OR RELEASED COD INVOICE                      
         BO    PRNT20              DON'T PRINT IT                               
*                                                                               
PRNT05   CLI   UNICOPY,C'Y'        IF THIS IS A UNION COPY                      
         BE    PRNT30                 PRINT IT (INVOICES BY CHECK DATE)         
         TM    TBSTA2,TAINSHLR     IF THIS IS A RELEASED INVOICE                
         BO    PRNT10                                                           
         TM    TBSTAT,TAINSHLD     OR IF THIS IS A COD INVOICE                  
         BNO   PRNT30                                                           
*                                                                               
PRNT10   TM    TBSTA2,TAINSHLP     THAT WASN'T PRINTED YET                      
         BNO   PRNT30                                                           
         CLC   TGTODAY1,TBHDTE     OR THAT WAS PRINTED TODAY                    
         BE    PRNT30              PRINT IT                                     
*                                                                               
PRNT20   MVC   P,SPACES            ELSE DON'T PRINT IT                          
         MVC   P2,SPACES                                                        
         B     PRNTX                                                            
*                                                                               
PRNT30   GOTO1 SPOOL,DMCB,(R8)     THEN PRINT IT                                
*                                                                               
         CLI   RECNUM,EB           EBILL?                                       
         BNE   PRNTX                                                            
         LA    RF,TBILLD                                                        
         AH    RF,=Y(TBOPTS3-TBILLD)   RF -> TBOPTS3                            
         OI    0(RF),TBECPYPR      STATUS - INVOICES ON PRINT QUEUE             
*                                                                               
PRNTX    MVI   SPACING,1           RESET SPACING                                
         XIT1                                                                   
         SPACE 3                                                                
         LTORG                                                                  
         EJECT                                                                  
*        GOTO PRINT ROUTINE                                                     
*                                                                               
PRTIT    NTR1  BASE=*,LABEL=*                                                   
         GOTO1 =A(PRINTIT),DMCB,(RC)                                            
         J     XIT                                                              
                                                                                
         LTORG                                                                  
         EJECT                                                                  
*                                                                               
*        SUM SUBTOTAL AND TOTALS OF INVOICE                                     
*                         R1 = AMOUNT TO ADD                                    
*                                                                               
         SPACE 1                                                                
SUMTOT   NTR1  BASE=*,LABEL=*                                                   
         L     R4,TBTOT            INVOICE TOTAL                                
         AR    R4,R1                                                            
         ST    R4,TBTOT                                                         
         L     R4,TBSTOT           SUBTOTAL                                     
         AR    R4,R1                                                            
         ST    R4,TBSTOT                                                        
         J     XIT                                                              
         EJECT                                                                  
*        GET BILLING DETAILS ELEMENT                                            
*        ON ENTRY ... P1 = A(INVOICE RECORD)                                    
                                                                                
GETBDEL  NTR1  BASE=*,LABEL=*                                                   
         L     R4,0(R1)            R4=A(INVOICE RECORD)                         
                                                                                
         CLI   RECNUM,AC           IF AGENCY COPY                               
         JE    GBE10                                                            
         CLI   RECNUM,EB           OR E-BILLING                                 
         JNE   GBE20                                                            
                                                                                
GBE10    TM    TBCLSTA2,TACISTCA   DOES CLIENT HAVE CAN STATUS?                 
         BO    *+12                                                             
         TM    TBAYSTA6,TAAYSTCA   DOES AGENCY HAVE CAN STATUS?                 
         JZ    GBE20                                                            
         TM    TBISTAT,TAPDSCAN    US$ INVOICE?                                 
         BNZ   GBE20                                                            
         MVI   ELCODE,TABDELQ4     FIRST ATTEMPT TO GET BILLING                 
         BRAS  RE,GETEL            DETAILS IN CAN$                              
         JE    GBEYES                                                           
         L     R4,0(R1)                                                         
                                                                                
GBE20    CLI   RECNUM,AC           IF AGENCY COPY                               
         JNE   GBE30                                                            
         MVI   ELCODE,TABDELQ3     FIRST ATTEMPT TO GET BILLING                 
         BRAS  RE,GETEL            DETAILS IN EUROS                             
         JE    GBEYES                                                           
                                                                                
         L     R4,0(R1)                                                         
GBE30    MVI   ELCODE,TABDELQ      ELSE ATTEMPT TO GET REGULAR                  
         BRAS  RE,GETEL            BILLING DETAILS ELEMENT                      
         JNE   GBENO                                                            
                                                                                
GBEYES   XR    RC,RC                                                            
GBENO    LTR   RC,RC                                                            
         XIT1  REGS=(R4)                                                        
                                                                                
         LTORG                                                                  
         EJECT                                                                  
                                                                                
*        GET CAN$ TABD ELEMENT IF CAN STATUS ON AND ACOPY/EBILL                 
*        OTHERWISE GET REGULAR TABD ELEMENT                                     
*        ON ENTRY ... P1 = A(INVOICE RECORD)                                    
                                                                                
GETBDCN  NTR1  BASE=*,LABEL=*                                                   
         L     R4,0(R1)            R4=A(INVOICE RECORD)                         
                                                                                
         CLI   RECNUM,AC           IF AGENCY COPY                               
         JE    GBDCN10                                                          
         CLI   RECNUM,EB           OR E-BILLING                                 
         JNE   GBDCN30                                                          
                                                                                
GBDCN10  TM    TBCLSTA2,TACISTCA   DOES CLIENT HAVE CAN STATUS?                 
         BO    *+12                                                             
         TM    TBAYSTA6,TAAYSTCA   DOES AGENCY HAVE CAN STATUS?                 
         JZ    GBDCN30                                                          
         TM    TBISTAT,TAPDSCAN    US$ INVOICE?                                 
         BNZ   GBDCN30                                                          
         MVI   ELCODE,TABDELQ4     FIRST ATTEMPT TO GET BILLING                 
         BRAS  RE,GETEL            DETAILS IN CAN$                              
         JE    GBCYES                                                           
                                                                                
         L     R4,0(R1)                                                         
GBDCN30  MVI   ELCODE,TABDELQ      ELSE ATTEMPT TO GET REGULAR                  
         BRAS  RE,GETEL            BILLING DETAILS ELEMENT                      
         JNE   GBCNO                                                            
                                                                                
GBCYES   XR    RC,RC                                                            
GBCNO    LTR   RC,RC                                                            
         XIT1  REGS=(R4)                                                        
                                                                                
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
*        GET CHLOE COPY TABD ELEMENT FROM INVOICE RECORD              *         
*        RETURN CC EQUAL IF BILLING TYPE IS 9 OR 13                   *         
***********************************************************************         
         USING TABDD,R4                                                         
GETBTYP2 NTR1  BASE=*,LABEL=*                                                   
         L     R4,AIO1                                                          
         MVI   ELCODE,TABDELQ2                                                  
         BRAS  RE,GETEL                                                         
         JNE   NO                                                               
         CLI   TABDTYPE,TABRTY9    TYPES 9 & 13 ALREADY COUNTED                 
         JE    YES                                                              
         CLI   TABDTYPE,TABRTY13   HANDLING IN TAX/HANDLING FIELD               
         JE    YES                                                              
         J     NO                                                               
                                                                                
         DROP  R4                                                               
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
*        ROUTINE TO SET COLUMNS                                       *         
***********************************************************************         
                                                                                
         USING BOXD,R3                                                          
BXCOL    NTR1  BASE=*,LABEL=*                                                   
         TM    LSTAT,PRB                                                        
         JNO   BXC10                                                            
         LA    R2,BOXCOLS                                                       
         USING P5LINED,R2          P & H REMITTANCE                             
         CLI   TBPRTINV,C'Y'       LEAVE COLUMN EMPTY FOR PRINT                 
         JE    BXC05                                                            
         CLI   TBMED,TACOMEDE      IF MEDIA = EVENT, DIFFERENT COLUMNS          
         JNE   BXC03                                                            
         MVI   P5CB10,C'C'                                                      
         MVI   P5CB11,C'C'                                                      
         MVI   P5CB12,C'C'                                                      
         MVI   P5CB13,C'C'                                                      
         J     BXC10                                                            
                                                                                
BXC03    MVI   P5CB2,C'C'                                                       
         MVI   P5CB3,C'C'                                                       
                                                                                
BXC05    MVI   P5CB4,C'C'                                                       
         MVI   P5CB5,C'C'                                                       
         MVI   P5CB6,C'C'                                                       
         MVI   P5CB7,C'C'                                                       
         MVI   P5CB8,C'C'                                                       
                                                                                
BXC10    TM    LSTAT,PRB2                                                       
         JNO   BXC20                                                            
         LA    R2,BOXCOLS                                                       
         USING P6LINED,R2           COMPANY                                     
         CLI   TBPRTINV,C'Y'                                                    
         JE    BXC15                                                            
         CLI   TBMED,TACOMEDE      IF MEDIA = EVENT, SKIP                       
         JE    BXC15                                                            
         MVI   P6CB2,C'C'                                                       
         MVI   P6CB3,C'C'                                                       
                                                                                
BXC15    MVI   P6CB4,C'C'                                                       
         J     XIT                                                              
                                                                                
BXC20    TM    LSTAT,CHK           WHICH BOX TOP ? CHECK                        
         JNO   BXC30                                                            
         LA    R2,BOXCOLS                                                       
         USING P4LINED,R2          CHECK HEADING LINE                           
         MVI   CO2,C'C'            INSERT CENTER COLUMNS                        
         MVI   CO3,C'C'                                                         
         CLI   TBMED,TACOMEDE                                                   
         JE    BXC29                                                            
         MVI   CO4,C'C'                                                         
         MVI   CO5,C'C'                                                         
         MVI   CO6,C'C'                                                         
         MVI   CO7,C'C'                                                         
         MVI   CO8,C'C'                                                         
         CLI   TBPRTINV,C'Y'       SET COLUMNS FOR PRINT                        
         JNE   BXC22                                                            
         MVI   PCO9,C'C'                                                        
         MVI   PCO10,C'C'                                                       
         MVI   PCO11,C'C'                                                       
         MVI   PCO12,C'C'                                                       
         J     BXC27                                                            
                                                                                
BXC22    MVI   CO9,C'C'                                                         
         MVI   CO10,C'C'                                                        
         MVI   CO11,C'C'                                                        
         TM    TGUSSTA3,NWKUSE     IF NETWORK USAGE (CLA,LNA,LNN,LNC)           
         JO    BXC24                                                            
         CLI   TGUSEQU,UVNR        IF VERSION NON-RENDERED                      
         JE    BXC25                                                            
         CLI   TGUSEQU,UVRE        OR VERSION RENDERED                          
         JE    BXC25                                                            
         TM    TGUSSTAT,SESSION    IF SESSION                                   
         JZ    BXC25                                                            
         MVI   CO11A,C'C'             PUT COL                                   
BXC24    MVI   CO12,C'C'                                                        
BXC25    MVI   CO13,C'C'                                                        
         MVI   CO14,C'C'                                                        
         MVI   CO15,C'C'                                                        
         MVI   CO16,C'C'                                                        
                                                                                
BXC27    MVI   CO17,C'C'                                                        
BXC28    MVI   CO18,C'C'                                                        
         MVI   CO19,C'C'                                                        
         MVI   CO20,C'C'                                                        
         MVI   CO21,C'C'                                                        
BXC29    CLI   TBMED,TACOMEDE                                                   
         JNE   XIT                                                              
         MVI   PPCO1,C'C'                                                       
         MVI   PPCO2,C'C'                                                       
         MVI   PPCO3,C'C'                                                       
         MVI   PPCO4,C'C'                                                       
         J     XIT                                                              
         DROP  R2                                                               
*                                                                               
BXC30    TM    LSTAT,ITOT          WHICH BOX TOP ? INVOICE TOTAL                
         JNO   BXC40                                                            
         LA    R2,BOXCOLS                                                       
         USING P7LINED,R2          INV TOTAL LINE                               
*        MVI   P7CB2,C'C'          INSERT CENTER COLUMNS                        
         J     XIT                                                              
         DROP  R2                                                               
*                                                                               
BXC40    TM    LSTAT,CENT          WHICH BOX TOP ? CLASS A                      
         JNO   XIT                                                              
         LA    R2,BOXCOLS                                                       
         USING P8LINED,R2          INV TOTAL LINE                               
         MVI   P8CB1,C'C'          INSERT CENTER COLUMNS                        
         MVI   P8CB2,C'C'                                                       
         MVI   P8CB3,C'C'                                                       
         MVI   P8CB4,C'C'                                                       
         J     XIT                                                              
         DROP  R3,R2                                                            
                                                                                
***********************************************************************         
*        CONSTANTS AND LITERALS                                       *         
***********************************************************************         
                                                                                
         LTORG                                                                  
         EJECT                                                                  
         USING P10LINED,R2                                                      
         USING TABCHKD,R3                                                       
PRRET    NTR1  BASE=*,LABEL=*                                                   
         TM    TBISTA3,TAPDSC16    CONTRACT YEAR 16?                            
         JZ    XIT                                                              
         MVC   P10RET,LTRETRO                                                   
         J     XIT                                                              
         DROP  R2,R3                                                            
***********************************************************************         
*        CONSTANTS AND LITERALS                                       *         
***********************************************************************         
LTRETRO  DC    C'* WAGES MAY BE SUBJECT TO RETROACTIVE INCREASE UPON RA+        
               TIFICATION OF ANY NEW 2016 CONTRACT *'                           
         LTORG                                                                  
         EJECT                                                                  
       ++INCLUDE DDDLCB                                                         
         EJECT                                                                  
       ++INCLUDE TABIDSECT                                                      
         EJECT                                                                  
       ++INCLUDE TABILLD                                                        
         EJECT                                                                  
*                                                                               
*        DSECT TO COVER TALENT MASTER RATE TABLE                                
*                                                                               
TLMSTBD  DSECT                                                                  
TLMSRATE DS    H                   SURCHARGE RATE (XX.XX %)                     
TLMSEFST DS    XL3                 EFFECTIVE DATE START                         
TLMSEFED DS    XL3                 EFFECTIVE DATE END                           
TLMSLNQ  EQU   *-TLMSTBD                                                        
         SPACE 3                                                                
*              DSECT FOR CNET/MKT/CSYS CODES AND THEIR DATES                    
PHEADS   DSECT                                                                  
PDATE    DS    CL8                                                              
         DS    CL1                                                              
PNAME    DS    CL50                                                             
PHEADLEN EQU   *-PHEADS                                                         
*              DSECT FOR INTERNET/NEW MEDIA                                     
         ORG   PDATE                                                            
PCODE2   DS    CL4                                                              
         DS    CL2                                                              
PNAME2   DS    CL30                                                             
         DS    CL15                                                             
PHEA2LEN EQU   *-PHEADS                                                         
         EJECT                                                                  
*&&DO                                                                           
*              DSECT TO COVER YEARTAB                                           
YEARD    DSECT                                                                  
YEARUN   DS    XL1                 UNION(S)                                     
YEARYR   DS    XL1                 CONTRACT YEAR(S)                             
CANRATE1 EQU   X'72'               OVERLAY FOR YEAR 1 CANADIAN RATES            
CANRATE2 EQU   X'71'               OVERLAY FOR YEAR 2 CANADIAN RATES            
CANRATE3 EQU   X'70'               OVERLAY FOR YEAR 3 CANADIAN RATES            
SORRATES EQU   X'6B'               OVERLAY FOR SOAP RESIDUAL RATES              
YEARPHAS DS    XL1                 CONTRACT RATE PHASE NUMBER                   
YEARFHNW DS    XL2                 FIXED H&W AMOUNT                             
YEARPNHT DS    XL2                 P&H RATE FOR TV                              
YEARPNHR DS    XL2                 P&H RATE FOR RADIO                           
YEARPHMT DS    XL2                 AFM P&H RATE FOR TV                          
YEARPHMR DS    XL2                 AFM P&H RATE FOR RADIO                       
YEARNEXT EQU   *                                                                
         EJECT                                                                  
*&&                                                                             
*              DSECT TO COVER FILE RECORD                                       
RECD     DSECT                                                                  
REC      DS    0CL480                                                           
RECTYP   DS    CL1                 RECORD TYPE                                  
RECTYPH  EQU   C'H'                HEADING RECORD                               
RECTYPB  EQU   C'B'                BOX HEADING RECORD                           
RECTYPC  EQU   C'C'                MORE HEADINGS RECORD                         
RECTYPM  EQU   C'M'                COMMENT RECORD                               
RECTYPA  EQU   C'A'                CLASS A RECORD                               
RECTYPP  EQU   C'P'                PERFORMER DETAILS RECORD                     
RECTYPR  EQU   C'R'                PERFORMER DETAILS PRINT RECORD               
RECTYPT  EQU   C'T'                TOTALS RECORD                                
*                                                                               
*                                  HEADING RECORD                               
RECAGYN  DS    CL36                AGENCY NAME                                  
RECGST   DS    CL10                GST NUMBER                                   
RECADD1  DS    CL30                ADDRESS LINE 1                               
RECADD2  DS    CL30                ADDRESS LINE 2                               
RECADD3  DS    CL30                ADDRESS LINE 3                               
RECADD4  DS    CL30                ADDRESS LINE 4                               
RECATTN  DS    CL36                ATTENTION NAME                               
RECCLIN  DS    CL36                CLIENT NAME                                  
RECFAIR  DS    CL8                 FIRST AIR DATE                               
RECPRDN  DS    CL36                PRODUCT NAME                                 
RECFFCY  DS    CL8                 FIRST FIXED CYCLE                            
RECCOM   DS    CL36                COMMERCIAL TITLE                             
RECEXP   DS    CL8                 EXPIRATION DATE                              
RECCID   DS    CL12                COMMERCIAL ID                                
RECCID2  DS    CL12                2ND COMMERCIAL ID                            
RECVER   DS    CL1                 VERSION CODE                                 
RECVID   DS    CL12                VERSION ID                                   
RECLFT   DS    CL12                LIFT ID                                      
         DS    CL96                SPARE                                        
*                                                                               
*                                  BOX HEADING RECORD                           
         ORG   RECAGYN                                                          
RECCLI   DS    CL6                 CLIENT CODE                                  
RECINV   DS    CL6                 INVOICE NUMBER                               
RECSPEC  DS    CL10                SPECIAL CODES                                
RECBDAT  DS    CL8                 BILLING DATE                                 
RECMED   DS    CL5                 MEDIA                                        
RECPONM  DS    CL16                PO NUMBER                                    
RECESTN  DS    CL16                ESTIMATE NUMBER                              
RECESTP  DS    CL1                 ESTIMATE PERIOD                              
RECVEN   DS    CL12                VENDOR                                       
RECCOLN  DS    CL3                 COMMERCIAL LENGTH                            
RECLFLN  DS    CL2                 LIFT LENGTH                                  
RECPTYP  DS    CL7                 PAY TYPE                                     
RECUTYP  DS    CL16                USE TYPE                                     
RECUNUM  DS    CL7                 USE NUMBERS OR AUTO (ITN ONLY)               
RECCWKS  DS    CL16                CYCLE WEEKS                                  
RECCSTE  DS    CL17                CYCLE START AND END DATES                    
RECMUSE  DS    CL11                MAIN USES                                    
RECLUSE  DS    CL11                LIFT USES                                    
RECLBL   DS    CL8                 LABEL FOR NEXT FIELD                         
RECMAJ   DS    CL27                MAJORS/UNITS/INSERTS/TAGS/DEMOS              
RECUMAJ  DS    CL27                UPGRADE MAJORS/UNITS/INSERTS                 
RECPHOT  DS    CL29                PHOTOGRAPHER                                 
RECAREA  DS    CL28                AREA                                         
RECUSE   DS    CL28                USE                                          
RECUNI1  DS    CL3                 UNION 1                                      
RECUNI2  DS    CL3                 UNION 2                                      
RECUNI3  DS    CL3                 UNION 3                                      
RECHOTT  DS    CL1                 HEADING FOR OT&TRAVL/HR.MN/USE RANGE         
RECHPNH  DS    CL1                 HEADING FOR P&H COLUMN                       
RECHHNW  DS    CL1                 HEADING FOR H&W COLUMN                       
         DS    CL150               SPARE                                        
*                                                                               
*                                  MORE HEADINGS RECORD                         
         ORG   RECAGYN                                                          
RECFDAT  DS    CL8                 FILM DATE                                    
RECFSTU  DS    CL12                FILM STUDIO                                  
RECFCTY  DS    CL12                FILM CITY                                    
RECSDAT  DS    CL8                 SHOOT DATE                                   
RECSSTU  DS    CL12                SHOOT STUDIO                                 
RECSCTY  DS    CL12                SHOOT CITY                                   
RECRDAT  DS    CL8                 RECORDING DATE                               
RECRSTU  DS    CL12                RECORDING STUDIO                             
RECRCTY  DS    CL12                RECORDING CITY                               
RECMDAT  DS    CL8                 MUSIC DATE                                   
RECMSTU  DS    CL12                MUSIC STUDIO                                 
RECMCTY  DS    CL12                MUSIC CITY                                   
RECCONT  DS    CL48                CONTRACTS 1,2,3,4                            
RECCOM1  DS    CL60                COMMENT 1                                    
RECCOM2  DS    CL73                COMMENT 2                                    
RECEMP   DS    CL36                EMPLOYER OF RECORD                           
RECTPC   DS    CL8                 TPC USER ID                                  
RECCTYP  DS    CL7                 COMMERCIAL TYPE NAME                         
         DS    CL119               SPARE                                        
*                                                                               
*                                  COMMENT RECORD                               
         ORG   RECAGYN                                                          
RECCMT   DS    CL80                1 LINE OF COMMENT                            
         DS    CL399               SPARE                                        
*                                                                               
*                                  CLASS A RECORD                               
         ORG   RECAGYN                                                          
RECCLA   DS    CL116               29*4=116 DATE,PROGRAM,LIFT,NETWRK            
         DS    CL363               SPARE                                        
*                                                                               
*                                  PERFORMER DETAILS                            
         ORG   RECAGYN                                                          
RECPER1  DS    CL130               PRINT LINE 1                                 
RECPER2  DS    CL59                PRINT LINE 2                                 
         DS    CL290               SPARE                                        
*                                                                               
*                                  PERFORMER DETAILS - PRINT                    
         ORG   RECAGYN                                                          
RECPPER1 DS    CL130               PRINT LINE 1                                 
RECPPER2 DS    CL59                PRINT LINE 2                                 
RECPPRNT DS    CL36                PRINT INFO                                   
         DS    CL254               SPARE                                        
*                                                                               
*                                  TOTALS RECORD                                
         ORG   RECAGYN                                                          
RECGROS  DS    CL11                GROSS WAGES                                  
RECMISC  DS    CL10                MISC. PAYMENT                                
RECPNH   DS    CL7                 P & H                                        
RECHNW   DS    CL7                 H & W                                        
RECWAGM  DS    CL11                WAGES AND MISC PAYMENTS                      
RECPTAX  DS    CL11                PAYROLL TAXES                                
RECPNHC  DS    CL11                P & H CONTRIBUTION                           
RECHAND  DS    CL11                HANDLING                                     
RECEMS   DS    CL11                EMS FEE                                      
RECITOT  DS    CL11                INVOICE TOTAL                                
RECUGST  DS    CL11                GST PAID ON US INVOICES                      
RECCSF   DS    CL11                CONTRACT SERVICE FEE                         
         DS    CL356               SPARE                                        
RECLNQ   EQU   *-RECD                                                           
         EJECT                                                                  
*                                                                               
*        OTHER DSECTS ARE HIDDEN IN HERE                                        
         SPACE 3                                                                
*TAREPFFD                                                                       
*DDGENTWA                                                                       
*DDSPOOLD                                                                       
*DDSPLWORKD                                                                     
*DDREMOTED                                                                      
*DDBIGBOX                                                                       
*DDMASTD                                                                        
*DDTWADCONS                                                                     
*DDLOGOD                                                                        
*TAGENFILE                                                                      
*CTGENFILE                                                                      
*TASYSDSECT                                                                     
*TASYSEQUS                                                                      
*TAREPWORKD                                                                     
         PRINT OFF                                                              
       ++INCLUDE TAREPFFD                                                       
       ++INCLUDE DDGENTWA                                                       
       ++INCLUDE DDSPOOLD                                                       
       ++INCLUDE DDSPLWORKD                                                     
       ++INCLUDE DDREMOTED                                                      
       ++INCLUDE DDBIGBOX                                                       
       ++INCLUDE DDMASTD                                                        
TWADCOND DSECT                                                                  
       ++INCLUDE DDTWADCONS                                                     
       ++INCLUDE DDLOGOD                                                        
       ++INCLUDE TAGENFILE                                                      
       ++INCLUDE CTGENFILE                                                      
       ++INCLUDE TASYSDSECT                                                     
       ++INCLUDE TASYSEQUS                                                      
       ++INCLUDE TAREPWORKD                                                     
         PRINT ON                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'218TABIPRT   07/10/17'                                      
         END                                                                    
