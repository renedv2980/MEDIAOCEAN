*          DATA SET PPRUNCON   AT LEVEL 018 AS OF 11/12/13                      
*CATALP PPRUNCON                                                                
         ENTRY EMLPUT                                                           
*******************************************************************             
*                                                                 *             
* THIS IS AN OS COMPATIBLE VERSION.                               *             
*                                                                 *             
*******************************************************************             
*                                                                               
*      CHANGE LOG                                                               
*                                                                               
*   BPLA 11/13    SYSNAID - 2 CHARACTER SYSTEMS                                 
*                                                                               
*   BPLA 04/02    CHANGES FOR CORE-RESIDENT PUBVAL AND PUBEDIT                  
*                                                                               
*   DEIS 01/02    CHANGE SPLOAD TO BE COMPATIBLE WITH NEW DDMASTIDF             
*                                                                               
*   SMYE 06/01    CHANGES FOR UPDATIVE SOONS                                    
*                                                                               
*   BPLA  10/00   COPY OF PPRUNCON MADE 10/30/00                                
*                 LEVEL RESET TO 1                                              
*                                                                               
*   BPLA 12/97    CHANGE TO USE CORE-RESIDENT GETINS                            
*                                                                               
*   BPLA 7/16/96  CHANGE FOR EXPANDED SSB                                       
*                                                                               
*   BPLA 2/7/94 CHANGE TO CLOSE SECOND PRINT                                    
*                                                                               
*******************************************************************             
         TITLE 'MODULE TO CONTROL PPG RUN'                                      
PPRUNCON CSECT                                                                  
         PRINT NOGEN                                                            
         NBASE 0,PPRUNCON,=V(PPSAVE)                                            
         L     RA,=V(PPWRKC)                                                    
         USING PPWORKD,RA                                                       
         L     R8,LOGOC                                                         
         USING LOGOD,R8                                                         
         L     R9,=V(MASTC)                                                     
         USING MASTD,R9                                                         
         EJECT                                                                  
*******************************************************************             
*                  INITIAL CONTROL CARDS                          *             
*******************************************************************             
*&&DO*&& MVC   MCAPHAS1,PPSPECS                                                 
*&&DO*&& MVC   MCAPHAS2,PPAPPLIC                                                
         ST    RA,MCAWORK                                                       
         GOTO1 =V(RUNSTART)                                                     
*                            GET CORE-RESIDENT VERSION OF GETINS                
         MVI   BYTE,QGETINS                                                     
         BAS   RE,SPLOAD                                                        
         MVC   GETINS,4(R1)                                                     
*                            GET CORE-RESIDENT VERSION OF PUBVAL                
         MVI   BYTE,QPUBVAL                                                     
         BAS   RE,SPLOAD                                                        
         MVC   PUBVAL,4(R1)                                                     
*                            GET CORE-RESIDENT VERSION OF PUBEDIT               
         MVI   BYTE,QPUBEDIT                                                    
         BAS   RE,SPLOAD                                                        
         MVC   PUBEDIT,4(R1)                                                    
*                                                                               
*&&OS*&& MVC   PPSPECS,MCAPHAS1                                                 
*&&OS*&& MVC   PPAPPLIC,MCAPHAS2                                                
         MVC   RCTRACE,MCTRACE                                                  
         MVC   RCDUMP,MCDUMP                                                    
         MVC   RCWRITE,MCWRITE                                                  
         MVC   RCDATE,MCDATE                                                    
         MVC   RCSPECNO,MCNUMBER                                                
         MVC   RCPROG,MCPROG                                                    
         MVC   RCORIGID,MCORIGID                                                
         MVC   ORIGNAME,MCORIGIN                                                
         MVC   ORIGADDR,MCORIGAD                                                
         MVC   RCRECOVR,MCRECOVR                                                
         MVC   RCRQFILT,MCREQCOD                                                
         CLC   MCREQCOD,SPACES                                                  
         BNE   *+10                                                             
         MVC   RCRQFILT,MCPROG                                                  
         CLC   MCREQCOD,SPACES                                                  
         BNE   *+10                                                             
         MVC   RCRQFILT,MCPROG                                                  
         MVC   RCMEDFLT,MCMEDIA                                                 
         MVC   RCAGYFLT,MCUSER                                                  
**NEW 10/31/89                                                                  
         MVC   RCLANG,MCLANG       LANGUAGE                                     
         MVC   RCCTRY,MCCTRY       COUNTRY                                      
*                                  DO FILE SPECS                                
         MVI   RCFLONLY,YES                                                     
         MVI   FORCEHED,YES                                                     
         MVI   RCSUBPRG,0                                                       
         GOTO1 REPORT                                                           
         MVI   RCFLONLY,NO                                                      
*                                                                               
         USING SSBD,RF                                                          
         L     RF,=V(SSB)                                                       
         OI    SSOSTAT2,SSOSNRCV   NO RECOVERY                                  
*                                                                               
         CLI   RCWRITE,NO                                                       
         BE    OPEN2                                                            
         CLC   FCUPPRTD(4),=C'NNNN'                                             
         BE    OPEN2                                                            
*                                                                               
         CLI   RCRECOVR,NO                                                      
         BE    *+8                                                              
         NI    SSOSTAT2,255-SSOSNRCV   TURN RECOVERY BACK ON                    
         DROP  RF                                                               
*                                                                               
         CLI   RCRECOVR,NO         MIGHT BE RECOVERY=W                          
         BE    *+8                                                              
         MVI   FRECV,C'U'          UPDATE                                       
*                                                                               
         CLI   FCUPPRTD,YES        PRTDIR                                       
         BNE   *+8                                                              
         MVI   FPRTDIR,C'U'                                                     
*                                                                               
         CLI   FCUPPRTF,YES        PRTFIL                                       
         BNE   *+8                                                              
         MVI   FPRTFIL,C'U'                                                     
*                                                                               
         CLI   FCUPPUBD,YES        PUBDIR                                       
         BNE   *+8                                                              
         MVI   FPUBDIR,C'U'                                                     
*                                                                               
         CLI   FCUPPUBF,YES        PUBFIL                                       
         BNE   *+8                                                              
         MVI   FPUBFIL,C'U'                                                     
*                                                                               
OPEN2    DS    0H                                                               
         CLI   MCRECOVR,C'W'       TEST FACWK RECOVERY                          
         BNE   OPEN3                                                            
         GOTO1 =V(DATCON),DMCB,(4,MCDATE),(1,FULL)                              
*                                                                               
         USING SSBD,RF                                                          
         L     RF,=V(SSB)                                                       
         NI    SSOSTAT2,255-SSOSNRCV                                            
         OI    SSOSTAT2,SSOSRWRK    SET FACWK RECOVERY BIT IN SSB               
         L     R0,SSOFWBUF                                                      
         L     RF,SSOFWNDX                                                      
*                                                                               
         USING UKRECD,RF            SET FACWRK KEY VALUES                       
         MVC   UKUSRID,MCORIGID     USERID                                      
         MVC   UKSYSPRG(1),MCSYSTEM SYSTEM                                      
         MVC   UKSYSPRG+1(2),MCPROG PROGRAM                                     
         CLI   MCFACPAK,C' '                                                    
         BNH   *+10                                                             
         MVC   UKSUBPRG,MCFACPAK    FACPAK SYSTEM ID                            
         MVC   UKDAY,FULL+2         DAY NUMBER                                  
         MVI   UKCLASS,C'R'         CLASS=RECOVERY                              
         MVI   UKFLAG,X'01'         FLAG DUPLICATES ALLOWED                     
         MVC   UKFILNO,=X'FFFF'     LET WRKR KNOW UKFLAG IS VALID               
         DROP  RF                                                               
*                                                                               
OPEN3    DS    0H                                                               
         MVC   WORK(2),RCAGYFLT                                                 
         BAS   RE,GETAGINF         SET FILE NO IN UTL                           
*                                                                               
         L     R3,=V(PPFILEC)                                                   
         GOTO1 DATAMGR,DMCB,=C'OPEN',=C'PRINT',FILELIST,(R3)                    
*&&DO                                                                           
         COMRG                                                                  
         MVC   RCUPSI,23(R1)                                                    
         MVC   RCJOB,24(R1)                                                     
*&&                                                                             
*&&OS                                                                           
         LA    R2,DUB                                                           
         EXTRACT (R2),FIELDS=TIOT                                               
         MVC   RCJOB,0(R2)                                                      
         MVC   RCUPSI,MCUPSI                                                    
*&&                                                                             
OPEN4    CLI   RCLINEUP,YES                                                     
         BNE   START                                                            
**NEW 5/5/89                                                                    
         L     RF,=V(REMOTEC)                                                   
         USING REMOTED,RF                                                       
         OC    REMOTKEY,REMOTKEY        TEST DIRECT TO PRTQUE                   
         BNZ   START                                                            
         DROP  RF                                                               
         OC    MCREMPQK,MCREMPQK        TEST SOON JOB                           
         BNZ   START                                                            
**NEW 5/5/89                                                                    
         MVI   FORCEHED,YES                                                     
         MVI   SKIPSPEC,YES                                                     
         LA    R2,7                                                             
RC0D     DS    0H                                                               
         MVI   P,C'X'                                                           
         MVC   P+1(109),P          BACK TO 110                                  
         GOTO1 REPORT                                                           
         BCT   R2,RC0D                                                          
         B     START                                                            
                                                                                
*********************************************************************           
* FILE TO OPEN FOR PRINT SYSTEM                                                 
*********************************************************************           
FILELIST DC    0D                                                               
FPRTDIR  DC    CL8'NPRTDIR'                                                     
FPRTFIL  DC    CL8'NPRTFILE'                                                    
FPUBDIR  DC    CL8'NPUBDIR'                                                     
FPUBFIL  DC    CL8'NPUBFILE'                                                    
         DC    CL8'NCTFILE'                                                     
FRECV    DC    CL8'XPRECV1'                                                     
         DC    CL8'X'              TERMINATOR                                   
         EJECT ,                                                                
***********************************************************************         
*                   CONTROL DIVIDERS AND REQUESTS                               
***********************************************************************         
START    DS    0H                                                               
         CLI   LOGONAME,C' '       IF NO DESTINATION NAME                       
         BNH   GOREQS              NO DIVIDERS                                  
         GOTO1 LOGO,DMCB,LOGOC     START DIVIDER                                
*                                                                               
GOREQS   DS    0H                                                               
         GOTO1 VFILCON                                                          
         SR    R2,R2               SET FOR NORMAL TERMINATION                   
*                                                                               
FINALISE DS    0H                                                               
         CLI   LOGONAME,C' '                                                    
         BNH   CLOSE1                                                           
         MVI   LOGOTYPE,C'E'                                                    
         ZAP   LOGOREQS,RCRQVAL                                                 
         GOTO1 LOGO,DMCB,LOGOC                                                  
***********************************************************************         
*                      CLOSE FILES AND END JOB                        *         
***********************************************************************         
         USING SSBD,RF                                                          
CLOSE1   DS    0H                                                               
         L     RF,=V(SSB)                                                       
         TM    SSOSTAT2,SSOSRWRK   TEST FACWK RECOVERY                          
         BZ    CLOSE2                                                           
*                                                                               
         ICM   R7,15,SSOFWNDX      CLOSE FACWK FILE                             
         USING UKRECD,R7                                                        
         BNZ   *+6                                                              
         DC    H'0'                                                             
         LA    R0,=C'CLOSE'                                                     
         CP    MCNDUMPS,=P'0'                                                   
         BE    *+8                                                              
         LA    R0,=C'CLO/PUR'                                                   
         GOTO1 DATAMGR,DMCB,(X'00',(R0)),=C'FACWRK',(R7),MCIO,SSOFWBUF          
         DROP  RF                                                               
         CLI   8(R1),0                                                          
         BE    *+6                                                              
         DC    H'0'                ERROR ON FACWRK FILE                         
*                                                                               
CLOSE2   DS    0H                                                               
         CLIY  EMLFLAG,YES                                                      
         BNE   CLOSE3              NO                                           
         L     RF,=A(EMLCLSE)      CLOSE IT                                     
         BASR  RE,RF                                                            
*********************************************************************           
*        CALL RECOVERY IF YOU WANT TO UNWIND                                    
*********************************************************************           
CLOSE3   ESTAE 0                                                                
         CP    MCNDUMPS,=P'0'      ANY DUMPS THEN TRY RECOVERY                  
         BE    CLOSE4                                                           
         WTO   'INVOKING SYSTEM RECOVERY'                                       
         GOTO1 MCVDMGR,DMCB,=C'RECOVR'                                          
*********************************************************************           
*                 CLOSE SYSTEM FILES                                            
*********************************************************************           
CLOSE4   L     R3,=V(PPFILEC)                                                   
         GOTO1 DATAMGR,DMCB,=C'DMCLSE',=C'PRINT',FILELIST,(R3)                  
*                                                                               
         GOTO1 ,DMCB,=C'CLOSE'                                                  
         CP    MCNDUMPS,=P'0'                                                   
         BE    GOPRT                                                            
         GOTO1 ,DMCB,=C'CLO/ERR'                                                
*                                                                               
GOPRT    GOTO1 PRINT                                                            
         CLI   MCREPTS,2                                                        
         BNE   GOEND2                                                           
         GOTO1 =V(PRINTS2)                                                      
*                                                                               
GOEND2   DS    0H                                                               
         CP    MCNDUMPS,=P'0'                                                   
         BE    NORMEND                                                          
*AH3     ABEND 664                 I ADDED THIS ON JUN28/13.                    
*                                  NOT SURE WHY YET WHY NOT CODED?              
*                                                                               
*AH3     ESTAE 0                   MOVED THIS TO ABOVE                          
*                                                                               
*&&DO*&& CANCEL                                                                 
*&&OS*&& ABEND 999                                                              
NORMEND  DS    0H                                                               
         XBASE                                                                  
         EJECT ,                                                                
*****************************************************************               
*        ROUTINE TO EXTRACT FILE NUNBER FROM AGY                                
*****************************************************************               
GETAGINF NTR1                                                                   
         L     R3,PPFILEC                                                       
         USING CND,R3                                                           
         XC    CNLEN,CNLEN                                                      
         MVC   CNAGY,WORK                                                       
         CLC   RCORIGID,=X'002B'   IF TCH1 (OR NO ORIG ID GIVEN)                
         BE    GETAG3              SKIP SET OF ORIGIN ID                        
         CLC   RCORIGID,=X'005A'   IF SJR?   ORIG=90                            
         BE    GETAG3              SKIP SET OF ORIGIN ID                        
         MVC   CNID,RCORIGID                                                    
GETAG3   GOTO1 =V(CONFID),DMCB,CND,FULL                                         
         OC    FULL,FULL                                                        
         BNZ   *+6                                                              
         DC    H'0'                                                             
*                                                                               
         L     R5,UTL                                                           
         MVC   4(1,R5),CNPSE                                                    
         MVC   PRNTSE,CNPSE                                                     
         MVC   SPOTSE,CNSSE                                                     
         MVC   ACCTSE,CNASE                                                     
         MVC   SPOTCD,CNSCD                                                     
         MVC   ACCTCD,CNACD                                                     
*                                                                               
         MVC   SYSSE#,PRNTSE                                                    
         GOTO1 DATAMGR,DMCB,(0,=C'DDNAME'),SYSSE,0                              
         TM    8(R1),X'10'                                                      
         JO    *+2                 SYSTEM NOT FOUND                             
         L     RE,8(,R1)           A(DDNADATA)                                  
         USING DDNAMED,RE                                                       
*******  MVC   FILENUM,DDNASEN3+1  OLD CODE                                     
         MVC   FILENUM,DDNASEID    ONE CHARACTER SYSYTEM                        
         MVC   SYSNAID,DDNASEID    2 CHARACTER SYSTEM                           
         XIT1                                                                   
         DROP  RE                                                               
*                                                                               
DDNAME   DC    CL8'DDNAME'                                                      
SYSSE    DC    C'SE=',X'00'                                                     
SYSSE#   DS    X                                                                
*                                                                               
         EJECT                                                                  
***********************************************************************         
* LOAD PHASES                                                                   
***********************************************************************         
SPLOAD   LR    R0,RE                                                            
         MVC   MCDUB,SPACES                                                     
         MVC   MCDUB(4),=C'T00A'                                                
         GOTO1 HEXOUT,DMCB,BYTE,MCDUB+4,1,0                                     
         GOTO1 MCVLOADM,DMCB,0                                                  
         LR    RE,R0                                                            
         BR    RE                                                               
*                                                                               
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
* PUT A PRINT LINE TO AN EXTRA OUTPUT FILE FOR LATER PROCESSING       *         
* NTRY:  P1    = A(PRINT LINE)                                        *         
*        P2    = A(PRINT CONTROL CHARACTERS)                          *         
***********************************************************************         
EMLPUT   NTR1  BASE=*,LABEL=*                                                   
         LR    R2,R1                                                            
*                                                                               
         CLIY  EMLFLAG,YES         FILE OPEN ALREADY?                           
         BE    *+8                                                              
         BRAS  RE,EMLOPEN          OPEN IT                                      
*                                                                               
         LARL  R4,EMLLINE                                                       
         L     RE,4(R2)                                                         
         MVC   0(4,R4),0(RE)       PRINTER CONTROL CHARACTERS                   
         L     RE,0(R2)                                                         
         MVC   4(132,R4),0(RE)     PRINT LINE                                   
*                                                                               
         LARL  R3,EMLFILE                                                       
         PUT   (R3),0(R4)          PUT RECORD TO FILE                           
         XIT1  ,                                                                
*                                                                               
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
* OPEN EMLFILE FOR OUTPUT                                             *         
***********************************************************************         
EMLOPEN  NTR1  BASE=*,LABEL=*                                                   
         LARL  R3,EMLFILE                                                       
         OPEN  ((R3),OUTPUT)                                                    
         LTR   RF,RF                                                            
         JNZ   *+2                                                              
         MVIY  EMLFLAG,YES         FILE OPEN ALREADY?                           
         XIT1  ,                                                                
*                                                                               
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
* CLOSE EMLFILE                                                       *         
***********************************************************************         
EMLCLSE  NTR1  BASE=*,LABEL=*                                                   
         LARL  R3,EMLFILE                                                       
         CLOSE (R3)                                                             
         MVIY  EMLFLAG,NO          SET FILE CLOSED                              
         XIT1  ,                                                                
         LTORG                                                                  
*                                                                               
EMLFLAG  DC    AL1(NO)                                                          
*                                                                               
         DC    0D                  HAS TO BE ON A BOUNDRY                       
EMLLINE  DC    136C' '                                                          
EMLFILE  DCB   DSORG=PS,MACRF=PM,DDNAME=EMLFILE,RECFM=FB,LRECL=(136)            
         EJECT                                                                  
***********************************************************************         
* OTHER INCLUDED DSECTS                                               *         
***********************************************************************         
       ++INCLUDE DDCNTRL                                                        
*                                                                               
         EJECT                                                                  
YES      EQU   C'Y'                                                             
NO       EQU   C'N'                                                             
       ++INCLUDE DDREMOTED                                                      
         EJECT                                                                  
         PRINT OFF                                                              
       ++INCLUDE DDCOREQUS                                                      
       ++INCLUDE DDREPMASTD                                                     
       ++INCLUDE DDLOGOD                                                        
       ++INCLUDE PPREPWORK                                                      
         PRINT ON                                                               
       ++INCLUDE FASSB                                                          
         ORG   SSBD                                                             
       ++INCLUDE FASSBOFF                                                       
         ORG                                                                    
       ++INCLUDE FAUTL                                                          
       ++INCLUDE DMWRKRK                                                        
DDNAMED DSECT                                                                   
       ++INCLUDE DMDDNAMED                                                      
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'018PPRUNCON  11/12/13'                                      
         END                                                                    
