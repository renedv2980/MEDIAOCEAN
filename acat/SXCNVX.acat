*          DATA SET SXCNVX     AT LEVEL 011 AS OF 05/01/02                      
*CATALP SXCNVX                                                                  
         TITLE 'SXCNVX - ROUTINES CONVERT DDS FIELDS TO SQL FORMAT'             
**********************************************************************          
*                                                                    *          
*                                                                    *          
* PARAMETERS:                                                        *          
*                                                                    *          
* AL1 LENGTH INPUT FIELD                                             *          
* AL3 ADR INPUT FIELD                                                *          
*                                                                    *          
* AL1 ON ENTRY INPUT CONTROL FLAGS                                   *          
*     RETURNS LENGTH OUTPUT FIELD                                    *          
* AL3 ADR OUTPUT FIELD                                               *          
*                                                                    *          
* AL1 SQL PLATFORM TYPE CODE                                         *          
* AL3 ADR FIELD TYPE DESCRIPTOR STRING (CL8)                         *          
*                                                                    *          
**********************************************************************          
         SPACE 1                                                                
         PRINT NOGEN                                                            
SXCNVX   CSECT                                                                  
         NMOD1 WORKX-WORKD,*SXCNVX*,CLEAR=YES,RR=RA                             
         USING WORKD,RC                                                         
         ST    R1,APARM                                                         
         ST    RA,RELO                                                          
         MVC   PARM,0(R1)          EXTRACT PARAMETER LIST                       
         B     MAIN                                                             
         EJECT                                                                  
MAIN     EQU   *                                                                
         MVI   LOADMODE,C'N'       TEST IF LOAD OR UPDATE MODE                  
         TM    FINPUT,X'80'                                                     
         BNO   *+8                                                              
         MVI   LOADMODE,C'Y'                                                    
         MVI   OPTIONAL,C'N'       TEST IF OPTIONAL OR REQUIRED FIELD           
         TM    FINPUT,X'40'                                                     
         BNO   *+8                                                              
         MVI   OPTIONAL,C'Y'                                                    
         LA    RE,FCNVTAB          LOOK UP FIELD TYPE ROUTINE                   
         SR    RF,RF                                                            
         ICM   RF,7,FIELDTYP                                                    
*                                                                               
MAIN010  CLI   0(RE),0                                                          
         BE    EXIT                                                             
         CLC   0(8,RF),FCTTYPE(RE) MATCH TYPE NAME                              
         BNE   MAIN020                                                          
         CLC   SQLCODE,FCTSQLC(RE) MATCH SQL TYPE CODE                          
         BE    MAIN030                                                          
*                                                                               
MAIN020  LA    RE,L'FCNVTAB(RE)                                                 
         B     MAIN010                                                          
*                                                                               
MAIN030  ZIC   R2,LINPUT           GET LENGTH OF INPUT                          
         SR    R3,R3                                                            
         ICM   R3,7,AINPUT         POINT TO ADDRESS INPUT                       
         SR    R4,R4                                                            
         ICM   R4,7,AOUTPUT        POINT TO ADRESS OUTPUT                       
         ICM   RF,15,FCTROUT(RE)                                                
         A     RF,RELO                                                          
         BASR  RE,RF               BRACH TO PROCESSING ROUTINE                  
         B     EXIT                                                             
*                                                                               
EXIT     L     R1,APARM            EXIT WITH UPDATED PARAMETER LIST             
         MVC   0(L'PARM,R1),PARM                                                
         XMOD1 1                                                                
         EJECT                                                                  
**********************************************************************          
* CONVERT CHARACTER STRING TO SYBASE FORMAT                          *          
**********************************************************************          
         SPACE 1                                                                
CHARSYB  NTR1                                                                   
         CLI   LOADMODE,C'Y'                                                    
         BE    *+12                                                             
         MVI   0(R4),C''''         WRAP IN DINKS IF NOT LOAD MODE               
         LA    R4,1(R4)                                                         
         LTR   R2,R2               HANDLE ZERO LENGTH FIELD                     
         BZ    CHSY012                                                          
         LA    RF,0(R2,R3)                                                      
*                                                                               
CHSY010  BCTR  RF,0                DROP TRAILING SPACES                         
         CLI   0(RF),C' '                                                       
         BH    CHSY020                                                          
****     BNE   CHSY020                                                          
         BCT   R2,CHSY010                                                       
*                                                                               
CHSY012  CLI   OPTIONAL,C'Y'                                                    
         BE    CHSY014                                                          
         MVI   0(R4),C' '          RETURN ONE SPACE IF ALL SPACES               
         LA    R4,1(R4)              AND REQUIRED FIELD                         
         LA    R2,1(R2)                                                         
         B     CHSY100                                                          
CHSY014  BAS   RE,NULLSYB          RETURN NULL CODE IF OPTIONAL FIELD           
         B     CHSYX                                                            
*                                                                               
CHSY020  BCTR  R2,0                MOVE CHARACTERS TO OUTPUT BUFFER             
         EX    R2,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R4),0(R3)                                                    
         LA    R2,1(R2)                                                         
         AR    R4,R2                                                            
*                                                                               
CHSY100  CLI   LOADMODE,C'Y'                                                    
         BE    *+12                                                             
         MVI   0(R4),C''''                                                      
         LA    R2,2(R2)                                                         
         STC   R2,LOUTPUT          SAVE OUTPUT BUFFER LENGTH                    
*                                                                               
CHSYX    XIT1                                                                   
         EJECT                                                                  
**********************************************************************          
* CONVERT TINY INTEGER TO SYBASE FORMAT                              *          
**********************************************************************          
         SPACE 1                                                                
TINTSYB  NTR1                                                                   
         LTR   R2,R2               HANDLE ZERO LENGTH FIELD                     
         BZ    TISY100                                                          
*                                                                               
         MVI   SIGNFLAG,0                                                       
         CLI   0(R3),C'+'                                                       
         BE    *+12                                                             
         CLI   0(R3),C'-'                                                       
         BNE   TISY010                                                          
         MVC   0(1,R4),0(R3)                                                    
         LA    R4,1(R4)                                                         
         LA    R3,1(R3)                                                         
         BCTR  R2,0                                                             
         MVI   SIGNFLAG,1                                                       
*                                                                               
TISY010  CLI   0(R3),C'0'          DROP LEADING ZEROES                          
         BE    TISY020                                                          
         CLI   0(R3),C'1'          CHECK IS DIGIT                               
         BL    TISY100                                                          
         CLI   0(R3),C'9'                                                       
         BH    TISY100                                                          
         B     TISY030                                                          
TISY020  LA    R3,1(R3)                                                         
         BCT   R2,TISY010                                                       
         B     TISY100                                                          
*                                                                               
TISY030  C     R2,=F'3'            3 DIGITS MAX                                 
         BH    TISY100                                                          
         BCTR  R2,0                MOVE DIGITS TO OUTPUT BUFFER                 
         EX    R2,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R4),0(R3)                                                    
         LA    R2,1(R2)                                                         
         CLI   SIGNFLAG,0                                                       
         BE    *+8                                                              
         LA    R2,1(R2)                                                         
         STC   R2,LOUTPUT          SAVE  OUTPUT LENGTH                          
         B     TISYX                                                            
*                                                                               
TISY100  CLI   OPTIONAL,C'Y'       PROCESS NULL FIELD                           
         BE    TISY110                                                          
         MVI   0(R4),C'0'          REQUIRED                                     
         MVI   LOUTPUT,1                                                        
         B     TISYX                                                            
TISY110  BAS   RE,NULLSYB          OR OPTIONAL                                  
         B     TISYX                                                            
*                                                                               
TISYX    XIT1                                                                   
         EJECT                                                                  
**********************************************************************          
* CONVERT SMALL INTEGER TO SYBASE FORMAT                             *          
**********************************************************************          
         SPACE 1                                                                
SINTSYB  NTR1                                                                   
         LTR   R2,R2               HANDLE ZERO LENGTH FIELD                     
         BZ    SISY100                                                          
*                                                                               
         MVI   SIGNFLAG,0                                                       
         CLI   0(R3),C'+'                                                       
         BE    *+12                                                             
         CLI   0(R3),C'-'                                                       
         BNE   SISY010                                                          
         MVC   0(1,R4),0(R3)                                                    
         LA    R4,1(R4)                                                         
         LA    R3,1(R3)                                                         
         BCTR  R2,0                                                             
         MVI   SIGNFLAG,1                                                       
*                                                                               
SISY010  CLI   0(R3),C'0'          DROP LEADING ZEROES                          
         BE    SISY020                                                          
         CLI   0(R3),C'1'          GET FIRST DIGIT                              
         BL    SISY100                                                          
         CLI   0(R3),C'9'                                                       
         BH    SISY100                                                          
         B     SISY030                                                          
SISY020  LA    R3,1(R3)                                                         
         BCT   R2,SISY010                                                       
         B     SISY100                                                          
*                                                                               
SISY030  C     R2,=F'5'            MAXIMUM OF 5 DIGITS                          
         BH    SISY100                                                          
         BCTR  R2,0                MOVE DIGITS TO OUTPUT BUFFER                 
         EX    R2,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R4),0(R3)                                                    
         LA    R2,1(R2)                                                         
         CLI   SIGNFLAG,0                                                       
         BE    *+8                                                              
         LA    R2,1(R2)                                                         
         STC   R2,LOUTPUT          SAVE OUTPUT LENGTH                           
         B     SISYX                                                            
*                                                                               
SISY100  CLI   OPTIONAL,C'Y'       PROCESS NULL FIELD                           
         BE    SISY110                                                          
         MVI   0(R4),C'0'          REQUIRED                                     
         MVI   LOUTPUT,1                                                        
         B     SISYX                                                            
SISY110  BAS   RE,NULLSYB          OR OPTIONAL                                  
         B     SISYX                                                            
*                                                                               
SISYX    XIT1                                                                   
         EJECT                                                                  
**********************************************************************          
* CONVERT INTEGER TO SYBASE FORMAT                                   *          
**********************************************************************          
         SPACE 1                                                                
INTSYB   NTR1                                                                   
         LTR   R2,R2               HANDLE ZERO LENGTH FIELD                     
         BZ    INSY100                                                          
*                                                                               
         MVI   SIGNFLAG,0                                                       
         CLI   0(R3),C'+'                                                       
         BE    *+12                                                             
         CLI   0(R3),C'-'                                                       
         BNE   INSY010                                                          
         MVC   0(1,R4),0(R3)                                                    
         LA    R4,1(R4)                                                         
         LA    R3,1(R3)                                                         
         BCTR  R2,0                                                             
         MVI   SIGNFLAG,1                                                       
*                                                                               
INSY010  CLI   0(R3),C'0'          DROP LEADING ZEROES                          
         BE    INSY020                                                          
         CLI   0(R3),C'1'          CHECK DIGITS                                 
         BL    INSY100                                                          
         CLI   0(R3),C'9'                                                       
         BH    INSY100                                                          
         B     INSY030                                                          
INSY020  LA    R3,1(R3)                                                         
         BCT   R2,INSY010                                                       
         B     INSY100                                                          
*                                                                               
INSY030  C     R2,=F'10'           MAXIMUM 10 DIGITS                            
         BH    INSY100                                                          
         BCTR  R2,0                MOVE DIGITS TO OUTPUT BUFFER                 
         EX    R2,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R4),0(R3)                                                    
         LA    R2,1(R2)                                                         
         CLI   SIGNFLAG,0                                                       
         BE    *+8                                                              
         LA    R2,1(R2)                                                         
         STC   R2,LOUTPUT                                                       
         B     INSYX                                                            
*                                                                               
INSY100  CLI   OPTIONAL,C'Y'       PROCESS NULL FIELD                           
         BE    INSY110                                                          
         MVI   0(R4),C'0'          REQUIRED                                     
         MVI   LOUTPUT,1                                                        
         B     INSYX                                                            
INSY110  BAS   RE,NULLSYB          OR OPTIONAL                                  
         B     INSYX                                                            
*                                                                               
INSYX    XIT1                                                                   
         EJECT                                                                  
**********************************************************************          
* CONVERT REAL TO SYBASE FORMAT                                      *          
* PROCESSES UPTO 7 DIGITS WITH 2 DECIMAL PLACES IMPLIED              *          
**********************************************************************          
         SPACE 1                                                                
REALSYB  NTR1                                                                   
         LTR   R2,R2               HANDLE ZERO LENGTH FIELD                     
         BZ    RESY100                                                          
         BCTR  R2,0                                                             
         BCTR  R2,0                                                             
*                                                                               
         MVI   SIGNFLAG,0                                                       
         CLI   0(R3),C'+'                                                       
         BE    *+12                                                             
         CLI   0(R3),C'-'                                                       
         BNE   RESY010                                                          
         MVC   0(1,R4),0(R3)                                                    
         LA    R4,1(R4)                                                         
         LA    R3,1(R3)                                                         
         BCTR  R2,0                                                             
         MVI   SIGNFLAG,1                                                       
*                                                                               
RESY010  CLI   0(R3),C'0'          DROP LEADING ZEROES                          
         BE    RESY020                                                          
         CLI   0(R3),C'1'                                                       
         BL    RESY100                                                          
         CLI   0(R3),C'9'                                                       
         BH    RESY100                                                          
         B     RESY030                                                          
RESY020  LA    R3,1(R3)                                                         
         BCT   R2,RESY010                                                       
         MVI   0(R4),C'0'                                                       
         LA    R4,1(R4)                                                         
         LA    R2,1                                                             
         B     RESY040                                                          
*                                                                               
RESY030  C     R2,=F'5'                                                         
         BH    RESY100                                                          
         BCTR  R2,0                MOE DIGITS TO OUTPUT BUFFER                  
         EX    R2,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R4),0(R3)                                                    
         LA    R2,1(R2)                                                         
         AR    R3,R2                                                            
         AR    R4,R2                                                            
*                                                                               
RESY040  MVI   0(R4),C'.'          OUTPUT DECIMAL POINT                         
         MVC   1(2,R4),0(R3)       MOVE OUT 2 DECIMAL PLACES OF DIGITS          
         LA    R2,3(R2)                                                         
         CLI   SIGNFLAG,0                                                       
         BE    *+8                                                              
         LA    R2,1(R2)                                                         
         STC   R2,LOUTPUT                                                       
         B     RESYX                                                            
*                                                                               
RESY100  CLI   OPTIONAL,C'Y'       PROCESS NULL FIELD                           
         BE    RESY110                                                          
         MVC   0(4,R4),=C'0.00'    REQUIRED                                     
         MVI   LOUTPUT,4                                                        
         B     RESYX                                                            
RESY110  BAS   RE,NULLSYB          OR OPTIONAL                                  
         B     RESYX                                                            
*                                                                               
RESYX    XIT1                                                                   
         EJECT                                                                  
**********************************************************************          
* CONVERT FLOAT TO SYBASE FORMAT                                     *          
* PROCESSES UPTO 15 DIGITS WITH 4 DECIMAL PLACES IMPLIED             *          
**********************************************************************          
         SPACE 1                                                                
FLOATSYB NTR1                                                                   
         LTR   R2,R2               HANDLE ZERO LENGTH FIELD                     
         BZ    FLSY100                                                          
         BCTR  R2,0                                                             
         BCTR  R2,0                                                             
         BCTR  R2,0                                                             
         BCTR  R2,0                                                             
*                                                                               
         MVI   SIGNFLAG,0                                                       
         CLI   0(R3),C'+'                                                       
         BE    *+12                                                             
         CLI   0(R3),C'-'                                                       
         BNE   FLSY010                                                          
         MVC   0(1,R4),0(R3)                                                    
         LA    R4,1(R4)                                                         
         LA    R3,1(R3)                                                         
         BCTR  R2,0                                                             
         MVI   SIGNFLAG,1                                                       
*                                                                               
FLSY010  CLI   0(R3),C'0'          DROP LEADING ZEROES                          
         BE    FLSY020                                                          
         CLI   0(R3),C'1'                                                       
         BL    FLSY100                                                          
         CLI   0(R3),C'9'                                                       
         BH    FLSY100                                                          
         B     FLSY030                                                          
FLSY020  LA    R3,1(R3)                                                         
         BCT   R2,FLSY010                                                       
         MVI   0(R4),C'0'                                                       
         LA    R4,1(R4)                                                         
         LA    R2,1                                                             
         B     FLSY040                                                          
*                                                                               
FLSY030  C     R2,=F'11'                                                        
         BH    FLSY100                                                          
         BCTR  R2,0                MOVE DIGITS TO OUTPUT BUFFER                 
         EX    R2,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R4),0(R3)                                                    
         LA    R2,1(R2)                                                         
         AR    R3,R2                                                            
         AR    R4,R2                                                            
*                                                                               
FLSY040  MVI   0(R4),C'.'          OUTPUT DECIMAL POINT                         
         MVC   1(4,R4),0(R3)       AND 4 DECIMAL PLACES                         
         LA    R2,5(R2)                                                         
         CLI   SIGNFLAG,0                                                       
         BE    *+8                                                              
         LA    R2,1(R2)                                                         
         STC   R2,LOUTPUT                                                       
         B     FLSYX                                                            
*                                                                               
FLSY100  CLI   OPTIONAL,C'Y'       PROCESS NULL FIELD                           
         BE    FLSY110                                                          
         MVC   0(6,R4),=C'0.0000'  REQUIRED                                     
         MVI   LOUTPUT,6                                                        
         B     FLSYX                                                            
FLSY110  BAS   RE,NULLSYB          OR OPTIONAL                                  
         B     FLSYX                                                            
*                                                                               
FLSYX    XIT1                                                                   
         EJECT                                                                  
**********************************************************************          
* CONVERT BIT TO SYBASE FORMAT                                       *          
**********************************************************************          
         SPACE 1                                                                
BITSYB   NTR1                                                                   
         MVI   LOUTPUT,1                                                        
         LTR   R2,R2               HANDLE ZERO LENGTH FIELD                     
         BZ    BISYOFF                                                          
         CLI   0(R3),0                                                          
         BE    BISYOFF                                                          
         CLI   0(R3),C'N'                                                       
         BE    BISYOFF                                                          
         CLI   0(R3),C'Y'                                                       
         BE    BISYON                                                           
         B     BISYON                                                           
*                                                                               
BISYON   MVI   0(R4),C'1'         BIT SETTING OF ON                             
         B     BISYX                                                            
*                                                                               
BISYOFF  MVI   0(R4),C'0'         BIT SETTING OF OFF                            
         B     BISYX                                                            
*                                                                               
BISYX    XIT1                                                                   
         EJECT                                                                  
**********************************************************************          
* CONVERT MONEY TO SYBASE FORMAT                                     *          
* PROCESSES UPTO 12 DIGITS WITH 2 DECIMAL PLACES IMPLIED             *          
**********************************************************************          
         SPACE 1                                                                
MONEYSYB NTR1                                                                   
         MVI   0(R4),C'$'          PREFIX $ SIGN                                
         LA    R4,1(R4)                                                         
         LTR   R2,R2               HANDLE ZERO LENGTH FIELD                     
         BZ    MOSY100                                                          
         BCTR  R2,0                                                             
         BCTR  R2,0                                                             
         MVI   SIGNFLAG,0                                                       
         CLI   0(R3),C'+'                                                       
         BE    *+12                                                             
         CLI   0(R3),C'-'                                                       
         BNE   MOSY010                                                          
         MVC   0(1,R4),0(R3)                                                    
         LA    R4,1(R4)                                                         
         LA    R3,1(R3)                                                         
         BCTR  R2,0                                                             
         MVI   SIGNFLAG,1                                                       
*                                                                               
MOSY010  CLI   0(R3),C'0'          DROP LEADING ZEROES                          
         BE    MOSY020                                                          
         CLI   0(R3),C'1'                                                       
         BL    MOSY100                                                          
         CLI   0(R3),C'9'                                                       
         BH    MOSY100                                                          
         B     MOSY030                                                          
MOSY020  LA    R3,1(R3)                                                         
         BCT   R2,MOSY010                                                       
         MVI   0(R4),C'0'                                                       
         LA    R4,1(R4)                                                         
         LA    R2,1                                                             
         B     MOSY040                                                          
*                                                                               
MOSY030  C     R2,=F'10'                                                        
         BH    MOSY100                                                          
         BCTR  R2,0                MOVE DATA TO OUTPUT BUFFER                   
         EX    R2,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R4),0(R3)                                                    
         LA    R2,1(R2)                                                         
         AR    R3,R2                                                            
         AR    R4,R2                                                            
*                                                                               
MOSY040  MVI   0(R4),C'.'          OUTPUT DECIMAL POINT                         
         MVC   1(2,R4),0(R3)       AND 2 DECIMAL PLACES                         
         LA    R2,4(R2)                                                         
         CLI   SIGNFLAG,0                                                       
         BE    *+8                                                              
         LA    R2,1(R2)                                                         
         STC   R2,LOUTPUT                                                       
         B     MOSYX                                                            
*                                                                               
MOSY100  CLI   OPTIONAL,C'Y'       PROCESS NULL FIELD                           
         BE    MOSY110                                                          
         MVC   0(4,R4),=C'0.00'    REQUIRED                                     
         MVI   LOUTPUT,5                                                        
         B     MOSYX                                                            
MOSY110  BAS   RE,NULLSYB          OR OPTIONAL                                  
         B     MOSYX                                                            
*                                                                               
MOSYX    XIT1                                                                   
         EJECT                                                                  
**********************************************************************          
* CONVERT DATE-TIME TO SYBASE FORMAT                                 *          
**********************************************************************          
         SPACE 1                                                                
DATIMSYB NTR1                                                                   
         CLI   LOADMODE,C'Y'                                                    
         BE    *+12                                                             
         MVI   0(R4),C''''         WRAP IN DINKS IF NOT LOAD MODE               
         LA    R4,1(R4)                                                         
         LTR   R2,R2               HANDLE ZERO LENGTH FIELD                     
         BZ    DASY010                                                          
         CLI   LINPUT,15                                                        
         BE    DASY020                                                          
*                                                                               
DASY010  CLI   OPTIONAL,C'Y'       PROCESS NULL FIELD                           
         BE    DASY012                                                          
         LA    RF,1                                                             
         MVI   0(R4),C' '                                                       
         B     DASY100                                                          
DASY012  BAS   RE,NULLSYB                                                       
         B     DASYX                                                            
*                                                                               
DASY020  MVC   0(2,R4),4(R3)       OUTPUT DATE MM/DD/YY                         
         MVI   2(R4),C'/'                                                       
         MVC   3(2,R4),6(R3)                                                    
         MVI   5(R4),C'/'                                                       
         MVC   6(2,R4),2(R3)                                                    
         MVI   8(R4),C' '          OUTPUT SPACE TO SEPARATE DATE-TIME           
         MVC   9(2,R4),9(R3)       OUTPUT TIME HH:MM:SS                         
         MVI   11(R4),C':'                                                      
         MVC   12(2,R4),11(R3)                                                  
         MVI   14(R4),C':'                                                      
         MVC   15(2,R4),13(R3)                                                  
         LA    R4,17(R4)                                                        
         LA    RF,17                                                            
*                                                                               
DASY100  CLI   LOADMODE,C'Y'                                                    
         BE    *+12                                                             
         MVI   0(R4),C''''                                                      
         LA    RF,2(RF)                                                         
         STC   RF,LOUTPUT          SAVE OUTPUT LENGTH                           
         B     DASYX                                                            
*                                                                               
DASYX    XIT1                                                                   
         EJECT                                                                  
**********************************************************************          
* OUTPUT NULL STRING FOR SYBASE                                      *          
**********************************************************************          
         SPACE 1                                                                
NULLSYB  EQU   *                                                                
         ICM   RF,7,AOUTPUT                                                     
* ??     MVI   LOUTPUT,0                                                        
         MVI   LOUTPUT,4                                                        
         MVC   0(4,RF),=CL4'NULL'                                               
*                                                                               
NUSYX    BR    RE                                                               
*                                                                               
YES      SR    RC,RC                                                            
NO       LTR   RC,RC                                                            
         XIT1                                                                   
*                                                                               
SPACES   DC    128C' '                                                          
*                                                                               
* FIELD TYPE CONVERSION TABLE                                                   
* AL1    SQL TYPE CODE                                                          
* CL8    TYPE NAME                                                              
* AL4    CONVERT ROUTINE ADDRESS                                                
*                                                                               
         DS    0D                                                               
FCNVTAB  DS    0CL13                                                            
         DC    AL1(01),CL8'CHAR    ',AL4(CHARSYB)                               
         DC    AL1(01),CL8'TINYINT ',AL4(TINTSYB)                               
         DC    AL1(01),CL8'SMALLINT',AL4(SINTSYB)                               
         DC    AL1(01),CL8'INT     ',AL4(INTSYB)                                
         DC    AL1(01),CL8'REAL    ',AL4(REALSYB)                               
         DC    AL1(01),CL8'FLOAT   ',AL4(FLOATSYB)                              
         DC    AL1(01),CL8'BIT     ',AL4(BITSYB)                                
         DC    AL1(01),CL8'MONEY   ',AL4(MONEYSYB)                              
         DC    AL1(01),CL8'DATETIME',AL4(DATIMSYB)                              
FCNVTABX DC    AL1(00)                                                          
         DS    0D                                                               
FCTSQLC  EQU   0                                                                
FCTTYPE  EQU   1                                                                
FCTROUT  EQU   9                                                                
         SPACE 2                                                                
         EJECT                                                                  
         LTORG                                                                  
         EJECT                                                                  
WORKD    DSECT                                                                  
*                                                                               
DMCB     DS    6F                                                               
RELO     DS    A                                                                
DUB      DS    D                                                                
APARM    DS    A                   A(PARAMETER LIST)                            
*                                                                               
PARM     DS    0XL12               PARAMETER LIST                               
LINPUT   DS    XL1                 LENGTH INPUT FIELD                           
AINPUT   DS    AL3                 A(INPUT FIELD)                               
*                                                                               
FINPUT   DS    XL1                 INPUT FIELD FLAGS                            
         ORG   FINPUT                                                           
LOUTPUT  DS    XL1                 LENGTH OUTPUT FIELD                          
AOUTPUT  DS    AL3                 A(OUTPUT FIELD)                              
*                                                                               
SQLCODE  DS    XL1                 SQL PLATFORM CODE                            
FIELDTYP DS    AL3                 A(FIELD TYPE DESCRIPTOR)                     
*                                                                               
LOADMODE DS    CL1                 LOAD MODE FLAG                               
OPTIONAL DS    CL1                 FIELD OPTIONAL FLAG                          
SIGNFLAG DS    CL1                 FIELD SIGN FLAG                              
*                                                                               
WORKX    EQU   *                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'011SXCNVX    05/01/02'                                      
         END                                                                    
