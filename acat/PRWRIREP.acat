*          DATA SET PRWRIREP   AT LEVEL 006 AS OF 04/22/03                      
*CATALP PRWRIREP                                                                
PRWRIREP CSECT                                                                  
         TITLE 'PRWRIREP - REP BUFFER ROUTINES - CHANGE LOG'                    
         TITLE 'PRWRIREP - BUILD REP BUFFER - BLDREP'                           
***********************************************************************         
*                                                                     *         
*        BUILD REP BUFFER ROUTINE                                     *         
*                                                                     *         
*NTRY                                                                 *         
*                                                                     *         
*        R8==>  SPOOL WORKAREA                                        *         
*        R9==>  PRINTWRITER WORKAREA                                  *         
*        RA==>  DRIVER GLOBAL AREA                                    *         
*        RC==>  GENCON WORKAREA                                       *         
*                                                                     *         
*        PBAREPBF ==>  REP TABLE                                      *         
*                                                                     *         
***********************************************************************         
         SPACE 2                                                                
         PRINT NOGEN                                                            
         DS    0D                                                               
         ENTRY BLDREP                                                           
BLDREP   NMOD1 500,**#BRP**                                                     
*                                                                               
         LR    R6,RC               SAVE WORKAREA POINTER                        
*                                                                               
         USING SPOOLD,R8           ESTABLISH SPOOL WORKAREA                     
         USING SYSD,R9             ESTABLISH PRINTWRITER WORKAREA               
         USING GLOBALD,RA          ESTABLISH DRIVER WORKAREA                    
*                                                                               
         LA    R7,PBLOCK           ESTABLISH PRINT BLOCK                        
         USING PBLOCK,R7                                                        
*                                                                               
         L     RC,GLAWORKD         RESET WORKING STORAGE POINTER                
         USING GEND,RC                                                          
*                                                                               
*        INIT BUFFER POINTERS                                                   
*                                                                               
         ICM   R4,15,PBAREPBF      GET ADDRESS OF BUFFER                        
         BZ    BRPERR                IF NOT SET UP EXIT                         
*                                                                               
         USING REPBUFD,R4          ESTABLISH REP BUFFER                         
*                                                                               
         LR    R5,R4               GET LENGTH OF BUFFER                         
         SH    R5,=H'4'                                                         
         L     R5,0(R5)            LENGTH                                       
*                                                                               
         SH    R5,=Y(REPBUFLN)     MAKE SURE THERE IS ROOM IN BUFFER            
         BP    *+6                                                              
         DC    H'0'                                                             
*                                                                               
*        SAVE CURRENT RECORD POINTERS                                           
*                                                                               
         MVC   BRPAIO,AIO          SAVE I/O AREA ADDRESS                        
         MVC   BRPFNAME,FILENAME   SAVE FILE NAME                               
         XC    FILENAME,FILENAME   USE DEFAULT FILE NAME                        
*                                                                               
*        READ 1ST/NEXT REP FOR BUFFER                                           
*                                                                               
         LA    R2,KEY              ESTABLISH KEY WORKAREA AS REPREC KEY         
         USING PREPRECD,R2                                                      
*                                                                               
         XC    PREPKEY,PREPKEY     INIT KEY                                     
*                                                                               
         MVC   PREPKAGY,PBAGY      SET AGENCY                                   
         MVC   PREPKMED,PBMED      SET MEDIA                                    
         MVI   PREPKRCD,PREPKIDQ   SET RECORD ID                                
*                                                                               
         CLI   PBQADVSW,C'Y'       IF ADVERTISER REPORT                         
         BNE   *+20                                                             
         MVC   PREPKAGY,PAORAGY    USE AOR AGENCY                               
         L     RF,PBUTLA           SET FILES TO AOR'S                           
         MVC   4(1,RF),PAORSE                                                   
******                                                                          
******   OC    PBQREPF,PBQREPF     USE ENTERED REP ID                           
******   BZ    *+10                                                             
******   MVC   PREPKREP,PBQREPF                                                 
*                                                                               
         ST    R6,AIO              READ INTO WORKAREA                           
*                                                                               
         GOTO1 HIGH                READ FIRST REP POINTER                       
*                                                                               
BRPRDLP  DS    0H                                                               
*                                                                               
         CLC   PREPKEY(PREPKREP-PREPKEY),KEYSAVE MATCH AGY/MED/ID               
         BNE   BRPRDDN                                                          
******                                                                          
******   OC    PBQREPF,PBQREPF     IF SINGLE REP REQUESTED                      
******   BZ    *+14                                                             
******   CLC   PREPKREP,PBQREPF      MUST MATCH REP ID                          
******   BNE   BRPRDDN                                                          
******                                                                          
         GOTO1 GETREC                 READ REP RECORD                           
         BE    *+6                                                              
         DC    H'0'                   NO ERRORS TOLERATED                       
*                                                                               
         USING REPBUFD,R4          ESTABLISH REP BUFFER                         
*                                                                               
         MVC   REPBUDA,KEY+27      SAVE DISK ADDRESS                            
*                                                                               
         ICM   RF,15,PBUTLA        IF A(UTL) KNOWN                              
         BZ    *+10                                                             
         MVC   REPBUFSE,1(RF)         SAVE SE NUMBER                            
*                                                                               
         LR    RF,R6               POINT TO FOUND RECORD                        
*                                                                               
         MVC   REPBUFN,PREPKREP-PREPKEY(RF)   SAVE REP ID                       
         MVC   REPBUFNA,PREPNAME-PREPKEY(RF)  SAVE REP NAME                     
*                                                                               
         SH    R5,=Y(REPBUFLN)     DECREMENT ROOM LEFT COUNTER                  
         BP    *+6                                                              
         DC    H'0'                  THERE MUST BE ROOM                         
*                                                                               
         LA    R4,REPBUFLN(R4)     BUMP TO NEXT TABLE ENTRY                     
         XC    0(REPBUFLN,R4),0(R4)  INIT NEXT ENTRY                            
*                                                                               
BRPRDCN  DS    0H                                                               
*                                                                               
         GOTO1 SEQ                 READ NEXT REP ON FILE                        
*                                                                               
         B     BRPRDLP                                                          
*                                                                               
BRPRDDN  DS    0H                                                               
*                                                                               
         CLI   PBQADVSW,C'Y'       IF ADVERTISER REPORT                         
         BNE   *+14                                                             
         L     RF,PBUTLA           RESTORE AGENCY'S FILES ID                    
         MVC   4(1,RF),PAOSE                                                    
*                                                                               
         CR    RB,RB               SET EQ CC                                    
         B     BLDREPX                                                          
*                                                                               
BRPERR   DS    0H                                                               
*                                                                               
         LTR   RB,RB               SET NEQ CC                                   
*                                                                               
BLDREPX  DS    0H                                                               
*                                                                               
*        RESET FILE POINTERS                                                    
*                                                                               
         MVC   AIO,BRPAIO          RESTORE I/O AREA ADDRESS                     
         MVC   FILENAME,BRPFNAME   RESTORE FILE NAME                            
         MVC   KEY,IOKEYSVE        RESTORE KEY                                  
         OI    DMINBTS,8           PASS BACK DELETED RECORDS                    
*                                                                               
         GOTO1 HIGH                RESTORE FILE POINTERS                        
*                                                                               
         XIT1                                                                   
*                                                                               
         DS    CL256               FOR NEW Z/OS PROCESSOR                       
BRPAIO   DS    A                   AIO SAVEAREA                                 
BRPFNAME DS    XL(L'FILENAME)      FILENAME SAVEAREA                            
*                                                                               
         LTORG                                                                  
*                                                                               
         DROP                                                                   
*                                                                               
         TITLE   'PRWRIREP DSECTS / STORAGE'                                    
* PRWRIWORKD                                                                    
         PRINT   OFF                                                            
       ++INCLUDE PRWRIWORKD                                                     
         EJECT                                                                  
DATELSTD DSECT                                                                  
       ++INCLUDE PRDATELSTD                                                     
         EJECT                                                                  
MYWORKD  DSECT                                                                  
         DS    10D                                                              
WORKAREA DS    60D                                                              
         SPACE 2                                                                
LASTIOD  DSECT                                                                  
SAVKEYS  DS   CL25                                                              
SVADKEY  DS   CL14                                                              
SVADCD   DS   CL6                                                               
SKPUB    DS   CL18                                                              
SVLSTDAT DS   CL4                                                               
MLTREC   DS   CL1                                                               
         PRINT ON                                                               
*        INCLUDE DRGLOBAL                                                       
         PRINT   OFF                                                            
       ++INCLUDE DRGLOBAL                                                       
         PRINT   ON                                                             
*        INCLUDE DRIVETABLE                                                     
         PRINT   OFF                                                            
       ++INCLUDE DRIVETABLE                                                     
         PRINT   ON                                                             
*        INCLUDE DRINTRECD2                                                     
         PRINT   OFF                                                            
       ++INCLUDE DRINTRECD2                                                     
         PRINT   ON                                                             
*        INCLUDE DDSPOOLD                                                       
         PRINT   OFF                                                            
       ++INCLUDE DDSPOOLD                                                       
         PRINT   ON                                                             
*        INCLUDE DDSPLWORKD                                                     
         PRINT OFF                                                              
       ++INCLUDE DDSPLWORKD                                                     
         PRINT   ON                                                             
* PRGENFILE                                                                     
         PRINT OFF                                                              
       ++INCLUDE PRGENFILE                                                      
         PRINT   ON                                                             
* PRGLOBEQUS                                                                    
         PRINT OFF                                                              
       ++INCLUDE PRGLOBEQUS                                                     
         PRINT   ON                                                             
* DDCOMFACSD                                                                    
         PRINT OFF                                                              
       ++INCLUDE DDCOMFACSD                                                     
         PRINT   ON                                                             
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'006PRWRIREP  04/22/03'                                      
         END                                                                    
