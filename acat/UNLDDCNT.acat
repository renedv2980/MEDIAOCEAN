*          DATA SET UNLDDCNT   AT LEVEL 001 AS OF 01/16/15                      
*CATALP UNLDDCNT                                                                
         TITLE 'LDCOUNT - UNIT - LOAD COUNT/PRINT PROGRAM'                      
***********************************************************************         
*                                                                     *         
* UNLDDCNT - TALLIES RECORDS LOADED/DUMPED AND PRINTS A SUMMARY.      *         
*                                                                     *         
*  INPUT:  (R1 POINTS TO PARAMETER LIST)                              *         
*          PARAMETER 1 - BYTE 1 - X'00' - COUNT                       *         
*                                 X'FF' - PRINT                       *         
*                        BYTE 2-4 - ADDRESS OF RECORD                 *         
*                                                                     *         
*  OUTPUT: PRINTS SUMMARY, MAINTAINS COUNTERS.                        *         
*                                                                     *         
*  CALLED FROM: DMLDMOD - GENERAL LOAD/DUMP MODULE                    *         
*                                                                     *         
*  GLOBALS: R8 - ADDRESS OF RECORD                                    *         
*           R7 - ADDRESS OF TALLY FIELDS FOR RECORD TYPE              *         
***********************************************************************         
         PRINT NOGEN                                                            
LDCOUNT  CSECT                                                                  
         NMOD1 0,UNLDDCNT                                                       
*                                                                               
         CLI   0(R1),X'FF'         IF PRINT                                     
         BNE   TALLY                                                            
         BAS   RE,PRINTIT                                                       
         B     XITCNT                                                           
*                                                                               
TALLY    L     R8,0(R1)            R8 POINTS TO RECORD                          
         USING NURECD,R8                                                        
*                                                                               
         CLI   NURSTAT,X'C0'       IF CLOSED OUT                                
         BNE   TAL1                                                             
         AF    CLOSED,=F'1'                                                     
         B     XITCNT                                                           
TAL1     CLI   0(R8),X'04'         CHECK IF A UNIT RECORD                       
         BNE   TAL2                                                             
         CLI   NURSTAT,X'80'       IF DELETED                                   
         BNE   UNTAL1                                                           
         AF    UNITDEL,=F'1'                                                    
         B     XITCNT                                                           
UNTAL1   TM    NUPACKST,X'01'      IF NO-SHOW(DEL LOCK PACK)                    
         BNO   UNTAL2                                                           
         AF    LOCKDELU,=F'1'                                                   
         B     XITCNT                                                           
UNTAL2   AF    UNITADD,=F'1'                                                    
         B     XITCNT                                                           
         DROP  R8                                                               
*                                                                               
TAL2     CLI   0(R8),X'84'         CHECK IF A PROG PASSIVE UNIT                 
         BNE   TAL4                                                             
         AF    UNPADD,=F'1'                                                     
         B     XITCNT                                                           
*                                                                               
TAL4     CLI   0(R8),X'94'         CHECK IF A DAY-TIME PASSIVE UNIT             
         BNE   TAL6                                                             
         AF    UNDADD,=F'1'                                                     
         B     XITCNT                                                           
*                                                                               
         USING NPRECD,R8                                                        
TAL6     CLI   0(R8),X'02'         CHECK IF A PACKAGE RECORD                    
         BNE   TAL7                                                             
         CLI   NPKRSTAT,X'80'      IF DELETED                                   
         BNE   PKTAL1                                                           
         AF    PACKDEL,=F'1'                                                    
         B     XITCNT                                                           
PKTAL1   TM    NPAKSTAT,X'01'      CHECK IF PACK NO-SHOW                        
         BNO   PKTAL2                                                           
         AF    LOCKDELP,=F'1'                                                   
         B     XITCNT                                                           
PKTAL2   AF    PACKADD,=F'1'                                                    
         B     XITCNT                                                           
         DROP  R8                                                               
*                                                                               
TAL7     CLI   0(R8),X'21'         CHECK IF TRAFFIC RECORD                      
         BNE   TAL8                                                             
         AF    TRAFFADD,=F'1'                                                   
         B     XITCNT                                                           
*                                                                               
TAL8     CLI   0(R8),X'0A'         IS IT INTEG REC                              
         BNE   TAL9                                                             
         AF    INTEGCNT,=F'1'                                                   
         B     XITCNT                                                           
*                                                                               
TAL9     CLI   0(R8),X'0C'         IS IT NET COMMENT                            
         BNE   TALX                                                             
         AF    NETCMCNT,=F'1'                                                   
         B     XITCNT                                                           
*                                                                               
TALX     AF    UFOTALY,=F'1'       INCREMENT UNIDENTIFIED TYPE                  
*NOP*    L     RA,=V(CPRINT)                                                    
*NOP*    USING DPRINT,RA                                                        
*NOP*    GOTO1 VHEXOUT,DMCB,0(R8),P,50                                          
*NOP*    GOTO1 VPRINT                                                           
*                                                                               
XITCNT   XMOD1 1                                                                
         EJECT                                                                  
***********************************************************************         
* PRINTIT - PRINT SUMMARY OF COUNTERS                                 *         
***********************************************************************         
PRINTIT  NTR1                                                                   
         L     RA,=V(CPRINT)                                                    
         USING DPRINT,RA                                                        
         ZAP   LINE,=P'99'                                                      
         L     RE,4(R1)                                                         
         MVC   HLINE+8(8),0(RE)    MOVE UNTDIR NAME TO HEADING                  
*                                                                               
         MVC   P,SPACES                                                         
         MVC   P(L'HLINE),HLINE                                                 
         GOTO1 VPRINT                                                           
         MVC   P(L'ULINE),ULINE                                                 
         GOTO1 VPRINT                                                           
*                                                                               
         MVC   P(15),=CL15'CLOSED OUT RECS'                                     
         L     R0,CLOSED                                                        
         BAS   RE,NUMBER                                                        
         GOTO1 VPRINT                                                           
*                                                                               
         MVC   P(15),=CL15'UNIT RECS KEPT'                                      
         L     R0,UNITADD                                                       
         BAS   RE,NUMBER                                                        
         GOTO1 VPRINT                                                           
*                                                                               
         MVC   P(15),=CL15'UNIT RECS DELD'                                      
         L     R0,UNITDEL                                                       
         BAS   RE,NUMBER                                                        
         GOTO1 VPRINT                                                           
*                                                                               
         MVC   P(15),=CL15'PROG PSVE UNITS'                                     
         L     R0,UNPADD                                                        
         BAS   RE,NUMBER                                                        
         GOTO1 VPRINT                                                           
*                                                                               
         MVC   P(15),=CL15'DAYT PSVE UNITS'                                     
         L     R0,UNDADD                                                        
         BAS   RE,NUMBER                                                        
         GOTO1 VPRINT                                                           
*                                                                               
         MVC   P(15),=CL15'PACKAGE KEPT'                                        
         L     R0,PACKADD                                                       
         BAS   RE,NUMBER                                                        
         GOTO1 VPRINT                                                           
*                                                                               
         MVC   P(15),=CL15'PACKAGE DELD'                                        
         L     R0,PACKDEL                                                       
         BAS   RE,NUMBER                                                        
         GOTO1 VPRINT                                                           
*                                                                               
         MVC   P(15),=CL15'TRAFFIC KEPT'                                        
         L     R0,TRAFFADD                                                      
         BAS   RE,NUMBER                                                        
         GOTO1 VPRINT                                                           
*                                                                               
         MVC   P(15),=CL15'NETWORK COMMENT'                                     
         L     R0,NETCMCNT                                                      
         BAS   RE,NUMBER                                                        
         GOTO1 VPRINT                                                           
*                                                                               
         MVC   P(15),=CL15'INTEGRATION '                                        
         L     R0,INTEGCNT                                                      
         BAS   RE,NUMBER                                                        
         GOTO1 VPRINT                                                           
*                                                                               
         MVC   P(15),=CL15'LCKD UNITS DELD'                                     
         L     R0,LOCKDELU                                                      
         BAS   RE,NUMBER                                                        
         GOTO1 VPRINT                                                           
*                                                                               
         MVC   P(15),=CL15'LCKD PCKGS DELD'                                     
         L     R0,LOCKDELP                                                      
         BAS   RE,NUMBER                                                        
         GOTO1 VPRINT                                                           
*                                                                               
         MVC   P(15),=CL15'UNKNOWN RECS'                                        
         L     R0,UFOTALY                                                       
         BAS   RE,NUMBER                                                        
         GOTO1 VPRINT                                                           
         B     PRINTITX                                                         
*                                                                               
NUMBER   ST    RE,SAVERE           R0=NUMBER                                    
         LTR   R0,R0                                                            
         BNZ   *+14                                                             
         MVC   P+17(9),=9C'.'                                                   
         B     NUMBERX                                                          
         CVD   R0,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  P+17(9),DUB                                                      
NUMBERX  L     RE,SAVERE                                                        
         BR    RE                                                               
*                                                                               
PRINTITX XIT1                                                                   
         EJECT                                                                  
UNITADD  DC    F'0'                                                             
CLOSED   DC    F'0'                                                             
UNITDEL  DC    F'0'                                                             
UNPADD   DC    F'0'                PROG PASSIVE UNITS                           
UNDADD   DC    F'0'                DAY-TIME PASSIVE UNITS                       
PACKADD  DC    F'0'                                                             
PACKDEL  DC    F'0'                                                             
UFOTALY  DC    F'0'                                                             
TRAFFADD DC    F'0'                                                             
NETCMCNT DC    F'0'                                                             
INTEGCNT DC    F'0'                                                             
LOCKDELU DC    F'0'                                                             
LOCKDELP DC    F'0'                                                             
*                                                                               
VPRINT   DC    V(PRINTER)                                                       
VHEXOUT  DC    V(HEXOUT)                                                        
*                                                                               
HLINE    DC    CL26'REC TYP XXXXXXX  COUNT    '                                 
ULINE    DC    CL26'---------------  ---------'                                 
*                                                                               
WORK     DS    CL40                                                             
DUB      DS    D                                                                
SAVERE   DS    A                                                                
DMCB     DS    6F                                                               
         EJECT                                                                  
         LTORG                                                                  
       ++INCLUDE NEGENUNIT                                                      
       ++INCLUDE NEGENPACK                                                      
       ++INCLUDE DDDPRINT                                                       
*                                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'001UNLDDCNT  01/16/15'                                      
         END                                                                    
