*          DATA SET FATERMVAL  AT LEVEL 006 AS OF 03/05/14                      
*CATALP TERMVAL                                                                 
         TITLE 'TERMVAL - VALIDATE INPUT TERMINAL ID'                           
*INPUT PARAMETER LIST                                                           
*                                                                               
*PARM1   XL1   80=INPUT IN CL8 FLD,40=DO NOT SEARCH UTL                         
*              20=READ TRM REC,10=SRCH TERM ELEM                                
*        AL3   ADR OF TWA INPUT FLD HDR OR ADR OF CL8 FIELD OR ZERO             
*PARM2   AL4   A(UTL) ENTRY IF PARM1 ZERO                                       
*PARM3   XL1   N/D                                                              
*        AL3   USED TO RETURN A(TERM REC) IF P1=X'20'                           
*PARM4   XL1   N/D                                                              
*        AL3   USED TO RETURN A(TERM DEFN ELEMENT) IF P1=X'10'                  
                                                                                
*RETURNED PARAMETER LIST                                                        
*                                                                               
*PARM1   XL1   80=MISSING INPUT FIELD,40=INVALID INPUT FIELD                    
*              20=TERM REC NOT FOUND,10=TERM NOT AUTH FOR THIS FACPAK           
*        AL3   ADR OF LOCAL CL8 FIELD IN STANDARD FORMAT                        
*                       AND CL8 FIELD CONTAINING VTAM LUID IF PRESENT           
*PARM2   AL4   ADR OF UTL ENTRY OR ZERO                                         
*PARM3   XL1   N/D                                                              
*        AL3   ADR OF LOCAL TERMINAL RECORD OR ZERO                             
*PARM4   XL1   N/D                                                              
*        AL3   ADR OF LOCAL TERMINAL RECORD DEFN ELEMENT OR ZERO                
                                                                                
         PRINT NOGEN                                                            
TERMVAL  CSECT                                                                  
         NMOD1 TWRKX-TWRK,**TVAL**                                              
         USING TWRK,RC             RC=A(WORK)                                   
         SAM31                                                                  
*                                                                               
         LR    R2,R1               R2=A(PARMS)                                  
         USING PARM,R2                                                          
         MVC   IFLAGS,PFLD                                                      
         SR    R3,R3                                                            
         ICM   R3,7,PFLD+1         R3=A(INPUT FIELD) OR ZERO                    
         USING FLDHDRD,R3                                                       
*                                                                               
         L     RE,=V(SSB)          EXTRACT VTAM APPLICATION ID                  
         MVC   VTAPLID,SSBVTID-SSBD(RE)                                         
*                                                                               
TERMVAL1 L     R5,=V(SYSFAC)                                                    
         L     R5,VUTL-SYSFACD(R5)                                              
         USING UTLD,R5             R5=A(UTL)                                    
         LH    R6,0(R5)                                                         
         L     R7,2(R5)                                                         
         LA    R5,6(R5)            R6/R7 USED FOR BXLE ON UTL                   
*                                                                               
TERMVAL2 MVC   TERMID(8),=CL8' '   TEST FORMAT OF INPUT FIELD                   
         MVC   VTLUID(8),=CL8' '                                                
         XC    TERMREC(25),TERMREC                                              
         LA    RE,TERMID                                                        
         ST    RE,PFLD             SET RETURN A(LLLLCUDV)                       
         LTR   R3,R3                                                            
         BZ    TERMVAL4            ZERO 1ST PARM MEANS A(UTL) IN 2ND            
*                                                                               
TERMVAL3 XC    PUTL,PUTL           SET RETURN A(UTL) TO ZERO                    
         TM    IFLAGS,X'80'        TEST IF CALLER PASSED TERMID                 
         BZ    VALINP              NO                                           
         XC    FHDR,FHDR           YES FAKE UP FIELD HEADER                     
         XC    FDATA,FDATA                                                      
         LR    RE,R3                                                            
         LA    R3,FHDR             R3=A(DUMMY FIELD HEADER)                     
         MVI   FLDIIND,FINPTHIS+FINPNUM                                         
         LA    RF,FDATA                                                         
         LA    R0,8                                                             
TERMVL3A CLI   0(RE),0             MOVE FIELD TO DUMMY FIELD                    
         BE    TERMVL3B                                                         
         CLI   0(RE),C' '                                                       
         BE    TERMVL3B                                                         
         MVC   0(1,RF),0(RE)                                                    
         CLI   0(RE),C'0'                                                       
         BNL   *+8                                                              
         NI    FLDIIND,255-FINPNUM                                              
         LA    RE,1(RE)                                                         
         LA    RF,1(RF)                                                         
         BCT   R0,TERMVL3A                                                      
TERMVL3B LA    RF,8                SET FIELD LENGTH                             
         SR    RF,R0                                                            
         STC   RF,FLDILEN                                                       
         B     VALINP                                                           
*                                                                               
TERMVAL4 L     R1,=V(SYSFAC)       CHECK IF UTL ADDR IS LOCAL                   
         L     R1,VTCB-SYSFACD(R1)                                              
         LH    RE,0(R1)                                                         
         L     RF,2(R1)                                                         
         AHI   R1,6                                                             
         USING TCBD,R1                                                          
         CLC   PUTL,TCBUTL         LOCAL ADDRESS                                
         BE    *+12                                                             
         BXLE  R1,RE,*-10                                                       
         B     TERMVAL5                                                         
         L     R5,PUTL                                                          
         MVC   TERMID,TLUID                                                     
         B     GETTRM                                                           
         DROP  R1                                                               
*                                                                               
TERMVAL5 L     R0,PUTL             CHECK IF UTL ADDR IS IN VUTL                 
         CR    R5,R0                                                            
         BH    INVALID                                                          
         CR    R7,R0                                                            
         BL    INVALID                                                          
         LR    R5,R0                                                            
         MVC   TERMID,TLUID                                                     
         ST    R5,PUTL                                                          
         B     GETTRM                                                           
         EJECT                                                                  
* VALIDATE TERMINAL ID IN INPUT FIELD                                           
* FORMAT CAN BE INTEGER   FOR TERMINAL NUMBER                                   
* FORMAT CAN BE LLLLCUDV  FOR LINE ID AND TERMINAL ADDRESS                      
* FORMAT CAN BE LLLL/CUDV WITH / SEPERATING LINE FROM HEX CUDV                  
* FORMAT CAN BE XXXXXXXX  FOR VTAM SYMBOLIC ID                                  
*                                                                               
VALINP   CLI   FLDILEN,0           EXIT IF NO INPUT                             
         BE    MISSING                                                          
         TM    FLDIIND,X'08'       ASSUME TERMINAL NUMBER IF NUMERIC            
         BO    VALINP6                                                          
*                                                                               
VALINP1  CLI   FLDDATA,C'A'        FIRST CHR MUST BE A-Z                        
         BL    INVALID                                                          
         CLI   FLDDATA,C'Z'                                                     
         BH    INVALID                                                          
         MVI   BYTE,0                                                           
         CLI   FLDILEN,4           RANGE CHECK LENGTH (WAS 7)                   
         BL    INVALID                                                          
         CLI   FLDILEN,8                                                        
         BH    INVALID                                                          
         SR    R1,R1               MOVE TO STANDARD INTERNAL FIELD              
         IC    R1,FLDILEN                                                       
         BCTR  R1,0                                                             
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   TERMID(0),FLDDATA                                                
         B     VALINP4             DONT LOOK FOR OLD BTAM SHIT                  
*                                                                               
VALINP2  SR    R1,R1               R1=FIELD LENGTH                              
         IC    R1,FLDILEN                                                       
         LA    R8,FLDDATA-4(R1)    R8=A(LAST FOUR CHRS OF FIELD)                
         LA    RE,FLDDATA                                                       
         LA    RF,TERMID                                                        
         LA    R0,TERMID+8                                                      
         MVI   BYTE,X'01'          SET LAST FOUR CHRS HEX                       
VALINP2A CLI   0(RE),C'/'                                                       
         BE    VALINP2C                                                         
         CLI   0(RE),C'A'                                                       
         BL    INVALID                                                          
         CLI   0(RE),C'9'                                                       
         BH    INVALID                                                          
         CR    RE,R8                                                            
         BL    VALINP2D                                                         
VALINP2B CLI   0(RE),C'F'                                                       
         BNH   VALINP2D                                                         
         CLI   0(RE),C'0'                                                       
         BNL   VALINP2D                                                         
         NI    BYTE,255-X'01'      SET LAST FOUR CHRS NOT HEX                   
         B     VALINP2D                                                         
VALINP2C TM    BYTE,X'02'                                                       
         BO    INVALID                                                          
         OI    BYTE,X'02'          SET DATA CONTAINS SLASH                      
         B     VALINP2E                                                         
VALINP2D CR    RF,R0               TEST END OF RECEIVING FIELD                  
         BNL   INVALID                                                          
         MVC   0(1,RF),0(RE)                                                    
         LA    RF,1(RF)                                                         
VALINP2E LA    RE,1(RE)                                                         
         BCT   R1,VALINP2A                                                      
*                                                                               
VALINP3  TM    BYTE,X'02'          IF LLLL/CUDV THEN CUDV MUST BE HEX           
         BZ    VALINP4                                                          
         TM    BYTE,X'01'                                                       
         BZ    INVALID                                                          
*                                                                               
VALINP4  TM    IFLAGS,X'40'        TEST DO NOT SEARCH UTL                       
         BO    VALINPX                                                          
*                                                                               
VALINP5  CLC   TERMID,TLUID        SEARCH UTL FOR TERMINAL ID                   
         BE    VALINP5A                                                         
         BXLE  R5,R6,VALINP5                                                    
         B     VALINPX                                                          
VALINP5A ST    R5,PUTL             RETURN A(UTL)                                
         B     VALINPX                                                          
*                                                                               
VALINP6  CLI   FLDILEN,5           N THRU NNNNN                                 
         BH    INVALID                                                          
         SR    R1,R1                                                            
         IC    R1,FLDILEN                                                       
         BCTR  R1,0                                                             
         EX    R1,*+8                                                           
         B     *+10                                                             
         PACK  DUB,FLDDATA(0)                                                   
         CVB   R1,DUB                                                           
         LTR   R1,R1                                                            
         BZ    INVALID                                                          
         STH   R1,TNO                                                           
VALINP6A CLC   TNUM,TNO            TERMINAL NUMBER MUST BE IN UTL               
         BE    VALINP6B                                                         
         BXLE  R5,R6,VALINP6A                                                   
         B     INVALID                                                          
VALINP6B MVC   TERMID,TLUID                                                     
         ST    R5,PUTL             RETURN A(UTL)                                
         B     VALINPX                                                          
*                                                                               
MISSING  OI    PFLD,X'80'          SET MISSING RETURN CODE                      
         B     EXIT                                                             
*                                                                               
INVALID  OI    PFLD,X'40'          SET INVALID RETURN CODE                      
         B     EXIT                                                             
*                                                                               
VALINPX  EQU   *                                                                
         EJECT                                                                  
*IF X'20' OR X'10' BITS SET THEN TERMINAL RECORD WILL BE READ.                  
*IF X'10' BIT SET THEN RECORD WILL BE SEARCHED AND CHECKED FOR VALID            
*FACPAK AUTHORISATION AND A(TERMINAL DEFN ELEMENT) RETURNED IN P4.              
*                                                                               
GETTRM   TM    IFLAGS,X'30'        TEST IF CALLER WANTS TERMINAL REC            
         BZ    GETTRMX                                                          
         XC    PTRMREC,PTRMREC     SET NO TERMINAL RECORD FOUND                 
         TM    IFLAGS,X'10'                                                     
         BZ    *+10                                                             
         XC    PTRMEL,PTRMEL       SET NO TERMINAL DEFN ELEMENT FOUND           
*                                                                               
         LA    R5,TERMREC                                                       
         USING CTTREC,R5                                                        
         XC    CTTKEY,CTTKEY                                                    
         MVI   CTTKTYP,C'T'                                                     
         MVC   CTTKTID,TERMID                                                   
         MVC   TERMKEY,TERMREC                                                  
         GOTO1 VDMGR,DMCB,DMREAD,CTFILE,TERMKEY,(R5)                            
         CLI   8(R1),0                                                          
         BE    GETTRM0                                                          
*                                                                               
         XC    CTTKEY,CTTKEY       NOW TRY FOR GENERIC (3 CHARACTER)            
         MVI   CTTKTYP,C'T'                                                     
         MVC   CTTKTID,TERMID                                                   
         MVC   CTTKTID+4(4),=C'%%%T'                                            
         MVC   TERMKEY,TERMREC                                                  
         GOTO1 VDMGR,DMCB,DMREAD,CTFILE,TERMKEY,(R5)                            
         CLI   8(R1),0                                                          
         BE    GETTRM0                                                          
*                                                                               
         OI    PFLD,X'20'          SET NOT FOUND                                
         B     GETTRMX                                                          
*                                                                               
GETTRM0  ST    R5,PTRMREC          RETURN A(TERMINAL RECORD)                    
         MVC   CTTKTID,TERMID      RESTORE LUID IN CASE IT IS A GENERIC         
*                                                                               
         TM    IFLAGS,X'10'                                                     
         BZ    GETTRMX                                                          
         MVC   DUB,=CL8' '                                                      
         MVI   BYTE,X'00'          SET FACPAK VTAM APPLIC ID FLAGS              
         LA    R5,CTTDATA                                                       
         LA    R0,255                                                           
         SR    R1,R1                                                            
*                                                                               
GETTRM1  CLI   0(R5),0             TEST END OF RECORD                           
         BE    GETTRM8                                                          
         CLI   0(R5),X'24'         TEST APPLICATION ID EL                       
         BE    GETTRM4                                                          
         CLI   0(R5),X'25'         TEST TERMINAL DEFN ELEMENT                   
         BE    GETTRM5                                                          
         CLI   0(R5),X'26'         TEST VTAM LUID ELEMENT                       
         BE    GETTRM6                                                          
*                                                                               
GETTRM2  IC    R1,1(R5)            BUMP TO NEXT ELEMENT                         
         CHI   R1,2                                                             
         BL    GETTRM8                                                          
         AR    R5,R1                                                            
         BCT   R0,GETTRM1                                                       
*                                                                               
GETTRM4  CLI   VTAPLID,C' '        TEST IF VTAM APPLICATION                     
         BNH   GETTRM2                                                          
         OI    BYTE,X'80'          SET APPLID ID ELEMENT FOUND                  
         IC    R1,1(R5)                                                         
*&&US                                                                           
         CLI   TERMID+7,C'S'       TEST SHUTTLE                                 
         BE    *+12                YES - REQUIRE EXACT MATCH                    
         LA    R1,2                SET TO COMPARE ONLY 3 CHARACTERS             
         B     GETTRM4A                                                         
*&&                                                                             
         SH    R1,=H'4'            GET LEN OF APPLID-1                          
         BP    *+10                                                             
         SR    R1,R1                                                            
         B     GETTRM2                                                          
GETTRM4A CLC   3(3,R5),=C'ALL'     TEST VALID FOR ALL FACPAKS                   
         BE    GETTRM4B            YES                                          
*&&UK*&& CLC   VTAPLID,=CL8'LADVA' LADVA IS AVAILABLE TO ALL                    
*&&UK*&& BE    GETTRM4B                                                         
         EX    R1,*+8                                                           
         B     *+10                                                             
         CLC   3(0,R5),VTAPLID     TEST VALID FOR THIS FACPAK                   
         BNE   GETTRM2                                                          
GETTRM4B OI    BYTE,X'01'          SET VALID FOR THIS FACPAK                    
         B     GETTRM2                                                          
*                                                                               
GETTRM5  ST    R5,PTRMEL           RETURN A(TERMINAL DEFN ELEMENT)              
         B     GETTRM2                                                          
*                                                                               
GETTRM6  TM    2(R5),X'80'         LUID ELEMENT FOUND                           
         BO    GETTRM8             IGNORE BTAM TERMINAL ID                      
         MVC   VTLUID,3(R5)        RETURN VTAM LUID                             
*                                                                               
GETTRM8  TM    BYTE,X'80'          ANY APPLID ELEMENTS FOUND                    
         BZ    GETTRMX             NO THEN ASSUME OK FOR THIS FACPAK            
         TM    BYTE,X'01'          VALID FOR THIS FACPAK                        
         BO    GETTRMX             YES THEN TERMINAL OK                         
         OI    PFLD,X'10'          SET TERMINAL NOT AUTHORISED                  
*                                                                               
GETTRMX  B     EXIT                                                             
*                                                                               
EXIT     XMOD1 1                                                                
         EJECT                                                                  
***********************************************************************         
* LITERALS AND CONSTANTS                                              *         
***********************************************************************         
         LTORG                                                                  
*                                                                               
VDMGR    DC    V(DATAMGR)                                                       
DMREAD   DC    CL8'DMREAD'                                                      
CTFILE   DC    CL8'CTFILE'                                                      
         EJECT                                                                  
***********************************************************************         
* WORKING STORAGE                                                     *         
***********************************************************************         
TWRK     DSECT                                                                  
DUB      DS    D                                                                
DMCB     DS    6F                                                               
TNO      DS    H                                                                
HALF     DS    H                                                                
IFLAGS   DS    X                                                                
BYTE     DS    X                                                                
VTAPLID  DS    CL8                                                              
FHDR     DS    XL8                                                              
FDATA    DS    XL8                                                              
*                                                                               
TERMID   DS    CL8                                                              
VTLUID   DS    CL8                                                              
*                                                                               
TERMKEY  DS    CL25                                                             
TERMREC  DS    2000C                                                            
*                                                                               
TWRKX    EQU   *                                                                
                                                                                
PARM     DSECT                                                                  
PFLD     DS    F                                                                
PUTL     DS    F                                                                
PTRMREC  DS    F                                                                
PTRMEL   DS    F                                                                
         EJECT                                                                  
***********************************************************************         
* OTHER INCLUDED DSECTS                                               *         
***********************************************************************         
* FLDHDR                                                                        
         PRINT OFF                                                              
       ++INCLUDE DDFLDHDR                                                       
         PRINT ON                                                               
* FASSB                                                                         
         PRINT OFF                                                              
       ++INCLUDE FASSB                                                          
         PRINT ON                                                               
* FAUTL                                                                         
         PRINT OFF                                                              
       ++INCLUDE FAUTL                                                          
         PRINT ON                                                               
* FATCB                                                                         
         PRINT OFF                                                              
       ++INCLUDE FATCB                                                          
         PRINT ON                                                               
* FASYSFAC                                                                      
         PRINT OFF                                                              
       ++INCLUDE FASYSFAC                                                       
         PRINT ON                                                               
* CTGENFILE                                                                     
         PRINT OFF                                                              
       ++INCLUDE CTGENFILE                                                      
         PRINT ON                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'006FATERMVAL 03/05/14'                                      
         END                                                                    
