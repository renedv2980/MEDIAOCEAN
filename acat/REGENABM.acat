*          DATA SET REGENABM   AT LEVEL 122 AS OF 05/01/02                      
*CATALP ALTBCMON                                                                
         TITLE 'REGENABM - REPPAK ALTERNATE CALENDAR DATE RTN'                  
***********************************************************************         
*    HISTORY OF CHANGES                                                         
*    JUL18/00  (BU ) --- CREATED FROM REGENBUC                                  
*                                                                               
***********************************************************************         
*                                                                     *         
*                                                                     *         
*                                                                     *         
*                                                                     *         
***********************************************************************         
         TITLE 'ALTBCMON - ALTERNATE CALENDAR ROUTINE'                          
***********************************************************************         
*                                                                               
* THIS ROUTINE WILL ONLY PROCESS ALTERNATE CALENDAR.                            
*                                                                               
* ALTBCMON  -                                                                   
*                                                                               
*  INPUT:                                                                       
*              PARAMETER 1 =       BYTE 0     CALENDAR OPTIONS                  
*                                               DEFAULT:  DETERMINE             
*                                                WHICH CALENDAR EXISTS          
*                                                                               
*                                  BYTE 1-3    A(INPUT DATE)                    
*                                               YYMMDD EBCDIC                   
*                                                                               
*                                                                               
*              PARAMETER 2 =       BYTE 1-3    A(16-BYTE OUTPUT AREA)           
*                                                                               
*                                                                               
*              PARAMETER 3 =       BYTE 1-3   A(ROUTINE ADDRESS BLOCK)          
*                                                WORD 1=A(GETBROAD)             
*                                                WORD 2=A(GETDAY)               
*                                                WORD 3=A(ADDAY)                
*                                                WORD 4=A(DATCON)               
*                                                WORD 5=A(DATAMGR)              
*                                                                               
*              PARAMETER 4 =       BYTE 2-3   REP AGENCY ALPHA CODE             
*                                                                               
*              PARAMETER 5 =       BYTE 1-3   A(STATION CALL LETTERS)           
*                                             0 IF NO STATION WANTED            
*                                             (CLEAR OUT PARAMETER)             
*                                                                               
*    PROCESSING SEQUENCE:                                                       
*    1.   IF PARAMETER 5 CONTAINS A STATION ADDRESS, THE ALT CAL FOR            
*            THAT STATION WILL BE SOUGHT, AND, IF FOUND, USED.                  
*    2.   IF NO STATION ADDRESS, OR NO ALT CAL FOR STATION, ALT CAL             
*            FOR THE REP WILL BE SOUGHT, AND, IF FOUND, USED.                   
*    3.   IF NO ALT CAL RECORD FOUND, A GETBROAD CALL WILL BE ISSUED            
*            FOR INPUT DATE, AND BROADCAST DATES RETURNED, ALONG WITH A         
*            FLAG INDICATING STANDARD GETBROAD CALL (SEE PARM 1 OUTPUT,         
*            BELOW)                                                             
*                                                                               
*              PARAMETER 6 =       BYTE 1-3   A(RESTART KEY)                    
*                                             0 IF NO RESTART                   
*                                             (CLEAR OUT PARAMETER)             
*                                                                               
*  OUTPUT :                                                                     
*    PARM 1        BYTE 0      X'FF' INVALID DATE                               
*                              X'80' STATION ALT CALENDAR FOUND                 
*                              X'10' NO ALT CALENDAR FOUND:                     
*                                    REGULAR GETBROAD USED.                     
*                                                                               
*    OUTPUT AREA   XL3         BROADCAST START DATE YMD BINARY                  
*                  XL3         BROADCAST END   DATE YMD BINARY                  
*                              REMAINDER OF AREA HAS BEEN USED                  
*                              FOR WORK PURPOSES.                               
*                                                                               
***********************************************************************         
ALTBCMON CSECT                                                                  
         NMOD1 GMWORKLQ,ALTBCMON,CLEAR=YES                                      
         USING GMWORKD,RC                                                       
         ST    R1,GMSAVER1         SAVE A(PARMS)                                
         LM    R2,R3,0(R1)                                                      
         L     RE,8(R1)                                                         
         MVC   GGETBRD(20),0(RE)   ROUTINES                                     
         MVC   GMAGENCY,12+2(R1)                                                
         L     RE,16(R1)                                                        
         MVC   GMSTA,0(RE)                                                      
*                                                                               
*---------------------------------------------------*                           
* READ ACLREC, DETERMINE MONTH, SETUP RETURN & EXIT                             
*---------------------------------------------------*                           
GETACL   DS    0H                                                               
         XC    GMFLAGS,GMFLAGS                                                  
*                                                                               
         GOTO1 GDATCON,GMDMCB,(0,(R2)),(3,GMFULL2)                              
         CLC   GMSTA,=CL5' '       ANY STATION ENTERED?                         
         BNH   GTAC0020            NO  - GO RIGHT TO REP ALT CAL                
*                                                                               
         OI    GMFLAGS,X'80'       SET 'STATION ALT CAL'                        
         BAS   RE,READALT          YES - READ FOR STATION ALT CAL               
         BZ    GTAC0040            FOUND - USE STATION RECORD                   
         XC    GMFLAGS,GMFLAGS     TURN OFF FLAG                                
GTAC0020 EQU   *                                                                
         MVC   GMSTA,=C'     '     NOT FOUND - LOOK FOR REP                     
         BAS   RE,READALT          READ FOR REP ALT CAL                         
         BZ    GTAC0040            FOUND                                        
         OI    GMFLAGS,X'10'       SET REGULAR BROADCAST                        
         B     GTAC0120            NOT FOUND - REGULAR GETBROAD                 
*                                                                               
*                                                                               
GTAC0040 DS    0H                                                               
         GOTO1 GDATCON,GMDMCB,(0,(R2)),(2,GMFULL)                               
*                                                                               
         LA    R6,GMIOAREA        GET END DATE ELEMENT                          
         MVI   GMELCODE,X'10'                                                   
         BAS   RE,GETEL                                                         
         BE    *+6                 REQ'D                                        
         DC    H'0'                                                             
         LR    R5,R6                                                            
         LA    R5,RACLDATE-RACLELEM(R5)                                         
*                                                                               
         ZIC   RF,GMFULL2+1        GET MONTH NUMBER FROM DATE                   
*                                  MONTH IS NOT ZERO RELATIVE WITHIN            
*                                     RECORD ARRAY.  MONTH NUMBER GIVES         
*                                     EXACT OFFSET TO END DATE OF MONTH         
         SLL   RF,1                DOUBLE THE DISPLACEMENT                      
         LR    R8,R5               CALCULATE END DATE                           
         AR    R8,RF               SET A(END DATE OF MONTH)                     
         L     R7,GMSAVER1         GET PARAMETER ADDRESS                        
         L     R7,4(R7)            SET A(OUTPUT AREA)                           
*                                     2ND PARAM IN PARAM LIST                   
         GOTO1 GDATCON,GMDMCB,(2,(R8)),(3,3(R7))                                
         LR    R8,R5                                                            
         ZIC   RF,GMFULL2+1        GET MONTH NUMBER FROM DATE                   
         SLL   RF,1                DOUBLE THE DISPLACEMENT                      
         BCTR  RF,0                BACK UP ONE MONTH                            
         BCTR  RF,0                (TWO BYTES PER MONTH)                        
         AR    R8,RF               SET A(START DATE OF MONTH)                   
         L     R7,GMSAVER1         GET PARAMETER ADDRESS                        
         L     R7,4(R7)            SET A(OUTPUT AREA)                           
*                                     2ND PARAM IN PARAM LIST                   
         GOTO1 GDATCON,GMDMCB,(2,(R8)),(3,0(R7))                                
*                                                                               
*   BUMP START DATE BY ONE DAY                                                  
*                                                                               
         GOTO1 GDATCON,GMDMCB,(3,0(R7)),(0,GMDUB)                               
         GOTO1 GADDAY,GMDMCB,GMDUB,GMDUB,1                                      
         GOTO1 GDATCON,GMDMCB,(0,GMDUB),(3,0(R7))                               
*                                                                               
         L     R1,GMSAVER1                                                      
         MVC   0(1,R1),GMFLAGS                                                  
         B     GTAC0160                                                         
         SPACE 3                                                                
GTAC0120 EQU   *                                                                
         L     R3,GMSAVER1         GET PARAMETER ADDRESS                        
         L     R3,4(R3)            SET A(OUTPUT AREA)                           
*                                     2ND PARAM IN PARAM LIST                   
         GOTO1 GGETBRD,GMDMCB,(1,(R2)),(R3),GGETDAY,GADDAY                      
         CLI   0(R1),X'FF'         INVALID?                                     
         BE    GTAC0140            YES                                          
*                                                                               
         GOTO1 GDATCON,GMDMCB,(0,0(R3)),(3,0(R3))                               
         GOTO1 GDATCON,GMDMCB,(0,6(R3)),(3,3(R3))                               
*                                                                               
         L     R1,GMSAVER1                                                      
         MVC   0(1,R1),GMFLAGS                                                  
         B     GTAC0160                                                         
         EJECT                                                                  
         DC    H'0'                KILL FOR NOW                                 
*---------------------------------------------------------------------          
GTAC0140 DS    0H                                                               
         L     R1,GMSAVER1                                                      
         MVI   0(R1),X'FF'                                                      
         B     GTAC0160                                                         
*                                                                               
GTAC0160 DS    0H                                                               
         L     R3,GMSAVER1         GET PARAMETER ADDRESS                        
         L     R3,20(R3)           SET A(RESTART KEY)                           
*                                     6TH PARAM IN PARAM LIST                   
         LTR   R3,R3               ANY ADDRESS IN PARAMETER?                    
         BZ    GTAC0240            NO  - FINISHED                               
         XC    GMKEY,GMKEY         CLEAR KEY                                    
         MVC   GMKEY,0(R3)         INSERT RESTART KEY                           
         MVC   GMKEYSV,GMKEY       SAVE KEY AGAIN                               
         GOTO1 GDATAMGR,GMDMCB,(0,=C'DMRDHI'),=C'REPDIR',GMKEY,GMKEY            
         CLI   GMDMCB+8,0                                                       
         BE    *+6                 RESTART OKAY                                 
         DC    H'0'                RESTART FAILED                               
*                                                                               
         CLC   GMKEY(L'RACLKEY),GMKEYSV                                         
*                                  RECORD FOUND?                                
         BE    GTAC0240            YES                                          
         DC    H'0'                RESTART FAILED                               
GTAC0240 EQU   *                                                                
         CR    RB,RB                                                            
         XIT1                                                                   
         EJECT                                                                  
READALT  NTR1                                                                   
         XC    GMKEY,GMKEY                                                      
         LA    R5,GMKEY                                                         
         USING RACLKEY,R5                                                       
         MVI   RACLKTYP,X'20'                                                   
         MVC   RACLKREP,GMAGENCY   INSERT REP CODE                              
         MVC   RACLKYR,GMFULL2     INSERT YEAR                                  
*                                                                               
         CLC   GMSTA,=CL5' '       ANY STATION ENTERED?                         
         BNH   RALT0020            NO  - GO RIGHT TO REP ALT CAL                
*                                                                               
         MVC   RACLKNAM,GMSTA      LOAD STATION INTO KEY                        
         CLI   RACLKNAM+4,C'T'                                                  
         BNE   *+8                                                              
         MVI   RACLKNAM+4,C' '                                                  
*                                                                               
RALT0020 DS    0H                                                               
         DROP  R5                                                               
*                                                                               
         MVC   GMKEYSV,GMKEY                                                    
         GOTO1 GDATAMGR,GMDMCB,(0,=C'DMRDHI'),=C'REPDIR',GMKEY,GMKEY            
         CLI   GMDMCB+8,0                                                       
         BNE   RALT0060            NO ACL REC                                   
*                                                                               
         CLC   GMKEY(L'RACLKEY),GMKEYSV                                         
*                                  RECORD FOUND?                                
         BNE   RALT0060            NO                                           
RALT0040 DS    0H                                                               
         GOTO1 GDATAMGR,GMDMCB,(0,=C'GETREC'),=C'REPFIL',GMKEY+28,     +        
               GMIOAREA,GMDMWORK                                                
         CLI   GMDMCB+8,0           DISK ERROR CHECK                            
         BE    *+6                                                              
         DC    H'0'                                                             
         SR    R0,R0               SET CC = ZERO                                
         B     RALT0080                                                         
RALT0060 EQU   *                                                                
         LTR   RB,RB               SET CC NOT ZERO                              
RALT0080 EQU   *                                                                
         XIT1                                                                   
*                                                                               
         EJECT                                                                  
***********************************************************************         
         GETEL R6,=Y(RACLDESC-RACLREC),GMELCODE                                 
*                                                                               
         LTORG                                                                  
*                                                                               
***>>>   FOR TESTING PURPOSES, KEEP THE SIZE OF THIS MODULE THE SAME            
         ORG   ALTBCMON+(((*-ALTBCMON)/2048)+1)*2048                            
*                                                                               
***********************************************************************         
* GETMONTH WORKING STORAGE                                                      
***********************************************************************         
ACLREC   DSECT                                                                  
       ++INCLUDE REGENACL                                                       
*                                                                               
GMWORKD  DSECT                                                                  
GMDMWORK DS    12D                                                              
GMDUB    DS    D                                                                
GMDMCB   DS    6F                                                               
*                                  ADDRESS PARAMETER BLOCK                      
GGETBRD  DS    A                     A(GETBROAD)                                
GGETDAY  DS    A                     A(GETDAY)                                  
GADDAY   DS    A                     A(ADDAY)                                   
GDATCON  DS    A                     A(DATCON)                                  
GDATAMGR DS    A                     A(DATAMGR)                                 
*                                                                               
GMSAVER1 DS    F                   R1                                           
*                                                                               
GMFULL   DS    F                                                                
GMFULL2  DS    F                                                                
*                                                                               
GMAGENCY DS    CL2                 REP AGENCY ALPHA CODE                        
GMELCODE DS    X                                                                
GMFLAGS  DS    X                   CONTROL FLAGS                                
*                                   - X'80' STATION ALT CAL USED                
*                                   - X'10' REGULAR BROADCAST CALENDAR          
*                                                                               
GMSTA    DS    CL5                 STATION CALL LETTERS                         
         DS    0H                                                               
*                                                                               
GM1STKEY DS    CL48                                                             
GMKEY    DS    CL48                                                             
GMKEYSV  DS    CL48                                                             
GMIOAREA DS    XL2000                                                           
GMWORKLQ EQU   *-GMWORKD                                                        
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'122REGENABM  05/01/02'                                      
         END                                                                    
