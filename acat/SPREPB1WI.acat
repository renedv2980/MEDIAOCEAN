*          DATA SET SPREPB1WI  AT LEVEL 016 AS OF 05/01/02                      
*CATALP SPB1WI                                                                  
         SPACE 2                                                                
*===============================================================*               
* THIS PROGRAM SUPPORTS SPOT B1 BILLING FOR WESTERN INTL MEDIA  *               
* PW CLIENTS.                                                   *               
*                                                               *               
* INPUT  -- SPOT PW RECORDS                                     *               
*                                                               *               
* OUTPUT -- TSAR BUFFER BUILT THEN HOOKS TO CALLER FOR EACH     *               
*           RECORD                                              *               
*===============================================================*               
         SPACE 2                                                                
*===============================================================*               
*                                                               *               
* 13APR98 MHER CHANGE FOR X'26' PAY ELEMENTS THAT OVERRIDE      *               
*              ALL OTHER PW AMOUNTS                             *               
*                                                               *               
* 08JUN98 MHER ADJ DR/CR MUST BE PRESENT TO HONOR X'26' ELEM    *               
*                                                               *               
*===============================================================*               
SPB1WI   TITLE 'WI/PW BILLING INTERFACE'                                        
SPB1WI   CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 0,SPB1WI                                                         
*                                                                               
         L     RC,0(R1)            ADDRESS OF SPREPWORKD                        
         LA    R9,2048(RC)                                                      
         LA    R9,2048(R9)                                                      
         USING SPWORKD,RC,R9                                                    
         MVC   USERRD,4(RD)        SAVE LINK BACK TO USER                       
*                                                                               
         L     R8,4(R1)            ADDRESS OF SPWIB1BLK                         
         USING SPWID,R8                                                         
*                                                                               
* INITIALIZE TSARD                                                              
*                                                                               
         OC    TSARBUFF,TSARBUFF                                                
         BNZ   WI2                                                              
* MOVE ADDRESS FROM ADCONLST TO LOCAL STORAGE                                   
         L     RE,ADCONLST                                                      
         USING SPADCONS,RE                                                      
         MVC   WTSAROFF,TSAROFF                                                 
         DROP RE                                                                
*                                                                               
         L     R0,TSARBUFL                                                      
         GETMAIN RU,LV=(0),LOC=(ANY,ANY)                                        
         LTR   RF,RF                                                            
         BZ    *+6                                                              
         DC    H'0'                COULDN'T GET TSAR BUFFER                     
         ST    R1,TSARBUFF                                                      
*                                                                               
WI2      XC    TSAREA,TSAREA                                                    
         LA    R2,TSAREA                                                        
         USING TSARD,R2                                                         
         MVI   TSOFFACT,TSAINI                                                  
         MVC   TSABUF,TSARBUFF     SET BUFFER ADDRESS                           
         MVC   TSAREC,TSARBUFL     AND BUFFER LENGTH                            
         LA    R0,WIKEYL                                                        
         STC   R0,TSKEYL                                                        
         LA    R0,WIRECL                                                        
         STH   R0,TSRECL                                                        
         GOTO1 WTSAROFF,(R2)                                                    
         CLI   TSERRS,0                                                         
         BE    *+6                                                              
         DC    H'0'                UNBELIEVABLE !                               
         B     WI10                                                             
         SPACE 2                                                                
EXIT     XIT1                                                                   
         EJECT                                                                  
*============================================================*                  
* INITIALIZE A TABLE WITH START/END DATES OF BILLING MONTHS  *                  
*                                                                               
* FOR WESTERN THE MONTHS ARE BUILT BASED ON THE SUNDAY OF THE*                  
* WEEK IN WHICH THE MONTH ENDS FOR OOW ESTIMATES             *                  
*                                                            *                  
* THE TESTS BELOW FOR WILA OR WITEST ARE UTTERLY IRRATIONAL, *                  
* BUT FEAR OF RAMPANT DESTRUCTION DEMANDS CAUTION            *                  
* IN OTHER WORDS, THEY DON'T BELONG THERE, BUT YOU TAKE THEM *                  
* OUT                                                        *                  
*============================================================*                  
WI10     DS    0H                                                               
         CLC   =C'WI',QAGY                                                      
         BE    WI10A                                                            
         CLC   =C'WJ',QAGY                                                      
         BE    WI10A                                                            
         B     WI10B                                                            
*                                                                               
WI10A    L     RE,ADEST                                                         
         USING ESTHDRD,RE                                                       
         CLI   EOWSDAY,0           ONLY IF THERE IS ONE INPUT                   
         BE    *+10                                                             
         MVC   SPOTPROF+8(1),EOWSDAY                                            
         DROP  RE                                                               
WI10B    MVC   DUB(2),SPWQSYM      MOVE START YEAR/MONTH                        
         MVI   DUB+2,1                                                          
         GOTO1 DATCON,DMCB,(3,DUB),WORK                                         
         MVC   DUB(2),SPWQEYM      MOVE END YEAR/MONTH                          
         MVI   DUB+2,31                                                         
         GOTO1 DATCON,DMCB,(3,DUB),WORK+6                                       
* BUILD 14 BROADCAST MONTHS                                                     
         GOTO1 MOBILE,DMCB,(14,WORK),(1,SPWAIO)                                 
*                                                                               
         MVC   DUB(2),SPWQSYM                                                   
         LA    R4,DATETAB                                                       
         XC    DATETAB(DATETABL),DATETAB                                        
         L     R5,SPWAIO                                                        
*                                                                               
WI14     MVC   0(2,R4),DUB         MOVE YEAR/MONTH                              
         MVC   2(4,R4),0(R5)       MOVE START/END DATES                         
         ZIC   RE,DUB+1                                                         
         LA    RE,1(RE)            NEXT MONTH                                   
         STC   RE,DUB+1                                                         
         CLI   DUB+1,13            TEST PAST DEC                                
         BL    WI16                                                             
         IC    RE,DUB                                                           
         LA    RE,1(RE)            NEXT YEAR                                    
         STC   RE,DUB                                                           
         MVI   DUB+1,1                                                          
*                                                                               
WI16     DS    0H                                                               
         LA    R4,L'DATETAB(R4)    NEXT DATE ENTRY                              
         CLC   DUB(2),SPWQEYM      (MOBILE MAY HAVE DONE                        
         BH    WI17                ONE TOO MANY MONTHS)                         
         LA    R5,4(R5)                                                         
         CLI   0(R5),X'FF'                                                      
         BNE   WI14                                                             
WI17     DS    0H                                                               
         MVI   0(R4),X'FF'         SET E-O-L FLAG                               
         EJECT                                                                  
*=========================================================*                     
* READ THROUGH PW RECORDS AND BUILD TSAR BUFFER           *                     
*=========================================================*                     
         SPACE 1                                                                
KEYD1    USING PWRECD,KEY                                                       
*                                                                               
WI20     XC    KEY,KEY                                                          
*                                                                               
         MVC   KEYD1.PWKTYP,=X'0D7A'                                            
         L     RE,ADCLT                                                         
         MVC   KEYD1.PWKAGMD,1(RE)                                              
         MVC   KEYD1.PWKCLT,2(RE)                                               
         MVC   KEYD1.PWKPRD,SPWQPRD                                             
         MVC   KEYD1.PWKEST,SPWQEST                                             
         MVC   KEYD1.PWKMKT,SPWQMKT                                             
*                                                                               
WI22     DS    0H                                                               
         GOTO1 HIGH                                                             
         B     WI26                                                             
*                                                                               
WI24     DS    0H                                                               
         GOTO1 SEQ                                                              
*                                                                               
WI26     CLC   KEY(6),KEYSAVE      TYPE/A-M/CLT/PRD                             
         BNE   WI70                NO - GO TO OUTPUT                            
*                                                                               
         CLI   SPWQEST,0           WAS EST REQUESTED = 0                        
         BE    WI28                                                             
         CLC   KEY(7),KEYSAVE      TYPE/A-M/CLT/PRD/EST                         
         BNE   WI70                NO - GO TO OUTPUT                            
         B     WI30                                                             
*                                                                               
WI28     SR    R0,R0                                                            
         IC    R0,KEYD1.PWKEST                                                  
         LA    R1,ESTLST           ESTLST                                       
         AR    R1,R0               PLUS OFFSET                                  
         CLI   0(R1),0             CAN USE THIS EST                             
         BNZ   WI30                YES                                          
         MVC   KEYD1.PWKMKT,=X'FFFF' NEXT EST                                   
         B     WI22                READ HIGH FOR NEXT EST                       
*                                                                               
         EJECT                                                                  
WI30     DS    0H                                                               
         OC    SPWQMKT,SPWQMKT     IF SPECIFIC MKT REQ'D                        
         BZ    *+14                                                             
         CLC   KEYD1.PWKMKT,SPWQMKT  MUST HAVE RIGHT ONE                        
         BNE   WI24                                                             
*                                                                               
         XC    WIREC(WIRECL),WIREC    CLEAR TSAR RECORD                         
*                                                                               
         L     R6,SPWAIO                                                        
         ST    R6,AREC                                                          
*                                                                               
         GOTO1 GET                                                              
*                                                                               
         MVI   ELCODE,01           LOOK FOR HEADER ELEM                         
         BAS   RE,GETEL                                                         
         USING PWGNEL,R6                                                        
         BE    *+6                                                              
         DC    H'0'                NO HEADER                                    
*                                                                               
         OC    KEYD1.PWKSTA,KEYD1.PWKSTA     IS IT A MARKET RECORD              
         BNZ   WI32                                                             
         DROP  KEYD1                                                            
*                                                                               
         MVI   SAVSTAT,C'N'        YES, SET LOCKIN STATUS                       
         TM    PWGNFLG,PWGNPLKQ                                                 
         BZ    WI24                NOT PW LOCKED, NEXT RECORD                   
         MVI   SAVSTAT,C'L'                                                     
         B     WI34                                                             
         EJECT                                                                  
WI32     DS    0H                  STATION RECORD                               
         CLI   SAVSTAT,C'N'        IF MARKET NOT LOCKED IN                      
         BE    WI24                SKIP STATION                                 
         B     WI40                RESET X'26' FLAGS AT MKT LVL ONLY            
         SPACE 1                                                                
WI34     LA    R4,DATETAB                                                       
*                                                                               
WI36     NI    6(R4),X'7F'         RESET PREVIOUS FLAGS                         
         LA    R4,L'DATETAB(R4)                                                 
         CLI   0(R4),X'FF'                                                      
         BNE   WI36                                                             
         SPACE 1                                                                
*================================================================*              
* LOOK FOR X'26' OVERRIDE ELEMENTS                               *              
*================================================================*              
         SPACE 1                                                                
WI40     L     R6,SPWAIO                                                        
         MVI   ELCODE,X'26'                                                     
         LA    R6,24(R6)                                                        
*                                                                               
WI42     BAS   RE,NEXTEL                                                        
         BNE   WI50                GO LOOK FOR WEEK ELEMENTS NEXT               
*                                                                               
         LA    R4,DATETAB                                                       
         USING PWOOWEL,R6                                                       
WI44     CLC   PWOOWYM,0(R4)       MATCH Y/M                                    
         BE    WI46                                                             
         LA    R4,L'DATETAB(R4)                                                 
         CLI   0(R4),X'FF'                                                      
         BNE   WI44                                                             
         B     WI42                                                             
WI46     OI    6(R4),X'80'         SET FLAG FOR X'26' ELEM IN DATETAB           
         BAS   RE,WIPOST                                                        
         B     WI42                                                             
         EJECT                                                                  
*===============================================================*               
* SEARCH FOR WEEK ELEMENTS                                      *               
*===============================================================*               
         SPACE 1                                                                
WI50     DS    0H                                                               
         L     R6,SPWAIO                                                        
         MVI   ELCODE,6                                                         
         BAS   RE,GETEL                                                         
         BNE   WI24                IF NO WEEK ELEMENTS, NEXT RECORD             
*                                                                               
         USING PWDOLEL,R6                                                       
WI52     DS    0H                                                               
*                                                                               
WI54     LA    R4,DATETAB          TEST IF ELEMENT IN BILLING PERIOD            
*                                                                               
WI56     TM    6(R4),X'80'         TEST X'26' ELEMENT WON THIS MONTH            
         BO    WI60                                                             
         CLC   PWDOLWK,2(R4)       TEST PRIOR TO START OF MONTH                 
         BL    WI60                IF PRIOR, DONE                               
         CLC   PWDOLWK,4(R4)       TEST AFTER END OF MONTH                      
         BH    WI58                                                             
*                                                                               
         BAS   RE,FINDOVRD         FIND ANY OVERRIDE                            
         BAS   RE,WIPOST           POST DOLLARS TO WIREC                        
         B     WI60                                                             
*                                                                               
WI58     LA    R4,L'DATETAB(R4)                                                 
         CLI   0(R4),X'FF'                                                      
         BNE   WI56                                                             
*                                                                               
WI60     MVI   ELCODE,6            RESTORE ELCODE                               
         BAS   RE,NEXTEL                                                        
         BE    WI52                                                             
* END OF RECORD                                                                 
         OC    WIKEY(WIKEYL),WIKEY   ANYTHING IN TSAR RECORD                    
         BZ    *+8                                                              
         BAS   RE,TSARPUT                                                       
         B     WI24                                                             
*                                                                               
DIV      DRNDR (R0),(RF)                                                        
         BR    RE                                                               
         EJECT                                                                  
*=============================================================*                 
* POST CURRENT ELEMENT TO TSAR RECORD                         *                 
* ON ENTRY R4 POINTS TO DATE TABLE ENTRY                      *                 
*          R6 POINTS TO PWDOLEL                               *                 
*=============================================================*                 
         SPACE 1                                                                
KEYD2    USING PWRECD,KEY                                                       
*                                                                               
WIPOST   NTR1                                                                   
         LR    R7,R6               SAVE 06 ELEM ADDRESS                         
         OC    WIKEY(WIKEYL),WIKEY     TEST PREVIOUS RECORD                     
         BZ    WIPOST4                 NO                                       
*                                                                               
WIPOST2  CLC   WIKYM,0(R4)             TEST SAME YEAR/MONTH AS PREVIOUS         
         BE    WIPOST10                                                         
         BAS   RE,TSARPUT                                                       
*                                                                               
WIPOST4  XC    WIREC(WIRECL),WIREC     BUILD NEW RECORD                         
         MVC   WIKEST,KEYD2.PWKEST                                              
         MVC   WIKMKT,KEYD2.PWKMKT                                              
         MVC   WIKSTA,KEYD2.PWKSTA                                              
         MVC   WIKYM,0(R4)             MOVE YEAR/MONTH                          
*                                                                               
WIPOST10 CLI   0(R6),X'26'         TEST POSTING X'26' ELEM                      
         BNE   WIPOST20                                                         
*                                                                               
         CLI   SPWQBTYP,C'D'       TEST DETAIL BILL                             
         BNE   WIPOST12                                                         
*                                                                               
         USING PWOOWEL,R6                                                       
*                                                                               
         ICM   R1,15,PWOOWBIL                                                   
         BZ    WIPOSTXX                                                         
         OI    WISTATUS,WISADJQ    SET HAVE ADJUSTMENT                          
*                                                                               
WIPOST12 L     R0,WISPTS                                                        
         ICM   R1,15,PWOOWSPT                                                   
         AR    R0,R1                                                            
         OC    KEYD2.PWKSTA,KEYD2.PWKSTA  TEST STATION PRESENT                  
         BZ    *+8                        NO - NO SPOTS FOR MKT                 
         ST    R0,WISPTS                                                        
*                                                                               
         L     R0,WICLTG           CLIENT GROSS                                 
         ICM   R1,15,PWOOWCG                                                    
         AR    R0,R1                                                            
         ST    R0,WICLTG                                                        
*                                                                               
         L     R0,WICLTN           CLIENT NET                                   
         ICM   R1,15,PWOOWCN                                                    
         AR    R0,R1                                                            
         ST    R0,WICLTN                                                        
*                                                                               
         L     R0,WITAX            CLIENT TAX                                   
         A     R0,PWOOWCTX                                                      
         ST    R0,WITAX                                                         
*                                                                               
         L     R0,WIWIMG           WIM GROSS                                    
         ICM   R1,15,PWOOWWG                                                    
         AR    R0,R1                                                            
         ST    R0,WIWIMG                                                        
*                                                                               
         L     R0,WIWIMN           WIM NET                                      
         ICM   R1,15,PWOOWWN                                                    
         AR    R0,R1                                                            
         ST    R0,WIWIMN                                                        
*                                                                               
         MVC   WIWIMX,PWOOWTAX     RETURN WIMTAX                                
*                                                                               
         B     WIPOSTX                                                          
         DROP  R6                                                               
         EJECT                                                                  
         USING PWDOLEL,R6                                                       
WIPOST20 L     R0,WISPTS                                                        
         ICM   R1,15,PWDOLSPT                                                   
         AR    R0,R1                                                            
         OC    KEYD2.PWKSTA,KEYD2.PWKSTA  TEST STATION PRESENT                  
         BZ    *+8                        NO - NO SPOTS FOR MKT                 
         ST    R0,WISPTS                                                        
*                                                                               
         L     R0,WICLTG                                                        
         ICM   R1,15,PWDOLCG       GROSS                                        
         AR    R0,R1                                                            
         ST    R0,WICLTG                                                        
*                                                                               
         L     R0,SVOVRG           SAVE GROSS FOR ADJ CALC                      
         ST    R0,SVCLTG           (NOTE- CLIENT GROSS FROM FINDOVRD)           
*                                                                               
         L     R0,WICLOVDG                                                      
         A     R0,SVOVRG           OVERRIDE                                     
         ICM   R1,15,PWDOLCG       LESS ACTUAL                                  
         SR    R0,R1                                                            
         ST    R0,WICLOVDG         =DIFFERENCE                                  
*                                                                               
         L     R0,WICLTN           CLIENT NET                                   
         ICM   R1,15,PWDOLCN                                                    
         ST    R1,SVCLTN           SAVE NET FOR ADJ CALC                        
         AR    R0,R1                                                            
         ST    R0,WICLTN                                                        
*                                                                               
         OC    PWDOLTAX,PWDOLTAX                                                
         BZ    WIPOST30                                                         
         MVC   WIWIMX,PWDOLTAX     RETURN WIMTAX TO GRANT                       
*                                                                               
         ICM   R0,15,PWDOLCTX      TAX ON CLIENT GROSS                          
         A     R0,WITAX                                                         
         ST    R0,WITAX            THIS LOOKS LIKE WIM BUT IS CLT TAX           
*                                                                               
WIPOST30 L     R0,WIWIMG           ROLL UP GROSS                                
         ICM   R1,15,PWDOLWG                                                    
         ST    R1,SVWIMG           SAVE FOR DIFF CALC                           
         AR    R0,R1                                                            
         ST    R0,WIWIMG                                                        
*                                                                               
         L     R0,WIWIMN           AND NET                                      
         ICM   R1,15,PWDOLWN                                                    
         ST    R1,SVWIMN           SAVE FOR DIFF CALC                           
         AR    R0,R1                                                            
         ST    R0,WIWIMN                                                        
*                                                                               
         CLI   SPWQBTYP,C'D'       TEST DETAIL BILL                             
         BNE   WIPOSTXX                                                         
*                                                                               
         L     R0,WICLADJG                                                      
         ICM   R1,15,PWDOLBIL                                                   
         BZ    *+8                                                              
         OI    WISTATUS,WISADJQ    SET HAVE ADJUSTMENT                          
         C     R1,=X'80000000'     TEST SPECIAL FORMAT $0                       
         BNE   *+6                                                              
         SR    R1,R1                                                            
         AR    R0,R1                                                            
         ST    R0,WICLADJG                                                      
*                                                                               
         L     R1,WICLADJG         SET ADJ NET = 85% OF GROSS                   
         M     R0,=F'85'                                                        
         LA    RF,100                                                           
         BAS   RE,DIV                                                           
         ST    R1,WICLADJN                                                      
* FIND CURRENT DOLLARS ELEMENT FOR THIS WEEK                                    
         LR    R7,R6                                                            
         MVI   ELCODE,7                                                         
WIPOST40 BAS   RE,NEXTEL                                                        
         BNE   WIPOSTX             NO ELEMENT-LEAVE WELL ENOUGH ALONE           
         USING PWCUREL,R6                                                       
         CLC   PWCURWK,PWDOLWK-PWDOLEL(R7)                                      
         BNE   WIPOST40                                                         
* ADJUST ADJUSTMENT DOLLARS BY + CURRENT - LOCKED                               
         L     R1,SVCLTG                                                        
         ICM   R0,15,PWCURCG                                                    
         SR    R0,R1                                                            
         A     R0,WICLADJG                                                      
         ST    R0,WICLADJG                                                      
*                                                                               
         L     R1,SVCLTN                                                        
         ICM   R0,15,PWCURCN                                                    
         SR    R0,R1                                                            
         A     R0,WICLADJN                                                      
         ST    R0,WICLADJN                                                      
****AND ROLLUP WIM CURRENT-LOCKED WIM$                                          
         L     R1,WIWIMGD          CUMED GROSS DIFFERENCE                       
         ICM   R0,15,PWCURWG       PLUS CURRENT                                 
         AR    R1,R0                                                            
         S     R1,SVWIMG           LESS LOCKED                                  
         ST    R1,WIWIMGD          NEW DIFFERENCE                               
*                                                                               
         L     R1,WIWIMND          CUMED NET DIFFERENCE                         
         ICM   R0,15,PWCURWN       PLUS CURRENT                                 
         AR    R1,R0                                                            
         S     R1,SVWIMN           LESS LOCKED                                  
         ST    R1,WIWIMND          NEW DIFFERENCE                               
         DROP  R6                                                               
         EJECT                                                                  
WIPOSTX  LR    R6,R7               RESTORE ELEM POINTER FOR TRACE               
WIPOSTXX DS    0H                                                               
         TM    SPWINDS,X'80'       TEST TO TRACE TSAR CALLS                     
         BZ    EXIT                                                             
         ZIC   R0,1(R6)            GET ELEMENT LENGTH                           
         GOTO1 PRNTBL,DMCB,=C'06 ELEM',(R6),C'DUMP',(R0),=C'TOG'                
         LA    R0,WIRECL                                                        
         GOTO1 PRNTBL,DMCB,=C'WIREC',WIREC,C'DUMP',(R0),=C'TOG'                 
         B     EXIT                                                             
         EJECT                                                                  
*===============================================================*               
* PUT CURRENT RECORD TO TSAR                                    *               
*===============================================================*               
         SPACE 1                                                                
TSARPUT  NTR1                                                                   
         TM    SPWINDS,X'80'       TEST TO TRACE TSAR CALLS                     
         BZ    TSARPUT2                                                         
         GOTO1 PRNTBL,DMCB,=C'TSARPUT',WIREC,C'DUMP',WIRECL,=C'TOG'             
*                                                                               
TSARPUT2 MVI   TSOFFACT,TSAADD     SET TO ADD RECORD                            
         LA    RE,WIREC                                                         
         ST    RE,TSAREC                                                        
         GOTO1 WTSAROFF,(R2)                                                    
         CLI   TSERRS,0                                                         
         BE    EXIT                                                             
         DC    H'0'                                                             
         EJECT                                                                  
*===============================================================*               
* ALL RECORDS READ - NOW READ THROUGH TSAR BUFFER AND HOOK      *               
* TO CALLING PROGRAM                                            *               
*===============================================================*               
         SPACE 1                                                                
WI70     DS    0H                                                               
         XC    WIREC(WIRECL),WIREC                                              
         LA    R0,1                SET TO GET RECORD 1                          
         STH   R0,TSRNUM                                                        
         MVI   TSOFFACT,TSAGET                                                  
         GOTO1 WTSAROFF,(R2)                                                    
         B     WI74                                                             
*                                                                               
WI72     MVI   TSOFFACT,TSANXT                                                  
         GOTO1 WTSAROFF,(R2)                                                    
*                                                                               
WI74     TM    TSERRS,TSERNF       TEST NOT FOUND                               
         BO    EXIT                END OF BUFFER                                
*                                                                               
         L     RE,SPWAIO           MOVE WIREC TO CALLER'S IOAREA                
         MVC   0(WIRECL,RE),WIREC                                               
*                                                                               
         BAS   RE,GOSB1                                                         
         B     WI72                                                             
         EJECT                                                                  
*                                                                               
GOSB1    NTR1                                                                   
         TM    SPWINDS,X'40'       TEST TO TRACE HOOK CALLS                     
         BZ    GOSB1A                                                           
         L     R0,SPWAIO                                                        
         GOTO1 PRNTBL,DMCB,=C'GO SB1',(R0),C'DUMP',WIRECL,=C'1D00'              
*                                                                               
GOSB1A   L     RF,SPWHOOK                                                       
         L     RE,USERRD                                                        
         LM    R0,RC,20(RE)                                                     
         BASR  RE,RF                                                            
*                                                                               
GOSB1X   XIT1                                                                   
         SPACE 2                                                                
         GETEL R6,24,ELCODE                                                     
         EJECT                                                                  
*--------------------------------------------------------                       
* FIND  X'16' ELEM, IF ANY, FOR A GIVEN X'06' ELEM                              
*            MATCH X'15' ELEMENTS (WEEKLY OVERRIDES)                            
*               TO X'06' ELEMENTS (WEEKLY DOLLARS  )                            
*       RETURNS GROSS DOLLARS IN SVOVRG                                         
*--------------------------------------------------------                       
         SPACE 1                                                                
FINDOVRD NTR1                                                                   
         USING PWDOLEL,R6                                                       
         MVC   SVOVRG,PWDOLCG     ASSUME OVERRIDE=ACTUAL (IE NO OVRD)           
         DROP  R6                                                               
*                                                                               
FINDOV2  LR    R7,R6               SAVE 06 ELEM                                 
         USING PWDOLEL,R7                                                       
*                                                                               
         MVI   ELCODE,X'16'                                                     
         BAS   RE,NEXTEL                                                        
         BNE   FINDOVX                                                          
*                                                                               
         USING PWCLCEL,R6                                                       
FINDOV4  CLC   PWCLCWK,PWDOLWK     MATCH WEEK                                   
         BE    FINDOV6                                                          
         BAS   RE,NEXTEL                                                        
         BNE   FINDOVX                                                          
         B     FINDOV4                                                          
*                                                                               
FINDOV6  MVC   SVOVRG,PWCLCAMT    SAVE OVERRIDE DOLLARS                         
*                                                                               
FINDOVX  DS    0H                                                               
         MVI   ELCODE,6            RESTORE                                      
         B     EXIT                                                             
         EJECT                                                                  
         DS    0D                                                               
         DC    CL8'*STORAGE'                                                    
USERRD   DS    A                                                                
TSARBUFF DS    A                                                                
*                                  SAVES                                        
SVCLTG   DS    F                   CLT GROSS                                    
SVCLTN   DS    F                   CLT NET                                      
*                                                                               
SVWIMG   DS    F                   WIM GROSS                                    
SVWIMN   DS    F                   WIM NET                                      
*                                                                               
SVOVRG   DS    F                                                                
SVOVRN   DS    F                                                                
*                                                                               
TSARBUFL DC    A(4*250*WIRECL)    4 MONTHS * 250 MKTS * BUFFER SIZE             
WTSAROFF DC    A(0)                                                             
TSAREA   DS    XL64                                                             
SAVSTAT  DS    X                                                                
ELCODE   DS    X                                                                
SVELCODE DS    X                                                                
         DS    0D                                                               
         DC    C'*DATETAB'                                                      
DATETAB  DS    15XL8               Y/M,STDT,ENDT,X'80' = 26 ELEM                
DATETABL EQU   *-DATETAB                                                        
*                                                                               
         DS    0D                                                               
         DC    CL8'**WIREC*'                                                    
       ++INCLUDE SPB1WIREC                                                      
*                                                                               
         LTORG                                                                  
         EJECT                                                                  
         PRINT OFF                                                              
       ++INCLUDE SPREPWORKD                                                     
         PRINT ON                                                               
       ++INCLUDE DDTSARD                                                        
         EJECT                                                                  
       ++INCLUDE SPB1WIBLK                                                      
         EJECT                                                                  
       ++INCLUDE SPPWBLOCK                                                      
         EJECT                                                                  
       ++INCLUDE SPGENWIPW                                                      
*                                                                               
         PRINT OFF                                                              
       ++INCLUDE SPGENCLT                                                       
       ++INCLUDE SPGENSTA                                                       
ESTHDRD  DSECT                                                                  
       ++INCLUDE SPGENEST                                                       
         PRINT ON                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'016SPREPB1WI 05/01/02'                                      
         END                                                                    
