*          DATA SET FAWLMQE    AT LEVEL 001 AS OF 04/26/10                      
*          DATA SET FAWLMJN    AT LEVEL 026 AS OF 12/17/09                      
*CATALP FAWLMQE                                                                 
         TITLE 'WLMQE - INTERFACE TO IBM WLM QUERY ENCLAVE'                     
********************************************************************            
* FACPACK - INTERFACE TO WORKLOAD MANAGER QUERY ENCLAVE STATE                   
* MODULE:   FAWLMQE                                                             
*                                                                               
* PROPERTIES:                                                                   
*    PROBLEM STATE                                                              
*    ADDRESSING MODE= 31                                                        
*    PRIMARY ASC MODE SAC 0                                                     
*    DISPATCHABLE UNIT = TCB                                                    
*                                                                               
* INVOCATION:                                                                   
*        SAM31                                                                  
*        SAC   0                                                                
*        MVC   ECLVTOKEN,WLMETOKEN                                              
*        GOTO1 VWLMQE,ELCVPARM,C'WLQE'                                          
*                                                                               
* PROCESSING:                                                                   
* - ENCLAVE TOKEN MUST BE PRESERVED                                             
* - ESTABLISH STANDEAD DDS LINKAGE                                              
* - UPON ENTRY REGISTER 1 CONTAINS THE ADDRESS OF THE ENCLAVE                   
*   REQUEST BLOCK.                                                              
* - ESTABLISH REGISTER 7 AS THE REQUEST BLOCK BASE REGISTER                     
* - SWITCH TO SUPERVISOR STATE                                                  
* - ISSUE IWMESQRY TO QUERY THE ENCLAVE STATE                                   
* - SWITCH BACK TO PROBLEM STATE                                                
* - RETURN TO CALLER                                                            
* -                                                                             
*                                                                               
*IWMESQRY                                                                       
*The purpose of this service is to query whether or not the                     
*current dispatchable workunit is associated with an Enclave.                   
*The output is either the Enclave token or the STOKEN of the                    
*address space associated with the caller's workunit.                           
*                                                                               
*RETURN CODE OF 0 -> RETURN TOKEN IS AN ENCLAVE TOKEN                           
*RETURN CODE OF 4 -> RETURN TOKEN IS AN Address Space STOKEN                    
*                                                                               
*                                                                               
* xxxx041C    Name: IwmRsnCodeNotEnclave                                        
*                                                                               
*             Meaning:  The current dispatchable                                
*             workunit is not associated with an                                
*             Enclave.  The returned token is the                               
*             STOKEN of the associated address space.                           
*                                                                               
*                                                                               
********************************************************************            
         USING WLMED,R7                                                         
FAWLMQE  CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 0,**WLMQE                                                        
         LR    R7,R1              PARAMETER LIST ADDRESS                        
         XR    R0,R0               INITIALIZATION BYTE                          
         ST    R0,ECLVRETC         CLEAR RETURN CODE                            
         ST    R0,ECLVRSNC         CLEAR REASON CODE                            
         XC    ECLQTOKEN,ECLQTOKEN CLEAR QUERY TOKEN                            
*                                                                               
         MODESET MODE=SUP          Switch to supervisor state                   
*                                                                               
         IWMESQRY TOKEN=ECLQTOKEN,                                     +        
               RETCODE=ECLVRETC,                                       +        
               RSNCODE=ECLVRSNC,                                       +        
               MF=(E,EQRYLIST,COMPLETE)                                         
                                                                                
         MODESET MODE=PROB         Switch back to problem state                 
*                                                                               
         ICM   RF,15,ECLVRETC      Check return code           WLM              
         JZ    EXIT                All is good                                  
         CHI   RF,4                IS TOKEN AN ENCLAVE ?       WLM              
         JNE   ERROR10             NO                          WLM              
         AP    WARNS,=P'1'         Count warnings                               
         J     EXIT                                                             
*                                                                               
ERROR10  STM   R0,RF,DUMPREGS                                                   
         MVC   ABEND#,=C'919'                                                   
         AP    ERRORS,=P'1'                                                     
                                                                                
         LA    R0,8                                                             
         LA    R1,MSGRTC           Format return code                           
ERROR12  XR    RE,RE                                                            
         SAM24 ,                   32 bit shift between regs                    
         SLDL  RE,4                                                             
         SAM31 ,                                                                
         LA    RE,HEX2CHR(RE)                                                   
         MVC   0(1,R1),0(RE)                                                    
         LA    R1,1(,R1)                                                        
         BRCT  R0,ERROR12                                                       
                                                                                
         L     RF,ECLVRSNC         Reason code                                  
         LA    R0,8                                                             
         LA    R1,MSGRSNC          Format reason code                           
ERROR14  XR    RE,RE                                                            
         SAM24 ,                   32 bit shift between regs                    
         SLDL  RE,4                                                             
         SAM31 ,                                                                
         LA    RE,HEX2CHR(RE)                                                   
         MVC   0(1,R1),0(RE)                                                    
         LA    R1,1(,R1)                                                        
         BRCT  R0,ERROR14                                                       
                                                                                
         LA   R3,MSG1                                                           
         WTO  TEXT=(R3)                                                         
                                                                                
EXIT     XMOD1 1                                                                
                                                                                
         LTORG ,                                                                
***********************************************************************         
* Constants                                                                     
***********************************************************************         
         DS    0D                                                               
         DC    CL4'WR#='                                                        
WARNS    DC    PL8'0'                                                           
         DC    CL4'ER#='                                                        
ERRORS   DC    PL8'0'                                                           
         DC    CL6'ABEND='                                                      
ABEND#   DC    CL3' '                                                           
         DC    CL3' '                                                           
         DC    C'DUMPREGS'                                                      
DUMPREGS DC    18A(0)                                                           
                                                                                
HEX2CHR  DC    C'0123456789ABCDEF'                                              
                                                                                
MSG1     DC    AL2(MSG1LNQ)                                                     
MSG1TXT  DC    C'**U919** FAWLMQE QUERYING ENCLAVE STATE'                       
         DC    C' RTC='                                                         
MSGRTC   DC    C'00000000'                                                      
         DC    C' RSN='                                                         
MSGRSNC  DC    C'00000000'                                                      
MSG1LNQ  EQU   *-MSG1TXT                                                        
*                                                                               
         DS    0D                                                               
         IWMESQRY PLISTVER=IMPLIED_VERSION,MF=(L,EQRYLIST)                      
*                                                                               
         EJECT                                                                  
***********************************************************************         
* DSECTS                                                                        
***********************************************************************         
* FASSB                                                                         
*        PRINT OFF                                                              
*      ++INCLUDE FASSB                                                          
*        PRINT ON                                                               
* FATCB                                                                         
******** PRINT OFF                                                              
*      ++INCLUDE FATCB                                                          
******** PRINT ON                                                               
* FAWLMED                                                                       
       ++INCLUDE FAWLMED                                                        
* CVT                                                                           
         CVT DSECT=YES                                                          
* IWMYCON                                                                       
         IWMYCON                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'001FAWLMQE   04/26/10'                                      
         END                                                                    
