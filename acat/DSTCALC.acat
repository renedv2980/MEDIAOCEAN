*          DATA SET DSTCALC    AT LEVEL 010 AS OF 01/24/94                      
*CATALP DSTCALC                                                                 
*                                                                               
* PARAMETER LIST - PASS ADDRESS OF STRUCTURE WHICH CONTAINS:                    
*                                                                               
* DSTIDATE   6 BYTE INPUT DATE IN YYMMDD FORMAT                                 
* DSTITIME   2 BYTE INPUT TIME IN COMPRESSED FORMAT                             
* DSTODATE   6 BYTE OUTPUT DATE AREA (YYMMDD FORMAT)                            
* DSTOTIME   2 BYTE OUPUT TIME AREA (COMPRESSED FORMAT)                         
* DSTADDA    ADDAY'S ADDRESS                                                    
* DSTTIMEV   1 BYTE TIME VALUE DISPLACEMENT FROM                                
*            ESTTIME. (GET FROM MKTZONE).                                       
* DSTFLG     1 BYTE DAYLIGHT SAVINGS TIME FLAG. IF 'Y'                          
*            THEN STATION DOES NOT ADHERE TO DST. GET FROM LOOKING              
*            AT SFLAG1 IN STATION RECORD.                                       
* DSTERR     24 BYTE OUTPUT ERROR MSG AREA.                                     
*                                                                               
* THIS ROUTINE COMPUTES THE TRUE EASTERN STANDARD TIME, AND                     
* ADJUSTS THE DATE IF NECESSARY. INPUT PARAMETERS ARE THE TIME,                 
* DATE, TIME ZONE VALUE, AND DAYLIGHT SAVINGS TIME FLAG.                        
* THE OUTPUT PARAMETERS ARE THE ADJUSTED TIME AND DATE.                         
*                                                                               
* IF MARKET IS IN A DAYLIGHT SAVINGS TIME LOCATION, THEN NO                     
* ADJUSTMENT IS NECESSARY. IF STATION LOCATION DOES NOT FOLLOW                  
* THE DAYLIGHT SAVINGS TIME STANDARD, THEN WE SEE IF THE DATE IS IN             
* THE DST RANGE, AND ADJUST ONE HOUR IF IT IS.                                  
* DSTFLG OF YES MEANS THE STATION DOES NOT ADHERE TO DST.                       
* THE TIME ZONE VALUE GETS ADDED TO THE INPUT TIME.                             
* EXAMPLE: INPUT IS 1:00P LOS ANG = OUTPUT OF 4:00 EST                          
*                                                                               
* IF THE BROADCAST TIME IS BETWEEN 1200AM AND 559AM THEN WE ADD                 
* 1 TO THE DATE.                                                                
*                                                                               
         TITLE 'DSTCALC - COMPUTES TRUE EST'                                    
         PRINT NOGEN                                                            
DSTCALC  CSECT                                                                  
         NMOD1 WORKX-WORKD,**DSTC**                                             
         USING WORKD,RC                                                         
         L     RA,0(R1)                                                         
         USING DSTD,RA                                                          
         SR    R3,R3                                                            
         SR    R4,R4                                                            
         MVC   OUTDATE,INPDATE                                                  
         LH    R3,INPTIME                                                       
* IF BROADCAST TIME IS BETWEEN 12AM AND 559AM, ADD 1 TO DATE                    
*                                                                               
         CH    R3,=H'2400'    IS IT 1200 AM?                                    
         BE    DSTDAY         IF YES, ADJUST DATE                               
         CH    R3,=H'0559'    IS IT BEFORE 6 AM?                                
         BH    DST05                                                            
DSTDAY   GOTO1 ADDAY,DMCB,INPDATE,OUTDATE,F'+1'                                 
*                                                                               
DST05    CLI   DSTFLG,C'Y'  IF TRUE, THEN FIGURE OUT DST ADJUSTMENT.            
         BNE   DST30                                                            
         CLC   INPDATE,START94     IS DATE AFTER DST START DATE                 
         BL    DST10                                                            
         CLC   INPDATE,END94       IS DATE BEFOR DST END DATE                   
         BH    DST95YR                                                          
         B     DST20               ADJUST TIME 1 HR                             
DST95YR  CLC   INPDATE,START95     IS DATE AFTER DST START DATE                 
         BL    DST30               NO DST ADJUSTMENT                            
         CLC   INPDATE,END95       IS DATE BEFOR DST END DATE                   
         BH    DSTERR              OUT OF RANGE ERROR                           
         B     DST20               ADJUST TIME 1 HR                             
DST10    CLC   INPDATE,END93                                                    
         BH    DST30               NOT DST                                      
DST20    AH    R3,=H'100'          ADD 1 HR TO TIME                             
DST30    PACK  DUB,TIMEV            # OF HOURS FROM EST                         
         CVB   R4,DUB                                                           
         MH    R4,=H'100'                                                       
         AR    R3,R4             ADD #OF HOURS TO EST                           
         CH    R3,=H'2400'                                                      
         BH    DRADDAY             ADD 1 TO THE DAY                             
         BE    DRADDAY2            IF 12M, DON'T SUBTRACT 24 HRS                
         STH   R3,OUTTIME                                                       
         B     DREXIT                                                           
DRADDAY  SH    R3,=H'2400'         SUBTRACT 24 HRS FROM TIME                    
DRADDAY2 STH   R3,OUTTIME                                                       
         CLC   INPDATE,SPACES                                                   
         BE    DREXIT                                                           
         GOTO1 ADDAY,DMCB,INPDATE,OUTDATE,F'+1'                                 
         B     DREXIT                                                           
DSTERR   MVC   ERRORS(23),=C'NO CALENDAR THIS PERIOD'                           
DREXIT   XMOD1 1                                                                
         SPACE 2                                                                
START94  DC    CL6'940403'                                                      
END94    DC    CL6'941030'                                                      
END93    DC    CL6'931030'                                                      
START95  DC    CL6'950403'                                                      
END95    DC    CL6'951030'                                                      
         LTORG                                                                  
*                                                                               
WORKD    DSECT                                                                  
DMCB     DS    6F                                                               
DUB      DS    D                                                                
SPACES   DC    CL6'      '                                                      
WORKX    EQU   *                                                                
*                                                                               
DSTD     DSECT                                                                  
INPDATE  DS    CL6                                                              
INPTIME  DS    XL2                                                              
OUTDATE  DS    CL6                                                              
OUTTIME  DS    XL2                                                              
ADDAY    DS    V                                                                
TIMEV    DS    XL1                                                              
DSTFLG   DS    CL1                                                              
ERRORS   DS    CL24                                                             
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'010DSTCALC   01/24/94'                                      
         END                                                                    
