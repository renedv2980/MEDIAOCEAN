*          DATA SET WKBUFFGET  AT LEVEL 002 AS OF 06/19/03                      
         TITLE 'WKBUFFGET - INTERFACE TO WKBUFFER VIA TABS DATASPACE'           
*CATALP WKGET                                                                   
WKGET    CSECT                                                                  
         ENTRY WKFLAG                                                           
         ENTRY WKREAD                                                           
         ENTRY WKWRITE                                                          
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO READ AN INDEX BUFFER FROM WKBUFFER                       *         
* R1 POINTS TO DMGR PARAM LIST WITH DMCB3(1) = EXTFILE                *         
***********************************************************************         
         SPACE 1                                                                
WKREAD   NMOD1 WORKL,*WKREAD*,CLEAR=YES                                         
         USING WORKD,RC                                                         
         USING DMSPACED,DSPHD                                                   
         ST    RD,SAVERD                                                        
         LR    R9,R1                                                            
         USING DMCBW,R9                                                         
         MVC   EXTFILE,DMCBW3                                                   
         MVI   DMCBW3,X'FF'        SET FLAG TO USE DISK                         
*                                                                               
         L     RA,ARDCOMM          CONSTANTS + USEFUL ROUTINES                  
         USING COMMON,RA                                                        
         CLI   WKFLAG,C'N'         PREVIOUS FAILURE TO INITIALISE?              
         BE    XMOD                YES - USE DISK                               
*                                                                               
         BRAS  RE,INIT             INITIALISE - WON'T RETURN IF ERROR           
*                                                                               
         BAS   RE,ARSOFF           GO INTO TABS DATASPACE FOR LIST              
         LAM   R2,R2,TBALET                                                     
         ICM   R2,15,DSPTFRST                                                   
         SAC   512                                                              
         USING WKBUFFD,R2                                                       
*                                                                               
         LA    R3,WKBUFFNV         ONLINE I/O COUNTER                           
         CLI   ONLINE,C'Y'                                                      
         BE    *+8                                                              
         LA    R3,WKBUFFFV         OFFLINE I/O COUNTER                          
         CPYA  R3,R2                                                            
*                                                                               
WKRD02   ZAP   DUB,0(8,R3)         UPDATE TOTAL I/O COUNT                       
         LM    R0,R1,DUB                                                        
         AP    DUB,PONE                                                         
         LM    RE,RF,DUB                                                        
         CDS   R0,RE,0(R3)                                                      
         BNE   WKRD02                                                           
         LAM   R3,R3,ARZERO        CLEAR ACCESS REGISTER ON R3                  
*                                                                               
         AHI   R2,WKBUFFPL         GO ON TO LIST OF WORKER FILES                
         USING WFINDEXD,R2                                                      
*                                                                               
WKRD04   CLC   WFNENUM,EXTFILE     MATCH THIS WORKER FILE                       
         BE    WKRD06              YES                                          
         ICM   R2,15,WFNXID        NEXT IN LIST                                 
         CLM   R2,15,EFFS                                                       
         BNE   WKRD04                                                           
         B     XMOD                NO FILE FOUND - JUST EXIT FOR DISK           
*                                                                               
WKRD06   LA    R2,WFBLKFST         LIST OF INDEX BLOCKS FOR FILE                
         USING WFKTABD,R2                                                       
         ICM   RF,15,DMCBW3        MAKE SURE A(D/A) SET CORRECTLY               
         BZ    WKRDBDA                                                          
         OC    0(4,RF),0(RF)       MAKE SURE D/A SET CORRECTLY                  
         BZ    WKRDBDA                                                          
*                                                                               
WKRD08   CLC   WFKDA,EFFS          END OF LIST OF D/A'S FOR INDEX               
         BE    WKRDBDA                                                          
         CLC   WFKDA,0(RF)                                                      
         BE    *+12                                                             
         AHI   R2,WFKTABLQ                                                      
         B     WKRD08                                                           
*                                                                               
         LA    R3,WFKONRD          ONLINE I/O COUNTER                           
         CLI   ONLINE,C'Y'                                                      
         BE    *+8                                                              
         LA    R3,WFKOFRD          OFFLINE I/O COUNTER                          
         CPYA  R3,R2                                                            
*                                                                               
WKRD10   ICM   RE,15,0(R3)                                                      
         LA    RF,1(RE)                                                         
         CS    RE,RF,0(R3)                                                      
         BNE   WKRD10                                                           
         LAM   R3,R3,ARZERO        CLEAR ACCESS REGISTER ON R3                  
*                                                                               
         BAS   RE,ON31                                                          
         L     R4,WFKADD                                                        
         LAM   R4,R4,WKALET                                                     
         LH    R5,WFKLEN                                                        
         L     RE,DMCBW4                                                        
         LH    RF,WFKLEN                                                        
         MVCL  RE,R4               COPY BLOCK INTO USER BUFFER                  
*                                                                               
         BAS   RE,ARSOFF                                                        
         BAS   RE,OFF31                                                         
         MVI   DMCBW3,0                                                         
         B     EXITOK                                                           
*                                                                               
WKRDBDA  DC    H'0'                BAD DISK ADDRESS                             
*                                                                               
ARDCOMM  DC    A(COMMON)                                                        
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO COPY A SPECIFIED INDEX RECORD BACK TO CORE               *         
***********************************************************************         
         SPACE 1                                                                
WKWRITE  NMOD1 WORKL,*WKWRITE,CLEAR=YES                                         
         USING WORKD,RC                                                         
         USING DMSPACED,DSPHD                                                   
         ST    RD,SAVERD                                                        
         LR    R9,R1                                                            
         USING DMCBW,R9                                                         
         MVC   EXTFILE,DMCBW3                                                   
         MVI   DMCBW3,X'FF'        SET FLAG TO USE DISK                         
*                                                                               
         L     RA,AWTCOMM          CONSTANTS + USEFUL ROUTINES                  
         USING COMMON,RA                                                        
         CLI   WKFLAG,C'N'         PREVIOUS FAILURE TO INITIALISE?              
         BE    XMOD                YES - USE DISK                               
*                                                                               
         BRAS  RE,INIT             INITIALISE - WON'T RETURN IF ERROR           
*                                                                               
         BAS   RE,ARSOFF           GO INTO TABS DATASPACE FOR LIST              
         LAM   R2,R2,TBALET                                                     
         ICM   R2,15,DSPTFRST                                                   
         SAC   512                                                              
         USING WKBUFFD,R2                                                       
*                                                                               
         LA    R3,WKBUFFNV         ONLINE I/O COUNTER                           
         CLI   ONLINE,C'Y'                                                      
         BE    *+8                                                              
         LA    R3,WKBUFFFV         OFFLINE I/O COUNTER                          
         CPYA  R3,R2                                                            
*                                                                               
WKWT02   ZAP   DUB,0(8,R3)           UPDATE TOTAL I/O COUNT                     
         LM    R0,R1,DUB                                                        
         AP    DUB,PONE                                                         
         LM    RE,RF,DUB                                                        
         CDS   R0,RE,0(R3)                                                      
         BNE   WKWT02                                                           
         LAM   R3,R3,ARZERO        CLEAR ACCESS REGISTER ON R3                  
*                                                                               
         AHI   R2,WKBUFFPL         GO ON TO LIST OF WORKER FILES                
         USING WFINDEXD,R2                                                      
*                                                                               
WKWT04   CLC   WFNENUM,EXTFILE     MATCH THIS WORKER FILE                       
         BE    WKWT06              YES                                          
         ICM   R2,15,WFNXID        NEXT IN LIST                                 
         CLM   R2,15,EFFS                                                       
         BNE   WKWT04                                                           
         B     XMOD                NO FILE FOUND - JUST EXIT FOR DISK           
*                                                                               
WKWT06   LA    R2,WFBLKFST         LIST OF INDEX BLOCKS FOR FILE                
         USING WFKTABD,R2                                                       
*                                                                               
         ICM   RF,15,DMCBW3        MAKE SURE A(D/A) SET CORRECTLY               
         BZ    WKWTBDA                                                          
         OC    0(4,RF),0(RF)       MAKE SURE D/A SET CORRECTLY                  
         BZ    WKWTBDA                                                          
*                                                                               
WKWT08   CLC   WFKDA,EFFS          END OF LIST OF D/A'S FOR INDEX               
         BE    WKWTBDA                                                          
         CLC   WFKDA,0(RF)                                                      
         BE    *+12                                                             
         AHI   R2,WFKTABLQ                                                      
         B     WKWT08                                                           
*                                                                               
         LA    R3,WFKONWT          ONLINE I/O COUNTER                           
         CLI   ONLINE,C'Y'                                                      
         BE    *+8                                                              
         LA    R3,WFKOFWT          OFFLINE I/O COUNTER                          
         CPYA  R3,R2                                                            
*                                                                               
WKWT10   ICM   RE,15,0(R3)                                                      
         LA    RF,1(RE)                                                         
         CS    RE,RF,0(R3)                                                      
         BNE   WKWT10                                                           
         LAM   R3,R3,ARZERO        CLEAR ACCESS REGISTER ON R3                  
*                                                                               
         BAS   RE,ON31                                                          
         L     R4,WFKADD                                                        
         LAM   R4,R4,WKALET                                                     
         LH    R5,WFKLEN                                                        
         L     RE,DMCBW4                                                        
         LH    RF,WFKLEN                                                        
         MVCL  R4,RE               COPY BLOCK FROM USER BUFFER                  
*                                                                               
         BAS   RE,ARSOFF                                                        
         BAS   RE,OFF31                                                         
         MVI   DMCBW3,0                                                         
         B     EXITOK                                                           
*                                                                               
WKWTBDA  DC    H'0'                BAD DISK ADDRESS                             
*                                                                               
AWTCOMM  DC    A(COMMON)                                                        
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO PERFORM INITIALISATION AND GET ENTRY IN DATASPACE QUEUE  *         
***********************************************************************         
         SPACE 1                                                                
INIT     NTR1  BASE=*,LABEL=*                                                   
         BAS   RE,ARSOFF                                                        
         ICM   R2,15,VSSB          BUILD LOCK TOKEN FOR QUEUE ENTRY             
         BNZ   *+6                                                              
         DC    H'0'                MUST HAVE SSB                                
         USING SSOOFF,R2                                                        
*                                                                               
         OC    SSOCNTL,SSOCNTL     OFFLINE?                                     
         BNZ   INIT02              NO                                           
         CLI   SSOXTND,X'FF'       EXTENDED SSB                                 
         BE    *+6                 YES                                          
         DC    H'0'                MUST HAVE EXTENDED SSB FOR ALET              
*                                                                               
         MVI   ONLINE,C'N'                                                      
         MVC   TBALET,SSOTBLET     TABS ALET OFFLINE                            
         B     INIT04                                                           
*                                                                               
         USING SSBD,R2                                                          
INIT02   MVI   ONLINE,C'Y'                                                      
         MVC   TBALET,SSBTBLET     TABS ALET                                    
         DROP  R2                                                               
*                                                                               
INIT04   ICM   RF,15,VPROTOFF      SP OFF IF NECESSARY                          
         BZ    *+6                                                              
         BASR  RE,RF                                                            
*                                                                               
         XC    DUB,DUB             GET DATASPACE HEADER FROM TABS               
         MVC   DUB(4),=AL4(DTWRKR)                                              
         MVI   DUB,X'20'           SET ENQUIRE                                  
         GOTO1 VLOCKSPC,DUB                                                     
*                                                                               
         L     RF,4(R1)            SAVE DSPACE HEADER                           
         MVC   DSPHD,0(RF)                                                      
         NC    DSPTFRST,=XL4'3FFFFFFF'                                          
*                                                                               
         CLI   WKFLAG,C'Y'         ALREADY BOUND TO WKBUFFER?                   
         BE    *+8                 YES                                          
         BRAS  RE,BIND                                                          
*                                                                               
         BAS   RE,ARSOFF           MAKE SURE IT IS STILL THERE                  
         LAM   R2,R2,TBALET                                                     
         ICM   R2,15,DSPTFRST                                                   
         SAC   512                                                              
         USING WKBUFFD,R2                                                       
         CLI   WKBUFFID,0          ADDRESS SPACE PRESENT?                       
         BNE   *+8                 YES                                          
         MVI   WKFLAG,C'N'                                                      
         BAS   RE,ARSOFF                                                        
*                                                                               
INITX    ICM   RF,15,VPROTON       SET BACK STORAGE PROTECT                     
         BZ    *+6                                                              
         BASR  RE,RF                                                            
*                                                                               
         CLI   WKFLAG,C'Y'         SET CC                                       
         BE    EXITOK                                                           
         B     XMOD                                                             
         EJECT                                                                  
***********************************************************************         
* BIND TO WKBUFFER ADDRESS SPACE                                      *         
***********************************************************************         
         SPACE 1                                                                
BIND     NTR1  BASE=*,LABEL=*                                                   
         BAS   RE,ARSOFF                                                        
         LAM   R2,R2,TBALET                                                     
         ICM   R2,15,DSPTFRST                                                   
         SAC   512                                                              
         USING WKBUFFD,R2                                                       
         CLI   WKBUFFID,0          ADDRESS SPACE PRESENT?                       
         BNE   *+16                YES                                          
         MVI   WKFLAG,C'N'                                                      
         BAS   RE,ARSOFF                                                        
         B     EXITL                                                            
*                                                                               
         XC    CARD,CARD           SVC 247 NEEDS CHANGING                       
         MVC   CARD+00(04),=C'PALE'                                             
         MVC   CARD+04(12),=C'TESTDATAMGRT'                                     
         MVC   CARD+28(08),WKBUFFSV                                             
         BAS   RE,ARSOFF                                                        
*                                                                               
         LHI   R0,21                                                            
         LNR   R0,R0                                                            
         LA    R1,CARD                                                          
         SVC   247                                                              
         LTR   RF,RF                                                            
         BZ    *+12                                                             
         MVI   WKFLAG,C'N'         WKBUFFER ADDRESS SPACE NOT THERE             
         B     EXITL                                                            
*                                                                               
         MVC   WKALET,CARD+24                                                   
         MVI   WKFLAG,C'Y'         FLAG PQ DSPACE SET UP OK                     
         B     EXITOK                                                           
         EJECT                                                                  
***********************************************************************         
* EXITS AND OTHER USEFUL ROUTINES                                     *         
***********************************************************************         
         SPACE 1                                                                
COMMON   DS    0D                                                               
*                                                                               
EXITL    CLI   *,255               SET CC LOW                                   
         B     EXIT                                                             
*                                                                               
EXITH    CLI   *,0                 SET CC HIGH                                  
         B     EXIT                                                             
*                                                                               
EXITOK   CR    RB,RB               SET CC EQUAL                                 
         B     EXIT                                                             
*                                                                               
XMOD     L     RD,SAVERD                                                        
         B     EXIT                                                             
*                                                                               
EXIT     XIT1  ,                                                                
*                                                                               
ON31     O     RE,=XL4'80000000'                                                
         BSM   0,RE                                                             
*                                                                               
OFF31    N     RE,=XL4'7FFFFFFF'                                                
         BSM   0,RE                                                             
*                                                                               
ARSOFF   SAC   0                                                                
         LAM   R0,RF,ARZERO                                                     
         BR    RE                                                               
         EJECT                                                                  
***********************************************************************         
* CONSTANTS YOU MIGHT NEED TO SEARCH FOR IN A DUMP                    *         
***********************************************************************         
         SPACE 1                                                                
         ORG   COMMON+(*-COMMON+15)/16*16                                       
         DC    CL8'WKALET=='                                                    
WKALET   DC    A(0)                                                             
         DC    A(0)                                                             
         DC    CL8'WKFLAG=='                                                    
WKFLAG   DC    C'?'                INITIALISATION FLAG                          
         DC    XL7'00'                                                          
         DC    CL8'ONLINE=='                                                    
ONLINE   DC    C'N'                ON/OFFLINE                                   
         DC    XL7'00'                                                          
*                                                                               
CARD     DC    XL48'00'                                                         
         EJECT                                                                  
***********************************************************************         
* CONSTANTS USED BY WK ROUTINES                                       *         
***********************************************************************         
         SPACE 1                                                                
ARZERO   DC    16F'0'                                                           
VPROTON  DC    V(PROTON)                                                        
VPROTOFF DC    V(PROTOFF)                                                       
VDMGR    DC    V(DATAMGR)                                                       
VLOCKSPC DC    V(LOCKSPC)                                                       
VSSB     DC    V(SSB)                                                           
*                                                                               
EFFS     DC    16X'FF'                                                          
PZERO    DC    P'0'                                                             
PONE     DC    P'1'                                                             
         EJECT                                                                  
***********************************************************************         
* LITERALS                                                            *         
***********************************************************************         
         SPACE 1                                                                
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
* WORKING STORAGE DSECT                                               *         
***********************************************************************         
         SPACE 1                                                                
WORKD    DSECT                                                                  
DUB      DS    D                                                                
SAVERD   DS    A                                                                
TBALET   DS    A                                                                
DSPHD    DS    XL64                                                             
*                                                                               
EXTFILE  DS    X                                                                
WORKL    EQU   *-WORKD                                                          
*                                                                               
DMCBW    DSECT                                                                  
DMCBW1   DS    A                                                                
DMCBW2   DS    A                                                                
DMCBW3   DS    A                                                                
DMCBW4   DS    A                                                                
DMCBW5   DS    A                                                                
DMCBW6   DS    A                                                                
DMCBW7   DS    A                                                                
DMCBW8   DS    A                                                                
         EJECT                                                                  
***********************************************************************         
* OTHER INCLUDED DSECTS                                               *         
***********************************************************************         
         SPACE 1                                                                
*FASSB                                                                          
         PRINT  OFF                                                             
       ++INCLUDE FASSB                                                          
         PRINT  ON                                                              
*FASSBOFF                                                                       
         PRINT  OFF                                                             
       ++INCLUDE FASSBOFF                                                       
         PRINT  ON                                                              
*DMDSYSHDR                                                                      
         PRINT  OFF                                                             
       ++INCLUDE DMDSYSHDR                                                      
         PRINT  ON                                                              
*DMDSHDR                                                                        
         PRINT  OFF                                                             
       ++INCLUDE DMDSHDR                                                        
         PRINT  ON                                                              
*DMSPACED                                                                       
         PRINT  OFF                                                             
       ++INCLUDE DMSPACED                                                       
         PRINT  ON                                                              
*DMWRKRW                                                                        
         PRINT  OFF                                                             
       ++INCLUDE DMPRTQW                                                        
         PRINT  ON                                                              
*WKBUFFERD                                                                      
         PRINT  OFF                                                             
       ++INCLUDE WKBUFFERD                                                      
         PRINT  ON                                                              
*FATABSDEQU                                                                     
         PRINT  OFF                                                             
       ++INCLUDE FATABSDEQU                                                     
         PRINT  ON                                                              
*FATABSD                                                                        
         PRINT  OFF                                                             
       ++INCLUDE FATABSD                                                        
         PRINT  ON                                                              
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'002WKBUFFGET 06/19/03'                                      
         END                                                                    
