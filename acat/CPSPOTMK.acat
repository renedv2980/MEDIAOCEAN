*          DATA SET CPSPOTMK   AT LEVEL 014 AS OF 05/13/04                      
*CATALP CPSPOTMK                                                                
         PRINT NOGEN                                                            
***************************************************************                 
*        MODULE WILL BUILD A LIST OF AGENCY MARKET NUMBERS  2 *                 
*                                    NSI    MARKET NUMBERS  2 *                 
*                                    MGROUP ASSIGNMENTS     3 *                 
*        ADDRESS OF LIST IS RETURNED IN INPUT PARAM           *                 
***************************************************************                 
         SPACE 2                                                                
CPSPOTMK CSECT                                                                  
         NMOD1 0,CPSPOTMK                                                       
         L     RA,0(R1)                                                         
         LA    RC,2048(RA)                                                      
         LA    RC,2048(RC)                                                      
         USING CPWORKD,RA,RC                                                    
         L     RE,ADMGROUP                                                      
         ST    RE,MPPTR                                                         
         L     RF,=F'4000'                                                      
         XCEF                                                                   
         CLI   QGRP,C'S'           SPOT GROUP REQUESTED                         
         BNE   EXIT                                                             
         LA    R1,4                FIND COMPARE LENGTH                          
         LA    RE,QMKT+3                                                        
         TM    0(RE),X'F0'                                                      
         BO    *+10                                                             
         BCTR  RE,0                                                             
         BCT   R1,*-10                                                          
         STC   R1,MGCOMPLN                                                      
         MVC   CPMAGY,QAGY                                                      
****************************SPECIAL FOR MINDSHARE                               
         CLC   QAGY,=C'JW'                                                      
         BNE   *+10                                                             
         CLC   QAGY,=C'OM'                                                      
         BNE   *+10                                                             
         MVC   CPMAGY,=C'H7'                                                    
*********************************************                                   
         SPACE 2                                                                
         B     SESET               SPOT FILE OPENED IN CONTROLLER               
         XC    DMCB+8(4),DMCB+8                                                 
         LA    RE,SETABLE          SET TO FILTER ON AGY                         
         USING CND,RE                                                           
         XC    CNLEN,CNLEN                                                      
         MVC   CNAGY,CPMAGY                                                     
         GOTO1 =V(CONFID),DMCB,(0,SETABLE),DMCB+8                               
         L     RF,UTL                                                           
         MVC   SVUTL,0(RF)                                                      
         LA    RE,SETABLE                                                       
         CLI   CNSSE,0             BYPASS GARBAGE FROM CTFILE                   
         BNE   *+12                                                             
         LA    RE,L'CNLEN(RE)                                                   
         B     *-12                                                             
         MVC   4(1,RF),CNSSE                                                    
         MVC   MGAGYNO,CNSCD                                                    
         OI    MGAGYNO,X'01'       SET MEDIA TO TV                              
         GOTO1 DATAMGR,DMCB,=C'OPEN',=C'SPOT',SPOTFILE                          
         MVI   OPENSW,1                                                         
SESET    DS    0C                                                               
         L     RF,UTL                                                           
         MVC   SVUTL,0(RF)                                                      
         MVC   4(1,RF),RCSSE                                                    
*                                                                               
MKR      MVI   STAFKEY,C'0'                                                     
         MVC   STAFKEY+1(16),STAFKEY                                            
         LA    RE,STAFKEY                                                       
         USING MKCREC,RE                                                        
         MVI   MKCKTYPE,C'M'                                                    
         MVC   MKCKMED,QMED                                                     
         MVC   MKCKAGY,CPMAGY                                                   
MKR1     GOTO1 DATAMGR,DMCB,=C'DMRDHI',=C'STATION',STAFKEY,STAFREC              
         MVC   STAFKEY,STAFREC                                                  
         LA    RE,STAFKEY                                                       
         CLI   MKCKTYPE,C'M'                                                    
         BNE   MKG1                                                             
         CLC   MKCKMED,QMED                                                     
         BNE   MKG1                                                             
         MVC   MKCKAGY,CPMAGY                                                   
         MVC   FULL,MKCKMKT                                                     
         GOTO1 DATAMGR,DMCB,=C'DMREAD',=C'STATION',STAFKEY,STAFREC              
         LA    RE,STAFREC                                                       
         CLC   MKCKAGY,CPMAGY                                                   
         BNE   MKR2                                                             
*        MVC   P(4),MKCKMKT        FOR TESTING                                  
*        MVC   P+10(24),MKCNAME    FOR TESTING                                  
         SR    R9,R9                                                            
         CLI   MKCRS1,C'0'                                                      
         BNE   *+8                                                              
         ICM   R9,3,MKCRSM1                                                     
         CLI   MKCRS2,C'0'                                                      
         BNE   *+8                                                              
         ICM   R9,3,MKCRSM2                                                     
*                                                                               
         LTR   R9,R9               ANY NSI NUMBER                               
         BZ    MKR2                NO - BYPASS                                  
         L     R8,MPPTR           POINT TO MARKET SLOT                          
         USING MPTABD,R8                                                        
         XC    MPTABLN,MPTABLN                                                  
         PACK  DUB,MKCKMKT                                                      
         CVB   RF,DUB                                                           
         STCM  RF,3,MPMKTNO        SAVE AGENCY NUMBER                           
         AH    R9,=H'400'          CORRECT FOR CPP MARKETS                      
         STCM  R9,3,MPNSINO        SAVE NSI NUMBER                              
         LA    R8,MPEND-MPSTART(R8) SET FOR NEXT                                
         XC    MPTABLN,MPTABLN                                                  
         ST    R8,MPPTR                                                         
*        EDIT  (R9),(4,P+5)        FOR TESTING                                  
*        GOTO1 REPORT              FOR TESTING                                  
*                                                                               
MKR2     MVC   STAFKEY,STAFREC                                                  
         LA    RE,STAFKEY                                                       
         MVC   MKCKMKT,FULL                                                     
         MVC   MKCKAGY,=X'FFFF'                                                 
         B     MKR1                                                             
MKG1     DS    0C                                                               
         XC    SPTFKEY,SPTFKEY                                                  
         LA    R2,SPTFKEY                                                       
         USING MKGPTYP,R2                                                       
         MVC   SPTFKEY(2),=X'0D82'                                              
         MVC   SPTFKEY+2(1),RCBAGYMD                                            
         MVC   MKGPMID,QGRP+1                                                   
         CLI   QGRP+1,C'F'                                                      
         BH    MKG1A                                                            
         GOTO1 =V(CLPACK),DMCB,QCLT,MKGKCLT                                     
MKG1A    GOTO1 DATAMGR,DMCB,=C'DMRDHI',=C'SPTDIR',SPTFKEY,SPTFKEY               
         B     MKG4                                                             
MKG2     GOTO1 DATAMGR,DMCB,=C'DMRSEQ',=C'SPTDIR',SPTFKEY,SPTFKEY               
MKG4     LA    R2,SPTFKEY                                                       
         CLC   MKGPMID,QGRP+1      SAME ID                                      
         BNE   MGEXIT              NO - EXIT                                    
         UNPK  DUB,MKGPMGRP(3)     CHECK THE GROUP                              
         ZIC   R1,MGCOMPLN                                                      
         LTR   R1,R1               ACCEPT ANY IF ALL REQUEST                    
         BZ    MKG5                                                             
         BCTR  R1,0                                                             
         EX    R1,*+8              COMPARE SPECIFIC                             
         B     *+10                                                             
         CLC   QMKT(0),DUB+3                                                    
         BNE   MKG2                                                             
         SPACE 2                                                                
* INSERT MARKET GROUP INTO TABLE                                                
MKG5     L     R8,ADMGROUP                                                      
MKG6     CLC   MPMKTNO,MKGPMKT     FIND AGENCY MARKET                           
         BE    MKG8                                                             
         OC    MPMKTNO,MPMKTNO     BYPASS IF NO NSI MARKET                      
         BZ    MKG2                                                             
         LA    R8,MPEND-MPSTART(R8)                                             
         B     MKG6                                                             
MKG8     MVC   MPMGRID,MKGPMID                                                  
         MVC   MPMGRNO,MKGPMGRP                                                 
* ***********************************FOR TESTING ***********                    
*        MVC   P(1),MKGPMID                                *                    
*        UNPK  DUB,MKGPMGRP(3)                             *                    
*        MVC   P+1(4),DUB+3                                *                    
*        EDIT  (B2,MKGPMKT),(4,P+6)                        *                    
*        EDIT  (B2,MPNSINO),(4,P+10)                       *                    
*        GOTO1 REPORT                                      *                    
************************************************************                    
         B     MKG2                                                             
         SPACE 2                                                                
MGEXIT   DS    0H                                                               
         L     RF,UTL                                                           
*        CLI   4(RF),X'02'         SPOT 1 STAYS OPEN                            
*        BE    MGEXIT2                                                          
*        GOTO1 DATAMGR,DMCB,=C'DMCLSE',=C'SPOT',SPOTFILE                        
         MVI   OPENSW,0                                                         
MGEXIT2  L     RF,UTL                                                           
         MVC   0(8,RF),SVUTL                                                    
EXIT     XMOD1 1                                                                
         SPACE 2                                                                
         LTORG                                                                  
         EJECT                                                                  
SPOTFILE DS    0C                                                               
         DC    CL8'NSPTFILE'                                                    
         DC    CL8'NSPTDIR'                                                     
         DC    CL8'NSTAFILE'                                                    
         DC    C'X'                                                             
         SPACE 2                                                                
CPMAGY   DS    CL2                                                              
OPENSW   DC    X'00'                                                            
MGAGYNO  DS    C                                                                
MGCOMPLN DS    C                                                                
SVPARM   DS    F                                                                
SVUTL    DS    D                                                                
MPPTR    DS    F                                                                
SPTFKEY  DS    CL20                                                             
STAFKEY  DS    CL17                                                             
STAFREC  DS    CL200                                                            
SETABLE  DS    1000C                                                            
SPMGROUP CSECT                                                                  
         DC    4000X'00'                                                        
         EJECT                                                                  
*PTABD   DSECT                                                                  
*PTABLN  DS    0CL(MPEND-MPSTART)                                               
*PSTART  DS    0C                                                               
*PMKTNO  DS    XL2                 AGENCY MARKET NUMBER                         
*PNSINO  DS    XL2                 NSI MARKET NUMBER                            
*PMGRID  DS    C                   MARKET GROUP ID                              
*PMGRNO  DS    CL2                 MARKET GROUP NUMBER                          
*PEND    DS    0C                                                               
         SPACE 2                                                                
       ++INCLUDE DDCNTRL                                                        
*              MARKET RECORD                                                    
         SPACE 2                                                                
MKCREC   DS    0CL144    V         MARKET RECORD                                
MKCKEY   DS    0CL15     V         KEY                                          
MKCKEYLN EQU   15                  NEW V/L RECORD KEYLENGTH                     
MKCKTYPE DS    CL1       A         RECORD TYPE (C'M')                           
MKCKTYPQ EQU   C'M'                                                             
MKCKMED  DS    CL1       A         MEDIA                                        
MKCKMKT  DS    CL4       N         MARKET CODE                                  
MKCKAGY  DS    CL2       N         AGENCY CODE                                  
MKCKFILL DS    CL7       N         7 C'0' FILL CHARACTERS                       
MKCKEYLQ EQU   *-MKCREC                                                         
         SPACE 1                                                                
MKCRECL  DS    CL2                 RECORD LENGTH                                
MKCCNTL  DS    CL1       B         CONTROL                                      
MKCNAME  DS    CL24      A         MARKET NAME                                  
MKCZONE  DS    CL1       A         TIME ZONE CODE                               
MKCCLASS DS    CL1       A         SWEEP CODE                                   
MKCRTG   DS    CL1       A         RATING SERVICE OVERRIDE N=NSI A=ARB          
MKCRANK  DS    CL3       N         MARKET RANK                                  
MKCHOMES DS    CL8       N         MARKET HOMES                                 
MKCREG   DS    CL3       N         REGION CODE                                  
MKCNTA   DS    CL2       N         NTA (1-29)                                   
MKCWT    DS    CL4       N         MARKET WEIGHTING FACTOR                      
MKCSHR   DS    CL4       N         MARKET COVERAGE PCT (FOR SYNDIC)             
MKCRS1   DS    CL1       N         RATING SRVC                                  
MKCRSM1  DS    CL2       B         RAT SRVC MARKET                              
MKCRS2   DS    CL1       N         RATING SRVC                                  
MKCRSM2  DS    CL2       B         RAT SRVC MARKET                              
MKCLTACC DS    CL3       A/N       LIMIT ACCESS CODES                           
MKCAMKTC DS    CL6       A/N       ALPHA MARKET CODE                            
MKCCLAS1 DS    CL1       N         SWEEP CLASS FOR RTG SVC 1                    
MKCCLAS2 DS    CL1       N         SWEEP CLASS FOR RTG SVC 2                    
MKCNODST DS    CL1       A         C'Y' = NO DAYLIGHT SAVINGS TIME              
MKCALST  DS    CL27                MAX 9 3 BYTE MKT ALPHA CODES                 
MKCUNIT  DS    XL2       N         UNITS                                        
         DS    CL28                SPARE                                        
MKCRECLQ EQU   *-MKCREC                                                         
       ++INCLUDE SPGENMKG                                                       
       ++INCLUDE CPREPWORKD                                                     
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'014CPSPOTMK  05/13/04'                                      
         END                                                                    
