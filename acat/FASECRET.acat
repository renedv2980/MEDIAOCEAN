*          DATA SET FASECRET   AT LEVEL 010 AS OF 02/26/19                      
*CATALP FASECRET                                                                
FASECRET TITLE '- RETURN SECURITY ACCESS INFO'                                  
SECRET   CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 WORKL,**SCRT**,R8,CLEAR=YES                                      
         USING WORKD,RC            RC=A(W/S)                                    
         LR    RA,R1                                                            
         USING SECPARMD,RA         RA=A(PARAMETER LIST)                         
         XR    R9,R9                                                            
         ICM   R9,7,SECPASEC                                                    
         USING SECD,R9             R9=A(SECURITY INFO AREA)                     
                                                                                
         MVC   ACTN,SECPACTN       SET ACTION                                   
         NI    ACTN,FF-(SECPOSP+SECPOVAL+SECPVALO+SECPOLP)                      
         MVC   INDICS,SECPACTN     SET INDICATORS                               
         NI    INDICS,SECPOSP+SECPOVAL+SECPVALO+SECPOLP                         
                                                                                
         CLI   ACTN,SECPINIT       TEST INITIALIZATION                          
         BE    INIT                                                             
                                                                                
         TM    SECINDS,SECIINIT    TEST INITIALIZED                             
         BO    *+6                                                              
         DC    H'0'                                                             
                                                                                
         CLI   ACTN,SECPUNDF                                                    
         BL    *+6                                                              
         DC    H'0'                ACTION IS UNDEFINED                          
                                                                                
         XR    RF,RF                                                            
         IC    RF,ACTN                                                          
         SLL   RF,2                                                             
         B     *(RF)                                                            
         B     GETOFFN             01 - SECPOFFN                                
         B     GETDPTN             02 - SECPDPTN                                
         B     TSTRCD              03 - SECPRCD                                 
         B     TSTACT              04 - SECPACT                                 
         B     TSTRACT             05 - SECPRACT                                
         B     TSTFLDS             06 - SECPFLDS                                
         B     TSTFLDP             07 - SECPFLDP                                
         B     TSTOPTS             08 - SECPOPTS                                
         B     TSTOPTP             09 - SECPOPTP                                
         B     GETRAL              10 - SECPGRAL                                
         B     GETFCL              11 - SECPGFCL                                
         B     GETOCL              12 - SECPGOCL                                
                                                                                
SECRETR  MVI   SECPOUT,SECPREAD    FIELD IS VALID FOR READ ONLY                 
         B     SECRETX                                                          
                                                                                
SECRETN  MVI   SECPOUT,SECPNO      INVALID                                      
         B     SECRETX                                                          
                                                                                
SECRETY  MVI   SECPOUT,SECPYES     VALID                                        
                                                                                
SECRETX  CLI   SECPOUT,SECPYES     SET CC FOR CALLER (HIGH/LOW/EQUAL)           
                                                                                
XIT      XIT1  ,                                                                
         EJECT                                                                  
***********************************************************************         
* INITIALIZE BLOCK AREA                                               *         
*  - CLEAR BLOCK AREA AND SET LENGTH                                  *         
*  - EXTRACT DATA REQUIRED FOR INITIALIZATION                         *         
***********************************************************************         
                                                                                
INIT     TM    SECINDS,SECIINIT    TEST ALREADY INITIALIZED                     
         BO    SECRETY                                                          
         ICM   R2,15,SECPLEN       TEST USER-DEFINED LENGTH                     
         BNZ   *+8                                                              
         LHI   R2,SECLENQ          IF NOT, SET TO DEFAULT                       
                                                                                
         TM    INDICS,SECPOVAL                                                  
         BZ    *+16                                                             
         MVC   SAVEOVAL,SECOVALS   SAVE SECURITY VALUES FROM BLOCK              
         MVC   SAVEAGPE,SECOAGPE   SAVE SECURITY AGENCY                         
                                                                                
         LR    RF,R2               ZEROISE BLOCK                                
         LA    RE,SECD                                                          
         XR    R1,R1                                                            
         MVCL  RE,R0                                                            
         STCM  R2,3,SECLEN         SET LENGTH                                   
                                                                                
         TM    INDICS,SECPOVAL                                                  
         BZ    *+16                                                             
         MVC   SECOVALS,SAVEOVAL   RESTORE SECURITY VALUES TO BLOCK             
         MVC   SECOAGPE,SAVEAGPE   RESTORE SECURITY AGENCY                      
                                                                                
         ICM   RF,15,VSSB          TEST ON/OFF LINE                             
         BZ    INITOFF                                                          
         USING SSBD,RF             RF=A(SYSTEM STATUS BLOCK)                    
         ICM   RE,3,SSBCNTL        TEST FOR TERMINAL NO.                        
         BNZ   INITON                                                           
         DROP  RF                                                               
                                                                                
INITOFF  OI    SECINDS,SECIOFLN    SET OFF-LINE                                 
         OC    SECOVALS,SECOVALS   TEST SECURITY VALUES SET                     
         BNZ   INIT02              YES - TREAT AS THOUGH ONLINE                 
                                                                                
         ICM   RF,15,VSSB          TEST IF OFFLINE EXTENDED SSB                 
         BZ    INITY                                                            
         CLI   2(RF),X'FF'         EXTENDED SSB                                 
         BNE   INITY                                                            
         ICM   R1,15,SSOMASTC-SSOOFF(RF)                                        
         BZ    INITY                                                            
                                                                                
         USING DDMASTD,R1                                                       
         TM    MCRHINFO,MCRHOLD    OLD STYLE MCRFHDR FORMAT                     
         BO    INITY                                                            
         MVC   SECOAGY,MCUSER      AGENCY ALPHA ID                              
         MVC   SECOAGYS,MCRHSAGY   AGENCY SECURITY ALPHA ID                     
         MVC   SECOUSER,MCORIGID   COMPANY USER ID                              
         MVC   SECOPASS,MCRHPSWD   PERSON PASSWORD NUMBER                       
         MVC   SECOSAGN,MCRHSAGN   PERSON SECURITY ACCESS GROUP                 
         MVC   SECOSYS,MCOVSYS                                                  
         MVC   SECOPRG,MCRHPRG     PROGRAM NUMBER                               
         MVC   SECOAGPE,MCRHSAGP   SECURITY AGENCY FOR THE PERSON               
         MVI   SECOFLAG,0                                                       
         MVI   SECOSTAT,0                                                       
         TM    MCRHINF1,MCRHISEC   TEST PASSWORD PROTECTED USER-ID              
         BZ    *+8                                                              
         OI    SECOFLAG,TFLAGSEC                                                
         TM    MCRHINFO,MCRHDDS    REQUEST ADDED BY DDS TERMINAL                
         BZ    *+8                                                              
         OI    SECOSTAT,TSTATDDS                                                
         B     INIT02                                                           
         DROP  R1                                                               
                                                                                
INITON   OI    SECINDS,SECIONLN    SET ON-LINE                                  
         OC    SECOVALS,SECOVALS   TEST SECURITY VALUES SET                     
         BNZ   INIT02              YES                                          
*                                  CALL GETFACT TO ESTABLISH VALUES             
         GOTOR VGETFACT,PARM,(X'80',0),F#UTLD                                   
         L     R1,0(R1)                                                         
         USING F@UTLD,R1                                                        
         MVC   SECOAGY,F@TAGY                                                   
         MVC   SECOAGYS,F@TAGYSC                                                
         MVC   SECOUSER,F@TUSER                                                 
         MVC   SECOPASS,F@TPASS                                                 
         MVC   SECOSAGN,F@TSAGN                                                 
         MVC   SECOSYS,F@TOVSYS                                                 
         MVC   SECOPRG,F@TPRG                                                   
         MVC   SECOFLAG,F@TFLAG                                                 
         MVC   SECOSTAT,F@TSTAT1                                                
         MVC   SECOAGPE,F@TAGYPE                                                
         DROP  R1                                                               
*                                                                               
INIT02   OI    SECINDS,SECIOVAL    SET SECOVALS PRESENT IN BLOCK                
         TM    INDICS,SECPVALO     TEST ONLY WANT SECOVALS SET                  
         BNZ   INITY                                                            
         MVC   SECAGY,SECOAGYS     SET AGENCY USED FOR SECURITY                 
         OC    SECAGY,SECAGY                                                    
         BNZ   *+10                                                             
         MVC   SECAGY,SECOAGY                                                   
*                                                                               
         GOTOR INITPER             SET PERSON DETAILS                           
         TM    SECINDS,SECIDDS     DDS PASSWORD USED?                           
         BO    INITY               YES: FINISHED                                
*                                                                               
         TM    INDICS,SECPOSP      OVERRIDE PROGRAM?                            
         BO    *+12                YES: NEED ACCESS AWARE FOR OVERRIDE          
         TM    SECINDS,SECIOLD     OLD SECURITY?                                
         BO    INITY               YES: FINISHED                                
                                                                                
         TM    INDICS,SECPOSP+SECPOVAL+SECPVALO+SECPOLP                         
         BZ    *+10                                                             
         MVC   SECINDS1,SECPIND1   SAVE RECORD/ACTION INIT OPTIONS              
                                                                                
         GOTOR INITRACT            SET RECORD/ACTION SECURITY                   
         GOTOR INITFOP             SET FIELD/OPTION SECURITY                    
                                                                                
INITY    OI    SECINDS,SECIINIT    SET BLOCK INITIALIZED                        
         B     SECRETY                                                          
         EJECT                                                                  
                                                                                
***********************************************************************         
* SET PERSONAL DETAILS                                                          
***********************************************************************         
INITPER  NTR1  ,                                                                
*                                                                               
         TM    SECOFLAG,TFLAGSEC   TEST PASSWORD PROTECTED USER-ID              
         BZ    *+12                                                             
         ICM   RE,3,SECOPASS       IF NO PASSWORD NUMBER                        
         BNZ   IPER00                                                           
*                                                                               
         TM    SECOSTAT,TSTATDDS                                                
         BNZ   *+16                                                             
         ICM   RE,3,SECOSAGN       SHOULD BE OLD SECURITY                       
         BNZ   *+12                                                             
         OI    SECINDS,SECIOLD                                                  
         B     IPERX                                                            
         DC    H'0'                                                             
*                                                                               
IPER00   LA    R2,IOKEY            READ PASSWORD RECORD                         
         USING SA0REC,R2                                                        
         XC    SA0KEY,SA0KEY                                                    
         MVI   SA0KTYP,SA0KTYPQ                                                 
         MVC   SA0KAGY,SECAGY                                                   
         OC    SECOAGPE,SECOAGPE   SWITCH TO SECURITY AGY FOR PERSON            
         BZ    *+10                                                             
         MVC   SA0KAGY,SECOAGPE                                                 
         MVC   SA0KNUM,SECOPASS                                                 
         GOTOR READSA                                                           
         JNE   *+2                                                              
*                                        DATAMGR MAY SWITCH TO SJ/#N/#E         
         OC    SECOAGPE,SECOAGPE         ANY SECURITY AGY FOR PERSON?           
         BNZ   *+10                      YES: SKIP                              
         MVC   SECOAGPE,IOAREA+(SA0KAGY-SA0REC) NO: SAVE AGENCY                 
*                                                                               
         XC    SECCLAL,SECCLAL           CLIENT LIMIT ACCESS LIST               
         LA    RE,SECCLAL                                                       
         ST    RE,ACLALIST                                                      
                                                                                
         LA    R3,IOAREA+(SA0DATA-SA0REC)                                       
IPER020  CLI   0(R3),0                                                          
         BE    IPER050                                                          
         CLI   0(R3),SACLAELQ            X'E1' CLIENT LIMIT ACCESS              
         BE    IPER040                                                          
IPER030  LLC   RF,1(R3)                                                         
         AR    R3,RF                                                            
         B     IPER020                                                          
*                                                                               
         USING SACLAD,R3                                                        
IPER040  CLC   SACLASYS,SECOSYS                                                 
         BNE   IPER030                                                          
         ICM   RE,15,ACLALIST                                                   
         LLC   RF,SACLALN                  PICK UP LENGTH OF ELEMENT            
         AHI   RF,-(SACLALNQ-L'SACLACOD)-1 REMOVE ELEMENT OVERHEAD              
         BM    IPER030                                                          
         CHI   RF,L'SECCLAL              CAN THIS FIT IN SECCLAL?               
         BL    *+8                       YES                                    
         LHI   RF,L'SECCLAL-1            NO: THEN CUT IT OFF                    
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   0(0,RE),SACLACOD          CLIENTS FROM ELEM TO SECCLAL           
         LA    RE,1(RF,RE)                                                      
         ST    RE,ACLALIST               STORE POSITION FOR NEXT CLIENT         
         LA    RF,SECCLAL+L'SECCLAL-1    LAST BYTE OF SECCLAL                   
         CR    RE,RF                     CAN WE STILL FIT MORE?                 
         BNL   IPER050                   NO: STOP IT HERE                       
         B     IPER030                   YES: GOT GET 'EM                       
         DROP  R3                                                               
*                                                                               
IPER050  LA    R3,IOAREA+(SA0DATA-SA0REC)                                       
         XR    RF,RF                                                            
IPER02   CLI   0(R3),0                   EXTRACT PERSON DETAILS                 
         BE    IPER10                                                           
         USING SAPASD,R3                 TEST FOR DDS PASSWORD                  
         CLI   SAPASEL,SAPASELQ                                                 
         BNE   IPER06                                                           
         CLI   SAPASLEN,X'0C'                                                   
         BNE   IPER08                                                           
         CLC   DDSPWD,SAPASDTA                                                  
         BNE   IPER08                                                           
         OI    SECINDS,SECIDDS                                                  
         B     IPER08                                                           
         DROP  R3                                                               
*                                                                               
         USING SAPALD,R3                 EXTRACT PERSONAL ID                    
IPER06   CLI   SAPALEL,SAPALELQ                                                 
         BNE   IPER08                                                           
         MVC   SECPID,SAPALPID                                                  
         B     IPER10                                                           
         DROP  R3                                                               
*                                                                               
IPER08   IC    RF,1(R3)                                                         
         BXH   R3,RF,IPER02                                                     
*                                                                               
IPER10   TM    SECINDS,SECIDDS     TEST DDS PASSWORD                            
         BZ    IPER12                                                           
         OC    SECPID,SECPID       TEST PERSONAL ID IS ON FILE                  
         BNZ   IPER20                                                           
         OI    SECINDS,SECIDUMY    SET DUMMY PERSONAL ID USED                   
         MVC   SECOID,DDSOID                                                    
         MVC   SECDID,DDSDID                                                    
         MVC   SECPID,DDSPID                                                    
         MVC   SECNAMES,DDSNAMES                                                
         B     IPERX                                                            
                                                                                
IPER12   ICM   RE,3,SECOSAGN       GROUP NUMBER PRESENT                         
         BNZ   IPER20              YES:                                         
         OI    SECINDS,SECIOLD     NO: IMPLIES OLD SECURITY IN USE              
*                                                                               
         USING SAPEREC,R2          READ PERSON RECORD                           
IPER20   XC    SAPEKEY,SAPEKEY                                                  
         MVI   SAPETYP,SAPETYPQ                                                 
         MVI   SAPESUB,SAPESUBQ                                                 
         MVC   SAPEAGY,SECAGY                                                   
         OC    SECOAGPE,SECOAGPE   SWITCH TO SECURITY AGY FOR PERSON            
         BZ    *+10                                                             
         MVC   SAPEAGY,SECOAGPE                                                 
         MVC   SAPEPID,SECPID                                                   
         GOTOR VDATCON,PARM,(5,0),(2,SAPEDEF)                                   
         XC    SAPEDEF,EFFS        COMPLEMENT DATE                              
         GOTOR RDHISA                                                           
         BNE   IPEROX              NO RECORD FOUND FOR THIS PERSON              
         CLC   SAPEKEY(SAPEDEF-SAPEKEY),IOAREA                                  
         BNE   IPEROX              NO RECORD FOUND FOR THIS PERSON              
         DROP  R2                                                               
                                                                                
         LA    R3,IOAREA+(SAPEDATA-SAPEREC)                                     
IPER22   CLI   0(R3),0             TEST E-O-R                                   
         BE    IPERX                                                            
         CLI   0(R3),SANAMELQ      TEST NAME ELEMENT                            
         BE    IPER30                                                           
         CLI   0(R3),SAPERELQ      TEST PERSONNEL DETAILS                       
         BE    IPER40                                                           
         CLI   0(R3),SAAGCELQ      ACCESS GROUP CODE ELEMENT                    
         BE    IPER50                                                           
IPER28   XR    RF,RF                                                            
         IC    RF,1(R3)                                                         
         BXH   R3,RF,IPER22                                                     
                                                                                
         USING SANAMD,R3           R3=A(NAME ELEMENT)                           
IPER30   XR    RE,RE                                                            
         LA    RF,SANAMES                                                       
         USING SANAMES,RF          RF=A(NAME SUB ELEMENT)                       
         MVC   SECNAMES,SPACES                                                  
                                                                                
         IC    RE,SANAMELN         COPY FIRST NAME                              
         BCTR  RE,0                                                             
         EX    RE,*+8                                                           
         B     *+10                                                             
         MVC   SECFNAME(0),SANAME                                               
         LA    RF,L'SANAMELN+1(RE,RF)  BUMP RF TO NEXT SUB-ELEMENT              
*                                                                               
         TM    SANAMIND,SANAMIMN   TEST MIDDLE NAME PRESENT                     
         BZ    IPER32                                                           
         IC    RE,SANAMELN         COPY MIDDLE NAME                             
         BCTR  RE,0                                                             
         EX    RE,*+8                                                           
         B     *+10                                                             
         MVC   SECMNAME(0),SANAME                                               
         LA    RF,L'SANAMELN+1(RE,RF)  BUMP RF TO NEXT SUB-ELEMENT              
                                                                                
IPER32   IC    RE,SANAMELN         COPY LAST NAME                               
         LHI   R1,L'SECLNAME       SHORTENED TO FIT SECCLAL                     
         CR    R1,RE                                                            
         BH    *+6                                                              
         LR    RE,R1                                                            
         BCTR  RE,0                                                             
         EX    RE,*+8                                                           
         B     *+10                                                             
         MVC   SECLNAME(0),SANAME                                               
         B     IPER28                                                           
         DROP  RF                                                               
*                                                                               
         USING SAPERD,R3           EXTRACT OFFICE/DEPARTMENT IDS                
IPER40   MVC   SECOID,SAPEROFF                                                  
         MVC   SECDID,SAPERDID                                                  
         B     IPER28                                                           
         DROP  R3                                                               
*                                                                               
         USING SAAGCD,R3           EXTRACT ACCESS GROUP                         
IPER50   TM    INDICS,SECPOSP      OVERRIDE PROGRAM?                            
         BZ    IPER28              NO                                           
         OC    SECOSAGN,SECOSAGN   WILL NEED GROUP NUMBER FOR OVERRIDE          
         BNZ   IPER28              ALREADY HAVE ONE, OR ELSE                    
         MVC   SECOSAGN,SAAGCNUM   GET GROUP# FOR OVERRIDE PROGRAM              
         B     IPER28                                                           
         DROP  R3                                                               
*                                                                               
IPEROX   TM    SECINDS,SECIOLD     OLD SECURITY?                                
         JNO   *+2                 NO: DUMP FOR NO PERSON RECORD FOUND          
IPERX    B     XIT                                                              
         EJECT                                                                  
                                                                                
***********************************************************************         
* SET ACCESS DETAILS FOR FIELDS AND OPTIONS                           *         
***********************************************************************         
INITFOP  NTR1  ,                                                                
*                                                                               
         GOTOR GETACC,PARM,MERGFCS,SAFCSUBQ,SECOSYS,0,0                         
         BE    IFOP10                                                           
         XC    SECFLDWS,SECFLDWS                                                
         XC    SECFLDRS,SECFLDRS                                                
                                                                                
IFOP10   GOTOR GETACC,PARM,MERGOCS,SAOCSUBQ,SECOSYS,0,0                         
         BE    IFOP20                                                           
         XC    SECOPTS,SECOPTS                                                  
                                                                                
IFOP20   TM    SECINDS1,SECPIFLD   TEST FIELDS FOR OTHER PROGRAMS               
         BZ    IFOP22                                                           
         TM    INDICS,INDIOLP                                                   
         BNZ   IFOP24                                                           
IFOP22   GOTOR GETACC,PARM,MERGFCP,SAFCSUBQ,SECOSYS,SECOPRG,SECFLDWP            
         BE    IFOP26                                                           
IFOP24   XC    SECFLDWP,SECFLDWP                                                
         XC    SECFLDRP,SECFLDRP                                                
IFOP26   TM    SECINDS1,SECPIFLD                                                
         BZ    IFOP30                                                           
         TM    INDICS,INDIOSP+INDIOLP TEST OTHER SYSTEM/PROGRAMS REQED          
         BZ    IFOP30                                                           
         LA    R7,SECRALST                                                      
         USING RALSTD,R7                                                        
IFOP28   OC    RALEN,RALEN         TEST END OF LIST                             
         BZ    IFOP30                                                           
         SR    RF,RF                                                            
         ICM   RF,3,RALEN                                                       
         LA    RF,RALSTD(RF)                                                    
         TM    SECINDS1,SECIOPTO   OPTIONS CONTROL LOOKED UP?                   
         BZ    *+8                 . NO                                         
         SHI   RF,L'SECOPTP        . YES, BACK UP OVER OPTIONS CONTROL          
         SHI   RF,L'SECFLDWP+L'SECFLDRP                                         
         GOTOR GETACC,PARM,MERGFCP,SAFCSUBQ,RASYS,RAPGM,(RF)                    
         SR    RF,RF                                                            
         ICM   RF,3,RALEN                                                       
         AR    R7,RF               BUMP TO NEXT RALST ENTRY                     
         B     IFOP28                                                           
         DROP  R7                                                               
                                                                                
IFOP30   TM    SECINDS1,SECPIOPT   TEST OPTIONS FOR OTHER PROGRAMS              
         BZ    IFOP32                                                           
         TM    INDICS,INDIOLP                                                   
         BNZ   IFOP34                                                           
IFOP32   GOTOR GETACC,PARM,MERGOCP,SAOCSUBQ,SECOSYS,SECOPRG,SECOPTP             
         BE    IFOP36                                                           
IFOP34   XC    SECOPTP,SECOPTP                                                  
IFOP36   TM    SECINDS1,SECPIOPT                                                
         BZ    IFOP40                                                           
         TM    INDICS,INDIOSP+INDIOLP TEST OTHER SYSTEM/PROGRAMS REQED          
         BZ    IFOP40                                                           
         LA    R7,SECRALST                                                      
         USING RALSTD,R7                                                        
IFOP38   OC    RALEN,RALEN         TEST END OF LIST                             
         BZ    IFOP40                                                           
         SR    RF,RF                                                            
         ICM   RF,3,RALEN                                                       
         LA    RF,RALSTD(RF)                                                    
         SHI   RF,L'SECOPTP                                                     
         GOTOR GETACC,PARM,MERGOCP,SAOCSUBQ,RASYS,RAPGM,(RF)                    
         SR    RF,RF                                                            
         ICM   RF,3,RALEN                                                       
         AR    R7,RF               BUMP TO NEXT RALST ENTRY                     
         B     IFOP38                                                           
         DROP  R7                                                               
                                                                                
IFOP40   B     XIT                                                              
         EJECT                                                                  
***********************************************************************         
* INITIALIZE RECORD/ACTION SECURITY FOR REQUIRED PROGRAMS             *         
***********************************************************************         
INITRACT NTR1  ,                                                                
*                                                                               
         TM    INDICS,INDIOLP      TEST OVERRIDE LINK PROGRAM                   
         BNZ   IRACT20                                                          
*                                                                               
         TM    SECINDS,SECIOLD     OLD SECURITY?                                
         BZ    IRACT00             NO: GET RECORD/ACTION SETTINGS               
         TM    INDICS,INDIOSP      OVERRIDE PROGRAM?                            
         BO    IRACT20             YES: NEED SETTINGS FOR OVERRIDE              
*                                                                               
IRACT00  GOTOR SETRACT,PARM,SECOSYS,SECOPRG                                     
         TM    INDICS,INDIOSP      TEST ANY OTHERS REQUIRED                     
         BZ    XIT                                                              
*                                                                               
IRACT20  SR    R3,R3                                                            
         ICM   R3,7,SECPASPL+1                                                  
IRACT30  CLI   0(R3),0                                                          
         BE    XIT                                                              
         GOTOR SETRACT,PARM,(R3),1(R3)                                          
         AHI   R3,L'RASPGM                                                      
         B     IRACT30                                                          
                                                                                
***********************************************************************         
* SET RECORD/ACTION ACCESS FOR PROGRAM                                *         
*  - CREATE DUMMY ACCESS RECORD FROM RECORD RECORDS                   *         
*  - SET RECORD/ACTION ACCESS FROM ACCESS RECORDS                     *         
*                                                                     *         
* NTRY: P1 = A(SYSTEM)                                                *         
*       P2 = A(PROGRAM)                                               *         
***********************************************************************         
                                                                                
SETRACT  NTR1  ,                                                                
                                                                                
         LM    R1,R2,0(R1)                                                      
         LA    R7,SECRALST         FIND NEXT FREE ENTRY                         
         USING RALSTD,R7                                                        
         XR    RF,RF                                                            
         ICM   RF,3,RALEN                                                       
         BZ    *+8                                                              
         BXH   R7,RF,*-8                                                        
         MVC   RASYS,0(R1)                                                      
         MVC   RAPGM,0(R2)                                                      
                                                                                
         LA    R2,IOKEY            READ PROGRAM RECORD                          
         USING SAPGREC,R2            TO EXTRACT ACTION CODE LIST                
         XC    SAPGKEY,SAPGKEY                                                  
         MVI   SAPGTYP,SAPGTYPQ                                                 
         MVI   SAPGSUB,SAPGSUBQ                                                 
         MVC   SAPGOVS,RASYS                                                    
         MVC   SAPGPGM,RAPGM                                                    
         GOTOR READSA                                                           
         BNE   XIT                                                              
                                                                                
         LA    R3,IOAREA+(SAPGDATA-SAPGREC)                                     
         USING SAPGMD,R3           FIND PROGRAM ELEMENT                         
         XR    RF,RF                                                            
         IC    RF,SAPGMLN                                                       
         CLI   SAPGMEL,SAPGMELQ                                                 
         BE    *+8                                                              
         BXH   R3,RF,*-12                                                       
         LA    RF,SAPGMD(RF)                                                    
         BCTR  RF,0                                                             
         CLI   0(RF),0             FIND LAST ACTION CODE IN LIST                
         BNE   *+8                                                              
         BCT   RF,*-8                                                           
         LA    R0,SAPGMACT-1                                                    
         SR    RF,R0                                                            
         STC   RF,RAACTSL                                                       
         BCTR  RF,0                                                             
         EX    RF,*+8              COPY FOR MINUMUM LENGTH OF LIST              
         B     *+10                                                             
         MVC   RAACTS(0),SAPGMACT                                               
         SRL   RF,3                                                             
         AHI   RF,RAMLNQ+1                                                      
         STC   RF,RAMIXL           SET LENGTH OF RAMIXD ENTRIES                 
         DROP  R3                                                               
                                                                                
         USING SARCREC,R2          SET UP DUMMY ACCESS RECORD                   
         MVI   SARCSUB,SARCSUBQ      FROM RECORD RECORDS                        
         LA    R2,IOAREA                                                        
                                                                                
         LA    R5,ACCREC+(SAASDATA-SAASREC)                                     
         USING SAMIXD,R5           R5=A(RECORD/ACTIONS ELEMENT)                 
                                                                                
         GOTOR RDHISA                                                           
         B     *+8                                                              
SRACT02  GOTOR RDSQSA                                                           
         CLC   SARCKEY(SARCRCD-SARCREC),IOKEY                                   
         BNE   SRACT10                                                          
                                                                                
         LA    R4,IOAREA+(SARCDATA-SARCREC)                                     
         USING SARCDD,R4           FIND RECORD ELEMENT                          
         XR    RF,RF                                                            
         CLI   SARCDEL,SARCDELQ                                                 
         BE    *+12                                                             
         IC    RF,SARCDLN                                                       
         BXH   R4,RF,*-12                                                       
                                                                                
         CLI   SARCDATT,0          TEST RECORD ATTACHED                         
         BNE   SRACT04                                                          
         GOTOR FINDACTS,PARM,(SARCRCD,SARCREC)                                  
         BE    *+6                                                              
         DC    H'0'                                                             
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   SAMIXD(0),0(R3)     COPY ELEMENT FROM RECORD RECORD              
         B     SRACT08                                                          
                                                                                
SRACT04  GOTOR FINDACTS,PARM,(SARCDATT,ACCREC)                                  
         BE    *+6                                                              
         DC    H'0'                                                             
         EX    RF,*+8              COPY SAMIX ELEMENT FOR ATTACHER              
         B     *+10                                                             
         MVC   SAMIXD(0),0(R3)                                                  
         MVC   SAMIXATT,SAMIXRCD                                                
         MVC   SAMIXRCD,SARCRCD                                                 
         GOTOR FINDACTS,PARM,(SARCRCD,SARCREC)                                  
         NC    FACTBITT,SAMIXACT   MERGE IN INVALID ACTIONS                     
         XC    SAMIXACT,FACTBITT                                                
                                                                                
SRACT08  XR    RF,RF                                                            
         IC    RF,SAMIXLN                                                       
         BXH   R5,RF,SRACT02                                                    
                                                                                
SRACT10  MVI   SAMIXEL,0           SET E-O-R                                    
         DROP  R2,R4,R5                                                         
                                                                                
***********************************************************************         
*  - MERGE IN ACCESS RECORDS                                          *         
***********************************************************************         
                                                                                
         XR    R5,R5                                                            
         IC    R5,RAACTSL                                                       
         LA    R5,RAACTS(R5)                                                    
         USING RAMIXD,R5           R5=A(RAMIX ENTRY)                            
                                                                                
         GOTOR GETACC,PARM,MERGSAAS,SAASSUBQ,RASYS,RAPGM,0                      
         BNE   SRACT20             NO RECORD/ACTIONS VALID                      
                                                                                
         LA    R4,ACCREC+(SAASDATA-SAASREC)                                     
         USING SAMIXD,R4           R4=A(RECORD/ACTIONS ELEMENT)                 
         XR    RE,RE                                                            
         IC    RE,RAMIXL           RE=LENGTH OF RAMIXD ENTRIES                  
         XR    RF,RF                                                            
                                                                                
SRACT12  CLI   SAMIXEL,0           TEST E-O-R                                   
         BE    SRACT20                                                          
         CLI   SAMIXEL,FF          TEST DELETED ELEMENT                         
         BE    SRACT18                                                          
                                                                                
         MVC   RAMRCD,SAMIXRCD     ADD RAMIX ENTRY                              
         IC    RF,SAMIXLN                                                       
         SHI   RF,SAMIXLNQ+1                                                    
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   RAMACT(0),SAMIXACT                                               
         AR    R5,RE               BUMP R5 TO NEXT SECMIX ENTRY                 
                                                                                
SRACT18  IC    RF,SAMIXLN          BUMP R4 TO NEXT ELEMENT                      
         BXH   R4,RF,SRACT12                                                    
                                                                                
SRACT20  MVI   RAMIXD,RAMEOL       SET END-OF LIST                              
         DROP  R4,R5                                                            
                                                                                
         LA    RF,1(R5)            SET LENGTH OF LIST FOR THIS PROG             
         LR    RE,RF                                                            
         LA    R0,RALSTD                                                        
         SR    RF,R0               RF=L'ENTRY SO FAR                            
                                                                                
         TM    SECINDS1,SECPIFLD+SECPIOPT                                       
         BZ    SRACT24                                                          
         TM    SECINDS1,SECPIFLD   TEST FIELDS REQUIRED                         
         BZ    SRACT22                                                          
         XC    0(L'SECFLDWP+L'SECFLDRP,RE),0(RE)                                
         AHI   RE,L'SECFLDWP+L'SECFLDRP                                         
         AHI   RF,L'SECFLDWP+L'SECFLDRP                                         
                                                                                
SRACT22  TM    SECINDS1,SECPIOPT   TEST OPTIONS REQUIRED                        
         BZ    SRACT24                                                          
         XC    0(L'SECOPTP,RE),0(RE)                                            
         AHI   RF,L'SECOPTP                                                     
                                                                                
SRACT24  STH   RF,RALEN                                                         
                                                                                
         LA    R0,SECD+2           CHECK SECURITY AREA HASN'T GONE              
         SR    R5,R0                 OVER IT'S END                              
         CLM   R5,3,SECLEN                                                      
         BL    *+6                                                              
         DC    H'0'                                                             
         B     XIT                                                              
                                                                                
         DROP  R7                                                               
         EJECT                                                                  
***********************************************************************         
* ROUTINES TO MERGE IN FCONTROL/OCONTROL RECORD DETAILS               *         
***********************************************************************         
                                                                                
MERGFCS  NTR1  ,                                                                
         GOTOR MERGBITT,PARM,('SAFCWELQ',SAFCWLNQ),SECFLDWS                     
         GOTOR MERGBITT,PARM,('SAFCRELQ',SAFCRLNQ),SECFLDRS                     
         B     XIT                                                              
                                                                                
MERGFCP  NTR1  ,                                                                
         GOTOR MERGBITT,PARM,('SAFCWELQ',SAFCWLNQ),AOUTPUT                      
         L     R0,AOUTPUT                                                       
         AHI   R0,L'SECFLDWP                                                    
         GOTOR MERGBITT,PARM,('SAFCRELQ',SAFCRLNQ),(R0)                         
         B     XIT                                                              
                                                                                
MERGOCS  NTR1  ,                                                                
         GOTOR MERGBITT,PARM,('SAOCTELQ',SAOCTLNQ),SECOPTS                      
         B     XIT                                                              
                                                                                
MERGOCP  NTR1  ,                                                                
         GOTOR MERGBITT,PARM,('SAOCTELQ',SAOCTLNQ),AOUTPUT                      
         B     XIT                                                              
                                                                                
***********************************************************************         
* ROUTINE TO MERGE BIT TABLE FROM RECORD                              *         
*                                                                     *         
* NTRY: P1=(ELEMENT CODE, DISP. OF TABLE IN ELEMENT)                  *         
*       P2=A(32 BYTE BIT TABLE)                                       *         
***********************************************************************         
                                                                                
MERGBITT NTR1  ,                                                                
         LR    R2,R1               R2=A(PARAMETER LIST)                         
                                                                                
         LA    R3,IOAREA+(SAACDATA-SAACREC)                                     
         XR    RF,RF               FIND ELEMENT IN R3                           
MBITT02  CLI   0(R3),0                                                          
         BNE   *+6                                                              
         DC    H'0'                                                             
         IC    RF,1(R3)            RF=L(ELEMENT)                                
         CLC   0(1,R3),0(R2)       MATCH ON ELEMENT CODE                        
         BE    *+8                                                              
         BXH   R3,RF,MBITT02                                                    
                                                                                
         XR    RE,RE                                                            
         IC    RE,3(R2)            RE=DISP. TO ELEMENT'S BIT TABLE              
                                                                                
         XC    BITTABLE,BITTABLE   COPY AND EXPAND BIT TABLE                    
         SR    RF,RE               RF=L(ELEMENT'S BIT TABLE)                    
         BZ    MBITT04                                                          
         AR    RE,R3               RE=A(ELEMENT'S BIT TABLE)                    
         BCTR  RF,0                                                             
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   BITTABLE(0),0(RE)                                                
                                                                                
MBITT04  L     R1,4(R2)            R1=A(OUTPUT BIT TABLE)                       
         NC    0(L'BITTABLE,R1),BITTABLE                                        
         TM    INDICS,INDIAGY      IF MAIN AGENCY RECORD                        
         BZ    *+10                  COPY BIT TABLE                             
         MVC   0(L'BITTABLE,R1),BITTABLE                                        
                                                                                
         B     XIT                                                              
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO MERGE IN ACCESS RECORD                                   *         
***********************************************************************         
                                                                                
MERGSAAS NTR1  ,                                                                
         LA    R4,ACCREC+(SAASDATA-SAASREC)                                     
         USING SAMIXD,R4           R4=A(RECORD/ACTIONS ELEMENT)                 
                                                                                
MSAAS2   CLI   SAMIXEL,0           TEST E-O-R                                   
         BE    MSAAS20                                                          
         CLI   SAMIXEL,FF          TEST ELEMENT DELETED                         
         BE    MSAAS18                                                          
                                                                                
         IC    R2,SAMIXLN          R2=L(BIT TABLE)-1                            
         SHI   R2,SAMIXLNQ+1                                                    
                                                                                
         XC    BITTABLE,BITTABLE   SET UP BIT TABLE                             
         EX    R2,*+8                                                           
         B     *+10                                                             
         MVC   BITTABLE(0),SAMIXACT                                             
                                                                                
         CLI   SAMIXATT,0          TEST RECORD ATTACHED                         
         BE    MSAAS4                                                           
         GOTOR FINDACTS,PARM,(SAMIXATT,ACCREC)                                  
         NC    BITTABLE,FACTBITT                                                
         BZ    MSAAS8                                                           
         GOTOR FINDACTS,PARM,(SAMIXRCD,IOAREA)                                  
         NC    FACTBITT,BITTABLE   TURN OFF POSITIVE 'NO'S                      
         XC    BITTABLE,FACTBITT                                                
         BZ    MSAAS8                                                           
         B     MSAAS6                                                           
                                                                                
MSAAS4   GOTOR FINDACTS,PARM,(SAMIXRCD,IOAREA)                                  
         NC    BITTABLE,FACTBITT                                                
         BZ    MSAAS8                                                           
                                                                                
MSAAS6   EX    R2,*+8              COPY IN NEW BIT TABLE                        
         B     *+10                                                             
         MVC   SAMIXACT(0),BITTABLE                                             
         B     MSAAS18                                                          
                                                                                
MSAAS8   MVI   SAMIXEL,FF          'DELETE' ELEMENT                             
                                                                                
MSAAS18  XR    RF,RF               BUMP R4 TO NEXT ELEMENT                      
         IC    RF,SAMIXLN                                                       
         BXH   R4,RF,MSAAS2                                                     
                                                                                
MSAAS20  B     XIT                                                              
                                                                                
         DROP  R4                                                               
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO FIND SAMIXD ELEMENT IN RECORD                            *         
*                                                                     *         
* NTRY: P1=(RECORD TYPE, A(ACCESS RECORD))                            *         
*                                                                     *         
* EXIT: IF FOUND:     CC=EQUAL,       FACTBITT=BIT MASK FOR ACTIONS   *         
*                     R3=A(ELEMENT),  RF=L(ELEMENT)                   *         
*       IF NOT FOUND: CC=NOT EQUAL,   FACTBITT=ZERO                   *         
***********************************************************************         
                                                                                
FINDACTS XR    R3,R3                                                            
         ICM   R3,7,1(R1)                                                       
         AHI   R3,SAASDATA-SAASREC                                              
         USING SAMIXD,R3           R3=A(RECORD/ACTIONS ELEMENT)                 
                                                                                
         XC    FACTBITT,FACTBITT                                                
         XR    RF,RF                                                            
                                                                                
FACTS2   CLI   SAMIXEL,0           TEST E-O-R                                   
         BE    FINDACTN                                                         
                                                                                
         IC    RF,SAMIXLN                                                       
         CLI   SAMIXEL,SAMIXELQ    MATCH ON ELEMENT CODE                        
         BNE   FACTS4                                                           
         CLC   SAMIXRCD,0(R1)      MATCH ON RECORD TYPE                         
         BE    FINDACTY                                                         
         BH    FINDACTN                                                         
                                                                                
FACTS4   BXH   R3,RF,FACTS2        BUMP R3 TO NEXT ELEMENT                      
                                                                                
FINDACTY SHI   RF,SAMIXLNQ+1       COPY TABLE                                   
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   FACTBITT(0),SAMIXACT                                             
         AHI   RF,SAMIXLNQ+1                                                    
         CR    RE,RE                                                            
         BR    RE                                                               
                                                                                
FINDACTN LTR   RE,RE               EXIT WITH CC=NOT EQUAL                       
         BR    RE                                                               
                                                                                
         DROP  R3                                                               
         EJECT                                                                  
***********************************************************************         
* READ ACCESS RECORDS FOR AGENCY/USER-ID/ACCESS GROUP                 *         
*                                                                     *         
* NTRY: P1 = A(MERGE ROUTINE)                                         *         
*       P2 = SUB-RECORD TYPE                                          *         
*       P3 = A(SYSTEM)                                                *         
*       P4 = A(PROGRAM) OR 0 FOR SYSTEM-WIDE RECORD                   *         
* EXIT: CC = EQUAL IF ACCESS GROUP LEVEL RECORD FOUND                 *         
***********************************************************************         
                                                                                
GETACC   NTR1  ,                                                                
         MVC   AMERGE,0(R1)                                                     
         MVC   AOUTPUT,16(R1)                                                   
                                                                                
         LA    R2,IOKEY            INITITIALIZE KEY                             
         USING SAASREC,R2                                                       
         XC    SAASKEY,SAASKEY                                                  
         MVI   SAASTYP,SAASTYPQ                                                 
         LM    R3,R5,4(R1)                                                      
         STC   R3,SAASSUB                                                       
         MVC   SAASAGY,SECAGY                                                   
         OC    SECOAGPE,SECOAGPE   SWITCH TO SECURITY AGY FOR PERSON            
         BZ    *+10                                                             
         MVC   SAASAGY,SECOAGPE                                                 
         MVC   SAASOVS,0(R4)                                                    
         LTR   R5,R5                                                            
         BZ    *+10                                                             
         MVC   SAASPGM,0(R5)                                                    
                                                                                
         GOTOR READSA              READ AGENCY-LEVEL RECORD                     
         BNE   GETACCN                                                          
         OI    INDICS,INDIAGY                                                   
         GOTOR AMERGE                                                           
         NI    INDICS,FF-INDIAGY                                                
                                                                                
         MVC   SAASUID,SECOUSER    READ USER-ID LEVEL RECORD                    
         GOTOR READSA                                                           
         BNE   GETACC2                                                          
         GOTOR AMERGE                                                           
                                                                                
GETACC2  MVC   SAASAGN,SECOSAGN    READ USER-ID/ACCESS GROUP LEVEL REC          
         GOTOR READSA                                                           
         BNE   GETACC4                                                          
         GOTOR AMERGE                                                           
         B     GETACCY                                                          
                                                                                
GETACC4  XC    SAASUID,SAASUID     READ ACCESS GROUP LEVEL RECORD               
         MVC   SAASAGN,SECOSAGN                                                 
         GOTOR READSA                                                           
         BNE   GETACCN                                                          
         GOTOR AMERGE                                                           
                                                                                
GETACCY  CR    RB,RB                                                            
         B     XIT                                                              
GETACCN  LTR   RB,RB                                                            
         B     XIT                                                              
         DROP  R2                                                               
         EJECT                                                                  
***********************************************************************         
* GET OFFICE NAME                                                     *         
***********************************************************************         
                                                                                
GETOFFN  TM    SECINDS,SECIOLD     TEST OLD SECURITY                            
         BNZ   *+12                                                             
         TM    SECINDS,SECIOVAL    TEST SECURITY VALUES PRESENT                 
         BNZ   GOFFN2                                                           
         LA    R1,SPACES                                                        
         B     GOFFN10                                                          
                                                                                
GOFFN2   TM    SECINDS,SECIDUMY    TEST DUMMY PERSONAL ID                       
         BZ    GOFFN4                                                           
         LA    R1,DDSONAME         OUTPUT DUMMY DDS OFFICE NAME                 
         B     GOFFN10                                                          
                                                                                
GOFFN4   LA    R2,IOKEY            INITIALIZE OFFICE RECORD KEY                 
         USING SAOFREC,R2                                                       
         XC    SAOFKEY,SAOFKEY                                                  
         MVI   SAOFTYP,SAOFTYPQ                                                 
         MVI   SAOFSUB,SAOFSUBQ                                                 
         MVC   SAOFAGY,SECAGY                                                   
         OC    SECOAGPE,SECOAGPE   SWITCH TO SECURITY AGY FOR PERSON            
         BZ    *+10                                                             
         MVC   SAOFAGY,SECOAGPE                                                 
         MVC   SAOFOID,SECOID                                                   
                                                                                
         GOTOR READSA              READ RECORD                                  
         BNE   SECRETN                                                          
                                                                                
         LA    R3,IOAREA+(SAOFDATA-SAOFREC)                                     
         USING SAOFFD,R3           FIND OFFICE ELEMENT                          
         XR    RF,RF                                                            
         IC    RF,SAOFFLN                                                       
         CLI   SAOFFEL,SAOFFELQ                                                 
         BE    *+8                                                              
         BXH   R3,RF,*-12                                                       
         AR    RF,R3               APPEND SPACES TO NAME                        
         MVC   0(L'SPACES,RF),SPACES                                            
         LA    R1,SAOFFNAM                                                      
         DROP  R3                                                               
                                                                                
GOFFN10  ICM   RF,1,SECPLOUT       RF=L(OUTPUT)                                 
         BNZ   *+8                                                              
         LHI   RF,L'SAOFFNAM                                                    
                                                                                
         XR    RE,RE               COPY NAME INTO OUTPUT                        
         ICM   RE,7,SECPAOUT                                                    
         BCTR  RF,0                                                             
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   0(0,RE),0(R1)                                                    
         B     SECRETY                                                          
                                                                                
         DROP  R2                                                               
         EJECT                                                                  
***********************************************************************         
* GET DEPARTMENT NAME                                                 *         
***********************************************************************         
                                                                                
GETDPTN  TM    SECINDS,SECIOLD     TEST OLD SECURITY                            
         BNZ   *+12                                                             
         TM    SECINDS,SECIOVAL    TEST SECOVALS PRESENT                        
         BNZ   GDPTN2                                                           
         LA    R1,SPACES                                                        
         B     GDPTN10                                                          
                                                                                
GDPTN2   TM    SECINDS,SECIDUMY    TEST DUMMY PERSONAL ID                       
         BZ    GDPTN4                                                           
         LA    R1,DDSONAME         OUTPUT DUMMY DDS OFFICE NAME                 
         B     GDPTN10                                                          
                                                                                
GDPTN4   LA    R2,IOKEY            INITIALIZE DEPARTMENT RECORD KEY             
         USING SADPREC,R2                                                       
         XC    SADPKEY,SADPKEY                                                  
         MVI   SADPTYP,SADPTYPQ                                                 
         MVI   SADPSUB,SADPSUBQ                                                 
         MVC   SADPAGY,SECAGY                                                   
         OC    SECOAGPE,SECOAGPE   SWITCH TO SECURITY AGY FOR PERSON            
         BZ    *+10                                                             
         MVC   SADPAGY,SECOAGPE                                                 
         MVC   SADPOID,SECOID                                                   
         MVC   SADPDID,SECDID                                                   
                                                                                
         GOTOR READSA              READ RECORD                                  
         BNE   SECRETN                                                          
                                                                                
         LA    R3,IOAREA+(SADPDATA-SADPREC)                                     
         USING SADPTD,R3           FIND DEPARTMENT ELEMENT                      
         XR    RF,RF                                                            
         IC    RF,SADPTLN                                                       
         CLI   SADPTEL,SADPTELQ                                                 
         BE    *+8                                                              
         BXH   R3,RF,*-12                                                       
         AR    RF,R3               APPEND SPACES TO NAME                        
         MVC   0(L'SPACES,RF),SPACES                                            
         LA    R1,SADPTNAM                                                      
         DROP  R3                                                               
                                                                                
GDPTN10  ICM   RF,1,SECPLOUT       RF=L(OUTPUT)                                 
         BNZ   *+8                                                              
         LHI   RF,L'SADPTNAM                                                    
                                                                                
         XR    RE,RE               COPY NAME INTO OUTPUT                        
         ICM   RE,7,SECPAOUT                                                    
         BCTR  RF,0                                                             
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   0(0,RE),0(R1)                                                    
         B     SECRETY                                                          
                                                                                
         DROP  R2                                                               
         EJECT                                                                  
***********************************************************************         
* TEST RECORD IS VALID (FOR AT LEAST ONE ACTION)                      *         
***********************************************************************         
                                                                                
TSTRCD   TM    SECINDS,SECIDDS+SECIOLD                                          
         BNZ   SECRETY             DDS/OLD SECURITY = YES                       
         TM    SECINDS,SECIOVAL                                                 
         BZ    SECRETY             NO SECURITY VALUES = YES                     
                                                                                
         GOTOR SETRALST            FIND RALST ENTRY                             
         L     R7,ARALST                                                        
         USING RALSTD,R7                                                        
                                                                                
         XR    R1,R1                                                            
         ICM   R1,7,SECPACOD       R1=A(RECORD TYPE)                            
         XR    R5,R5                                                            
         IC    R5,RAACTSL                                                       
         LA    R5,RAACTS(R5)                                                    
         USING RAMIXD,R5           R5=(RECORD/ACTION LIST ENTRY)                
         XR    RF,RF                                                            
         IC    RF,RAMIXL           RF=LENGTH OF LIST ENTRY                      
                                                                                
TRCD2    CLI   RAMIXD,RAMEOL       TEST END-OF-LIST                             
         BE    SECRETN                                                          
                                                                                
         CLC   RAMRCD,0(R1)        MATCH ON RECORD TYPE                         
         BE    SECRETY                                                          
         BH    SECRETN             RECORD TYPE NOT IN LIST                      
                                                                                
         BXH   R5,RF,TRCD2         BUMP R5 TO NEXT LIST ENTRY                   
                                                                                
         DROP  R5,R7                                                            
         EJECT                                                                  
***********************************************************************         
* TEST ACTION IS VALID (FOR AT LEAST ONE RECORD)                      *         
***********************************************************************         
                                                                                
TSTACT   TM    SECINDS,SECIDDS+SECIOLD                                          
         BNZ   SECRETY             DDS/OLD SECURITY = YES                       
         TM    SECINDS,SECIOVAL                                                 
         BZ    SECRETY             NO SECURITY VALUES = YES                     
                                                                                
         GOTOR SETRALST            FIND RALST ENTRY                             
         L     R7,ARALST                                                        
         USING RALSTD,R7                                                        
                                                                                
         GOTOR CONVACT             R0=ACTION SEQUENCE NUMBER                    
         BNE   SECRETN                                                          
                                                                                
         XR    R5,R5                                                            
         IC    R5,RAACTSL                                                       
         LA    R5,RAACTS(R5)                                                    
         USING RAMIXD,R5           R5=A(RECORD/ACTIONS LIST)                    
         XR    R3,R3                                                            
         IC    R3,RAMIXL           R3=L(RAMIX ENTRY)                            
                                                                                
TACT2    CLI   RAMIXD,RAMEOL       TEST END-OF-LIST                             
         BE    SECRETN                                                          
                                                                                
         GOTOR TESTBIT,RAMACT                                                   
         BO    SECRETY                                                          
                                                                                
TACT4    BXH   R5,R3,TACT2                                                      
                                                                                
         DROP  R5,R7                                                            
         EJECT                                                                  
***********************************************************************         
* TEST RECORD ACTION COMBINATION                                      *         
***********************************************************************         
                                                                                
TSTRACT  TM    SECINDS,SECIDDS+SECIOLD                                          
         BNZ   SECRETY             DDS/OLD SECURITY = YES                       
         TM    SECINDS,SECIOVAL                                                 
         BZ    SECRETY             NO SECURITY VALUES = YES                     
                                                                                
         CLI   SECPRTYP,0          IF RECORD TYPE IS ZERO                       
         BE    TSTACT                TEST ACTION IS VALID FOR ANY REC.          
                                                                                
         GOTOR SETRALST            FIND RALST ENTRY                             
         L     R7,ARALST                                                        
         USING RALSTD,R7                                                        
                                                                                
         XR    R5,R5                                                            
         IC    R5,RAACTSL                                                       
         LA    R5,RAACTS(R5)                                                    
         USING RAMIXD,R5           R5=(RECORD/ACTION LIST ENTRY)                
         XR    RF,RF                                                            
         IC    RF,RAMIXL           RF=LENGTH OF LIST ENTRY                      
                                                                                
TRACT2   CLI   RAMIXD,RAMEOL       TEST END-OF-LIST                             
         BE    SECRETN                                                          
                                                                                
         CLC   RAMRCD,SECPRTYP     MATCH ON RECORD CODE                         
         BE    TRACT4                                                           
         BH    SECRETN             RECORD TYPE NOT IN LIST                      
                                                                                
         BXH   R5,RF,TRACT2        BUMP R5 TO NEXT LIST ENTRY                   
                                                                                
TRACT4   GOTOR CONVACT             SET R0 TO ACTION SEQUENCE NUMBER             
         BH    SECRETY               (RECORD IS VALID FOR >0 ACTIONS)           
         BL    SECRETN             UNKNOWN ACTION NUMBER                        
                                                                                
         GOTOR TESTBIT,RAMACT                                                   
         BO    SECRETY                                                          
         B     SECRETN                                                          
                                                                                
         DROP  R5,R7                                                            
         EJECT                                                                  
***********************************************************************         
* GET RECORD ACTION ACCESS LIST                                       *         
***********************************************************************         
                                                                                
GETRAL   TM    SECINDS,SECIDDS+SECIOLD                                          
         BNZ   SECRETY             DDS/OLD SECURITY = YES                       
         TM    SECINDS,SECIOVAL                                                 
         BZ    SECRETY             NO SECURITY VALUES = YES                     
                                                                                
         SR    R3,R3               R3=L(OUTPUT)                                 
         ICM   R3,1,SECPLOUT                                                    
         BZ    SECRETY                                                          
                                                                                
         XR    R4,R4               R4=A(OUTPUT)                                 
         ICM   R4,7,SECPAOUT                                                    
         BZ    SECRETY                                                          
                                                                                
         GOTOR SETRALST            FIND RALST ENTRY                             
         L     R7,ARALST                                                        
         USING RALSTD,R7                                                        
                                                                                
         XR    R5,R5                                                            
         IC    R5,RAACTSL                                                       
         LA    R5,RAACTS(R5)                                                    
         USING RAMIXD,R5           R5=(RECORD/ACTION LIST ENTRY)                
                                                                                
GRAL010  CLI   RAMIXD,RAMEOL       TEST END-OF-LIST                             
         BE    GRAL100                                                          
                                                                                
         SHI   R3,2                                                             
         BM    SECRETN                                                          
         GOTOR VHEXOUT,PARM,RAMRCD,0(R4),1,=C'TOG'                              
         AHI   R4,2                                                             
                                                                                
         LA    R2,RAACTS           R2=A(ACTION CODE LIST)                       
         CLI   RAACTSL,0                                                        
         BE    GRAL040                                                          
         XR    R6,R6                                                            
         IC    R6,RAACTSL          R6=L(ACTION CODE LIST)                       
                                                                                
GRAL020  MVC   BYTE,0(R2)                                                       
         B     GRAL030                                                          
                                                                                
GRAL022  AHI   R2,1                BUMP R1 TO NEXT ACTION CODE                  
         BCT   R6,GRAL020                                                       
         B     GRAL040                                                          
                                                                                
GRAL030  XR    R0,R0                                                            
         IC    R0,RAACTSL                                                       
         SR    R0,R6               R0=ACTION SEQUENCE NUMBER                    
         GOTOR TESTBIT,RAMACT                                                   
         BNO   GRAL022                                                          
         SHI   R3,2                                                             
         BM    SECRETN                                                          
         GOTOR VHEXOUT,PARM,BYTE,0(R4),1,=C'TOG'                                
         AHI   R4,2                                                             
         B     GRAL022             BUMP R2 TO NEXT LIST ENTRY                   
                                                                                
GRAL040  SHI   R3,2                                                             
         BM    SECRETN                                                          
         MVC   0(2,R4),=CL2'00'                                                 
         AHI   R4,2                                                             
         XR    RF,RF                                                            
         IC    RF,RAMIXL                                                        
         AR    R5,RF                                                            
         B     GRAL010                                                          
                                                                                
GRAL100  SHI   R3,2                                                             
         BM    SECRETN                                                          
         MVC   0(2,R4),=CL2'00'                                                 
         AHI   R4,2                                                             
                                                                                
         B     SECRETY                                                          
                                                                                
         DROP  R5,R7                                                            
         EJECT                                                                  
***********************************************************************         
* GET FIELD CONTROLS LIST                                             *         
***********************************************************************         
                                                                                
GETFCL   TM    SECINDS,SECIDDS+SECIOLD                                          
         BNZ   SECRETY             DDS/OLD SECURITY = YES                       
         TM    SECINDS,SECIOVAL                                                 
         BZ    SECRETY             NO SECURITY VALUES = YES                     
         SR    R3,R3               R3=L(OUTPUT)                                 
         ICM   R3,1,SECPLOUT                                                    
         BZ    SECRETY                                                          
                                                                                
         XR    R4,R4               R4=A(OUTPUT)                                 
         ICM   R4,7,SECPAOUT                                                    
         BZ    SECRETY                                                          
                                                                                
         MVC   SAVPACOD,SECPACOD                                                
         LA    RF,BYTE                                                          
         STCM  RF,7,SECPACOD                                                    
         SR    R7,R7                                                            
         LHI   R6,255                                                           
                                                                                
GFCL010  STC   R7,BYTE                                                          
         GOTOR TSTACOD,SECFLDWP                                                 
         BNO   GFCL020                                                          
                                                                                
         SHI   R3,2                                                             
         BM    GFCLNO                                                           
         GOTOR VHEXOUT,PARM,BYTE,0(R4),1,=C'TOG'                                
         AHI   R4,2                                                             
                                                                                
GFCL020  AHI   R7,1                                                             
         BCT   R6,GFCL010                                                       
                                                                                
GFCL100  SHI   R3,2                                                             
         BM    GFCLNO                                                           
         MVC   0(2,R4),=CL2'00'                                                 
         AHI   R4,2                                                             
                                                                                
         SR    R7,R7                                                            
         LHI   R6,255                                                           
                                                                                
GFCL110  STC   R7,BYTE                                                          
         GOTOR TSTACOD,SECFLDWP                                                 
         BO    GFCL120                                                          
         GOTOR TSTACOD,SECFLDRP                                                 
         BNO   GFCL120                                                          
                                                                                
         SHI   R3,2                                                             
         BM    GFCLNO                                                           
         GOTOR VHEXOUT,PARM,BYTE,0(R4),1,=C'TOG'                                
         AHI   R4,2                                                             
                                                                                
GFCL120  AHI   R7,1                                                             
         BCT   R6,GFCL110                                                       
                                                                                
GFCL200  SHI   R3,2                                                             
         BM    GFCLNO                                                           
         MVC   0(2,R4),=CL2'00'                                                 
         AHI   R4,2                                                             
         B     GFCLOK                                                           
                                                                                
GFCLNO   MVC   SECPACOD,SAVPACOD                                                
         B     SECRETN                                                          
GFCLOK   MVC   SECPACOD,SAVPACOD                                                
         B     SECRETY                                                          
                                                                                
         B     SECRETY                                                          
                                                                                
         EJECT                                                                  
***********************************************************************         
* GET OPTION CONTROLS LIST                                            *         
***********************************************************************         
                                                                                
GETOCL   TM    SECINDS,SECIDDS+SECIOLD                                          
         BNZ   SECRETY             DDS/OLD SECURITY = YES                       
         TM    SECINDS,SECIOVAL                                                 
         BZ    SECRETY             NO SECURITY VALUES = YES                     
         SR    R3,R3               R3=L(OUTPUT)                                 
         ICM   R3,1,SECPLOUT                                                    
         BZ    SECRETY                                                          
                                                                                
         XR    R4,R4               R4=A(OUTPUT)                                 
         ICM   R4,7,SECPAOUT                                                    
         BZ    SECRETY                                                          
                                                                                
         MVC   SAVPACOD,SECPACOD                                                
         LA    RF,BYTE                                                          
         STCM  RF,7,SECPACOD                                                    
         SR    R7,R7                                                            
         LHI   R6,255                                                           
                                                                                
GOCL010  STC   R7,BYTE                                                          
         GOTOR TSTACOD,SECOPTP                                                  
         BNO   GOCL020                                                          
                                                                                
         SHI   R3,2                                                             
         BM    GOCLNO                                                           
         GOTOR VHEXOUT,PARM,BYTE,0(R4),1,=C'TOG'                                
         AHI   R4,2                                                             
                                                                                
GOCL020  AHI   R7,1                                                             
         BCT   R6,GOCL010                                                       
                                                                                
GOCL100  SHI   R3,2                                                             
         BM    GOCLNO                                                           
         MVC   0(2,R4),=CL2'00'                                                 
         AHI   R4,2                                                             
         B     GOCLOK                                                           
                                                                                
GOCLNO   MVC   SECPACOD,SAVPACOD                                                
         B     SECRETN                                                          
GOCLOK   MVC   SECPACOD,SAVPACOD                                                
         B     SECRETY                                                          
                                                                                
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO SET A(RALSTD) IN ARALST                                  *         
***********************************************************************         
                                                                                
SETRALST NTR1  ,                                                                
         LA    R7,SECRALST                                                      
         USING RALSTD,R7                                                        
         TM    SECINDS1,SECIFLDO+SECIOPTO                                       
         BZ    *+12                                                             
         TM    SECPIND1,SECPIFLD+SECPIOPT                                       
         BNZ   SALST02                                                          
         TM    INDICS,INDIOLP      TEST OVERRIDE LINK PROGRAM                   
         BNZ   SALST02                                                          
         TM    INDICS,INDIOSP      TEST OVERRIDE SYSTEM/PROGRAM                 
         BZ    SALSTX                                                           
SALST02  L     R3,SECPAOSP                                                      
         XR    RF,RF                                                            
SALST04  CLC   RASPGM,0(R3)                                                     
         BE    SALSTX                                                           
         ICM   RF,3,RALEN                                                       
         BZ    *+8                                                              
         BXH   R7,RF,SALST04                                                    
         DC    H'0'                                                             
SALSTX   ST    R7,ARALST                                                        
         B     XIT                                                              
         DROP  R7                                                               
                                                                                
***********************************************************************         
* ROUTINE TO CONVERT ACTION CODE TO ACTION SEQUENCE NUMBER            *         
*                                                                     *         
* NTRY: SECPACOD=A(ACTION CODE)                                       *         
*         ARALST=RALSTD ENTRY                                         *         
* EXIT: IF ACTION CODE IS ZERO (FOR ALL) CC=HIGH                      *         
*       IF ACTION CODE IS UNDEFINED CC=LOW                            *         
*       IF ACTION CODE IS FOUND CC=EQUAL & R0=ACTION SEQUENCE #       *         
***********************************************************************         
                                                                                
CONVACT  ICM   RF,7,SECPACOD       R0=ACTION CODE                               
         BZ    CONVACTH                                                         
         MVC   BYTE,0(RF)                                                       
         CLI   BYTE,0                                                           
         BE    CONVACTH                                                         
                                                                                
         L     R6,ARALST                                                        
         USING RALSTD,R7                                                        
                                                                                
         LA    R1,RAACTS           R1=A(ACTION CODE LIST)                       
         XR    RF,RF                                                            
         IC    RF,RAACTSL          RF=L(ACTION CODE LIST)                       
         LR    R0,RF                                                            
                                                                                
CACT02   CLC   BYTE,0(R1)          MATCH ON ACTION CODE                         
         BE    CACT04                                                           
         AHI   R1,1                BUMP R1 TO NEXT ACTION CODE                  
         BCT   RF,CACT02                                                        
         B     CONVACTL                                                         
                                                                                
CACT04   SR    R0,RF               R0=ACTION SEQUENCE NUMBER                    
                                                                                
CONVACTE CR    RE,RE               SET CC=EQUAL                                 
         BR    RE                                                               
CONVACTH CLI   *,0                 SET CC=HIGH FOR ACTION CODE=0                
         BR    RE                                                               
CONVACTL CLI   *,FF                SET CC=LOW FOR ACTION CODE UNDEFINED         
         BR    RE                                                               
         DROP  R7                                                               
         EJECT                                                                  
***********************************************************************         
* TEST SYSTEM-WIDE FIELD                                              *         
***********************************************************************         
                                                                                
TSTFLDS  TM    SECINDS,SECIDDS+SECIOLD                                          
         BNZ   SECRETY             DDS/OLD SECURITY = YES                       
         TM    SECINDS,SECIOVAL                                                 
         BZ    SECRETY             NO SECURITY VALUES = YES                     
         GOTOR TSTACOD,SECFLDWS                                                 
         BO    SECRETY             MAY READ AND WRITE                           
         GOTOR TSTACOD,SECFLDRS                                                 
         BO    SECRETR             MAY READ ONLY                                
         B     SECRETN             MAYN'T READ NOR WRITE                        
                                                                                
***********************************************************************         
* TEST PROGRAM-SPECIFIC FIELD                                         *         
***********************************************************************         
                                                                                
TSTFLDP  TM    SECINDS,SECIDDS+SECIOLD                                          
         BNZ   SECRETY             DDS/OLD SECURITY = YES                       
         TM    SECINDS,SECIOVAL                                                 
         BZ    SECRETY             NO SECURITY VALUES = YES                     
                                                                                
         TM    SECINDS1,SECIFLDO                                                
         BZ    *+12                                                             
         TM    SECPIND1,SECPIFLD   TEST FIELDS FOR OVERRIDE PROGRAM             
         BNZ   TSTFLDP2                                                         
                                                                                
         GOTOR TSTACOD,SECFLDWP                                                 
         BO    SECRETY             MAY READ AND WRITE                           
         GOTOR TSTACOD,SECFLDRP                                                 
         BO    SECRETR             MAY READ ONLY                                
         B     SECRETN             MAYN'T READ NOR WRITE                        
                                                                                
TSTFLDP2 GOTOR SETRALST                                                         
         L     R7,ARALST                                                        
         USING RALSTD,R7                                                        
         SR    R5,R5                                                            
         ICM   R5,3,RALEN                                                       
         LA    R5,RALSTD(R5)                                                    
         TM    SECINDS1,SECIOPTO   OPTIONS CONTROL LOOKED UP?                   
         BZ    *+8                 . NO                                         
         SHI   R5,L'SECOPTP        . YES, BACK UP OVER OPTIONS CONTROL          
         SHI   R5,L'SECFLDWP+L'SECFLDRP                                         
         GOTOR TSTACOD,0(R5)                                                    
         BO    SECRETY                                                          
         GOTOR TSTACOD,L'SECFLDWP(R5)                                           
         BO    SECRETR                                                          
         B     SECRETN                                                          
         DROP  R7                                                               
                                                                                
***********************************************************************         
* TEST SYSTEM-WIDE OPTION                                             *         
***********************************************************************         
                                                                                
TSTOPTS  TM    SECINDS,SECIDDS+SECIOLD                                          
         BNZ   SECRETY             OFF-LINE/DDS/OLD SECURITY = YES              
         TM    SECINDS,SECIOVAL                                                 
         BZ    SECRETY             NO SECURITY VALUES = YES                     
         GOTOR TSTACOD,SECOPTS                                                  
         BO    SECRETY                                                          
         B     SECRETN                                                          
                                                                                
***********************************************************************         
* TEST PROGRAM-SPECIFIC OPTION                                        *         
***********************************************************************         
                                                                                
TSTOPTP  TM    SECINDS,SECIDDS+SECIOLD                                          
         BNZ   SECRETY             DDS/OLD SECURITY = YES                       
         TM    SECINDS,SECIOVAL                                                 
         BZ    SECRETY             NO SECURITY VALUES = YES                     
                                                                                
         TM    SECINDS1,SECIOPTO                                                
         BZ    *+12                                                             
         TM    SECPIND1,SECPIOPT   TEST OPTIONS FOR OVERRIDE PROGRAM            
         BNZ   TSTOPTP2                                                         
                                                                                
         GOTOR TSTACOD,SECOPTP                                                  
         BO    SECRETY                                                          
         B     SECRETN                                                          
                                                                                
TSTOPTP2 GOTOR SETRALST                                                         
         L     R7,ARALST                                                        
         USING RALSTD,R7                                                        
         SR    R5,R5                                                            
         ICM   R5,3,RALEN                                                       
         LA    R5,RALSTD(R5)                                                    
         SHI   R5,L'SECOPTP                                                     
         GOTOR TSTACOD,0(R5)                                                    
         BO    SECRETY                                                          
         B     SECRETN                                                          
         DROP  R7                                                               
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO TEST A BIT OF A BIT TABLE                                *         
*                                                                     *         
* NTRY: R1=A(BIT TABLE), SECPACOD=A(CODE)                             *         
***********************************************************************         
                                                                                
TSTACOD  XR    RF,RF               R0=CODE                                      
         ICM   RF,7,SECPACOD                                                    
         XR    R0,R0                                                            
         IC    R0,0(RF)                                                         
                                                                                
***********************************************************************         
* NTRY: R1=A(BIT TABLE), R0=CODE                                      *         
***********************************************************************         
                                                                                
TESTBIT  LR    RF,R0               R1=A(BYTE OF BIT)                            
         SRL   RF,3                                                             
         AR    R1,RF                                                            
                                                                                
         LHI   RF,X'07'            RF=MASK                                      
         NR    RF,R0                                                            
         IC    RF,MASKS(RF)                                                     
                                                                                
         EX    RF,TESTBITM         EX A TM                                      
         BR    RE                                                               
                                                                                
TESTBITM TM    0(R1),0             A TM                                         
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO READ SECURITY ACCESS RECORD                              *         
***********************************************************************         
                                                                                
READSA   NTR1  ,                                                                
         MVC   IOAREA(L'IOKEY),IOKEY                                            
         GOTOR VDATAMGR,PARM,DMREAD,CTFILE,IOAREA,IOAREA                        
         B     XIT                                                              
                                                                                
***********************************************************************         
* ROUTINE TO READ HIGH FOR A SECURITY ACCESS RECORD                   *         
***********************************************************************         
                                                                                
RDHISA   NTR1  ,                                                                
         MVC   IOAREA(L'IOKEY),IOKEY                                            
         GOTOR VDATAMGR,PARM,DMRDHI,CTFILE,IOAREA,IOAREA                        
         B     XIT                                                              
                                                                                
***********************************************************************         
* ROUTINE TO READ SEQUENTIAL FOR A SECURITY ACCESS RECORD             *         
***********************************************************************         
                                                                                
RDSQSA   NTR1  ,                                                                
         MVC   IOAREA(L'IOKEY),IOKEY                                            
         GOTOR VDATAMGR,PARM,DMRDSQ,CTFILE,IOAREA,IOAREA                        
         B     XIT                                                              
         EJECT                                                                  
FF       EQU   X'FF'                                                            
                                                                                
         LTORG                                                                  
                                                                                
VDATAMGR DC    V(DATAMGR)          A(DATA MANAGER)                              
VDATCON  DC    V(DATCON)           A(DATCON)                                    
VHEXOUT  DC    V(HEXOUT)           A(HEXOUT)                                    
VSSB     DC    V(SSB)              A(SYSTEM STATUS BLOCK)                       
VGETFACT DC    V(GETFACT)          A(GETFACT)                                   
                                                                                
SPACES   DC    CL132' '                                                         
EFFS     DC    X'FFFFFFFFFFFFFFFF'                                              
MASKS    DC    X'8040201008040201'                                              
                                                                                
DMREAD   DC    C'DMREAD '          FOR DATA MANAGER CALLS                       
DMRDHI   DC    C'DMRDHI '                                                       
DMRDSQ   DC    C'DMRSEQ '                                                       
CTFILE   DC    C'CTFILE '                                                       
                                                                                
DDSPWD   DC    C'DDS       '       DDS PASSWORD                                 
DDSOID   DC    C'99'               DDS OFFICE ID                                
DDSDID   DC    C'999'              DDS DEPARTMENT ID                            
DDSPID   DC    C'DDS     '         DDS PERSONAL ID                              
DDSNAMES DS    0CL(L'SECNAMES)     DDS NAME                                     
DDSFNAME DC    CL(L'SECFNAME)'DONOVAN'                                          
DDSMNAME DC    CL(L'SECMNAME)'DATA'                                             
DDSLNAME DC    CL(L'SECLNAME)'SYSTEMS'                                          
DDSONAME DC    CL30'DONOVAN DATA SYSTEMS'                                       
         EJECT                                                                  
* SEACSFILE                                                                     
         PRINT OFF                                                              
       ++INCLUDE SEACSFILE                                                      
         PRINT ON                                                               
                                                                                
* DDMASTD                                                                       
         PRINT OFF                                                              
DDMASTD  DSECT                                                                  
       ++INCLUDE DDMASTC                                                        
         PRINT ON                                                               
                                                                                
* FASSB                                                                         
         PRINT OFF                                                              
       ++INCLUDE FASSB                                                          
         PRINT ON                                                               
                                                                                
* FASSBOFF                                                                      
         PRINT OFF                                                              
SSOOFFD  DSECT                                                                  
       ++INCLUDE FASSBOFF                                                       
         PRINT ON                                                               
                                                                                
* FATCB                                                                         
         PRINT OFF                                                              
       ++INCLUDE FATCB                                                          
         PRINT ON                                                               
                                                                                
* FAUTL                                                                         
         PRINT OFF                                                              
       ++INCLUDE FAUTL                                                          
         PRINT ON                                                               
         EJECT                                                                  
       ++INCLUDE FASECRETD                                                      
         EJECT                                                                  
***********************************************************************         
* LOCAL WORKING STORAGE                                               *         
***********************************************************************         
                                                                                
WORKD    DSECT                                                                  
PARM     DS    8A                                                               
                                                                                
AMERGE   DS    A                   A(MERGE ROUTINE) (USED IN GETACCS)           
AOUTPUT  DS    A                   A(OUTPUT AREA FOR MERGE ROUTINES)            
ARALST   DS    A                   A(CURRENT RALSTD ENTRY)                      
ACLALIST DS    A                   A(CLIENT CODE LIMIT ACCESS LIST)             
                                                                                
SAVPACOD DS    XL(L'SECPACOD)      SAVED SECPACOD                               
SAVEOVAL DS    XL(L'SECOVALS)      SAVED PASSED SECURITY VALUES                 
SAVEAGPE DS    XL2                 SAVED SECURITY AGENCY FOR PERSON             
                                                                                
BITTABLE DS    XL32                BIT TABLE                                    
                                                                                
FACTBITT DS    XL32                BIT TABLE RETURNED BY FINDACTS               
                                                                                
ACTN     DS    X                   SECPACTN                                     
                                                                                
INDICS   DS    X                   * INDICATORS *                               
INDIOSP  EQU   SECPOSP             OVERRIDE SYSTEM/PROGRAM                      
INDIOVAL EQU   SECPOVAL            SECOVALS BEING PASSED IN BLOCK               
INDIVALO EQU   SECPVALO            RETURN SECOVALS ONLY                         
INDIOLP  EQU   SECPOLP             OVERRIDE LINK PROGRAM                        
INDIAGY  EQU   X'01'               MAIN AGENCY ACCESS RECORD                    
                                                                                
BYTE     DS    X                                                                
                                                                                
IOKEY    DS    XL(L'SAASKEY)       IO KEY                                       
IOAREA   DS    XL2048              IO AREA                                      
                                                                                
ACCREC   DS    XL2048              AREA TO BUILD DUMMY ACCESS RECORD            
                                                                                
WORKL    EQU   *-WORKD                                                          
                                                                                
         PRINT OFF                                                              
       ++INCLUDE FAFACTS                                                        
         PRINT ON                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'010FASECRET  02/26/19'                                      
         END                                                                    
