*          DATA SET CTGENDTB   AT LEVEL 074 AS OF 03/09/99                      
*CATALP CTGENDTB                                                                
***********************************************************************         
*                                                                     *         
*         CTGENDTB --- READS CONTROL GENFILE FOR X'005C' RECORDS TO   *         
*                  --- BUILD DARE REP TABLE ON THE FLY                *         
*                                                                     *         
* ------------------------------------------------------------------- *         
* UPDATE HISTORY:                                                     *         
*                                                                     *         
* 03MAR99 (SKU) --- ORIGINATION DATE                                  *         
*                                                                     *         
***********************************************************************         
         TITLE 'CTGENDTB - DARE TABLE'                                          
***********************************************************************         
*  INPUT:                                                                       
*              PARAMETER 1 =       A(TABLE)                                     
*                        2 =       BYTE 1: C'T' TV REPS ONLY                    
*                                          C'R' RADIO REPS ONLY                 
*                                  BYTE 2: SPARE                                
*                                  BYTES 3-4: MAX LENGTH OF TABLE               
*                        3 =       A(DATAMGR)                                   
*                                                                               
*  OUTPUT:                                                                      
*              PARAMETER 1 =       BYTE 1: X'00' NO ERROR                       
*                                          X'80' TABLE MAX LEN REACHED          
*                                          X'40' ERROR ENCOUNTERED              
*                                                WHILE BULDING TABLE            
***********************************************************************         
CTGENDTB CSECT                                                                  
         PRINT GEN                                                              
         NMOD1 WORKX-WORKD,CTGENDTB                                             
         USING WORKD,RC                                                         
         MVC   ATABLE,0(R1)                                                     
         MVC   MEDIA,4(R1)                                                      
         MVC   MAXLEN,6(R1)                                                     
         MVC   DATAMGR,8(R1)                                                    
         MVC   DATADISP,=H'42'                                                  
*                                                                               
         MVI   ERRCODE,0                                                        
         BAS   RE,BUILDTAB                                                      
         MVC   DMCB(1),ERRCODE                                                  
         CLI   ERRCODE,0                                                        
         BNE   NO                                                               
*                                                                               
YES      SR    RC,RC                                                            
NO       LTR   RC,RC               SET CONDITION CODE AT EXIT                   
EXIT     XIT1                                                                   
         EJECT                                                                  
*********************************************************************           
*                                                                               
*********************************************************************           
BUILDTAB NTR1                                                                   
         L     R5,ATABLE                                                        
         USING DARETABD,R5                                                      
*                                                                               
         LA    R6,KEY                                                           
         USING DARKEYD,R6                                                       
         XC    KEY,KEY                                                          
         MVI   DARKSYS,DARKSYSQ                                                 
         MVI   DARKTYP,DARKTYPQ                                                 
         MVC   DARKMEDA,MEDIA                                                   
         DROP  R6                                                               
*                                                                               
         GOTO1 DATAMGR,DMCB,(0,=C'DMHIGH'),=C'GENDIR',KEYSAVE,KEY               
         CLI   DMCB+8,0                                                         
         BNE   BERRX                                                            
*                                                                               
BTAB05   DS    0H                                                               
         CLC   KEY(19),KEYSAVE                                                  
         BNE   BTABX                                                            
*                                                                               
         GOTO1 DATAMGR,DMCB,(0,=C'GETREC'),=C'GENFIL',KEY+36,IOAREA,   +        
               DMWORK                                                           
         CLI   DMCB+8,0                                                         
         BNE   BERRX                                                            
*                                                                               
         LA    R6,IOAREA                                                        
         USING DARKEYD,R6                                                       
         MVC   DTABREP,DARKREP                                                  
         DROP  R6                                                               
*                                                                               
         MVI   ELCODE,X'10'                                                     
         BAS   RE,GETEL                                                         
         BNE   BERRX                                                            
*                                                                               
         USING DARREPD,R6                                                       
         MVC   DTABNAME,DARREPNM                                                
         MVC   DTABFLAG,DARREPFG                                                
         MVC   DTABPFIX,DARREPFX                                                
*                                                                               
* GET LENGTH OF PREFIX                                                          
*                                                                               
         LA    R3,9                                                             
         LA    R4,DARREPFX+9                                                    
*                                                                               
BTAB10   DS    0H                                                               
         OC    0(1,R4),0(R4)                                                    
         BNZ   BTAB20                                                           
         AHI   R4,-1                                                            
         BCT   R3,BTAB10                                                        
*                                                                               
BTAB20   DS    0H                                                               
         AHI   R3,1                                                             
         STC   R3,DTABLEN                                                       
*                                  GET NEXT TABLE ENTRY                         
         GOTO1 DATAMGR,DMCB,(0,=C'DMSEQ'),=C'GENDIR',KEYSAVE,KEY                
         CLI   DMCB+8,0                                                         
         BNE   BERRX                                                            
*                                                                               
         AHI   R5,DTABLENQ         ADVANCE TABLE                                
         B     BTAB05                                                           
*                                                                               
BERRX    DS    0H                                                               
         MVI   ERRCODE,X'40'       ERROR ENCOUNTERED                            
*                                                                               
BTABX    DS    0H                                                               
         MVI   0(R5),X'FF'         TABLE TERMINATOR                             
         B     EXIT                                                             
         DROP  R5,R6                                                            
         EJECT                                                                  
         GETEL R6,DATADISP,ELCODE                                               
         EJECT                                                                  
WORKD    DSECT                                                                  
DMWORK   DS    12D                                                              
KEY      DS    CL40                                                             
KEYSAVE  DS    CL40                                                             
DMCB     DS    6F                                                               
DUB      DS    D                                                                
MEDIA    DS    C                                                                
ATABLE DS      A                                                                
DATAMGR  DS    A                                                                
MAXLEN   DS    XL2                                                              
DATADISP DS    H                                                                
ELCODE   DS    X                                                                
WORK     DS    CL17                FOR EDIT                                     
ELEM     DS    XL256                                                            
ERRCODE  DS    X                                                                
IOAREA   DS    CL2000                                                           
WORKX    EQU   *                                                                
*                                                                               
DARETABD DSECT                                                                  
DTABREP  DS    CL3                                                              
DTABNAME DS    CL10                                                             
DTABFLAG DS    X                                                                
DTABLEN  DS    X                                                                
DTABPFIX DS    CL10                                                             
DTABLENQ EQU   *-DARETABD                                                       
*                                                                               
       ++INCLUDE CTGENDTBD                                                      
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'074CTGENDTB  03/09/99'                                      
         END                                                                    
