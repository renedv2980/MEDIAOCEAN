*          DATA SET DRIVPUTA   AT LEVEL 003 AS OF 05/01/02                      
*          DATA SET DRIVPUT    AT LEVEL 005 AS OF 05/18/93                      
*CATALP DRIVPUTA                                                                
         TITLE 'DRIVPUT - STACK INPUT RECORDS AND PUT TO SORT'                  
DRIVPUT  CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 0,**DPUT**                                                       
         L     RA,0(R1)            PARAMETER 1 A(GLOBAL)                        
         USING GLOBALD,RA                                                       
*                                                                               
         CLI   GLPUTMAX,0          TEST STACK SIZE OVERRIDE                     
         BE    DP1                                                              
         LA    RE,RECMAX                                                        
         CLI   GLPUTMAX,RECMAX                                                  
         BH    *+8                                                              
         IC    RE,GLPUTMAX                                                      
         ST    RE,PUTMAX                                                        
*                                                                               
DP1      L     R2,4(R1)            PARAMETER 2 A(RECORD)                        
         LTR   R2,R2                           ZERO = END OF INPUT              
         BNZ   DP2                                                              
         BAS   RE,EMPTY                        SO CLEAR BUFFER                  
******   EDIT  (P8,COUNT),(10,P+15)                                             
******   GOTO1 PRINT,DMCB,P,=C'BL01'                                            
******   ZAP   COUNT,=P'0'                                                      
         B     XIT                                                              
         EJECT                                                                  
*              CHECK FOR THIS RECORD IN STACK                                   
         SPACE 3                                                                
DP2      ZIC   R9,0(R2)            FIRST BYTE OF RECORD = REC NO                
         BCTR  R9,0                                                             
         SLL   R9,2                                                             
         LA    R9,GLAINTD(R9)      USE THIS TO GET TO GLINT                     
         L     R9,0(R9)                                                         
         USING GLINTD,R9                                                        
         MVI   ISITDET,C'N'        CHECK IF THIS IS A DETAIL                    
         CLC   GLDETLEV,1(R2)                                                   
         BNE   *+8                                                              
         MVI   ISITDET,C'Y'                                                     
         LH    R8,GLKEYLEN                                                      
         AH    R8,GLCONLEN                                                      
         BCTR  R8,0                R8=(L'KEY-1)                                 
         SPACE 1                                                                
DP4      LA    R3,PUTDATA                                                       
         L     R0,PUTMAX                                                        
         SPACE 1                                                                
DP6      CLI   0(R3),0             END OF BLOCK?                                
         BNZ   DP10                                                             
         LH    R1,GLRECLEN                                                      
         MOVE  ((R3),(R1)),(R2)    MOVE IN DRIVER RECORD                        
         B     XIT                 AND WE'RE THROUGH                            
         SPACE 1                                                                
DP10     EX    R8,DPCOMP           DOES RECORD THERE MATCH                      
         BNE   DP12                                                             
*                                  YES - SO ADD TO PREVIOUS                     
         GOTO1 =V(DRIVADD),DMCB,(RA),(R3),(R2)                                  
         CLI   DMCB,1              IF DRIVADD SETS RECORD INACTIVE              
         BNE   XIT                                                              
         CLI   ISITDET,C'Y'        AND IT IS A DETAIL                           
         BNE   XIT                                                              
         MVI   0(R3),0             'REMOVE' IT                                  
         B     XIT                                                              
         SPACE 1                                                                
DP12     AH    R3,=H'4096'                                                      
         BCT   R0,DP6                                                           
         BAS   RE,EMPTY            NO - SO EMPTY STACK                          
         B     DP4                         AND TRY AGAIN!                       
         SPACE 1                                                                
DPCOMP   CLC   0(0,R3),0(R2)       DOES IT HAVE SAME CONTROL & KEY              
         EJECT                                                                  
*              ROUTINE TO EMPTY AND CLEAR STACK                                 
         SPACE 3                                                                
EMPTY    NTR1                                                                   
         LA    R3,PUTDATA                                                       
         L     R0,PUTMAX                                                        
         SPACE 1                                                                
EMPTY2   CLI   0(R3),0                                                          
         BE    EMPTY4                                                           
         CLI   GLANYSRT,C'N'       IF THIS IS THE FIRST PUT                     
         BNE   EMPTY3                                                           
         MVI   GLANYSRT,C'Y'                                                    
         L     R1,=V(SORTCARD)     INITIALIZE SORTER                            
         ST    R1,DMCB                                                          
         L     R1,=V(RECCARD)                                                   
         ST    R1,DMCB+4                                                        
         GOTO1 SORTER,DMCB,,,0                                                  
         CLI   TRACOPT,C'Y'                                                     
         BNE   EMPTY3                                                           
         L     R1,=V(SORTCARD)                                                  
         MVC   P(8),=C'SORTCARD'                                                
         MVC   P+10(80),0(R1)                                                   
         GOTO1 PRINT,DMCB,P-1,=C'BL01'                                          
         L     R1,=V(RECCARD)                                                   
         MVC   P(7),=C'RECCARD'                                                 
         MVC   P+10(80),0(R1)                                                   
         GOTO1 PRINT,DMCB,P-1,=C'BL01'                                          
         SPACE 1                                                                
EMPTY3   CLI   0(R3),0                                                          
         BNE   *+6                                                              
         DC    H'0'                                                             
         GOTO1 SORTER,DMCB,=C'PUT',(R3)                                         
******   AP    COUNT,=P'1'                                                      
         MVI   0(R3),0                                                          
         SPACE 1                                                                
EMPTY4   AH    R3,=H'4096'                                                      
         BCT   R0,EMPTY2                                                        
         SPACE 1                                                                
XIT      XIT1                                                                   
         SPACE 1                                                                
SKIP     DS    0H                                                               
         GOTO1 SORTER,DMCB,=C'PUT',(R2)                                         
******   AP    COUNT,=P'1'                                                      
         B     XIT                                                              
         EJECT                                                                  
*              LTORG AND RECORD STACK                                           
         SPACE 3                                                                
         LTORG                                                                  
         SPACE 1                                                                
ISITDET  DS    CL1                                                              
P        DC    CL133' '                                                         
COUNT    DC    PL8'0'                                                           
PUTMAX   DC    F'10'                                                            
PUTDATA  DC    (8+(4096*RECMAX))X'00'                                           
RECMAX   EQU   20                                                               
         PRINT OFF                                                              
       ++INCLUDE DRGLOBAL                                                       
       ++INCLUDE DRLOCAL                                                        
       ++INCLUDE DRINTRECD2                                                     
       ++INCLUDE DRIVETABLE                                                     
         PRINT ON                                                               
         SPACE 1                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'003DRIVPUTA  05/01/02'                                      
         END                                                                    
