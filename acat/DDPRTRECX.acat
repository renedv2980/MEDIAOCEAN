*          DATA SET DDPRTRECX  AT LEVEL 002 AS OF 05/23/00                      
*CATALP PRTREC                                                                  
***********************************************************************         
* ROUTINE TO PRINT OUT A RECORD                                       *         
* NTRY:  P1 B0   TYPE OF FORMATTING (E=ELEM, R=REC) E=DEFAULT         *         
*        P1 B1-3 A(RECORD)                                            *         
*        P2 B0   DISP TO FIRST ELEMENT                                *         
*           B1-3 DISP TO RECORD LENGTH                                *         
*        P3 B0-3 A(PRINT)                                             *         
*        P4 B0-3 A(HEXOUT)                                            *         
***********************************************************************         
         TITLE 'DUMP OUT A RECORD TO PRINT'                                     
         SPACE 1                                                                
PRTREC   CSECT                                                                  
         NMOD1 WORKL,*PRTREC*,CLEAR=YES                                         
         USING WORKD,RC                                                         
         MVC   IPARMS,0(R1)                                                     
         MVC   P,SPACES                                                         
*                                                                               
         L     RF,IREC             GET RECORD LENGTH                            
         AH    RF,IDSPLN                                                        
         XR    R0,R0                                                            
         ICM   R0,3,0(RF)                                                       
         BNZ   *+6                                                              
         DC    H'0'                CHECK PARAMETERS PASSED                      
         STH   R0,RECLEN                                                        
*                                                                               
         CLI   ITYPE,C'R'          SET DEFAULT TYPE                             
         BE    *+8                                                              
         MVI   ITYPE,C'E'                                                       
         CLI   ITYPE,C'R'          JUST DUMP RECORD?                            
         BE    DMPR08              YES                                          
*                                                                               
         L     R2,IREC             CHECK ELEMENTS ARE INTACT                    
         LA    R2,0(R2)                                                         
         XR    RF,RF                                                            
         IC    RF,IDSP1                                                         
         AR    R2,RF                                                            
         L     R3,IREC                                                          
         LA    R3,0(R3)                                                         
         AH    R3,RECLEN           R3=A(END OF RECORD)                          
*                                                                               
         XR    RF,RF                                                            
DMPR02   CLI   0(R2),0             END OF RECORD                                
         BE    DMPR04                                                           
         ICM   RF,1,1(R2)          PICK UP ELEMENT LENGTH                       
         BZ    DMPR06                                                           
         AR    R2,RF                                                            
         CR    R2,R3               SEE IF PAST END OF RECORD                    
         BH    DMPR06                                                           
         B     DMPR02                                                           
*                                                                               
DMPR04   CR    R2,R3               ALLOW FOR 0 AT END AS PART OF LENGTH         
         BE    DMPR08                                                           
         CLI   0(R2),0                                                          
         BNE   DMPR06                                                           
         AHI   R2,1                                                             
         CR    R2,R3                                                            
         BE    DMPR08                                                           
*                                                                               
DMPR06   MVC   P(L'ERRORT),ERRORT                                               
         MVC   P+L'ERRORT(L'BADELEM),BADELEM                                    
         BRAS  RE,PRINT                                                         
         MVI   ITYPE,C'R'                                                       
*                                                                               
DMPR08   CLI   ITYPE,C'R'                                                       
         BNE   DMPR10                                                           
         BRAS  RE,DMPHEX                                                        
         B     EXIT                                                             
*                                                                               
DMPR10   BRAS  RE,DMPELM                                                        
         B     EXIT                                                             
*                                                                               
EXIT     XIT1  ,                                                                
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO HEX DUMP RECORD                                          *         
***********************************************************************         
         SPACE 1                                                                
DMPHEX   NTR1  ,                                                                
         L     R2,IREC                                                          
         LH    R0,RECLEN                                                        
         XR    R3,R3                                                            
*                                                                               
DMPH02   LTR   R4,R0                                                            
         BNP   EXIT                                                             
         CHI   R4,L'WORK                                                        
         BL    *+8                                                              
         LHI   R4,L'WORK                                                        
*                                                                               
         CVD   R3,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  P(4),DUB                                                         
*                                                                               
         LR    RF,R4                                                            
         BCTR  RF,0                                                             
         EX    RF,*+4                                                           
         MVC   WORK(0),0(R2)                                                    
*                                                                               
         GOTO1 IHEXOUT,DMCB,WORK,WORKH,(R4),0                                   
*                                                                               
         LR    RF,R4               MOVE HEX OUTPUT TO P+6                       
         SLL   RF,1                                                             
         BCTR  RF,0                                                             
         EX    RF,*+4                                                           
         MVC   P+6(0),WORKH                                                     
*                                                                               
         TR    WORK,TRTAB          MOVE TRANSLATED CHARACTERS                   
         LR    RF,R4                                                            
         BCTR  RF,0                                                             
         EX    RF,*+4                                                           
         MVC   P+8+L'WORKH(0),WORK                                              
*                                                                               
         BRAS  RE,PRINT            PRINT LINE                                   
*                                                                               
         AR    R2,R4                                                            
         AR    R3,R4                                                            
         SR    R0,R4                                                            
         B     DMPH02                                                           
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO HEX DUMP RECORD AS ELEMENTS                              *         
***********************************************************************         
         SPACE 1                                                                
DMPELM   NTR1  ,                                                                
         L     R2,IREC                                                          
         LA    R2,0(R2)                                                         
         XR    R0,R0                                                            
         IC    R0,IDSP1                                                         
         XR    R3,R3                                                            
         BRAS  RE,WRTEL            OUTPUT ALL DIRECTORY INFO                    
*                                                                               
         XR    R3,R3                                                            
         IC    R3,IDSP1                                                         
         AR    R2,R3                                                            
         XR    R0,R0                                                            
*                                                                               
DMPEL02  CLI   0(R2),0                                                          
         BE    EXIT                                                             
         CH    R3,RECLEN                                                        
         BH    EXIT                                                             
*                                                                               
         IC    R0,1(R2)                                                         
         BRAS  RE,WRTEL                                                         
*                                                                               
         AR    R2,R0                                                            
         AR    R3,R0                                                            
         B     DMPEL02                                                          
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO PUT OUT AN ELEMENT FOR LENGTH                            *         
* NTRY: R2= A(ELEMENT)                                                *         
*       R0= LENGTH                                                    *         
*       R3= DISPLACEMENT                                              *         
***********************************************************************         
         SPACE 1                                                                
WRTEL    NTR1  ,                                                                
         CVD   R3,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  P(4),DUB                                                         
*                                                                               
WRTE02   LTR   R4,R0                                                            
         BNP   EXIT                                                             
         CHI   R4,L'WORK                                                        
         BL    *+8                                                              
         LHI   R4,L'WORK                                                        
*                                                                               
         LR    RF,R4                                                            
         BCTR  RF,0                                                             
         EX    RF,*+4                                                           
         MVC   WORK(0),0(R2)                                                    
         GOTO1 IHEXOUT,DMCB,WORK,WORKH,(R4),0                                   
*                                                                               
         TR    WORK,TRTAB          MOVE TRANSLATED CHARACTERS                   
         LR    RF,R4                                                            
         BCTR  RF,0                                                             
         EX    RF,*+4                                                           
         MVC   P+8+L'WORKH(0),WORK                                              
*                                                                               
         LR    RF,R4               MOVE HEX OUTPUT                              
         SLL   RF,1                                                             
         BCTR  RF,0                                                             
         EX    RF,*+4                                                           
         MVC   P+6(0),WORKH                                                     
         BRAS  RE,PRINT                                                         
*                                                                               
         AR    R2,R4                                                            
         SR    R0,R4                                                            
         B     WRTE02                                                           
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO DO PRINTING OF A LINE                                    *         
***********************************************************************         
         SPACE 1                                                                
PRINT    NTR1  ,                                                                
         GOTO1 IPRINT,PCB,PLINE,=C'BL01'                                        
         MVC   P,SPACES                                                         
         J     EXIT                                                             
         EJECT                                                                  
***********************************************************************         
* LITERALS AND CONSTANTS                                              *         
***********************************************************************         
         SPACE 1                                                                
         LTORG                                                                  
*                                                                               
SPACES   DC    132C' '                                                          
*                                                                               
ERRORT   DC    CL8'*ERROR* '                                                    
BADELEM  DC    C'Element structure invalid - hex dump follows'                  
*                                                                               
TRTAB    DC    XL16'4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B' 00-0F                     
         DC    XL16'4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B' 10-1F                     
         DC    XL16'4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B' 20-2F                     
         DC    XL16'4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B' 30-3F                     
         DC    XL16'4B4B4B4B4B4B4B4B4B4B4A4B4C4D4E4F' 4B-4F                     
         DC    XL16'4B4B4B4B4B4B4B4B4B4B5A5B5C5D5E5F' 50-5F                     
         DC    XL16'60614B4B4B4B4B4B4B4B4B4B6C6D6E6F' 60-6F                     
         DC    XL16'4B4B4B4B4B4B4B4B4B797A7B7C7D7E7F' 70-7F                     
         DC    XL16'4B8182838485868788894B4B4B4B4B4B' 80-8F                     
         DC    XL16'4B9192939495969798994B4B4B4B4B4B' 90-9F                     
         DC    XL16'4BA1A2A3A4A5A6A7A8A94B4B4B4B4B4B' A0-AF                     
         DC    XL16'4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B' B0-BF                     
         DC    XL16'4BC1C2C3C4C5C6C7C8C94B4B4B4B4B4B' C0-CF                     
         DC    XL16'D0D1D2D3D4D5D6D7D8D94B4B4B4B4B4B' D0-DF                     
         DC    XL16'4B4BE2E3E4E5E6E7E8E94B4B4B4B4B4B' E0-EF                     
         DC    XL16'F0F1F2F3F4F5F6F7F8F94B4B4B4B4B4B' F0-FF                     
         EJECT                                                                  
***********************************************************************         
* WORKING STORAGE DSECT                                               *         
***********************************************************************         
         SPACE 1                                                                
WORKD    DSECT                                                                  
DUB      DS    D                                                                
DMCB     DS    6F                                                               
IPARMS   DS    0XL16                                                            
ITYPE    DS    0X                                                               
IREC     DS    A                                                                
IDSP1    DS    X                                                                
         DS    X                                                                
IDSPLN   DS    H                                                                
IPRINT   DS    A                                                                
IHEXOUT  DS    A                                                                
*                                                                               
PCB      DS    4F                                                               
*                                                                               
RECLEN   DS    H                                                                
WORK     DS    XL32                                                             
WORKH    DS    XL64                                                             
*                                                                               
PLINE    DS    0CL133                                                           
PH       DS    X                                                                
P        DS    CL132                                                            
*                                                                               
WORKL    EQU   *-WORKD                                                          
         EJECT                                                                  
***********************************************************************         
* OTHER INCLUDED DSECTS                                               *         
***********************************************************************         
         SPACE 1                                                                
         PRINT OFF                                                              
         PRINT ON                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'002DDPRTRECX 05/23/00'                                      
         END                                                                    
