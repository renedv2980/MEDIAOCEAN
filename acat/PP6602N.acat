*          DATA SET PP6602N    AT LEVEL 046 AS OF 05/01/02                      
*CATALP PP6602N                                                                 
         TITLE 'PP6602 - CROSS AGY REPORT - CONTROL '                           
*                                                                               
*        QOPT2 Y= SHOW ONLY CONTRACTS WHOSE END MTH MATCHES REQ END MTH         
*        QOPT3 Y= SUMMARIES ONLY                                                
*        QOPT4 1=PRD,A=+SPACE          SORT OPTION                              
*              2=CUST,B=+SPACE                                                  
*              3=PRD+CUST,C=+SPACE                                              
*              S=SPACE                                                          
*              BLANK= INSERTION DATE                                            
*        QOPT6 A=AUDIT,F=FORECAST                                               
*        QOPT7 ADFILE INFO 1,2,A,B                                              
*        QPUB+1 ASPO NUMBER                                                     
*                                                                               
PP6602   CSECT                                                                  
         NMOD1 0,PP6602,RR=R9                                                   
*                                                                               
         ST    R9,RELO                                                          
         B     *+8                                                              
RELO     DC    F'0'                                                             
*                                                                               
         L     RA,0(R1)                                                         
         USING PPWORKD,RA                                                       
         L     RC,PPFILEC                                                       
         LA    R9,1(RC)                                                         
         LA    R9,4095(R9)                                                      
         USING PPFILED,RC,R9                                                    
         LA    R8,SPACEND                                                       
         USING PP66WRKD,R8                                                      
         LA    R7,PP6602+4095                                                   
         LA    R7,1(R7)                                                         
         USING PP6602+4096,R7      **NOTE SECOND BASE REG**                     
         EJECT                                                                  
         MVC   SPPGMODE,MODE       SAVE PPG'S MODE                              
         CLI   MODE,UNSRTREC                                                    
         BNE   EXIT                                                             
         MVC   SAVPARS,DMCB+4                                                   
         LM    R2,R3,SAVPARS                                                    
         B     TLBRTAB(R2)                                                      
*                                                                               
TLBRTAB  B     TLFIRST                                                          
         B     TLINPUT                                                          
         B     TLOUTPUT                                                         
         B     TLLAST                                                           
         SPACE 3                                                                
TLFIRST  DS    0H                                                               
*                                                                               
         CLC   QPRODUCT,=C'ALL'                                                 
         BNE   *+10                                                             
         MVC   QPRODUCT,SPACES                                                  
         MVI   CONSW,0                                                          
         MVI   BUDSW,0                                                          
         MVI   LINE,200            FORCE NEW PAGE                               
         MVC   PAGE,=H'1'                                                       
         XC    SAVPUBN1,SAVPUBN1                                                
         XC    SAVPUBN2,SAVPUBN2                                                
         XC    SAVPUBN3,SAVPUBN3                                                
         MVC   SPAGE,=H'1'                                                      
         MVC   RPAGE,=H'1'                                                      
         MVI   PLISREC,0           CLEAR COMMENT REC                            
         MVI   ESTACTIV,0          SET NO ACTIVITY FOR REQUEST                  
         MVI   PRNTSW,0                                                         
         MVI   FCRDACTV,C'N'                                                    
         CLC   QPRODUCT,SPACES                                                  
         BNE   *+8                                                              
         MVI   FCRDACTV,C'Y'       NO PASSIVE POINTERS                          
         OI    DMINBTS,X'08'       SET TO PASS DELETES                          
         OI    DMOUTBTS,X'FD'                                                   
         MVI   WFREGSW,0                                                        
         MVI   WFDSTSW,0                                                        
         MVI   RDPACT,0                                                         
         XC    EDITCNT,EDITCNT                                                  
         XC    PBRCNT,PBRCNT                                                    
         MVI   COMSW,0                                                          
         MVI   ESCTL,C'0'                                                       
         MVC   ESCTL+1(L'ESCTL-1),ESCTL                                         
         MVC   SAVEMAX,MAXLINES                                                 
         MVC   P,SPACES                                                         
         MVC   PSECOND,SPACES                                                   
         MVI   DASHES,C'-'                                                      
         MVC   DASHES+1(L'DASHES-1),DASHES                                      
         MVC   WORK(2),RCDATE+6                                                 
         MVC   WORK+2(2),RCDATE                                                 
         MVC   WORK+4(2),RCDATE+3                                               
         GOTO1 DTCNV,DMCB,WORK,(1,BTODAY)                                       
*                                                                               
* RELOCATE BRAND ACCUM ADDRESSES                                                
         LA    R0,(ACONX-ACONS)/4       NUMBER TO RELOCATE                      
         LA    R1,ACONS                                                         
         LA    R2,APUBTOT                                                       
ESRELO2  L     R3,0(R1)                                                         
         A     R3,RELO                                                          
         ST    R3,0(R2)                                                         
         LA    R1,4(R1)                                                         
         LA    R2,4(R2)                                                         
         BCT   R0,ESRELO2                                                       
*                                                                               
*                                                                               
         XC    LASTCKEY,LASTCKEY   ASSURE NO MATCH                              
*                             CLEAR ALL ACCUMS                                  
         L     RE,APUBTOT                                                       
         L     RF,=V(PP66ALLX)                                                  
         A     RF,RELO                                                          
         SR    RF,RE                                                            
         XCEF                                                                   
*                                                                               
         MVC   ACURKLN,AKLINES                                                  
         MVC   ACURILN,AILINES                                                  
*                                                                               
*                                                                               
         MVI   FORCEHED,C'Y'                                                    
         MVC   SVASPO,SPACES                                                    
         CLI   QPUB,C' '           SEE IF DOING ONE PUB                         
         BNE   TLIN1                                                            
         CLI   QPUB+1,C' '                                                      
         BE    TLIN1         ALL PUBS                                           
         CLC   QPUB+1(3),=C'ALL'                                                
         BE    TLIN1                                                            
         MVC   SVASPO(9),QPUB+1                                                 
*                                                                               
TLIN1    DS    0H                                                               
*                                       FIRST TIME                              
         XC    BSTART,BSTART                                                    
         MVC   BEND,=3X'FF'                                                     
         CLI   QSTART,C' '                                                      
         BE    TLIN2                                                            
         GOTO1 DTCNV,DMCB,QSTART,(1,BSTART)                                     
*                                                                               
TLIN2    DS    0H                                                               
         CLI   QEND,C' '                                                        
         BE    TLIN4                                                            
         GOTO1 DTCNV,DMCB,QEND,(1,BEND)                                         
*                                                                               
TLIN4    DS    0H                                                               
         MVC   SAVPARS+4(2),=C'96'                                              
         MVC   SAVPARS+6(2),=C'79'                                              
         XC    KEY,KEY                                                          
         XC    PBUYREC,PBUYREC                                                  
         XC    SAVEKEY,SAVEKEY                                                  
*                                                                               
TLIN5    DS    0H                                                               
         CLI   QPUB,C' '           SEE IF DOING ONE PUB                         
         BE    EXIT                                                             
         XC    LNKPUBS,LNKPUBS         BUILD LIST OF PUB LINKS                  
         XC    KEY,KEY                                                          
         MVC   KEY(1),QMEDIA                                                    
         PACK  KEY+1(6),QPUB(11)                                                
         MVC   KEY+6(1),QPUB+10                                                 
         CLI   QPUB+10,C' '                                                     
         BNE   *+8                                                              
         MVI   KEY+6,0                                                          
         MVC   KEY+7(2),=C'BD'         GO READ BD PUB TO GET LNKS               
         MVI   KEY+9,X'81'                                                      
         BAS   RE,READPUB                                                       
         BAS   RE,GETP                                                          
         MVC   LNKPUBS(2),=C'BD'                                                
         MVC   LNKPUBS+2(6),KEY+1          FIRST ENTRY IS BD PUB                
         LA    R5,LNKPUBS+9         SET R5 TO NEXT ENTRY                        
*                                                                               
         LA    R2,PUBREC+33                                                     
         MVI   ELCODE,X'14'                                                     
TLIN5C   BAS   RE,NEXTEL                                                        
         BNE   TLIN5X                                                           
         CLI   2(R2),X'FF'                                                      
         BNE   TLIN5C                                                           
         MVC   0(2,R5),3(R2)                                                    
         MVC   2(6,R5),17(R2)                                                   
         LA    R5,9(R5)                                                         
         B     TLIN5C                                                           
*                                                                               
TLIN5X   MVC   0(5,R5),=5X'FF'                                                  
         B     EXIT                                                             
         SPACE 3                                                                
TLINPUT  DS    0H                                                               
         BAS   RE,TLIN                                                          
         B     EXIT                                                             
         SPACE 3                                                                
TLOUTPUT DS    0H                                                               
         BAS   RE,TLOUT                                                         
         B     EXIT                                                             
         SPACE 3                                                                
TLLAST   DS    0H                                                               
         CLI   ESTACTIV,0          TEST ANY ACTIVITY FOR REQ                    
         BNE   TLLST1                                                           
         MVI   SKIPSPEC,C'Y'                                                    
         MVI   PRNTSW,C'S'                                                      
         MVC   P,SPACES                                                         
         MVC   PSECOND,SPACES                                                   
         MVC   P(80),QRECORD                                                    
         MVC   PSECOND(17),=C'NO DATA GENERATED'                                
         GOTO1 REPORT                                                           
*                                                                               
         B     EXIT                                                             
*                                                                               
TLLST1   DS    0H                                                               
         MVI   PBUYREC,X'FF'                                                    
         MVC   PBUYREC+1(24),PBUYREC                                            
         MVC   PBDJOB,=6X'FF'                                                   
         BAS   RE,TLOUT                                                         
         B     EXIT                                                             
EXIT     DS    0H                                                               
         MVC   MODE,SPPGMODE       RESTORE PPG'S MODE                           
         MVC   DMCB+4(8),SAVPARS                                                
XIT      DS    0H                                                               
         XIT1                                                                   
         EJECT                                                                  
TLIN     NTR1                                                                   
TL0      OC    KEY,KEY                                                          
         BNZ   TL10                                                             
         MVC   KEY,SAVEKEY                                                      
         OC    KEY,KEY                                                          
         BNZ   TL3                                                              
         MVC   KEY(3),RCSVAGY                                                   
         MVI   KEY+3,X'02'                                                      
         CLC   QCLIENT,=C'ALL'                                                  
         BE    TL2                                                              
         MVC   KEY+4(3),QCLIENT                                                 
         B     TL2                                                              
*                                                                               
TL1      CLC   KEYSAVE(4),KEY                                                   
         BNE   TLEND               LAST CLIENT (AGY)                            
         CLC   KEYSAVE(7),KEY                                                   
         BE    TL6                                                              
         CLC   QCLIENT,=C'ALL'                                                  
         BE    TL6                                                              
         B     TL4                                                              
TL2      BAS   RE,HIGH                                                          
         B     TL1                                                              
*                                                                               
TL3      BAS   RE,HIGH                                                          
TL4      BAS   RE,SEQ                                                           
         B     TL1                                                              
*                                                                               
*              PROCESS THIS CLIENT (AGY)                                        
*                                                                               
TL6      TM    KEY+25,X'80'          NO DELETED CLTHDRS                         
         BNZ   TL4                                                              
         BAS   RE,GETCLI                                                        
         CLC   PCLTAGYR(3),=3X'00'                                              
         BE    TL4                 BYPASS THIS CLIENT                           
         MVC   SAVEKEY,KEY         SAVE THIS KEY                                
         XC    KEY,KEY             READ BUYS                                    
         MVC   KEY(2),PCLTKCLT                                                  
         MVC   KEY+2(1),PCLTKMED                                                
         MVI   KEY+3,X'20'                                                      
         CLI   QPUB,C' '           SEE IF DOING ONE PUB                         
         BE    *+8                                                              
         MVI   KEY+3,X'21'         ONE PUB SO USE 21 POINTER                    
         MVC   KEY+4(2),QAGENCY                                                 
         MVI   KEY+6,C' '                                                       
         CLI   QPUB,C' '           SEE IF DOING ONE PUB                         
         BE    TL6K                NO - GO SEE IF ONE PRD                       
*                                                                               
*                                  ONE PUB                                      
*                                                                               
         MVC   KEY+7(6),LNKPUBS+2         BD PUB                                
         TM    PCLTACTL,X'80'         SEE IF CONVERSION REQUIRED                
         BZ    TL6G                NO GO SEE IF DOING ONE PRD                   
*                                                                               
         LA    R5,LNKPUBS                                                       
TL6C     CLC   0(2,R5),PCLTKCLT        REALLY DP AGY CODE                       
         BE    TL6E                                                             
         CLC   0(5,R5),=5X'FF'                                                  
         BE    TL90                GO DO NEXT CLT (AGY)                         
         LA    R5,9(R5)                                                         
         B     TL6C                                                             
*                                                                               
TL6E     MVC   KEY+7(6),2(R5)                                                   
TL6G     CLC   QPRODUCT,SPACES                                                  
         BE    TL6X                                                             
         MVC   KEY+13(3),QPRODUCT                                               
         B     TL6X                                                             
*                                                                               
TL6K     DS    0H                  ALL PUBS                                     
         CLC   QPRODUCT,SPACES                                                  
         BE    TL6X                                                             
         MVC   KEY+7(3),QPRODUCT                                                
         B     TL6X                                                             
*                                                                               
TL6X     DS    0H                                                               
         BAS   RE,HIGH                                                          
         B     TL10A                                                            
*                                                                               
TL10     BAS   RE,SEQ                                                           
*                                                                               
TL10A    DS    0H                                                               
         CLI   KEY+25,X'FF'        DELETES                                      
         BE    TL10                                                             
*                                                                               
         CLC   KEY(6),KEYSAVE      ONLY CHECK FIRST 2 CHARS OF CLT              
*                                                                               
         BNE   TL90                END OF A/M CLT                               
         OC    KEY+21(3),KEY+21    PASSIVE POINTER                              
         BNZ   TL10                                                             
*                                                                               
TL11     CLI   QPUB,C' '           SEE IF DOING ONE PUB                         
         BE    TL11C               NO GO SEE IF DOING ONE PRODUCT               
         CLC   KEY+7(6),KEYSAVE+7                                               
         BNE   TL90                                                             
         CLC   QPRODUCT,SPACES                                                  
         BE    TL11A                                                            
         CLC   KEY+13(3),QPRODUCT  ONE PRODUCT - WILL PROCESS *PRD              
         BNE   TL90                                                             
         B     TL12                                                             
*                                                                               
TL11A    CLI   KEY+13,C'*'         FOR ALL PRD REQ SKIP *PRDS                   
         BE    TL10                                                             
         B     TL12                                                             
*                                                                               
TL11C    CLC   QPRODUCT,SPACES     ALL PUBS                                     
         BE    TL11E                                                            
         CLC   KEY+7(3),QPRODUCT   ONE PRD - PROCESS *PRDS                      
         BNE   TL90                                                             
         B     TL12                                                             
*                                                                               
TL11E    CLI   KEY+7,C'*'         FOR ALL PRD REQ SKIP *PRDS                    
         BE    TL10                                                             
         B     TL12                                                             
*                                                                               
*                                                                               
TL12     CLC   BSTART,KEY+16                                                    
         BH    TL10                                                             
         CLC   BEND,KEY+16                                                      
         BL    TL10                                                             
*                                                                               
TL14     DS    0H                                                               
         BAS   RE,GETBUY                                                        
*****                                                                           
         CLI   PBDBFD,C'Y'                                                      
         BE    TL10                                                             
*****                                                                           
         TM    PBUYCNTL,X'80'      DELETED - SEE IF IT WAS BILLED               
         BNZ   TL15                                                             
         CLI   QOPT6,C'A'       NOT AN AUDIT SO PASS ALL BUYS                   
         BNE   TL17                                                             
TL15     LA    R2,PBUYREC+33                                                    
         MVI   ELCODE,X'26'                                                     
         USING PPDUMD02,R2                                                      
TL16     BAS   RE,NEXTEL                                                        
         BNE   TL10                                                             
         OC    PBLDATE,PBLDATE     SEE IF BILLED                                
         BZ    TL16                                                             
*                                                                               
TL17     DS    0H                                                               
*                                                                               
TL18     DS    0H                                                               
         L     R3,SAVPARS+4                                                     
         USING SORTRECD,R3                                                      
         XC    SORTREC,SORTREC                                                  
         BAS   RE,GETASPO                                                       
         MVC   SAVEASPO,WORK                                                    
         CLI   QPUB,C' '           SEE IF DOING ONE PUB                         
         BNE   TL18A                                                            
         CLI   QPUB+1,C' '                                                      
         BE    TL18A         ALL PUBS                                           
         CLC   QPUB+1(3),=C'ALL'                                                
         BNE   TL181                                                            
         MVC   SORTASPO,WORK                                                    
         B     TL18A                                                            
*                                                                               
TL181    CLC   SVASPO,WORK         DOING ONE ASPO                               
         BNE   TL10                                                             
         MVC   SORTASPO,WORK                                                    
*                                                                               
TL18A    MVC   SORTPUB+20(6),PBUYKPUB                                           
         TM    PCLTACTL,X'80'      SEE IF CONVERSION REQUIRED                   
         BZ    TL18E               NO                                           
         CLC   OLDPUB,SORTPUB+20                                                
         BE    TL18D                                                            
         MVC   OLDPUB,SORTPUB+20                                                
         MVC   OLDCPUB,SORTPUB+20                                               
         MVC   SVKEY,KEY                                                        
         XC    KEY,KEY                                                          
         MVC   KEY(1),PBUYKMED                                                  
         MVC   KEY+1(6),SORTPUB+20                                              
         MVC   KEY+7(2),PBUYKAGY                                                
         MVI   KEY+9,X'81'                                                      
         BAS   RE,READPUB                                                       
         BAS   RE,GETP                                                          
*                                                                               
         LA    R2,PUBREC+33                                                     
         MVI   ELCODE,X'14'                                                     
TL18B    BAS   RE,NEXTEL                                                        
         BNE   TL18C                                                            
         CLC   2(3,R2),PBUYKCLT                                                 
         BNE   TL18B                                                            
         MVC   OLDCPUB,17(R2)      CONVERTED PUB NUMBER                         
*                                                                               
TL18C    MVC   KEY,SVKEY           RESTORE KEY                                  
TL18D    MVC   SORTPUB+20(6),OLDCPUB      SORT ON CONVERTED PUB NUMBER          
*                                                                               
TL18E    DS    0H                                                               
         CLI   QMEDIA,C'N'                                                      
         BE    TL18E2                                                           
         MVC   SVKEY,KEY                                                        
         MVC   KEY+10(6),SORTPUB+20     USE CONVERTED PUB                       
         BAS   RE,READCONT         READ CONTRACT                                
         MVC   KEY,SVKEY           RESTORE KEY                                  
*                                  NEED TO RESTORE READHI WITH PROPER           
         BAS   RE,HIGH             PUB                                          
*                                                                               
         CLI   QOPT2,C'Y'          SEE IF FILTERING ON END DATE                 
         BNE   TL18E2                                                           
         CLI   HAVCSW,1            SEE IF I FOUND A CONTRACT                    
         BNE   TL10                NO BYPASS THIS BUY                           
         CLC   BEND(2),PCONEND     MATCH END MONTHS                             
         BNE   TL10                                                             
*                                                                               
TL18E2   MVC   SORTDATE,PBUYKDAT                                                
         MVC   SORTLIN,PBUYKLIN                                                 
         MVC   SORTDISK,KEY+27                                                  
         CLI   QOPT4,C' '          BLANK=INS DATE ALSO                          
         BE    TL18E8                                                           
         CLI   QOPT4,C'D'          SORT BY DATE                                 
         BE    TL18E8                                                           
         CLI   QOPT4,C'S'          SORT BY SPACE ALONE                          
         BE    TL18E7                                                           
         TM    QOPT4,X'01'         SEE IF SORTING BY AGY/PRD                    
         BZ    TL18E4              1,3,A,C                                      
         MVC   SORTAP(2),PBUYKAGY                                               
         MVC   SORTAP+2(3),PBUYKPRD                                             
TL18E4   TM    QOPT4,X'02'         SEE IF SORTING ON CUST                       
         BZ    TL18E6              2,3,B,C                                      
         BAS   RE,GETCUST                                                       
         MVC   SORTCUST,WORK                                                    
TL18E6   DS    0H                                                               
         TM    QOPT4,X'F0'         SEE IF SORTING BY SPACE                      
         BO    TL18E8              A,B,C                                        
TL18E7   MVC   SORTSPCE,PBDSPACE                                                
TL18E8   CLI   QPUB,C' '           SEE IF DOING ONE PUB                         
         BNE   TL18F                                                            
         CLC   QPUB+1(3),=C'   '     ASPO - THEN NO CONTRACT INFO               
         BNE   *+10                SO DON'T PUT IT INTO SORT                    
TL18F    MVC   SORTCON,PCONNUM                                                  
         CLI   HAVCSW,1           SEE IF I FOUND A CONTRACT                     
         BE    TLINX                                                            
         MVC   SORTCON,=3X'FF'                                                  
         B     TLINX                                                            
*                                                                               
TL90     XC    KEY,KEY             CLEAR KEY FOR NEXT CLIENT (AGY)              
         XC    OLDPUB,OLDPUB                                                    
         XC    OLDCPUB,OLDCPUB                                                  
         B     TL0                                                              
*                                                                               
TLEND    MVI   SAVPARS+3,8         SET END OF INPUT                             
         XC    LSORTREC,LSORTREC                                                
*                                                                               
TLINX    DS    0H                                                               
         XIT1                                                                   
         EJECT                                                                  
TLOUT    NTR1                      OUTPUT - CONTROL BREAKS                      
         L     R3,SAVPARS+4                                                     
         USING SORTRECD,R3                                                      
SDOUTPUT EQU   *                                                                
         CLI   PBUYKEY,X'FF'                                                    
         BE    SDLAST                                                           
         CLI   RCTRACE,C'Y'                                                     
         BNE   *+8                                                              
         BAS   RE,TRACESRT                                                      
         MVC   KEY+27(4),SORTDISK                                               
         BAS   RE,GETBUY                                                        
         MVC   PBUYKPUB(6),SORTPUB+20     PUT CONVERTED PUB INTO KEY            
         MVC   KEY(25),PBUYKEY                                                  
         OC    LSORTREC,LSORTREC                                                
         BZ    SD26                                                             
         CLC   0(3,R3),LSORTREC        CHANGE OF CLIENT                         
         BE    SD18A                                                            
         MVI   MODE,LBUYCNT                                                     
         BAS   RE,GO                                                            
         MVI   MODE,LBUYPUB                                                     
         BAS   RE,GO                                                            
         MVI   MODE,LBUYVEN                                                     
         BAS   RE,GO                                                            
         MVI   MODE,LBUYREG                                                     
         BAS   RE,GO                                                            
         MVI   MODE,LBUYDST                                                     
         BAS   RE,GO                                                            
         MVI   MODE,LBUYEST                                                     
         BAS   RE,GO                                                            
         MVI   MODE,LBUYPRO                                                     
         BAS   RE,GO                                                            
         MVI   MODE,LBUYDIV                                                     
         BAS   RE,GO                                                            
         MVI   MODE,LBUYCLI                                                     
         BAS   RE,GO                                                            
         B     SD26                                                             
         SPACE 2                                                                
SD18A    CLC   0(6,R3),LSORTREC        CHANGE OF DIVISION                       
         BE    SD20                                                             
         MVI   MODE,LBUYCNT                                                     
         BAS   RE,GO                                                            
         MVI   MODE,LBUYPUB                                                     
         BAS   RE,GO                                                            
         MVI   MODE,LBUYVEN                                                     
         BAS   RE,GO                                                            
         MVI   MODE,LBUYREG                                                     
         BAS   RE,GO                                                            
         MVI   MODE,LBUYDST                                                     
         BAS   RE,GO                                                            
         MVI   MODE,LBUYEST                                                     
         BAS   RE,GO                                                            
         MVI   MODE,LBUYPRO                                                     
         BAS   RE,GO                                                            
         MVI   MODE,LBUYDIV                                                     
         BAS   RE,GO                                                            
         B     SD28                                                             
         SPACE 2                                                                
SD20     CLC   0(19,R3),LSORTREC       CHANGE OF JO  (ASPO)                     
         BE    SD24                                                             
         MVI   MODE,LBUYCNT                                                     
         BAS   RE,GO                                                            
         MVI   MODE,LBUYPUB                                                     
         BAS   RE,GO                                                            
         MVI   MODE,LBUYVEN                                                     
         BAS   RE,GO                                                            
         MVI   MODE,LBUYPRO        REALLY LAST FOR ASPO                         
         BAS   RE,GO                                                            
         B     SD36                                                             
         SPACE 2                                                                
SD24     DS    0H                                                               
         CLC   0(43,R3),LSORTREC       CHANGE OF VENDOR                         
         BE    SD24A                                                            
         MVI   MODE,LBUYCNT                                                     
         BAS   RE,GO                                                            
         MVI   MODE,LBUYPUB                                                     
         BAS   RE,GO                                                            
         MVI   MODE,LBUYVEN                                                     
         BAS   RE,GO                                                            
         B     SD44                                                             
        SPACE 2                                                                 
SD24A    DS    0H                                                               
         CLC   0(45,R3),LSORTREC       CHANGE OF PUB - (EDITION)                
         BE    SD24A4                                                           
         MVI   MODE,LBUYCNT                                                     
         BAS   RE,GO                                                            
         MVI   MODE,LBUYPUB                                                     
         BAS   RE,GO                                                            
         B     SD44                                                             
         SPACE 2                                                                
SD24A4   CLI   FCGTCONT,C'Y'                                                    
         BNE   SD50                                                             
         CLI   SORTCON,X'FF'       MEANS NO CONTRACT                            
         BE    SD24B                                                            
         CLC   KEY+16(3),PCONSTRT                                               
         BL    SD24B                                                            
         CLC   KEY+16(3),PCONEND                                                
         BNH   SD50                                                             
         SPACE 2                                                                
SD24B    MVI   MODE,LBUYCNT                                                     
         BAS   RE,GO                                                            
         B     SD46                                                             
         EJECT                                                                  
*                   SORT BUYS BY DISTRICT (OUTPUT) - FIRST TIMES                
         SPACE 3                                                                
SD26     XC    KEY,KEY                                                          
         MVC   KEY(7),PBUYKEY      GET A CLIENT                                 
         MVC   RCSVCLI,KEY+4                                                    
         SPACE 2                                                                
SD28     XC    KEY,KEY                                                          
         MVC   KEY(7),PBUYKEY      GET A DIVISION                               
         MVI   KEY+3,X'03'                                                      
         CLI   SORTDIV,0                                                        
         BZ    SD30                                                             
         MVC   KEY+7(3),SORTDIV                                                 
         BAS   RE,HIGH                                                          
         BAS   RE,GETDIV                                                        
         MVC   RCSVDIV,KEY+7                                                    
         MVI   MODE,FBUYDIV                                                     
         BAS   RE,GO                                                            
         SPACE 2                                                                
SD30     XC    KEY,KEY             GET A PRODUCT                                
         MVC   KEY(10),PBUYKEY                                                  
         MVI   KEY+3,X'06'                                                      
         BAS   RE,READ                                                          
         BAS   RE,GETPROD                                                       
         MVC   RCSVPRD,KEY+7                                                    
         SPACE 2                                                                
SD32     XC    KEY,KEY             GET AN ESTIMATE                              
         MVC   KEY(10),PBUYKEY                                                  
         MVI   KEY+3,X'07'                                                      
         MVC   KEY+10(2),PBUYKEY+19                                             
         BAS   RE,READ                                                          
         BAS   RE,GETEST                                                        
         BAS   RE,CHKES                                                         
         MVI   MODE,FBUYEST                                                     
         BAS   RE,GO                                                            
         SPACE 2                                                                
SD36     MVI   MODE,FBUYPRO        REALLY FIRST BUY FOR JOB (ASPO)              
         BAS   RE,GO                                                            
         SPACE 2                                                                
SD43     DS    0H                                                               
         MVI   MODE,FBUYMKT                                                     
         BAS   RE,GO                                                            
         SPACE 2                                                                
SD44     MVC   KEY,PBUYKEY                                                      
         BAS   RE,GETPUB                                                        
         MVI   MODE,FBUYPUB                                                     
         BAS   RE,GO                                                            
         SPACE 2                                                                
SD46     BAS   RE,READCONT                                                      
         MVI   MODE,FBUYCNT                                                     
         BAS   RE,GO                                                            
         SPACE 2                                                                
SD50     MVI   MODE,PROCBUY                                                     
         MVC   BUYASPO,SAVEASPO                                                 
         LA    R4,PBUYKPRD                                                      
         GOTO1 GETINS,DMCB,PBUYKEY,GROSS,(R4)                                   
*                                                                               
SD50B    DS    0H                                                               
         BAS   RE,GO                                                            
         MVC   LSORTREC,0(R3)                                                   
         B     SDEND                                                            
         EJECT                                                                  
*                   SORT BUYS BY DISTRICT (LAST)                                
         SPACE 3                                                                
SDLAST   DS    0H                                                               
         MVI   MODE,LBUYCNT                                                     
         BAS   RE,GO                                                            
         MVI   MODE,LBUYPUB                                                     
         BAS   RE,GO                                                            
         MVI   MODE,LBUYVEN                                                     
         BAS   RE,GO                                                            
         MVI   MODE,LBUYPRO                                                     
         BAS   RE,GO                                                            
         MVI   MODE,LBUYDIV                                                     
         BAS   RE,GO                                                            
         MVI   MODE,LBUYCLI                                                     
         BAS   RE,GO                                                            
         B     SDEND                                                            
         SPACE 2                                                                
SDEND1   EQU   *                                                                
         CLI   RCTRACE,C'Y'                                                     
         BNE   *+8                                                              
         BAS   RE,TRACESRT                                                      
SDEND    XIT1                                                                   
         SPACE 2                                                                
CHKES    CLC   QSTART(2),=C'ES'                                                 
         BNE   CHKESD                                                           
         MVC   QSTART,PESTST                                                    
         MVC   QEND,PESTEND                                                     
CHKESD   BR    RE                                                               
         EJECT                                                                  
TRACESRT NTR                                                                    
         MVC   P(15),=C'SORT TRACE (IN)'                                        
         L     R1,SAVPARS                                                       
         CLI   3(R1),4                                                          
         BE    *+10                                                             
         MVC   P+12(4),=C'OUT)'                                                 
         GOTO1 HEXOUT,DMCB,(R3),P+20,48,=C'N'                                   
         MVI   SKIPSPEC,C'Y'                                                    
         GOTO1 REPORT                                                           
         XIT                                                                    
         EJECT                                                                  
*                   ROUTINE TO GO TO APPLICATION PROGRAMS                       
         SPACE 2                                                                
GO       NTR                                                                    
         CLI   RCTRACE,C'Y'                                                     
         BNE   GO2                                                              
         CLC   RCTRCLI,SPACES                                                   
         BNE   GO2                                                              
         MVC   P,SPACES                                                         
         MVC   P(10),=C'MODE=X''  '''                                           
         MVI   SKIPSPEC,C'Y'                                                    
         GOTO1 HEXOUT,WORK,MODE,P+7,1,=C'N'                                     
         GOTO1 REPORT                                                           
         SPACE 2                                                                
GO2      DS    0H                                                               
         MVC   DMCB+4(8),SAVPARS                                                
         GOTO1 =V(PP6602A),DMCB,PPWORKC,RR=RELO                                 
         CLI   MODE,LBUYREQ        CHECK TO GO ON TO NEXT REQ                   
         BNE   GOEND                                                            
         SPACE 2                                                                
         L     RD,4(RD)            UNWIND WITHOUT EXIT                          
         LM    RE,RC,12(RD)                                                     
         MVC   BYTE,DMOUTBTS                                                    
         NC    BYTE,DMCB+8                                                      
         BE    GOEND                                                            
         DC    H'0'           DATAMGR ERROR                                     
         SPACE 2                                                                
         SPACE 2                                                                
GOEND    XIT                                                                    
         EJECT                                                                  
*                   ROUTINE TO READ A PUBLICATION FROM CONTRACT/BUY             
         SPACE 3                                                                
GETPUB   CLI   FCGTPUB,C'Y'                                                     
         BCR   7,RE                                                             
         ST    RE,SAVERE                                                        
         LR    R0,RE                                                            
         MVC   WORK,KEY                                                         
         XC    KEY,KEY                                                          
         MVC   KEY(1),WORK+2       MEDIA                                        
         MVC   KEY+1(6),WORK+10    BUY RECORD                                   
         CLI   WORK+3,X'10'                                                     
         BNE   *+10                                                             
         MVC   KEY+1(6),WORK+7     CONTRACT                                     
         MVI   KEY+9,X'81'                                                      
         MVC   KEY+7(2),PCLTAGYR   USE AGENCY OF RECORD PUB                     
         CLC   KEY(7),PUBKEY                                                    
         MVC   HALF(1),KEY+6       SAVE EDITION CODE                            
         BE    GETPUB03             DONT BOTHER TO READ IF ITS THERE            
         MVC   KEYSAVE,KEY                                                      
         LA    RF,HIGHPUB                                                       
*              CODE CHANGED - SO I WON'T BLOW UP IF THE PUB IS NOT              
*              LINKED   I NOW USE THE AGY CODE FROM THE BUY REC                 
*              NOTE -*** NO SRDS DEFAULT LOGIC                                  
*                                                                               
         BASR  RE,RF                                                            
         CLC   KEYSAVE(10),KEY     DID WE HIT THE AGENCY OVERRIDE               
         BE    GETPUB02                                                         
         CLC   KEY+7(2),PBUYKAGY                                                
         BNE   GETPUB01                                                         
         CLC   KEYSAVE(7),KEY      MAYBE WE LUCKED OUT WITH THE ZZ              
         BE    GETPUB02                                                         
         SPACE 2                                                                
GETPUB01 MVC   KEY,KEYSAVE         NO - THEN WE NEED TO READ FOR                
         MVC   KEY+7(2),PBUYKAGY       THE ZZ RECORD                            
         BAS   RE,READPUB                                                       
         SPACE 2                                                                
GETPUB02 BAS   RE,GETP                                                          
GETPUB03 DS    0H                                                               
         MVC   PUBKED,HALF         SET EDITION CODE                             
         SPACE 2                                                                
GETPUB2  MVC   KEY,WORK                                                         
         L     RE,SAVERE                                                        
         BR    RE                                                               
         EJECT                                                                  
*                   ROUTINE TO READ A CONTRACT FROM A BUY                       
         SPACE 3                                                                
READCONT CLI   FCGTCONT,C'Y'                                                    
         BCR   7,RE                                                             
         ST    RE,SAVERE                                                        
         MVI   HAVCSW,1                                                         
         MVC   WORK,KEY                                                         
         XC    KEY,KEY                                                          
         MVC   KEY(7),WORK                                                      
         CLI   PCLTAGYR,0                                                       
         BE    *+10                                                             
         MVC   KEY(2),PCLTAGYR                                                  
         MVI   KEY+3,X'10'                                                      
         MVC   KEY+7(6),WORK+10                                                 
         BAS   RE,ANYCONT                                                       
         BAS   RE,HIGH                                                          
         SPACE 2                                                                
READCN2  CLC   KEYSAVE(13),KEY                                                  
         BNE   NOCON                                                            
         BAS   RE,GETCONT                                                       
         CLC   WORK+16(3),PCONSTRT BUY MUST FIT CONTRACT DATES                  
         BL    READCN4                                                          
         CLC   WORK+16(3),PCONEND                                               
         BNH   READALL                                                          
         SPACE 3                                                                
READCN4  BAS   RE,SEQ                                                           
         B     READCN2                                                          
         SPACE 2                                                                
NOCON    MVI   HAVCSW,0                                                         
*                                                                               
READALL  DS    0H                                                               
         MVC   KEY,WORK                                                         
         CLI   MODE,C'S'                                                        
         BE    *+8                                                              
         BAS   RE,HIGH                                                          
         L     RE,SAVERE                                                        
         BR    RE                                                               
         EJECT                                                                  
*                   CHECK IF WE ALREADY HAVE CONTRACT COVERING BUY              
         SPACE 3                                                                
ANYCONT  CLC   KEY(13),PCONKEY                                                  
         BCR   7,RE                                                             
         SPACE 2                                                                
         CLC   WORK+16(3),PCONSTRT                                              
         BCR   4,RE                                                             
         CLC   WORK+16(3),PCONEND                                               
         BCR   2,RE                                                             
         SPACE 2                                                                
         B     GETPUB2                                                          
         SPACE 2                                                                
GETCUST  DS    0H                  GET CUSTOMER NUMBER                          
         ST    RE,SAVERE                                                        
         XC    WORK(10),WORK                                                    
         MVC   WORK(2),=2X'FF'     SO NON-BD BUYS WILL BE LAST                  
         CLC   PBUYKAGY,=C'BD'                                                  
         BNE   GETCUSTX                                                         
         MVI   WORK+1,0            SO BD BUYS WITHOUT CUSTOMER NUM              
*                                  WILL SORT BEFORE OTHER AGYS                  
*                                  BUT AFTER BUYS WITH CUSTOMER                 
         LA    R2,PBUYREC+33                                                    
         MVI   ELCODE,X'66'                                                     
         BAS   RE,NEXTEL                                                        
         BNE   GETCUSTX            NO COMMENT ELEM                              
         ZIC   R1,1(R2)                                                         
         SH    R1,=H'2'                                                         
         BNP   GETCUSTX                                                         
         C     R1,=F'10'                                                        
         BNH   GETCUST5                                                         
         L     R1,=F'10'           MOVE A MAX OF 10 BYTES                       
GETCUST5 BCTR  R1,0                                                             
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   WORK(0),2(R2)                                                    
GETCUSTX L     RE,SAVERE                                                        
         BR    RE                                                               
         SPACE 2                                                                
GETASPO  DS    0H                                                               
         ST    RE,SAVERE                                                        
         MVC   WORK(L'SORTASPO),SPACES                                          
         CLC   PBUYKAGY(2),=C'BD'            USE JOB FOR BD                     
         BNE   GETA1                                                            
         MVC   WORK(2),PBUYKAGY                                                 
         MVI   WORK+1,C'S'         SET                                          
         MVI   WORK+2,C'-'                                                      
         MVC   WORK+3(6),PBDJOB                                                 
         BR    RE                                                               
*                                                                               
GETA1    MVC   WORK+20(L'KEY),KEY      SAVE KEY                                 
         XC    KEY,KEY                                                          
         MVC   KEY(10),PBUYKEY                                                  
         MVI   KEY+3,X'07'                                                      
         MVC   KEY+10(2),PBUYKEY+19                                             
         CLC   PESTKEY(13),KEY                                                  
         BE    GETA2                                                            
         BAS   RE,READ                                                          
         BAS   RE,GETEST                                                        
GETA2    MVC   WORK(9),PESTNAME                                                 
         MVC   KEY,WORK+20                                                      
         GOTO1 HIGH                                                             
         L     RE,SAVERE                                                        
         BR    RE                                                               
*                                                                               
         EJECT                                                                  
GETCLI   LA    R0,PCLTKEY                                                       
         ST    R0,AREC                                                          
         B     GETR                                                             
*                                                                               
GETDIV   LA    R0,PDIVKEY                                                       
         ST    R0,AREC                                                          
         B     GETR                                                             
*                                                                               
GETPROD  LA    R0,PPRDKEY                                                       
         ST    R0,AREC                                                          
         B     GETR                                                             
*                                                                               
GETEST   LA    R0,PESTKEY                                                       
         ST    R0,AREC                                                          
         B     GETR                                                             
GETCONT  LA    R0,PCONKEY                                                       
         ST    R0,AREC                                                          
         B     GETR                                                             
*                                                                               
GETBUY   LA    R0,PBUYKEY                                                       
         ST    R0,AREC                                                          
         B     GETR                                                             
*                                                                               
*                                  DATAMGR LINES                                
*                                                                               
HIGH     LA    R0,DMRDHI                                                        
         MVC   KEYSAVE,KEY                                                      
         B     DIR                                                              
*                                                                               
SEQ      LA    R0,DMRSEQ                                                        
         B     READ+4                                                           
*                                                                               
READ     LA    R0,DMREAD                                                        
         MVC   KEYSAVE,KEY                                                      
*                                                                               
DIR      NTR1                                                                   
         ST    R0,DMCB                                                          
         MVC   DMCB(1),DMINBTS                                                  
         GOTO1 DATAMGR,DMCB,,PRTDIR,KEY,KEY                                     
*                                                                               
         B     DMCHK                                                            
         SPACE 3                                                                
GETR     LA    R0,GETREC                                                        
         B     FILE                                                             
*                                                                               
PUTR     LA    R0,PUTREC                                                        
*                                                                               
FILE     NTR1                                                                   
         ST    R0,DMCB                                                          
         MVC   DMCB(1),DMINBTS                                                  
         GOTO1 DATAMGR,DMCB,,PRTFILE,KEY+27,AREC,(0,DMWORK)                     
*                                                                               
DMCHK    DS    0H                                                               
         MVC   BYTE,DMCB+8                                                      
         NC    BYTE,DMOUTBTS                                                    
         BZ    *+6                                                              
         DC    H'0'                                                             
         B     XIT                                                              
         SPACE 3                                                                
HIGHPUB  LA    R0,DMRDHI                                                        
         MVC   KEYSAVE,KEY                                                      
         B     DIRPUB                                                           
*                                                                               
READPUB  LA    R0,DMREAD                                                        
         MVC   KEYSAVE,KEY                                                      
*                                                                               
DIRPUB   NTR1                                                                   
         SPACE 2                                                                
         GOTO1 DATAMGR,DMCB,(R0),PUBDIR,KEY,KEY                                 
*                                                                               
         B     DMCHK                                                            
         SPACE 3                                                                
GETP     NTR1                                                                   
         SPACE 2                                                                
         GOTO1 DATAMGR,DMCB,GETREC,PUBFILE,KEY+27,PUBKEY,(0,DMWORK)             
*                                                                               
         B     DMCHK                                                            
         SPACE 3                                                                
NEXTEL   DS    0H                                                               
         SR    R0,R0                                                            
         IC    R0,1(R2)                                                         
         AR    R2,R0                                                            
         CLI   0(R2),0                                                          
         BE    NEXTEL2                                                          
         CLC   ELCODE,0(R2)                                                     
         BER   RE                                                               
         B     NEXTEL+2                                                         
NEXTEL2  LTR   RE,RE                                                            
         BR    RE                                                               
*                                                                               
ACONS    DS    0F                                                               
         DC    V(PUBTOT)                                                        
         DC    V(VENTOT)                                                        
         DC    V(MKTTOT)                                                        
         DC    V(DSTTOT)                                                        
         DC    V(REGTOT)                                                        
         DC    V(ESTTOT)                                                        
         DC    V(PRDTOT)                                                        
         DC    V(DIVTOT)                                                        
         DC    V(CLTTOT)                                                        
         DC    V(DSTTOTB)                                                       
         DC    V(REGTOTB)                                                       
         DC    V(ESTTOTB)                                                       
         DC    V(PRDTOTB)                                                       
         DC    V(DIVTOTB)                                                       
         DC    V(CLTTOTB)                                                       
         DC    V(BRDTOT)                                                        
         DC    V(BRDTOTX)                                                       
         DC    V(BRDTOTB)                                                       
         DC    V(BRDTOTBX)                                                      
         DC    V(PRDTAB)                                                        
         DC    V(PRDTABX)                                                       
         DC    V(KLINES)                                                        
         DC    V(ILINES)                                                        
         DC    V(SPCTAB)                                                        
         DC    V(SPCTABX)                                                       
         DC    V(JOBLST)                                                        
         DC    V(ESSUM)                                                         
         DC    V(BRDSUM)                                                        
         DC    V(RATELOOK)                                                      
         DC    V(SUMFMT)                                                        
         DC    V(COMPRT)                                                        
         DC    V(WFMOD)                                                         
         DC    V(PUBEDIT)                                                       
         DC    V(PUBOUT)                                                        
         DC    V(ENDPUBC)                                                       
         DC    V(ENDPTOT)                                                       
         DC    V(GETBUD)                                                        
         DC    V(BUYACC)                                                        
         DC    V(PRNT)                                                          
         DC    V(OUTER)                                                         
         DC    V(FNDJOB)                                                        
         DC    V(RTLKELS)                                                       
         DC    V(BUYINV)                                                        
         DC    V(BUYCON)                                                        
         DC    V(ENDBRKS)                                                       
         DC    V(ESMONTHS)                                                      
         DC    V(PPRDSHR)                                                       
         DC    V(PPUNBUY)                                                       
         DC    V(CHOPPER)                                                       
ACONX    EQU   *                                                                
         LTORG                                                                  
       ++INCLUDE PP66WRK                                                        
*                                                                               
         PRINT OFF                                                              
*                                                                               
       ++INCLUDE PPMODEQU                                                       
*                                                                               
       ++INCLUDE PPWORKD                                                        
*                                                                               
         PRINT ON                                                               
         ORG   QREGION                                                          
QJOB     DS    CL6                                                              
*                                                                               
         PRINT OFF                                                              
*                                                                               
       ++INCLUDE PPFILED                                                        
*                                                                               
         PRINT ON                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'046PP6602N   05/01/02'                                      
         END                                                                    
