*          DATA SET FALIMACC   AT LEVEL 002 AS OF 03/27/95                      
*CATALP LIMACC                                                                  
         PRINT NOGEN                                                            
LIMACC   TITLE '- LIMIT ACCESS LIST VALIDATION ROUTINE'                         
***********************************************************************         
* PARAMETER LIST                                                      *         
* XL1    -  C'I' INITIALISE BLOCK                                     *         
*        -  C'T' TEST ACCESS VALID FOR RECORD                         *         
* XL3    -  A(XL256 LIMIT ACCESS BLOCK - SAVED BY PROGRAM)            *         
*                                                                     *         
* XL4    -  A(COMFACS)          - FOR ACTION 'I'                      *         
*        -  A(XL4 LIMIT ACCESS) - FOR ACTION 'T'                      *         
*                                                                     *         
* LIMACC EXPECTS THAT FOR ACTION 'I',                                 *         
*      1. THE BLOCK HAS BEEN CLEARED TO ZERO.                         *         
*      2. THE FIELDS LASYS, LAAGY AND LALIMACC HAVE BEEN FILLED IN    *         
*                                                                     *         
* LIMACC RETURNS CC EQUAL FOR VALID ACCESS                            *         
*                CC NEQ FOR ACCESS NOT VALID                          *         
*                                                                     *         
* NOTE: LIMACC WILL NOT ALLOW ACTION 'T' UNTIL AFTER AN 'I' HAS       *         
*       OCCURRED.                                                     *         
***********************************************************************         
         SPACE 1                                                                
LIMACC   CSECT                                                                  
         NMOD1 WORKX-WORKD,**LIMA**,CLEAR=YES                                   
         USING WORKD,RC                                                         
         ST    R1,APARM                                                         
         L     R9,0(R1)            A(BLOCK IN R9)                               
         USING LIMACCD,R9                                                       
         CLI   0(R1),C'I'          IS THIS AN INITIALISE CALL                   
         BE    SETUP               YES                                          
         CLI   0(R1),C'T'          IS THIS A TEST CALL                          
         BE    CHECKIT             YES                                          
         DC    H'0'                INVALID ACTION CALL                          
         EJECT                                                                  
***********************************************************************         
* INITIALISE ACCESS BLOCK                                             *         
***********************************************************************         
         SPACE 1                                                                
SETUP    MVI   0(R1),0             RESET RETURN BYTE                            
         ICM   RF,15,VMASTC        TEST MASTC RESOLVED                          
         BZ    SETUPON                                                          
         MVC   ADMGR,MCVDMGR-MASTD(RF)                                          
         OI    LAINDS,LAIOFLN                                                   
         B     SETUP02                                                          
*                                                                               
SETUPON  L     RF,4(R1)            A(COMFACS)                                   
         MVC   ADMGR,CDATAMGR-COMFACSD(RF)                                      
*                                                                               
SETUP02  CLC   LALIMEFF,EFFS                                                    
         BNE   OLDONE              OLD STYLE LIMIT ACCESS CODE                  
         SPACE 2                                                                
***********************************************************************         
* INITIALISE BLOCK FOR NEW LIMIT ACCESS LIST                          *         
***********************************************************************         
         SPACE 1                                                                
NEWONE   LA    R2,IOAREA                                                        
         USING SALMREC,R2          BUILD LIST RECORD KEY                        
         XC    SALMKEY,SALMKEY                                                  
         MVI   SALMTYP,SALMTYPQ                                                 
         MVI   SALMSUB,SALMSUBQ                                                 
         MVC   SALMSYS,LASYS       CONNECTED SYSTEM                             
         MVC   SALMAGY,LAAGY       AGY ALPHA                                    
         MVC   SALMLID,LALIMLID    LIST ID NAME                                 
         GOTO1 ADMGR,DMCB,DMREAD,CTFILE,SALMREC,SALMREC                         
         BE    *+6                 FAILED ON RECORD READ                        
         DC    H'0'                                                             
         LA    R0,4                MUST BE 4 OF THESE THINGS                    
         LA    R3,SALMDATA                                                      
         LA    R4,LAMASKS          WHERE TO MOVE THE DATA                       
*                                                                               
NEW010   CLI   0(R3),0                                                          
         BNE   *+6                                                              
         DC    H'0'                                                             
         CLI   0(R3),SALIMELQ                                                   
         BE    NEW020                                                           
         SR    RF,RF                                                            
         IC    RF,1(R3)                                                         
         LA    R3,0(RF,R3)                                                      
         B     NEW010                                                           
*                                                                               
         USING SALIMD,R3                                                        
NEW020   SR    R1,R1                                                            
         IC    R1,SALIMSUB         SUB NUMBER                                   
         IC    RF,SALIMBIT         NUMBER OF BITS USED FOR THIS LIMIT           
         STC   RF,LABWID1-1(R1)    SAVE IT IN BLOCK                             
         SR    RE,RE                                                            
         IC    RE,SALIMLEN                                                      
         SH    RE,=Y(SALIMDTA-SALIMD+1)                                         
         BM    *+8                 NOTHING STATED - LIMIT IS 0                  
         EX    RE,NEWMOVE          MOVE BIT MASK TO BLOCK                       
         LA    RE,1                                                             
         SLL   RE,0(RF)            THIS IS QUITE NEAT                           
         SRL   RE,3                RE=NUMBER OF MASK BYTES                      
         BCTR  RE,0                                                             
         EX    RE,*+8                                                           
         BNZ   NEW025                                                           
         OC    0(0,R4),0(R4)       TEST ANYTHING IN ACCESS MASK                 
         OI    0(R4),X'80'         OLD LIMIT ACCESS FIELD                       
         B     NEW030                                                           
*                                                                               
NEW025   LA    R1,BITMASK-1(R1)    POINT TO MASK BIT                            
         L     RF,APARM                                                         
         OC    0(1,RF),0(R1)       SET MASK BIT ON IN DMCB                      
*                                                                               
NEW030   LA    R4,1(RE,R4)         NEXT FREE SPACE FOR NEXT MASK                
         IC    RE,SALIMLEN                                                      
         LA    R3,0(RE,R3)         NEXT ELEMENT                                 
         BCT   R0,NEW010                                                        
         OI    LAINDS,LAIINIT                                                   
         B     YES                                                              
*                                                                               
NEWMOVE  MVC   0(0,R4),SALIMDTA                                                 
         DROP  R3                                                               
         EJECT                                                                  
***********************************************************************         
* INITIALISE BLOCK FOR OLD LIMIT ACCESS                               *         
***********************************************************************         
         SPACE 1                                                                
OLDONE   MVC   LABWID1(4),=4AL1(8) WIDTHS ARE FIXED IN THIS CASE                
         LA    R4,LAMASKS          R4=A(CURRENT LIMIT ACCESS MASK)              
         LA    R5,LALIMACC         R5=A(CURRENT LIMIT ACCESS BYTE)              
         LA    R0,4                THERE ARE 4 OF THESE THINGS                  
         LA    RE,BITMASK                                                       
         L     RF,APARM                                                         
*                                                                               
OLD010   SR    R2,R2                                                            
         ICM   R2,1,0(R5)                                                       
         BZ    *+10                                                             
         OC    0(1,RF),0(RE)       SET MASK BIT ON IN DMCB                      
         SRDL  R2,3                R2 HOLDS DISP TO THIS BYTE                   
         SRL   R3,32-3             R3 HOLDS DISP INTO BYTE                      
         LA    R3,BITMASK(R3)                                                   
         LA    R2,0(R2,R4)                                                      
         OC    0(1,R2),0(R3)       OR THE BIT FOR THIS NUMBER ON                
*                                                                               
         LA    R4,32(R4)           R4 HOLDS A(NEXT MASK)                        
         LA    R5,1(R5)            R5=A(NEXT LIMIT ACCESS)                      
         LA    RE,1(RE)                                                         
         BCT   R0,OLD010                                                        
         OI    LAINDS,LAIINIT                                                   
         B     YES                                                              
         EJECT                                                                  
***********************************************************************         
* TEST LIMIT ACCESS                                                   *         
***********************************************************************         
         SPACE 1                                                                
CHECKIT  L     R5,4(R1)            CHECK LIMIT ACCESS IS VALID                  
         ST    R5,ALIMIT           A(LIMIT ACCESS)                              
         TM    LAINDS,LAIINIT      DID WE INITIALISE?                           
         BO    *+6                                                              
         DC    H'0'                BLOCK NOT INITIALISED                        
*                                                                               
         LA    R1,4                THERE ARE 4 VALUES                           
         LA    R2,LABWID1          A(FIRST BIT WIDTH VALUE)                     
         LA    R4,LAMASKS          A(FIRST BITMASK)                             
*                                                                               
CHE010   TM    0(R4),X'80'         THIS IS FOR '0' IN LIMIT FIELD               
         BZ    CHE030              IF ZERO, GO VALIDATE LIMIT                   
*                                                                               
CHE020   LA    RE,1                                                             
         SR    RF,RF                                                            
         IC    RF,0(R2)            RF HOLDS # OF BITS IN THIS MASK              
         SLL   RE,0(RF)                                                         
         SRL   RE,3                                                             
         LA    R4,0(RE,R4)         NEXT FREE MASK                               
         LA    R2,1(R2)            NEXT BIT WIDTH VALUE                         
         LA    R5,1(R5)            NEXT LIMIT ACCESS VALUE - PASSED             
         BCT   R1,CHE010                                                        
         B     YES                 ALL PAN OUT                                  
*                                                                               
CHE030   LA    RE,4                CHECK IF TO IGNORE THIS ONE                  
         SR    RE,R1                                                            
         LA    RE,BITMASK(RE)      DISPLACEMENT INTO BITMASK                    
         MVC   TESTBYTE,ALIMIT     GET BIT PATTERN FROM PARAMS                  
         OC    TESTBYTE,0(RE)      TURN ON BIT TO CHECK FOR                     
         CLC   TESTBYTE,ALIMIT     WAS IT ON ANYWAY?                            
         BE    CHE020              YES                                          
*                                                                               
         SR    RE,RE                                                            
         IC    RE,0(R5)            NON-ZERO LIMIT ACCESS VALUE                  
         SRDL  RE,3                                                             
         SRL   RF,32-3                                                          
         LA    RF,BITMASK(RF)                                                   
         LA    RE,0(R4,RE)                                                      
         MVC   TESTBYTE,0(RE)                                                   
         OC    TESTBYTE,0(RF)                                                   
         CLC   TESTBYTE,0(RE)                                                   
         BE    CHE020                                                           
         B     NO                                                               
         SPACE 2                                                                
YES      CLI   *+1,0               RETURN CC EQUAL                              
         B     EXIT                                                             
NO       CLI   *+0,0               RETURN CC N.EQ                               
EXIT     XIT1  ,                                                                
         EJECT                                                                  
         LTORG                                                                  
VMASTC   DC    V(MASTC)                                                         
EFFS     DC    X'FFFFFFFF'                                                      
BITMASK  DC    X'8040201008040201'                                              
DMREAD   DC    C'DMREAD  '                                                      
CTFILE   DC    C'CTFILE  '                                                      
         SPACE 2                                                                
WORKD    DSECT                     ** LOCAL W/S **                              
APARM    DS    A                   PASSED PARAMETER LIST                        
ACOMFACS DS    A                   A(COMFACS)                                   
ADMGR    DS    A                   A(DATA MANAGER)                              
ALIMIT   DS    A                   A(LIMIT ACCESS TO VALIDATE)                  
TESTBYTE DS    X                   CURRENT COMPARISON BYTE                      
DMCB     DS    6F                                                               
IOAREA   DS    2000X                                                            
WORKX    DS    0C                                                               
         SPACE 2                                                                
* FALIMACCD                                                                     
         PRINT OFF                                                              
       ++INCLUDE FALIMACCD                                                      
         PRINT ON                                                               
         SPACE 1                                                                
* DDCOMFACS                                                                     
         PRINT OFF                                                              
       ++INCLUDE DDCOMFACS                                                      
         PRINT ON                                                               
         SPACE 1                                                                
* SEACSFILE                                                                     
         PRINT OFF                                                              
       ++INCLUDE SEACSFILE                                                      
         PRINT ON                                                               
         SPACE 1                                                                
* DDMASTD                                                                       
         PRINT OFF                                                              
       ++INCLUDE DDMASTD                                                        
         PRINT ON                                                               
         SPACE 1                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'002FALIMACC  03/27/95'                                      
         END                                                                    
