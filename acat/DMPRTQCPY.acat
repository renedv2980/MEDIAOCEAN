*          DATA SET DMPRTQCPY  AT LEVEL 002 AS OF 11/14/05                      
*CATALP PRTQCPY                                                                 
*********************************************************************           
* COPY ONE PQ REPORT TO ANOTHER PQ REPORT                           *           
*                                                                   *           
* PARAMETER LISTS ARE AS FOLLOWS                                    *           
*                                                                   *           
* P1  ENTRY       A(COMFACS)                                        *           
*     EXIT        BYTE 0   - X'00'   OKAY                           *           
*                            X'F1'   INPUT PQ REPORT NOT FOUND      *           
*                            X'F2'   CAN'T OPEN OUTPUT PQ REPORT    *           
*                            X'F3'   DISK ERROR ON OPEN             *           
*                            X'F4'   CAN'T ADD REPORT FOR USER ID   *           
*                            X'F5'   INVALID OPEN INFO PASSED       *           
*                                                                   *           
* P2              A(INPUT PQ KEY), CL7                              *           
*                      SEE "UKKEY" IN DMPRTQK FOR DETAIL            *           
*                      XL2 - USERID NUMBER                          *           
*                      CL3 - REPORT SUB ID                          *           
*                      XL2 - REPORT SEQ NUMBER                      *           
*                                                                   *           
*     EXIT        OVERRIDE THE INPUT PQ KEY BY THE OUTPUT PQ KEY    *           
*                 A(OUTPUT PQ KEY), CL7                             *           
*                      SEE "UKKEY" IN DMPRTQK FOR DETAIL            *           
*                      XL2 - USERID NUMBER                          *           
*                      CL3 - REPORT SUB ID                          *           
*                      XL2 - REPORT SEQ NUMBER                      *           
*                                                                   *           
* P3              A(INPUT PQ BUFFER), 14K                           *           
*                      OBTAIN WORKING STORAGE FROM RD-CHAIN WHEN NO *           
*                      BUFFER IS GIVEN                              *           
*                                                                   *           
* P4  ENTRY       A(OUTPUT PQ INFO BLOCK TO OVERRIDE INPUT'S),CL133 *           
*                      SEE DMPRTQL FOR DETAIL                       *           
*                      FIELDS CAN BE OVERRIDED:                     *           
*                      QLSRCID - XL2  USERID NUMBER                 *           
*                      QLSUBID - CL3  SUB ID                        *           
*                      QLCLASS - CL1  REPORT CLASS                  *           
*                      QLTYPE  - XL1  REPORT TYPE                   *           
*                      QLATTB  - XL1  REPORT ATTRIBUTES             *           
*                      QLSTAT  - XL1  REPORT STATUS                 *           
*                      QLRETNL - XL2  RETENTION HRS FOR LIVE REP    *           
*                      QLRETND - XL2  RETENTION HRS FOR DEAD REP    *           
*                      QLFORMS - CL4  REPORT FORMS CODE             *           
*                      QLCHARS - CL4  REPORT CHARACTER SET          *           
*                      QLDESC  - CL11 REPORT DESCRIPTION            *           
*                      QLSECINF- CL6  REPORT SECURITY INFO          *           
*                 NOTE:  IF NO P4 IS GIVEN, COPY PQ INFO FROM INPUT *           
*                        PQ REPORT                                  *           
*                                                                   *           
* P5              A(OUTPUT PQ BUFFER), 14K                          *           
*                      OBTAIN WORKING STORAGE FROM RD-CHAIN WHEN NO *           
*                      BUFFER IS GIVEN                              *           
*                                                                   *           
* P6              OPTION FLAG                                       *           
*                      X'80000000' - PRINT END OF REPORT MESSAGE    *           
*                                                                   *           
*********************************************************************           
         TITLE 'PRTQCPY - COPY A PQ REPORT TO ANOTHER PQ REPORT'                
PRTQCPY  CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 WORKX-WORKD,PRTQCPY*                                             
         USING WORKD,RC                                                         
*                                                                               
         LR    RA,R1               SAVE PARMS ADDRESS                           
         USING PARMD,RA                                                         
*                                                                               
         SR    R0,R0                                                            
         OC    P3,P3               ANY INPUT PQ BUFFER?                         
         BNZ   *+12                YES                                          
         AHI   R0,14336                                                         
         ST    RD,P3                                                            
*                                                                               
         OC    P5,P5               ANY OUTPUT PQ BUFFER?                        
         BNZ   *+16                YES                                          
         LR    RE,RD                                                            
         AR    RE,R0                                                            
         ST    RE,P5                                                            
         AHI   R0,14336                                                         
*                                                                               
         LTR   R0,R0                                                            
         BZ    PQCPY20             NO NEED FOR MORE WORKING STORAGE             
*                                                                               
         AHI   R0,7                ALIGN TO THE DOUBLE WORD BOUNDARY            
         SRL   R0,3                                                             
         SLL   R0,3                                                             
*                                                                               
         LR    R1,RD                                                            
         AR    R1,R0               EXPAND WORKING STORAGE LENGTH                
         MVC   4(4,R1),4(RD)       COPY THE BACKWARD POINTER                    
         LR    RD,R1               UPDATE RD                                    
*                                                                               
         L     R1,4(RD)            PT TO THE PREV CHAIN ENTRY                   
         ST    RD,8(R1)            UPDATE THE FORWARD POINTER                   
*                                                                               
PQCPY20  L     R5,P1                                                            
         USING COMFACSD,R5                                                      
*                                                                               
*OPEN INPUT PQ REPORT                                                           
         XC    INDEX,INDEX         CLEAR ALL INDEX FIELDS                       
         XC    R,R                                                              
         L     RF,P2                                                            
         LA    R2,INDEX                                                         
         USING UKRECD,R2                                                        
         MVC   UKSRCID,UKSRCID-UKRECD(RF)   REPORT USER ID                      
         MVC   UKSUBID,UKSUBID-UKRECD(RF)   REPORT ID                           
         MVC   UKREPNO,UKREPNO-UKRECD(RF)   REPORT SEQUENCE NUMBER              
         MVC   PQFILE,=CL8'PRTQUE'                                              
         GOTO1 CDATAMGR,DMCB,(X'00',=C'GFILE'),PQFILE,INDEX,0,P3                
         MVC   PQFILE,UKUSRINF                                                  
         DROP  R2                                                               
*                                                                               
         GOTO1 CDATAMGR,DMCB,(X'00',=C'INDEX'),PQFILE,INDEX,R,P3                
         CLI   DMCB+8,0                                                         
         BE    PQCPY30                                                          
         TM    DMCB+8,X'80'        EOF - REPORT NOT FOUND                       
         BO    NOTFOUND                                                         
         B     DSKERR              DSK - DISK ERROR ON OPEN                     
*                                                                               
PQCPY30  EQU   *                                                                
*                                  ASSUME USE INPUT PQ'S INFO                   
         XC    R,R                                                              
         MVI   R+4,C'L'            SILLY PARAMETER FOR RANDOM READ              
         GOTO1 CDATAMGR,DMCB,(X'00',=C'RANDOM'),PQFILE,0,R,P3                   
         CLI   DMCB+8,0                                                         
         BNE   DSKERR              DSK - DISK ERROR ON OPEN                     
*                                                                               
         LA    RE,R                                                             
         USING PQPLD,RE                                                         
         MVI   QLEXTRA,X'FF'       OPEN PRINT QUEUE REPORT                      
         DROP  RE                                                               
*                                                                               
         OC    P4,P4                                                            
         BZ    PQCPY50             NO PQ INFO GIVEN, USE INPUT PQ'S             
*                                                                               
*CHECK PQ INFO OVERRIDE                                                         
         L     RF,P4               LOAD OUTPUT PQ INFO BLOCK                    
O        USING PQPLD,R                                                          
         USING PQPLD,RF                                                         
         OC    QLSRCID,QLSRCID                                                  
         BZ    *+10                                                             
         MVC   O.QLSRCID,QLSRCID   USERID NUMBER                                
*                                                                               
         OC    QLSUBID,QLSUBID                                                  
         BZ    *+10                                                             
         MVC   O.QLSUBID,QLSUBID   SUB ID                                       
*                                                                               
         OC    QLCLASS,QLCLASS                                                  
         BZ    *+10                                                             
         MVC   O.QLCLASS,QLCLASS   REPORT CLASS                                 
*                                                                               
         OC    QLTYPE,QLTYPE                                                    
         BZ    *+10                                                             
         MVC   O.QLTYPE,QLTYPE     REPORT TYPE                                  
*                                                                               
         OC    QLATTB,QLATTB                                                    
         BZ    *+10                                                             
         MVC   O.QLATTB,QLATTB     REPORT ATTRIBUTES                            
*                                                                               
         OC    QLSTAT,QLSTAT                                                    
         BZ    *+10                                                             
         MVC   O.QLSTAT,QLSTAT     REPORT STATUS                                
*                                                                               
         OC    QLRETNL,QLRETNL                                                  
         BZ    *+10                                                             
         MVC   O.QLRETNL,QLRETNL   RETENTION HRS FOR LIVE REP                   
*                                                                               
         OC    QLRETND,QLRETND                                                  
         BZ    *+10                                                             
         MVC   O.QLRETND,QLRETND   RETENTION HRS FOR DEAD REP                   
*                                                                               
         OC    QLFORMS,QLFORMS                                                  
         BZ    *+10                                                             
         MVC   O.QLFORMS,QLFORMS   REPORT FORMS CODE                            
*                                                                               
         OC    QLCHARS,QLCHARS                                                  
         BZ    *+10                                                             
         MVC   O.QLCHARS,QLCHARS   REPORT CHARACTER SET                         
*                                                                               
         OC    QLDESC,QLDESC                                                    
         BZ    *+10                                                             
         MVC   O.QLDESC,QLDESC     REPORT DESCRIPTION                           
*                                                                               
PQCPY40  TM    QLATTB,QLATPW       REPORT SECURITY INFO                         
         BZ    PQCPY40X                                                         
         TM    QLSECF1,QLSINONO    TEST IF SECURITY FLAGS VALIS                 
         BZ    PQCPY40A                                                         
         CLC   QLPSWD,=CL4' '      TEST IF PSWD/PIN DEFINED                     
         BH    *+12                                                             
         NI    QLATTB,255-QLATPW                                                
         B     PQCPY40X                                                         
         MVC   O.QLPSWD,QLPSWD     COPY REPORT PSWD                             
         MVI   O.QLSECF1,QLSIPIN                                                
         MVI   O.QLSECF2,0                                                      
         B     PQCPY40X                                                         
PQCPY40A MVC   O.QLPSWD,QLPSWD     COPY SECURITY INFO                           
         MVC   O.QLSECF1,QLSECF1                                                
         MVC   O.QLSECF2,QLSECF2                                                
PQCPY40X EQU   *                                                                
         DROP  O                                                                
         DROP  RF                                                               
*                                                                               
*OPEN OUTPUT PQ REPORT                                                          
PQCPY50  GOTO1 CDATAMGR,DMCB,(X'00',=C'DMPRINT'),=C'PRTQUE',0,R,P5              
         CLI   DMCB+8,0                                                         
         BE    PQCPY60                                                          
         TM    DMCB+8,X'80'        EOF - CANT ADD REPORT FOR USER ID            
         BO    EOFERR                                                           
         TM    DMCB+8,X'41'        FMT - INVALID OPEN INFO PASSED               
         BO    FMTERR                                                           
         B     DSKERR              DSK - DISK ERROR ON OPEN                     
*                                                                               
*SAVE OUTPUT PQ KEY                                                             
PQCPY60  EQU   *                                                                
         LA    RE,R                                                             
         USING PQPLD,RE                                                         
         MVC   SVUID,QLSRCID      SAVE RETURNED REPORT USERID NUM               
         MVC   SVSUBID,QLSUBID    SAVE RETURNED REPORT SUB ID                   
         MVC   SVREPNO,QLREPRNO   SAVE RETURNED REPORT SEQ NUM                  
         DROP  RE                                                               
*                                                                               
*COPY REPORT LINE BY LINE                                                       
PQCPY80  EQU   *                                                                
         GOTO1 CDATAMGR,DMCB,(X'00',=C'READ'),PQFILE,INDEX,R,P3                 
         CLI   DMCB+8,0                                                         
         BE    PQCPY100                                                         
         TM    DMCB+8,X'80'        END OF REPORT?                               
         BO    PQCPY120            YES - CLOSE OUTPUT PQ REPORT                 
         B     DSKERR              DSK - DISK ERROR ON WRITE                    
*                                                                               
PQCPY100 EQU   *                                                                
         GOTO1 CDATAMGR,DMCB,(X'00',=C'DMPRINT'),=C'PRTQUE',0,R,P5              
         CLI   DMCB+8,0                                                         
         BE    PQCPY80             CONTINUE COPY UNTIL EOF                      
         TM    DMCB+8,X'80'        EOF - REPORT TOO BIG OR PRTQUE FULL          
         BO    EOFERR                                                           
         B     DSKERR              DSK - DISK ERROR ON WRITE                    
*                                                                               
PQCPY120 EQU   *                                                                
         TM    P6,FLG0EOR          PRINT THE END OF REPORT MESSAGE?             
         BNO   PQCPY150            NO                                           
*                                                                               
         MVC   R+1(L'EOR),EOR      PRINT OUT THE END OF REPORT MESSAGE          
*                                                                               
         GOTO1 CDATAMGR,DMCB,(X'00',=C'DMPRINT'),=C'PRTQUE',0,R,P5              
         CLI   DMCB+8,0                                                         
         BE    PQCPY150            CONTINUE CLOSING THE REPORT                  
         TM    DMCB+8,X'80'        EOF - REPORT TOO BIG OR PRTQUE FULL          
         BO    EOFERR                                                           
         B     DSKERR              DSK - DISK ERROR ON WRITE                    
*                                                                               
*CLOSE OUTPUT PQ REPORT                                                         
PQCPY150 EQU   *                                                                
         MVI   R,X'FF'             SET END OF REPORT                            
         GOTO1 CDATAMGR,DMCB,(X'00',=C'DMPRINT'),=C'PRTQUE',0,R,P5              
         CLI   DMCB+8,0                                                         
         BE    PQCPY160                                                         
         TM    DMCB+8,X'80'        EOF - REPORT TOO BIG OR PRTQUE FULL          
         BO    EOFERR                                                           
         B     DSKERR              DSK - DISK ERROR ON WRITE                    
*                                                                               
*SET RETURN VALUES                                                              
PQCPY160 EQU   *                                                                
         MVI   P1,0                NO ERROR                                     
         L     RE,P2               A(OUTPUT PQ KEY)                             
         USING UKRECD,RE                                                        
         MVC   UKSRCID,SVUID       REPORT USER ID                               
         MVC   UKSUBID,SVSUBID     REPORT ID                                    
         MVC   UKREPNO,SVREPNO     REPORT SEQUENCE NUMBER                       
         DROP  RE                                                               
*                                                                               
EXIT     XMOD1                                                                  
*                                                                               
NOTFOUND MVI   P1,X'F1'            INPUT PQ REPORT NOT FOUND                    
         B     EXIT                                                             
CANTOPEN MVI   P1,X'F2'            CAN'T OPEN OUTPUT PQ REPORT                  
         B     EXIT                                                             
DSKERR   MVI   P1,X'F3'            DISK ERROR ON OPEN                           
         B     EXIT                                                             
EOFERR   MVI   P1,X'F4'            CAN'T ADD REPORT FOR USER ID                 
         B     EXIT                                                             
FMTERR   MVI   P1,X'F5'            INVALID OPEN INFO PASSED                     
         B     EXIT                                                             
*                                                                               
EOR      DC    CL80'***** END OF REPORT *****'                                  
*                                                                               
         LTORG                                                                  
         EJECT                                                                  
WORKD    DSECT                                                                  
DMCB     DS    6F                                                               
PQFILE   DS    CL8                                                              
R        DS    CL256                                                            
INDEX    DS    CL40                                                             
SVUID    DS    XL2                 REPORT USERID NUMBER                         
SVSUBID  DS    CL3                 REPORT SUB-ID                                
SVREPNO  DS    XL2                 REPORT SEQUENCE NUMBER                       
WORKX    EQU   *                                                                
*                                                                               
* DDCOMFACS                                                                     
* DMPRTQL                                                                       
* DMPRTQK                                                                       
         PRINT OFF                                                              
       ++INCLUDE DDCOMFACS                                                      
       ++INCLUDE DMPRTQL                                                        
       ++INCLUDE DMPRTQK                                                        
         PRINT ON                                                               
*                                                                               
PARMD    DSECT                                                                  
P1       DS    A                   A(COMFACS)                                   
P2       DS    A                   A(INPUT PQ KEY), CL7                         
P3       DS    A                   A(INPUT PQ BUFFER), 14K                      
P4       DS    A                   A(OUTPUT PQ INFO BLOCK), CL133               
P5       DS    A                   A(OUTPUT PQ BUFFER), 14K                     
P6       DS    F                   OPTION FLAGS                                 
FLG0EOR  EQU   X'80'               PRINT END OF REPORT MESSAGE                  
*                                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'002DMPRTQCPY 11/14/05'                                      
         END                                                                    
