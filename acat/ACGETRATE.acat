*          DATA SET ACGETRATE  AT LEVEL 154 AS OF 07/02/02                      
*CATALP ACGETRTE                                                                
         TITLE 'GETRATE'                                                        
*************************************************************                   
*   THIS MODULE IS USED TO LOOKUP RATE RECORDS AND PASS     *                   
*   BACK PERTINENT INFORMATION                              *                   
*                                                           *                   
*   ON ENTRY:                                               *                   
*   PARM 1 -- A(RATE INTERFACE BLOCK)                       *                   
*                                                           *                   
*************************************************************                   
ACGETRTE CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 CRTX-CRT,**ACGR**,R7,RR=R2,CLEAR=YES                             
         USING CRT,RC                                                           
         LR    R8,R1                                                            
         USING RATED,R8            R8=A(RATE INTERFACE BLOCK)                   
         ST    R2,RELO             INITIALIZE                                   
*                                                                               
         MVI   SPACES,C' '                                                      
         MVC   SPACES+1(L'SPACES-1),SPACES                                      
         MVI   MYEFFS,X'FF'                                                     
         MVC   MYEFFS+1(L'MYEFFS-1),MYEFFS                                      
*                                                                               
         MVC   DATAMGR,RDATAMGR    GET ADDRESS OF DATAMGR                       
*                                                                               
         EJECT                                                                  
*************************************************************                   
*         PREPROCESS                                        *                   
*************************************************************                   
         SPACE 1                                                                
PREP     OC    RAT1RLVS,RAT1RLVS                                                
         BNZ   PREP10                                                           
         LA    R2,RAT1RLVS                                                      
         MVC   SVLEDSV,=C'1R'                                                   
         BAS   RE,GETLEVS                                                       
*                                                                               
PREP10   OC    RATSJLVS,RATSJLVS                                                
         BNZ   PREPX                                                            
         LA    R2,RATSJLVS                                                      
         MVC   SVLEDSV,=C'SJ'                                                   
         BAS   RE,GETLEVS                                                       
*                                                                               
PREPX    DS    0H                                                               
         EJECT                                                                  
*************************************************************                   
*          GET RATE  -  UK VERSION                          *                   
*************************************************************                   
         SPACE 1                                                                
*&&UK                                                                           
         USING SRCH1D,R3           R3=A(RATE SEARCH TABLE)                      
         LA    R3,SRCHTBL                                                       
         XC    RATEEFFD,RATEEFFD                                                
         NI    RATCFLG,X'FF'-BCFLCUR2  TURN OFF SECONDARY CURR SWITCH           
         ZAP   RAT2AMNT,=P'0'          CLEAR EURO RATE                          
         ZAP   RATEAMNT,=P'0'                                                   
*                                                                               
         USING PCRRECD,R6          PRODUCTION CHARGE RATES RECORD               
GTR50    LA    R6,KEY                                                           
         XC    KEY,KEY                                                          
         MVC   PCRKEY(32),MYEFFS                                                
         MVI   PCRKTYP,PCRKTMSQ    X'29' RECORD                                 
         CLI   RATCTRY,CTRYGER                                                  
         BNE   *+8                                                              
         MVI   PCRKTYP,PCRKTYPQ    X'2A' RECORD FOR GERMANY                     
         MVC   PCRKCPY,RAT1CMPY                                                 
         MVC   PCRKPER,SPACES                                                   
*                                                                               
         LA    RE,PCRKPER                                                       
         LA    RF,RAT1RACT                                                      
         SR    R1,R1                                                            
         IC    R1,RAT1RL4                                                       
         TM    S1STAT,S11RLEVD     PERSON                                       
         BO    GTR350                                                           
*                                                                               
         IC    R1,RAT1RL3                                                       
         TM    S1STAT,S11RLEVC     SUBDEPT                                      
         BO    GTR350                                                           
*                                                                               
         IC    R1,RAT1RL2                                                       
         TM    S1STAT,S11RLEVB     DEPARTMENT                                   
         BO    GTR350                                                           
*                                                                               
         IC    R1,RAT1RL1                                                       
         TM    S1STAT,S11RLEVA     OFFICE                                       
         BO    GTR350                                                           
         B     GTR400                                                           
*                                                                               
GTR350   SHI   R1,1                                                             
         BM    GTR400                                                           
         EX    R1,*+4                                                           
         MVC   0(0,RE),0(RF)                                                    
*                                                                               
GTR400   TM    S1STAT,S1TSK        MOVE IN TASK CODE                            
         BZ    *+10                                                             
         MVC   PCRKTSK,RATSJTSK                                                 
*                                                                               
         TM    S1STAT,S1OFF        MOVE IN CLIENT OFFICE CODE                   
         BZ    *+10                                                             
         MVC   PCRKOFF,RATSJOFF                                                 
*                                                                               
         TM    S1STAT,S1CLI        MOVE IN CLIENT                               
         BZ    GTR500                                                           
         SR    R1,R1                                                            
         IC    R1,RATSJL1                                                       
         SH    R1,=H'1'                                                         
         BNM   *+6                                                              
         DC    H'0'                                                             
         EX    R1,*+4                                                           
         MVC   PCRKJOB(0),RATSJCPJ                                              
*                                                                               
GTR500   TM    S1STAT,S1PRO        MOVE IN PRODUCT CODE                         
         BZ    GTR550                                                           
         LA    RE,PCRKJOB                                                       
         LA    RF,RATSJCPJ                                                      
         SR    R1,R1                                                            
         IC    R1,RATSJL1                                                       
         AR    RE,R1                                                            
         AR    RF,R1                                                            
         IC    R1,RATSJL2                                                       
         SH    R1,=H'1'                                                         
         BNM   *+6                                                              
         DC    H'0'                                                             
         EX    R1,*+4                                                           
         MVC   0(0,RE),0(RF)                                                    
         EX    R1,*+4                                                           
         OC    0(0,RE),SPACES                                                   
*                                                                               
GTR550   MVC   KEYSAVE,KEY                                                      
         GOTO1 DATAMGR,DMCB,(0,=C'DMRDHI'),=C'ACCDIR',KEY,KEY                   
         CLC   KEY(L'PCRKEY),KEYSAVE                                            
         BE    GTR600                                                           
GTR570   LA    R3,1(R3)                                                         
         CLI   0(R3),0                                                          
         BNE   GTR50                                                            
         B     GTRX                                                             
*                                                                               
GTR600   GOTO1 DATAMGR,DMCB,(0,=C'GETREC'),=C'ACCMST',PCRKDA,IO,DMWORK          
         LA    R6,IO                                                            
*                                                                               
         TM    RATCFLG,BCFLSEC     TEST IF 2ND CURRENCY BILLED                  
         BNO   GTR620                                                           
*                                                                               
         MVC   WORK(L'RATCUR2),RATCUR2                                          
         MVC   WORK+L'RATCUR2(L'RATCUR1),RATCUR1                                
         GOTO1 RTOBACCO,DMCB,('TOBAACVS',WORK),(R6),RCOMFACS,0                  
*                                                                               
GTR620   AH    R6,=Y(ACTRFST-ACTRECD)                                           
GTR625   CLI   0(R6),0                                                          
         BNE   GTR650                                                           
         TM    RATSTAT2,RATEFND    TEST RATE WAS FOUND                          
         BNZ   GTR800                                                           
         B     GTR570              TRY NEXT HIGHER LEVEL                        
         CLI   0(R6),X'53'         RATE ELEMENT                                 
         BE    GTR700                                                           
GTR650   SR    R1,R1                                                            
         IC    R1,1(R6)                                                         
         AR    R6,R1                                                            
         B     GTR625                                                           
*                                                                               
         USING TCIELD,R6                                                        
GTR700   CLC   TCIDTE,RATCRDTE     RATE > CURRENT DATE?                         
         BH    GTR650              YES                                          
         CLC   TCIDTE,RATEEFFD                                                  
         BL    GTR650                                                           
         MVC   RATEEFFD,TCIDTE     SAVE HIGHEST DATE                            
         ZAP   RATEAMNT,TCIAMT     SAVE RATE                                    
         OI    RATSTAT2,RATEFND    INDICATE A RATE WAS FOUND                    
         B     GTR650                                                           
*                                                                               
GTR800   TM    RATCSTAT,CPYSSCNV   AGENCY ON DUAL CURRENCY?                     
         BZ    GTRX                                                             
         ZAP   DUB,RATEAMNT        CALCULATE RATE IN PRIMARY CURRENCY           
         BZ    GTRX                                                             
         GOTO1 RCASHVAL,DMCB,(X'80',DUB),(X'28',0)                              
         ORG   *-2                                                              
         LA    RE,RATCUR1          2ND -> PRIMARY CURRENCY                      
         TM    RATCFLG,BCFLSEC     TEST IF 2ND CURRENCY BILLED                  
         BNZ   *+8                                                              
         LA    RE,RATCUR2          PRIMARY -> 2ND CURRENCY                      
         ST    RE,8(R1)                                                         
         BASR  RE,RF                                                            
         LA    RE,RATEAMNT                                                      
         LA    RF,RAT2AMNT                                                      
         TM    RATCFLG,BCFLSEC     TEST IF 2ND CURRENCY BILLED                  
         BNZ   *+12                                                             
         LA    RE,RAT2AMNT                                                      
         LA    RF,RATEAMNT                                                      
         ZAP   0(L'RATEAMNT,RF),4(8,R1)                                         
         ZAP   0(L'RATEAMNT,RE),12(8,R1) RATE IN OTHER CURRENCY                 
*                                                                               
GTRX     DS    0H                  GO TO GET COST RATE                          
*&&                                                                             
         EJECT                                                                  
*************************************************************                   
*          GET COST RATE  -  UK VERSION                     *                   
*************************************************************                   
         SPACE 1                                                                
*&&UK                                                                           
GETCRTE  DS    0H                                                               
         XC    RATEEFFC,RATEEFFC                                                
         ZAP   RATEAMTC,=P'0'                                                   
         ZAP   RAT2AMTC,=P'0'                                                   
         NI    RATCFLG,X'FF'-BCFLCUR2  TURN OFF SECONDARY CURR SWITCH           
*                                                                               
         USING PHIRECD,R6          READ PAYROLL HISTORY RECORD                  
         LA    R6,KEY                                                           
         MVC   PHIKEY,SPACES                                                    
         MVI   PHIKTYP,PHIKTYPQ    X'3E' RECORD                                 
         MVI   PHIKSUB,PHIKSUBQ    X'05' SUB TYPE                               
         MVC   PHIKCPY,RAT1CMPY                                                 
         MVC   PHIKOFC,RAT1ROFF    OFFICE CODE                                  
         MVC   PHIKDPT,RAT1RDPT    DEPT CODE                                    
         MVC   PHIKSBD(L'RAT1RSUB),RAT1RSUB                                     
         MVC   PHIKPER(L'RAT1RPER),RAT1RPER                                     
         SR    R1,R1                                                            
         ICM   R1,3,RATCRDTE                                                    
         LNR   R1,R1                                                            
         STCM  R1,3,PHIKMOA                                                     
         XC    PHIKSEQ,PHIKSEQ                                                  
         MVC   KEYSAVE,KEY                                                      
         GOTO1 DATAMGR,DMCB,(0,=C'DMRDHI'),=C'ACCDIR',KEY,KEY                   
         CLC   KEY(L'PHIKEY),KEYSAVE                                            
         BNE   GETCRX                                                           
         GOTO1 DATAMGR,DMCB,(0,=C'GETREC'),=C'ACCMST',PHIKDA,IO,DMWORK          
         LA    R6,IO                                                            
*                                                                               
         TM    RATCFLG,BCFLSEC     TEST IF 2ND CURRENCY BILLED                  
         BNO   GETCR20                                                          
*                                                                               
GETCR10  MVC   WORK(L'RATCUR2),RATCUR2                                          
         MVC   WORK+L'RATCUR2(L'RATCUR1),RATCUR1                                
         GOTO1 RTOBACCO,DMCB,('TOBAACVS',WORK),(R6),RCOMFACS,0                  
GETCR20  AH    R6,=Y(ACTRFST-ACTRECD)                                           
GETCR30  CLI   0(R6),0                                                          
         BE    GETCR60                                                          
         CLI   0(R6),PDEELQ        X'86' PAYROLL DETAIL ELEMENT                 
         BE    GETCR50                                                          
GETCR40  SR    R1,R1                                                            
         IC    R1,1(R6)                                                         
         AR    R6,R1                                                            
         B     GETCR30                                                          
*                                                                               
         USING PDEELD,R6                                                        
GETCR50  DS    0H                                                               
         TM    PDESTAT2,PDESHRTE   HOURLY RATES                                 
         BNO   GETCR40                                                          
         CLC   PDEDTE,RATCRDTE     SKIP RATES > PERIOD ENDING DATE              
         BH    GETCR40                                                          
         CLC   PDEDTE,RATEEFFD                                                  
         BL    GETCR40                                                          
         ZAP   DUB,PDEAMT                                                       
         AP    DUB,PDEADJ                                                       
         MVC   RATEEFFC,PDEDTE     SAVE HIGHEST DATE                            
         ZAP   RATEAMTC,DUB        SAVE COST RATE                               
         OI    RATSTAT2,RATEFNDC   COST RATE FOUND                              
         B     GETCR40                                                          
*                                                                               
GETCR60  TM    RATCSTAT,CPYSSCNV   AGENCY ON DUAL CURRENCY?                     
         BZ    GETCRX                                                           
         ZAP   DUB,RATEAMTC        CALCULATE HOURLY RATE IN 1ST CURR            
         BZ    GETCRX                                                           
         GOTO1 RCASHVAL,DMCB,(X'80',DUB),(X'28',0)                              
         ORG   *-2                                                              
         LA    RE,RATCUR1          2ND -> PRIMARY CURRENCY                      
         TM    RATCFLG,BCFLSEC     TEST IF 2ND CURRENCY BILLED                  
         BNZ   *+8                                                              
         LA    RE,RATCUR2          PRIMARY -> 2ND CURRENCY                      
         ST    RE,8(R1)                                                         
         BASR  RE,RF                                                            
         LA    RE,RATEAMTC                                                      
         LA    RF,RAT2AMTC                                                      
         TM    RATCFLG,BCFLSEC     TEST IF 2ND CURRENCY BILLED                  
         BNZ   *+12                                                             
         LA    RE,RAT2AMTC                                                      
         LA    RF,RATEAMTC                                                      
         ZAP   0(L'RATEAMTC,RF),4(8,R1)                                         
         ZAP   0(L'RATEAMTC,RE),12(8,R1) RATE IN OTHER CURRENCY                 
*                                                                               
GETCRX   XIT1                                                                   
*&&                                                                             
         EJECT                                                                  
*************************************************************                   
*          GET RATE  -  US VERSION                          *                   
*************************************************************                   
         SPACE 1                                                                
*&&US                                                                           
         USING SRCH1D,R4           R4=A(RATE SEARCH TABLE)                      
         LA    R4,SRCHTBL1                                                      
         XC    RATEEFFD,RATEEFFD   CLEAR EFFECTIVE DATE                         
         ZAP   RATEAMNT,=P'0'                                                   
*                                                                               
GTR50    DS    0H                                                               
         USING PCHRECD,R5          PRODUCTION CHARGE RATES RECORD               
         LA    R5,KEY                                                           
         XC    PCHKEY,PCHKEY       CLEAR PCHKEY                                 
         MVI   PCHKTYP,PCHKTYPQ    X'2A' RECORD                                 
         MVC   PCHKCPY,RAT1CMPY    COMPANY CODE                                 
*                                                                               
         TM    S1STAT,S1ODP        MOVE IN OFFICE                               
         BZ    *+16                                                             
         MVC   PCHKDOF,RAT1ROFF                                                 
         OC    PCHKDOF,SPACES                                                   
*                                                                               
         TM    S1STAT,S1DPT        MOVE IN DEPARTMENT                           
         BZ    *+16                                                             
         MVC   PCHKDEP,RAT1RDPT                                                 
         OC    PCHKDEP,SPACES                                                   
*                                                                               
         TM    S1STAT,S1SUB        MOVE IN SUBDEPT                              
         BZ    *+16                                                             
         MVC   PCHKSUB,RAT1RSUB                                                 
         OC    PCHKSUB,SPACES                                                   
*                                                                               
         TM    S1STAT,S1STF        MOVE IN PERSON                               
         BZ    *+16                                                             
         MVC   PCHKSTF,RAT1RPER                                                 
         OC    PCHKSTF,SPACES                                                   
*                                                                               
         TM    S1STAT,S1TSK        MOVE IN TASK CODE                            
         BZ    *+10                                                             
         MVC   PCHKTSK,RATSJTSK                                                 
*                                                                               
         TM    S1STAT,S1OFF        MOVE IN CLIENT OFFICE CODE                   
         BZ    *+10                                                             
         MVC   PCHKOFF,RATSJOFF                                                 
*                                                                               
         TM    S1STAT,S1CLI        MOVE IN CLIENT                               
         BZ    *+10                                                             
         MVC   PCHKCLI,RATSJCLI                                                 
*                                                                               
         TM    S1STAT,S1PRO        MOVE IN PRODUCT CODE                         
         BZ    *+10                                                             
         MVC   PCHKPRO,RATSJPRD                                                 
         DROP  R5                                                               
*                                                                               
         MVC   KEYSAVE,KEY                                                      
         GOTO1 DATAMGR,DMCB,(0,=C'DMRDHI'),=C'ACCDIR',KEY,KEY                   
         CLC   KEYSAVE,KEY         FOUND RECORD?                                
         BE    GTR500              YES                                          
*                                                                               
GTR300   LA    R4,1(R4)            NO - GO TO NEXT ENTRY IN TABLE               
         CLI   0(R4),0             CHECK FOR END OF TABLE                       
         BNE   GTR50                                                            
         B     GTRX                                                             
*                                                                               
GTR500   DS    0H                                                               
         USING ACTRECD,R5                                                       
         GOTO1 DATAMGR,DMCB,(0,=C'GETREC'),=C'ACCMST',ACTKDA,IO,DMWORK          
         LA    R6,IO                                                            
         AH    R6,=Y(ACTRFST-ACTRECD)   DISPLACEMENT TO FIRST ELEMENT           
*                                                                               
GTR525   CLI   0(R6),0                                                          
         BE    GTRX                                                             
*                                                                               
         USING TCIELD,R6                                                        
GTR530   CLI   0(R6),TCIELQ        RATE ELEMENT                                 
         BE    GTR600                                                           
*                                                                               
GTR550   SR    R0,R0                                                            
         IC    R0,1(R6)                                                         
         AR    R6,R0                                                            
         B     GTR525                                                           
*                                                                               
GTR600   DS    0H                                                               
         CLC   TCIDTE,RATCRDTE     DATE > CURRENT DATE?                         
         BH    GTR550              YES                                          
         CLC   TCIDTE,RATEEFFD                                                  
         BL    GTR550                                                           
         MVC   RATEEFFD,TCIDTE     SAVE HIGHEST DATE                            
         ZAP   RATEAMNT,TCIAMT     SAVE RATE                                    
         OI    RATSTAT2,RATEFND    TURN ON FOUND BIT                            
         B     GTR550                                                           
*                                                                               
GTRX     DS    0H                                                               
         TM    RATSTAT,RATEADJ     ELIGIBLE FOR ADJ RATE?                       
         BNZ   GETADJT             YES                                          
XIT      XIT1                                                                   
         EJECT                                                                  
*************************************************************                   
*            READ ADJUSTMENT RATES                          *                   
*************************************************************                   
         SPACE 1                                                                
GETADJT  DS    0H                                                               
         USING SRCH2D,R4                                                        
         LA    R4,SRCHTBL2         R4=A(ADJ RATE SEARCH TABLE)                  
         LA    R2,SRCH2N           LENGTH OF TABLE                              
         ZAP   GRPL13,=P'0'        CLEAR WORK ACCUMULATOR                       
         XC    GRYYMMDD,GRYYMMDD                                                
*                                                                               
GTA50    DS    0H                                                               
         USING PAJRECD,R5                                                       
         LA    R5,KEY                                                           
         XC    PAJKEY,PAJKEY                                                    
         MVI   PAJKTYP,PAJKTYPQ    X'19' RECORD                                 
         MVC   PAJKCPY,RAT1CMPY                                                 
         TM    S2STAT,S2OFF        MOVE IN CLIENT OFFICE CODE                   
         BZ    *+10                                                             
         MVC   PAJKOFF,RATSJOFF                                                 
         TM    S2STAT,S2CLI        MOVE IN CLIENT CODE                          
         BZ    *+10                                                             
         MVC   PAJKCLI,RATSJCLI                                                 
         TM    S2STAT,S2PRO        MOVE IN PRODUCT CODE                         
         BZ    *+10                                                             
         MVC   PAJKPRO,RATSJPRD                                                 
         TM    S2STAT,S2JOB        MOVE IN JOB CODE                             
         BZ    *+10                                                             
         MVC   PAJKJOB,RATSJJOB                                                 
         TM    S2STAT,S2DPT        OFFICE                                       
         BZ    GTA200                                                           
         MVC   PAJKDOF,RAT1ROFF                                                 
         OC    PAJKDOF,SPACES                                                   
         MVC   PAJKDEP,RAT1RDPT    DEPARTMENT                                   
         OC    PAJKDEP,SPACES                                                   
*                                                                               
GTA200   DS    0H                                                               
         TM    S2STAT,S2SUB        MOVE IN SUBDEPT                              
         BZ    GTA300                                                           
         MVC   PAJKSUB,RAT1RSUB                                                 
         OC    PAJKSUB,SPACES                                                   
*                                                                               
GTA300   DS    0H                                                               
         TM    S2STAT,S2STF        MOVE IN PERSON                               
         BZ    GTA400                                                           
         MVC   PAJKSTF,RAT1RPER                                                 
         OC    PAJKSTF,SPACES                                                   
*                                                                               
GTA400   DS    0H                                                               
         TM    S2STAT,S2TSK        MOVE IN TASK CODE                            
         BZ    *+10                                                             
         MVC   PAJKTSK,RATSJTSK                                                 
*                                                                               
         MVC   KEYSAVE,KEY                                                      
         GOTO1 DATAMGR,DMCB,(0,=C'DMRDHI'),=C'ACCDIR',KEY,KEY                   
         CLC   KEYSAVE,KEY         FOUND RECORD?                                
         BE    GTA600              YES                                          
*                                                                               
GTA500   LA    R4,1(R4)            NO - GO TO NEXT ENTRY IN TABLE               
         CLI   0(R4),0             CHECK FOR END OF TABLE                       
         BNE   GTA50                                                            
         B     GTAX                                                             
*                                                                               
GTA600   DS    0H                                                               
         USING ACTRECD,R5                                                       
         GOTO1 DATAMGR,DMCB,(0,=C'GETREC'),=C'ACCMST',ACTKDA,IO,DMWORK          
         LA    R6,IO                                                            
         AH    R6,=Y(ACTRFST-ACTRECD)   DISPLACEMENT TO FIRST ELEMENT           
*                                                                               
         USING TCIELD,R6                                                        
GTA625   DS    0H                                                               
         CLI   0(R6),0                                                          
         BE    GTAX                                                             
         CLI   0(R6),TCIELQ        RATE ELEMENT                                 
         BE    GTA700                                                           
*                                                                               
GTA650   DS    0H                                                               
         SR    R0,R0                                                            
         IC    R0,1(R6)                                                         
         AR    R6,R0                                                            
         B     GTA625                                                           
*                                                                               
GTA700   DS    0H                                                               
         CLC   TCIDTE,RATCRDTE     DATE > CURRENT DATE?                         
         BH    GTA650                                                           
         CLC   TCIDTE,GRYYMMDD                                                  
         BL    GTA650                                                           
         MVC   GRYYMMDD,TCIDTE     SAVE HIGHEST DATE                            
         ZAP   GRPL13,TCIAMT       SAVE RATE ADJUSTMENT                         
         B     GTA650                                                           
*                                                                               
GTAX     DS    0H                                                               
         ZAP   GRPL13,GRPL13                                                    
         BZ    XIT                                                              
         MP    GRPL13,RATEAMNT                                                  
         SRP   GRPL13,64-6,5                                                    
         AP    RATEAMNT,GRPL13                                                  
         OI    RATSTAT2,RATEWADJ   INDICATE RATE WAS ADJUSTED                   
         B     XIT                                                              
*                                                                               
*&&                                                                             
         EJECT                                                                  
***********************************************************************         
* GETLEVS ROUTINE                                                     *         
* R2 - A(SAVED LEVEL LENGTHS)                                         *         
***********************************************************************         
         SPACE 1                                                                
         USING LDGRECD,R3                                                       
GETLEVS  NTR1                                                                   
         LA    R3,KEY                                                           
         MVC   LDGKCPY,RAT1CMPY    BUILD KEY                                    
         MVC   LDGKUNT(2),SVLEDSV                                               
         MVC   KEYSAVE,KEY                                                      
         GOTO1 DATAMGR,DMCB,(0,=C'DMRDHI'),=C'ACCDIR',KEY,KEY                   
         CLC   KEYSAVE(LDGKEND),KEY                                             
         BNE   GETX                                                             
         GOTO1 DATAMGR,DMCB,(0,=C'GETREC'),=C'ACCMST',LDGKDA,IO,DMWORK          
         LA    R6,IO                                                            
         AH    R6,=Y(LDGRFST-LDGRECD)                                           
*                                                                               
         USING ACLELD,R6                                                        
GET10    CLI   0(R6),0                                                          
         BNE   *+6                                                              
         DC    H'0'                                                             
         CLI   0(R6),ACLELQ        X'16' ELEMENT                                
         BE    GET20                                                            
         SR    R1,R1                                                            
         IC    R1,1(R6)                                                         
         AR    R6,R1                                                            
         B     GET10                                                            
*                                                                               
GET20    SR    R1,R1                                                            
         LA    R6,ACLLN1Q(R6)      POINT TO FIRST LENGTH                        
         LA    R0,4                AT MOST 4 LEVELS                             
GET30    CLI   0(R6),0                                                          
         BE    GETX                                                             
         SR    R3,R3                                                            
         IC    R3,0(R6)                                                         
         SR    R3,R1                                                            
         STC   R3,0(R2)                                                         
         SR    R1,R1                                                            
         IC    R1,0(R6)                                                         
         LA    R2,1(R2)            POINT TO NEXT SAVE LEVEL                     
         LA    R6,L'ACLVALS(R6)    POINT TO NEXT LENGTH                         
         BCT   R0,GET30                                                         
*                                                                               
GETX     XIT1                                                                   
         EJECT                                                                  
***********************************************************************         
* LTORG                                                               *         
***********************************************************************         
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
* RATE SEARCH TABLE                                                   *         
***********************************************************************         
         SPACE 1                                                                
*&&UK                                                                           
SRCHTBL  DS    0XL1                                                             
         DC    B'11111111'     W-C/PROD/CLI/OFF/D/C/B/A                         
         DC    B'01111111'         PROD/CLI/OFF/D/C/B/A                         
         DC    B'01101111'         PROD/CLI     D/C/B/A                         
         DC    B'10111111'     W-C      CLI/OFF/D/C/B/A                         
         DC    B'10101111'     W-C      CLI     D/C/B A                         
         DC    B'00111111'              CLI/OFF/D/C/B/A                         
         DC    B'00101111'              CLI     D/C/B/A                         
         DC    B'10011111'     W-C          OFF/D/C/B/A                         
         DC    B'00011111'                  OFF/D/C/B/A                         
         DC    B'10001111'     W-C              D/C/B/A                         
         DC    B'00001111'                      D/C/B/A                         
         DC    B'11110111'     W-C/PROD/CLI/OFF   C/B/A                         
         DC    B'01110111'         PROD/CLI/OFF   C/B/A                         
         DC    B'01100111'         PROD/CLI       C/B/A                         
         DC    B'10110111'     W-C      CLI/OFF   C/B/A                         
         DC    B'10100111'     W-C      CLI       C/B A                         
         DC    B'00110111'              CLI/OFF   C/B/A                         
         DC    B'00100111'              CLI       C/B/A                         
         DC    B'10010111'     W-C          OFF   C/B/A                         
         DC    B'00010111'                  OFF   C/B/A                         
         DC    B'10000111'     W-C                C/B/A                         
         DC    B'00000111'                        C/B/A                         
         DC    B'11110011'     W-C/PROD/CLI/OFF     B/A                         
         DC    B'01110011'         PROD/CLI/OFF     B/A                         
         DC    B'01100011'         PROD/CLI         B/A                         
         DC    B'10110011'     W-C      CLI/OFF     B/A                         
         DC    B'10100011'     W-C      CLI         B/A                         
         DC    B'00110011'              CLI/OFF     B/A                         
         DC    B'00100011'              CLI         B/A                         
         DC    B'10010011'     W-C          OFF     B/A                         
         DC    B'00010011'                  OFF     B/A                         
         DC    B'10000011'     W-C                  B/A                         
         DC    B'00000011'                          B/A                         
         DC    B'11110001'     W-C/PROD/CLI/OFF       A                         
         DC    B'01110001'         PROD/CLI/OFF       A                         
         DC    B'10110001'     W-C      CLI/OFF       A                         
         DC    B'10100001'     W-C      CLI           A                         
         DC    B'00110001'              CLI/OFF       A                         
         DC    B'00100001'              CLI           A                         
         DC    B'10010001'     W-C          OFF       A                         
         DC    B'00010001'                  OFF       A                         
         DC    B'10000001'     W-C                    A                         
         DC    B'00000001'                            A                         
         DC    B'11110000'     W-C/PROD/CLI/OFF                                 
         DC    B'01110000'         PROD/CLI/OFF                                 
         DC    B'10110000'     W-C      CLI/OFF                                 
         DC    B'10100000'     W-C      CLI                                     
         DC    B'00100000'              CLI                                     
         DC    B'00110000'              CLI/OFF                                 
         DC    B'10010000'     W-C          OFF                                 
         DC    B'00010000'                  OFF                                 
         DC    B'10000000'     W-C                                              
         DC    X'00'           E-O-T POSSIBLE SEARCHES                          
*&&                                                                             
         EJECT                                                                  
***********************************************************************         
* RATE SEARCH TABLE                                                   *         
***********************************************************************         
         SPACE 1                                                                
*&&US                                                                           
SRCHTBL1 DS    0XL1                                                             
         DC    B'11111111'     PRO/CLI/OFF/TASK/STF/SUB/DEP/OFF                 
         DC    B'11101111'     PRO/CLI/OFF      STF/SUB/DEP/OFF                 
         DC    B'01111111'         CLI/OFF/TASK/STF/SUB/DEP/OFF                 
         DC    B'01101111'         CLI/OFF      STF/SUB/DEP/OFF                 
         DC    B'00111111'             OFF/TASK/STF/SUB/DEP/OFF                 
         DC    B'00101111'             OFF      STF/SUB/DEP/OFF                 
         DC    B'00011111'                 TASK/STF/SUB/DEP/OFF                 
         DC    B'00001111'                      STF/SUB/DEP/OFF                 
         DC    B'11110111'     PRO/CLI/OFF/TASK     SUB/DEP/OFF                 
         DC    B'11100111'     PRO/CLI/OFF          SUB/DEP/OFF                 
         DC    B'01110111'         CLI/OFF/TASK     SUB/DEP/OFF                 
         DC    B'01100111'         CLI/OFF          SUB/DEP/OFF                 
         DC    B'00110111'             OFF/TASK     SUB/DEP/OFF                 
         DC    B'00100111'             OFF          SUB/DEP/OFF                 
         DC    B'00010111'                 TASK     SUB/DEP/OFF                 
         DC    B'00000111'                          SUB/DEP/OFF                 
         DC    B'11110011'     PRO/CLI/OFF/TASK         DEP/OFF                 
         DC    B'11100011'     PRO/CLI/OFF              DEP/OFF                 
         DC    B'01110011'         CLI/OFF/TASK         DEP/OFF                 
         DC    B'01100011'         CLI/OFF              DEP/OFF                 
         DC    B'00110011'             OFF/TASK         DEP/OFF                 
         DC    B'00100011'             OFF              DEP/OFF                 
         DC    B'00010011'                 TASK         DEP/OFF                 
         DC    B'00000011'                              DEP/OFF                 
         DC    B'11110001'     PRO/CLI/OFF/TASK             OFF                 
         DC    B'11100001'     PRO/CLI/OFF                  OFF                 
         DC    B'01110001'         CLI/OFF/TASK             OFF                 
         DC    B'01100001'         CLI/OFF                  OFF                 
         DC    B'00110001'             OFF/TASK             OFF                 
         DC    B'00100001'             OFF                  OFF                 
         DC    B'00010001'                 TASK             OFF                 
         DC    B'00000001'                                  OFF                 
         DC    X'00'                                                            
         EJECT                                                                  
***********************************************************************         
* ADJUSTMENT RATE SEARCH TABLE                                        *         
***********************************************************************         
         SPACE 1                                                                
SRCHTBL2 DS    0XL1                                                             
         DC    B'11111111'     TASK/STF/SUB/DEP/JOB/PRO/CLI/OFF                 
         DC    B'01111111'          STF/SUB/DEP/JOB/PRO/CLI/OFF                 
         DC    B'10111111'     TASK     SUB/DEP/JOB/PRO/CLI/OFF                 
         DC    B'00111111'              SUB/DEP/JOB/PRO/CLI/OFF                 
         DC    B'10011111'     TASK         DEP/JOB/PRO/CLI/OFF                 
         DC    B'00011111'                  DEP/JOB/PRO/CLI/OFF                 
         DC    B'10001111'     TASK             JOB/PRO/CLI/OFF                 
         DC    B'00001111'                      JOB/PRO/CLI/OFF                 
         DC    B'11110111'     TASK/STF/SUB/DEP     PRO/CLI/OFF                 
         DC    B'01110111'          STF/SUB/DEP     PRO/CLI/OFF                 
         DC    B'10110111'     TASK     SUB/DEP     PRO/CLI/OFF                 
         DC    B'00110111'              SUB/DEP     PRO/CLI/OFF                 
         DC    B'10010111'     TASK         DEP     PRO/CLI/OFF                 
         DC    B'00010111'                  DEP     PRO/CLI/OFF                 
         DC    B'10000111'     TASK                 PRO/CLI/OFF                 
         DC    B'00000111'                          PRO/CLI/OFF                 
         DC    B'11110011'     TASK/STF/SUB/DEP         CLI/OFF                 
         DC    B'01110011'          STF/SUB/DEP         CLI/OFF                 
         DC    B'10110011'     TASK     SUB/DEP         CLI/OFF                 
         DC    B'00110011'              SUB/DEP         CLI/OFF                 
         DC    B'10010011'     TASK         DEP         CLI/OFF                 
         DC    B'00010011'                  DEP         CLI/OFF                 
         DC    B'10000011'     TASK                     CLI/OFF                 
         DC    B'00000011'                              CLI/OFF                 
         DC    B'11110001'     TASK/STF/SUB/DEP             OFF                 
         DC    B'01110001'          STF/SUB/DEP             OFF                 
         DC    B'10110001'     TASK     SUB/DEP             OFF                 
         DC    B'00110001'              SUB/DEP             OFF                 
         DC    B'10010001'     TASK         DEP             OFF                 
         DC    B'00010001'                  DEP             OFF                 
         DC    B'10000001'     TASK                         OFF                 
         DC    B'00000001'                                  OFF                 
         DC    B'11110000'     TASK/STF/SUB/DEP                                 
         DC    B'01110000'          STF/SUB/DEP                                 
         DC    B'10110000'     TASK     SUB/DEP                                 
         DC    B'00110000'              SUB/DEP                                 
         DC    B'10010000'     TASK         DEP                                 
         DC    B'00010000'                  DEP                                 
         DC    B'10000000'     TASK                                             
         DC    B'00000000'                                     AGY              
SRCH2N   EQU   *-SRCHTBL2                                                       
*&&                                                                             
         EJECT                                                                  
***********************************************************************         
* RATE SEARCH TABLE DSECT                                             *         
***********************************************************************         
         SPACE 1                                                                
SRCH1D   DSECT                     RATE SEARCH TABLE                            
S1STAT   DS    XL1                 DESCRIBES HOW TO BUILD KEY                   
*&&US                                                                           
S1PRO    EQU   X'80'               PRODUCT                                      
S1CLI    EQU   X'40'               CLIENT                                       
S1OFF    EQU   X'20'               OFFICE                                       
S1TSK    EQU   X'10'               TASK                                         
S1STF    EQU   X'08'               STAFF                                        
S1SUB    EQU   X'04'               SUBDEPT                                      
S1DPT    EQU   X'02'               DEPT                                         
S1ODP    EQU   X'01'               OFFICE                                       
*&&                                                                             
*&&UK                                                                           
S1TSK    EQU   X'80'               TASK                                         
S1PRO    EQU   X'40'               PRODUCT                                      
S1CLI    EQU   X'20'               CLIENT                                       
S1OFF    EQU   X'10'               OFFICE                                       
S11RLEVD EQU   X'08'               1R STAFF                                     
S11RLEVC EQU   X'04'               1R SUBDEPT                                   
S11RLEVB EQU   X'02'               1R DEPT                                      
S11RLEVA EQU   X'01'               1R OFFICE                                    
*&&                                                                             
         SPACE 3                                                                
***********************************************************************         
* ADJUSTMENT RATE SEARCH TABLE DSECT                                  *         
***********************************************************************         
         SPACE 1                                                                
SRCH2D   DSECT                     ADJUSTMENT RATE SEARCH TABLE                 
S2STAT   DS    XL1                 DESCRIBES HOW TO BUILD KEY                   
S2TSK    EQU   X'80'               TASK                                         
S2STF    EQU   X'40'               STAFF                                        
S2SUB    EQU   X'20'               SUBDEPT                                      
S2DPT    EQU   X'10'               DEPT                                         
S2JOB    EQU   X'08'               JOB                                          
S2PRO    EQU   X'04'               PRODUCT                                      
S2CLI    EQU   X'02'               CLIENT                                       
S2OFF    EQU   X'01'               OFFICE                                       
         EJECT                                                                  
**********************************************************************          
*              WORKING STORAGE FOR GETRTE                            *          
**********************************************************************          
         SPACE 1                                                                
CRT      DSECT                                                                  
RELO     DS    A                                                                
DATAMGR  DS    A                                                                
WORK     DS    CL64                                                             
DUB      DS    D                                                                
DMWORK   DS    12D                                                              
DMCB     DS    CL24                                                             
*                                                                               
KEYSAVE  DS    CL42                                                             
**********                                                                      
KEY      DS    CL42                                                             
IO       DS    2000C                                                            
         ORG   KEY                                                              
IO2      DS    2000C                                                            
**********                                                                      
*                                                                               
SPACES   DS    CL132                                                            
MYEFFS   DS    CL132                                                            
GRPL13   DS    PL13                TEMP ACCUMULATOR                             
GRYYMMDD DS    PL3                 TEMP DATE FIELD                              
SVLEDSV  DS    XL2                                                              
CRTX     EQU   *                                                                
*                                                                               
GOBLOCKD DSECT                                                                  
       ++INCLUDE ACGOBLOCK                                                      
         EJECT                                                                  
**********************************************************************          
*              INCLUDES                                              *          
**********************************************************************          
         SPACE 1                                                                
       ++INCLUDE ACGETRATED                                                     
         EJECT                                                                  
*                                                                               
* ACGENFILE                                                                     
* DDCTRYEQUS                                                                    
       ++INCLUDE ACGENFILE                                                      
       ++INCLUDE DDCTRYEQUS                                                     
       ++INCLUDE ACCAP30DST                                                     
       ++INCLUDE ACCAP30GW                                                      
       ++INCLUDE ACTOBACCOD                                                     
*                                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'154ACGETRATE 07/02/02'                                      
         END                                                                    
