*          DATA SET NERWOUT    AT LEVEL 011 AS OF 05/01/02                      
*  NERWOUT - OUTPUT MODULE FOR NETPAK OVERNIGHT REPORT WRITER                   
*  PRINTS INTERNAL RECORDS AND IN-LINE TOTALS                                   
*                                                                               
*  INPUTS FROM EDIT : GEND, NERWBLOCK                                           
*                                                                               
*CATALP NERWOUT                                                                 
         TITLE 'NERWOUT - NETWORK OVERNIGHT REPORT WRITER OUTPUT PHASE'         
***      PRINT NOGEN                                                            
NERWOUT  CSECT                                                                  
         NMOD1 0,**RWOUT**,RR=R2                                                
         L     RC,0(R1)                                                         
         USING GEND,RC                                                          
         L     R8,4(R1)                                                         
         USING NERWBLOK,R8                                                      
         LA    RA,ASPOOLD                                                       
         USING SPOOLD,RA                                                        
         L     R9,ASYSD                                                         
         USING NETSYSD,R9                                                       
         L     R7,AWS                                                           
         USING WORKD,R7                                                         
*                                                                               
*                                                                               
         LA    R2,HOOK                                                          
         ST    R2,HEADHOOK                                                      
*                                                                               
         EJECT                                                                  
*** LOCALS                                                                      
*    R4 - PREVIOUS INTERNAL RECORD                                              
*    R5 - CURRENT INTERNAL RECORD                                               
*    R3 - CURRENT ENTRY IN DRIVE TABLE                                          
*                                                                               
         L     R4,APREVREC         CK IF FIRST RECORD                           
         L     R5,AINTERN                                                       
         L     R2,IRECLEN                                                       
         EXOC  R2,0(R4),0(R4)                                                   
         BZ    CKOVER                                                           
*                                                                               
         EXOC  R2,0(R5),0(R5)      CK IF LAST RECORD                            
         BZ    TOTALS                                                           
*                                                                               
         USING RWINBLOK,R3                                                      
CKBREAK  L     R3,BRK1DE                                                        
         LTR   R3,R3                                                            
         BZ    CKOVER                                                           
         BAS   RE,CKIT                                                          
         BZ    CKB2                                                             
         BAS   RE,PRB6                                                          
         BAS   RE,PRB5                                                          
         BAS   RE,PRB4                                                          
         BAS   RE,PRB3                                                          
         BAS   RE,PRB2                                                          
         BAS   RE,PRB1                                                          
         B     CKOVER                                                           
*                                                                               
CKB2     L     R3,BRK2DE                                                        
         LTR   R3,R3                                                            
         BZ    CKOVER                                                           
         BAS   RE,CKIT                                                          
         BZ    CKB3                                                             
         BAS   RE,PRB6                                                          
         BAS   RE,PRB5                                                          
         BAS   RE,PRB4                                                          
         BAS   RE,PRB3                                                          
         BAS   RE,PRB2                                                          
         B     CKOVER                                                           
*                                                                               
CKB3     L     R3,BRK3DE                                                        
         LTR   R3,R3                                                            
         BZ    CKOVER                                                           
         BAS   RE,CKIT                                                          
         BZ    CKB4                                                             
         BAS   RE,PRB6                                                          
         BAS   RE,PRB5                                                          
         BAS   RE,PRB4                                                          
         BAS   RE,PRB3                                                          
         B     CKOVER                                                           
*                                                                               
CKB4     L     R3,BRK4DE                                                        
         LTR   R3,R3                                                            
         BZ    CKOVER                                                           
         BAS   RE,CKIT                                                          
         BZ    CKB5                                                             
         BAS   RE,PRB6                                                          
         BAS   RE,PRB5                                                          
         BAS   RE,PRB4                                                          
         B     CKOVER                                                           
*                                                                               
CKB5     L     R3,BRK5DE                                                        
         LTR   R3,R3                                                            
         BZ    CKOVER                                                           
         BAS   RE,CKIT                                                          
         BZ    CKB6                                                             
         BAS   RE,PRB6                                                          
         BAS   RE,PRB5                                                          
         B     CKOVER                                                           
*                                                                               
CKB6     L     R3,BRK6DE                                                        
         LTR   R3,R3                                                            
         BZ    CKOVER                                                           
         BAS   RE,CKIT                                                          
         BZ    CKB7                                                             
         BAS   RE,PRB6                                                          
         B     CKOVER                                                           
*                                                                               
CKB7     EQU   *                                                                
*                                                                               
CKOVER   L     R2,IRECLEN                                                       
         EXMVC R2,0(R4),0(R5)      SET PREVREC. USED FOR HEADLINES              
         BAS   RE,PRINIT           PRINT DETAIL LINE                            
         B     TXMOD               THATS ALL                                    
*                                                                               
*                                                                               
*                                                                               
TOTALS   BAS   RE,PRB6                                                          
         BAS   RE,PRB5                                                          
         BAS   RE,PRB4                                                          
         BAS   RE,PRB3                                                          
         BAS   RE,PRB2                                                          
         BAS   RE,PRB1                                                          
         BAS   RE,REPTOT                                                        
*                                                                               
TXMOD    XMOD1                                                                  
*                                                                               
PROCERR  DC    F'0'                                                             
*                                                                               
*                                                                               
*****************************************************************               
         EJECT                                                                  
*****************************************************************               
* CKIT - ROUTINE TO CK IF BREAKS HAVE OCCURRED                                  
*  INPUT: R3 - A(DRIVE RECORD FOR THIS BREAK)                                   
* OUTPUT: STATUS WORD - SET TO NZ IF BREAK OCCURS                               
CKIT     NTR1                                                                   
         USING RWINBLOK,R3                                                      
         LH    R2,RWOFFS           OFFSET OF BREAK FIELD                        
         L     R4,AINTERN                                                       
         LA    R4,0(R4,R2)         BREAK FIELD IN CURRENT                       
         L     R5,APREVREC                                                      
         LA    R5,0(R5,R2)                                                      
         ZIC   R6,RWOUTLEN                                                      
         EXCLC R6,0(R5),0(R4)      CK IF CHANGED                                
         BE    CKSAME                                                           
         LA    R1,1                                                             
         B     CKITXIT                                                          
CKSAME   LA    R1,0                                                             
CKITXIT  LTR   R1,R1                                                            
         B     XIT                                                              
*****************************************************************               
         EJECT                                                                  
*****************************************************************               
* INTINIT -  INITIALIZE THE INTERNAL RECORD POINTED TO BY ARG1                  
*           MOVE ZEROES FOR NUMERICS AND ALPHAS                                 
*           PACKED ZEROES FOR COST                                              
*       INPUTS: ARG1 (R6) - A(INTERNAL RECORD TO INITIALIZE)                    
INTINIT  NTR1                                                                   
         L     R6,0(R1)                                                         
         L     R5,ADRIVE1          GET A(DRIVE TABLE)                           
         USING RWINBLOK,R5                                                      
ININ2    CLI   RWINTYPE,0                                                       
         BE    ININXIT                                                          
         ZIC   RF,RWOUTLEN          PUT LENGTH IN RF                            
         CLI   RWINTYPE,NRICOST      CK FOR COST                                
         BE    ININCOST                                                         
         XCEF  (R6),(RF)            CLEAR IT                                    
         B     ININNEXT                                                         
*                                                                               
ININCOST MVI   0(R6),0              FOR COST 1ST BYTE IS ZERO                   
         ZAP   1(8,R6),ZERO         AND ZAP NEXT 8                              
*                                                                               
ININNEXT ZIC   R1,RWOUTLEN         BUMP TO NEXT PIECE OF INTERNAL REC           
         LA    R6,0(R1,R6)                                                      
         LA    R5,RWBLKLEN(R5)      BUMP TO NEXT DRIVE                          
         B     ININ2                                                            
*                                                                               
ININXIT  B     XIT                                                              
************************************************************                    
         DROP  R5                                                               
         EJECT                                                                  
*****************************************************************               
* PRB6 - PRINT THE BREAK6 TOTALS                                                
*                                                                               
PRB6     NTR1                                                                   
*                                                                               
         OC    ABRK6IR,ABRK6IR     MAKE SURE THIS TOTAL EXISTS                  
         BZ    PRB6XIT2                                                         
*                                                                               
         GOTO1 SPOOL,DMCB,(R8)     PRINT BLANK LINE                             
*                                                                               
         USING RWINBLOK,R5                                                      
         L     R5,ADRIVE1          GET A(DRIVE TABLE)                           
         L     R6,ABRK6IR          GET A(BREAK RECORD)                          
         LA    R4,P1               GET PRINT LINE                               
PRB6LOOP CLI   RWINTYPE,0                                                       
         BE    PRB6PRIN                                                         
         CLI   RWOUTTYP,NROALPH    DONT PRINT ALPHAS                            
         BE    PRB6CONT                                                         
         ST    R6,RWAOUT                                                        
         ST    R4,RWAPR                                                         
         MVI   RWFUNC,RWPRINT                                                   
         GOTO1 VNEWRW,DMCB,NETSYSD,(R5)                                         
*                                                                               
PRB6CONT ZIC   R2,RWPRLEN          BUMP PRINT POSITION + 1 SPACE                
         LA    R4,1(R4,R2)                                                      
         ZIC   R2,RWOUTLEN         BUMP TO NEXT PIECE OF INTERNAL REC           
         LA    R6,0(R2,R6)                                                      
         LA    R5,RWBLKLEN(R5)     BUMP TO NEXT DRIVE                           
         B     PRB6LOOP                                                         
PRB6PRIN CLC   P1(132),SPACES                                                   
         BE    PRB6XIT                                                          
         OC    P1(132),P1                                                       
         BZ    PRB6XIT                                                          
*******  GOTO1 DATCON,DMCB,(2,SAVEPER),(4,P1)                                   
*******  MVI   P1+6,C'-'                                                        
*******  GOTO1 DATCON,DMCB,(2,SAVEPER+2),(4,P1+8)                               
         MVC   P1+14(5),=C'TOTAL'                                               
         GOTO1 SPOOL,DMCB,(R8)     PRINT IT                                     
         GOTO1 SPOOL,DMCB,(R8)     AND BLANK LINE AFTER                         
*                                                                               
PRB6XIT  GOTO1 INTINIT,DMCB,ABRK6IR      RESET WEEK TOTALS TO 0                 
PRB6XIT2 B     XIT                                                              
         DROP  R5                                                               
************************************************************                    
         EJECT                                                                  
************************************************************                    
PRB1     EQU   *                                                                
PRB2     EQU   *                                                                
PRB3     EQU   *                                                                
PRB4     EQU   *                                                                
PRB5     EQU   *                                                                
REPTOT   EQU   *                                                                
         BR    RE                                                               
************************************************************                    
         EJECT                                                                  
************************************************************                    
* PRINIT      ROUTINE TO PRINT INTERNAL RECORD                                  
*              ADDS RECORD TO APPROPRIATE BREAKS                                
*  LOCALS - R5 -A(CURRENT PIECE OF DRIVE TABLE)                                 
*           R6 -A(CURRENT INTERNAL RECORD)                                      
*           R4 -A(CUR POSITON IN PRINT LINE)                                    
*   OUTPUT: FILLS PREVIOUS RECORD                                               
*           FILLS INTERNAL RECORD                                               
*           ADDS INTERNAL RECORD TO WEEK TOTALS                                 
*                                                                               
PRINIT   NTR1                                                                   
*                                                                               
         USING RWINBLOK,R5                                                      
         L     R5,ADRIVE1          GET A(DRIVE TABLE)                           
         L     R6,AINTERN          GET A(INTERNAL RECORD)                       
         LA    R4,P1               GET PRINT LINE                               
UN2      CLI   RWINTYPE,0                                                       
         BE    UNPRIN                                                           
         ST    R4,RWAPR                                                         
         MVI   RWFUNC,RWPRINT                                                   
         GOTO1 VNEWRW,DMCB,NETSYSD,(R5)                                         
*                                                                               
UN4      ZIC   R2,RWPRLEN          BUMP PRINT POSITION + 1 SPACE                
         LA    R4,1(R4,R2)                                                      
         ZIC   R2,RWOUTLEN         BUMP TO NEXT PIECE OF INTERNAL REC           
         LA    R6,0(R2,R6)                                                      
         LA    R5,RWBLKLEN(R5)     BUMP TO NEXT DRIVE                           
         B     UN2                                                              
UNPRIN   GOTO1 SPOOL,DMCB,(R8)                                                  
*                                                                               
*                                  ADD CURRENT RECORD TO BREAK TOTALS           
UN6      GOTO1 ADDINTS,DMCB,ABRK1IR,AINTERN                                     
         GOTO1 ADDINTS,DMCB,ABRK2IR,AINTERN                                     
         GOTO1 ADDINTS,DMCB,ABRK3IR,AINTERN                                     
         GOTO1 ADDINTS,DMCB,ABRK4IR,AINTERN                                     
         GOTO1 ADDINTS,DMCB,ABRK5IR,AINTERN                                     
         GOTO1 ADDINTS,DMCB,ABRK6IR,AINTERN                                     
         B     XIT                                                              
         DROP  R5                                                               
**********************************************************                      
*******************************************************                         
         SPACE 3                                                                
TOTTOTS  NTR1                                                                   
         GOTO1 SPOOL,DMCB,(R8)     PRINT BLANK LINE                             
*                                                                               
         USING RWINBLOK,R5                                                      
         L     R5,ADRIVE1          GET A(DRIVE TABLE)                           
         L     R6,AREPTOT          GET A(REPORT TOTALS)                         
         LA    R4,P1               GET PRINT LINE                               
TOT2     CLI   RWINTYPE,0                                                       
         BE    TOTPRIN                                                          
         CLI   RWOUTTYP,NROALPH    DONT PRINT ALPHAS                            
         BE    TOT4                                                             
         ST    R6,RWAOUT                                                        
         ST    R4,RWAPR                                                         
         MVI   RWFUNC,RWPRINT                                                   
         GOTO1 VNEWRW,DMCB,NETSYSD,(R5)                                         
*                                                                               
TOT4     ZIC   R2,RWPRLEN          BUMP PRINT POSITION + 1 SPACE                
         LA    R4,1(R4,R2)                                                      
         ZIC   R2,RWOUTLEN         BUMP TO NEXT PIECE OF INTERNAL REC           
         LA    R6,0(R2,R6)                                                      
         LA    R5,RWBLKLEN(R5)     BUMP TO NEXT DRIVE                           
         B     TOT2                                                             
TOTPRIN  MVC   P1+2(13),=C'REPORT TOTAL '                                       
         GOTO1 SPOOL,DMCB,(R8)     PRINT IT                                     
*                                                                               
         B     XIT                                                              
         DROP  R5                                                               
         EJECT                                                                  
********************************************************                        
* ADDINTS      ROUTINE TO ADD INTERNAL RECORDS                                  
*      ADDS INTERNAL RECORD IN ARG2 TO INTERNAL RECORD IN ARG1                  
*        INPUTS :ARG1 (R4) - A(INTERNAL RECORD TO ADD INTO)                     
*                ARG2 (R5) - A(INTERNAL RECORD TO ADD FROM)                     
*                                                                               
ADDINTS  NTR1                                                                   
         L     R4,0(R1)                                                         
         L     R5,4(R1)                                                         
*                                                                               
         USING RWINBLOK,R6                                                      
         L     R6,ADRIVE1          GET A(DRIVE TABLE)                           
ADDI2    CLI   RWINTYPE,0                                                       
         BE    ADDIXIT                                                          
*                                                                               
         ST    R5,RWAIN                                                         
         ST    R4,RWAOUT                                                        
         MVI   RWFUNC,RWADD                                                     
         GOTO1 VNEWRW,DMCB,NETSYSD,(R6)                                         
*                                                                               
ADDI4    ZIC   R2,RWOUTLEN                                                      
         LA    R5,0(R5,R2)                                                      
         LA    R4,0(R4,R2)                                                      
         LA    R6,RWBLKLEN(R6)      BUMP TO NEXT DRIVE                          
         B     ADDI2                                                            
ADDIXIT  B     XIT                                                              
         DROP  R6                                                               
*********************************************************                       
         EJECT                                                                  
********************************************************                        
*              HEADLINE ROUTINES                                                
*                                                                               
HOOK     NTR1                                                                   
*                                                                               
*                                                                               
         L     R6,APREVREC         USE THESE VALUES FOR HEADLINES               
         LA    R5,HEAD1            BASE ADDRESS FOR OFFSETS                     
         USING PHEAD,R5                                                         
         LH    R1,CLIOFF                                                        
         LA    R1,0(R6,R1)                                                      
         MVC   PHCLI,0(R1)                                                      
         MVC   PHCLNM,CLINAME                                                   
*                                                                               
         LH    R1,PRDOFF                                                        
         LA    R1,0(R6,R1)                                                      
         MVC   PHPRD,0(R1)                                                      
         MVC   PHPRNM,PRDNAME                                                   
*                                                                               
         LH    R2,ESTOFF                                                        
         LA    R2,0(R6,R2)                                                      
         EDIT  (1,0(R2)),(4,PHEST),ALIGN=LEFT                                   
         MVC   PHESNM,ESTNAME                                                   
         MVC   PHNET,=C'ALL '                                                   
*                                                                               
         LH    R2,NETOFF                                                        
         LA    R2,0(R6,R2)                                                      
         MVC   PHNET,0(R2)                                                      
*                                                                               
HH2      CLI   NBSELPAK,0          IF ALL PACKAGES DONT PRINT                   
         BE    HH4                                                              
         MVC   PHPAKH(10),=C'PACKAGE - '                                        
         LH    R2,PKGOFF                                                        
         LA    R2,0(R6,R2)                                                      
         EDIT  (3,0(R2)),(4,PHPAK),ALIGN=LEFT                                   
         MVC   PHPKNM(L'PHPKNM),PAKNAME                                         
HH4      MVC   PHDP(3),=C'ALL'                                                  
         CLI   NBSELDP,0                                                        
         BE    HH6                                                              
         MVC   PHDP(1),NBSELDP                                                  
HH6      EQU   *                                                                
         CLI   NBSELFLT,0                                                       
         BE    HH10                                                             
         CLI   NBSELFLT,1                                                       
         BNE   HH7                                                              
         MVC   PHFILT,=CL11'(CLEARED)'                                          
HH7      CLI   NBSELFLT,2                                                       
         BNE   HH8                                                              
         MVC   PHFILT,=CL11'(UNCLEARED)'                                        
HH8      CLI   NBSELFLT,3                                                       
         BNE   HH9                                                              
         MVC   PHFILT,=CL11'(BILLED)'                                           
HH9      CLI   NBSELFLT,4                                                       
         BNE   HH10                                                             
         MVC   PHFILT,=CL11'(BILLABLE)'                                         
HH10     EQU   *                                                                
         LA    R4,PHBPLINE         A(PRINT LINE)                                
         OC    NACSBDT,NACSBDT     PRINT BILL LINE IF DATES GIVEN               
         BZ    HH20                                                             
         MVC   0(L'PHBPLINE,R4),SPACES    CLEAR LINE                            
         MVC   0(6,R4),=C'BILLED'                                               
         OC    NACSBDT(2),NACSBDT         IF START DATE                         
         BZ    HH14                                                             
         OC    NACSBDT+2(2),NACSBDT+2     ....IF END DATE                       
         BZ    HH12                                                             
         GOTO1 DATCON,DMCB,(2,NACSBDT),(5,8(R4))                                
         MVI   17(R4),C'-'                                                      
         GOTO1 DATCON,DMCB,(2,NACSBDT+2),(5,19(R4))                             
         B     HH16                       ....ELSE                              
HH12     MVC   8(5,R4),=C'AFTER'                                                
         GOTO1 DATCON,DMCB,(2,NACSBDT),(5,14(R4))                               
         B     HH16                       ....FI                                
HH14     MVC   8(6,R4),=C'BEFORE'         ELSE (END, NO START)                  
         GOTO1 DATCON,DMCB,(2,NACSBDT+2),(5,15(R4))                             
HH16     EQU   *                          FI                                    
         LA    R4,PHBPLIN2                                                      
*                                                                               
HH20     OC    NACSPDT,NACSPDT     PRINT PAY LINE IF DATES GIVEN                
         BZ    HH30                                                             
         MVC   0(L'PHBPLINE,R4),SPACES    CLEAR LINE                            
         MVC   0(4,R4),=C'PAID'                                                 
         OC    NACSPDT(2),NACSPDT         IF START DATE                         
         BZ    HH24                                                             
         OC    NACSPDT+2(2),NACSPDT+2     ....IF END DATE                       
         BZ    HH22                                                             
         GOTO1 DATCON,DMCB,(2,NACSPDT),(5,6(R4))                                
         MVI   15(R4),C'-'                                                      
         GOTO1 DATCON,DMCB,(2,NACSPDT+2),(5,17(R4))                             
         B     HH26                       ....ELSE                              
HH22     MVC   6(5,R4),=C'AFTER'                                                
         GOTO1 DATCON,DMCB,(2,NACSPDT),(5,14(R4))                               
         B     HH26                       ....FI                                
HH24     MVC   6(6,R4),=C'BEFORE'         ELSE (END, NO START)                  
         GOTO1 DATCON,DMCB,(2,NACSPDT+2),(5,13(R4))                             
HH26     EQU   *                          FI                                    
HH30     EQU   *                                                                
         DROP  R5                                                               
*                                                                               
* COLUMN HEADERS                                                                
*                                                                               
         USING RWINBLOK,R5                                                      
         L     R5,ADRIVE1          GET A(DRIVE TABLE)                           
         LA    R4,H9               GET HEAD LINE                                
HK2      CLI   RWINTYPE,0                                                       
         BE    HKXIT                                                            
         MVI   RWHEDILN,HEDLEN                                                  
         ST    R4,RWAHEDOU                                                      
         MVI   RWFUNC,RWHEAD                                                    
         CLI   RWHEDTYP,0          DEFAULT TO ALPHA                             
         BNE   HK3                                                              
         MVI   RWHEDTYP,NRHALPH                                                 
HK3      GOTO1 VNEWRW,DMCB,NETSYSD,(R5)                                         
         ZIC   R2,RWPRLEN                                                       
         EXCLC R2,132(R4),SPACES                                                
         BNE   HK4                 UNDERLINE IF OTHING THERE ALREADY            
         GOTO1 UNDERLIN,DMCB,(RWPRLEN,0(R4)),132(R4)                            
HK4      ZIC   R2,RWPRLEN          BUMP PRINT POSITION + 1 SPACE                
         LA    R4,1(R4,R2)                                                      
         LA    R5,RWBLKLEN(R5)      BUMP TO NEXT DRIVE                          
         B     HK2                                                              
HKXIT    B     XIT                                                              
         DROP  R5                                                               
*********************************************************                       
         EJECT                                                                  
XIT      XIT1                                                                   
*                                                                               
*              LTORG ETC                                                        
         SPACE 3                                                                
*                                                                               
ZERO     DC    XL1'0F'             PACKED ZERO                                  
         SPACE 2                                                                
         EJECT                                                                  
HEDLEN   EQU   10                                                               
         EJECT                                                                  
         LTORG                                                                  
         EJECT                                                                  
PHEAD    DSECT                     DSECT FOR HEADERS                            
*                                  MUST MATCH SSPEC                             
PHLENGTH EQU   132                                                              
*                                                                               
         ORG   PHEAD+3*PHLENGTH+11                                              
PHCLI    DS    CL3                 CLIENT ABBR                                  
         DS    CL1                                                              
PHCLNM   DS    CL20                CLIENT NAME                                  
         ORG   PHEAD+4*PHLENGTH+11                                              
PHPRD    DS    CL3                 PRODUCT ABBR                                 
         DS    CL1                                                              
PHPRNM   DS    CL20                PRODUCT NAME                                 
         ORG   PHEAD+4*PHLENGTH+40                                              
PHPAKH   DS    CL10                C'PACKAGE - '                                
PHPAK    DS    CL3                 PACKAGE ABBR                                 
         ORG   PHEAD+4*PHLENGTH+86                                              
PHNET    DS    CL4                 NETWORK                                      
         ORG   PHEAD+5*PHLENGTH+11                                              
PHEST    DS    CL1                 ESTIMATE ABBR                                
         DS    CL3                                                              
PHESNM   DS    CL24                ESTIMATE NAME                                
         DS    CL1                                                              
PHPKNM   DS    CL27                PACKAGE NAME                                 
         ORG   PHEAD+5*PHLENGTH+50                                              
PHFILT   DS    CL11                                                             
         ORG   PHEAD+5*PHLENGTH+86                                              
PHDP     DS    CL8                 DAYPART                                      
         ORG   PHEAD+6*PHLENGTH+40                                              
PHBPLINE DS    CL30                BILLED OR PAID DATE FILTER LINE              
         ORG   PHEAD+7*PHLENGTH+40                                              
PHBPLIN2 DS    CL30                BILLED OR PAID DATE FILTER LINE              
*                                                                               
*                                                                               
WORKD    DSECT                                                                  
       ++INCLUDE NEACCTBLK                                                      
*                                                                               
WORKX    EQU   *                                                                
*                                                                               
         EJECT                                                                  
       ++INCLUDE NERWBLOCK                                                      
         EJECT                                                                  
       ++INCLUDE SPGENCLT                                                       
         EJECT                                                                  
       ++INCLUDE NERWINCLS                                                      
       ++INCLUDE NERWEQUS                                                       
         EJECT                                                                  
*                                                                               
       ++INCLUDE NETINCLS                                                       
*                                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'011NERWOUT   05/01/02'                                      
         END                                                                    
