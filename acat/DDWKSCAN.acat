*          DATA SET DDWKSCAN   AT LEVEL 026 AS OF 09/27/19                      
*CATALP WKSCAN                                                                  
         TITLE '$WK - SCAN'                                                     
         PRINT NOGEN                                                            
***********************************************************************         
* NOTES -                                                                       
*         IF PARAMETER 1 BYTE 1 IS C'D' THEN WKSCAN PERFORMS A                  
*  DEMO FILE ACCESS CHECK.  THIS REQUIRES THAT PARAMETER 6 POINT TO THE         
*  2 CHARACTER AGENCY CODE.                                                     
***********************************************************************         
WKSCAN   CSECT                                                                  
         NMOD1 WORKX-WORKD,**WRKS**,RR=R4,CLEAR=YES                             
         USING WORKD,RC                                                         
*                                                                               
         ST    R1,APARM                                                         
         L     R4,0(R1)                                                         
         USING WSBLOCKD,R4         R4=A(FILTERS)                                
         L     R7,4(R1)                                                         
         USING WSCOUNTD,R7         R7=A(COUNTERS)                               
         ZAP   0(3,R7),=P'0'                                                    
         MVC   3(45,R7),0(R7)      ZAP COUNTS TO ZERO                           
         MVI   WSFILES,1           SET NO OF FILES 1                            
*                                                                               
         L     R6,8(R1)            R6=A(BUFFER)                                 
         ST    R6,ABUFFER                                                       
         USING WSEDATAD,R6         ENTRY DSECT                                  
         MVC   WSEDATA,FFS         MARK LAST ENTRY                              
*                                                                               
         L     RF,12(R1)                                                        
         ST    RF,ACXREC                                                        
*                                                                               
         L     RF,16(R1)           GET COMFACS ROUTINES                         
         USING COMFACSD,RF                                                      
         MVC   ADATAMGR,CDATAMGR                                                
         MVC   ADATCON,CDATCON                                                  
         MVC   AXSORT,CXSORT                                                    
         MVC   ASCANNER,CSCANNER                                                
*&&US*&& MVC   ADEMAND,CDEMAND                                                  
*                                                                               
         L     RF,CCALLOV                                                       
         DROP  RF                                                               
         ICM   R0,15,=X'D9000A00'                                               
         GOTO1 (RF),DMCB,0,(R0)                                                 
         MVC   ABOOKVAL,0(R1)                                                   
*                                                                               
         MVC   WRKFID,WRKFIL       SET WKFILE DATA                              
*                                                                               
         TBIN  SECS                GET TIME & DATE                              
         SR    R0,R0                                                            
         D     R0,=F'600'                                                       
         ST    R1,TIMENOW          TIME NOW BIN 10MINS                          
*                                                                               
         GOTO1 ADATCON,DMCB,(5,0),(30,TODAYC)                                   
*                                                                               
         GOTO1 ADATAMGR,DMCB,SHMUSS,ATTACH,MEMORY,0,0                           
         ICM   R1,15,DMCB+12                                                    
         JZ    *+2                 DEATH - CANNOT ATTACH                        
         ST    R1,FIWSHA                                                        
*                                                                               
         XC    AWRKFLST,AWRKFLST                                                
         GOTO1 ADATAMGR,WKDMCB,(X'00',=C'GLIST'),WRKFID,NDX,SAVE,ACXREC         
         ICM   RE,15,NXUSRINF                                                   
         CLI   0(RE),0             CHECK NUM OF WRKF FILES                      
         BNE   *+6                                                              
         DC    H'0'                DIE IF NOT IN MY RANGE                       
         CLI   0(RE),17                                                         
         BNH   *+6                                                              
         DC    H'0'                                                             
*                                                                               
         SR    R1,R1               R1=NUM OF FILES IN LIST                      
         IC    R1,0(RE)                                                         
         SR    R0,R0                                                            
         IC    R0,1(RE)                                                         
         AR    R1,R0                                                            
         LA    R1,2(R1)            ADD TWO FOR HDR AND TRL                      
         SLL   R1,3                                                             
         BCTR  R1,0                                                             
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   WRKFLST(0),0(RE)    COPY WRKF LIST TO MY OWN AREA                
         XC    WRKFLSTX,WRKFLSTX   SET END OF MAXIMUM LIST                      
         EJECT                                                                  
*                                                                               
         XC    NDX,NDX                                                          
         CLI   WSFILE,X'FF'        TEST FOR SEARCH ALL QUEUES                   
         BE    SRCH0A                                                           
         CLI   WSFILE,0            SEE IF SPECIFIC QUEUE                        
         BE    *+14                                                             
         MVC   WRKFID+4(1),WSFILE SET SPECIFIC                                  
         B     SRCH0B                                                           
*                                                                               
         TM    WSSRCID,X'80'       IF GENERIC ID SEARCH ALL                     
         BO    SRCH0A                                                           
*                                                                               
         MVC   NXSRCID,WSSRCID     WRKF ID FROM SPECIFIC USER ID                
         GOTO1 ADATAMGR,WKDMCB,(0,=C'GFILE'),WRKFID,NDX,SAVE,ACXREC             
         MVC   WRKFID,NXUSRINF                                                  
         B     SRCH0B                                                           
*                                                                               
SRCH0A   MVI   WSFILE,X'FF'        SET TO ALL QUEUES IF NOT ALREADY             
         LA    RE,WRKFLST+8        POINT TO FIRST WRKF FILE                     
         ST    RE,AWRKFLST                                                      
         MVC   WRKFID+4(1),1(RE)   SET WRKF ID FROM LIST                        
*                                                                               
SRCH0B   MVI   FIND,1              SET SEARCHING PART1 INDEX                    
         GOTO1 ADATAMGR,WKDMCB,(0,=C'BUFFER'),WRKFID                            
         CLI   8(R1),0                                                          
         BE    *+6                                                              
         DC    H'0'                                                             
         L     R1,ACXREC                                                        
         MVC   BUFFDATA,0(R1)      SAVE V(W_TAB)/V(W_FILE)                      
*                                                                               
         LA    R5,NDX                                                           
         USING W_RECD,R5           R5=A(WRKFIL INDEX ENTRY)                     
*                                                                               
         SAM31                                                                  
         MVC   FIWRES,SPACES                                                    
         MVC   FIWRES(5),WRKFID       WORKER FILE                               
         BRAS  RE,FIRSET           SET SHARED MEMORY ADDRESSES                  
         JNE   *+2                 DEATH - INVALID WORKER FILE                  
         MVC   FIWNDA,FIWP1A       START WITH FIRST INDEX                       
         B     SRCH020                                                          
*                                                                               
SRCH010  SAM31                                                                  
         CLI   FIND,2                                                           
         BE    SRCH030                                                          
*                                                                               
         BRAS  RE,FIRNSN           GET NEXT PART1 NODE                          
         BNE   SRCH050                                                          
SRCH020  L     R1,FIWNDA                                                        
         USING SI1PARD,R1                                                       
         MVC   NDX(L'W_INDEX),SI1NDX                                            
         B     SRCH060                                                          
*                                                                               
SRCH030  BRAS  RE,FIRNSN2          GET NEXT PART2 NODE                          
         BNE   SRCH050                                                          
SRCH040  L     R1,FIWNDA                                                        
         USING SI2PARD,R1                                                       
         MVC   NDX(L'W_INDEX),SI2NDX                                            
         B     SRCH060                                                          
*                                                                               
SRCH050  SAM24                                                                  
         B     SRCHXX              END OF INDEX PART1S OR PART2S                
         DROP  R1                                                               
                                                                                
SRCH060  BRAS  RE,FIRNC            A(NODE) TO A(CI)                             
         MVC   NXCIADDR,FIWCIA                                                  
         SAM24                                                                  
*                                                                               
SRCH2    OC    W_KEY,W_KEY         IGNORE PURGED                                
         BZ    SRCHX                                                            
         CLI   W_STAT,W_STPU       IGNORE PURGED                                
         BE    SRCHX                                                            
         CLI   W_AGERT,X'FF'       TEST IF TEMPORARY                            
         BE    SRCH2D              COUNT AS NOT ACTIVE & NOT AVAIL              
*                                                                               
SRCH2A   TM    W_STAT,W_STAC+W_STKE ACTIVE KEEPS DON'T TEST RETAIN              
         BO    SRCH2B                                                           
*                                                                               
         MVC   HALF,W_AGERD                                                     
         TM    W_ATTB,W_ATNCD                                                   
         BO    SRCH2A1                                                          
         GOTO1 ADATCON,DMCB,(2,HALF),(30,HALF)                                  
*                                                                               
SRCH2A1  CLC   HALF,TODAYC         TEST RETAIN DATE WITH TODAY                  
         BL    SRCH2F                                                           
         BH    SRCH2B                                                           
         CLI   W_AGERT,X'FE'       TEST TIME NOT AVAILABLE                      
         BE    SRCH2B                                                           
         CLC   W_AGERT,TIMENOW+3   TEST RETAIN TIME VALUE                       
         BL    SRCH2F                                                           
*                                                                               
SRCH2B   TM    W_STAT,W_STKE       UNEXPIRED KEEPS ALWAYS COUNT ACTIVE          
         BO    *+12                                                             
         TM    W_STAT,W_STAC       TEST FOR ACTIVE CI                           
         BZ    SRCH2D                                                           
         CLI   FIND,1              TEST PART 1 OR 2                             
         BNE   *+14                                                             
         AP    WSC3,=P'1'          BUMP PART1 ACTIVE                            
         B     SRCH2D                                                           
         AP    WSC4,=P'1'          BUMP PART2 ACTIVE                            
*                                                                               
SRCH2D   CLI   FIND,1              TEST PART 1 OR 2                             
         BNE   SRCH2E                                                           
         AP    WSC1,=P'1'          BUMP PART1 NOTAVAIL                          
         B     SRCH2G                                                           
SRCH2E   AP    WSC2,=P'1'          BUMP PART2 NOTAVAIL                          
         B     SRCHX                                                            
*                                                                               
SRCH2F   TM    WSFLAGS,WSFEXPD     DO WE WANT EXPIRED                           
         BZ    SRCHX                                                            
*                                                                               
SRCH2G   EQU   *                                                                
SRCH3    OC    WSSRCID,WSSRCID     TEST USER ID DEFINED                         
         BZ    SRCH3G                                                           
         TM    WSSRCID,X'80'       TEST GENERIC USER ID                         
         BZ    SRCH3B                                                           
         CLI   AGENIDS,X'FF'       TEST VGENIDS PRESENT                         
         BE    SRCHX                                                            
         GOTO1 AGENIDS,DMCB,WSSRCID,ADATAMGR                                    
         BNE   SRCHX                                                            
         LM    RE,RF,0(R1)         RE=N'ENTRIES,RF=A(ENTRIES)                   
         CLC   W_USRID,0(RF)                                                    
         BE    SRCH3G              MATCH FOUND                                  
         LA    RF,2(RF)                                                         
         BCT   RE,*-14                                                          
         B     SRCHX               NO MATCH ON GENERIC                          
*                                                                               
SRCH3B   CLC   W_USRID,WSSRCID     ID WAS SPECIFIED                             
         BNE   SRCHX                                                            
         CLI   W_STAT,W_STPU       IGNORE PURGED                                
         BE    SRCHX                                                            
*                                                                               
SRCH3G   OC    WSCLASN,WSCLASN     FILTER ON CLASS                              
         BZ    SRCH3GA                                                          
         LA    R1,8                WSCLASN IS 8 CHRS                            
         LA    RF,WSCLASN-1(R1)                                                 
         CLC   W_CLASS,0(RF)       TEST FOR EXCLUDE                             
         BE    SRCHX                                                            
         BCT   R1,*-14                                                          
SRCH3GA  OC    WSCLAS,WSCLAS       ZERO MEANS INCLUDE ALL                       
         BZ    SRCH3H                                                           
         LA    R1,8                WSCLAS IS 8 CHRS                             
         LA    RF,WSCLAS-1(R1)                                                  
         CLC   W_CLASS,0(RF)       TEST FOR INCLUDE                             
         BE    SRCH3H                                                           
         BCT   R1,*-14                                                          
         B     SRCHX                                                            
*                                                                               
SRCH3H   OC    WSSYSPRG(4),WSSYSPRG   TEST ALL                                  
         BE    SRCH3I                                                           
         LA    R0,4                                                             
         LA    RF,WSSYSPRG                                                      
         LA    RE,W_SYSPRG                                                      
SRCH3HA  CLI   0(RF),C'*'          * IS WILDCARD                                
         BE    SRCH3HB                                                          
         CLI   0(RF),X'0'          ZERO ALSO                                    
         BE    SRCH3HB                                                          
         CLC   0(1,RE),0(RF)       ELSE MUST MATCH                              
         BNE   SRCHX                                                            
SRCH3HB  LA    RF,1(RF)                                                         
         LA    RE,1(RE)                                                         
         BCT   R0,SRCH3HA          TEST NEXT CHAR                               
*                                                                               
SRCH3I   CLI   WSDAY,0             ZERO DAY MATCHES ALL                         
         BE    SRCH3X                                                           
         CLC   WSDAY,W_DAY                                                      
         BNE   SRCHX                                                            
*                                                                               
SRCH3X   DS    0H                                                               
         AP    WSR,=P'1'           BUMP TOTAL REPORTS                           
         TM    W_STAT,W_STAC                                                    
         BZ    *+10                                                             
         AP    WSA,=P'1'           BUMP ACTIVE                                  
         TM    W_STAT,W_STHO                                                    
         BZ    *+10                                                             
         AP    WSH,=P'1'           BUMP HOLD                                    
         CLI   W_AGERT,X'FE'                                                    
         BNE   *+10                                                             
         AP    WSG,=P'1'           BUMP PRINTING                                
*        TM    W_STAT,W_STPR                                                    
*        BZ    *+10                                                             
*        AP    WSD,=P'1'           BUMP PRINTED                                 
         TM    W_STAT,W_STSE                                                    
         BZ    *+10                                                             
         AP    WSE,=P'1'           BUMP SENT                                    
         TM    W_STAT,W_STKE                                                    
         BZ    *+10                                                             
         AP    WSK,=P'1'           BUMP KEEP                                    
*                                                                               
WKSRCH4A SR    R1,R1               ANY ATTRIBS TO EXCLUDE                       
         IC    R1,WSATTBN                                                       
         EX    R1,*+8                                                           
         B     *+8                                                              
         TM    W_ATTB,0            IF ANY BITS MATCH EXCLUDE IT                 
         BNZ   SRCHX                                                            
         ICM   R1,1,WSATTB                                                      
         BZ    WKSRCH4B            ZERO MEANS INCLUDE ALL                       
         EX    R1,*+8                                                           
         B     *+8                                                              
         TM    W_ATTB,0            IF ANY BITS MATCH INCLUDE IT                 
         BZ    SRCHX                                                            
*                                                                               
WKSRCH4B SR    R1,R1               ANY STATUS TO EXCLUDE                        
         IC    R1,WSSTATN                                                       
         EX    R1,*+8                                                           
         B     *+8                                                              
         TM    W_STAT,0            IF ANY BITS MATCH EXCLUDE IT                 
         BNZ   SRCHX                                                            
         ICM   R1,1,WSSTAT                                                      
         BZ    WKSRCH4C            ZERO MEANS INCLUDE ALL                       
         EX    R1,*+8                                                           
         B     *+8                                                              
         TM    W_STAT,0            IF ANY BITS MATCH INCLUDE IT                 
         BZ    SRCHX                                                            
*                                                                               
WKSRCH4C LA    R1,8                WSTYPEN IS 8 CHRS                            
         LA    RF,WSTYPEN-1(R1)                                                 
         CLI   0(RF),0             IGNORE ZEROS                                 
         BE    *+14                                                             
         CLC   W_TYPE,0(RF)        TEST FOR EXCLUDE                             
         BE    SRCHX                                                            
         BCT   R1,*-22                                                          
WKSRCH4D CLI   WSTYPE,0            IGNORE IF 1ST IS ZERO                        
         BE    WKSRCH5                                                          
         LA    R1,8                WSTYPE IS 8 CHRS                             
         LA    RF,WSTYPE-1(R1)                                                  
         CLI   0(RF),0             IGNORE ZEROS                                 
         BE    *+14                                                             
         CLC   W_TYPE,0(RF)        TEST FOR INCLUDE                             
         BE    WKSRCH5                                                          
         BCT   R1,*-22                                                          
         B     SRCHX                                                            
*                                                                               
WKSRCH5  CLI   WSAGESV,0           TEST FOR NO LIMIT                            
         BE    WKSRCH5X                                                         
         SR    R1,R1                                                            
         ICM   R1,1,WSAGESF        GET BC FILTER IN R1                          
         BZ    WKSRCH5X                                                         
         CLC   W_AGES,WSAGESV      TEST AGE FILTER                              
         EX    R1,*+8                                                           
         B     SRCHX                                                            
         BC    0,WKSRCH5X                                                       
WKSRCH5X EQU   *                                                                
*                                                                               
WKSRCH6  OC    WSAGELV,WSAGELV     TEST LIVE DATE                               
         BZ    WKSRCH6X                                                         
         SR    R1,R1                                                            
         ICM   R1,1,WSAGELF        GET BC FILTER IN R1                          
         BZ    WKSRCH6X                                                         
*                                                                               
         MVC   HALF,W_AGELD                                                     
         TM    W_ATTB,W_ATNCD                                                   
         BO    WKSRCH61                                                         
         ST    R1,FULL                                                          
         GOTO1 ADATCON,DMCB,(2,HALF),(30,HALF)                                  
         L     R1,FULL                                                          
*                                                                               
WKSRCH61 CLC   HALF,WSAGELV        TEST CDATE FILTER                            
         EX    R1,*+8                                                           
         B     SRCHX                                                            
         BC    0,WKSRCH6X                                                       
WKSRCH6X EQU   *                                                                
*                                                                               
WKSRCH7  OC    WSAGEDV,WSAGEDV     TEST DEAD DATE                               
         BZ    WKSRCH7X                                                         
         SR    R1,R1                                                            
         ICM   R1,1,WSAGEDF        GET BC FILTER IN R1                          
         BZ    WKSRCH7X                                                         
*                                                                               
         MVC   HALF,W_DATED                                                     
         TM    W_ATTB,W_ATNCD                                                   
         BO    WKSRCH71                                                         
         ST    R1,FULL                                                          
         GOTO1 ADATCON,DMCB,(2,HALF),(30,HALF)                                  
         L     R1,FULL                                                          
*                                                                               
WKSRCH71 CLC   HALF,WSAGEDV        TEST CDATE FILTER                            
         EX    R1,*+8                                                           
         B     SRCHX                                                            
         BC    0,WKSRCH7X                                                       
WKSRCH7X EQU   *                                                                
*                                                                               
WKSRCH8  OC    WSAGERV,WSAGERV     TEST RETAIN DATE                             
         BZ    WKSRCH8X                                                         
         SR    R1,R1                                                            
         ICM   R1,1,WSAGERF        GET BC FILTER IN R1                          
         BZ    WKSRCH8X                                                         
*                                                                               
         MVC   HALF,W_AGERD                                                     
         TM    W_ATTB,W_ATNCD                                                   
         BO    WKSRCH81                                                         
         ST    R1,FULL                                                          
         GOTO1 ADATCON,DMCB,(2,HALF),(30,HALF)                                  
         L     R1,FULL                                                          
*                                                                               
WKSRCH81 CLC   HALF,WSAGERV        TEST CDATE FILTER                            
         EX    R1,*+8                                                           
         B     SRCHX                                                            
         BC    0,WKSRCH8X                                                       
WKSRCH8X EQU   *                                                                
*                                                                               
*&&US                                                                           
         L     R1,APARM            CHECK DEMO FILE ACCESS                       
         CLI   0(R1),C'D'          DEMO CHECK INDICATED                         
         BNE   DMOACCX             NO - DON'T BOTHER                            
*                                                                               
         XC    WORK,WORK                                                        
*                                                                               
*----------------------------------------------------------------------         
* CHECK CONNECTED ID HAS ACCESS TO DEMO FILE                                    
*   PROBABLY BELONGS SOMEWHERE ELSE BUT THIS WORKS                              
*                                                                               
* WORKER FILE NAME FORMAT                                                       
*                                                                               
*   CL3   - MARKET                   -  W_SYSPRG                                
*   CL1   - BOOK TYPE                -  W_SUBPRG                                
*   PWOS1 - BOOK SEASON(SEE BKNTAB) \_  W_DAY                                   
*   PWOS1 - (BOOK YEAR)             /                                           
*   CL1   - SURVEY REGION            -  W_CLASS (NOT USED FOR ANYTHING)         
*----------------------------------------------------------------------         
         MVC   WORK(1),W_DAY                                                    
         NI    WORK,X'F0'          FIRST DIGIT IS BOOK MONTH                    
         LA    R1,BKNTAB                                                        
DMOACC02 CLI   0(R1),X'FF'                                                      
         BE    SRCHX               INVALID BOOK - SKIP FILE                     
         CLC   0(1,R1),WORK                                                     
         BE    *+12                                                             
         LA    R1,L'BKNTAB(R1)                                                  
         B     DMOACC02                                                         
         MVC   WORK2+8(3),1(R1)    COPY BOOK MONTH                              
*                                                                               
         MVC   WORK(1),W_DAY                                                    
         NI    WORK,X'0F'          SECOND DIGIT IS YEAR                         
*                                                                               
         ZIC   R0,WORK                                                          
*                                  GET THIS YEAR                                
         GOTO1 ADATCON,DMCB,(5,0),(15,WORK+1)                                   
*                                                                               
         ZAP   DUB,=P'0'           GET YEAR IN BINARY                           
         MVO   DUB,WORK+2(1)                                                    
         CVB   R1,DUB                                                           
*                                                                               
         NI    WORK+2,X'0F'        GET THE LAST DIGIT THIS YEAR                 
         ZIC   RE,WORK+2           PWOS IS BINARY IN SINGLE DIGIT               
*                                                                               
         AHI   RE,-3               KEEP 3 YEARS OF HISTORY                      
         AHI   R1,-3                                                            
*                                                                               
         CHI   R1,0                FIX FOR CENTURY CHANGES                      
         BH    *+8                                                              
         AHI   R1,100                                                           
*                                                                               
         CHI   RE,0                FIX FOR CENTURY CHANGES                      
         BH    *+8                                                              
         AHI   RE,10                                                            
*                                                                               
         CR    R0,RE               IF BOOK YEAR LESS THAN 3 YEAR DATE           
         BH    *+8                  CORRECT FORWARD 10 YEARS                    
         AHI   R0,10                                                            
         SR    R0,RE               CORRECT TO BASE YEAR                         
*                                                                               
         AR    R0,R1                                                            
*                                                                               
         CHI   R0,100              CONVERT TO 2 CHAR YEAR                       
         BL    *+8                                                              
         AHI   R0,-100                                                          
*                                                                               
         EDIT  (R0),(2,WORK2+8+3),FILL=0                                        
*                                                                               
         MVI   WORK2,8+10          FIELD LENGTH                                 
         MVI   WORK2+5,5           LENGTH                                       
*                                                                               
         GOTO1 ABOOKVAL,DMCB,(C'A',WORK2),(1,WORK4),(0,ASCANNER),0              
         CLI   4(R1),1                                                          
         BNE   SRCHX               INVALID BOOK                                 
*                                                                               
         LA    RE,ARB2DDS          CONVERT ARB TO DDS MARKET                    
DMOACC10 DS    0H                                                               
         CLI   0(RE),X'FF'                                                      
         BE    SRCHX               NO CONVERSION FOUND - SKIP                   
         CLC   W_SYSPRG,0(RE)                                                   
         BE    *+12                                                             
         LA    RE,L'ARB2DDS(RE)                                                 
         B     DMOACC10                                                         
         MVC   WORK3(3),3(RE)                                                   
*                                                                               
         XC    DBLOCK,DBLOCK       BUILD DBLOCK                                 
         MVC   DBFILE,=C'TP '                                                   
         LA    R1,IOAREA                                                        
         ST    R1,DBAREC                                                        
         MVI   DBSELSRC,C'A'                                                    
         MVI   DBSELMED,C'R'                                                    
         MVC   DBSELBK,WORK4+1     BOOK W/O RATING SERVICE                      
*                                                                               
         CLI   W_SUBPRG,C'E'       BLACK BOOK TYPE?                             
         BNE   *+8                 NO                                           
         MVI   DBBTYPE,C'B'                                                     
*                                                                               
         CLI   W_SUBPRG,C'F'       HISPANIC BOOK TYPE?                          
         BNE   *+8                 NO                                           
         MVI   DBBTYPE,C'H'                                                     
*                                                                               
         L     R1,APARM                                                         
         L     R1,20(R1)                                                        
         MVC   DBSELAGY,0(R1)                                                   
*                                                                               
         MVC   DBSELALF,WORK3                                                   
*                                                                               
         L     R1,APARM                                                         
         LA    R1,16(R1)                                                        
         MVC   DBCOMFCS,0(R1)                                                   
         MVI   DBFUNCT,DBTSTACS                                                 
*                                                                               
         GOTO1 ADEMAND,DMCB,DBLOCK,0,0                                          
         CLI   DBERROR,0           ALLOWED ACCESS?                              
         BNE   SRCHX               NO                                           
*                                                                               
DMOACCX  DS    0H                                                               
*----------------------------------------------------------------------         
*&&                                                                             
         TM    WSFLAGS,WSFTOTAL    TEST FOR TOTALS                              
         BZ    SRCHT                                                            
*                                                                               
TOTAL01  ST    R6,FULL             SAVE CURRENT R6                              
         L     R6,ABUFFER                                                       
TOTAL02  C     R6,FULL                                                          
         BE    SRCHT               NOT FOUND                                    
         CLC   WSEUSER,W_USRID     SAME USERID                                  
         BE    TOTAL03                                                          
         LA    R6,L'WSEDATA(R6)    NEXT ENTRY                                   
         B     TOTAL02                                                          
*                                                                               
TOTAL03  ICM   R1,15,WSESORT       BUMP COUNT                                   
         LA    R1,1(R1)                                                         
         STCM  R1,15,WSESORT                                                    
         L     R6,FULL             RESTORE R6                                   
         B     SRCHX                                                            
*                                                                               
SRCHT    OC    WSSMAX,WSSMAX                                                    
         BZ    SRCHTA                                                           
         CP    WST,WSSMAX          ROOM IN TABLE                                
         BL    SRCHU                                                            
         BH    SRCHT1                                                           
SRCHTA   CP    WST,QTMAX           ROOM IN TABLE                                
         BL    SRCHU               YES                                          
SRCHT1   AP    WSX,=P'1'           WSX=COUNT OF NO ROOM ENTRYS                  
         B     SRCHX                                                            
*                                                                               
SRCHU    XC    0(L'WSEDATA,R6),0(R6) SET UP TABLE ENTRY                         
         MVC   WSEUSER,W_USRID                                                  
         MVC   WSESTAT,W_STAT                                                   
         MVC   WSEATTB,W_ATTB                                                   
         MVC   WSEWRKF,WRKFID+4                                                 
         MVC   WSECIAD,NXCIADDR    RETURN CI ADDR                               
         TM    WSFLAGS,WSFREFNO                                                 
         BZ    *+10                                                             
         MVC   WSECIAD,W_FILENO    OR REF# IF REQUESTED                         
*                                                                               
SORT     TM    WSFLAGS,WSFTOTAL    TEST FOR TOTALS                              
         BNO   SORT1                                                            
         MVC   WSESORT,=F'1'       SET TO 1                                     
         B     SORTX                                                            
*                                                                               
SORT1    CLI   WSSORT,1                                                         
         BNE   SORT2                                                            
         MVC   WSESORT+0(4),W_SYSPRG     ALPHA SORT                             
         B     SORTX                                                            
*                                                                               
SORT2    CLI   WSSORT,3                                                         
         BH    SORT4                                                            
         MVC   WSESORT+0(2),W_AGELD      CREATE TIME SORT                       
         TM    W_ATTB,W_ATNCD                                                   
         BO    SORT21                                                           
         GOTO1 ADATCON,DMCB,(2,WSESORT),(30,WSESORT)                            
SORT21   MVC   WSESORT+2(2),W_AGELT                                             
         CLI   WSSORT,2                                                         
         BE    SORTX                                                            
         XC    WSESORT,FFS               -CREATE                                
         B     SORTX                                                            
*                                                                               
SORT4    CLI   WSSORT,5                                                         
         BH    SORT6                                                            
         MVC   WSESORT+2(2),W_DATED      SENT DATE SORT                         
         TM    W_ATTB,W_ATNCD                                                   
         BO    SORT41                                                           
         GOTO1 ADATCON,DMCB,(2,WSESORT+2),(30,WSESORT+2)                        
SORT41   CLI   WSSORT,4                                                         
         BE    SORTX                                                            
         XC    WSESORT,FFS               -SENT                                  
         B     SORTX                                                            
*                                                                               
SORT6    CLI   WSSORT,7                                                         
         BH    SORT8                                                            
         MVC   WSESORT+0(2),W_AGERD      RETAIN TIME SORT                       
         TM    W_ATTB,W_ATNCD                                                   
         BO    SORT61                                                           
         GOTO1 ADATCON,DMCB,(2,WSESORT),(30,WSESORT)                            
SORT61   MVC   WSESORT+2(1),W_AGERT                                             
         CLI   WSSORT,6                                                         
         BE    SORTX                                                            
         XC    WSESORT,FFS               -RETAIN                                
         B     SORTX                                                            
*                                                                               
SORT8    CLI   WSSORT,9                                                         
         BH    SORT10                                                           
         MVC   WSESORT+3(1),W_AGES       SIZE SORT                              
         CLI   WSSORT,8                                                         
         BE    SORTX                                                            
         XC    WSESORT,FFS               -RETAIN                                
         B     SORTX                                                            
*                                                                               
SORT10   DC    H'0'                INVALID SORT VALUE                           
*                                                                               
SORTX    LA    R6,L'WSEDATA(R6)    NEXT ENTRY                                   
         AP    WST,=P'1'                                                        
         B     SRCHX                                                            
*                                                                               
SRCHX    B     SRCH010             BUMP TO NEXT INDEX ENTRY                     
*                                                                               
SRCHXX   CLI   FIND,2                                                           
         BE    SRCHY                                                            
         TM    WSFLAGS,WSFPART2    TEST SEARCH PART 2S                          
         BZ    SRCHY                                                            
         MVI   FIND,2              SET TO SEARCH PART2 FOR SIZE ACTION          
         MVC   FIWNDA,FIWP2A       START WITH FIRST INDEX                       
         SAM31                                                                  
         B     SRCH040                                                          
*                                                                               
SRCHY    ICM   RE,15,AWRKFLST      TEST FOR MULTIPLE FILE SEARCH                
         BZ    SORT010                                                          
         LA    RE,8(RE)            BUMP TO NEXT FILE                            
         ST    RE,AWRKFLST                                                      
         CLI   0(RE),0             TEST FOR LAST FILE                           
         BE    SORT010                                                          
         MVC   WRKFID+4(1),1(RE)                                                
         MVC   FULL(3),WST         SAVE WST                                     
         LA    R7,L'WSCOUNT(R7)    POINT TO NEXT COUNT AREA                     
         SR    R1,R1               BUMP NO OF FILES SEARCHED                    
         IC    R1,WSFILES                                                       
         LA    R1,1(R1)                                                         
         STC   R1,WSFILES                                                       
         ZAP   0(3,R7),=P'0'       ZAP ALL THE COUNTS                           
         MVC   3(45,R7),0(R7)                                                   
         MVC   WST(3),FULL         RESTORE WST                                  
         B     SRCH0B              START AGAIN                                  
*                                                                               
SRCHURR  L     R1,APARM                                                         
         MVI   8(R1),X'80'         FLAG ERROR                                   
         B     EXITX                                                            
*                                                                               
SORT010  MVC   0(L'WSEDATA,R6),FFS SET END OF TABLE AND SORT ON KEY             
         XC    FULL,FULL                                                        
         ZAP   DUB,WST                                                          
         CVB   R6,DUB                                                           
         LA    R6,1(R6)                                                         
         CH    R6,=H'2'                                                         
         BNH   SORT01X                                                          
*                                                                               
         L     R0,ABUFFER                                                       
         XC    DMCB(24),DMCB                                                    
         MVI   DMCB+15,6           LENGTH                                       
         MVI   DMCB+19,0           DISPLACEMENT                                 
         TM    WSFLAGS,WSFXUSER                                                 
         BNO   SORT011             EXCLUDE USERID                               
*                                                                               
         MVI   DMCB+15,4           ON XSORT                                     
         MVI   DMCB+19,2                                                        
SORT011  GOTO1 AXSORT,DMCB,(R0),(R6),L'WSEDATA                                  
*                                                                               
SORT01X  EQU   *                                                                
EXITX    XMOD1                                                                  
*                                                                               
QTMAX    DC    PL3'800'            MAX NUMBER OF REPORTS                        
*                                                                               
SPACES   DC    16C' '                                                           
FFS      DC    16X'FF'                                                          
WRKFIL   DC    C'WRKFIL'                                                        
*                                                                               
SHMUSS   DC    CL8'SHMUSS'                                                      
ATTACH   DC    CL8'ATTACH'                                                      
MEMORY   DC    CL8'WRKF'                                                        
         LTORG                                                                  
                                                                                
***********************************************************************         
* BOOK NAME TABLE                                                               
***********************************************************************         
BKNTAB   DS    0XL4                                                             
         DC    X'10',C'FEB'                                                     
         DC    X'20',C'MAY'                                                     
         DC    X'30',C'JUL'                                                     
         DC    X'40',C'NOV'                                                     
         DC    X'FF'                                                            
                                                                                
***********************************************************************         
* ARB TO DDS MARKET CODE CONVERSION TABLE                                       
***********************************************************************         
*&&US                                                                           
         PRINT OFF                                                              
       ++INCLUDE ARB2DDSTB                                                      
         PRINT ON                                                               
         EJECT                                                                  
                                                                                
*&&                                                                             
***********************************************************************         
* SHARED MEMORY FILE INDEX ROUTINES                                             
***********************************************************************         
       ++INCLUDE SHFIR                                                          
         EJECT                                                                  
                                                                                
***********************************************************************         
* WORKING STORAGE                                                               
***********************************************************************         
WORKD    DSECT                                                                  
DUB      DS    D                                                                
FULL     DS    F                                                                
HALF     DS    H                                                                
BYTE     DS    X                                                                
FIND     DS    X                                                                
DMCB     DS    7F                  ONE EXTRA FOR SHMUSS CALL                    
WKDMCB   DS    6F                                                               
FLAG     DS    X                                                                
TODAYC   DS    XL2                                                              
*                                                                               
TIMENOW  DS    F                                                                
*                                                                               
ABUFFER  DS    A                                                                
APARM    DS    A                                                                
ADATAMGR DS    A                                                                
ADATCON  DS    A                                                                
AGENIDS  DS    A                                                                
AXSORT   DS    A                                                                
ACXREC   DS    A                                                                
ABOOKVAL DS    A                                                                
ASCANNER DS    A                                                                
ADEMAND  DS    A                                                                
*                                                                               
WORK     DS    0CL80                FOR BOOKVAL ETC                             
WORK1    DS    CL20                                                             
WORK2    DS    CL20                                                             
WORK3    DS    CL20                                                             
WORK4    DS    CL20                                                             
*                                                                               
SAVE     DS    360C                SAVE AREA FOR DATAMGR CALLS                  
*                                                                               
AWRKFLST DS    A                                                                
WRKFIDSV DS    CL8                 SAVED WRKF NAME                              
WRKFID   DS    CL8                 NAME OF WRKF FOR DMGR                        
WRKFLST  DS    0X                                                               
WRKFMAX  DS    X                   NUMBER OF WRKF FILES                         
         DS    X                                                                
WRKFFLG  DS    X                                                                
         DS    XL5                                                              
WRKFNTRY DS    17XL8                MAXIMUM OF 17 WRKF FILES                    
WRKFLSTX DS    XL8                                                              
*                                                                               
BUFFDATA DS    0XL12               WRKFIL DATA GIVEN BY BUFFER ACTION           
BUWKTAB  DS    V                                                                
BUWKFILE DS    V                                                                
BUSAVE   DS    XL4                                                              
*                                                                               
         DS    0F                                                               
NDX      DS    0XL40               INDEX ENTRY                                  
NXSRCID  DS    XL2                                                              
NXSYSPRG DS    CL3                                                              
NXSUBPRG DS    XL1                                                              
NXDAY    DS    CL1                                                              
NXCLASS  DS    CL1                                                              
NXFILENO DS    CL2                                                              
NXTYPE   DS    XL1                                                              
NXATTB   DS    XL1                                                              
NXSTAT   DS    XL1                                                              
NXSEQ    DS    XL1                                                              
NXAGES   DS    XL1                                                              
NXAGELD  DS    XL2                                                              
NXAGEDD  DS    XL2                                                              
NXAGERD  DS    XL2                                                              
NXAGERT  DS    XL1                                                              
NXAGELT  DS    XL2                                                              
*                                                                               
         DS    XL2                                                              
NXINFO   DS    XL2                                                              
NXFILNOX DS    XL2                                                              
NXCIADDR DS    XL2                                                              
NXFLAG   DS    XL1                                                              
         DS    XL1                                                              
NXUSRINF DS    XL8                                                              
         DS    CL24                                                             
*                                                                               
       ++INCLUDE SHFIW                                                          
       ++INCLUDE DEDBLOCK                                                       
*                                                                               
IOAREA   DS    2000X                                                            
*                                                                               
WORKX    EQU   *                                                                
         EJECT                                                                  
*DDCOMFACS                                                                      
*DDWKSCAND                                                                      
*DMWRKFD                                                                        
*DMWRKFK                                                                        
*SHFID                                                                          
*IHAASCB                                                                        
       ++INCLUDE DDCOMFACS                                                      
       ++INCLUDE DDWKSCAND                                                      
       ++INCLUDE DMWRKFD                                                        
       ++INCLUDE DMWRKFK                                                        
       ++INCLUDE SHFID                                                          
         IHAASCB LIST=YES                                                       
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'026DDWKSCAN  09/27/19'                                      
         END                                                                    
