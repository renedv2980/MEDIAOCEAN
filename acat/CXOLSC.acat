*          DATA SET CXOLSC     AT LEVEL 036 AS OF 02/03/95                      
*CATALP CXOLSC                                                                  
         TITLE 'CXOLSC - CONTROL EXTRACT - ACCOUNT SYSTEM OFFICE LIST'          
**********************************************************************          
* AL4 A(EXTRACT RECORD)                                              *          
* AL4 A(CONFILE RECORD)                                              *          
**********************************************************************          
         SPACE 1                                                                
         PRINT NOGEN                                                            
CXOLSC   CSECT                                                                  
         NMOD1 WORKX-WORKD,*CXOLSC*,RR=RA                                       
         USING WORKD,RC                                                         
         ST    R1,APARM                                                         
         ST    RA,RELO                                                          
         MVC   PARM,0(R1)          EXTRACT PARAMETER LIST                       
         LM    R2,R3,0(R1)                                                      
         USING CXOLSD,R2           R2=A(EXTRACT RECORD)                         
         USING CTUREC,R3           R3=A(CONFILE RECORD)                         
*                                                                               
CXOLEN   XC    CXOLSLEN,CXOLSLEN   RECORD LENGTH/TYPE/ENDOFREC                  
         MVC   CXOLSLEN(2),=AL2(CXOLSDL)                                        
         MVC   CXOLSTYP,CXOLSDQ                                                 
         MVI   CXOLSDX,MXTRTQ                                                   
         MVI   CXOLSDX+1,MXTRTQ                                                 
*                                                                               
CXOCMP   MVI   CXOLSAGY-1,MXTRTQ   AGENCY ALPHA ID                              
         MVC   CXOLSAGY,CTUKAGY                                                 
*                                                                               
CXOCOD   MVI   CXOLSCOD-1,MXTRTQ   OFFICE LIST CODE                             
         CLI   CTUKPROG,X'00'                                                   
         BE    *+14                                                             
         MVC   CXOLSCOD,CTUKPROG+1                                              
         B     *+10                                                             
         MVC   CXOLSCOD,CTUKPROG+2                                              
*                                                                               
CXOSUB   MVI   CXOLSSUB-1,MXTRTQ   OFFICE LIST SUB CODE                         
         CLI   CTUKPROG,X'00'                                                   
         BE    *+14                                                             
         MVC   CXOLSSUB,CTUKPROG+2                                              
         B     *+8                                                              
         MVI   CXOLSSUB,C' '                                                    
*                                                                               
         MVI   CXOLSNAM-1,MXTRTQ                                                
         MVC   CXOLSNAM,SPACES                                                  
*                                                                               
         LA    R4,CTUDATA          R4=A(FIRST ELEMENT)                          
*                                                                               
CXOELM   CLI   0(R4),0             GET ELEMENT FROM RECORD                      
         BE    CXONXTX                                                          
         CLI   0(R4),X'02'                                                      
         BE    CXONAM                                                           
CXONXT   SR    RF,RF               BUMP TO NEXT ELEMENT                         
         IC    RF,1(R4)                                                         
         CH    RF,=H'1'                                                         
         BNH   EXIT                EXIT IF FUNNY ELEMENT LENGTH                 
         AR    R4,RF                                                            
         B     CXOELM                                                           
*                                                                               
CXONAM   MVC   CXOLSNAM,2(R4)      OFFICE LIST NAME                             
         DC    H'0'                                                             
         SR    RF,RF                                                            
         IC    RF,1(R4)                                                         
         BCTR  RF,0                                                             
         BCTR  RF,0                                                             
         BCTR  RF,0                                                             
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   CXOLSNAM(0),2(R4)                                                
         TR    CXOLSNAM,MXTRT                                                   
         B     CXONXT                                                           
*                                                                               
CXONXTX  B     EXIT                                                             
*                                                                               
DELREC   L     R1,APARM            DELETE THIS RECORD                           
         MVI   8(R1),X'80'                                                      
         SPACE 1                                                                
EXIT     XMOD1 1                                                                
         EJECT                                                                  
         DS    0D                                                               
SPACES   DC    80C' '                                                           
         DS    0D                                                               
         LTORG                                                                  
         SPACE 1                                                                
       ++INCLUDE MXTRT                                                          
       ++INCLUDE CXRECID                                                        
         SPACE 1                                                                
         DS    0D                                                               
VHEXOUT  DC    V(HEXOUT)                                                        
VCRYPT   DC    V(CRYPT)                                                         
         EJECT                                                                  
* INCLUDE CXTRACT LOCAL DSECTS                                                  
         SPACE 1                                                                
       ++INCLUDE CXTRACTD                                                       
         EJECT                                                                  
WORKD    DSECT                                                                  
PARM     DS    0XL16               PARAMETER LIST                               
AXREC    DS    A                   A(EXTRACT RECORD)                            
CFREC    DS    A                   A(CONFILE RECORD)                            
         DS    A                                                                
         DS    A                                                                
*                                                                               
DMCB     DS    6F                                                               
DUB      DS    D                                                                
APARM    DS    A                   A(PARAMETER LIST)                            
RELO     DS    A                                                                
*                                                                               
WORKX    EQU   *                                                                
         EJECT                                                                  
       ++INCLUDE CXOLSD                                                         
         EJECT                                                                  
       ++INCLUDE CTGENFILE                                                      
         SPACE 1                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'036CXOLSC    02/03/95'                                      
         END                                                                    
