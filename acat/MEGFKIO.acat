*          DATA SET MEGFKIO    AT LEVEL 008 AS OF 05/01/02                      
*CATALP GFKIO                                                                   
         TITLE 'MEGFKIO - I/O ROUTINES FOR GFK DEMO FILE'                       
***********************************************************************         
*PARAMETER LIST SAME AS THAT FOR V(DMDMGR)                            *         
*P1 A(ACTION) HOB X'80'=READ FOR UPDATE FLAG                          *         
*P2 A(FILE) NOT REQUIRED AS ASSUMED TO BE GFKFIL                      *         
*P3 A(KEY)                                                            *         
*P4 A(RECORD)                                                         *         
***********************************************************************         
         PRINT NOGEN                                                            
GFKIO    CSECT                                                                  
         ENTRY GFKFIL                                                           
         NMOD1 WORKX-WORKD,**GFIO**                                             
         USING WORKD,RC                                                         
         LR    R2,R1               R2=A(CALLER'S DMCB)                          
         LM    R6,R7,8(R2)                                                      
         LA    R6,0(R6)            R6=A(CALLERS KEY)                            
         LA    R7,0(R7)            R7=A(CALLERS RECORD)                         
*                                                                               
GFKIO1   L     R8,ARPLLST          R8=A(FIRST RPL IF OFFLINE)                   
         USING GFRPLD,R8                                                        
         CLI   OFFLINE,C'N'                                                     
         BE    GFKIO2              ONLINE                                       
         BH    GFKIO3              OFFLINE                                      
         BAS   RE,INIT                                                          
         B     GFKIO1                                                           
*                                                                               
GFKIO2   L     RF,ASSB             R8=A(TASK RPL IF ONLINE)                     
         ICM   RF,15,SSBTKADR-SSBD(RF)                                          
         BNZ   *+6                                                              
         DC    H'0'                                                             
         L     RE,ATCB                                                          
         LH    R0,0(RE)            R0=WIDTH OF EACH TCB ENTRY                   
         SR    RF,RE                                                            
         SR    RE,RE                                                            
         DR    RE,R0                                                            
         LA    RF,1(RF)            RF=TASK NUMBER 1 THRU 35                     
         MH    RF,=AL2(GFRPLX-GFRPLD)                                           
         AR    R8,RF                                                            
*                                                                               
GFKIO3   LR    R3,R8               R3=A(GFKFIL RPL)                             
         USING IFGRPL,R3                                                        
         ST    R7,RPLAREA          SET A(RECORD)                                
*                                                                               
GFKIO4   MVI   8(R2),0             SET OK RETURN                                
         XC    FDBK,FDBK                                                        
         MVI   HIGH,C'N'                                                        
         MVI   UPDT,C'N'                                                        
         B     ACTNVAL                                                          
*                                                                               
EXIT     CLI   8(R2),0             EXIT WITH CC EQL IF VALID ACTION             
         XMOD1 1                                                                
         EJECT                                                                  
***********************************************************************         
*VALIDATE ACTION IN PARAM1 AND OPEN FILE IF NOT ALREADY OPEN          *         
***********************************************************************         
         SPACE 1                                                                
ACTNVAL  L     RF,=A(ACTNTAB)      VALIDATE ACTION IN FIRST PARAMETER           
         SR    R1,R1                                                            
         SR    RE,RE                                                            
         ICM   RE,7,1(R2)          RE=A(ACTION NAME) OR ACTION NUMBER           
         CH    RE,=Y(ACTNMAX)                                                   
         BH    ACTNV1              ACTION IS A NAME                             
         SH    RE,=H'1'                                                         
         BM    ACTNINV             ACTION IS A NUMBER IN LAST BYTE              
         MH    RE,=Y(L'ACTNTAB)                                                 
         AR    RF,RE               INDEX INTO ACTION TABLE                      
         B     ACTNV2                                                           
*                                                                               
ACTNV1   ICM   R1,1,1(RF)          SEARCH TABLE OF VALID ACTION NAMES           
         BZ    ACTNINV                                                          
         BCTR  R1,0                                                             
         EX    R1,*+8                                                           
         B     *+10                                                             
         CLC   2(0,RF),0(RE)                                                    
         BE    ACTNV2                                                           
         LA    RF,L'ACTNTAB(RF)                                                 
         B     ACTNV1                                                           
*                                                                               
ACTNV2   IC    R1,0(RF)            EXTRACT ACTION NUMBER AND FLAGS              
         STC   R1,ACTN                                                          
         STC   R1,SACTN            SAVE THIS ACTION                             
         MVC   ACTNFLGS,8(RF)                                                   
         SLL   R1,2                                                             
         L     R5,=A(ACTNRTN)      R5=A(ROUTINE) FOR ACTION                     
         L     R5,0(R1,R5)                                                      
         TM    ACTNFLGS+1,X'01'                                                 
         BOR   R5                  GO DIRECT TO OPEN/CLOSE ACTIONS              
         TM    OPNFLG,X'01'                                                     
         BOR   R5                                                               
         BAS   RE,OPN              DO A NORMAL OPEN ON FIRST CALL               
         BER   R5                                                               
         L     R0,FDBK                                                          
         STCM  R0,15,*+6                                                        
         DC    H'0',XL4'00',CL12' OPN ERR '                                     
         EJECT                                                                  
***********************************************************************         
*PROCESS EACH INDIVIDUAL ACTION AND SET CORRECT RETURN IN DMCB+8      *         
***********************************************************************         
         SPACE 1                                                                
ACTNGET  MVC   GFKEY(9),0(R6)      SET KEY FROM KEY                             
         TM    0(R2),X'80'                                                      
         BZ    *+8                                                              
         MVI   UPDT,C'Y'                                                        
         MVI   HIGH,C'Y'           SET TO DO KGE                                
         BAS   RE,GET                                                           
         BNE   ACTNGET1                                                         
         CLC   GFKEY(9),0(R7)      TEST IF KEY MATCHES                          
         BE    EXIT                YES                                          
         OI    8(R2),X'10'         NO SET NOT FOUND                             
         B     EXIT                                                             
ACTNGET1 CLI   FDBK+3,X'10'        TEST RECORD NOT FOUND                        
         BNE   *+12                                                             
         OI    8(R2),X'10'                                                      
         B     EXIT                                                             
         L     R0,FDBK                                                          
         STCM  R0,15,*+6                                                        
         DC    H'0',XL4'00',CL12' GET FDBK'                                     
         SPACE 2                                                                
ACTNGHI  MVC   GFKEY(9),0(R6)      SET KEY FROM KEY                             
         TM    0(R2),X'80'                                                      
         BZ    *+8                                                              
         MVI   UPDT,C'Y'                                                        
         MVI   HIGH,C'Y'                                                        
         BAS   RE,GET                                                           
         BE    EXIT                                                             
         CLI   FDBK+3,X'10'        TEST KEY HIGHER THAN THE MAXIMUM             
         BNE   *+12                                                             
         OI    8(R2),X'80'+X'10'                                                
         B     EXIT                                                             
         L     R0,FDBK                                                          
         STCM  R0,15,*+6                                                        
         DC    H'0',XL4'00',CL12' GHI FDBK'                                     
         SPACE 2                                                                
ACTNSEQ  BAS   RE,SEQ                                                           
         BE    EXIT                                                             
         CLI   FDBK+3,X'04'        TEST EOF                                     
         BNE   *+12                                                             
         OI    8(R2),X'80'+X'10'                                                
         B     EXIT                                                             
         L     R0,FDBK                                                          
         STCM  R0,15,*+6                                                        
         DC    H'0',XL4'00',CL12' SEQ FDBK'                                     
         SPACE 2                                                                
ACTNADD  MVC   GFKEY(9),0(R7)      SET KEY FROM RECORD                          
         SR    RF,RF                                                            
         ICM   RF,3,14(R7)         SET LEN FROM RECORD                          
         ST    RF,RPLRLEN                                                       
         BAS   RE,ADD                                                           
         BE    EXIT                                                             
*                                  TEST VSAM SPACE ALLOCATION REPORTED          
         CLC   FDBK+1(3),=XL3'000004'   IN REASON CODE BYTE (RETURN=0)          
         BE    EXIT                                                             
*                                                                               
         CLI   FDBK+3,X'08'        TEST DUPLICATE KEY                           
         BNE   *+12                                                             
         OI    8(R2),X'20'                                                      
         B     EXIT                                                             
         L     R0,FDBK                                                          
         STCM  R0,15,*+6                                                        
         DC    H'0',XL4'00',CL12' ADD FDBK'                                     
         SPACE 2                                                                
ACTNPUT  MVC   GFKEY(9),0(R7)      SET KEY FROM RECORD                          
         SR    RF,RF                                                            
         ICM   RF,3,14(R7)         SET LEN FROM RECORD                          
         ST    RF,RPLRLEN                                                       
         BAS   RE,PUT                                                           
         BE    EXIT                                                             
         L     R0,FDBK                                                          
         STCM  R0,15,*+6                                                        
         DC    H'0',XL4'00',CL12' PUT FDBK'                                     
         SPACE 2                                                                
ACTNDEL  MVC   GFKEY(9),0(R6)      SET KEY FROM KEY                             
         BAS   RE,DEL                                                           
         BE    EXIT                                                             
         L     R0,FDBK                                                          
         STCM  R0,15,*+6                                                        
         DC    H'0',XL4'00',CL12' DEL FDBK'                                     
         SPACE 2                                                                
ACTNOPN  TM    OPNFLG,X'01'        FILE MUST NOT ALREADY BE OPEN                
         BO    ACTNOPNE                                                         
         L     R4,AGFKACB          SET ACB FOR NORMAL MODE                      
         USING IFGACB,R4                                                        
         OI    ACBMACR1,ACBDIR     TURN ON MACRF=DIR                            
         OI    ACBMACR1,ACBIN      TURN ON MACRF=IN                             
         NI    ACBMACR1,255-ACBOUT TURN OFF MACRF=OUT                           
         NI    ACBMACR3,255-ACBSIS TURN OFF MACRF=SIS                           
         MVI   MODE,X'00'                                                       
         BAS   RE,OPN                                                           
         BE    EXIT                                                             
ACTNOPNE L     R0,FDBK                                                          
         STCM  R0,15,*+6                                                        
         DC    H'0',XL4'00',CL12' OPN ERR '                                     
         SPACE 2                                                                
ACTNOPU  TM    OPNFLG,X'01'        FILE MUST NOT ALREADY BE OPEN                
         BO    ACTNOPUE                                                         
         L     R4,AGFKACB          SET ACB FOR NORMAL MODE                      
         USING IFGACB,R4                                                        
         OI    ACBMACR1,ACBDIR     TURN ON MACRF=DIR                            
         NI    ACBMACR1,255-ACBIN  TURN OFF MACRF=IN                            
         OI    ACBMACR1,ACBOUT     TURN ON MACRF=OUT                            
         NI    ACBMACR3,255-ACBSIS TURN OFF MACRF=SIS                           
         MVI   MODE,X'01'          SET MODE IS UPDATIVE                         
         BAS   RE,OPN                                                           
         BE    EXIT                                                             
ACTNOPUE L     R0,FDBK                                                          
         STCM  R0,15,*+6                                                        
         DC    H'0',XL4'00',CL12' OPU ERR '                                     
         SPACE 2                                                                
ACTNOPL  TM    OPNFLG,X'01'        FILE MUST NOT ALREADY BE OPEN                
         BO    ACTNOPLE                                                         
         L     R4,AGFKACB          SET ACB FOR LOAD MODE                        
         USING IFGACB,R4                                                        
         NI    ACBMACR1,255-ACBDIR TURN OFF MACRF=DIR                           
         NI    ACBMACR1,255-ACBIN  TURN OFF MACRF=IN                            
         OI    ACBMACR1,ACBOUT     TURN ON MACRF=OUT                            
         OI    ACBMACR3,ACBSIS     TURN ON MACRF=SIS                            
         MVI   MODE,X'02'          SET MODE IS LOAD                             
         BAS   RE,OPN                                                           
         BE    EXIT                                                             
ACTNOPLE L     R0,FDBK                                                          
         STCM  R0,15,*+6                                                        
         DC    H'0',XL4'00',CL12' OPL ERR '                                     
         SPACE 2                                                                
ACTNCLS  BAS   RE,CLS                                                           
         BE    EXIT                                                             
         L     R0,FDBK                                                          
         STCM  R0,15,*+6                                                        
         DC    H'0',XL4'00',CL12' CLS ERR '                                     
         SPACE 2                                                                
ACTNINV  DC    H'0',CL16'INVALID ACTION'                                        
         EJECT                                                                  
***********************************************************************         
*SUBROUTINES TO INTERFACE TO VSAM MACROS                              *         
***********************************************************************         
         SPACE 1                                                                
OPN      ST    RE,SAVERE           OPEN GFK FILE                                
         XC    FDBK,FDBK                                                        
         TM    OPNFLG,X'01'        EXIT IF ALREADY OPEN                         
         BOR   RE                                                               
         L     R4,AGFKACB                                                       
         USING IFGACB,R4                                                        
         OPEN  ((R4))                                                           
         STC   RF,FDBK+1                                                        
         LTR   RF,RF                                                            
         BZ    OPNOK                                                            
         MVC   FDBK+3(1),ACBERFLG                                               
         B     OPNNOK                                                           
OPNOK    OI    OPNFLG,X'01'        EXIT WITH CC EQL IF OPENED OK                
         L     RE,SAVERE                                                        
         CR    RE,RE                                                            
         BR    RE                                                               
OPNNOK   OI    OPNFLG,X'80'        EXIT WITH CC NEQ IF OPEN FAILURE             
         L     RE,SAVERE                                                        
         CR    RE,RF                                                            
         BR    RE                                                               
         SPACE 2                                                                
CLS      ST    RE,SAVERE           CLOSE GFK FILE                               
         XC    FDBK,FDBK                                                        
         TM    OPNFLG,X'01'        EXIT IF ALREADY CLOSED                       
         BZ    RE                                                               
         L     R4,AGFKACB                                                       
         USING IFGACB,R4                                                        
         CLOSE ((R4))                                                           
         STC   RF,FDBK+1                                                        
         LTR   RF,RF                                                            
         BZ    CLSOK                                                            
         MVC   FDBK+3(1),ACBERFLG                                               
         B     CLSNOK                                                           
CLSOK    NI    OPNFLG,255-X'01'    EXIT WITH CC EQL IF CLOSED OK                
         L     RE,SAVERE                                                        
         CR    RE,RE                                                            
         BR    RE                                                               
CLSNOK   OI    OPNFLG,X'80'        EXIT WITH CC NEQ IF FAILURE                  
         L     RE,SAVERE                                                        
         CR    RE,RF                                                            
         BR    RE                                                               
         SPACE 2                                                                
ADD      ST    RE,SAVERE           ADD RECORD WITH KEY IN GFKEY                 
         XC    FDBK,FDBK                                                        
         OI    RPLOPT1,RPLDIR      SET OPTCD=DIR                                
         NI    RPLOPT1,255-RPLSEQ                                               
         NI    RPLOPT2,255-RPLUPD  SET OPTCD=NUP                                
         TM    MODE,X'02'                                                       
         BZ    ADD1                                                             
         OI    RPLOPT1,RPLSEQ      SET OPTCD=SEQ FOR LOAD MODE                  
         NI    RPLOPT1,255-RPLDIR                                               
ADD1     XC    GFECB,GFECB                                                      
         PUT   RPL=(R3)                                                         
         LTR   RF,RF                                                            
         BNZ   ADD2                                                             
         BAS   RE,CHECK            WAIT FOR I/O COMPLETION                      
ADD2     MVC   FDBK+1(3),RPLFDBK                                                
         OC    FDBK,FDBK           EXIT WITH CC EQL IF ALL OK                   
         L     RE,SAVERE                                                        
         BR    RE                                                               
         SPACE 2                                                                
DEL      ST    RE,SAVERE           DEL RECORD WITH KEY IN GFKEY                 
         XC    FDBK,FDBK                                                        
         OI    RPLOPT1,RPLDIR      SET OPTCD=DIR                                
         NI    RPLOPT1,255-RPLSEQ                                               
         OI    RPLOPT2,RPLUPD      SET OPTCD=UPD                                
DEL1     XC    GFECB,GFECB                                                      
         ERASE RPL=(R3)                                                         
         LTR   RF,RF                                                            
         BNZ   DEL2                                                             
         BAS   RE,CHECK            WAIT FOR I/O COMPLETION                      
DEL2     MVC   FDBK+1(3),RPLFDBK                                                
         OC    FDBK,FDBK           EXIT WITH CC EQL IF ALL OK                   
         L     RE,SAVERE                                                        
         BR    RE                                                               
         SPACE 2                                                                
GET      ST    RE,SAVERE           GET RECORD WITH KEY IN GFKEY                 
         XC    FDBK,FDBK                                                        
         OI    RPLOPT1,RPLDIR                                                   
         NI    RPLOPT1,255-RPLSEQ  SET OPTCD=DIR                                
         OI    RPLOPT2,RPLNSP      SET OPTCD=NSP (NOTE STRING POSN)             
         NI    RPLOPT2,255-RPLUPD                                               
         NI    RPLOPT1,255-RPLKGE  SET OPTCD=KEQ                                
         CLI   HIGH,C'Y'                                                        
         BNE   *+8                                                              
         OI    RPLOPT1,RPLKGE      SET OPTCD=KGE                                
         CLI   UPDT,C'Y'                                                        
         BNE   *+12                                                             
         OI    RPLOPT2,RPLUPD      SET OPTCD=UPD                                
         NI    RPLOPT2,255-RPLNSP                                               
GET1     XC    GFECB,GFECB                                                      
         GET   RPL=(R3)                                                         
         LTR   RF,RF                                                            
         BNZ   GET2                                                             
         BAS   RE,CHECK            WAIT FOR I/O COMPLETION                      
GET2     MVC   FDBK+1(3),RPLFDBK                                                
         OC    FDBK,FDBK           EXIT WITH CC EQL IF ALL OK                   
         L     RE,SAVERE                                                        
         BR    RE                                                               
         SPACE 2                                                                
PUT      ST    RE,SAVERE           PUT RECORD WITH KEY IN GFKEY                 
         XC    FDBK,FDBK                                                        
         OI    RPLOPT1,RPLDIR      SET OPTCD=DIR                                
         NI    RPLOPT1,255-RPLSEQ                                               
         OI    RPLOPT2,RPLUPD      SET OPTCD=UPD                                
PUT1     XC    GFECB,GFECB                                                      
         PUT   RPL=(R3)                                                         
         LTR   RF,RF                                                            
         BNZ   PUT2                                                             
         BAS   RE,CHECK            WAIT FOR I/O COMPLETION                      
PUT2     MVC   FDBK+1(3),RPLFDBK                                                
         OC    FDBK,FDBK           EXIT WITH CC EQL IF ALL OK                   
         L     RE,SAVERE                                                        
         BR    RE                                                               
         SPACE 2                                                                
SEQ      ST    RE,SAVERE           GET NEXT SEQUENTIAL RECORD                   
         XC    FDBK,FDBK                                                        
         OI    RPLOPT1,RPLSEQ      SET OPTCD=SEQ                                
         NI    RPLOPT1,255-RPLDIR                                               
         OI    RPLOPT2,RPLNSP      SET OPTCD=NSP (NOTE STRING POSN)             
         NI    RPLOPT2,255-RPLUPD                                               
         CLI   UPDT,C'Y'                                                        
         BNE   *+12                                                             
         OI    RPLOPT2,RPLUPD      SET OPTCD=UPD                                
         NI    RPLOPT2,255-RPLNSP                                               
SEQ1     XC    GFECB,GFECB                                                      
         GET   RPL=(R3)                                                         
         LTR   RF,RF                                                            
         BNZ   SEQ2                                                             
         BAS   RE,CHECK            WAIT FOR I/O COMPLETION                      
SEQ2     MVC   FDBK+1(3),RPLFDBK                                                
         OC    FDBK,FDBK           EXIT WITH CC EQL IF ALL OK                   
         L     RE,SAVERE                                                        
         BR    RE                                                               
         SPACE 2                                                                
CHECK    ST    RE,SAVERECH         WAIT FOR AND CHECK I/O COMPLETION            
         LA    R1,GFECB                                                         
         ICM   RF,15,=V(ADWAIT)                                                 
         BZ    CHECKW                                                           
         BASR  RE,RF               LET FACPAK DO THE WAIT FOR US                
         B     CHECKW                                                           
CHECKW   CHECK RPL=(R3)            LET VTAM DO WAIT HERE                        
CHECKX   L     RE,SAVERECH                                                      
         BR    RE                                                               
         EJECT                                                                  
***********************************************************************         
*INITIALISATION ROUTINE - INVOKED ON FIRST CALL ONLY                  *         
***********************************************************************         
         SPACE 1                                                                
INIT     NTR1                                                                   
         L     R8,ARPLLST          R8=A(LIST OF RPL'S)                          
         USING GFRPLD,R8                                                        
         LR    R3,R8               R3=A(ACTUAL RPL)                             
         USING IFGRPL,R3                                                        
*                                                                               
INIT1    LA    R1,1                OFFLINE NUMBER OF RPL'S                      
         MVI   OFFLINE,C'Y'                                                     
         ICM   RF,15,=V(SSB)                                                    
         BZ    INIT2                                                            
         OC    0(2,RF),0(RF)                                                    
         BZ    INIT2                                                            
         ICM   RE,15,=V(SYSFAC)                                                 
         BNZ   *+6                                                              
         DC    H'0'                                                             
         ICM   RE,15,VTCB-SYSFACD(RE)                                           
         BNZ   *+6                                                              
         DC    H'0'                                                             
         ST    RF,ASSB             SET FACPAK ADDRESSES                         
         ST    RE,ATCB                                                          
         LA    R1,36               ONLINE NUMBER OF RPL'S                       
         MVI   OFFLINE,C'N'                                                     
*                                                                               
INIT2    L     RF,AGFKRPL          COPY MODEL RPL TO TABLE                      
         MVC   GFRPL,0(RF)                                                      
         LA    RF,GFECB                                                         
         ST    RF,RPLECB           SET A(ECB)                                   
         LA    RF,GFKEY                                                         
         ST    RF,RPLARG           SET A(KEY)                                   
*                                                                               
INIT3    LA    R8,GFRPLX-GFRPLD(R8)                                             
         LR    R3,R8                                                            
         BCT   R1,INIT2                                                         
*                                                                               
INITX    XIT1                                                                   
         EJECT                                                                  
***********************************************************************         
*MODULE CONSTANTS AND VSAM I/O AREAS                                  *         
***********************************************************************         
         DS    0D                                                               
AGFKACB  DC    A(GFKFIL)           A(GFK FILE ACB)                              
AGFKRPL  DC    A(GFKRPL)           A(GFK FILE RPL)                              
ARPLLST  DC    A(RPLLST)           A(GFK FILE RPL LIST)                         
ASSB     DC    A(0)                A(SSB IF ONLINE)                             
ATCB     DC    A(0)                A(TCB IF ONLINE)                             
*                                                                               
OPNFLG   DC    X'00'               OPEN 00=CLOSED,01=OPN,80=ERROR               
MODE     DC    X'00'               MODE 00=NORMAL,01=UPD,02=LOAD                
SACTN    DC    X'00'               THIS ACTION                                  
OFFLINE  DC    C' '                OFFLINE FLAG (N=ONLINE,Y=OFFLINE)            
         DC    XL4'00'                                                          
         SPACE 1                                                                
         LTORG                                                                  
         DS    0F                                                               
         EJECT                                                                  
***********************************************************************         
*TABLES OF VALID ACTIONS AND PROCESSING ROUTINES                      *         
***********************************************************************         
         SPACE 1                                                                
ACTNRTN  DC    A(0)                                                             
         DC    A(ACTNGET)                    READ                               
         DC    A(ACTNGHI)                    READ HIGH                          
         DC    A(ACTNSEQ)                    READ SEQUENTIAL                    
         DC    A(ACTNSEQ)                    READ BACKWARDS                     
         DC    A(ACTNADD)                    ADD                                
         DC    A(ACTNPUT)                    WRITE                              
         DC    A(ACTNDEL)                    DELETE                             
         DC    A(ACTNOPN)                    OPEN                               
         DC    A(ACTNOPU)                    OPEN FOR UPDATE                    
         DC    A(ACTNOPL)                    OPEN FOR LOAD                      
         DC    A(ACTNCLS)                    CLOSE                              
         SPACE 2                                                                
ACTNTAB  DS    0CL10                                                            
         DC    AL1(01,03),C'REA   ',X'8000'  READ                               
         DC    AL1(02,03),C'RHI   ',X'8000'  READ HIGH                          
         DC    AL1(03,03),C'RFW   ',X'8000'  READ FORWARD SEQUENTIALLY          
         DC    AL1(04,03),C'RBK   ',X'8000'  READ BACKWARD SEQUENTIALLY         
         DC    AL1(05,03),C'ADD   ',X'0002'  ADD                                
         DC    AL1(06,03),C'WRI   ',X'0002'  WRITE                              
         DC    AL1(07,03),C'DEL   ',X'0002'  DELETE                             
         DC    AL1(08,03),C'OPN   ',X'0001'  OPEN FOR NORMAL I/O                
         DC    AL1(09,03),C'OPU   ',X'0001'  OPEN FOR UPDATE I/O                
         DC    AL1(10,03),C'OPL   ',X'0001'  OPEN FOR LOAD I/O                  
         DC    AL1(11,03),C'CLS   ',X'0001'  CLOSE                              
*                                                                               
         DC    AL1(03,03),C'SEQ   ',X'8000'                                     
*                                                                               
         DC    AL1(01,04),C'DMREAD',X'8000'                                     
         DC    AL1(02,04),C'DMRDHI',X'8000'                                     
         DC    AL1(03,04),C'DMRSEQ',X'8000'                                     
         DC    AL1(05,04),C'DMADD ',X'0002'                                     
         DC    AL1(06,04),C'DMWRT ',X'0002'                                     
*                                                                               
ACTNTABX DC    AL1(00,00),C'      ',X'0000'                                     
ACTNMAX  EQU   10                                                               
         EJECT                                                                  
***********************************************************************         
*GFK DEMO FILE ACB                                                    *         
***********************************************************************         
         PRINT GEN                                                              
GFKFIL   ACB   AM=VSAM,DDNAME=GFKFIL,                                  X        
               MACRF=(KEY,DIR,SEQ)                                              
*                                                                               
         PRINT GEN                                                              
         SHOWCB ACB=GFKFIL,AREA=GFKECB,FIELDS=(XENDRBA),LENGTH=8                
         EJECT                                                                  
***********************************************************************         
*GFK DEMO FILE RPL                                                    *         
***********************************************************************         
         PRINT GEN                                                              
GFKRPL   RPL   ACB=GFKFIL,AM=VSAM,KEYLEN=9,RECLEN=222,AREALEN=222,     X        
               ECB=GFKECB,AREA=GFKREC,                                 X        
               OPTCD=(KEY,DIR,NUP,MVE,ASY)                                      
GFKECB   DC    F'0'                                                             
GFKREC   DC    F'0'                                                             
         EJECT                                                                  
***********************************************************************         
*LIST OF RPLS. ONE ENTRY PER FACPAK TASK. MAX TASKS 35.               *         
*XL76 RPL                                                             *         
*XL04 ECB                                                             *         
*XL04 TCB SIN FOR TASK                                                *         
*XL44 KEY OF RECORD (MAX KEY LEN 44)                                  *         
***********************************************************************         
         SPACE 1                                                                
         DS    0D                                                               
         DC    C'*RPLLST*'                                                      
RPLLST   DS    0XL128                                                           
RPL0     DC    XL128'00'           RPL FOR OFFLINE                              
RPL1     DC    35XL128'00'         RPL FOR TASKS 1-35                           
         SPACE 1                                                                
GFRPLD   DSECT                                                                  
GFRPL    DS    XL76                RPL                                          
GFECB    DS    XL4                 ECB                                          
GFSIN    DS    XL4                 SIN                                          
GFKEY    DS    XL44                KEY                                          
GFRPLX   DS    0X                                                               
         EJECT                                                                  
***********************************************************************         
*LOCAL WORKING STORAGE                                                *         
***********************************************************************         
         SPACE 1                                                                
WORKD    DSECT                                                                  
DUB      DS    D                                                                
FULL     DS    F                                                                
HALF     DS    H                                                                
BYTE     DS    X                                                                
FLAG     DS    X                                                                
SAVERE   DS    F                                                                
SAVERECH DS    F                                                                
FDBK     DS    F                                                                
ACTN     DS    X                                                                
ACTNFLGS DS    XL2                                                              
UPDT     DS    C                                                                
HIGH     DS    C                                                                
WORKX    EQU   *                                                                
         EJECT                                                                  
       ++INCLUDE MEGFKFILED                                                     
         EJECT                                                                  
         IFGACB AM=VSAM                                                         
         EJECT                                                                  
         IFGRPL AM=VSAM                                                         
         EJECT                                                                  
*FASSB                                                                          
*FATCB                                                                          
*FASYSFAC                                                                       
         PRINT OFF                                                              
       ++INCLUDE FASSB                                                          
       ++INCLUDE FATCB                                                          
       ++INCLUDE FASYSFAC                                                       
         PRINT ON                                                               
         SPACE 1                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'008MEGFKIO   05/01/02'                                      
         END                                                                    
