*          DATA SET GETCOST    AT LEVEL 027 AS OF 05/29/14                      
*CATALP GETCOST                                                                 
         TITLE 'GETCOST- ROUTINE FOR HANDLING BILLING FORMULAS'                 
         PRINT NOGEN                                                            
*                                                                               
*        CHANGE LOG                                                             
*                                                                               
* BPLA 5/8/14     FIX DISPLAY OF COMMISSION ONLY FORMULAS                       
*                                                                               
* BPLA 1/8/13     NEW PGSTTAB - QST RATE CHANGE 1/1/13                          
*                 9.5% TO 9.975% + GST NO LONGER IN BASE                        
*                                                                               
* BPLA 1/5/12     NEW PGSTTAB - QST RATE CHANGE 1/1/12                          
*                 8.5% TO 9.5%                                                  
*                                                                               
* BPLA 12/16/10   NEW PGSTTAB - QST RATE CHANGE 1/1/11                          
*                 7.5% TO 8.5%                                                  
*                                                                               
* BPLA 6/17/03    IF NO ADJ % DON'T DIE WHEN COMPUTING                          
*                                                                               
* BPLA 4/12/95    IF PARAM1 BYTE 0 IS "T" SUBTRACT TAX FROM                     
*                 BASE OF ADJUSTMENT BEFORE APPLYING PERCENTAGE                 
*                 (CORRESPONDS TO "Y" IN 13TH BYTE OF B2 PROFILE)               
*                 USES TAX PERCENTAGE IN PBUYREC.                               
*                                                                               
* BPLA 12/21/94 - FIX BUG IN ROUNDING LOGIC (IN COMP5)                          
*                                                                               
GETCOST  CSECT                                                                  
         NMOD1 10,GETCOST                                                       
         USING WORKD,RC                                                         
         SPACE 3                                                                
*                                                                               
*  PARAM 1  BYTE  0      C'T' = SUBTRACT TAX FROM BASE AMOUNT                   
*                        BEFORE APPLYING ADJUSTMENT PERCENTAGE                  
*  PARAM 1  BYTES 1-3    A(FORMULA)                                             
*                                                                               
*  PARAM 2  BYTE  0      X'00' = COMPUTE COST (PUT IN PARAM2)                   
*           BYTES 1-3    A(GROSS,CD,AC)                                         
*  PARAM 2  BYTE  0      X'01' = EXPAND FORMULA DESCRIPTION                     
*           BYTES 1-3    A(OUTPUT AREA) - 51 BYTES MAX                          
*  PARAM 3  BYTE  0      C'C' = DON'T RECALCULATE AGY COM FOR 'C' RATE          
*                        INSERTIONS                                             
*  PARAM 3  BYTES 1-3    A(PUBYREC)                                             
*                        (NOT NEEDED FOR FORMULA EXPANSION)                     
*                        (SAME AS C'C' IF AL3(0))                               
*                                                                               
         LM    R2,R3,0(R1)                                                      
*                                                                               
         MVI   TAXOPT,C'N'                                                      
         CLI   0(R1),C'T'                                                       
         BNE   *+8                                                              
         MVI   TAXOPT,C'Y'                                                      
*                                                                               
         CLI   4(R1),0                                                          
         BE    COMP                                                             
         SPACE 3                                                                
*                                  TEXT EXPANSION                               
         MVI   0(R3),C' '                                                       
         MVC   1(52,R3),0(R3)                                                   
         LR    R4,R2                                                            
         LR    R5,R3                                                            
         BAS   RE,TXT                                                           
         OC    2(3,R2),2(R2)       TEST ANY ADJ                                 
         BZ    EXIT                NO                                           
         MVC   0(4,R5),=C'LESS'                                                 
         TM    2(R2),X'80'                                                      
         BNZ   *+10                                                             
         MVC   0(4,R5),=C'PLUS'                                                 
         MVC   FULL(3),2(R2)                                                    
         L     R6,FULL                                                          
         SRA   R6,8                                                             
         LPR   R6,R6                                                            
         EDIT  (R6),(7,5(R5)),4,ALIGN=LEFT                                      
         AR    R5,R0                                                            
         LA    R5,4(R5)                                                         
BP2      DS    0H                                                               
         CLI   0(R5),C'0'                                                       
         BH    BP4                                                              
         MVI   0(R5),C' '                                                       
         BL    BP4B                DECIMAL POINT                                
         BCT   R5,BP2                                                           
*                                                                               
BP4      DS    0H                                                               
         LA    R5,1(R5)                                                         
BP4B     DS    0H                                                               
         MVC   1(6,R5),=C'PCT OF'                                               
         LA    R5,8(R5)                                                         
         LA    R4,1(R2)                                                         
         BAS   RE,TXT                                                           
         CLI   0(R4),8         SEE IF AGY COM                                   
         BE    EXIT                                                             
         CLI   TAXOPT,C'Y'                                                      
         BNE   EXIT                                                             
         BCTR  R5,0      MUST BACK-UP ONE SPACE                                 
         MVC   0(4,R5),=C'-TAX'                                                 
         B     EXIT                                                             
         SPACE 3                                                                
TXT      DS    0H                                                               
         LA    R7,=C'GROSS LESS CD'                                             
         LA    R6,4                                                             
         CLI   0(R4),1                                                          
         BE    TXT2                                                             
         LA    R6,12                                                            
         CLI   0(R4),5                                                          
         BE    TXT2                                                             
         LA    R7,=C'CGROSS LESS CD'                                            
         LA    R6,5                                                             
         CLI   0(R4),X'41'    CGROSS (FROM BFORMULA RECORD)                     
         BE    TXT2                                                             
         CLI   0(R4),X'11'    CGROSS (FROM NORMAL FORMULA)                      
         BE    TXT2                                                             
         LA    R6,13                                                            
         CLI   0(R4),X'45'    CGROSS-CD (FROM BFORMULA REC)                     
         BE    TXT2                                                             
         CLI   0(R4),X'15'    CGROSS-CD (FROM NORMAL FORMULA)                   
         BE    TXT2                                                             
         LA    R7,=C'NET LESS CD'                                               
         LA    R6,2                                                             
         CLI   0(R4),2                                                          
         BE    TXT2                                                             
         LA    R6,10                                                            
         CLI   0(R4),6                                                          
         BE    TXT2                                                             
         LA    R7,=C'CNET LESS CD'                                              
         LA    R6,3                                                             
         CLI   0(R4),X'42'    CNET (FROM BFROMULA REC)                          
         BE    TXT2                                                             
         CLI   0(R4),X'12'    CNET (FROM NORMAL FORMULA)                        
         BE    TXT2                                                             
         LA    R6,11                                                            
         CLI   0(R4),X'46'    CNET-CD (FROM BFORMULA REC)                       
         BE    TXT2                                                             
         CLI   0(R4),X'16'    CNET-CD (FROM NORMAL FORMULA)                     
         BE    TXT2                                                             
         LA    R7,=C'AGY. COMM.'                                                
         LA    R6,9                                                             
         CLI   0(R4),8                                                          
         BE    TXT2                                                             
         LA    R7,=C'CAGY. COMM.'                                               
         LA    R6,10                                                            
         CLI   0(R4),X'48'   CAGYCOM (FROM BFORMULA REC)                        
         BE    TXT2                                                             
         CLI   0(R4),X'18'   CAGYCOM (FROM NORMAL FORMULA)                      
         BE    TXT2                                                             
         DC    H'0'                  INVALID FORMULA                            
**NEW 4/25/89                                                                   
TXT2     DS    0H                                                               
         EX    R6,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R5),0(R7)                                                    
         LA    R5,2(R5,R6)                                                      
         BR    RE                                                               
         SPACE 3                                                                
COMP     DS    0H                                                               
         SR    R8,R8                                                            
         ICM   R8,7,9(R1)          A(PBUYREC)                                   
         ST    R8,ADBUY                                                         
         USING PBUYRECD,R8                                                      
         LR    R6,R1                                                            
         MVC   GROSS,0(R3)                                                      
         MVC   CD,4(R3)                                                         
         MVC   AC,8(R3)                                                         
         L     R0,0(R3)                                                         
         S     R0,AC                                                            
         ST    R0,NET                                                           
         XC    TAX,TAX            CLEAR TAX  - IT WILL BE CALCULATED            
*                                 ONLY IF NEEDED                                
         LTR   R8,R8                                                            
         BZ    COMP3               NOT PROVIDED                                 
         CLI   PBDCOSIN,C'C'                                                    
         BNE   COMP3                                                            
         XC    NET,NET             CLEAR NET                                    
         CLI   8(R6),C'C'                                                       
         BE    COMP3                                                            
         BAS   RE,FIXAC                                                         
*                                                                               
COMP3    DS    0H                                                               
         SR    R5,R5                                                            
         OC    2(3,R2),2(R2)       ADJ % PRESENT?                               
         BZ    COMP8                                                            
*                                                                               
         L     R5,AC               FIRST TEST FOR AGY COMM.                     
         TM    1(R2),X'08'                                                      
         BO    COMP5                                                            
**NOTE   AGY-CD NO ALLOWED                                                      
*                                                                               
         CLI   TAXOPT,C'Y'         SEE IF SUBTRACTING TAX FROM BASE             
         BNE   COMP3T              IF NOT DON'T NEED TO CALC TAX                
         LA    R4,NET                                                           
         BAS   RE,NTAXCALC                                                      
         MVC   TAX,FULL                                                         
*                                                                               
COMP3T   DS    0H                                                               
         L     R5,GROSS            GROSS                                        
         TM    1(R2),X'01'                                                      
         BO    COMP4                                                            
         L     R5,NET                                                           
         TM    1(R2),X'02'         NET                                          
         BO    COMP4                                                            
         DC    H'0'                BAD FORMULA                                  
*                                                                               
COMP4    TM    1(R2),X'04'         TEST CD                                      
         BZ    COMP4T                                                           
         S     R5,CD               LESS CD                                      
*                                                                               
COMP4T   DS    0H                                                               
         CLI   TAXOPT,C'Y'                                                      
         BNE   COMP5                                                            
         S     R5,TAX                                                           
*                                                                               
COMP5    MVC   FULL(3),2(R2)                                                    
         L     R0,FULL                                                          
         SRA   R0,8                                                             
         MR    R4,R0                                                            
*                                                                               
         SLDA  R4,1                                                             
         D     R4,=F'1000000'                                                   
         LTR   R5,R5                                                            
         BNP   *+8                                                              
         AH    R5,=H'1'                                                         
         SRA   R5,1                                                             
*                                                                               
******   L     R0,=F'500000'                                                    
******   LTR   R4,R4                                                            
******   BNM   *+6                                                              
******   LCR   R0,R0                                                            
******   AR    R5,R0                                                            
******   D     R4,=F'1000000'                                                   
*                                                                               
*                                                                               
COMP8    TM    0(R2),X'08'    FIRST TEST AGY COMM                               
         BZ    COMP10                                                           
         A     R5,AC                                                            
         B     COMPX                                                            
*                                                                               
COMP10   TM    0(R2),X'01'          GROSS                                       
         BNO   COMP12                                                           
         A     R5,GROSS                                                         
         B     COMP15                                                           
*                                                                               
COMP12   A     R5,NET              MUST BE NET                                  
COMP15   TM    0(R2),X'04'         SEE IF BASE WAS - LESS CD                    
         BZ    *+8                                                              
         S     R5,CD                                                            
*                                                                               
COMPX    DS    0H                                                               
         ST    R5,4(R6)            COST                                         
*                                                                               
EXIT     XIT1                                                                   
         EJECT                                                                  
***TAX                                                                          
NTAXCALC NTR1         USED TO EXTRACT TAX FROM NET + TAX                        
*                     RETURNS TAX IN FULL                                       
         XC    FULL,FULL                                                        
         LTR   R8,R8                  SEE IF I HAVE A BUYREC                    
         BZ    TAXCX                                                            
*                                                                               
         MVC   DUB(4),0(R4)           SET TO NET (+TAX)                         
         L     RF,ADBUY                                                         
         OC    PBDTAX-PBUYREC(3,RF),PBDTAX-PBUYREC(RF)                          
         BZ    NTAXCX                 IF NO TAX - DONE                          
*                                                                               
NTAXC2   TM    PBDCNDA-PBUYREC(RF),X'80'   SEE IF CANADIAN                      
         BZ    NTAXC40                NO USE OLD ROUTINE                        
*                                                                               
         MVC   FULL(1),PBDGST-PBUYREC(RF)                                       
         LA    RE,PGSTTAB                                                       
*                                                                               
         CLI   FULL,0                                                           
         BNE   *+8                                                              
         MVI   FULL,C'S'           DEFAULT TO STANDARD                          
*                                                                               
NTAXC4LP DS    0H                  FIND CORRECT GST ENTRY IN TABLE              
*                                                                               
         CLI   0(RE),X'FF'         CHECK FOR END OF TABLE                       
         BE    NTAXC40         INVALID TAX CODE - TREAT LIKE NO GST             
*                                                                               
         CLC   0(1,RE),FULL        MATCH ON GST CODE                            
         BNE   NTAXC4CN                                                         
*                                                                               
*        BUY MUST FALL INTO TABLE ENTRY'S DATE RANGE                            
*                                                                               
         CLC   PBUYKDAT-PBUYREC(3,RF),4(RE)  INSERTION DATE                     
         BL    NTAXC4CN                                                         
*                                                                               
         B     NTAXC4FD                                                         
*                                                                               
NTAXC4CN DS    0H                  FIND CORRECT GST ENTRY IN TABLE              
*                                                                               
         LA    RE,8(RE)            BUMP TO NEXT TABLE ENTRY                     
         B     NTAXC4LP                                                         
*                                                                               
NTAXC4FD DS    0H                                                               
*                                                                               
NTAXC5   OC    2(2,RE),2(RE)      CHK FOR GST PCT                               
         BZ    NTAXC40                                                          
         TM    1(RE),X'01'        SEE IF SALES TAX BASED ON NET                 
         BO    NTAXC40                                                          
*                                                                               
NTAXC7   DS    0H                                                               
*        DUB = NET + SALES TAX % (NET + GST % OF NET)                           
*     SO DUB = NET + SALES TAX % (1+GST%) X NET                                 
*     SO DUB = NET + (SALES TAX % X (1+GST%)) NET                               
*     SO DUB = (1 + (SALES TAX % X (1+GST %))) X NET                            
*  THEREFORE NET = DUB DIVIDED BY (1+(SALES TAX % X (1+GST %)))                 
*                                                                               
         XC    FULL,FULL                                                        
         MVC   FULL+2(2),2(RE)   GST %                                          
         L     RE,FULL                                                          
         A     RE,=F'100000'     1 + GST % (3 DECIMALS)                         
         XC    FULL2,FULL2                                                      
         MVC   FULL2+1(3),PBDTAX-PBUYREC(RF)                                    
         L     RF,FULL2                                                         
         MR    RE,RE                                                            
         D     RE,=F'1000000' SINCE PBDTAX IS 4 DECIMALS                        
*                             RF = SALES TAX % X (1+GST%)                       
         A     RF,=F'100000'  RF = 1 + (SALES % X (1+GST%))                     
         ST    RF,FULL                                                          
         L     RF,DUB                                                           
         M     RE,=F'100000'                                                    
         SLDL  RE,1                                                             
         D     RE,FULL                                                          
         LTR   RF,RF                                                            
         BM    *+8                                                              
         AH    RF,=H'1'                                                         
         SRA   RF,1                                                             
         ST    RF,DUB            SHOULD NOW BE NET                              
         B     TAXC3                                                            
*                                                                               
*                                                                               
NTAXC40  DS    0H                                                               
         XC    FULL2,FULL2                                                      
         MVC   FULL2+1(3),PBDTAX-PBUYREC(RF)                                    
*                                                                               
         L     RE,FULL2           N+TAX= N + TAX PCT(N)                         
         A     RE,=F'1000000'     N = N+TAX/100.0000 + TAX PCT                  
         ST    RE,FULL2                                                         
*                                                                               
         L     RF,DUB                                                           
         M     RE,=F'1000000'                                                   
         SLDL  RE,1                                                             
         D     RE,FULL2            SINCE PBDTAX HAS 4 DECIMALS                  
         LTR   RF,RF                                                            
         BM    *+8                                                              
         AH    RF,=H'1'                                                         
         SRA   RF,1                                                             
         ST    RF,DUB                                                           
NTAXCX   DS    0H                                                               
         B     TAXC3             REST SAME AS TAXCALC                           
*                                                                               
TAXCALC  NTR1                     R4 POINTS TO NET                              
         MVC   DUB(4),0(R4)                                                     
TAXC3    XC    FULL,FULL                                                        
         L     RF,ADBUY                                                         
         OC    PBDTAX-PBUYREC(3,RF),PBDTAX-PBUYREC(RF)                          
         BZ    TAXCX                                                            
**GST                                                                           
         TM    PBDCNDA-PBUYREC(RF),X'80'     SEE IF CANADIAN BUY                
         BZ    TAXC8                                                            
         MVC   FULL(1),PBDGST-PBUYREC(RF)                                       
         LA    RE,PGSTTAB                                                       
         CLI   FULL,0                                                           
         BNE   *+8                                                              
         MVI   FULL,C'S'           DEFAULT TO STANDARD                          
*                                                                               
TAXC4LP DS     0H                  FIND CORRECT GST ENTRY IN TABLE              
*                                                                               
         CLI   0(RE),X'FF'         CHECK FOR END OF TABLE                       
         BE    TAXC7           INVALID TAX CODE - TREAT LIKE NO GST             
*                                                                               
         CLC   0(1,RE),FULL        MATCH ON GST CODE                            
         BNE   TAXC4CN                                                          
*                                                                               
*        BUY MUST FALL INTO TABLE ENTRY'S DATE RANGE                            
*                                                                               
         CLC   PBUYKDAT-PBUYREC(3,RF),4(RE)  INSERTION DATE                     
         BL    TAXC4CN                                                          
*                                                                               
         B     TAXC4FD                                                          
*                                                                               
TAXC4CN DS     0H                  FIND CORRECT GST ENTRY IN TABLE              
*                                                                               
         LA    RE,8(RE)            BUMP TO NEXT TABLE ENTRY                     
         B     TAXC4LP                                                          
*                                                                               
TAXC4FD DS     0H                                                               
*                                                                               
TAXC5    OC    2(2,RE),2(RE)      CHK FOR GST PCT                               
         BZ    TAXC7                                                            
*                                                                               
TAXC6    TM    1(RE),X'01'        SEE IF TAX BASED ON NET                       
         BO    TAXC7              TREAT AS NO GST                               
*                                                                               
TAXC6C   DS    0H                                                               
*                                                                               
         XC    FULL,FULL                                                        
         MVC   FULL+2(2),2(RE)                                                  
         L     RE,FULL                                                          
         L     RF,DUB                                                           
         MR    RE,RE                                                            
         SLDL  RE,1                                                             
         D     RE,=F'100000'          GST HAS 3 DECIMALS                        
         LTR   RF,RF                                                            
         BM    *+8                                                              
         AH    RF,=H'1'                                                         
         SRA   RF,1                                                             
         A     RF,DUB                 ADD GST TO DUB (NET)                      
         ST    RF,DUB                                                           
*                                                                               
TAXC7    XC    FULL,FULL                                                        
TAXC8    DS    0H                                                               
         L     RF,ADBUY                                                         
         MVC   FULL+1(3),PBDTAX-PBUYREC(RF)                                     
*                                                                               
         L     RE,FULL                                                          
         L     RF,DUB                                                           
         MR    RE,RE                                                            
         SLDL  RE,1                                                             
         D     RE,=F'1000000'      SINCE PBDTAX HAS 4 DECIMALS                  
         LTR   RF,RF                                                            
         BM    *+8                                                              
         AH    RF,=H'1'                                                         
         SRA   RF,1                                                             
         ST    RF,FULL            SALES TAX                                     
*                                                                               
TAXCX    XIT1                                                                   
***TAX                                                                          
*                                                                               
**GST                                                                           
       ++INCLUDE PGSTTAB                                                        
         SPACE 3                                                                
*                                                                               
FIXAC    NTR1                                                                   
         ZAP   DUB,PBDACP                                                       
         BNZ   FIXAC5                                                           
         XC    AC,AC                                                            
         B     FIXACX                                                           
*                                                                               
FIXAC5   CVB   R0,DUB                                                           
         L     R1,GROSS                                                         
         L     RF,=F'100000'                                                    
         SR    RF,R0                                                            
         MR    RE,R1                                                            
         SLDL  RE,1                                                             
         D     RE,=F'100000'                                                    
         LTR   RF,RF                                                            
         BM    *+8                                                              
         AH    RF,=H'1'                                                         
         SRA   RF,1                                                             
*                                                                               
         LR    R0,R1                                                            
         SR    R0,RF                                                            
         ST    R0,AC               STORE 'FIXED' AGY COM                        
FIXACX   XIT1                                                                   
         LTORG                                                                  
         SPACE 3                                                                
WORKD    DSECT                                                                  
DUB      DS    D                                                                
FULL     DS    F                                                                
FULL2    DS    F                                                                
GROSS    DS    F                                                                
CD       DS    F                                                                
AC       DS    F                                                                
NET      DS    F                                                                
TAX      DS    F                                                                
*                                                                               
ADBUY    DS    F                                                                
*                                                                               
TAXOPT   DS    CL1                                                              
WORK     DS    CL20                                                             
*                                                                               
         EJECT                                                                  
PBUYRECD DSECT                                                                  
       ++INCLUDE PBUYREC                                                        
       ++INCLUDE PBDELEM                                                        
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'027GETCOST   05/29/14'                                      
         END                                                                    
