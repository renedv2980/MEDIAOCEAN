*          DATA SET DDESSRQS   AT LEVEL 062 AS OF 06/10/14                      
*CATALP ESSRQS                                                                  
         TITLE 'ESSRQS - ESSIO REQUEST QUEUE SERVICES'                          
**********************************************************************          
* ESSIO REQUEST QUEUE SERVICES                                       *          
*                                                                    *          
* PARAMETERS:                                                        *          
*           P1 = A(ESSRQS RECORD)                                    *          
*           P2 = A(RETURN CODE)                                      *          
**********************************************************************          
         SPACE 1                                                                
         PRINT NOGEN                                                            
ESSRQS   CSECT                                                                  
         NMOD1 WORKX-WORKD,**ERQS**,CLEAR=YES,RA,R9,RR=RE                       
         USING WORKD,RC                                                         
         ST    R1,APARM                                                         
         ST    RE,RELO                                                          
         B     INIT                                                             
         SPACE 1                                                                
**********************************************************************          
* INITIALISATION                                                     *          
**********************************************************************          
         SPACE 1                                                                
INIT     EQU   *                                                                
         MVC   PARM,0(R1)          SAVE INPUT PARAMETER LIST                    
*                                  GET THE CURRENT MVS DATE/TIME                
         GOTO1 =V(DATCON),DMCB,(5,0),(2,TODAY2)                                 
*                                  GET THE CURRENT MVS DATE/TIME                
         SR    R0,R0                                                            
         SR    R1,R1                                                            
         TIME                                                                   
         LTR   RF,RF                                                            
         JNZ   *+2                 BAD RETURN FROM MACRO                        
         LTR   R0,R0                                                            
         BNZ   *+8                                                              
         LA    R0,1                SET TO 1/100 SEC IF 0 TIME                   
         ST    R0,MVSTIME                                                       
*                                                                               
         ICM   R7,15,AESSRQS       R7=A(REQUEST QUEUE RECORD)                   
         BNZ   *+6                                                              
         DC    H'0'                                                             
*                                  GET ACTION CODE                              
         ICM   RF,15,AERQACT                                                    
         CLC   0(4,RF),=AL4(ERQAINIQ)                                           
         BE    PROCINIT            INITIALISE REQUEST QUEUE                     
*                                                                               
         CLC   0(4,RF),=AL4(ERQAREAQ)                                           
         BE    PROCREAD            READ ENTRY                                   
*                                                                               
         CLC   0(4,RF),=AL4(ERQARHIQ)                                           
         BE    PROCRHI             READ HIGH FOR ENTRY                          
*                                                                               
         CLC   0(4,RF),=AL4(ERQARSQQ)                                           
         BE    PROCRSQ             READ SEQUENTIAL FOR ENTRY                    
*                                                                               
         CLC   0(4,RF),=AL4(ERQAADDQ)                                           
         BE    PROCADD             ADD AN ENTRY                                 
*                                                                               
         CLC   0(4,RF),=AL4(ERQAWRTQ)                                           
         BE    PROCWRT             WRITE AN ENTRY                               
*                                                                               
         CLC   0(4,RF),=AL4(ERQADELQ)                                           
         BE    PROCDEL             DELETE AN ENTRY                              
*                                                                               
         CLC   0(4,RF),=AL4(ERQACLOQ)                                           
         BE    PROCCLOS            CLOSE DOWN REQUEST QUEUE                     
*                                                                               
         CLC   0(4,RF),=AL4(ERQAELGQ)                                           
         BE    PROCELG             GET ELEMENT FROM ENTRY                       
*                                                                               
         CLC   0(4,RF),=AL4(ERQAELNQ)                                           
         BE    PROCELN             GET NEXT ELEMENT FROM ENTRY                  
*                                                                               
         CLC   0(4,RF),=AL4(ERQAELDQ)                                           
         BE    PROCELD             DELETE ELEMENT FROM ENTRY                    
*                                                                               
         CLC   0(4,RF),=AL4(ERQAELAQ)                                           
         BE    PROCELA             ADD ELEMENT TO ENTRY                         
*                                                                               
         DC    H'0'                UNKNOWN ACTION CODE                          
*                                                                               
EXIT     L     R1,APARM            EXIT WITH UPDATED PARAMETER LIST             
         MVC   0(L'PARM,R1),PARM                                                
         XMOD1 1                                                                
         EJECT                                                                  
**********************************************************************          
* PROCESS TO INITIALISE REQUEST QUEUE                                *          
**********************************************************************          
         SPACE 1                                                                
PROCINIT EQU   *                                                                
*                                                                               
         MVC   DUB,=CL8'T00A7D'    LOAD IN TSAROFF                              
         GOTO1 =V(LOADER),DMCB,DUB,0,0                                          
         OC    VTSAROFF,4(R1)      TEST LOAD WAS OK                             
         BNZ   *+6                                                              
         DC    H'0'                                                             
*                                                                               
         SR    R0,R0                                                            
         LA    R1,ERQLENQ          SET LENGTH OF REQUEST QUEUE FOR TSAR         
         M     R0,REQMAX                                                        
         ST    R1,REQBUFLN                                                      
*                                                                               
         L     R0,REQBUFLN         INITIALISE TSAR BUFFER FOR REQQUE            
         GETMAIN RU,LV=(0),LOC=(ANY,ANY)                                        
         LTR   RF,RF                                                            
         BZ    *+6                                                              
         DC    H'0'                                                             
         ST    R1,REQBUFF          A(TSAR BUFFER IN 31 BIT MEMORY)              
*                                                                               
         XC    TSAREA,TSAREA       CLEAR TSAR PARAMETER BLOCK                   
         LA    R2,TSAREA           INITIALISE TSAR BUFFER                       
         USING TSARD,R2                                                         
         MVI   TSOFFACT,TSAINI     SET ACTION TO INITIALISE                     
         MVC   TSABUF,REQBUFF      SET BUFFER ADDRESS                           
         MVC   TSAREC,REQBUFLN     SET BUFFER LENGTH                            
         LA    R0,ERQKEYQ                                                       
         STC   R0,TSKEYL           SET TSAR RECORD KEY LENGTH                   
         LA    R0,ERQLENQ                                                       
         STH   R0,TSRECL           SET TSAR RECORD LENGTH                       
         GOTO1 VTSAROFF,(R2)                                                    
         CLI   TSERRS,0                                                         
         BE    PINIOK                                                           
         B     PINIER1                                                          
*                                                                               
PINIER1  EQU   *                                                                
         MVI   RETCODE,ERQRERRQ    SET RETURN CODE ERROR                        
         B     EXIT                AND EXIT                                     
*                                                                               
PINIOK   EQU   *                   OK EXIT                                      
         MVI   RETCODE,ERQROKQ     SET RETURN CODE TO OK                        
         B     EXIT                AND EXIT                                     
         DROP  R2                  AND EXIT                                     
         EJECT                                                                  
**********************************************************************          
* PROCESS TO READ                                                    *          
**********************************************************************          
         SPACE 1                                                                
PROCREAD EQU   *                                                                
*                                                                               
         LA    R2,TSAREA                                                        
         USING TSARD,R2                                                         
         MVI   TSOFFACT,TSAGET     SET ACTION TO GET                            
         L     RE,AESSRQS                                                       
         ST    RE,TSAREC           SET RECORD ADDRESS                           
         GOTO1 VTSAROFF,(R2)                                                    
         CLI   TSERRS,0                                                         
         BE    PREAOK                                                           
         TM    TSERRS,TSERNF                                                    
         BO    PREARNF                                                          
         TM    TSERRS,TSEEOF                                                    
         BO    PREAEOF                                                          
         B     PREAER1                                                          
*                                                                               
PREAEOF  EQU   *                                                                
         MVI   RETCODE,ERQREOFQ    SET RETURN CODE ERROR                        
         B     EXIT                AND EXIT                                     
*                                                                               
PREARNF  EQU   *                                                                
         MVI   RETCODE,ERQRRNFQ    SET RETURN CODE ERROR                        
         B     EXIT                AND EXIT                                     
*                                                                               
PREAER1  EQU   *                                                                
         MVI   RETCODE,ERQRERRQ    SET RETURN CODE ERROR                        
         B     EXIT                AND EXIT                                     
*                                                                               
PREAOK   EQU   *                   OK EXIT                                      
         MVI   RETCODE,ERQROKQ     SET RETURN CODE TO OK                        
         B     EXIT                AND EXIT                                     
         EJECT                                                                  
**********************************************************************          
* PROCESS TO READ HIGH                                               *          
**********************************************************************          
         SPACE 1                                                                
PROCRHI  EQU   *                                                                
*                                                                               
         LA    R2,TSAREA                                                        
         USING TSARD,R2                                                         
         MVI   TSOFFACT,TSARDH     SET ACTION TO READ HIGH                      
         L     RE,AESSRQS                                                       
         ST    RE,TSAREC           SET RECORD ADDRESS                           
         GOTO1 VTSAROFF,(R2)                                                    
         CLI   TSERRS,0                                                         
         BE    PRHIOK                                                           
         TM    TSERRS,TSEEOF                                                    
         BO    PRHIEOF                                                          
         TM    TSERRS,TSERNF                                                    
         BO    PRHIOK              READ HIGH RETURNS RECORD NOT                 
         B     PRHIER1             FOUND ERROR IF KEYS DONT MATCH               
*                                                                               
PRHIEOF  EQU   *                                                                
         MVI   RETCODE,ERQREOFQ    SET RETURN CODE ERROR                        
         B     EXIT                AND EXIT                                     
*                                                                               
PRHIER1  EQU   *                                                                
         MVI   RETCODE,ERQRERRQ    SET RETURN CODE ERROR                        
         B     EXIT                AND EXIT                                     
*                                                                               
PRHIOK   EQU   *                   OK EXIT                                      
         MVI   RETCODE,ERQROKQ     SET RETURN CODE TO OK                        
         B     EXIT                AND EXIT                                     
         EJECT                                                                  
**********************************************************************          
* PROCESS TO READ SEQUENTIAL                                         *          
**********************************************************************          
         SPACE 1                                                                
PROCRSQ  EQU   *                                                                
*                                                                               
         LA    R2,TSAREA                                                        
         USING TSARD,R2                                                         
         MVI   TSOFFACT,TSANXT     SET ACTION TO READ NEXT                      
         GOTO1 VTSAROFF,(R2)                                                    
         CLI   TSERRS,0                                                         
         BE    PRSQOK                                                           
         TM    TSERRS,TSEEOF                                                    
         BO    PRSQEOF                                                          
         TM    TSERRS,TSERNF                                                    
         BO    PRSQRNF                                                          
         B     PRSQER1                                                          
*                                                                               
PRSQEOF  EQU   *                                                                
         MVI   RETCODE,ERQREOFQ    SET RETURN CODE ERROR                        
         B     EXIT                AND EXIT                                     
*                                                                               
PRSQRNF  EQU   *                                                                
         MVI   RETCODE,ERQRRNFQ    SET RETURN CODE ERROR                        
         B     EXIT                AND EXIT                                     
*                                                                               
PRSQER1  EQU   *                                                                
         MVI   RETCODE,ERQRERRQ    SET RETURN CODE ERROR                        
         B     EXIT                AND EXIT                                     
*                                                                               
PRSQOK   EQU   *                   OK EXIT                                      
         MVI   RETCODE,ERQROKQ     SET RETURN CODE TO OK                        
         B     EXIT                AND EXIT                                     
         EJECT                                                                  
**********************************************************************          
* PROCESS TO ADD                                                     *          
**********************************************************************          
         SPACE 1                                                                
PROCADD  EQU   *                                                                
*                                                                               
         LA    R2,TSAREA           INITIALISE TSAR BUFFER                       
         USING TSARD,R2                                                         
         MVI   TSOFFACT,TSAADD     SET ACTION TO ADD                            
         L     RE,AESSRQS                                                       
         ST    RE,TSAREC           SET RECORD ADDRESS                           
         GOTO1 VTSAROFF,(R2)                                                    
         CLI   TSERRS,0                                                         
         BE    PADDOK                                                           
         TM    TSERRS,TSEDUP                                                    
         BO    PADDDUP                                                          
         TM    TSERRS,TSEEOF                                                    
         BO    PADDEOF                                                          
         B     PADDER1                                                          
*                                                                               
PADDEOF  EQU   *                                                                
         MVI   RETCODE,ERQREOFQ    SET RETURN CODE ERROR                        
         B     EXIT                AND EXIT                                     
*                                                                               
PADDDUP  EQU   *                                                                
         MVI   RETCODE,ERQRDUPQ    SET RETURN CODE ERROR                        
         B     EXIT                AND EXIT                                     
*                                                                               
PADDER1  EQU   *                                                                
         MVI   RETCODE,ERQRERRQ    SET RETURN CODE ERROR                        
         B     EXIT                AND EXIT                                     
*                                                                               
PADDOK   EQU   *                   OK EXIT                                      
         MVI   RETCODE,ERQROKQ     SET RETURN CODE TO OK                        
         B     EXIT                AND EXIT                                     
         DROP  R2                  AND EXIT                                     
         EJECT                                                                  
**********************************************************************          
* PROCESS TO WRITE                                                   *          
**********************************************************************          
         SPACE 1                                                                
PROCWRT  EQU   *                                                                
*                                                                               
         LA    R2,TSAREA                                                        
         USING TSARD,R2                                                         
         MVI   TSOFFACT,TSAWRT     SET ACTION TO WRITE                          
         L     RE,AESSRQS                                                       
         ST    RE,TSAREC           SET RECORD ADDRESS                           
         GOTO1 VTSAROFF,(R2)                                                    
         CLI   TSERRS,0                                                         
         BE    PWRTOK                                                           
         TM    TSERRS,TSERNF                                                    
         BO    PWRTRNF                                                          
         TM    TSERRS,TSEEOF                                                    
         BO    PWRTEOF                                                          
         B     PWRTER1                                                          
*                                                                               
PWRTEOF  EQU   *                                                                
         MVI   RETCODE,ERQREOFQ    SET RETURN CODE ERROR                        
         B     EXIT                AND EXIT                                     
*                                                                               
PWRTRNF  EQU   *                                                                
         MVI   RETCODE,ERQRRNFQ    SET RETURN CODE ERROR                        
         B     EXIT                AND EXIT                                     
*                                                                               
PWRTER1  EQU   *                                                                
         MVI   RETCODE,ERQRERRQ    SET RETURN CODE ERROR                        
         B     EXIT                AND EXIT                                     
*                                                                               
PWRTOK   EQU   *                   OK EXIT                                      
         MVI   RETCODE,ERQROKQ     SET RETURN CODE TO OK                        
         B     EXIT                AND EXIT                                     
         EJECT                                                                  
**********************************************************************          
* PROCESS TO DELETE                                                  *          
**********************************************************************          
         SPACE 1                                                                
PROCDEL  EQU   *                                                                
         LA    R2,TSAREA                                                        
         USING TSARD,R2                                                         
         MVI   TSOFFACT,TSADEL     SET ACTION TO DELETE                         
         L     RE,AESSRQS                                                       
         ST    RE,TSAREC           SET RECORD ADDRESS                           
         GOTO1 VTSAROFF,(R2)                                                    
         CLI   TSERRS,0                                                         
         BE    PDELOK                                                           
         TM    TSERRS,TSERNF                                                    
         BO    PDELRNF                                                          
         TM    TSERRS,TSEEOF                                                    
         BO    PDELEOF                                                          
         B     PDELER1                                                          
*                                                                               
PDELEOF  EQU   *                                                                
         MVI   RETCODE,ERQREOFQ    SET RETURN CODE ERROR                        
         B     EXIT                AND EXIT                                     
*                                                                               
PDELRNF  EQU   *                                                                
         MVI   RETCODE,ERQRRNFQ    SET RETURN CODE ERROR                        
         B     EXIT                AND EXIT                                     
*                                                                               
PDELER1  EQU   *                                                                
         MVI   RETCODE,ERQRERRQ    SET RETURN CODE ERROR                        
         B     EXIT                AND EXIT                                     
*                                                                               
PDELOK   EQU   *                   OK EXIT                                      
         MVI   RETCODE,ERQROKQ     SET RETURN CODE TO OK                        
         B     EXIT                AND EXIT                                     
         EJECT                                                                  
**********************************************************************          
* PROCESS TO CLOSE                                                   *          
**********************************************************************          
         SPACE 1                                                                
PROCCLOS EQU   *                                                                
*                                                                               
         SR    R0,R0                                                            
         LA    R1,ERQLENQ                                                       
         M     R0,REQMAX                                                        
         LR    R0,R1                                                            
         FREEMAIN RC,A=(1),LV=(0)                                               
*                                                                               
PCLOOK   EQU   *                   OK EXIT                                      
         MVI   RETCODE,ERQROKQ     SET RETURN CODE TO OK                        
         B     EXIT                AND EXIT                                     
         EJECT                                                                  
**********************************************************************          
* PROCESS TO GET ELEMENT                                             *          
**********************************************************************          
         SPACE 1                                                                
PROCELG  EQU   *                                                                
*                                                                               
         ICM   R2,15,AESSRQS                                                    
         USING ESSREQD,R2                                                       
         ICM   R3,15,AERQOFF                                                    
         ICM   R5,15,AERQEL                                                     
*                                                                               
PELG010  CLI   0(R3),0                                                          
         BE    PELGER1                                                          
         CLC   0(1,R3),0(R5)                                                    
         BE    PELG020                                                          
         SR    RF,RF                                                            
         IC    RF,1(R3)                                                         
         AR    R3,RF                                                            
         B     PELG010                                                          
*                                                                               
PELG020  STCM  R3,15,AERQOFF                                                    
         SR    RF,RF                                                            
         IC    RF,1(R3)                                                         
         BCTR  RF,0                                                             
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R5),0(R3)                                                    
         B     PELGOK                                                           
*                                                                               
PELGER1  EQU   *                                                                
         MVI   RETCODE,ERQREORQ    SET RETURN CODE END OF RECORD                
         B     EXIT                AND EXIT                                     
*                                                                               
PELGOK   EQU   *                   OK EXIT                                      
         MVI   RETCODE,ERQROKQ     SET RETURN CODE TO OK                        
         B     EXIT                AND EXIT                                     
         EJECT                                                                  
**********************************************************************          
* PROCESS TO GET NEXT ELEMENT                                        *          
**********************************************************************          
         SPACE 1                                                                
PROCELN  EQU   *                                                                
*                                                                               
         ICM   R2,15,AESSRQS                                                    
         USING ESSREQD,R2                                                       
         ICM   R3,15,AERQOFF                                                    
         ICM   R5,15,AERQEL                                                     
*                                  BUMP PAST LAST ELEMENT                       
         CLI   0(R3),0                                                          
         BE    PELNER1                                                          
         SR    RF,RF                                                            
         IC    RF,1(R3)                                                         
         AR    R3,RF                                                            
*                                                                               
PELN010  CLI   0(R3),0                                                          
         BE    PELNER1                                                          
         CLC   0(1,R3),0(R5)                                                    
         BE    PELN020                                                          
         SR    RF,RF                                                            
         IC    RF,1(R3)                                                         
         AR    R3,RF                                                            
         B     PELN010                                                          
*                                                                               
PELN020  STCM  R3,15,AERQOFF                                                    
         SR    RF,RF                                                            
         IC    RF,1(R3)                                                         
         BCTR  RF,0                                                             
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R5),0(R3)                                                    
         B     PELNOK                                                           
*                                                                               
PELNER1  EQU   *                                                                
         MVI   RETCODE,ERQREORQ    SET END OF RECORD RETURN CODE                
         B     EXIT                AND EXIT                                     
*                                                                               
PELNOK   EQU   *                   OK EXIT                                      
         MVI   RETCODE,ERQROKQ     SET RETURN CODE TO OK                        
         B     EXIT                AND EXIT                                     
         EJECT                                                                  
**********************************************************************          
* PROCESS TO DELETE ELEMENT                                          *          
**********************************************************************          
         SPACE 1                                                                
PROCELD  EQU   *                                                                
*                                                                               
         ICM   R2,15,AESSRQS                                                    
         USING ESSREQD,R2                                                       
         LA    R3,ESSRFEL                                                       
         ICM   R5,15,AERQEL                                                     
*                                                                               
PELD010  CLI   0(R3),0                                                          
         BE    PELDER1                                                          
         CLC   0(1,R3),0(R5)                                                    
         BE    PELD020                                                          
         SR    RF,RF                                                            
         IC    RF,1(R3)                                                         
         AR    R3,RF                                                            
         B     PELD010                                                          
*                                                                               
PELD020  STCM  R3,15,AERQOFF                                                    
         SR    RF,RF                                                            
         SR    R1,R1                                                            
         ICM   R1,3,ESSRRLEN                                                    
         IC    RF,1(R3)                                                         
         SR    R1,RF                                                            
         STCM  R1,3,ESSRRLEN                                                    
         LR    RF,R3                                                            
         SR    RF,R2                                                            
         SR    R1,RF                                                            
         LR    R0,R3                                                            
         SR    RF,RF                                                            
         IC    RF,1(R3)                                                         
         AR    R3,RF                                                            
         LR    RE,R3                                                            
         LR    RF,R1                                                            
         MVCL  R0,RE                                                            
         B     PELDOK                                                           
*                                                                               
PELDER1  EQU   *                                                                
         MVI   RETCODE,ERQRERRQ    SET RETURN CODE ERROR                        
         B     EXIT                AND EXIT                                     
*                                                                               
PELDOK   EQU   *                   OK EXIT                                      
         MVI   RETCODE,ERQROKQ     SET RETURN CODE TO OK                        
         B     EXIT                AND EXIT                                     
         EJECT                                                                  
**********************************************************************          
* PROCESS TO ADD ELEMENT                                             *          
**********************************************************************          
         SPACE 1                                                                
PROCELA  EQU   *                                                                
*                                                                               
         ICM   R2,15,AESSRQS                                                    
         USING ESSREQD,R2                                                       
         LA    R3,ESSRFEL                                                       
         ICM   R5,15,AERQEL                                                     
*                                                                               
PELA010  CLI   0(R3),0                                                          
         BE    PELA020                                                          
         CLC   0(1,R3),0(R5)                                                    
         BL    PELA020                                                          
         SR    RF,RF                                                            
         IC    RF,1(R3)                                                         
         AR    R3,RF                                                            
         B     PELA010                                                          
*                                                                               
PELA020  STCM  R3,15,AERQOFF                                                    
         SR    RF,RF                                                            
         SR    R1,R1                                                            
         ICM   R1,3,ESSRRLEN                                                    
         IC    RF,1(R5)                                                         
         LR    R0,R1                                                            
         AR    R1,RF                                                            
         STCM  R1,3,ESSRRLEN                                                    
         LR    RF,R2                                                            
         AR    RF,R1                                                            
         LR    RE,R2                                                            
         AR    RE,R0                                                            
*                                                                               
PELA030  CR    RE,R3                                                            
         BNH   PELA100                                                          
         MVC   0(1,RF),0(RE)                                                    
         BCTR  RE,0                                                             
         BCTR  R1,0                                                             
         B     PELA030                                                          
*                                                                               
PELA100  SR    RF,RF                                                            
         IC    RF,1(R5)                                                         
         BCTR  RF,0                                                             
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R3),0(R5)                                                    
         B     PELAOK                                                           
*                                                                               
PELAOK   EQU   *                   OK EXIT                                      
         MVI   RETCODE,ERQROKQ     SET RETURN CODE TO OK                        
         B     EXIT                AND EXIT                                     
*                                                                               
YES      SR    RC,RC                                                            
NO       LTR   RC,RC                                                            
         XIT1                                                                   
*                                                                               
FIRST    DC    C' '                                                             
SPACES   DC    128C' '                                                          
*                                                                               
         EJECT                                                                  
         LTORG                                                                  
         EJECT                                                                  
         DS    0D                                                               
VTSAROFF DC    A(0)                                                             
VVSAMIO  DS    V(VSAMIO)                                                        
REQMAX   DC    F'10000'            MAXIMUM NUMBER ENTRIES IN REQQUE             
TSAREA   DC    XL64'00'            AREA TO BE COVERED BY TSARD                  
*                                                                               
         EJECT                                                                  
WORKD    DSECT                                                                  
*                                                                               
DMCB     DS    6F                                                               
DMCBSAVE DS    6F                                                               
RELO     DS    A                                                                
DUB      DS    D                                                                
DUB2     DS    D                                                                
APARM    DS    A                   A(PARAMETER LIST)                            
*                                                                               
PARM     DS    XL(8*4)             PARAMETER LIST                               
PARMX    EQU   *                                                                
         ORG   PARM                                                             
RETCODE  DS    XL1                 RETURN CODE                                  
         ORG   RETCODE                                                          
AERQACT  DS    AL4                 A(ESS REQUEST ACTION CODE)                   
AESSRQS  DS    AL4                 A(ESS REQUEST QUEUE RECORD)                  
AERQEL   DS    AL4                 A(ERQ. RECORD ELEMENT)                       
AERQOFF  DS    AL4                 A(ESS REQUEST QUEUE RECORD OFFSET)           
         DS    XL(4*4)                                                          
         ORG   PARMX                                                            
*                                                                               
REQBUFF  DS    A                   A(REQQUE TSAR BUFFER)                        
REQBUFLN DS    A                   LENGTH REQQUE TSAR BUFFER                    
FULL     DS    F                                                                
HALF     DS    H                                                                
BYTE     DS    XL1                                                              
MVSTIME  DS    F                   TIME RETURNED  UNSIGNED FOR EXTRACT          
TODAY2   DS    XL2                 TODAYS DATE BINARY                           
WORK     DS    CL256                                                            
*                                                                               
GENDA    DS    F                   GENFIL DISK ADDRESS                          
KEY      DS    XL25                IO KEY FOR CTFILE READS                      
IOL      DS    F                   IO DATA BUFFER                               
IO       DS    2048X                                                            
         SPACE 2                                                                
*                                                                               
WORKX    EQU   *                                                                
         EJECT                                                                  
       ++INCLUDE DDESSRQSD                                                      
         EJECT                                                                  
       ++INCLUDE DDTSARD                                                        
         SPACE 2                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'062DDESSRQS  06/10/14'                                      
         END                                                                    
