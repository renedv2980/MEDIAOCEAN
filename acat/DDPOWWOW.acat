*          DATA SET DDPOWWOW   AT LEVEL 003 AS OF 05/01/02                      
*CATALP POWWOW                                                                  
         SPACE 2                                                                
*DOS PARAMETER LIST                                                             
*                                                                               
*PUT TO        A(=C'PUT') A(=C'POW')    A(KEY)      A(88 BYTE AREA)             
*READER                                 0-7 JOBID   0-7 SET TO 8X'00'           
*                                       8   CLASS   8-87 CARD AREA              
*                                       9   DISP                                
*                                                                               
*GET FROM      A(=C'GET') A(=C'POW')    A(KEY)      A(133 BYTE AREA)            
*PRINTER                                0-7 JOBID   0 CCW OP CODE               
*                                       8   CLASS   1-132 PRINT LINE            
*                                       9   DISP                                
*                                                                               
*ALTER JOB     A(=C'PUT') A(=C'CON')    A(KEY)      A(STATUS AREA)              
*STATUS                                 0-7 JOBID   A(=C'DISP=X') OR            
*                                       8   CLASS   A(=C'CLASS=X')              
*                                       9   DISP                                
*                                                                               
*AFTER DOS CALL RETURN IN P3            X'80'=LAST RECORD ON GET                
*                                       X'10'=JOB NOT FOUND                     
*                                       X'FF'=BUSY                              
         SPACE 2                                                                
*MVS PARAMETER LIST                                                             
*                                                                               
*PUT TO        A(=C'PUT') A(=C'POW')    A(KEY)      A(88 BYTE AREA)             
*READER                   A(=C'OPEN')   0-7 JOBID   0-03 SET TO ASAVE           
*                         A(=C'CLOSE')              8-87 CARD AREA              
*                         A(=C'ADR')                                            
*                         A(=C'END')                                            
*                                                                               
*AFTER MVS CALL RETURN IN P3            X'80'=LAST RECORD ON PUT                
         TITLE 'POWWOW - DDS POWER/JES INTERFACE'                               
         PRINT NOGEN                                                            
POWWOW   CSECT                                                                  
         ENTRY POWWOWS                                                          
         NMOD1 WORKX-WORKD,**POWWOW                                             
         USING WORKD,RC            RC=A(LOCAL WORKING STORAGE)                  
         LR    R2,R1                                                            
         USING PARAMS,R2           R2=A(PARAMETER LIST)                         
         MVI   P3,0                                                             
*                                                                               
POWWOW0  TM    POWWOWS,X'02'       TEST IF ONLINE                               
         BZ    POWWOW1             NO                                           
         GOTO1 =V(TICTOC),DUB,C'SSET'                                           
         B     POWWOW1                                                          
         SPACE 2                                                                
EXIT     TM    POWWOWS,X'02'       TEST IF ONLINE                               
         BZ    EXIT0               NO                                           
         GOTO1 =V(TICTOC),DUB,C'RSET'                                           
EXIT0    XMOD1 1                                                                
         EJECT                                                                  
*&&DO                                                                           
POWWOW1  LA    RA,POWSPL           RA=A(POWER DSECT)                            
         USING SPL,RA                                                           
POW1     TM    POWWOWS,X'01'       TEST FIRST CALL                              
         BZ    POW2                                                             
         L     RE,P2               TEST CNTL/INIT                               
         CLC   0(3,RE),=C'CON'                                                  
         BE    CNTL                                                             
         CLC   0(3,RE),=C'INI'                                                  
         BE    POW4                                                             
         L     RE,P1               TEST GET/PUT                                 
         CLC   0(3,RE),=C'GET'                                                  
         BE    GET                                                              
         CLC   0(3,RE),=C'PUT'                                                  
         BE    PUT                                                              
         DC    H'0'                                                             
*                                                                               
POW2     LA    R9,RELO             RELOCATE ADDRESS CONSTANTS                   
         S     R9,RELO                                                          
         LA    R1,XECB1                                                         
         L     R0,12(R1)                                                        
         AR    R0,R9                                                            
         ST    R0,12(R1)                                                        
         LA    R1,XECB2                                                         
         L     R0,12(R1)                                                        
         AR    R0,R9                                                            
         ST    R0,12(R1)                                                        
         L     R0,ICRXECB+4                                                     
         AR    R0,R9                                                            
         ST    R0,ICRXECB+4                                                     
         L     R0,SPMXECB+4                                                     
         AR    R0,R9                                                            
         ST    R0,SPMXECB+4                                                     
         L     R0,SPPB                                                          
         AR    R0,R9                                                            
         ST    R0,SPPB                                                          
         MVI   POWWOWS,X'01'       SET INITIALISED FLAG                         
*                                                                               
POW3     L     RE,=V(SSB)                                                       
         LTR   RE,RE                                                            
         BZ    POW4                                                             
         OC    0(2,RE),0(RE)                                                    
         BZ    POW4                                                             
         OI    POWWOWS,X'02'       SET ONLINE FLAG                              
*                                                                               
POW4     XC    SPJB,SPJB           CLEAR JOB INFO                               
         MVI   SPCL,C' '                                                        
         MVI   SPDP,C' '                                                        
         BAS   R8,DELICR           DELETE ICRXECB                               
         BAS   R8,DELSPM           DELETE SPMXECB                               
         L     RE,P2                                                            
         CLC   0(3,RE),=C'INI'                                                  
         BNE   POW1                                                             
         B     EXIT                                                             
         EJECT                                                                  
CNTL     BAS   R8,DEFSPM           DEFINE SPMXECB                               
         LM    R4,R5,P3                                                         
         MVC   SPJB,0(R4)          SET JOB NAME                                 
         MVC   SPCL,8(R4)          SET CLASS                                    
         MVC   SPDP,9(R4)          SET DISP                                     
*                                                                               
         CLC   0(6,R5),=C'CLASS='                                               
         BNE   CNTL2                                                            
         OI    SPR2,SPRC                                                        
         MVC   SPNV,6(R5)                                                       
         B     CNTLX                                                            
*                                                                               
CNTL2    CLC   0(5,R5),=C'DISP='                                                
         BNE   CNTL4                                                            
         OI    SPR2,SPRD                                                        
         MVC   SPNV,5(R5)                                                       
         B     CNTLX                                                            
*                                                                               
CNTL4    DS    0H                                                               
         BAS   R8,DELSPM                                                        
         DC    H'0'                                                             
*                                                                               
CNTLX    DS    0H                                                               
         CTLSPOOL SPL=(10)                                                      
*                                                                               
         LTR   RF,RF                                                            
         BZ    *+10                                                             
         BAS   R8,DELSPM                                                        
         DC    H'0'                                                             
*                                                                               
         CLI   SPSQ,C'N'           TEST NOT FOUND                               
         BNE   *+12                                                             
         MVI   P3,X'10'            SET NOT FOUND                                
         B     CNTLX2                                                           
*                                                                               
         CLI   SPMXECB+4,0         TEST OTHER ERRORS                            
         BE    *+10                                                             
         BAS   R8,DELSPM                                                        
         DC    H'0'                                                             
*                                                                               
CNTLX2   BAS   R8,DELSPM           DELETE SPMXECB                               
         B     EXIT                                                             
         EJECT                                                                  
GET      BAS   R8,DEFSPM           DEFINE SPMXECB                               
         L     R4,P3                                                            
         OC    SPJB,SPJB           TEST FIRST REQUEST                           
         BNZ   GET2                                                             
         MVC   SPJB,0(R4)          SET NAME                                     
         MVC   SPCL,8(R4)          SET CLASS                                    
         MVC   SPDP,9(R4)          SET DISP                                     
         MVI   SPSN,X'04'          SIGNED START CONTROL                         
         XC    SPCT,SPCT           RESET COUNT                                  
         CTLSPOOL SPL=(10),REQ=STATUS                                           
         CLI   SPSQ,C'L'           TEST ON LIST QUEUE                           
         BNE   GET12               NO - TREAT AS NOT FOUND                      
         MVC   SPDP,SPQD           MOVE DISP TO LIST Q DISP                     
*                                                                               
         CLI   SPDP,C'D'           CAN ONLY PRINT JOB IF DISP=K OR D            
         BE    GET10                                                            
         CLI   SPDP,C'K'                                                        
         BE    GET10                                                            
* CHANGE DISP TO K                                                              
         CTLSPOOL SPL=(10),REQ=DISP,NEWVAL='K'                                  
*                                                                               
         LTR   RF,RF                                                            
         BZ    *+10                                                             
         BAS   R8,DELSPM                                                        
         DC    H'0'                                                             
*                                                                               
         CLI   SPSQ,C'N'                                                        
         BNE   *+12                                                             
         MVI   P3,X'10'                                                         
         B     GETX2                                                            
*                                                                               
         CLI   SPMXECB+4,0                                                      
         BE    *+10                                                             
         BAS   R8,DELSPM                                                        
         DC    H'0'                                                             
*                                                                               
         MVI   SPDP,C'K'                                                        
         B     GET10                                                            
*                                                                               
GET2     CLC   SPJB,0(R4)                                                       
         BE    *+10                                                             
         BAS   R8,DELSPM                                                        
         DC    H'0'                                                             
*                                                                               
GET10    DS    0H                                                               
         GETSPOOL SPL=(10),CC=YES                                               
*                                                                               
         LTR   RF,RF                                                            
         BZ    *+10                                                             
         BAS   R8,DELSPM                                                        
         DC    H'0'                                                             
*                                                                               
         CLI   SPMXECB+4,SPLR                                                   
         BE    GETX                                                             
*                                                                               
GET12    CLI   SPSQ,C'N'           TEST NOT FOUND                               
         BNE   *+12                                                             
         MVI   P3,X'10'            SET ERROR FOR USER                           
         B     GETX2                                                            
*                                                                               
         CLI   SPMXECB+4,0         TEST FOR OTHER ERRORS                        
         BE    *+10                                                             
         BAS   R8,DELSPM                                                        
         DC    H'0'                                                             
*                                                                               
         L     R5,P4               USER BUFFER                                  
         MVI   0(R5),C' '                                                       
         MVC   1(132,R5),0(R5)                                                  
         MVC   0(1,R5),SPCC        CONTROL CHAR                                 
         L     RE,SPRL             REC LENGTH                                   
         LTR   RE,RE                                                            
         BZ    EXIT                                                             
         BCTR  RE,0                                                             
         EX    RE,*+8                                                           
         B     EXIT                                                             
         MVC   1(0,R5),POWBUFF ** EXECUTED **                                   
*                                                                               
GETX     MVI   P3,X'80'            SET E-O-F                                    
*                                                                               
GETX2    DS    0H                                                               
         BAS   R8,DELSPM           DELETE SPMXECB                               
         B     EXIT                                                             
         EJECT                                                                  
PUT      BAS   R8,DEFICR           DEFINE ICRXECB                               
         L     R4,P3                                                            
         L     R7,P4               BUFFER ADDRESS                               
         OC    SPJB,SPJB           TEST FIRST PUT                               
         BNZ   PUT2                                                             
         CLC   =C'* $$ EOJ',8(R7)  THIS TEST FOR EOJ CARD AFTER /&              
         BE    PUTX                IF YES, IGNORE IT                            
         MVC   SPJB,0(R4)          SET NAME                                     
         MVC   SPCL,8(R4)          SET CLASS                                    
         MVC   SPDP,9(R4)          SET DISP                                     
         B     PUT10                                                            
*                                                                               
PUT2     CLC   SPJB,0(R4)          TEST SAME JOB                                
         BE    *+10                                                             
         BAS   R8,DELICR                                                        
         DC    H'0'                                                             
*                                                                               
PUT10    LA    R4,PUTERR           SET ERROR TEST AS CONTINUATION ADDR          
         CLC   =C'/&&',8(R7)       TEST FOR EOJ                                 
         BE    PUT12                                                            
         CLC   =C'* $$ EOJ',8(R7)                                               
         BE    PUT12                                                            
         B     PUT14                                                            
*                                                                               
PUT12    SR    R4,R4               CLEAR CONTINATION ADDRESS                    
*                                                                               
PUT14    DS    0H                                                               
         XC    0(8,R7),0(R7)       CLEAR 'NEXT' BUFFER ADDRESS                  
*                                                                               
         PUTSPOOL SPL=(10),CONT=(4),CBUF=(7)                                    
* NOTE THIS EXECUTES LAST TIME ONLY                                             
         LTR   RF,RF                                                            
         BZ    *+10                                                             
         BAS   R8,DELICR                                                        
         DC    H'0'                                                             
*                                                                               
PUTX     BAS   R8,DELICR           DELETE ICRXECB                               
         B     EXIT                                                             
*                                                                               
PUTERR   LTR   RF,RF               TEST ERROR ON PUT                            
         BZ    EXIT                                                             
         BAS   R8,DELICR                                                        
         DC    H'0'                                                             
         EJECT                                                                  
DEFICR   TM    POWWOWS,X'02'       TEST IF SAME TASK                            
         BZ    DEFICR1                                                          
         TM    POWWOWS,X'C0'                                                    
         BZ    DEFICR1                                                          
         L     RE,=V(SSB)                                                       
         USING SSBD,RE                                                          
         CLC   POWWOWT,SSBTKADR+1                                               
         BNE   DEFICR2                                                          
DEFICR1  TM    POWWOWS,X'80'       TEST IF ICRXECB IS ALREADY DEFINED           
         BOR   R8                                                               
XECB1    XECBTAB TYPE=DEFINE,ACCESS=XWAIT,XECB=ICRXECB                          
         STC   RF,POWRES                                                        
         LTR   RF,RF                                                            
         BNZ   DEFICR2                                                          
         OI    POWWOWS,X'80'                                                    
         TM    POWWOWS,X'02'                                                    
         BZR   R8                                                               
         L     RE,=V(SSB)                                                       
         MVC   POWWOWT,SSBTKADR+1                                               
         BR    R8                                                               
DEFICR2  MVI   P3,X'FF'            RETURN ICRXECB NOT AVAILABLE                 
         B     EXIT                                                             
         SPACE 2                                                                
DEFSPM   TM    POWWOWS,X'02'       TEST IF SAME TASK                            
         BZ    DEFSPM1                                                          
         TM    POWWOWS,X'C0'                                                    
         BZ    DEFSPM1                                                          
         L     RE,=V(SSB)                                                       
         USING SSBD,RE                                                          
         CLC   POWWOWT,SSBTKADR+1                                               
         BNE   DEFSPM2                                                          
DEFSPM1  TM    POWWOWS,X'40'       TEST IF SPMXECB IS ALREADY DEFINED           
         BOR   R8                                                               
XECB2    XECBTAB TYPE=DEFINE,ACCESS=XWAIT,XECB=SPMXECB                          
         STC   RF,POWRES                                                        
         LTR   RF,RF                                                            
         BNZ   DEFSPM2                                                          
         OI    POWWOWS,X'40'                                                    
         TM    POWWOWS,X'02'                                                    
         BZR   R8                                                               
         L     RE,=V(SSB)                                                       
         MVC   POWWOWT,SSBTKADR+1                                               
         BR    R8                                                               
DEFSPM2  MVI   P3,X'FF'            RETURN SPMXECB NOT AVAILABLE                 
         B     EXIT                                                             
         SPACE 2                                                                
DELICR   NI    POWWOWS,X'7F'       DELETE ICRXECB                               
         TM    POWWOWS,X'40'                                                    
         BO    *+10                                                             
         XC    POWWOWT,POWWOWT                                                  
         XECBTAB TYPE=DELETE,XECB=ICRXECB                                       
         STC   RF,POWRES                                                        
         XC    SPJB,SPJB                                                        
         BR    R8                                                               
         SPACE 2                                                                
DELSPM   NI    POWWOWS,X'BF'       DELETE SPMXECB                               
         TM    POWWOWS,X'80'                                                    
         BO    *+10                                                             
         XC    POWWOWT,POWWOWT                                                  
         XECBTAB TYPE=DELETE,XECB=SPMXECB                                       
         STC   RF,POWRES                                                        
         XC    SPJB,SPJB                                                        
         BR    R8                                                               
*&&                                                                             
         EJECT                                                                  
*&&OS                                                                           
POWWOW1  LA    RA,ORPL0            RA=A(REQUEST PARAM LIST DSECT)               
         USING IFGRPL,RA                                                        
JES      TM    POWWOWS,X'01'       TEST FIRST CALL                              
         BZ    JESFIR              YES                                          
         TM    POWWOWS,X'02'       TEST ONLINE                                  
         BO    JESONL                                                           
         B     JESOFF                                                           
         SPACE 2                                                                
JESFIR   OI    POWWOWS,X'01'       FIRST TIME ONLY LOGIC                        
         L     RE,=V(SSB)                                                       
         LTR   RE,RE               TEST/SET ONLINE                              
         BZ    JESFIR1                                                          
         OC    0(2,RE),0(RE)                                                    
         BZ    JESFIR1                                                          
         OI    POWWOWS,X'02'       SET ONLINE FLAG                              
         GOTO1 =V(TICTOC),DUB,C'SSET'                                           
JESFIR1  EQU   *                                                                
*                                                                               
JESFIR2  TM    POWWOWS,X'02'       OPEN INTERNAL READER IF OFFLINE              
         BO    JES                                                              
         TM    POWWOWS,X'80'                                                    
         BO    JESFIR2A                                                         
         OPEN  (IRDR)                                                           
         OI    POWWOWS,X'80'       SET OPEN FLAG                                
JESFIR2A B     JES                                                              
*                                                                               
JESOFF   L     RE,P2                                                            
         CLC   0(3,RE),=C'OPE'     OPEN IRDR                                    
         BE    OFFOPN                                                           
         CLC   0(3,RE),=C'CLO'     CLOSE IRDR                                   
         BE    OFFCLO                                                           
         CLC   0(3,RE),=C'END'     ISSUE ENDREQ FOR IRDR                        
         BE    OFFEND                                                           
         CLC   0(3,RE),=C'ADR'     RETURN A(IRDR SAVE AREA)                     
         BE    OFFPUTX                                                          
         L     RE,P1               PARAM1 MUST BE C'PUT'                        
         CLC   0(3,RE),=C'PUT'                                                  
         BE    OFFPUT                                                           
         DC    H'0'                                                             
         SPACE 2                                                                
OFFOPN   TM    POWWOWS,X'80'       OPEN INTERNAL READER                         
         BO    EXIT                                                             
         OPEN  (IRDR)                                                           
         OI    POWWOWS,X'80'                                                    
         B     EXIT                                                             
         SPACE 2                                                                
OFFCLO   TM    POWWOWS,X'80'       CLOSE INTERNAL READER                        
         BZ    EXIT                                                             
         CLOSE (IRDR)                                                           
         NI    POWWOWS,X'7F'                                                    
         B     EXIT                                                             
         SPACE 2                                                                
OFFEND   MVC   AREA0,SPACES        END PUT SEQUENCE                             
         MVC   AREA0(2),=C'//'                                                  
         TM    POWWOWS,X'04'       EXIT IF LAST COMMAND WAS ENDREQ              
         BO    EXIT                                                             
         PUT   RPL=ORPL0                                                        
         ENDREQ RPL=ORPL0                                                       
         OI    POWWOWS,X'04'       SET LAST COMMAND IS ENDREQ                   
         B     EXIT                                                             
         SPACE 2                                                                
OFFPUT   L     R7,P4               POINT TO CARD                                
         LA    R7,8(R7)                                                         
         MVC   AREA0,0(R7)         MOVE TO OUTPUT AREA                          
OFFPUT1  PUT   RPL=ORPL0                                                        
         NI    POWWOWS,X'FB'                                                    
         CLC   0(2,R7),=C'//'                                                   
         BNE   OFFPUTX                                                          
         CLC   2(40,R7),SPACES                                                  
         BNE   OFFPUTX                                                          
         ENDREQ RPL=ORPL0                                                       
         OI    POWWOWS,X'04'       SET LAST COMMAND IS ENDREQ                   
         L     R4,P3               RETURN JES JOB ID                            
         MVC   0(8,R4),RPLRBAR                                                  
         MVI   P3,X'80'            SET RETURN JOB ID FLAG                       
OFFPUTX  L     R4,P4               RETURN A(IRDR SAVE AREA )                    
         LA    RE,IRDRTAB                                                       
         LA    RE,IRSAVE-IRDRTABD(RE)                                           
         ST    RE,0(R4)                                                         
         B     EXIT                                                             
         SPACE 2                                                                
JESONL   L     RE,=V(SSB)          ONLINE FIND READER FOR TASK                  
         USING SSBD,RE                                                          
         L     R5,SSBTKADR         R5=A(TCB ENTRY)                              
         USING TCBD,R5                                                          
         LA    R5,0(R5)                                                         
         ST    R5,SVTKADR                                                       
         LA    R6,IRDRTAB          R6=A(IRDR TABLE ENTRY)                       
         USING IRDRTABD,R6                                                      
*                                                                               
JESONL1  OC    TCBIRDR,TCBIRDR     TEST IF TASK HAS READER ALLOCATED            
         BZ    JESONL1A                                                         
         L     R6,TCBIRDR          POINT TO ALLOCATED TABLE ENTRY               
         LA    R6,0(R6)                                                         
         CLC   SVTKADR+1(3),IRATCB+1                                            
         BE    JESONL3                                                          
         DC    H'0'                DIE IF NO CROSS CHECK                        
JESONL1A L     RE,P2                                                            
         CLC   0(3,RE),=C'END'                                                  
         BE    EXIT                EXIT IF ENDING UNALLOCATED IRDR              
JESONL1B CLI   IRFLAG1,X'FF'                                                    
         BNE   JESONL1C                                                         
         MVI   P3,X'FF'            RETURN READER NOT AVAILABLE                  
         B     EXIT                                                             
JESONL1C OC    IRATCB+1(3),IRATCB+1                                             
         BZ    JESONL2             AVAILABLE ENTRY FOUND                        
         CLC   IRATCB+1(3),SVTKADR+1                                            
         BNE   *+6                                                              
         DC    H'0'                DIE IF READER ALLOCATED TO TASK              
         LA    R6,IRDRTABL(R6)                                                  
         B     JESONL1B                                                         
*                                                                               
JESONL2  ST    R6,TCBIRDR          ALLOCATE READER TO TASK                      
         NI    TCBFLAG1,255-TCBIRALC-TCBIRUSE-TCBIREND                          
         OI    TCBFLAG1,TCBIRALC   SET ALLOCATED FLAG                           
*                                                                               
         STCM  R5,7,IRATCB+1       ALLOCATE TASK TO READER                      
         NI    IRFLAG1,X'80'                                                    
         OI    IRFLAG1,TCBIRALC    SET ALLOCATED FLAG                           
         XC    IRSAVE,IRSAVE       CLEAR SAVE STORAGE AREA                      
*                                                                               
         TM    IRFLAG1,X'80'       OPEN READER IF NECESSARY                     
         BO    JESONL3                                                          
         L     R3,IRAACB           POINT TO ACCESS CONTROL BLOCK                
         OPEN  ((3))                                                            
         MVI   IRFLAG1,X'80'                                                    
*                                                                               
JESONL3  ST    R6,SVIRADR          SAVE ADDR OF IRDR TABLE ENTRY                
         L     R3,IRAACB           R3=A(IRDR ACB)                               
         L     RE,P2                                                            
         CLC   0(3,RE),=C'OPE'     OPEN IRDR                                    
         BE    ONLOPN                                                           
         CLC   0(3,RE),=C'CLO'     CLOSE IRDR                                   
         BE    ONLCLO                                                           
         CLC   0(3,RE),=C'END'     ISSUE ENDREQ ON IRDR                         
         BE    ONLEND                                                           
         CLC   0(3,RE),=C'ADR'     RETURN A(IRDR SAVE DATA)                     
         BE    ONLPUTX                                                          
         L     RE,P1               PARAM1 MUST BE C'PUT'                        
         CLC   0(3,RE),=C'PUT'                                                  
         BE    ONLPUT                                                           
         DC    H'0'                                                             
         SPACE 2                                                                
ONLOPN   TM    IRFLAG1,X'80'       OPEN INTERNAL READER                         
         BO    EXIT                                                             
         OPEN  ((3))                                                            
         MVI   IRFLAG1,X'80'                                                    
         B     EXIT                                                             
         SPACE 2                                                                
ONLCLO   TM    IRFLAG1,X'80'       CLOSE INTERNAL READER                        
         BZ    EXIT                                                             
         CLOSE ((3))                                                            
         MVI   IRFLAG1,X'00'                                                    
         B     EXIT                                                             
         SPACE 2                                                                
ONLEND   TM    IRFLAG1,TCBIRUSE    TEST IF ANY CARDS OUTPUT                     
         BZ    ONLEND2             NO                                           
         TM    IRFLAG1,TCBIREND    TEST IF LAST CARD WAS MVS EOJ                
         BO    ONLEND2             YES                                          
*                                                                               
ONLEND1  L     R4,IRAPUT           POINT TO PUT AREA FOR IRDR                   
         MVC   0(80,R4),SPACES                                                  
         MVC   0(2,R4),=C'//'      SET UP MVS EOJ CARD                          
         L     RA,IRARPL           POINT TO REQ PARAM LIST FOR IRDR             
         PUT   RPL=(10)                                                         
         ENDREQ RPL=(10)                                                        
*                                                                               
ONLEND2  NI    TCBFLAG1,255-TCBIRALC-TCBIRUSE-TCBIREND                          
         XC    TCBIRDR,TCBIRDR                                                  
         NI    IRFLAG1,X'80'                                                    
         XC    IRATCB+1(3),IRATCB+1                                             
         B     EXIT                                                             
         SPACE 2                                                                
ONLPUT   L     R7,P4               R7=A(PASSED CARD DATA)                       
         LA    R7,8(R7)                                                         
         L     R4,IRAPUT           POINT TO PUT AREA FOR IRDR                   
         MVC   0(80,R4),0(R7)                                                   
*                                                                               
         L     RA,IRARPL           POINT TO REQ PARAM LIST FOR IRDR             
         PUT   RPL=(10)                                                         
         OI    TCBFLAG1,TCBIRUSE   SET IRDR USED BY TASK                        
         NI    TCBFLAG1,255-TCBIREND                                            
         OI    IRFLAG1,TCBIRUSE                                                 
         NI    IRFLAG1,255-TCBIREND                                             
*                                                                               
         CLC   0(2,R7),=C'//'      TEST MVS EOJ CARD                            
         BNE   ONLPUTX                                                          
         CLC   2(40,R7),SPACES                                                  
         BNE   ONLPUTX                                                          
         ENDREQ RPL=(10)                                                        
         OI    TCBFLAG1,TCBIREND   SET IRDR ENDED BY TASK                       
         OI    IRFLAG1,TCBIREND                                                 
         L     R4,P3               RETURN JES JOB ID                            
         MVC   0(8,R4),RPLRBAR                                                  
         MVI   P3,X'80'            SET RETURN JOB ID FLAG                       
*                                                                               
ONLPUTX  L     R4,P4               RETURN A(IRDR SAVE AREA FOR TASK)            
         LA    RE,IRSAVE                                                        
         ST    RE,0(R4)                                                         
         B     EXIT                                                             
*&&                                                                             
         EJECT                                                                  
         LTORG                                                                  
         SPACE 2                                                                
RELO     DC    A(*)                                                             
POWRES   DC    X'00'                                                            
         DC    3X'00'                                                           
SPACES   DC    CL80' '                                                          
         EJECT                                                                  
*&&DO                                                                           
         DC    0F'0'               DOS ENTRY POINT                              
POWWOWS  DC    X'00'               POWWOW STATUS BYTE                           
POWWOWT  DC    AL3(0)              A(ONLINE TASK OWNING XECB'S)                 
*                                                                               
POWSPL   SPL   TYPE=DEFINE,PBUF=POWBUFF,PBUFL=140                               
         DC    CL16' '                                                          
POWBUFF  DC    CL140' '                                                         
         DC    CL16' '                                                          
*                                                                               
ICRXECB  DC    A(0,POWSPL)                                                      
SPMXECB  DC    A(0,POWSPL)                                                      
*&&                                                                             
         EJECT                                                                  
*&&OS                                                                           
         DC    0F'0'               MVS ENTRY POINT                              
POWWOWS  DC    X'00'               POWWOW STATUS BYTE                           
POWWOWT  DC    AL3(IRDRTAB)        A(INTERNAL READER TABLE)                     
         SPACE 1                                                                
IRDRTAB  DS    0CL64                                                            
         DC    A(00),A(IRDR),A(ORPL0),A(AREA0),XL48'00'                         
         DC    X'FFFFFFFF'                                                      
         SPACE 1                                                                
IRDR     ACB   AM=VSAM,MACRF=(ADR,SEQ,OUT),DDNAME=IRDR                          
*                                                                               
ORPL0    RPL   ACB=IRDR,OPTCD=(ADR,SEQ,SYN,NUP),RECLEN=80,             *        
               AREA=AREA0,AREALEN=80                                            
AREA0    DC    CL80' '                                                          
*&&                                                                             
         EJECT                                                                  
PARAMS   DSECT                                                                  
P1       DS    A                   A(C'GET' OR C'PUT')                          
P2       DS    A                   A(C'CON' OR C'INI')                          
P3       DS    A                   JOB NAME(8)/CLASS(1)/DISP(1)                 
P4       DS    A                   A(BUFFER)                                    
         SPACE 2                                                                
WORKD    DSECT                                                                  
DUB      DS    D                                                                
SVTKADR  DS    A                                                                
SVIRADR  DS    A                                                                
WORKX    DS    0C                                                               
         SPACE 2                                                                
IRDRTABD DSECT                                                                  
IRFLAG1  DS    0X                  ENTRY FLAGS                                  
IRATCB   DS    A                   A(OWNER TCB ENTRY)                           
IRAACB   DS    A                   A(READER ACCESS CONTROL BLOCK)               
IRARPL   DS    A                   A(READER REQUEST PARAM LIST)                 
IRAPUT   DS    A                   A(READER PUT AREA FOR RPL)                   
IRSAVE   DS    XL48                SAVE AREA FOR OWNER TASK                     
IRDRTABL EQU   *-IRDRTABD                                                       
         EJECT                                                                  
*FASSB                                                                          
       ++INCLUDE FASSB                                                          
         EJECT                                                                  
*FATCB                                                                          
       ++INCLUDE FATCB                                                          
         EJECT                                                                  
*&&DO                                                                           
SPL      SPL   TYPE=MAP                                                         
*&&                                                                             
         EJECT                                                                  
*&&OS                                                                           
         IFGRPL                                                                 
*&&                                                                             
         SPACE 2                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'003DDPOWWOW  05/01/02'                                      
         END                                                                    
