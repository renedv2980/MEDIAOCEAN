*          DATA SET PRRCVCHK   AT LEVEL 015 AS OF 05/01/02                      
*CATALP PRRCVCHK                                                                
         TITLE 'PRRCVCHK - COMPARE PRTFILE AND SORTED RECOVERY FILE'            
PRRCVCHK CSECT                                                                  
         PRINT NOGEN                                                            
         NBASE 0,PRRCVCHK,=V(REGSAVE)                                           
*                                                                               
         L     RA,=V(CPRINT)                                                    
         USING DPRINT,RA                                                        
*                                                                               
KEYLEN   EQU   25                                                               
*                                                                               
         GOTO1 =V(CARDS),P1,REC1,=C'RE00'                                       
*                                                                               
         CLC   =C'PRNT',REC1                                                    
         BNE   BADSYS                                                           
         MVC   MULTIVOL,REC1+5     SET MULTI VOLUME SWITCH                      
         LA    R3,PRINTSES                                                      
CHKSYS   CLC   0(1,R3),REC1+4                                                   
         BE    CHK2                                                             
         LA    R3,2(R3)                                                         
         CLI   0(R3),0                                                          
         BNE   CHKSYS                                                           
BADSYS   DC    0H'0'                                                            
         MVC   MSG1SYS(7),REC1                                                  
         GOTO1 =V(LOGIO),P1,1,(48,MSG1)                                         
         MVC   P(LMSG1),MSG1                                                    
         GOTO1 =V(PRINTER)                                                      
         ABEND 999                                                              
MSG1     DC    C'** PRRCVCHK * ERROR **'                                        
MSG1SYS  DC    CL8' '                                                           
         DC    C'NOT A VALID SYSTEM'                                            
LMSG1    EQU   *-MSG1                                                           
       ++INCLUDE PRINTSES                                                       
         EJECT                                                                  
CHK2     MVC   SVSE,REC1                                                        
         L     RE,=A(UTL)                                                       
         MVC   4(1,RE),1(R3)       SET SE NUMBER                                
*                                                                               
         MVC   TITLE(6),SVSE                                                    
         MVC   TITLE+7(19),=C'RECOVERY TAPE AUDIT'                              
*                                                                               
         GOTO1 =V(DATAMGR),DMCB,=C'DMOPEN',=C'PRINT',OPENLST,A(REC1)            
         B     CHK3                                                             
OPENLST  DC    C' PRTFILEX       '                                              
*                                                                               
CHK3     DC    0H'0'                                                            
         OPEN  (IN,INPUT)                                                       
         GOTO1 =V(SORTER),DMCB,SORTCARD,RECCARD                                 
         SR    R8,R8               CLEAR COUNTER                                
         B     CHKIN                                                            
*                                                                               
SORTCARD DC    CL80'SORT FIELDS=(5,28,A),FORMAT=BI,WORK=1'                      
RECCARD  DC    CL80'RECORD TYPE=V,LENGTH=(2500,,,56,516)'                       
*                                                                               
CHKIN    GET   IN,REC1-4                                                        
**NEW 1/30/90        WAS X'12'                                                  
         CLI   REC1,X'42'               TEST PRTFILE                            
         BNE   CHKIN                                                            
* SET REC LEN TO INCLUDE SORT KEY                                               
         LH    RE,REC1-4                                                        
         LA    RE,KEYLEN+3(RE)                                                  
         SLL   RE,16                                                            
         ST    RE,SRTLEN                                                        
         MVC   SRTKEY(KEYLEN),REC1+24  SORT ON FILE KEY                         
         LA    R8,1(R8)                                                         
         STCM  R8,7,SRTKEY+KEYLEN       AND SEQUENCE NUMBER                     
         GOTO1 =V(SORTER),DMCB,=C'PUT',SRTLEN                                   
         B     CHKIN                                                            
*                                                                               
CHKEOF   DC    0H'0'                                                            
         CLOSE (IN,)                                                            
         CLI   MULTIVOL,C' '                                                    
         BE    CHKEOFX                                                          
CHKEOF2  MVC   P(45),=C'*PRRCVCHK* ANY MORE INPUT TAPES? REPLY Y OR N'          
         GOTO1 =V(LOGIO),DMCB,1,(45,P)                                          
         MVI   P,C'N'                                                           
         GOTO1 (RF),(R1),0,(3,P)                                                
         CLI   P,C'N'                                                           
         BE    CHKEOFX                                                          
         CLI   P,C'Y'                                                           
         BNE   CHKEOF2                                                          
         OPEN  (IN,INPUT)                                                       
         B     CHKIN                                                            
CHKEOFX  DC    0H'0'                                                            
         BAS   RE,GET                                                           
         SPACE 2                                                                
CHK4     L     RE,=A(REC1-4)                                                    
         CLI   4(RE),X'FF'         TEST E-O-F ON TAPE                           
         BE    CHKX                                                             
         LH    R0,0(RE)                                                         
         L     RF,=A(REC2-4)                                                    
* MOVE REC1 TO REC2                                                             
CHK6     CH    R0,=H'256'                                                       
         BNH   CHK8                                                             
         MVC   0(256,RF),0(RE)                                                  
         LA    RE,256(RE)                                                       
         LA    RF,256(RF)                                                       
         SH    R0,=H'256'                                                       
         BP    CHK6                                                             
         B     CHK10                                                            
CHK8     LR    R1,R0                                                            
         BCTR  R1,0                                                             
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   0(0,RF),0(RE) *EXECUTED*                                         
*                                                                               
CHK10    BAS   RE,GET                                                           
*                                                                               
         L     R4,=A(REC1+24)                                                   
         L     R5,=A(REC2+24)                                                   
         CLC   0(KEYLEN,R4),0(R5)                                               
         BE    CHK4                                                             
* READ REC FROM FILE                                                            
         L     R4,=A(REC2)                                                      
         LA    R4,RVCHR-RECVHDR(R4)                                             
         GOTO1 =V(DATAMGR),DMCB,(X'08',GETREC),PRTFILE,(R4),A(FILREC), X        
               A(GETWORK)                                                       
*                                                                               
         TM    8(R1),X'FD'         TEST ALL BUT DELETED                         
         BNZ   ERR1                                                             
*                                                                               
         L     R4,=A(FILREC)                                                    
         L     R5,=A(REC2+24)                                                   
*                                                                               
         CLC   0(16,R4),0(R5)      TEST SAME KEY THRU PUB                       
         BNE   ERR3                                                             
         CLC   16(3,R4),16(R5)     TEST SAME INSERTION DATE                     
         BNE   CHK4                NO - IGNORE KEY CHANGES                      
*                                                                               
         CLC   KEYLEN(2,R4),KEYLEN(R5)     TEST SAME LEN                        
         BNE   ERR2                                                             
*                                                                               
         MVC   DUB(2),KEYLEN(R5)                                                
         LH    RE,DUB                                                           
*                                                                               
CHK12    CH    RE,=H'256'                                                       
         BNH   CHK14                                                            
         CLC   0(256,R4),0(R5)                                                  
         BNE   ERR3                                                             
         LA    R4,256(R4)                                                       
         LA    R5,256(R5)                                                       
         SH    RE,=H'256'                                                       
         BP    CHK12                                                            
         B     CHK16                                                            
CHK14    BCTR  RE,0                                                             
         EX    RE,*+8                                                           
         B     *+10                                                             
         CLC   0(0,R4),0(R5) *EXECUTED*                                         
*                                                                               
CHK16    B     CHK4                                                             
*                                                                               
CHKX     MVC   P(LMSGEOJ),MSGEOJ                                                
         GOTO1 =V(PRINTER)                                                      
         XBASE                                                                  
*                                                                               
MSGEOJ   DC    C'** PRRCVCHK * END OF JOB **'                                   
LMSGEOJ  EQU   *-MSGEOJ                                                         
         EJECT                                                                  
ERR1     MVC   P(20),=CL20'FILE READ ERROR'                                     
         GOTO1 =V(PRINTER)                                                      
*                                                                               
         BAS   R9,PRTREC2                                                       
         B     CHK4                                                             
*                                                                               
ERR2     MVC   P(20),=CL20'DIFFERENT LENGTHS'                                   
         GOTO1 =V(PRINTER)                                                      
         B     ERRX                                                             
*                                                                               
ERR3     MVC   P(20),=CL20'DIFFERENT DATA   '                                   
         GOTO1 =V(PRINTER)                                                      
*                                                                               
ERRX     BAS   R9,PRTREC2                                                       
         BAS   R9,PRTFIL                                                        
         B     CHK4                RETURN TO MAIN LINE                          
         SPACE 2                                                                
PRTREC2  L     R4,=A(REC2)                                                      
         GOTO1 =V(PRNTBL),P1,(16,RCVTITLE),(R4),C'DUMP',24,=C'2D'               
         LA    R4,24(R4)                                                        
         MVC   DUB(2),KEYLEN(R4)                                                
         LH    R0,DUB                                                           
         GOTO1 =V(PRNTBL),P1,(16,RCVDATA),(R4),C'DUMP',(R0),=C'2D'              
         B     PRTFILX                                                          
*                                                                               
PRTFIL   L     R4,=A(FILREC)                                                    
         MVC   DUB(2),KEYLEN(R4)                                                
         LH    R0,DUB                                                           
         GOTO1 =V(PRNTBL),P1,(16,FILDATA),(R4),C'DUMP',(R0),=C'2D'              
PRTFILX  GOTO1 =V(PRINTER)         SKIP 2 LINES                                 
         GOTO1 =V(PRINTER)                                                      
         BR    R9                                                               
*                                                                               
RCVTITLE DC    CL16'RECOVERY HEADER'                                            
RCVDATA  DC    CL16'RECOVERY DATA  '                                            
FILDATA  DC    CL16'FILE DATA'                                                  
         EJECT                                                                  
GET      NTR1                                                                   
         GOTO1 =V(SORTER),DMCB,=C'GET'                                          
         L     RE,4(R1)                                                         
         LTR   RE,RE                                                            
         BNZ   GET2                                                             
         L     RE,=A(REC1-4)                                                    
         MVI   4(RE),X'FF'                                                      
         B     GETX                                                             
*                                                                               
* REMOVE SORT KEY AND SAVE RECORD IN REC1                                       
*                                                                               
GET2     LH    RF,0(RE)                                                         
         LA    R0,KEYLEN+3                                                      
         SR    RF,R0                                                            
         SLL   RF,16                                                            
         ST    RF,REC1-4                                                        
*                                                                               
         LH    RF,0(RE)                                                         
         LA    R0,(KEYLEN+3)+4     ADJUST FOR SORT KEY AND LEN FIELD            
         SR    RF,R0               SET 'FROM' LEN = DATA LEN ONLY               
         AR    RE,R0               SET 'FROM' ADDR                              
         L     R4,=A(REC1)         SET 'TO' ADDR                                
         LA    R5,1(RF)            SET 'TO' LEN = 'FROM' LEN + 1                
         MVCL  R4,RE                                                            
GETX     XIT1                                                                   
         EJECT                                                                  
SVSE     DS    XL10                                                             
DMCB     DC    6A(0)                                                            
*                                                                               
PRTFILE  DC    C'PRTFILE'                                                       
GETREC   DC    C'GETREC'                                                        
GETWORK  DS    12D                                                              
*                                                                               
P1       DC    F'0'                                                             
P2       DC    F'0'                                                             
P3       DC    F'0'                                                             
P4       DC    F'0'                                                             
P5       DC    F'0'                                                             
P6       DC    F'0'                                                             
*                                                                               
DUB      DS    D                                                                
MULTIVOL DC    C' '                                                             
*                                                                               
         LTORG                                                                  
         EJECT                                                                  
*                                                                               
IN       DCB   DDNAME=IN,              DOS SYS004                      X        
               DSORG=PS,                                               X        
               RECFM=VB,                                               X        
               LRECL=04628,                                            X        
               BLKSIZE=08200,                                          X        
               MACRF=GM,                                               X        
               EODAD=CHKEOF                                                     
         DS    D                                                                
SRTLEN   DS    F                                                                
SRTKEY   DS    CL28                                                             
REC1     DS    4100C                                                            
*                                                                               
         DS    D                                                                
REC2     DS    4100C                                                            
*                                                                               
         DS    D                                                                
FILREC   DS    4100C                                                            
*                                                                               
         SPACE 2                                                                
UTL      CSECT                                                                  
         DC    2F'0'                                                            
         EJECT                                                                  
       ++INCLUDE DMRCVRHDR                                                      
         SPACE 2                                                                
         EJECT                                                                  
       ++INCLUDE DDDPRINT                                                       
 END                                                                            
