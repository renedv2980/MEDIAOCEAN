*          DATA SET RETEXT     AT LEVEL 020 AS OF 05/01/02                      
*CATALP RETEXT                                                                  
         SPACE 2                                                                
* MODULE EDITS 4 TEXT LINES ASSUMED TO BE OF EQUAL LENGTH.  FOR                 
* SINGLE ADD IT CONSTRUCTS ELEMENTS AND ADDS TO RECORD.  FOR                    
* MULTIPLE ADD IT JUST MOVES THE ELEMENTS TO AN AREA SPECIFIED                  
* BY A PARAMETER.                                                               
*                                                                               
* PARAMETER 1  BYTES 0-3           A(FIRST FIELD HEADER)                        
* PARAMETER 2  BYTES 0-3           A(RECORD OR OUTPUT AREA)                     
* PARAMETER 3  BYTE  0             C'A' - AVAIL                                 
*                                  C'P' - PROPOSAL                              
*                                  X'14'- NEW AVAIL                             
*                                  X'16'- NEW PROPOSAL                          
*              BYTES 1-3           ZERO - SINGLE ADD                            
*                                  NON-ZERO - MULTIPLE ADD                      
* PARAMETER 4  BYTES 0-3           A(SCANNER)                                   
* PARAMETER 5  BYTES 0-3           A(RECUP)                                     
*                                                                               
* AFTER MODULE                                                                  
*                                                                               
* PARAMETER  1 BYTES 0-3           ZERO IF OK                                   
*                                  A(ERROR FIELD HEADER)                        
* PARAMETER  2 BYTE  0             NUMBER OF TEXT ELEMENTS                      
         SPACE                                                                  
         TITLE 'BUILD REP TEXT ELEMENTS'                                        
RETEXT   CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 TEXTEND-TEXTD,**TEXT,CLEAR=YES                                   
         USING TEXTD,RC                                                         
         SPACE                                                                  
INIT     ST    R1,PLIST                                                         
         LM    R2,R6,0(R1)                                                      
         STM   R2,R6,VFIELD                                                     
         SPACE                                                                  
         CLI   8(R1),C'A'                                                       
         BNE   *+12                                                             
         OI    CONTROL,AVAIL                                                    
         B     INIT2                                                            
         CLI   8(R1),C'P'                                                       
         BNE   *+12                                                             
         OI    CONTROL,PROPOSAL                                                 
         B     INIT2                                                            
         CLI   8(R1),X'14'                                                      
         BNE   *+12                                                             
         OI    CONTROL,NAVAIL                                                   
         B     INIT2                                                            
         CLI   8(R1),X'16'                                                      
         BE    *+6                                                              
         DC    H'0'                                                             
         OI    CONTROL,NPROP                                                    
         SPACE                                                                  
INIT2    OI    CONTROL,SINGLE                                                   
         SLL   R4,8                ZERO OUT THE 'A' OR 'P'                      
         SRL   R4,8                                                             
         LTR   R4,R4               IF NON -ZERO ITS MULTIPLE ADD                
         BZ    INIT4                                                            
         XI    CONTROL,MULTIPLE+SINGLE                                          
         SPACE                                                                  
INIT4    L     R5,VFIELD                                                        
         ZIC   R6,0(R5)            L'HEADER+FIELD                               
         SH    R6,=H'8'                                                         
         TM    1(R5),X'02'         EXTENDED?                                    
         BZ    *+8                 NO                                           
         SH    R6,=H'8'                                                         
         STC   R6,FLDLEN                                                        
         SH    R6,=H'10'           SECOND HALF OVERRIDE FOR                     
         STC   R6,SECHALF          SCANNER COMPUTED                             
         LA    R4,ELEMENTS         R4 POINTS TO ELEMENTS                        
         USING RAVLTEL,R4                                                       
         LA    R3,4                R3 IS COUNTER OF SCREEN LINES                
         SPACE 2                                                                
* REGISTER CONVENTIONS FOR REST OF CODE:                                        
* R2 POINTS TO SCANNER BLOCK       R3 COUNTS SCREEN LINES TO EDIT               
* R4 POINTS TO ELEMENT             R5 CONTAINS A(HEADER)                        
* R6 COUNTS SCANNER LINES          R8 POINTS TO KEYWORD TABLE                   
*                                                                               
*                                                                               
SCAN     ZIC   R6,SECHALF                                                       
         LA    R2,BLOCK                                                         
         CLI   5(R5),0             LOOK FOR INPUT                               
         BE    EXIT                NONE                                         
         GOTO1 SCANNER,DMCB,((R6),(R5)),(15,(R2)),0                             
         CLI   DMCB+4,0                                                         
         BE    ERROR                                                            
         ZIC   R6,DMCB+4           SCANNER LINES                                
         ZIC   R1,SEQ                                                           
         AH    R1,=H'32'           BUMP SEQUENCE BITS FOR                       
         STC   R1,SEQ              NEW LINE                                     
         NI    SEQ,X'E0'                                                        
         SPACE                                                                  
SCAN2    CLI   0(R2),0                                                          
         BE    ERROR                                                            
         CLI   1(R2),0             UNDIVIDED FIELD MEANS MANUAL                 
         BE    SCANMAN                                                          
         SPACE                                                                  
         CLI   0(R2),1             KEYWORDS MUST BE EITHER 1 OR 3               
         BE    *+12                BYTES IN LENGTH.                             
         CLI   0(R2),3                                                          
         BNE   ERROR                                                            
         LA    R7,KEYENT           LOOK FOR A VALID KEYWORD                     
         LA    R8,KEYTAB                                                        
         USING KEYSD,R8                                                         
         ZIC   R1,0(R2)            INPUT LENGTH                                 
         BCTR  R1,0                                                             
         EX    R1,COMPKEY                                                       
         BE    SCAN4               FOUND IT                                     
         LA    R8,L'KEYENTRY(R8)                                                
         BCT   R7,*-12             TRY AGAIN                                    
         B     ERROR               NO GOOD                                      
         SPACE 2                                                                
SCAN4    MVI   RAVLTCOD,X'08'                                                   
         MVC   RAVLTLEN,KEYELLN    EL LENGTH DETERMINED BY KEYWORD              
         MVC   RAVLTTYP,KEYTYPE                                                 
         TM    CONTROL,AVAIL+SINGLE                                             
         BO    SCAN6               ALL KEYS VALID FOR AVAIL/SINGLE              
         SPACE 1                                                                
         TM    CONTROL,PROPOSAL                                                 
         BZ    *+12                                                             
         TM    KEYCNTL,PROPOSAL    KEYWORD VALID FOR PROPOSALS                  
         BZ    ERROR               NO                                           
         SPACE 1                                                                
         TM    CONTROL,NPROP       NEW PROPOSALS                                
         BZ    *+12                                                             
         TM    KEYCNTL,NPROP                                                    
         BZ    ERROR                                                            
         SPACE 1                                                                
         TM    CONTROL,AVAIL+MULTIPLE                                           
         BNO   *+12                                                             
         TM    KEYCNTL,AVAIL+MULTIPLE                                           
         BNO   ERROR                                                            
         SPACE 2                                                                
SCAN6    TM    3(R2),X'80'         DEAL WITH PARAMETER                          
         BZ    SCAN8                                                            
         CLC   KEYMAXLN,1(R2)                                                   
         BL    ERROR                                                            
         OC    8(4,R2),8(R2)       ZERO IS AN INVALID PARAMETER VALUE.          
         BZ    ERROR                                                            
         CLI   KEYTYPE,C'S'                                                     
         BE    *+14                                                             
         MVC   RAVLTNUM,10(R2)                                                  
         B     *+10                                                             
         MVC   RAVLTSPC,11(R2)     ONLY 1 BYTE FOR SPACE                        
         B     SCAN10                                                           
         SPACE 2                                                                
SCAN8    TM    3(R2),X'40'                                                      
         BZ    ERROR                                                            
         CLI   1(R2),3             ONLY ALPHA VALUE ALLOWED IS                  
         BNE   ERROR               3 BYTE 'ALL'                                 
         CLC   22(3,R2),=C'ALL'                                                 
         BNE   ERROR                                                            
         TM    KEYCNTL,ALL+ALLSING    MAKE SURE KEY PERMITS IT AND              
         BZ    ERROR                                                            
         TM    CONTROL,AVAIL+NPROP    THE EDIT IS FOR AVAIL OR NEW PROP         
         BZ    ERROR                                                            
         TM    CONTROL,MULTIPLE       TEST FOR MULTIPLE ADD                     
         BZ    *+12                                                             
         TM    KEYCNTL,ALLSING        TEST WHETHER KEY RESTRICTS                
         BO    ERROR                  'ALL' TO SINGLE ADD                       
         SPACE 2                                                                
SCAN10   B     NEXTEL                                                           
         SPACE 2                                                                
* COMMAS MAY BE INTERSPERSED IN MANUAL TEXT. CONCATENATE MANUAL                 
* TEXT TO LAST ELEMENT'S TEXT IF IT WAS MANUAL.                                 
*                                                                               
SCANMAN  TM    CONTROL,AVAIL+MULTIPLE                                           
         BO    ERROR               NO MANUAL FOR AVAIL MULTIPLE                 
         ICM   RE,15,ALASTEL       RE CONTAINS LAST ELEMENT ADDRESS             
         BZ    SCANMAN2            START OF LINE                                
         CLI   RAVLTTYP-RAVLTEL(RE),C'M'                                        
         BE    SCANMAN4            CONCATENATE TEXT TO LAST ELEMENT             
         SPACE 1                                                                
SCANMAN2 MVI   RAVLTCOD,X'08'                                                   
         MVI   RAVLTTYP,C'M'                                                    
         ZIC   R1,0(R2)            TEXT LENGTH                                  
         BCTR  R1,0                                                             
         EX    R1,MOVEMAN                                                       
         LA    R1,5(R1)                                                         
         STC   R1,RAVLTLEN                                                      
         B     NEXTEL                                                           
         SPACE 1                                                                
SCANMAN4 MVI   0(R4),COMMA                                                      
         ZIC   R1,0(R2)            LENGTH OF NEW TEXT FOR CONCATENATION         
         BCTR  R1,0                                                             
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   1(0,R4),12(R2)      SPLICE TEXT TOGETHER                         
         LA    R1,2(R1)            COMMA PLUS DATA LENGTH                       
         LR    R4,RE               SET ELEMENT POINTER BACK                     
         ZIC   R0,RAVLTLEN                                                      
         AR    R1,R0               COMBINED ELEMENT LENGTH                      
         STC   R1,RAVLTLEN                                                      
         B     RESET               SKIP NEXTEL CODE HERE                        
         SPACE 2                                                                
NEXTEL   ZIC   R1,SEQ              INCREMENT SEQUENCE NUMBER                    
         LA    R1,1(R1)                                                         
         STC   R1,SEQ                                                           
         MVC   RAVLTSEQ,SEQ                                                     
         ZIC   RE,ELCOUNT          AND ELEMENT COUNT                            
         LA    RE,1(RE)                                                         
         STC   RE,ELCOUNT                                                       
         SPACE 1                                                                
RESET    ST    R4,ALASTEL          UPDATE LAST ELEMENT POINTER                  
         ZIC   R1,RAVLTLEN         BUMP POINTER TO POSITION FOR                 
         AR    R4,R1               NEXT ELEMENT                                 
         ZIC   RE,SECHALF                                                       
         LA    R2,22(RE,R2)        POINT TO NEXT SCANNER LINE                   
         BCT   R6,SCAN2                                                         
         SPACE                                                                  
NEWLINE  ZIC   R1,0(R5)            POINT TO NEXT SCREEN HEADER                  
         AR    R5,R1                                                            
         XC    ALASTEL,ALASTEL     RE-CLEAR LAST ELEMENT POINTER                
         BCT   R3,SCAN             SCAN NEXT LINE                               
         SPACE 2                                                                
EXIT     CLI   ELCOUNT,0           GET OUT NOW IF NO ELEMENTS                   
         BE    EXIT4                                                            
         TM    CONTROL,MULTIPLE    MOVE ELEMENTS BACK TO CALLER'S               
         BO    EXIT2               STORAGE FOR MULTIPLE ADD AND                 
         BAS   RE,ADDELS           ADD THEM TO RECORD FOR SINGLE ADD.           
         B     EXIT4                                                            
         SPACE                                                                  
EXIT2    LR    R1,R4               POINTER TO END OF ELEMENTS                   
         LA    RE,ELEMENTS         START                                        
         SR    R1,RE               DIFFERENCE IS LENGTH OF ALL ELEMENTS         
         L     RF,VRECORD                                                       
         CH    R1,=H'256'                                                       
         BH    EXIT3                                                            
         BCTR  R1,0                                                             
         EX    R1,*+8                                                           
         B     EXIT4                                                            
         MVC   0(0,RF),ELEMENTS                                                 
         SPACE                                                                  
EXIT3    MVC   0(256,RF),0(RE)                                                  
         SH    R1,=H'256'                                                       
         BCTR  R1,0                                                             
         LA    RF,256(RF)                                                       
         LA    RE,256(RE)                                                       
         EX    R1,*+8                                                           
         B     EXIT4                                                            
         MVC   0(0,RF),0(RE)                                                    
         SPACE                                                                  
EXIT4    L     R1,PLIST                                                         
         XC    0(4,R1),0(R1)       OK                                           
         MVC   4(1,R1),ELCOUNT                                                  
         B     EXIT6                                                            
         SPACE                                                                  
ERROR    L     R1,PLIST                                                         
         ST    R5,0(R1)            FIELD HEADER WHERE ERROR IS                  
         SPACE                                                                  
EXIT6    XMOD1 1                                                                
         EJECT                                                                  
* SUB-ROUTINE TO ADD ELEMENTS TO RECORD                                         
*                                                                               
ADDELS   NTR1                                                                   
         ZIC   R7,ELCOUNT          COUNTER                                      
         LA    R6,ELEMENTS         POINTER TO ELEMENTS                          
         SPACE                                                                  
ADD2     L     R2,VRECORD                                                       
         MVC   HALF,27(R2)                                                      
         LH    R5,HALF                                                          
         AR    R5,R2                                                            
         BCTR  R5,0                R5 POINTS TO EOR-BXLE LIMIT                  
         SPACE 1                                                                
         TM    CONTROL,NAVAIL+NPROP                                             
         BZ    *+6                                                              
         BCTR  R5,0       FOR GENCON RECORDS, NEED TO BACK UP 1 MORE            
         SPACE 1                                                                
         SR    R4,R4                                                            
         LA    R3,34(R2)           R3 POINTS TO ELEM ON RECORD                  
         SPACE                                                                  
ADD4     CLC   0(1,R6),0(R3)                                                    
         BL    *+12                                                             
         IC    R4,1(R3)                                                         
         BXLE  R3,R4,ADD4                                                       
         GOTO1 RECUP,DMCB,(2,(R2)),(R6),(R3)                                    
         SPACE                                                                  
         ZIC   R1,1(R6)                                                         
         AR    R6,R1               BUMP POINTER BY EL LENGTH                    
         BCT   R7,ADD2                                                          
         XIT1                                                                   
         SPACE 2                                                                
COMPKEY  CLC   12(0,R2),KEYWORD                                                 
MOVEMAN  MVC   RAVLTDTA(0),12(R2)                                               
         SPACE 2                                                                
KEYTAB   DS    0CL7                                                             
         DC    CL3'S',C'S',AL1(1),AL1(5)                                        
         DC    AL1(AVAIL+PROPOSAL+SINGLE+MULTIPLE)                              
         DC    CL3'T',C'T',AL1(4),AL1(6)                                        
         DC    AL1(AVAIL+PROPOSAL+SINGLE+MULTIPLE+ALL+NPROP)                    
         DC    CL3'MKT',C'Y',AL1(4),AL1(6)                                      
         DC    AL1(AVAIL+SINGLE+MULTIPLE+ALLSING)                               
         DC    CL3'STA',C'X',AL1(4),AL1(6)                                      
         DC    AL1(AVAIL+SINGLE+MULTIPLE+ALLSING)                               
         DC    CL3'L',C'L',AL1(4),AL1(6)                                        
         DC    AL1(AVAIL+PROPOSAL+SINGLE)                                       
KEYENT   EQU   (*-KEYTAB)/L'KEYTAB                                              
         SPACE 2                                                                
         LTORG                                                                  
         SPACE 2                                                                
AVAIL    EQU   X'01'                                                            
PROPOSAL EQU   X'02'                                                            
SINGLE   EQU   X'04'                                                            
MULTIPLE EQU   X'08'                                                            
ALL      EQU   X'10'               ALL VALID ON SINGLE AND MULT ADD             
ALLSING  EQU   X'20'               ALL VALID ONLY ON SINGLE ADD                 
NPROP    EQU   X'40'               NEW PROPOSALS                                
NAVAIL   EQU   X'80'                                                            
COMMA    EQU   C','                                                             
         SPACE 2                                                                
* DSECT TO COVER WORKING STORAGE                                                
*                                                                               
TEXTD    DSECT                                                                  
PLIST    DS    V                                                                
VFIELD   DS    V                                                                
VRECORD  DS    V                                                                
VCONTROL DS    V                                                                
SCANNER  DS    V                                                                
RECUP    DS    V                                                                
         SPACE                                                                  
ALASTEL  DS    A                                                                
         SPACE                                                                  
DMCB     DS    6F                                                               
FULL     DS    F                                                                
HALF     DS    H                                                                
         SPACE                                                                  
FLDLEN   DS    X                                                                
SECHALF  DS    X                                                                
CONTROL  DS    X                                                                
SEQ      DS    B                                                                
ELCOUNT  DS    X                                                                
         SPACE                                                                  
         DS    0D                                                               
ELEMENTS DS    CL512                                                            
BLOCK    DS    CL1500                                                           
TEXTEND  EQU   *                                                                
         SPACE 2                                                                
* DSECT TO COVER KEYWORD TABLE ENTRIES                                          
*                                                                               
KEYSD    DSECT                                                                  
KEYENTRY DS    0CL7                                                             
KEYWORD  DS    CL3                                                              
KEYTYPE  DS    C                                                                
KEYMAXLN DS    X                   MAX LENGTH OF PARAM VALUE                    
KEYELLN  DS    X                                                                
KEYCNTL  DS    X                                                                
         SPACE 2                                                                
RAVLD    DSECT                                                                  
*              REGENAVL                                                         
         PRINT OFF                                                              
       ++INCLUDE REGENAVL                                                       
         PRINT ON                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'020RETEXT    05/01/02'                                      
         END                                                                    
