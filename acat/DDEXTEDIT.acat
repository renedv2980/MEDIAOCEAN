*          DATA SET DDEXTEDIT  AT LEVEL 020 AS OF 05/10/99                      
*CATALP EXTEDIT                                                                 
         TITLE 'EDITS CASH VALUES IN ANY CURRENCY'                              
EXTEDIT  CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 WORKX-WORKD,**EXTE**                                             
*                                                                               
         USING WORKD,RC            RC=A(W/S)                                    
*                                                                               
         USING EXTPARMD,RA         RA=A(PARAMETER LIST)                         
         LR    RA,R1                                                            
*                                                                               
         USING COMFACSD,R2         MAP  COMFACS                                 
         L     R2,EXTCMFCS         ->   COMFACS                                 
         GOTO1 CGETFACT,DMCB,0,0   CALL GETFACT                                 
*                                                                               
         USING FACTSD,R2           MAP  GETFACT BLOCK                           
         L     R2,0(,R1)           ->   GETFACT BLOCK                           
         MVC   LANG,FALANG         GET  CURRENT LANGUAGE                        
         MVC   CTRY,FACTRY         GET  CURRENT COUNTY                          
         DROP  R2                                                               
*                                                                               
         CLI   EXTPCLNG,X'00'      ANY  SPECFIC LANGUAGE SPECIFIED ?            
         BE    *+10                NO,  SKIP                                    
         MVC   LANG,EXTPCLNG       YES, USE SPECIFIED LANGUAGE                  
         MVI   DECPT,C'.'                                                       
         MVI   COMMA,C','                                                       
         CLI   LANG,LANGEUS        ENGLISH LANGUAGE ?                           
         BNH   CUR04               YES, SKIP                                    
         TM    EXTPEDT2,EXTPDEC    SUPPRESS SWITCH ?                            
         BO    CUR04               NO,  SKIP                                    
         MVI   DECPT,C','          SWAP DEC POINT AND COMMA                     
         MVI   COMMA,C'.'                                                       
         CLI   CTRY,CTRYCANQ       CANADA ?                                     
         BNE   CUR04               NO,  SKIP                                    
         MVI   COMMA,C' '                                                       
         TM    EXTPEDT1,EXTPMINY   MINUS=YES ?                                  
         BZ    CUR04               NO,  SKIP                                    
*                                  INSTEAD OF MINUS=YES                         
         NI    EXTPEDT1,X'FF'-EXTPMINY                                          
         OI    EXTPEDT2,EXTPFLON   DO   FLOAT=-                                 
*                                                                               
CUR04    ICM   R1,15,EXTPIVAL      GET  INPUT VALUE OR ADDRESS IN R1            
         SR    R0,R0               CLEAR ALIGN=LEFT RETURNED LENGTH             
         B     CUR0                                                             
         EJECT ,                                                                
***********************************************************************         
* CONVERT INPUT TO PACKED VALUE IN DUB                                *         
***********************************************************************         
         SPACE 1                                                                
CUR0     TM    EXTPINPT,EXTPISET   BINARY VALUE PASSED IN PLIST                 
         BZ    CUR2                                                             
         CVD   R1,DUB                                                           
         B     CUR6                                                             
*                                                                               
CUR2     MVC   DUB(1),EXTPINPT     EXTRACT INPUT FIELD LENGTH                   
         NI    DUB,EXTPILEN                                                     
         ZIC   RE,DUB                                                           
*                                                                               
         TM    EXTPINPT,EXTPIBIN   BINARY INPUT VALUE                           
         BZ    CUR4                                                             
         XC    DUB(4),DUB                                                       
         TM    0(R1),X'80'         TEST IF VALUE IS NEGATIVE                    
         BZ    *+10                                                             
         MVC   DUB(4),=F'-1'                                                    
         LA    RF,4                                                             
         SR    RF,RE                                                            
         LA    RF,DUB(RF)                                                       
         BCTR  RE,0                                                             
         EXMVC RE,0(RF),0(R1)      MODE INPUT TO DUB                            
         L     R1,DUB                                                           
         CVD   R1,DUB                                                           
         B     CUR6                                                             
*                                                                               
CUR4     TM    EXTPINPT,EXTPIPAK   PACKED INPUT VALUE                           
         BO    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         BCTR  RE,0                                                             
         EX    RE,*+8                                                           
         B     *+10                                                             
         ZAP   DUB,0(0,R1)                                                      
         B     CUR6                                                             
*                                                                               
CUR6     TM    EXTPEDT1,EXTPDECN   TEST DECIMAL PLACES REQUIRED                 
         BZ    CUR8                                                             
         SR    R1,R1                                                            
         ICM   R1,1,EXTPCDEC       R1=N'DECIMAL PLACES TO SHIFT OFF             
         BZ    CUR8                                                             
         LNR   R1,R1               GET TW0'S COMPLEMENT                         
         TM    EXTPEDT2,EXTPDECR   TEST IF ROUNDING REQUIRED                    
         BZ    CUR7                                                             
         SRP   DUB,0(R1),5         SHIFT AND ROUND                              
         B     CUR8                                                             
*                                                                               
CUR7     SRP   DUB,0(R1),0         SHIFT WITHOUT ROUND                          
         B     CUR8                                                             
         EJECT ,                                                                
***********************************************************************         
* BUILD EDIT MASK                                                     *         
***********************************************************************         
         SPACE 1                                                                
CUR8     SR    R2,R2                                                            
         ICM   R2,7,EXTPOADD       R2=A(OUTPUT)                                 
         SR    R3,R3                                                            
         ICM   R3,1,EXTPOLEN       R3=L'OUTPUT                                  
         BZ    CUREXIT                                                          
         MVI   0(R2),C' '          CLEAR OUTPUT TO SPACES                       
         LR    RE,R3                                                            
         SH    RE,=H'2'                                                         
         BM    CUR10                                                            
         EXMVC RE,1(R2),0(R2)                                                   
*                                                                               
CUR10    TM    EXTPEDT1,EXTPZERB   DEAL WITH ZERO=BLANK                         
         BZ    CUR11                                                            
         CP    DUB,=P'0'                                                        
         BE    CUREXIT                                                          
*                                                                               
CUR11    XC    MASK,MASK           BUILD VIRGIN MASK                            
         LA    R4,MASK+L'MASK-1    POINT R4 TO END OF MASK                      
         LA    R5,L'MASK           R5=L'MASK                                    
*                                                                               
         TM    EXTPEDT1,EXTPMINY+EXTPCRDY                                       
         BZ    CUR14                                                            
         TM    EXTPEDT1,EXTPCRDY                                                
         BZ    CUR12                                                            
*                                                                               
         SH    R4,=H'2'            BACK-UP 2 BYTES                              
         SH    R5,=H'2'                                                         
         MVC   1(2,R4),=C'CR'                                                   
         B     CUR14                                                            
*                                                                               
CUR12    BCTR  R4,0                BACK-UP 1 BYTE                               
         BCTR  R5,0                                                             
         MVI   1(R4),C'-'                                                       
*                                                                               
CUR14    TM    EXTPEDT1,EXTPDECN                                                
         BO    CUR16                                                            
         SR    R1,R1                                                            
         ICM   R1,1,EXTPCDEC                                                    
         BZ    CUR16                                                            
         LA    R1,1(,R1)                                                        
         SR    R4,R1               BACK-UP N BYTES                              
         SR    R5,R1                                                            
         MVC   1(1,R4),DECPT       SET DECIMAL POINT CHR                        
*&&US                                                                           
         CLI   LANG,LANGEUS        IF   NOT ENGLISH LANGUAGE                    
         BNH   CUR16                                                            
         CLI   EXTPCDEC,1          AND  ONLY ONE DECIMAL PLACE                  
         BNE   CUR16                                                            
         MVI   1(R4),C'.'          THEN-THIS IS NOT MONEY, SO FORCE A .         
*&&                                                                             
*                                                                               
CUR16    BCTR  R4,0                BACK-UP 1 BYTE                               
         BCTR  R5,0                                                             
         OI    0(R4),X'01'         SET SIGNIFICANCE INDICATOR                   
         LA    R1,1(,R4)           PRESET R1 FOR EDMK                           
*                                                                               
         TM    EXTPEDT1,EXTPCOMY                                                
         BZ    CUR20                                                            
         AH    R4,=H'2'            GO FORWARD TWO BYTES                         
         AH    R5,=H'2'                                                         
*                                                                               
CUR18    SH    R5,=H'4'            INSERT COMMAS                                
         BNP   CUR20                                                            
         SH    R4,=H'4'                                                         
         MVC   0(1,R4),COMMA       SET COMMA CHR                                
         B     CUR18                                                            
*                                                                               
CUR20    LA    R0,L'DUB*2-1        R0=DIGIT COUNT                               
         LA    RF,MASK+L'MASK      RF=A(END OF MASK+1)                          
         LA    RE,MASK-1           RE=A(MASK-1)                                 
*                                                                               
CUR22    BCTR  RF,0                                                             
         TM    0(RF),X'F0'                                                      
         BNZ   CUR22                                                            
         OI    0(RF),X'20'                                                      
         BCT   R0,CUR22                                                         
         BCTR  RF,0                                                             
*                                                                               
CUR22A   CR    RF,RE                                                            
         BE    CUR22B                                                           
         MVI   0(RF),C' '                                                       
         BCT   RF,CUR22A                                                        
*                                                                               
CUR22B   MVC   NUMBER,DUB          SAVE PACKED NUMBER                           
         EDMK  MASK,DUB                                                         
*                                                                               
         LR    R4,R1               SAVE A(SIGNIFICANT DIGIT) IN R4              
         TM    EXTPEDT1,EXTPSYMN   CURRENCY SYMBOL REQUIRED ?                   
         BO    CUR25               NO,  SKIP                                    
         NC    EXTPCSYM,EXTPCSYM   ANY  CURRENCY TABLE ?                        
         BNZ   CUR23               YES, USE THE TABLE                           
         ZIC   RF,LANG             GET  CURRENT LANGUAGE                        
         MH    RF,=AL2(L'DEFCURTB) ->   RIGHT ENTRY                             
         LA    RF,DEFCURTB(RF)                                                  
         ZIC   RE,0(,RF)           GET  LENGTH OF CURRENCY SYMBOL               
         MVC   SAVPCSYM(2),1(RF)   SAVE CURRENCY SYMBOL                         
         TM    EXTPEDT2,EXTPSYMS   SHORT SYMBOL ?                               
         BZ    *+8                 NO,  SKIP                                    
         LA    RE,1                SET  LENGTH TO ONE                           
         SR    R1,RE               BACK-UP BY SYMBOL LENGTH                     
         BCTR  RE,0                                                             
*&&UK*&& B     CUR24               TREAT AS PREFIX                              
*&&US                                                                           
         CLI   LANG,LANGFRE        FRENCH ?                                     
         BNE   CUR24               NO,  TREAT AS PREFIX                         
         MVI   SAVPCIND,EXTPCSFX   TREAT AS SUFFIX                              
         B     CUR23D              CONTINUE                                     
*&&                                                                             
*                                                                               
CUR23    DS    0H                                                               
         MVC   SAVPCIND,EXTPCIND   SAVE EXTPCIND                                
         TM    EXTPEDT2,EXTPSYMS   SHORT SYMBOL ?                               
         BZ    CUR23B              NO,  SKIP                                    
         LA    RE,1                SET  LENGTH TO ONE                           
         B     CUR23C                                                           
*                                                                               
CUR23B   MVC   SAVPCSYM,EXTPCSYM   SAVE CURRENCY SYMBOL                         
         MVC   DUB+4(1),EXTPCIND                                                
         NI    DUB+4,EXTPCLEN      TURN OFF INDICATORS                          
         BZ    CUR25               EXIT IF ZERO LENGTH SYMBOL                   
         ZIC   RE,DUB+4                                                         
*                                                                               
CUR23C   SR    R1,RE               BACK-UP BY SYMBOL LENGTH                     
         BCTR  RE,0                                                             
*                                                                               
CUR23D   TM    SAVPCIND,EXTPCPFX   PREFIX ?                                     
         BO    CUR24               YES, CONTINUE                                
         LA    RF,MASK+L'MASK-1    SUFFIX                                       
         SR    RF,R4                                                            
         EXMVC RF,0(R1),0(R4)                                                   
         LA    R1,1(RF,R1)         ->   END                                     
*                                                                               
CUR24    EXMVC RE,0(R1),SAVPCSYM   SET  PREFIX/SUFFIX                           
         SR    R4,RE               BACK-UP BY SYMBOL LENGTH                     
         BCTR  R4,0                                                             
*                                                                               
CUR25    TM    EXTPEDT2,EXTPFLON   DEAL WITH FLOAT=-                            
         BZ    CUR26                                                            
         CP    NUMBER,=P'0'        TEST IF VALUE IS NEGATIVE                    
         BNL   CUR26                                                            
         BCTR  R4,0                                                             
         MVI   0(R4),C'-'          PREFIX NEGATIVE SIGN                         
*                                                                               
CUR26    TM    EXTPEDT1,EXTPBRAY   DEAL WITH BRACKET=YES                        
         BZ    CUR28                                                            
         LA    RF,MASK+L'MASK-1    MAKE ROOM FOR RIGHT BRACKET                  
         SR    RF,R4                                                            
         BCTR  R4,0                                                             
         EXMVC RF,0(R4),1(R4)                                                   
         BCTR  R4,0                                                             
         MVI   0(R4),C'('          MOVE THEM IN                                 
         LA    RF,MASK+L'MASK-2                                                 
*                                                                               
CUR27    CLI   0(RF),C' '                                                       
         BH    *+8                                                              
         BCT   RF,CUR27                                                         
         MVI   1(RF),C')'                                                       
*                                                                               
CUR28    LA    RF,MASK+L'MASK      HANDLE LEFT OR RIGHT-ALIGN                   
         SR    RF,R4               RF=SIGNIFICANT LENGTH                        
         ZIC   RE,EXTPOLEN         RE=OUTPUT LENGTH                             
         CR    RF,RE                                                            
         BNH   *+6                                                              
         LR    RF,RE               SIGNIFICANT LEN GTR THAN OUTPUT LEN          
         SR    R1,R1               SO USE OUTPUT LEN                            
         ICM   R1,7,EXTPOADD                                                    
         TM    EXTPEDT1,EXTPALNL                                                
         BO    CUR30                                                            
         AR    R1,RE               ADJUST O/P POINTER FOR RIGHT ALIGN           
         SR    R1,RF                                                            
         CR    RF,RE               IF SIGNIF LEN GTR THAN OUTPUT LEN            
         BL    CUR32               CHOP LEFT DIGITS IF RIGHT-ALIGN              
         LA    R4,MASK+L'MASK                                                   
         SR    R4,RF                                                            
         B     CUR32                                                            
*                                                                               
CUR30    LR    R0,RF               RETURN SIGNFCNT LENGTH IF LEFT-ALIGN         
         LA    RE,MASK+L'MASK-1                                                 
*                                                                               
CUR31    CLI   0(RE),C' '                                                       
         BH    CUR32                                                            
         BCTR  R0,0                                                             
         BCT   RE,CUR31                                                         
*                                                                               
CUR32    BCTR  RF,0                MOVE TO OUTPUT AREA                          
         EXMVC RF,0(R1),0(R4)                                                   
*                                                                               
CUREXIT  TM    EXTPEDT1,EXTPALNL   RETURN SIGNIFICANT LENGTH IN R0              
         BNO   CUREXIT2            IF ALIGN=LEFT                                
         XIT1  REGS=(R0)                                                        
*                                                                               
CUREXIT2 XIT1                                                                   
         EJECT ,                                                                
***********************************************************************         
* DEFAULT CURRENCY TABLE                                              *         
***********************************************************************         
         SPACE 1                                                                
DEFCURTB DS    0XL3                DEFAULT CURRENCY TABLE                       
         DC    AL1(1),CL2'$ '      0 - ENGLISH                                  
         DC    AL1(1),CL2'# '      1 - ENGLISH UK                               
         DC    AL1(1),CL2'$ '      2 - ENGLISH US                               
         DC    AL1(2),CL2'DM'      3 - DEUTSCH                                  
*&&UK*&& DC    AL1(2),CL2'FF'      4 - FRANCAIS                                 
*&&US*&& DC    AL1(1),CL2'$ '      4 - FRENCH CANADA                            
         DC    AL1(2),CL2'PT'      5 - ESPANOL                                  
         DC    AL1(2),CL2'L '      6 - ITALIANO                                 
         DC    AL1(1),CL2'G '      7 - DUTCH                                    
         EJECT ,                                                                
         SPACE 1                                                                
         LTORG                                                                  
         SPACE 2                                                                
WORKD    DSECT                                                                  
DUB      DS    D                                                                
NUMBER   DS    D                                                                
*                                                                               
DMCB     DS    6F                                                               
*                                                                               
CTRY     DS    X                   ACTUAL COUNTRY                               
LANG     DS    X                   LANGUAGE                                     
*                                                                               
DECPT    DS    C                                                                
COMMA    DS    C                                                                
*                                                                               
MASK     DS    CL30                                                             
WORK     DS    CL30                                                             
*                                                                               
SAVPCIND DS    CL(L'EXTPCIND)      SAVE PREFIX OR SUFFIX INDICATOR              
SAVPCSYM DS    CL(L'EXTPCSYM)      SAVE CURRENCY SYMBOL                         
*                                                                               
WORKX    EQU   *                                                                
         EJECT ,                                                                
         SPACE 1                                                                
* DDCOMFACS                                                                     
         PRINT OFF                                                              
       ++INCLUDE DDCOMFACS                                                      
         PRINT ON                                                               
* DDLANGEQUS                                                                    
         PRINT OFF                                                              
       ++INCLUDE DDLANGEQUS                                                     
         PRINT ON                                                               
* FAFACTS                                                                       
         PRINT OFF                                                              
       ++INCLUDE FAFACTS                                                        
         PRINT ON                                                               
* FACTRYEQUS                                                                    
         PRINT OFF                                                              
       ++INCLUDE FACTRYEQUS                                                     
         PRINT ON                                                               
         EJECT ,                                                                
         SPACE 1                                                                
* DDEXTEDITD                                                                    
       ++INCLUDE DDEXTEDITD                                                     
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'020DDEXTEDIT 05/10/99'                                      
         END                                                                    
