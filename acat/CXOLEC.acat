*          DATA SET CXOLEC     AT LEVEL 048 AS OF 02/03/95                      
*CATALP CXOLEC                                                                  
         TITLE 'CXOLEC - CONTROL EXTRACT - ACCOUNT SYSTEM OFFICE LIST'          
**********************************************************************          
* AL4 A(EXTRACT RECORD)                                              *          
* AL4 A(CONFILE RECORD)                                              *          
**********************************************************************          
         SPACE 1                                                                
         PRINT NOGEN                                                            
CXOLEC   CSECT                                                                  
         NMOD1 WORKX-WORKD,*CXOLEC*,RR=RA                                       
         USING WORKD,RC                                                         
         ST    R1,APARM                                                         
         ST    RA,RELO                                                          
         MVC   PARM,0(R1)          EXTRACT PARAMETER LIST                       
         LM    R2,R3,0(R1)                                                      
         USING CXOLED,R2           R2=A(EXTRACT RECORD)                         
         USING CTUREC,R3           R3=A(CONFILE RECORD)                         
*                                                                               
CXOLEN   XC    CXOLELEN,CXOLELEN   RECORD LENGTH/TYPE/ENDOFREC                  
         MVC   CXOLELEN(2),=AL2(CXOLEDL)                                        
         MVC   CXOLETYP,CXOLEDQ                                                 
         MVI   CXOLEDX,MXTRTQ                                                   
         MVI   CXOLEDX+1,MXTRTQ                                                 
*                                                                               
CXOCMP   MVI   CXOLEAGY-1,MXTRTQ   AGENCY ALPHA ID                              
         MVC   CXOLEAGY,CTUKAGY                                                 
*                                                                               
CXOCOD   MVI   CXOLECOD-1,MXTRTQ   OFFICE LIST CODE                             
         CLI   CTUKPROG,X'00'                                                   
         BE    *+14                                                             
         MVC   CXOLECOD,CTUKPROG+1                                              
         B     *+10                                                             
         MVC   CXOLECOD,CTUKPROG+2                                              
*                                                                               
CXOSUB   MVI   CXOLESUB-1,MXTRTQ   OFFICE LIST SUB CODE                         
         CLI   CTUKPROG,X'00'                                                   
         BE    *+14                                                             
         MVC   CXOLESUB,CTUKPROG+2                                              
         B     *+8                                                              
         MVI   CXOLESUB,C' '                                                    
*                                                                               
         MVI   CXOLEOFF-1,MXTRTQ   SET FIELD SEPS. FOR OTHER KEY FIELDS         
*                                                                               
         CLI   SAVEACTN,C'C'       TEST SAVED ACTION CODE                       
         BNE   *+12                                                             
         MVI   CXOLEACT,C'A'       IF LAST TIME CHANGE GOTO ADD PROCESS         
         B     CXOTAB                                                           
         CLI   CXOLEACT,C'C'       IF CHANGE DO 'K' DELETE ALL ENTRIES          
         BE    CXOKOF                                                           
         CLI   CXOLEACT,C'D'       IF DELETE DO 'K' DELETE ALL ENTRIES          
         BE    CXOKOF                                                           
         B     CXOTAB              OTHER ACTIONS GOTO ADD PROCESS               
*                                                                               
CXOKOF   MVC   SAVEACTN,CXOLEACT   SAVE THIS ACTION                             
         MVI   CXOLEACT,C'K'       OVERIDE WITH 'K' DELETE ALL ENTRIES          
         CLI   SAVEACTN,C'C'                                                    
         BE    RETEXIT             IF CHANGE RETURN FOR ADDS                    
         B     CXOEOT                                                           
*                                                                               
         USING OFFTABD,R5                                                       
CXOTAB   ICM   R5,15,AOFFTAB                                                    
         BZ    *+12                                                             
         LA    R5,OFFTLEN(R5)                                                   
         B     CXOOFF                                                           
         L     R5,=A(OFFTABC)                                                   
         BAS   RE,INITOFFT                                                      
*                                                                               
CXOOFF   CLI   OFFTOFF,0           TEST IF LAST OFFICE PROCESSED                
         BE    CXOEOT                                                           
         C     R5,AOFFTEND                                                      
         BH    CXOEOT                                                           
         ST    R5,AOFFTAB                                                       
         MVC   CXOLEOFF,OFFTOFF                                                 
         B     RETEXIT                                                          
*                                                                               
RETEXIT  L     R1,APARM            EXIT WITH RETURN REQUIRED                    
         OI    8(R1),X'40'         SET RETURN FLAG                              
         B     EXIT                                                             
*                                                                               
CXOEOT   XC    AOFFTAB,AOFFTAB                                                  
         MVI   SAVEACTN,C' '       CLEAR SAVED ACTION CODE                      
         L     R1,APARM            END OF TABLE                                 
         MVI   8(R1),X'88'                                                      
         B     EXIT                                                             
*                                                                               
DELREC   XC    AOFFTAB,AOFFTAB                                                  
         L     R1,APARM            DELETE THIS RECORD                           
         MVI   8(R1),X'80'                                                      
*                                                                               
EXIT     XMOD1 1                                                                
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO LOAD OFFICE TABLE FROM PROFILE RECORD                    *         
* R5=A(OFFICE TABLE)                                                  *         
***********************************************************************         
         SPACE 1                                                                
         USING OFFTABD,R5                                                       
INITOFFT NTR1  ,                                                                
         LR    RE,R5                                                            
         LA    RF,OFFTABL                                                       
         XCEF                                                                   
         LA    R4,CTUDATA          R4=A(FIRST ELEMENT)                          
*                                                                               
         USING CTPVD,R4                                                         
IOFF010  CLI   CTPVEL,0            GET ELEMENT FROM RECORD                      
         BE    IOFF100                                                          
         CLI   CTPVEL,CTPVELQ                                                   
         BE    IOFF030                                                          
IOFF020  SR    RF,RF               BUMP TO NEXT ELEMENT                         
         IC    RF,1(R4)                                                         
         CH    RF,=H'1'                                                         
         BNH   EXIT                EXIT IF FUNNY ELEMENT LENGTH                 
         AR    R4,RF                                                            
         B     IOFF010                                                          
*                                                                               
IOFF030  LA    RF,CTPVALUE                                                      
         LA    R1,16                                                            
*                                                                               
IOFF032  CLI   0(RF),0                                                          
         BE    IOFF100                                                          
         CLI   0(RF),C'0'                                                       
         BE    IOFF040                                                          
         MVC   OFFTOFF,0(RF)                                                    
         LA    R5,OFFTLEN(R5)                                                   
*                                                                               
IOFF040  LA    RF,1(RF)                                                         
         BCT   R1,IOFF032                                                       
*                                                                               
IOFF100  ST    R5,AOFFTAB          SAVE A(ENTRY)                                
         ST    R5,AOFFTEND         SAVE A(END OF TABLE)                         
*                                                                               
IOFFX    EQU   *                                                                
         XIT1                                                                   
         EJECT                                                                  
         DS    0D                                                               
SAVEACTN DC    CL1' '                                                           
SPACES   DC    80C' '                                                           
         DS    0D                                                               
         LTORG                                                                  
         SPACE 1                                                                
       ++INCLUDE MXTRT                                                          
       ++INCLUDE CXRECID                                                        
         SPACE 1                                                                
         DS    0D                                                               
VHEXOUT  DC    V(HEXOUT)                                                        
VCRYPT   DC    V(CRYPT)                                                         
         SPACE 1                                                                
         DS    0D                                                               
AOFFTAB  DC    A(0)                OFFICE TABLE POINTER                         
AOFFTEND DC    A(0)                OFFICE TABLE END OF TABLE POINTER            
         DS    0D                  OFFICE TABLE                                 
OFFTABC  DC    (OFFTABL)X'00'                                                   
OFFTABL  EQU   (OFFTMAX*OFFTLEN)                                                
OFFTMAX  EQU   17                                                               
         SPACE 1                                                                
         DS    0D                                                               
         SPACE 2                                                                
OFFTABD  DSECT                     DSECT FOT OFFICE TABLE                       
OFFTOFF  DS    XL1                 OFFICE CODE                                  
OFFTLEN  EQU   *-OFFTABD                                                        
         SPACE 2                                                                
         EJECT                                                                  
* INCLUDE CXTRACT LOCAL DSECTS                                                  
         SPACE 1                                                                
       ++INCLUDE CXTRACTD                                                       
         EJECT                                                                  
WORKD    DSECT                                                                  
PARM     DS    0XL16               PARAMETER LIST                               
AXREC    DS    A                   A(EXTRACT RECORD)                            
CFREC    DS    A                   A(CONFILE RECORD)                            
         DS    A                                                                
         DS    A                                                                
*                                                                               
DMCB     DS    6F                                                               
DUB      DS    D                                                                
APARM    DS    A                   A(PARAMETER LIST)                            
RELO     DS    A                                                                
*                                                                               
OFFNEXT  DS    A                   NEXT ENTRY IN OFFICE TABLE                   
OFFEND   DS    A                   END OF OFFICE TABLE ENTRIES                  
*                                                                               
WORKX    EQU   *                                                                
         EJECT                                                                  
       ++INCLUDE CXOLED                                                         
         EJECT                                                                  
       ++INCLUDE CTGENFILE                                                      
         SPACE 1                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'048CXOLEC    02/03/95'                                      
         END                                                                    
