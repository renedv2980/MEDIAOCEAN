*          DATA SET RELDRCVD   AT LEVEL 001 AS OF 03/28/12                      
*CATALP RELDRCVD                                                                
         TITLE 'LDRCVD - REP - RECOVERY DUMP ROUTINE'                           
                                                                                
* PARAMS VIA R1                                                                 
* XL1    X'00'=INIT,X'01'=PROCESS,X'FF'=FINAL.                                  
*        RETURN X'FF'=DONT WRITE RECORD TO DUMP TAPE                            
*        RETURN X'FE'=DELETED REC,X'FD'=RESTORED REC                            
* AL3    A(RECOVERY FILE RECORD)                                                
                                                                                
         PRINT NOGEN                                                            
LDRCVD   CSECT                                                                  
         ENTRY LDRCVDT             TABLE OF COUNTERS AND ACTIONS                
                                                                                
         NMOD1 WORKX-WORKD,ACLDRCVD                                             
         USING WORKD,RC                                                         
         LR    RA,R1               RA=A(PARAMETER LIST)                         
         ST    R1,APARM                                                         
         SR    R3,R3                                                            
         ICM   R3,7,1(R1)          R3=A(RECOVERY HEADER)                        
         USING RCVRECD,R3                                                       
         CLI   0(R1),X'01'         TEST ACTION                                  
         BL    INIT                                                             
         BE    PROCESS                                                          
         BH    FINAL                                                            
                                                                                
INIT     LA    RE,LDRCVDT          CLEAR COUNTERS ON INIT CALL                  
INIT1    CLC   0(4,RE),=4X'FF'                                                  
         BE    EXIT                                                             
         XC    0(4,RE),0(RE)                                                    
         LA    RE,L'LDRCVDT(RE)                                                 
         B     INIT1                                                            
                                                                                
FINAL    B     EXIT                FINAL CALL                                   
                                                                                
LGCLDEL  MVI   0(RA),X'FE'         RETURN RECORD IS LOGICALLY DELETED           
         B     EXIT                                                             
                                                                                
LGCLRST  MVI   0(RA),X'FD'         RETURN RECORD IS LOGICALLY RESTORED          
         B     EXIT                                                             
                                                                                
DELETE   MVI   0(RA),X'FF'         RETURN DELETE RECORD FROM DUMP TAPE          
                                                                                
EXIT     XMOD1 1                                                                
                                                                                
         EJECT                                                                  
* PROCESS A RECOVERY FILE RECORD                                                
* UPDATE TABLE OF ANY ACTIONS THAT NEED PRINTING AND ERRORS/WARNINGS            
* EXIT VIA DELETE TO REQUEST RECORD NOT WRITTEN TO RECOVERY DUMP TAPE           
                                                                                
PROCESS  CLI   RFILTY,REPFILQ      REPFIL RECOVERY RECORD                       
         BE    FIL1                                                             
         CLI   RFILTY,REPDIRQ      REPDIR RECOVERY RECORD                       
         BE    DIR1                                                             
         CLI   RFILTY,ROIFILQ      ROIFIL RECOVERY RECORD                       
         BE    ROF1                                                             
         CLI   RFILTY,ROIDIRQ      ROIDIR RECOVERY RECORD                       
         BE    ROD1                                                             
         B     EXIT                                                             
                                                                                
DIR1     CLI   RRECTY,2            TEST REPDIR CHANGE                           
         BNE   SAVE                                                             
         TM    KEYSTAT,X'80'       TEST DELETED                                 
         BZ    DIR2                                                             
         L     RF,=A(LASTRCV)                                                   
         CLI   RFILTY-RCVHDR(RF),REPDIRQ                                        
         BNE   SAVE                                                             
         CLI   RRECTY-RCVHDR(RF),1 TEST LAST RECORD REPDIR COPY                 
         BNE   SAVE                                                             
         TM    KEYSTAT-RCVHDR(RF),X'80'                                         
         BO    SAVE                                                             
         CLC   KEYKEY,KEYKEY-RCVHDR(RF)                                         
         BNE   SAVE                                                             
         L     R0,RCVDIRD          BUMP REPDIR DELETED RECORDS                  
         AHI   R0,1                                                             
         ST    R0,RCVDIRD                                                       
         MVI   0(RA),X'FE'         RETURN RECORD IS LOGICALLY DELETED           
         B     SAVE                                                             
                                                                                
DIR2     L     RF,=A(LASTRCV)      TEST IF COPY RECORD WAS ACTIVE               
         CLI   RFILTY-RCVHDR(RF),REPDIRQ                                        
         BNE   SAVE                                                             
         CLI   RRECTY-RCVHDR(RF),1                                              
         BNE   SAVE                                                             
         TM    KEYSTAT-RCVHDR(RF),X'80'                                         
         BZ    SAVE                                                             
         CLC   KEYKEY,KEYKEY-RCVHDR(RF)                                         
         BNE   SAVE                                                             
         L     R0,RCVDIRR          BUMP REPDIR RESTORED RECORDS                 
         AHI   R0,1                                                             
         ST    R0,RCVDIRR                                                       
         MVI   0(RA),X'FD'         RETURN RECORD IS LOGICALLY RESTORED          
         B     SAVE                                                             
                                                                                
FIL1     CLI   RRECTY,2            TEST REPFIL CHANGE                           
         BNE   SAVE                                                             
         TM    RECSTAT,X'80'       TEST DELETED                                 
         BZ    FIL2                                                             
         L     RF,=A(LASTRCV)                                                   
         CLI   RFILTY-RCVHDR(RF),REPFILQ                                        
         BNE   SAVE                                                             
         CLI   RRECTY-RCVHDR(RF),1 TEST LAST RECORD REPFIL COPY                 
         BNE   SAVE                                                             
         TM    RECSTAT-RCVHDR(RF),X'80'                                         
         BO    SAVE                                                             
         CLC   RECKEY,RECKEY-RCVHDR(RF)                                         
         BNE   SAVE                                                             
         L     R0,RCVFILD          BUMP REPFIL DELETED RECORDS                  
         AHI   R0,1                                                             
         ST    R0,RCVFILD                                                       
         MVI   0(RA),X'FE'         RETURN RECORD IS LOGICALLY DELETED           
         B     SAVE                                                             
                                                                                
FIL2     L     RF,=A(LASTRCV)      TEST IF COPY RECORD WAS ACTIVE               
         CLI   RFILTY-RCVHDR(RF),REPFILQ                                        
         BNE   SAVE                                                             
         CLI   RRECTY-RCVHDR(RF),1                                              
         BNE   SAVE                                                             
         TM    RECSTAT-RCVHDR(RF),X'80'                                         
         BZ    SAVE                                                             
         CLC   RECKEY,RECKEY-RCVHDR(RF)                                         
         BNE   SAVE                                                             
         L     R0,RCVFILR          BUMP REPFIL RESTORED RECORDS                 
         AHI   R0,1                                                             
         ST    R0,RCVFILR                                                       
         MVI   0(RA),X'FD'         RETURN RECORD IS LOGICALLY RESTORED          
         B     SAVE                                                             
                                                                                
ROD1     CLI   RRECTY,2            TEST ROIDIR CHANGE                           
         BNE   SAVE                                                             
         TM    RODSTAT,X'80'       TEST DELETED                                 
         BZ    DIR2                                                             
         L     RF,=A(LASTRCV)                                                   
         CLI   RFILTY-RCVHDR(RF),ROIDIRQ                                        
         BNE   SAVE                                                             
         CLI   RRECTY-RCVHDR(RF),1 TEST LAST RECORD ROIDIR COPY                 
         BNE   SAVE                                                             
         TM    RODSTAT-RCVHDR(RF),X'80'                                         
         BO    SAVE                                                             
         CLC   RODKEY,RODKEY-RCVHDR(RF)                                         
         BNE   SAVE                                                             
         L     R0,RCVRODD          BUMP ROIDIR DELETED RECORDS                  
         AHI   R0,1                                                             
         ST    R0,RCVRODD                                                       
         MVI   0(RA),X'FE'         RETURN RECORD IS LOGICALLY DELETED           
         B     SAVE                                                             
                                                                                
ROD2     L     RF,=A(LASTRCV)      TEST IF COPY RECORD WAS ACTIVE               
         CLI   RFILTY-RCVHDR(RF),ROIDIRQ                                        
         BNE   SAVE                                                             
         CLI   RRECTY-RCVHDR(RF),1                                              
         BNE   SAVE                                                             
         TM    KEYSTAT-RCVHDR(RF),X'80'                                         
         BZ    SAVE                                                             
         CLC   RODKEY,RODKEY-RCVHDR(RF)                                         
         BNE   SAVE                                                             
         L     R0,RCVRODR          BUMP ROIDIR RESTORED RECORDS                 
         AHI   R0,1                                                             
         ST    R0,RCVRODR                                                       
         MVI   0(RA),X'FD'         RETURN RECORD IS LOGICALLY RESTORED          
         B     SAVE                                                             
                                                                                
ROF1     CLI   RRECTY,2            TEST ROIFIL CHANGE                           
         BNE   SAVE                                                             
         TM    ROFSTAT,X'80'       TEST DELETED                                 
         BZ    FIL2                                                             
         L     RF,=A(LASTRCV)                                                   
         CLI   RFILTY-RCVHDR(RF),ROIFILQ                                        
         BNE   SAVE                                                             
         CLI   RRECTY-RCVHDR(RF),1 TEST LAST RECORD ROIFIL COPY                 
         BNE   SAVE                                                             
         TM    ROFSTAT-RCVHDR(RF),X'80'                                         
         BO    SAVE                                                             
         CLC   ROFKEY,ROFKEY-RCVHDR(RF)                                         
         BNE   SAVE                                                             
         L     R0,RCVROFD          BUMP ROIFIL DELETED RECORDS                  
         AHI   R0,1                                                             
         ST    R0,RCVROFD                                                       
         MVI   0(RA),X'FE'         RETURN RECORD IS LOGICALLY DELETED           
         B     SAVE                                                             
                                                                                
ROF2     L     RF,=A(LASTRCV)      TEST IF COPY RECORD WAS ACTIVE               
         CLI   RFILTY-RCVHDR(RF),ROIFILQ                                        
         BNE   SAVE                                                             
         CLI   RRECTY-RCVHDR(RF),1                                              
         BNE   SAVE                                                             
         TM    ROFSTAT-RCVHDR(RF),X'80'                                         
         BZ    SAVE                                                             
         CLC   ROFKEY,ROFKEY-RCVHDR(RF)                                         
         BNE   SAVE                                                             
         L     R0,RCVROFR          BUMP ROIFIL RESTORED RECORDS                 
         AHI   R0,1                                                             
         ST    R0,RCVROFR                                                       
         MVI   0(RA),X'FD'         RETURN RECORD IS LOGICALLY RESTORED          
         B     SAVE                                                             
                                                                                
SAVE     L     R0,=A(LASTRCV)      SAVE RECOVERY RECORD                         
         LHI   R1,LASTRCVL         R1=MAXIMUM RECORD LENGTH                     
         LR    RE,R3                                                            
         AHI   RE,-4                                                            
         LH    RE,0(RE)                                                         
         AHI   RE,-4               RE=ACTUAL RECORD LENGTH                      
         BM    SAVE1                                                            
         CR    RE,R1                                                            
         BNL   SAVE1                                                            
         LR    R1,RE                                                            
SAVE1    LR    RE,R3                                                            
         LR    RF,R1                                                            
         MVCL  R0,RE                                                            
         B     EXIT                                                             
                                                                                
         LTORG                                                                  
                                                                                
* THIS ENTRY POINT TABLE IS PROCESSED BY RCVDMP CALLER AFTER FINAL CALL         
* EACH ENTRY HAS A COUNTER,TEXT,TYPE,AND SPARE                                  
* ENTRIES WITH A NON-ZERO COUNTER WILL BE PRINTED                               
* ERRORS/WARNINGS WILL GO TO CONSOLE AND WILL BE EMAILED                        
         DS    0F                                                               
LDRCVDT  DS    0CL24                                                            
RCVDIRD  DC    F'0',CL16'REPDIR DELETES  ',C'I ',AL2(0)                         
RCVDIRR  DC    F'0',CL16'REPDIR RESTORES ',C'I ',AL2(0)                         
RCVFILD  DC    F'0',CL16'REPFIL DELETES  ',C'I ',AL2(0)                         
RCVFILR  DC    F'0',CL16'REPFIL RESTORES ',C'I ',AL2(0)                         
RCVRODD  DC    F'0',CL16'ROIDIR DELETES  ',C'I ',AL2(0)                         
RCVRODR  DC    F'0',CL16'ROIDIR RESTORES ',C'I ',AL2(0)                         
RCVROFD  DC    F'0',CL16'ROIFIL DELETES  ',C'I ',AL2(0)                         
RCVROFR  DC    F'0',CL16'ROIFIL RESTORES ',C'I ',AL2(0)                         
RCVERR   DC    F'0',CL16'REP ERRORS      ',C'E ',AL2(0)                         
RCVWARN  DC    F'0',CL16'REP WARNINGS    ',C'W ',AL2(0)                         
LDRCVDTX DC    4X'FF'                                                           
                                                                                
LASTRCV  DC    2048X'00'                                                        
LASTRCVL EQU   *-LASTRCV                                                        
                                                                                
REPDIRQ  EQU   X'81'                                                            
REPFILQ  EQU   X'82'                                                            
ROIDIRQ  EQU   X'87'                                                            
ROIFILQ  EQU   X'88'                                                            
                                                                                
* DSECT TO LOCAL WORKING STORAGE                                                
*                                                                               
WORKD    DSECT                                                                  
APARM    DS    A                                                                
WORKX    EQU   *                                                                
                                                                                
* DSECT TO COVER RECOVERY HEADER                                                
*                                                                               
RCVRECD  DSECT                                                                  
RCVHDR   DS    0XL24                                                            
*DMRCVRHDR                         RECOVERY HEADER                              
       ++INCLUDE DMRCVRHDR                                                      
                                                                                
RECREC   DS    0X                  RECORD                                       
RECKEY   DS    XL27                RECORD KEY                                   
RECLEN   DS    XL2                 RECORD LENGTH                                
RECSTAT  DS    XL1                 RECORD STATUS                                
RECDATA  DS    0X                  RECORD DATA                                  
                                                                                
         ORG   RECREC                                                           
KEYREC   DS    0XL32               KEY RECORD                                   
KEYKEY   DS    XL27                KEY KEY                                      
KEYSTAT  DS    XL1                 KEY STATUS                                   
KEYADDR  DS    XL4                 KEY DISK ADDRESS                             
                                                                                
         ORG   RECREC                                                           
ROFREC   DS    0X                  ROI RECORD                                   
ROFKEY   DS    XL31                ROI RECORD KEY                               
ROFLEN   DS    XL2                 ROI RECORD LENGTH                            
ROFSTAT  DS    XL1                 ROI RECORD STATUS                            
ROFDATA  DS    0X                  ROI RECORD DATA                              
                                                                                
         ORG   RECREC                                                           
RODREC   DS    0XL36               ROI KEY RECORD                               
RODKEY   DS    XL31                ROI KEY KEY                                  
RODSTAT  DS    XL1                 ROI KEY STATUS                               
RODADDR  DS    XL4                 ROI KEY DISK ADDRESS                         
                                                                                
*DMRCVREXT                         RECOVERY TRAILER                             
       ++INCLUDE DMRCVREXT                                                      
                                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'001RELDRCVD  03/28/12'                                      
         END                                                                    
