*          DATA SET SPA8WW     AT LEVEL 060 AS OF 07/24/06                      
*CATALP SPA8WW                                                                  
         TITLE 'WW BILLING INTERFACE'                                           
SPA8WW   CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 0,SPA8WW                                                         
*                                                                               
         L     RC,0(R1)            ADDRESS OF SPREPWORKD                        
         LA    R9,2048(RC)                                                      
         LA    R9,2048(R9)                                                      
         USING SPWORKD,RC,R9                                                    
         MVC   USERRD,4(RD)        SAVE LINK BACK TO USER                       
*                                                                               
         L     R8,4(R1)            ADDRESS OF SPWWA8BLK                         
         USING SPWWD,R8                                                         
*                                                                               
         LA    RA,2048(RB)                                                      
         LA    RA,2048(RA)                                                      
         USING SPA8WW+4096,RA                                                   
*                                                                               
         B     WW01                                                             
         SPACE 2                                                                
EXIT     XIT1                                                                   
         EJECT                                                                  
*                                                                               
WW01     XC    KEY,KEY                                                          
         LA    R1,KEY                                                           
         USING INPKEY,R1                                                        
*                                                                               
         MVI   INPKTYPE,INPKTYPQ   SET UP PASSIVE PTR                           
         MVI   INPKSTYP,INPKSTPQ                                                
         MVC   INPKAM,SPWWAGMD                                                  
         MVC   INPKCLT,SPWWCLT                                                  
         MVC   INPKPRD,SPWWPRD                                                  
         MVC   INPKCMP,SPWWEST                                                  
*                                                                               
         GOTO1 HIGH                                                             
         DROP  R1                                                               
         EJECT                                                                  
*                                                                               
WW05     MVI   LASTSW,C'N'         INIT FOR LAST PHYSICAL REC                   
         MVC   PASSIVE,KEY         SAVE PASSIVE KEY                             
*                                                                               
         CLI   SPWWPRD,0           WAS PRODUCT REQUESTED                        
         BNE   WW05D                                                            
         CLC   KEY(5),KEYSAVE      ONLY CHECK AM/CLT                            
         BE    WW08                                                             
         B     WWX                                                              
*                                                                               
WW05D    CLI   SPWWEST,0           WAS EST REQUESTED = 0                        
         BNE   WW07                                                             
*                                                                               
WW06     CLC   KEY(6),KEYSAVE      SAME AM/CLT/PRD                              
         BNE   WWX                 LAST MKT & EST                               
         B     WW08                                                             
*                                                                               
WW07     CLC   KEY(7),KEYSAVE      COMPLETED REQUEST                            
         BNE   WWX                 LAST MKT & EST                               
*                                                                               
         USING INVTABD,R3                                                       
WW08     LA    R3,INVTAB                                                        
         XCEF  INVTAB,6144         CLEAR TABLE 12 X 16 X L'INVTAB               
         MVI   0(R3),X'FF'         END OF TABLE MARKER                          
         MVC   SPWWPRDO,KEY+5      PRODUCT                                      
         MVC   SPWWESTO,KEY+6      EST                                          
*                                                                               
         L     R6,SPWWAIO                                                       
         ST    R6,AREC                                                          
*                                                                               
         GOTO1 GET                                                              
*                                                                               
         MVC   MASTKEY,0(R6)       SAVE MASTER KEY                              
*                                                                               
* GET MARKET CODE AND TAX RATE FROM STATION REC                                 
*                                                                               
         XC    KEY,KEY                                                          
         MVC   SVCLT,PASSIVE+3                                                  
         GOTO1 CLUNPK,DMCB,SVCLT,ZCLT                                           
*                                                                               
         XC    MYMKT(5),MYMKT      CLEAR MKT/STA                                
         MVC   MYSTA,PASSIVE+7     PACKED STA                                   
         MVC   SPWWSTA,MYSTA                                                    
*                                                                               
         GOTO1 MSUNPK,DMCB,MYMKSTA,ZMKT,ZSTA                                    
*                                                                               
         CLI   ZSTA+4,C' '         IF BLANK INSERT T                            
         BNE   WW09                                                             
         MVI   ZSTA+4,C'T'                                                      
*                                                                               
WW09     MVI   KEY,C'0'            REC IS INIT WITH C'0'                        
         MVC   KEY+1(16),KEY                                                    
         MVI   KEY,C'S'            STATION                                      
         MVC   KEY+1(1),QMED       MEDIA                                        
         MVC   KEY+2(5),ZSTA       STATION                                      
         MVC   KEY+7(2),QAGY       ALPHA AGY                                    
         MVC   KEY+9(3),ZCLT       CLIENT                                       
*                                                                               
         GOTO1 READSTA             READ STATION REC                             
*                                                                               
         L     R1,ADSTAT           ADDRESS OF STATION REC                       
         USING STAREC,R1                                                        
*                                                                               
         PACK  DUB,SMKT                                                         
         CVB   R0,DUB                                                           
         STCM  R0,3,SPWWMKT        OUTPUT                                       
*                                                                               
         DROP  R1                                                               
         EJECT                                                                  
*                                                                               
* GET INVOICE RECORD                                                            
*                                                                               
         XC    KEY,KEY                                                          
         MVC   KEY(7),MASTKEY      READ FROM PASSIVE PTR                        
         GOTO1 HIGH                                                             
         B     WW15                                                             
*                                                                               
WW10     GOTO1 SEQ                                                              
*                                                                               
WW15     CLC   KEY(7),KEYSAVE      MUST BE THERE                                
         BE    *+6                 BECAUSE PASSIVE POINTER WAS READ             
         DC    H'0'                                                             
*                                                                               
         CLC   KEY(13),MASTKEY     IS THIS THE LAST REC                         
         BNE   WW20                                                             
         MVI   LASTSW,C'Y'                                                      
*                                                                               
WW20     GOTO1 GET                                                              
         L     R6,SPWWAIO          1ST ELEMENT                                  
         LA    R6,24(R6)                                                        
*                                                                               
WW25     CLI   0(R6),0             END OF PHYSICAL RECORD                       
         BE    WW100                                                            
*                                                                               
         USING IHELEM,R6                                                        
         CLI   0(R6),IHCODEQ       INVOICE HEADER                               
         BNE   WW70                                                             
         EJECT                                                                  
*                                                                               
* REACHED AN INVOICE HEADER ELEMENT                                             
*                                                                               
         OC    IHBUYER,IHBUYER                                                  
         BZ    WW70                NOT PAID, GET NEXT                           
*                                                                               
* THIS INVOICE IS PAID - GET DATES                                              
*                                                                               
WW30     MVC   TINVSDT(1),IHYEAR                                                
         MVC   TINVSDT+1(1),IHMONTH                                             
*                                                                               
         MVC   TINVPDT,IHAPPR      DATE OF PAYMENT                              
*                                                                               
         MVC   TINVID,IHID         MOVE ID TO TABLE                             
*                                                                               
WW55     ICM   R1,15,IHAMNT                                                     
*                                                                               
         L     RF,TINVGRS          ACTUAL GROSS                                 
         AR    RF,R1                                                            
         ST    RF,TINVGRS                                                       
*                                                                               
         M     R0,=F'170'          TAKE 85% OF GROSS = NET                      
         D     R0,=F'100'                                                       
         A     R1,=F'1'            ROUND                                        
         SRA   R1,1                                                             
*                                                                               
         L     RF,TINVNET                                                       
         AR    RF,R1                                                            
         ST    RF,TINVNET                                                       
*                                                                               
         XR    R0,R0                                                            
         MH    R1,IHTAX                                                         
         D     R0,=F'100000'                                                    
*                                                                               
         L     RF,TINVTAX                                                       
         AR    RF,R1               ACTUAL TAX PAID                              
         ST    RF,TINVTAX                                                       
*                                                                               
         LA    R3,L'INVTAB(R3)     BUMP TABLE                                   
         MVI   0(R3),X'FF'         MARK END OF TABLE                            
*                                                                               
WW70     SR    R0,R0               BUMP TO NEXT ELEM                            
         ICM   R0,1,1(R6)                                                       
         BNZ   *+6                                                              
         DC    H'0'                                                             
*                                                                               
         AR    R6,R0                                                            
         B     WW25                                                             
         DROP  R6                                                               
         EJECT                                                                  
*                                                                               
* END OF PHYSICAL RECORD                                                        
*                                                                               
WW100    CLI   LASTSW,C'N'         LAST PHYSICAL REC                            
         BE    WW10                NO, GET THE NEXT REC                         
*                                                                               
* FINISHED LOGICAL RECORD - 1 MARKET - SEND INFO                                
*                                                                               
         LA    R3,INVTAB                                                        
*                                                                               
WW120    CLI   0(R3),X'FF'                                                      
         BE    WW150                                                            
*                                                                               
         MVC   SPWWCMT(12),TINVID  INVOICE ID                                   
         MVC   SPWWSDT,TINVSDT     SERVICE DATE YM                              
         MVC   SPWWPDT,TINVPDT     PAID DATE YMD                                
         MVC   SPWWGRS,TINVGRS     TOTAL GROSS OF INVOICE                       
         MVC   SPWWNET,TINVNET     TOTAL NET   OF INVOICE                       
         MVC   SPWWTAX,TINVTAX     TOTAL TAX   OF INVOICE                       
*                                                                               
         BAS   RE,GO1                                                           
*                                                                               
         XC    SPWWCMT(32),SPWWCMT CLEAR AREA                                   
         LA    R3,L'INVTAB(R3)     BUMP TABLE                                   
         B     WW120                                                            
*                                                                               
* NEXT MARKET                                                                   
*                                                                               
WW150    MVC   KEY,PASSIVE         RESTORE PASSIVE KEY                          
         GOTO1 HIGH                REREAD LAST RECORD                           
         CLC   KEY(13),KEYSAVE                                                  
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         GOTO1 SEQ                 AND GET NEXT REC                             
         B     WW05                                                             
*                                                                               
WWX      B    EXIT                                                              
         EJECT                                                                  
*                                                                               
GO1      NTR1                                                                   
         ICM   RF,15,SPWWHOOK                                                   
         BZ    GOX                                                              
*                                                                               
         L     RE,USERRD                                                        
         LM    R0,RC,20(RE)                                                     
         BASR  RE,RF                                                            
*                                                                               
GOX      XIT1                                                                   
         EJECT                                                                  
         SPACE 2                                                                
         DS    0D                                                               
         DC    CL8'*STORAGE'                                                    
LASTSW   DS    CL1                 LAST PHYSICAL RECORD SWITCH                  
VALEST   DS    CL1                 VALID EST FOR REQUEST                        
NOEST    DS    CL1                 USE EST OR NOT                               
USERRD   DS    A                                                                
*                                                                               
MYMKSTA  DS    0XL5                                                             
MYMKT    DS    XL2                 B MARKET                                     
MYSTA    DS    XL3                 PACKED STATION                               
*                                                                               
ZMKT     DS    CL4                 EBCDIC MARKET                                
ZSTA     DS    CL5                 EBCDIC STATION                               
ZCLT     DS    CL3                 EBCDIC CLIENT                                
ZPRD     DS    CL3                 EBCDIC PRODUCT                               
*                                                                               
SVTAX    DS    CL2                 TAX RATE                                     
*                                                                               
PASSIVE  DS    XL18                                                             
MASTKEY  DS    XL13                                                             
         DS    0D                                                               
         DC    CL8'*TABLE**'                                                    
INVTAB   DS    194CL32             TABLE OF INVOICES                            
INVTABX  DS    CL1                 END OF TABLE MARKER                          
*                                                                               
         LTORG                                                                  
         EJECT                                                                  
         PRINT OFF                                                              
       ++INCLUDE SPREPWORKD                                                     
         PRINT ON                                                               
       ++INCLUDE SPA8WWBLK                                                      
         EJECT                                                                  
INVTABD  DSECT                                                                  
TINVID   DS    CL12                                                             
TINVSDT  DS    XL2                 SERVICE DATE YM                              
TINVPDT  DS    XL3                 PAYMENT DATE YMD                             
         DS    XL3                 3 BYTES FOR FULL WORD ALLIGN                 
TINVGRS  DS    F                                                                
TINVNET  DS    F                                                                
TINVTAX  DS    F                                                                
TTABX    EQU   *-TINVID                                                         
         EJECT                                                                  
         PRINT OFF                                                              
       ++INCLUDE SPGENWBS                                                       
       ++INCLUDE SPGENCLT                                                       
       ++INCLUDE SPGENSTA                                                       
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'060SPA8WW    07/24/06'                                      
         END                                                                    
