*          DATA SET PUBVALR    AT LEVEL 023 AS OF 05/01/02                      
*CATALP PUBVAL                                                                  
         TITLE 'PUBVAL - CHANGE LOG '                                           
*                                                                               
* BPLA 12/97    CHANGE TO DISALLOW INPUT AFTER EDITION                          
*               TO FIX THIS PROGRAM ACCEPTING EDITION BEFORE ZONE               
*               IT USED TO EXIT AFTER VALIDATING EDITION                        
*                                                                               
* BPLA 4/26/95  REMOVE OI 0(R2),C'0'  AT VALP3                                  
*               IT CAUSED PUBVAL TO ACCEPT INPUTS OF ONE                        
*               CHARACTER NON-NUMERICS                                          
*               SEND ERROR IF DOESN'T START WITH NUMERIC                        
*                                                                               
* ROSA 1/4/89  ADD 2 NEW EDITION CODES FOR OUTDOOR              L01             
         TITLE 'PUBVAL - VALIDATE PUBLICATION,ZONE,EDITION'                     
*        PARAMETERS VIA R1                                                      
*        CL1   L'SOURCE - SET TO X'FF' IF INVALID SOURCE                        
*              X'00' = CONVERT Q FMT TO KEY FMT                                 
*        CL3   A(SOURCE)                                                        
*        CL1   DESTINATION FORMAT - 0=BINARY (KEY) - 1=ALPHA (REQUEST)          
*        CL3   A(DESTINATION)                                                   
         SPACE 2                                                                
PUBVAL   CSECT                                                                  
         NMOD1 05,**PUBV**                                                      
         USING WRKD,RC                                                          
         EJECT                                                                  
VALP1    L     R2,0(R1)            R2=A(SOURCE)                                 
         LA    R2,0(R2)                                                         
         CLI   0(R1),0             IF NO LENGTH CONVERT FROM                    
         BE    CONV                Q FMT TO KEY FMT                             
*                                                                               
         SR    R3,R3               R3=L'SOURCE                                  
         IC    R3,0(R1)                                                         
         CLI   0(R1),1                                                          
         BL    ERROR                                                            
         CLI   1(R2),C'-'                                                       
         BNE   VALP3                                                            
         CLI   5(R2),C'-'                                                       
         BNE   ERROR                                                            
         SPACE 2                                                                
VALP2    CLI   0(R1),10            FORMAT = 1-234-5678                          
         BL    ERROR                                                            
         MVC   TEMP(1),0(R2)                                                    
         MVC   TEMP+1(3),2(R2)                                                  
         MVC   TEMP+4(4),6(R2)                                                  
         LA    R4,10                                                            
         B     VALP4                                                            
         SPACE 2                                                                
VALP3    DS    0H                  FORMAT=12345678                              
         CLI   0(R2),C'0'                                                       
         BL    ERROR               MUST START WITH NUMERIC                      
*******  OI    0(R2),C'0'          WHY WAS THIS HERE????                        
         LR    R4,R2                                                            
VALP3B   DS    0H                                                               
         CLI   0(R4),C'0'                                                       
         BL    VALP3D                                                           
         LA    R4,1(R4)                                                         
         B     VALP3B                                                           
VALP3D   DS    0H                                                               
         SR    R4,R2                                                            
         CH    R4,=H'8'            MAX IS 8 DIGITS                              
         BH    ERROR                                                            
         BCTR  R4,R0               R4 = LEN-1                                   
*                                                                               
         MVC   TEMP(8),=8C'0'                                                   
         LA    R5,TEMP+7                                                        
         SR    R5,R4               R5=TO ADDR                                   
         EX    R4,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R5),0(R2)                                                    
         LA    R4,1(R4)            R4=LEN                                       
         SPACE 2                                                                
VALP4    BAS   RE,NUMERIC          PUB MUST BE NUMERIC                          
         BNE   ERROR                                                            
         MVC   PUBR(8),TEMP                                                     
         MVC   PUBR+8(3),=3C' '                                                 
         MVC   PUBK(4),TEMP1                                                    
         XC    PUBK+4(2),PUBK+4                                                 
         SPACE 2                                                                
VALP5    CR    R3,R4                                                            
         BE    VALOK               ONLY PUBLICATION NUM INPUT                   
         AR    R2,R4                                                            
         LA    R4,1(R4)                                                         
         CLI   0(R2),C','                                                       
         BNE   ERROR               INVALID DELIMITER                            
         LA    R5,1(R2)            R5=A(1ST ZONE CHR)                           
         SPACE 2                                                                
VALP6    LA    R2,1(R2)            SCAN FOR DELIMITER                           
         LA    R4,1(R4)                                                         
         CR    R4,R3                                                            
         BH    VALP7               END OF SOURCE                                
         CLI   0(R2),C','                                                       
         BNE   VALP6                                                            
         CR    R4,R3                                                            
         BE    ERROR               CANT BE LAST CHR                             
         SPACE 2                                                                
VALP7    CR    R5,R2                                                            
         BE    VALP8               ZONE NOT INPUT                               
         LR    R6,R2                                                            
         SR    R6,R5               R6=LEN OF ZONE FLD @ R5                      
         MVI   DUB,C'0'                                                         
         MVC   DUB+1(1),0(R5)                                                   
         CH    R6,=H'2'                                                         
         BH    VALP9                                                            
         BL    *+14                                                             
         MVC   DUB(2),0(R5)                                                     
         B     *+12                                                             
         CLI   DUB+1,C'0'                                                       
         BL    VALP9               SINGLE DIGIT MUST BE NUMERIC                 
*                                                                               
         BAS   RE,VALZON                                                        
         BNZ   VALP9                                                            
         MVC   PUBK+4(1),BYTE                                                   
         MVC   PUBR+8(2),DUB                                                    
         SPACE 2                                                                
VALP8    CR    R4,R3                                                            
         BH    VALOK               EDITION NOT INPUT                            
         LA    R5,1(R2)            R5=A(1ST EDITION CHR)                        
         LR    R6,R3                                                            
         SR    R6,R4               R6=LEN OF EDITION FLD                        
         SPACE 2                                                                
VALP9    CH    R6,=H'3'                                                         
         BH    ERROR                                                            
         BCTR  R6,R0                                                            
         MVC   DUB(3),=3C' '                                                    
         EX    R6,EDMOV                                                         
         LA    R7,EDTABLE                                                       
         SPACE 2                                                                
VALP10   CLC   0(3,R7),=C'END'                                                  
         BE    ERROR                                                            
         CLC   DUB(3),0(R7)                                                     
         BE    VALP11                                                           
         LA    R7,4(R7)                                                         
         B     VALP10                                                           
         SPACE 2                                                                
EDMOV    MVC   DUB(0),0(R5)                                                     
         SPACE 2                                                                
EDTABLE  DS    0C                                                               
         DC    C'M  A'                                                          
         DC    C'E  B'                                                          
         DC    C'D  C'                                                          
         DC    C'ME D'                                                          
         DC    C'SAME'                                                          
         DC    C'SAEF'                                                          
         DC    C'SD G'                                                          
         DC    C'SMEH'                                                          
         DC    C'SU I'                                                          
         DC    C'OM J'                                                          
         DC    C'OE K'                                                          
         DC    C'OD L'                                                          
         DC    C'OMEM'                                                          
         DC    C'PR N'             PRINTED                         L01          
         DC    C'P  P'                                                          
         DC    C'R  R'                                                          
         DC    C'S  S'                                                          
         DC    C'T  T'                                                          
         DC    C'MONU'                                                          
         DC    C'W  W'                                                          
         DC    C'PM X'             PERMANENT                       L01          
         DC    C'END '                                                          
         SPACE 2                                                                
VALP11   MVC   PUBR+10(1),3(R7)                                                 
         MVC   PUBK+5(1),3(R7)                                                  
*                                                                               
         L     R2,0(R1)            R2=A(SOURCE)                                 
         LA    R2,0(R2)                                                         
         AR    R5,R6              ADD LENGTH OF EDITION TO END                  
         LA    R5,1(R5)           SINCE R6 WAS DECREMENTED                      
         SR    R5,R2               TO GET WHAT I THINK SHOULD BE                
*                                  THE LENGTH OF INPUT                          
         CR    R5,R3               COMPARE VS ACTUAL                            
         BL    ERROR                                                            
                                                                                
         SPACE 2                                                                
VALOK    L     R8,4(R1)            RETURN BIN OR ALPHA INFO                     
         CLI   4(R1),0                                                          
         BNE   *+14                                                             
         MVC   0(6,R8),PUBK                                                     
         B     EXIT                                                             
         CLI   4(R1),1                                                          
         BNE   *+14                                                             
         MVC   0(11,R8),PUBR                                                    
         B     EXIT                                                             
         SPACE 2                                                                
ERROR    MVI   0(R1),X'FF'                                                      
         SPACE 2                                                                
EXIT     XMOD1 1                                                                
         SPACE 2                                                                
NUMERIC  MVC   DUB,=8C'0'          EBCDIC IN TEMP TO PACKED IN TEMP1            
         MVZ   DUB,TEMP                                                         
         CLC   DUB,=8C'0'                                                       
         BNE   NUMERICX                                                         
         PACK  TEMP1(5),TEMP(9)                                                 
         SR    R0,R0                                                            
NUMERICX BR    RE                                                               
         SPACE 2                                                                
*                                  CONVERT FROM Q FMT TO KEY FMT                
CONV     DS    0H                                                               
         L     R8,4(R1)                                                         
         XC    0(6,R8),0(R8)                                                    
         CLI   0(R2),C'0'                                                       
         BL    EXIT                NO PUB                                       
         PACK  0(5,R8),0(9,R2)                                                  
         MVI   4(R8),0                                                          
         CLI   8(R2),C' '                                                       
         BNH   CONV4                                                            
         MVC   DUB(2),8(R2)                                                     
         BAS   RE,VALZON                                                        
         BNZ   ERROR                                                            
         MVC   4(1,R8),BYTE                                                     
CONV4    DS    0H                                                               
         CLI   10(R2),C' '                                                      
         BNH   EXIT                                                             
         MVC   5(1,R8),10(R2)                                                   
         B     EXIT                                                             
         SPACE 3                                                                
*                                                                               
VALZON   DS    0H                  'HEXIN'                                      
         CLI   DUB,C'0'                                                         
         BNL   VALZON2                                                          
         CLI   DUB,C'F'                                                         
         BH    VALZON6                                                          
         CLI   DUB,C'A'                                                         
         BL    VALZON6                                                          
VALZON2  DS    0H                                                               
         CLI   DUB+1,C'0'                                                       
         BNL   VALZON4                                                          
         CLI   DUB+1,C'F'                                                       
         BH    VALZON6                                                          
         CLI   DUB+1,C'A'                                                       
         BL    VALZON6                                                          
VALZON4  DS    0H                                                               
         MVO   BYTE,DUB(1)                                                      
         MVN   BYTE,DUB+1                                                       
         IC    RF,BYTE                                                          
         CLI   DUB,C'0'                                                         
         BNL   *+8                                                              
         LA    RF,144(RF)                                                       
         CLI   DUB+1,C'0'                                                       
         BNL   *+8                                                              
         LA    RF,9(RF)                                                         
         STC   RF,BYTE                                                          
         SR    R0,R0               SET CC OK                                    
         BR    RE                                                               
VALZON6  DS    0H                                                               
         LTR   RE,RE               SET CC TO ERROR                              
         BR    RE                                                               
         SPACE 2                                                                
         LTORG                                                                  
         SPACE 2                                                                
WRKD     DSECT                                                                  
DUB      DS    D                                                                
TEMP     DS    CL8                                                              
         DS    CL1                                                              
TEMP1    DS    CL4                                                              
         DS    CL1                                                              
PUBR     DS    CL11                                                             
PUBK     DS    CL6                                                              
BYTE     DS    X                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'023PUBVALR   05/01/02'                                      
         END                                                                    
