*          DATA SET SPMEDADDWT AT LEVEL 055 AS OF 03/13/17                      
*CATALP MEDADDWT                                                                
         TITLE 'MARKET WEIGHT ADDITION AND CALCULATION ROUTINES'                
*  PARAMETERS                                                                   
*        1=A(SPWORKC)                                                           
VMDADDWT CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 200,MEDADDWT,RR=R8                                               
         ST    RC,ADEMREC          SAVE IO AREA ADDRESS                         
         L     RA,0(R1)                                                         
         LA    RC,2048(RA)                                                      
         LA    RC,2048(RC)                                                      
         USING SPWORKD,RA,RC                                                    
         STM   RA,RC,SPMWRA                                                     
         L     RE,=A(MWTAB)                                                     
         AR    RE,R8                                                            
         ST    RE,AMWTAB                                                        
         CLI   MODE,REQFRST                                                     
         BNE   ADDWT                                                            
         CLC   QMED,MYMED          CHANGE OF MEDIA                              
         BE    *+8                                                              
         MVI   PRRTGSRV,0          FORCE NEW SERVICE                            
*                                                                               
ADDWT    CLI   MODE,MKTLAST                                                     
         BNL   MAWT                                                             
         CLI   MODE,ESTFRST        ACTIVATE AFTER ESTFRST                       
         BL    EXIT                                                             
*                                                                               
         CLC   QCLT,MYCLT          CHANGE OF CLIENT OR                          
         BNE   *+14                                                             
         CLC   QMED,MYMED          CHANGE OF MEDIA                              
         BE    *+14                                                             
         MVI   MYPRD,0             FORCE NEW PRODUCT                            
         XC    DEMO,DEMO           AND DEMOS                                    
         CLC   BPRD,MYPRD          CHECK FOR NEW PRD OR REQ                     
         BE    *+8                                                              
         BAS   RE,GETPRMY          GET PRIMARY DEMO                             
         CLC   MYRTGSRV,PRRTGSRV   SAME SERVICE                                 
         BNE   GTWT0                                                            
         CLC   MKT,MYMKT                                                        
         BE    EXIT1                                                            
*                                                                               
         CLI   FRSTTIM,1           FIRST TIME FOR RUN - CLEAR TABLES            
         BNE   GTWT1                                                            
GTWT0    XC    DEMLST,DEMLST                                                    
         XC    MWTCTR,MWTCTR                                                    
         MVC   PRRTGSRV,MYRTGSRV                                                
         MVI   NXTSLT,X'FF'        FORCE TO GREATER THAN MAX                    
         XC    MKTCNT(10),MKTCNT                                                
         XC    WDETAIL(36),WDETAIL                                              
         XC    USTOTAL,USTOTAL                                                  
         XC    DEMO,DEMO                                                        
         LA    RE,MLST                                                          
         L     RF,=F'3600'                                                      
         XCEF                                                                   
         L     RE,AMWTAB                                                        
         L     RF,=F'6000'                                                      
         XCEF                                                                   
         XC    TABCNT,TABCNT                                                    
         XC    MGR1CNTM(28),MGR1CNTM                                            
         MVI   FRSTTIM,0                                                        
*                                                                               
GTWT1    CLI   PRMYWTSW,C'Y'       WANT PRIMARY DEMO WEIGHTS                    
         BNE   GTWT3                                                            
         CLI   SPOTPROF+1,C'N'     BYPASS IF NO WEIGHTING                       
         BE    GTWT3                                                            
         BAS   R9,FINDDEM                                                       
         CLC   DEMO,PRMYDEM                                                     
         BE    GTWT3                                                            
         MVC   DEMO,PRMYDEM                                                     
         XC    USTOTAL,USTOTAL                                                  
         BAS   RE,RDUNIV           NO - READ A NEW SET                          
         L     RE,ASLOT            SET TO CURRECT SLOT                          
         MVC   4(4,RE),USTOTAL     SET THE US TOTAL IN SLOT                     
*                                                                               
GTWT3    MVI   ACTSW,1                                                          
         MVC   MYPRD,BPRD                                                       
         MVC   MYCLT,QCLT                                                       
         MVC   MYMED,QMED                                                       
         MVC   MYMKT,MKT                                                        
         CLI   MYMKT,C' '                                                       
         BE    *+14                                                             
         OC    MYMKT,MYMKT                                                      
         BNZ   *+10                                                             
         MVC   MYMKT,=C'0001'      DON'T LET IT DUMP                            
         PACK  DUB,MYMKT                                                        
         CVB   RF,DUB                                                           
         STH   RF,MYSVMKT                                                       
         MVI   SPDUPTOT,C'N'                                                    
         XC    SPNOMKTS,SPNOMKTS                                                
         XC    SPWEIGHT,SPWEIGHT                                                
*                                                                               
         CLI   SPOTPROF+1,C'N'     NO WEIGHTING                                 
         BE    EXIT                                                             
         L     RE,ADMARKET         USE WEIGHTS IN MARKET RECORD                 
         USING MKTREC,RE                                                        
         CLI   PRMYWTSW,C'Y'       DO WE WANT PRIMARY DEMO WEIGHTS              
         BE    GTPRMYWT                                                         
         CLC   MKTWT,SPACES        CHECK FOR ZERO WEIGHTS                       
         BE    GTWT4                                                            
         OC    MKTWT,MKTWT                                                      
         BZ    GTWT4                                                            
         PACK  DUB,MKTWT           CONVERT WEIGHT TO BINARY                     
         CVB   R0,DUB                                                           
         LTR   R0,R0                                                            
         BNZ   *+8                                                              
GTWT4    LHI   R0,1                                                             
         ST    R0,SPWEIGHT                                                      
         B     EXIT1                                                            
         EJECT                                                                  
GTPRMYWT MVC   SPWEIGHT,=F'1'      FORCE DEFAULT WEIGHT                         
         CLI   QMED,C'R'           NA FOR RADIO                                 
         BE    EXIT1                                                            
         CLC   MKTRS1,MYRTGSRV     SET FOR PROPER RATING SERVICE                
         BE    GPW1                                                             
         LA    RE,3(RE)                                                         
         CLC   MKTRS1,MYRTGSRV                                                  
         BNE   EXIT1                                                            
GPW1     L     R6,AMWTAB           FIND THE UNIVERSE                            
         USING MWTABD,R6                                                        
         L     RF,MWTCTR                                                        
         LTR   RF,RF               DID NOT FIND VALID MARKET - EXIT             
         BZ    EXIT1                                                            
GPW2     CLC   MKTRSM1,MWMNUM                                                   
         BE    GPW3                                                             
         LA    R6,MWTABLN(R6)                                                   
         BCT   RF,GPW2                                                          
         B     EXIT1               NOT FOUND-RETURN WEIGHT OF 1                 
         SPACE 2                                                                
GPW3     SR    RE,RE               CONVERT TO PERCENT OF US POP                 
         ZIC   RF,SLOT             GET THE SLOT                                 
         SLL   RF,2                TIMES 4                                      
         LA    R6,0(RF,R6)         INDEX FOR SLOT                               
         ICM   RF,15,MWMUNIV                                                    
         M     RE,=F'20000'                                                     
         OC    USTOTAL,USTOTAL     SOMETHING WENT WRONG                         
         BNZ   *+10                 BUT STOP THE DUMP                           
         MVC   USTOTAL,=F'1'                                                    
         D     RE,USTOTAL                                                       
         A     RF,=F'1'                                                         
         SRA   RF,1                                                             
         LTR   RF,RF                                                            
         BZ    EXIT1                                                            
         STCM  RF,15,SPWEIGHT                                                   
         B     EXIT1                                                            
         DROP  RE                                                               
         EJECT                                                                  
MAWT     DS    0H                                                               
         CLI   MODE,MKTLAST                                                     
         BNE   MAWT2                                                            
         XC    MYMKT,MYMKT                                                      
         CLI   ACTSW,0             ANY ACTIVITYY                                
         BE    EXIT                 NO - EXIT                                   
         MVI   ACTSW,0                                                          
         ZIC   RE,MKTCNT                                                        
         LA    RE,1(RE)                                                         
         STC   RE,MKTCNT                                                        
         ZIC   RE,MKTCNTP                                                       
         LA    RE,1(RE)                                                         
         STC   RE,MKTCNTP                                                       
         LA    R8,WDETAIL                                                       
         LA    R9,5                                                             
MAWT1    L     RE,0(R8)                                                         
         A     RE,SPWEIGHT                                                      
         ST    RE,0(R8)                                                         
         LA    R8,4(R8)                                                         
         BCT   R9,MAWT1                                                         
         LA    R8,MGR1CNTM                                                      
         LA    R9,3                                                             
MAWT1A   L     RE,0(R8)                                                         
         LA    RE,1(RE)                                                         
         ST    RE,0(R8)                                                         
         LA    R8,4(R8)                                                         
         BCT   R9,MAWT1A                                                        
         L     R7,TABCNT                                                        
         GOTO1 BINSRCH,MYDMCB,(X'01',MYSVMKT),MLST,(R7),3,(0,2),1200            
         MVC   TABCNT,MYDMCB+8                                                  
         OC    MYDMCB,MYDMCB                                                    
         BNZ   *+6                                                              
         DC    H'0'                TABLE IS FULL                                
         L     R4,MYDMCB           GET SLOT ADDRESS                             
         TM    2(R4),4             ALREADY THERE                                
         BO    APGR2                                                            
         OI    2(R4),4              NO - SET INDICATOR AND ADD                  
         L     RE,PGR1CNTM                                                      
         LA    RE,1(RE)                                                         
         ST    RE,PGR1CNTM                                                      
         L     RE,PGR1WT                                                        
         A     RE,SPWEIGHT                                                      
         ST    RE,PGR1WT                                                        
         SPACE 2                                                                
APGR2    TM    2(R4),2                                                          
         BO    APGR3                                                            
         OI    2(R4),2                                                          
         L     RE,PGR2CNTM                                                      
         LA    RE,1(RE)                                                         
         ST    RE,PGR2CNTM                                                      
         L     RE,PGR2WT                                                        
         A     RE,SPWEIGHT                                                      
         ST    RE,PGR2WT                                                        
         SPACE 2                                                                
APGR3    TM    2(R4),1                                                          
         BO    ACLT                                                             
         OI    2(R4),1                                                          
         L     RE,PGR3CNTM                                                      
         LA    RE,1(RE)                                                         
         ST    RE,PGR3CNTM                                                      
         L     RE,PGR3WT                                                        
         A     RE,SPWEIGHT                                                      
         ST    RE,PGR3WT                                                        
         SPACE 2                                                                
ACLT     TM    2(R4),8                                                          
         BO    AEND                                                             
         OI    2(R4),8                                                          
         L     RE,CLTCNTM                                                       
         LA    RE,1(RE)                                                         
         ST    RE,CLTCNTM                                                       
         L     RE,CLTWT                                                         
         LA    RE,1(RE)                                                         
         ST    RE,CLTWT                                                         
AEND     DS    0H                                                               
         SPACE 2                                                                
         B     EXIT                                                             
         SPACE 2                                                                
MAWT2    CLC   SAVMODE,MODE                                                     
         BE    MADUP                                                            
         XC    SPWEIGHT,SPWEIGHT                                                
         XC    SPNOMKTS,SPNOMKTS                                                
         CLI   MODE,MGR3LAST                                                    
         BNE   MAWT3                                                            
         MVI   SPDUPTOT,C'Y'                                                    
         CLI   MKTCNT,0                                                         
         BE    MAWT2X                                                           
         ZIC   RE,MGR3CNT                                                       
         LA    RE,1(RE)                                                         
         STC   RE,MGR3CNT                                                       
         CLI   MKTCNT,1                                                         
         BE    MAWT2X                                                           
         MVI   SPDUPTOT,C'N'                                                    
MAWT2X   MVI   MKTCNT,0                                                         
         MVC   SPWEIGHT,MGR3WT                                                  
         MVC   SPNOMKTS,MGR3CNTM                                                
         XC    MGR3WT,MGR3WT                                                    
         XC    MGR3CNTM,MGR3CNTM                                                
         B     EXIT                                                             
         SPACE 2                                                                
MAWT3    CLI   MODE,MGR2LAST                                                    
         BNE   MAWT4                                                            
         MVI   SPDUPTOT,C'Y'                                                    
         CLI   MKTCNT,0                                                         
         BNE   *+12                                                             
         CLI   MGR3CNT,0                                                        
         BE    MAWT3X                                                           
         ZIC   RE,MGR2CNT                                                       
         LA    RE,1(RE)                                                         
         STC   RE,MGR2CNT                                                       
         CLI   MGR3CNT,1                                                        
         BE    MAWT3X                                                           
         CLI   MKTCNT,1                                                         
         BE    MAWT3X                                                           
         MVI   SPDUPTOT,C'N'                                                    
MAWT3X   MVC   SPNOMKTS,MGR2CNTM                                                
         MVC   SPWEIGHT,MGR2WT                                                  
         MVI   MKTCNT,0                                                         
         MVI   MGR3CNT,0                                                        
         XC    MGR2WT,MGR2WT                                                    
         XC    MGR2CNTM,MGR2CNTM                                                
         B     EXIT                                                             
         SPACE 2                                                                
MAWT4    CLI   MODE,MGR1LAST                                                    
         BNE   MAWT5                                                            
         MVI   SPDUPTOT,C'Y'                                                    
         CLI   MKTCNT,0                                                         
         BNE   *+12                                                             
         CLI   MGR2CNT,0                                                        
         BE    MAWT4X                                                           
         ZIC   RE,MGR1CNT                                                       
         LA    RE,1(RE)                                                         
         STC   RE,MGR1CNT                                                       
         CLI   MKTCNT,1                                                         
         BE    MAWT4X                                                           
         CLI   MGR2CNT,1                                                        
         BE    MAWT4X                                                           
         MVI   SPDUPTOT,C'N'                                                    
MAWT4X   MVC   SPWEIGHT,MGR1WT                                                  
         MVC  SPNOMKTS,MGR1CNTM                                                 
         MVI   MKTCNT,0                                                         
         MVI   MGR2CNT,0                                                        
         XC    MGR1WT,MGR1WT                                                    
         XC    MGR1CNTM,MGR1CNTM                                                
         B     EXIT                                                             
         SPACE 2                                                                
MAWT5    CLI   MODE,PRDLAST                                                     
         BNE   MAWT6                                                            
         MVI   SPDUPTOT,C'Y'                                                    
         CLI   MGR1CNT,1                                                        
         BE    MAWT5X                                                           
         CLI   MKTCNTP,1                                                        
         BNH   MAWT5X                                                           
         MVI   SPDUPTOT,C'N'                                                    
MAWT5X   DS    0H                                                               
         MVI   MYPRD,0             RESET INTERNAL PRODUCT FOR NXT REQ           
         MVI   MYMED,0                                                          
         MVC   MYCLT,=C'   '                                                    
         MVC   SPWEIGHT,PRDWT                                                   
         XC    SPNOMKTS,SPNOMKTS                                                
         MVC   SPNOMKTS+3(1),MKTCNTP                                            
         CLI   MKTCNTP,0                                                        
         BE    MAWT5X1                                                          
         ZIC   RE,PRDCNT                                                        
         LA    RE,1(RE)                                                         
         STC   RE,PRDCNT                                                        
         ZIC   RE,CPRDCNT                                                       
         LA    RE,1(RE)                                                         
         STC   RE,CPRDCNT                                                       
MAWT5X1  MVI   MGR1CNT,0                                                        
         MVI   MKTCNTP,0                                                        
         XC    PRDWT,PRDWT                                                      
         B     EXIT                                                             
         SPACE 2                                                                
MAWT6    CLI   MODE,PGR3LAST                                                    
         BNE   MAWT7                                                            
         MVI   SPDUPTOT,C'Y'                                                    
         CLI   PRDCNT,0                                                         
         BE    MAWT6X                                                           
         ZIC   RE,PGR3CNT                                                       
         LA    RE,1(RE)                                                         
         STC   RE,PGR3CNT                                                       
         CLI   PRDCNT,1                                                         
         BE    MAWT6X                                                           
         MVC   SPWEIGHT,PGR3WT                                                  
         MVI   SPDUPTOT,C'N'                                                    
*        MVC   SPNOMKTS,PGR3CNTM                                                
         ZIC   RE,PGR3CNT                                                       
         LA    RE,1(RE)                                                         
         STC   RE,PGR3CNT                                                       
MAWT6X   MVI   PRDCNT,0                                                         
         XC    PGR3WT,PGR3WT                                                    
         XC    PGR3CNTM,PGR3CNTM                                                
         MVI   CLRCODE,1                                                        
         BAS   R9,CLRTAB                                                        
         B     EXIT                                                             
         SPACE 2                                                                
MAWT7    CLI   MODE,PGR2LAST                                                    
         BNE   MAWT8                                                            
         MVI   SPDUPTOT,C'Y'                                                    
         CLI   PRDCNT,0                                                         
         BNE   *+12                                                             
         CLI   PGR3CNT,0                                                        
         BE    MAWT7X                                                           
         ZIC   RE,PGR2CNT                                                       
         LA    RE,1(RE)                                                         
         STC   RE,PGR2CNT                                                       
         CLI   PGR3CNT,1                                                        
         BE    MAWT7X                                                           
         CLI   PRDCNT,1                                                         
         BE    MAWT7X                                                           
         MVC   SPWEIGHT,PGR2WT                                                  
         MVI   SPDUPTOT,C'N'                                                    
*        MVC   SPNOMKTS,PGR2CNTM                                                
MAWT7X   MVI   PRDCNT,0                                                         
         MVI   MGR3CNT,0                                                        
         MVI   PGR3CNT,0                                                        
         XC    PGR2WT,PGR2WT                                                    
         XC    PGR2CNTM,PGR2CNTM                                                
         MVI   CLRCODE,2                                                        
         BAS   R9,CLRTAB                                                        
         B     EXIT                                                             
         SPACE 2                                                                
MAWT8    CLI   MODE,PGR1LAST                                                    
         BNE   MAWT9                                                            
         MVI   SPDUPTOT,C'Y'                                                    
         CLI   PRDCNT,0                                                         
         BNE   *+12                                                             
         CLI   PGR2CNT,0                                                        
         BE    MAWT8X                                                           
         ZIC   RE,PGR1CNT                                                       
         LA    RE,1(RE)                                                         
         STC   RE,PGR1CNT                                                       
         CLI   PGR2CNT,1                                                        
         BE    MAWT7X                                                           
         CLI   PRDCNT,1                                                         
         BE    MAWT8X                                                           
         MVC   SPWEIGHT,PGR1WT                                                  
         MVI   SPDUPTOT,C'N'                                                    
*        MVC   SPNOMKTS,PGR1CNTM                                                
MAWT8X   MVI   PRDCNT,0                                                         
         MVI   MGR2CNT,0                                                        
         MVI   PGR2CNT,0                                                        
         XC    PGR1WT,PGR1WT                                                    
         XC    PGR1CNTM,PGR1CNTM                                                
         MVI   CLRCODE,4                                                        
         BAS   R9,CLRTAB                                                        
         B     EXIT                                                             
         SPACE 2                                                                
MAWT9    CLI   MODE,CLTLAST                                                     
         BNE   MAWT10                                                           
         MVI   SPDUPTOT,C'Y'                                                    
         CLI   PGR1CNT,0                                                        
         BE    *+12                                                             
         CLI   PGR1CNT,1                                                        
         BE    MAWT9X                                                           
         CLI   CPRDCNT,1                                                        
         BNH   MAWT9X                                                           
         MVC   SPWEIGHT,CLTWT                                                   
         MVI   SPDUPTOT,C'N'                                                    
*        MVC   SPNOMKTS,CLTCNTM                                                 
MAWT9X   MVI   CPRDCNT,0                                                        
         XC    CLTWT,CLTWT                                                      
         XC    CLTCNTM,CLTCNTM                                                  
         MVI   CLRCODE,8                                                        
         BAS   R9,CLRTAB                                                        
         B     EXIT                                                             
         SPACE 2                                                                
MADUP    MVC   SPDUPTOT,SVDUPTOT   SAME MODE AS LAST TIME                       
         MVC   SPWEIGHT,SVWEIGHT    YES - RESTORE FIELDS                        
         MVC   SPNOMKTS,SVNOMKTS                                                
         B     EXIT1                                                            
         SPACE 2                                                                
MAWT10   CLI   MODE,REQLAST                                                     
         BNE   EXIT                                                             
         XC    MKTCNT(10),MKTCNT                                                
         XC    WDETAIL(36),WDETAIL                                              
         LA    RE,MLST                                                          
         L     RF,=F'3600'                                                      
         XCEF                                                                   
         XC    TABCNT,TABCNT                                                    
         XC    MGR1CNTM(28),MGR1CNTM                                            
         SPACE 2                                                                
EXIT     MVC   SVDUPTOT,SPDUPTOT   SAVE ACTIVE FIELDS                           
         MVC   SVWEIGHT,SPWEIGHT                                                
         MVC   SVNOMKTS,SPNOMKTS                                                
         MVC   SAVMODE,MODE                                                     
         SPACE 2                                                                
EXIT1    XMOD1 1                                                                
         EJECT                                                                  
CLRTAB   LA    RE,MLST                                                          
         L     RF,TABCNT                                                        
         LTR   RF,RF                                                            
         BZR   R9                                                               
         XC    CLRCODE,=X'FF'                                                   
         ZIC   R7,CLRCODE                                                       
CLRTAB1  EX    R7,*+8                                                           
         B     *+8                                                              
         NI    2(RE),0                                                          
         LA    RE,3(RE)                                                         
         BCT   RF,CLRTAB1                                                       
         BR    R9                                                               
         EJECT                                                                  
* FIND PRIMARY DEMO FOR A PRODUCT                                               
GETPRMY  NTR1                                                                   
         CLI   MODE,MKTFRST        ONLY DO AFTER WE HAVE ACTIVITY               
         BL    EXIT1                                                            
         ZIC   RE,BPRD                                                          
         CH    RE,=H'218'          USE BRAND PRIMARY                            
         BL    *+8                                                              
         LA    RE,220              USE POL PRIMARY                              
         BCTR  RE,0                                                             
         MH    RE,PRDBUFLN                                                      
         A     RE,PRDBUFF                                                       
         MVC   PRMYDEM,28(RE)      SET DEMO NUMBER                              
*                                                                               
         CLI   PRMYDEM+2,0         TEST COMSCORE DEMO                           
         JNE   *+8                                                              
         MVI   PRMYDEM+2,1         FORCE HOMES                                  
*                                                                               
         MVI   PRMYDEM+1,C'U'      FORCE FOR UNIVERSE LOOKUP                    
         L     RE,ADCLT                                                         
         USING CLTHDR,RE                                                        
*        MVI   MYRTGSRV,C'1'                                                    
*        CLI   CPROF+3,C'1'                                                     
*        BE    *+8                                                              
         MVI   MYRTGSRV,C'0'       ALWAYS NSI                                   
         B     EXIT1                                                            
         DROP  RE                                                               
         EJECT                                                                  
* FIND A DEMO AND ITS SLOT IN TABLE                                             
FINDDEM  LA    RE,DEMLST           POINT TO LIST OF DEMOS                       
         LA    RF,0                                                             
         MVC   DEMO,PRMYDEM        SET FOR SUCCESSFUL FIND                      
FINDDEM2 OC    0(3,RE),0(RE)       FIND SLOT FOR THIS DEMO                      
         BZ    FINDDEM4                                                         
         CLC   0(3,RE),PRMYDEM     DEMO OK                                      
         BE    FINDDEM6            DO SETUP                                     
         LA    RF,1(RF)            BUMP SLOT COUNTER                            
         LA    RE,8(RE)            BUMP SLOT                                    
         B     FINDDEM2            TRY NEXT                                     
         SPACE 2                                                                
FINDDEM4 ZIC   RF,NXTSLT                                                        
         CH    RF,=H'5'            MORE THAN MAX                                
         BNH   *+6                                                              
         SR    RF,RF               SET FOR WRAP AROUND                          
         STC   RF,NXTSLT           SET FOR NEXT SLOT                            
         SLL   RF,3                TIMES 8                                      
         LA    RE,DEMLST(RF)       SET TO TABLE SLOT                            
         MVC   0(3,RE),PRMYDEM     SET DEMO                                     
         MVC   3(1,RE),NXTSLT      SET SLOT                                     
         XC    4(4,RE),4(RE)       CLEAR US TOTAL                               
         XC    DEMO,DEMO           CLEAR THE DEMO CODE                          
FINDDEM6 ST    RE,ASLOT            SET ADDRESS OF DEMLST                        
         MVC   USTOTAL,4(RE)       SET USTOTAL                                  
         MVC   SLOT,3(RE)          SET CURRENT SLOT                             
         BR    R9                                                               
         EJECT                                                                  
* READ IN ALL MARKET UNIVERSES FOR SPECIFIED BOOK                               
* AUTOMATICALLY RESOLVE BOOK TO NOVEMBER OF PRIOR YEAR (NOTE IF YOU             
* CHANGE THIS TO ALSO CHANGE THE SAME CODE IN SPOTMKWT)                         
RDUNIV   NTR1  WORK=(R2,40)                                                     
         MVI   BOOK+1,11           NOVEMBER                                     
         GOTO1 DATCON,MYDMCB,(5,0),(3,FULL)                                     
         XR    RF,RF                                                            
         IC    RF,FULL                                                          
         BCTR  RF,0                                                             
         CLI   FULL+1,1            TWO YEARS BACK FOR JANUARY                   
         BNE   *+6                                                              
         BCTR  RF,0                                                             
         STC   RF,BOOK                                                          
*                                                                               
         L     R4,ADBLOCK                                                       
         USING DBLOCK,R4                                                        
         MVC   0(256,R2),0(R4)                                                  
         XC    0(256,R4),0(R4)     SET UP TO READ MARKETS                       
         MVC   DBFILE,=C'TP '                                                   
         MVI   DBFUNCT,DBGETMKB                                                 
         CLI   QMED,C'T'                                                        
         BNE   EXITU                                                            
         MVI   DBSELMED,C'T'                                                    
         MVI   DBSELSRC,C'N'                                                    
         MVC   DBSELAGY,QAGY                                                    
         MVC   DBSELBK,BOOK                                                     
         MVC   DBCOMFCS,ACOMFACS                                                
         L     RE,ADEMREC         READ MARKETS AND SAVE THE NUMBERS             
         ST    RE,DBAREC                                                        
         OC    MWTCTR,MWTCTR      MARKETS ALREADY READ                          
         BNZ   GETUNIV                                                          
         GOTO1 DEMAND,DMCB,ADBLOCK,SVMRKT                                       
*                                                                               
* FOR THE NOV/05 & NOV/12 BOOK ONLY, ADD A FEW EXTRA MKTS TO THE TABLE          
* BECAUSE THEY DON'T EXIST IN THE NSI DATA (HURRICANES)                         
*                                                                               
         CLC   BOOK,=X'690B'       NOV/05 BOOK?                                 
         BE    *+14                YES                                          
         CLC   BOOK,=X'700B'       NOV/12 BOOK?                                 
         BNE   GETUNIV             NO                                           
*                                                                               
         L     R6,AMWTAB           POINT TO MARKET WEIGHT TABLE ENTRY           
         USING MWTABD,R6                                                        
*                                                                               
         OC    MWMNUM,MWMNUM       SAVE MARKET NUMBERS                          
         BZ    *+12                                                             
         LA    R6,MWTABLN(R6)                                                   
         B     *-14                                                             
*                                                                               
         L     RF,MWTCTR           MARKET WEIGHT COUNTER                        
         LA    RE,4                ADD 4 MARKETS                                
         CLC   BOOK,=X'690B'       NOV/05 BOOK?                                 
         BE    RDU10               YES                                          
         LA    RE,1                ADD ONE MARKET                               
         MVC   MWMNUM,=H'101'      ADD NEW YORK                                 
         B     RDU20               UPDATE MARKET WEIGHT COUNTER                 
*                                                                               
RDU10    MVC   MWMNUM,=H'128'      ADD MIAMI                                    
         LA    R6,MWTABLN(R6)                                                   
         MVC   MWMNUM,=H'148'      AND WEST PALM BEACH                          
         LA    R6,MWTABLN(R6)                                                   
         MVC   MWMNUM,=H'222'      AND NEW ORLEANS                              
         LA    R6,MWTABLN(R6)                                                   
         MVC   MWMNUM,=H'346'      AND BILOXI                                   
RDU20    AR    RF,RE               ADD MARKETS TO MKT WEIGHT COUNTER            
         ST    RF,MWTCTR           UPDATE MARKET WEIGHT COUNTER                 
*                                                                               
         DROP  R6                                                               
* END NOV/05                                                                    
*                                                                               
GETUNIV  L     R6,AMWTAB           READ THE UNIVERSES                           
         USING MWTABD,R6                                                        
GETUNIV2 OC    MWMNUM,MWMNUM                                                    
         BZ    EXITU                                                            
         ST    R6,SVMWTAB          SAVE TABLE ADDRESS                           
         L     R4,ADBLOCK                                                       
         XC    0(256,R4),0(R4)     SET UP TO READ MARKETS                       
         MVC   DBFILE,=C'TP '                                                   
         MVI   DBFUNCT,DBGETMKB                                                 
         CLI   QMED,C'T'                                                        
         BNE   EXITU                                                            
         MVI   DBSELMED,C'T'                                                    
         MVI   DBSELSRC,C'N'                                                    
         MVC   DBSELAGY,QAGY                                                    
         MVC   DBSELBK,BOOK                                                     
         MVC   DBCOMFCS,ACOMFACS                                                
         L     RE,ADEMREC         READ MARKETS AND SAVE THE NUMBERS             
         ST    RE,DBAREC                                                        
         MVC   DBSELRMK,MWMNUM                                                  
*                                                                               
         CLC   DBSELBK,=X'700B'    NOV/12 BOOK?                                 
         BNE   GETU15              NO                                           
         CLC   DBSELRMK,=H'101'    NEW YORK MARKET?                             
         BNE   GETU15              NO                                           
         MVC   DBSELBK,=X'700A'    YES - USE THE OCT/12 BOOK INSTEAD            
*                                                                               
* FOR THE FOLLOWING MARKETS, SUBSTITUTE THE NOV/04 DATA FOR NOV/05              
*                                                                               
GETU15   CLC   DBSELBK,=X'690B'    NOV/05 BOOK?                                 
         BNE   GETU30                                                           
         CLC   DBSELRMK,=H'128'    IF MARKET IS MIAMI                           
         BE    GETU20                                                           
         CLC   DBSELRMK,=H'148'    OR WEST PALM BEACH                           
         BE    GETU20                                                           
         CLC   DBSELRMK,=H'346'    OR BILOXI                                    
         BE    GETU20                                                           
         CLC   DBSELRMK,=H'222'    OR NEW ORLEANS                               
         BNE   GETU30                                                           
GETU20   MVC   DBSELBK,=X'680B'    USE THE NOV/04 BOOK INSTEAD                  
*                                                                               
GETU30   MVI   DBSELDAY,X'40'      SET TO READ MON 5-515P                       
         MVC   DBSELTIM(2),=H'1700'                                             
         MVC   DBSELTIM+2(2),=H'1715'                                           
         MVI   DBFUNCT,DBGETTOT                                                 
         GOTO1 DEMAND,DMCB,ADBLOCK,SVUNIV                                       
         LA    R6,MWTABLN(R6)                                                   
         B     GETUNIV2                                                         
         SPACE 2                                                                
EXITU    L     R4,ADBLOCK          RESTORE THE DBLOCK                           
         MVC   0(256,R4),0(R2)                                                  
         XIT1                                                                   
         EJECT                                                                  
* SAVE MARKET NUMBER FROM RATING SERVICE MARKET RECORD                          
         DS    0D                                                               
         USING *,RF                                                             
SVMRKT   NTR1  BASE=SPMWRB                                                      
         LM    RA,RC,SPMWRA                                                     
         DROP  RF                                                               
         L     R4,ADBLOCK                                                       
         USING DBLOCK,R4                                                        
         L     R5,DBAREC                                                        
         USING DMKEY,R5                                                         
         OC    DMRMKT,DMRMKT                                                    
         BZ    EXIT1                                                            
         L     R6,AMWTAB           POINT TO SAVE AREA                           
         USING MWTABD,R6                                                        
SVMRKT2  OC    MWMNUM,MWMNUM       SAVE MARKET NUMBERS                          
         BZ    SVMRKT3                                                          
         CLC   DMRMKT,MWMNUM       CHECK FOR DUPLICATE                          
         BE    EXIT1                                                            
         LA    R6,MWTABLN(R6)                                                   
         B     SVMRKT2                                                          
SVMRKT3  L     R1,MWTCTR                                                        
         LA    R1,1(R1)                                                         
         ST    R1,MWTCTR                                                        
         MVC   MWMNUM,DMRMKT                                                    
         B     EXIT1                                                            
         EJECT                                                                  
* SAVE UNIVERSE IN MARKET TABLE                                                 
         DS    0D                                                               
         USING *,RF                                                             
SVUNIV   NTR1  BASE=SPMWRB                                                      
         LM    RA,RC,SPMWRA                                                     
         DROP  RF                                                               
         L     R4,ADBLOCK                                                       
         L     R9,ACOMFACS                                                      
         USING COMFACSD,R9                                                      
         GOTO1 CDEMOUT,DMCB,(C'D',DEMO),ADBLOCK,FULL                            
         DROP  R9                                                               
         L     R5,DBAREC                                                        
         USING DRKEY,R5                                                         
         LA    R7,DRFRSTEL                                                      
         USING MARELEM,R7                                                       
         L     R6,SVMWTAB          SAVE UNIVERSE IN MARKET TABLE                
         USING MWTABD,R6                                                        
         ZIC   RF,SLOT             SET TO CORRECT SLOT                          
         SLL   RF,2                                                             
         LA    R6,0(RF,R6)                                                      
SUNIV3   MVC   MWMUNIV,FULL                                                     
*                                                                               
         CLI   DBSELSRC,C'N'       FOR NSI                                      
         BNE   SUNIV5                                                           
         LA    RE,SMATAB           DON'T ADD IN SMA MARKETS                     
SUNIV4   CLI   0(RE),X'FF'                                                      
         BE    SUNIV5                                                           
         CLC   0(2,RE),DBSELRMK                                                 
         BE    EXIT1                                                            
         LA    RE,2(RE)                                                         
         B     SUNIV4                                                           
*                                                                               
SUNIV5   L     R8,FULL             DO US TOTAL                                  
         A     R8,USTOTAL                                                       
         ST    R8,USTOTAL                                                       
         B     EXIT1                                                            
         EJECT                                                                  
SMATAB   DC    H'185'              AKRON                                        
         DC    H'199'              SARASOTA                                     
         DC    H'165'              ELMIRA                                       
         DC    H'260'              ANNISTON                                     
         DC    H'226'              VICTORIA                                     
         DC    H'342'              ENSIGN-GARDEN CITY                           
         DC    H'290'              GREAT BEND                                   
         DC    H'341'              HAYS - GOODLAND                              
         DC    X'FF'                                                            
         SPACE 3                                                                
         LTORG                                                                  
         EJECT                                                                  
BOOK     DS    XL2                                                              
PRMYWTSW EQU   SPOTPROF+14                                                      
AMWTAB   DC    F'0'                                                             
SPMWRA   DC    F'0'                                                             
SPMWRB   DC    F'0'                                                             
SPMWRC   DC    F'0'                                                             
USTOTAL  DC    F'0'                                                             
MWTCTR   DC    F'0'                                                             
SVMWTAB  DC    F'0'                                                             
ADEMREC  DC    F'0'                                                             
MYPRD    DC    H'0'                                                             
MYMED    DC    X'00'                                                            
MYCLT    DC    CL3' '                                                           
PRMYDEM  DC    AL3(0)                                                           
MYRTGSRV DC    X'00'                                                            
PRRTGSRV DC    X'FF'                                                            
CLRCODE  DS    C                                                                
ACTSW    DC    X'00'                                                            
MKTCNT   DC    X'00'                                                            
MGR1CNT  DC    X'00'                                                            
MGR2CNT  DC    X'00'                                                            
MGR3CNT  DC    X'00'                                                            
MKTCNTP  DC    X'00'                                                            
PRDCNT   DC    X'00'                                                            
PGR3CNT  DC    X'00'                                                            
PGR2CNT  DC    X'00'                                                            
PGR1CNT  DC    X'00'                                                            
CPRDCNT  DC    X'00'                                                            
WDETAIL  DC    F'0'                                                             
MGR3WT   DC    F'0'                                                             
MGR2WT   DC    F'0'                                                             
MGR1WT   DC    F'0'                                                             
PRDWT    DC    F'0'                                                             
PGR3WT   DC    F'0'                                                             
PGR2WT   DC    F'0'                                                             
PGR1WT   DC    F'0'                                                             
CLTWT    DC    F'0'                                                             
PGR1CNTM DS    F                                                                
PGR2CNTM DS    F                                                                
PGR3CNTM DS    F                                                                
CLTCNTM  DS    F                                                                
TABCNT   DS    F                                                                
MGR1CNTM DS    F                                                                
MGR2CNTM DS    F                                                                
MGR3CNTM DS    F                                                                
ASLOT    DS    F                                                                
MYDMCB   DS    6F                                                               
MYMKT    DS    CL4                                                              
MYSVMKT  DS    H                                                                
SVDUPTOT DS    CL4                                                              
SVNOMKTS DS    CL4                                                              
SVWEIGHT DS    CL4                                                              
SAVMODE  DC    X'00'                                                            
FRSTTIM  DC    X'01'                                                            
DEMO     DC    X'00D301'                                                        
SLOT     DS    C                                                                
NXTSLT   DS    C                                                                
DEMLST   DS    CL56                                                             
         EJECT                                                                  
       ++INCLUDE DEDBLOCK                                                       
         DS    CL8                                                              
         EJECT                                                                  
MLST     DS    3600C                                                            
MWTAB    DS    6000C                                                            
MWTABD   DSECT                                                                  
MWMNUM   DS    CL2                                                              
MWMUNIV  DS    CL4                                                              
         DS    CL20                                                             
MWMEN    DS    0C                                                               
MWTABLN  EQU   MWMEN-MWMNUM                                                     
         PRINT OFF                                                              
       ++INCLUDE DEDEMEQUS                                                      
       ++INCLUDE DDCOMFACS                                                      
       ++INCLUDE DEDEMFILE                                                      
       ++INCLUDE SPREPWORKD                                                     
       ++INCLUDE SPGENMKT                                                       
       ++INCLUDE SPGENCLT                                                       
       ++INCLUDE SPREPMODES                                                     
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'055SPMEDADDWT03/13/17'                                      
         END                                                                    
