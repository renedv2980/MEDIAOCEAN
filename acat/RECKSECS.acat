*          DATA SET RECKSECS   AT LEVEL 097 AS OF 11/17/99                      
*CATALP RECKSECA                                                                
         TITLE 'RECKSEC' - REPPAK ACCESS SECURITY VALIDATION ROUTINE            
*                                                                               
* INPUT  PARAMETER 1 = A(SECURITY CONTROL BLOCK) (REGENSBLK)                    
*                                                                               
*        PARAMETER 2 = A(FLD HEADER FOR ERROR MESSAGE) (OPTIONAL)               
*                                                                               
*        PARAMETER 3 = A(TIOB) (OPTIONAL - FOR BEEP ON ERROR)                   
*                                                                               
*        ---------------------------------------------------------              
*                                                                               
*        PARAMETER 4 = A(RFBLOCK)                                               
*                                                                               
*        PARAMETER 5 = VREPFACS                                                 
*                                                                               
*        ---------------------------------------------------------              
*                                                                               
* OUTPUT          CC = EQUAL     - REQUEST VAILD                                
*                 CC = NOT EQUAL - REQUEST INVALID                              
*                                                                               
*        PARAMETER 1 = BYTE 0 ----- RETURN CODE ------ (ONLY ONE)               
*                             X'00' PAR REC READ, REQUEST VALID                 
*                             X'01' INVALID REQUEST ERROR TYPE 1                
*                             X'02' INVALID REQUEST ERROR TYPE 2                
*                             X'03' INVALID REQUEST ERROR TYPE 3                
*                             X'04' INVALID REQUEST ERROR TYPE 4                
*                             X'05' INVALID REQUEST ERROR TYPE 5                
*                             X'06' INVALID REQUEST ERROR TYPE 6                
*                             X'51' NO PAR RECORD ON FILE - VALID               
*                             X'52' NO PID NUMBER CONNECTED - VALID             
*                             X'60' SPCL CASE OWNER CODE - VALID                
*                                                                               
*        PARAMETER 2 = BYTE 0 ----- RETURN FLAGS ------ (MULTIPLE)              
*                             X'80' GROUP VALID ACCESS MATCHED                  
*                             X'40' OFFICE VALID ACCESS MATCHED                 
*                             X'20' STATION VALID ACCESS MATCHED                
*                             X'10' OWNER VALID ACCESS MATCHED                  
*                             X'08' MARKET VALID ACCESS MATCHED                 
*                                                                               
RECKSEC  CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 (WORKDX-WORKD),*CKSEC*,CLEAR=YES,RR=R5                           
         USING WORKD,RC                                                         
         ST    R5,RELO                                                          
         ST    RD,SAVERD                                                        
         ST    R1,SVPARM                                                        
         L     RA,0(R1)            A(SBLOCK)                                    
         USING SECBLK,RA                                                        
         L     RF,12(R1)           P4                                           
         MVC   RFBLOCK,0(RF)       COMFACS,AGENCY CODE                          
         MVC   VREPFACS,16(R1)                                                  
*                                                                               
         L     RE,COMFACS          RESOLVE ADDRESSES                            
         USING COMFACSD,RE                                                      
         MVC   GETFACT,CGETFACT                                                 
         MVC   HELLO,CHELLO                                                     
         DROP  RE                                                               
*                                                                               
         MVI   STATUS,0            INITIALIZE                                   
         MVI   VASTAT,0                                                         
         XC    ERRCODE,ERRCODE                                                  
         MVI   ERRTYPE,0                                                        
*                                                                               
         MVC   AGENCYCT,AGENCY     SAVE CONNECTED REP CODE                      
*                                                                               
         XC    KEY,KEY             READ REPREC FOR MASTER REP CODE              
         LA    R6,KEY                                                           
         USING RREPREC,R6                                                       
         MVI   RREPKTYP,X'01'                                                   
         MVC   RREPKREP,AGENCY                                                  
         GOTOX (RFGETREC,VREPFACS),DMCB,KEY,IOAREA,0,RFBLOCK                    
         BE    *+6                                                              
         DC    H'0'                                                             
         LA    R6,IOAREA                                                        
         CLC   RREPMAST,=X'FFFF'   MASTER?                                      
         BNE   *+12                                                             
         OI    STATUS,X'80'                                                     
         B     CKS030                                                           
         OC    RREPMAST,=X'4040'                                                
         CLC   RREPMAST,=X'4040'                                                
         BE    *+10                NO MASTER/SUBSID CONTROL                     
         MVC   AGENCY,RREPMAST     USE MASTER REP CODE TO READ PAR REC          
         DROP  R6                                                               
*                                                                               
CKS030   DS    0H                                                               
         GOTO1 GETFACT,DMCB,0      GET PID NUMBER                               
         L     RF,0(R1)                                                         
         MVC   PIDNUM,FAPASSWD-FACTSD(RF)                                       
*                                                                               
***>>    MVC   PIDNUM,=X'FFFF'    ****>>> FOR NOW <<<****                       
*                                                                               
         OC    PIDNUM,PIDNUM                                                    
         BNZ   CKS040                                                           
         L     R1,SVPARM                                                        
         MVI   0(R1),X'52'         NO PID NUMBER - SECURITY INACTIVE            
         MVI   4(R1),0                                                          
         B     EXITOK              RETURN CC:EQUAL                              
*                                                                               
CKS040   DS    0H                                                               
         BAS   RE,GETSEC           GET SECURITY RECORD                          
         BE    CKS100                                                           
         L     R1,SVPARM                                                        
         MVI   0(R1),X'51'         NO PAR REC FOUND                             
         MVI   4(R1),0                                                          
         B     EXITOK              RETURN CC:EQUAL                              
*                                                                               
CKS100   DS    0H                  MAIN ACCESS CHECK LOOP                       
         LA    R9,TYPES                                                         
         B     *+8                                                              
CKS110   DS    0H                                                               
         LA    R9,L'TYPES(R9)      NEXT TABLE ENTRY                             
         CLI   0(R9),X'FF'         END OF TABLE?                                
         BNE   CKS150              NO - PERFORM ACCESS CHECK                    
*                                                                               
         L     R1,SVPARM           DONE - *** RETURN REQUEST VALID ***          
         MVC   4(1,R1),VASTAT      RETURN VALID ACCESS FLAGS                    
*                                                                               
         TM    STATUS,XOWNFLG      SPECIAL CASE FLAG TRIPPED?                   
         BZ    CKS120              NO - REGULAR EXIT                            
         MVI   0(R1),X'60'         RETURN CODE = X'60'                          
         B     EXITOK              RETURN CC:EQUAL                              
*                                                                               
CKS120   DS    0H                                                               
         MVI   0(R1),0             RETURN FLAG = 0                              
         B     EXITOK              RETURN CC:EQUAL                              
*                                                                               
CKS150   DS    0H                  ACCESS CHECK                                 
*                                                                               
         ICM   RF,15,13(R9)        PRE-CHECK HOOK ROUTINE                       
         A     RF,RELO                                                          
         BASR  RE,RF                                                            
*                                                                               
         BAS   RE,CKACC            CHECK VALID ACCESS                           
*                                                                               
         BAS   RE,CKBRK            CHECK BREAKS VALID                           
*                                                                               
         BAS   RE,CKFLT            CHECK FILTERS VALID                          
*                                                                               
         ICM   RF,15,17(R9)        POST-CHECK HOOK ROUTINE                      
         A     RF,RELO                                                          
         BASR  RE,RF                                                            
*                                                                               
         B     CKS110              NEXT TABLE ENTRY                             
**********************************************************************          
TYPES    DS    0CL(TYPE2-TYPE1)                                                 
*                                                                               
* BYTES                                                                         
*  1-7    TEXT OF PAR LEVEL NAME                                                
*   8     SUBSIDIARY SECURITY ELEMENT ELCODE                                    
*   9     MASTER SECURITY ELEMENT ELCODE                                        
* 10-11   DISPLACEMENT OF FILTER IN SBLOCK DSECT                                
*   12    FLAG VALUE IN SBREAKS                                                 
*   13    FLAG VALUE IN SBBRK#4                                                 
* 14-17   A(PRE-CHECK HOOK ROUTINE)                                             
* 18-21   A(POST-CHECK HOOK ROUTINE)                                            
* 22-23   SET TYPE OR NULLS                                                     
*   24    RSECREC SUB-ELEM CODE FOR DETAIL LEVEL                                
*   25    LEVEL VALUE FOR VASTAT                                                
*   26    FILTER INHERITANCE FLAGS                                              
*                X'80' FILTER CAN BE INHERITED FROM STATION                     
**********************************************************************          
TYPE1    DC    CL7'Group',X'1011',AL2(SBGROUP-SBLOCK),X'8080'                   
         DC    AL4(GRP1),AL4(GRP2),C'GS',X'02',X'80',X'80'                      
*                                                                               
TYPE2    DC    CL7'Owner',X'3031',AL2(SBOWNER-SBLOCK),X'1010'                   
         DC    AL4(NOHOOK),AL4(NOHOOK),X'0000',X'06',X'10',X'80'                
*                                                                               
         DC    CL7'Market',X'5051',AL2(SBMARKET-SBLOCK),X'0808'                 
         DC    AL4(NOHOOK),AL4(NOHOOK),X'0000',X'04',X'08',X'80'                
*                                                                               
         DC    CL7'Station',X'4041',AL2(SBSTATN-SBLOCK),X'2020'                 
         DC    AL4(NOHOOK),AL4(NOHOOK),C'ST',X'03',X'20',X'00'                  
*                                                                               
         DC    CL7'Office',X'2021',AL2(SBOFFICE-SBLOCK),X'4040'                 
         DC    AL4(NOHOOK),AL4(NOHOOK),C'OF',X'05',X'40',X'80'                  
*                                                                               
         DC    X'FF'                                                            
**********************************************************************          
*                                                                               
**********************************************************************          
GRP1     NTR1                                                                   
         B     EXITOK                                                           
**********************************************************************          
*                                                                               
**********************************************************************          
GRP2     NTR1                                                                   
         B     EXITOK                                                           
**********************************************************************          
* CKACC - CHECK VALID ACCESS ROUTINE                                            
**********************************************************************          
CKACC    NTR1                                                                   
         MVC   ELCODE,7(R9)        SUBSID ELCODE                                
         TM    STATUS,MASTERQ                                                   
         BZ    *+10                                                             
         MVC   ELCODE,8(R9)        MASTER ELCODE                                
         GOTO1 HELLO,DMCB,(C'G',=C'REPFIL'),(ELCODE,SECREC),(1,=X'1'),0         
         CLI   12(R1),0            GOT IT?                                      
         BNE   EXITOK              NO ACCESS RESTRICTIONS                       
         ZICM  R6,13(R1),3         ELEMENT                                      
         BAS   RE,GETCODES         BUILD TABLE OF PAR CODES                     
*                                                                               
         LA    R2,BLKCODES         BLKCODES                                     
         ZICM  R3,=AL2(CODESLNQ),2                                              
         SR    R4,R4                                                            
         SR    R5,R5                                                            
         MVCL  R2,R4               CLEAR BLKCODES                               
         LA    R2,BLKCODES         RESET R2                                     
         XC    SETS,SETS                                                        
*                                                                               
         ZICM  R3,9(R9),2          DISP OF FILTER IN BLOCK                      
         LA    R3,SBLOCK(R3)       A(FILTER)                                    
         OC    0(6,R3),=6C' '      SPACE PAD FILTER                             
         CLC   0(6,R3),=6C' '      ANY FILTER?                                  
         BNE   CKACC030            YES - CONTINUE CHECKING                      
*                                                                               
         CLI   25(R9),0            CAN CODE BE INHERITED?                       
         BE    CKACC020            NO                                           
         BAS   RE,INHERIT                                                       
         BE    CKACC030            GOT CODE                                     
*                                                                               
CKACC020 DS    0H                                                               
         MVI   ERRTYPE,1                                                        
         BAS   RE,ERROR                                                         
*                                                                               
CKACC030 DS    0H                                                               
         OC    21(2,R9),21(R9)     SETS WITH THIS CODE TYPE?                    
         BZ    CKACC050            NO                                           
         CLI   0(R3),C'*'          SET?                                         
         BNE   CKACC050            NO                                           
*                                                                               
         MVC   SETS(4),1(R3)       PUT SET IN BEGINNING OF LIST                 
         LA    R4,SETS             START OF LIST                                
CKACC040 DS    0H                                                               
         OC    0(4,R4),0(R4)       DONE WITH SET LIST?                          
         BZ    CKACC100            YES                                          
         BAS   RE,EXPSET           NO - EXPLODE SET                             
         LA    R4,4(R4)            NEXT SET                                     
         B     CKACC040                                                         
*                                                                               
CKACC050 DS    0H                                                               
         MVC   0(5,R2),0(R3)       FILTER INTO BLK CODES                        
         OC    0(5,R2),=5C' '      SPACE PAD CODE                               
*                                                                               
CKACC100 DS    0H                  NOW CHECK ALL BLKCODES IN PARCODES           
         LA    R2,BLKCODES         R2=BLKCODES                                  
         ZICM  R3,=AL2(CODESLNQ),2                                              
         AR    R3,R2               PLUS LEN, R3=PARCODES                        
CKACC110 DS    0H                                                               
         OC    0(5,R2),0(R2)       END OF BLK CODES?                            
         BZ    EXITOK              YES - DONE                                   
         BAS   RE,INLIST                                                        
         BE    CKACC120                                                         
         MVI   ERRTYPE,2                                                        
         MVC   ERRCODE(5),0(R2)    REPORT FAILED CODE TO ERROR ROUTINE          
         BAS   RE,ERROR                                                         
CKACC120 DS    0H                                                               
         OC    VASTAT,24(R9)       VALID ACCESS MATCH FOR THIS LEVEL            
         LA    R2,5(R2)            NEXT CODE IN BLK CODES                       
         B     CKACC110                                                         
**********************************************************************          
* CKBRK - CHECK BREAKS VALID ROUTINE                                            
**********************************************************************          
CKBRK    NTR1                                                                   
         CLI   SBBREAKS,0          DOES REPORT BREAK?                           
         BE    EXITOK              NO - VALID                                   
*                                                                               
         LA    R8,TYPES            TYPES TABLE (AGAIN)                          
CKBRK020 DS    0H                                                               
         CLI   0(R8),X'FF'         END OF TYPES?                                
         BE    EXITOK              YES - VALID                                  
         MVC   BYTE,SBBREAKS                                                    
         NC    BYTE,11(R8)         BREAK AT CURRENT BREAK LEVEL?                
         BZ    CKBRK200            NO - NEXT BREAK LEVEL                        
*                                                                               
         MVC   BYTE,SBBRK#4                                                     
         NC    BYTE,12(R8)         BYPASS CHECK AT CURRENT BREAK LEVEL?         
         BNZ   CKBRK200            YES - NEXT BREAK LEVEL                       
*                                                                               
         MVC   ELCODE,7(R9)        SUBSID ELCODE                                
         TM    STATUS,MASTERQ                                                   
         BZ    *+10                                                             
         MVC   ELCODE,8(R9)        MASTER ELCODE                                
         MVC   BYTE,23(R8)         SUB ELEM CODE FOR THIS BREAK LEVEL           
         GOTO1 HELLO,DMCB,(C'G',=C'REPFIL'),(ELCODE,SECREC),(1,BYTE),0          
         CLI   12(R1),0            GOT IT?                                      
         BNE   CKBRK200            NO BREAK RESTRICTIONS AT THIS LEVEL          
         ZICM  R6,13(R1),3         ELEMENT                                      
         BAS   RE,GETCODES         BUILD TABLE OF PAR CODES                     
*                                                                               
         LA    R2,BLKCODES         BLKCODES                                     
         ZICM  R3,=AL2(CODESLNQ),2                                              
         SR    R4,R4                                                            
         SR    R5,R5                                                            
         MVCL  R2,R4               CLEAR BLKCODES                               
         LA    R2,BLKCODES         RESET R2                                     
         XC    SETS,SETS                                                        
*                                                                               
         ZICM  R3,9(R9),2          DISP OF FILTER IN BLOCK                      
         LA    R3,SBLOCK(R3)       A(FILTER)                                    
         OC    0(6,R3),=6C' '      SPACE PAD FILTER                             
         CLC   0(6,R3),=6C' '      ANY FILTER?                                  
         BNE   CKBRK030            YES - CONTINUE                               
*                                                                               
         CLI   25(R9),0            CAN CODE BE INHERITED?                       
         BE    CKBRK025            NO                                           
         BAS   RE,INHERIT                                                       
         BE    CKBRK030            GOT CODE                                     
*                                                                               
CKBRK025 DS    0H                                                               
         MVI   ERRTYPE,3                                                        
         BAS   RE,ERROR                                                         
*                                                                               
CKBRK030 DS    0H                                                               
         OC    21(2,R9),21(R9)     SETS WITH THIS CODE TYPE?                    
         BZ    CKBRK050            NO                                           
         CLI   0(R3),C'*'          SET?                                         
         BNE   CKBRK050            NO                                           
*                                                                               
         MVC   SETS(4),1(R3)       PUT SET IN BEGINNING OF LIST                 
         LA    R4,SETS             START OF LIST                                
CKBRK040 DS    0H                                                               
         OC    0(4,R4),0(R4)       DONE WITH SET LIST?                          
         BZ    CKBRK100            YES                                          
         BAS   RE,EXPSET           NO - EXPLODE SET                             
         LA    R4,4(R4)            NEXT SET                                     
         B     CKBRK040                                                         
*                                                                               
CKBRK050 DS    0H                                                               
         MVC   0(5,R2),0(R3)       FILTER INTO BLK CODES                        
         OC    0(5,R2),=5C' '      SPACE PAD CODE                               
*                                                                               
CKBRK100 DS    0H                  NOW CHECK ALL BLKCODES IN PARCODES           
         LA    R2,BLKCODES         R2=BLKCODES                                  
         ZICM  R3,=AL2(CODESLNQ),2                                              
         AR    R3,R2               PLUS LEN, R3=PARCODES                        
CKBRK110 DS    0H                                                               
         OC    0(5,R2),0(R2)       END OF BLK CODES?                            
         BZ    EXITOK              YES - DONE                                   
         BAS   RE,INLIST                                                        
         BNE   CKBRK120                                                         
         MVI   ERRTYPE,4                                                        
         MVC   ERRCODE(5),0(R2)    REPORT FAILED CODE TO ERROR ROUTINE          
         BAS   RE,ERROR                                                         
CKBRK120 DS    0H                                                               
         LA    R2,5(R2)            NEXT CODE IN BLK CODES                       
         B     CKBRK110                                                         
CKBRK200 DS    0H                                                               
         LA    R8,L'TYPES(R8)      NEXT BREAK LEVEL                             
         B     CKBRK020                                                         
**********************************************************************          
* CKFLT - CHECK FILTERS VALID ROUTINE                                           
**********************************************************************          
CKFLT    NTR1                                                                   
         LA    R8,TYPES            TYPES TABLE (AGAIN)                          
CKFLT020 DS    0H                                                               
         CLI   0(R8),X'FF'         END OF TYPES?                                
         BE    EXITOK              YES - VALID                                  
*                                                                               
         ZICM  R3,9(R8),2          DISP OF FILTER IN BLOCK                      
         LA    R3,SBLOCK(R3)       A(FILTER)                                    
         OC    0(6,R3),=6C' '      SPACE PAD FILTER                             
         CLC   0(6,R3),=6C' '      ANY FILTER AT THIS LEVEL?                    
         BE    CKFLT200            NO - NEXT FILTER LEVEL                       
*                                                                               
         MVC   BYTE,SBBRK#4                                                     
         NC    BYTE,12(R8)         BYPASS CHECK AT CURRENT BREAK LEVEL?         
         BNZ   CKFLT200            YES - NEXT BREAK LEVEL                       
*                                                                               
         MVC   ELCODE,7(R9)        SUBSID ELCODE                                
         TM    STATUS,MASTERQ                                                   
         BZ    *+10                                                             
         MVC   ELCODE,8(R9)        MASTER ELCODE                                
         MVC   BYTE,23(R8)         SUB ELEM CODE FOR THIS BREAK LEVEL           
         GOTO1 HELLO,DMCB,(C'G',=C'REPFIL'),(ELCODE,SECREC),(1,BYTE),0          
         CLI   12(R1),0            GOT IT?                                      
         BNE   CKFLT200            NO BREAK RESTRICTIONS AT THIS LEVEL          
         ZICM  R6,13(R1),3         ELEMENT                                      
         BAS   RE,GETCODES         BUILD TABLE OF PAR CODES                     
*                                                                               
         LA    R2,BLKCODES         BLKCODES                                     
         ZICM  R3,=AL2(CODESLNQ),2                                              
         SR    R4,R4                                                            
         SR    R5,R5                                                            
         MVCL  R2,R4               CLEAR BLKCODES                               
         LA    R2,BLKCODES         RESET R2                                     
         XC    SETS,SETS                                                        
*                                                                               
         ZICM  R3,9(R9),2          DISP OF FILTER IN BLOCK                      
         LA    R3,SBLOCK(R3)       A(FILTER)                                    
         OC    0(6,R3),=6C' '      SPACE PAD FILTER                             
         CLC   0(6,R3),=6C' '      ANY FILTER?                                  
         BNE   CKFLT030            OK - CONTINUE                                
*                                                                               
         CLI   25(R9),0            CAN CODE BE INHERITED?                       
         BE    CKFLT025            NO                                           
         BAS   RE,INHERIT                                                       
         BE    CKFLT030            GOT CODE                                     
*                                                                               
CKFLT025 DS    0H                                                               
         MVI   ERRTYPE,5                                                        
         BAS   RE,ERROR                                                         
*                                                                               
CKFLT030 DS    0H                                                               
         OC    21(2,R9),21(R9)     SETS WITH THIS CODE TYPE?                    
         BZ    CKFLT050            NO                                           
         CLI   0(R3),C'*'          SET?                                         
         BNE   CKFLT050            NO                                           
*                                                                               
         MVC   SETS(4),1(R3)       PUT SET IN BEGINNING OF LIST                 
         LA    R4,SETS             START OF LIST                                
CKFLT040 DS    0H                                                               
         OC    0(4,R4),0(R4)       DONE WITH SET LIST?                          
         BZ    CKFLT100            YES                                          
         BAS   RE,EXPSET           NO - EXPLODE SET                             
         LA    R4,4(R4)            NEXT SET                                     
         B     CKFLT040                                                         
*                                                                               
CKFLT050 DS    0H                                                               
         MVC   0(5,R2),0(R3)       FILTER INTO BLK CODES                        
         OC    0(5,R2),=5C' '      SPACE PAD CODE                               
*                                                                               
CKFLT100 DS    0H                  NOW CHECK ALL BLKCODES IN PARCODES           
         LA    R2,BLKCODES         R2=BLKCODES                                  
         ZICM  R3,=AL2(CODESLNQ),2                                              
         AR    R3,R2               PLUS LEN, R3=PARCODES                        
CKFLT110 DS    0H                                                               
         OC    0(5,R2),0(R2)       END OF BLK CODES?                            
         BZ    EXITOK              YES - DONE                                   
         BAS   RE,INLIST                                                        
         BNE   CKFLT120                                                         
         MVI   ERRTYPE,6                                                        
         MVC   ERRCODE(5),0(R2)    REPORT FAILED CODE TO ERROR ROUTINE          
         BAS   RE,ERROR                                                         
CKFLT120 DS    0H                                                               
         LA    R2,5(R2)            NEXT CODE IN BLK CODES                       
         B     CKFLT110                                                         
CKFLT200 DS    0H                                                               
         LA    R8,L'TYPES(R8)      NEXT BREAK LEVEL                             
         B     CKFLT020                                                         
**********************************************************************          
* ERROR - GENERATES ERROR MSG, INTERRUPTS MODULE EXECUTION & EXITS              
*          ERRTYPE: CONTAINS NUMBER OF ERROR                                    
*          ERRCODE: CONTAINS CODE ACCESS CHECK FAILED ON IF APPLICABLE          
**********************************************************************          
ERROR    NTR1                                                                   
         L     R1,SVPARM                                                        
         ICM   R2,15,4(R1)         A(MSG FIELD HEADER)                          
         BZ    ERROR850            NO MSG FIELD                                 
         ZIC   R3,0(R2)                                                         
         SH    R3,=H'8'            MINUS HEADER                                 
         TM    1(R2),X'02'         EXT?                                         
         BZ    *+8                                                              
         SH    R3,=H'8'            MINUS EXT                                    
*                                                                               
         BCTR  R3,0                                                             
         EX    R3,*+8                                                           
         B     *+10                                                             
         XC    8(0,R2),8(R2)       CLEAR MSG FIELD                              
         LA    R3,1(R2,R3)         END OF FIELD+1                               
         OI    1(R2),X'08'         HIGH INTENSITY                               
         OI    6(R2),X'80'         XMIT                                         
         LA    R2,8(R2)            FIRST TEXT SPOT IN FIELD                     
*                                                                               
         MVC   0(15,R2),=C'Access denied: '                                     
         LA    R2,15(R2)                                                        
*                                                                               
* ERRTYPE:1 - ACCESS RESTRICTED ON PAR, MISSING FILTER IN BLOCK                 
*                                                                               
         CLI   ERRTYPE,1                                                        
         BNE   ERROR050                                                         
         MVC   0(15,R2),=C'Must filter by '                                     
         LA    R2,15(R2)                                                        
         LA    RF,0(R9)            PAR LEVEL NAME FROM TABLE                    
         MVI   BYTE,7                                                           
         BAS   RE,ADDWORD                                                       
         ZIC   RF,BYTE                                                          
         AR    R2,RF                                                            
         B     ERROR800                                                         
*                                                                               
* ERRTYPE:2 - ACCESS RESTRICTED ON PAR, INVALID FILTER IN BLOCK                 
*                                                                               
ERROR050 DS    0H                                                               
         CLI   ERRTYPE,2                                                        
         BNE   ERROR100                                                         
         MVC   0(20,R2),=C'No valid access for '                                
         LA    R2,20(R2)                                                        
*                                                                               
         LA    RF,0(R9)            PAR LEVEL NAME FROM TABLE                    
         MVI   BYTE,7                                                           
         BAS   RE,ADDWORD                                                       
*                                                                               
         LA    R2,1(R2)                                                         
*                                                                               
         LA    RF,ERRCODE          CODE FAILED ON                               
         MVI   BYTE,5                                                           
         BAS   RE,ADDWORD                                                       
         B     ERROR800                                                         
*                                                                               
* ERRTYPE:3 - BREAKS IN BLOCK, DETAIL RESTRICTED IN PAR, MISSING FLTER          
*                                                                               
ERROR100 DS    0H                                                               
         CLI   ERRTYPE,3                                                        
         BNE   ERROR150                                                         
         MVC   0(15,R2),=C'Must filter by '                                     
         LA    R2,15(R2)                                                        
*                                                                               
         LA    RF,0(R9)            PAR LEVEL NAME FROM TABLE                    
         MVI   BYTE,7                                                           
         BAS   RE,ADDWORD                                                       
         B     ERROR800                                                         
*                                                                               
* ERRTYPE:4 - BREAKS IN BLOCK, DETAIL RESTRICTED IN PAR, INVALID FLTER          
*                                                                               
ERROR150 DS    0H                                                               
         CLI   ERRTYPE,4                                                        
         BNE   ERROR200                                                         
         MVC   0(3,R2),=C'No '                                                  
         LA    R2,3(R2)                                                         
*                                                                               
         LA    RF,0(R8)            PAR LEVEL NAME FROM TABLE                    
         MVI   BYTE,7                                                           
         BAS   RE,ADDWORD                                                       
*                                                                               
         MVC   0(12,R2),=C' detail for '                                        
         LA    R2,12(R2)                                                        
*                                                                               
         LA    RF,0(R9)            PAR LEVEL NAME FROM TABLE                    
         MVI   BYTE,7                                                           
         BAS   RE,ADDWORD                                                       
*                                                                               
         LA    R2,1(R2)                                                         
         LA    RF,ERRCODE          CODE FAILED ON                               
         MVI   BYTE,5                                                           
         BAS   RE,ADDWORD                                                       
         B     ERROR800                                                         
*                                                                               
* ERRTYPE:5 - FILTER IN BLOCK, DETAIL RESTRICTED IN PAR, MISSING FLTER          
*                                                                               
ERROR200 DS    0H                                                               
         CLI   ERRTYPE,5                                                        
         BNE   ERROR250                                                         
         MVC   0(15,R2),=C'Must filter by '                                     
         LA    R2,15(R2)                                                        
*                                                                               
         LA    RF,0(R9)            PAR LEVEL NAME FROM TABLE                    
         MVI   BYTE,7                                                           
         BAS   RE,ADDWORD                                                       
         B     ERROR800                                                         
*                                                                               
* ERRTYPE:6 - FILTER IN BLOCK, DETAIL RESTRICTED IN PAR, INVALID FLTER          
*                                                                               
ERROR250 DS    0H                                                               
         CLI   ERRTYPE,6                                                        
         BNE   ERROR300                                                         
         MVC   0(3,R2),=C'No '                                                  
         LA    R2,3(R2)                                                         
*                                                                               
         LA    RF,0(R8)            PAR LEVEL NAME FROM TABLE                    
         MVI   BYTE,7                                                           
         BAS   RE,ADDWORD                                                       
*                                                                               
         MVC   0(19,R2),=C'-level filters for '                                 
         LA    R2,19(R2)                                                        
*                                                                               
         LA    RF,0(R9)            PAR LEVEL NAME FROM TABLE                    
         MVI   BYTE,7                                                           
         BAS   RE,ADDWORD                                                       
*                                                                               
         LA    R2,1(R2)                                                         
         LA    RF,ERRCODE          CODE FAILED ON                               
         MVI   BYTE,5                                                           
         BAS   RE,ADDWORD                                                       
         B     ERROR800                                                         
*                                                                               
ERROR300 DS    0H                                                               
         MVC   0(5,R2),=C'*****'                                                
         LA    R2,5(R2)                                                         
*                                                                               
ERROR800 DS    0H                                                               
         L     R1,SVPARM                                                        
         L     RF,4(R1)            A(MSG FIELD HEADER)                          
         SR    R2,RF               LEN OF MSG                                   
         STC   R2,7(RF)                                                         
*                                                                               
         ICM   R3,15,8(R1)         ATIOB                                        
         BZ    ERROR850                                                         
         USING TIOBD,R3                                                         
         OI    TIOBINDS,TIOBALRM   BEEP                                         
         DROP  R3                                                               
*                                                                               
ERROR850 DS    0H                                                               
         L     R1,SVPARM                                                        
         MVC   0(1,R1),ERRTYPE     PASS ERROR CODE TO APPLICATION               
         MVC   4(1,R1),VASTAT      RETURN VALID ACCESS FLAGS                    
         L     RD,SAVERD           GET ALL THE WAY OUT                          
         B     EXITL               EXIT CC NOT EQUAL                            
*                                                                               
* ADDWORD - APPENDS WORD TO MESSAGE TEXT                                        
*                                                                               
*   BEFORE: RF=A(WORD TO APPEND)                                                
*           R2=A(NEXT SPACE IN MESSAGE TEXT)                                    
*           BYTE=MAX LENGTH OF WORD (SPACE OR NULL PADDED)                      
*                                                                               
*    AFTER: R2=A(NEXT AVAILABLE SPACE IN MSG)                                   
*           BYTE=ACTUAL LENGTH OF WORD APPENDED                                 
*                                                                               
*     NOTE: USES R1                                                             
*                                                                               
ADDWORD  DS    0H                                                               
         ZIC   R1,BYTE                                                          
         AR    R1,RF                                                            
ADDW010  DS    0H                                                               
         BCTR  R1,0                                                             
         CR    R1,RF                                                            
         BL    ADDW050                                                          
         CLI   0(R1),0                                                          
         BE    ADDW010                                                          
         CLI   0(R1),C' '                                                       
         BE    ADDW010                                                          
*                                                                               
         SR    R1,RF               LEN OF WORD-1                                
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R2),0(RF)                                                    
         LA    R1,1(R1)                                                         
         AR    R2,R1               BUMP INSERTION POINT                         
         STC   R1,BYTE                                                          
         BR    RE                                                               
ADDW050  DS    0H                  NO WORD                                      
         MVI   BYTE,0                                                           
         BR    RE                                                               
**********************************************************************          
* INLIST - CHECK FOR CODE IN LIST                                               
*          R2 = A(5 CHAR CODE TO CHECK FOR IN LIST)                             
*          R3 = A(CODE LIST TO CHECK AGAINST)                                   
*                                                                               
*          RETURNS: CC EQUAL: CODE FOUND IN TABLE                               
*                   CC NOT EQUAL: CODE NOT FOUND IN TABLE                       
**********************************************************************          
INLIST   NTR1                                                                   
INLIST10 DS    0H                                                               
         OC    0(5,R3),0(R3)                                                    
         BZ    EXITL               CODE NOT FOUND                               
         CLC   0(5,R2),0(R3)       COMPARE CODES                                
         BE    EXITOK              MATCH                                        
         CLI   4(R3),C'+'          SPECIAL CASE CODE IN LIST?                   
         BNE   INLIST20            NO SKIP RECHECK                              
         CLC   0(4,R2),0(R3)       CHECK 1ST 4 CHARACTERS FOR MATCH             
         BNE   INLIST20                                                         
         OI    STATUS,XOWNFLG      SET SPECIAL CASE FLAG                        
         B     EXITOK                                                           
INLIST20 DS    0H                                                               
         LA    R3,5(R3)            NEXT CODE                                    
         B     INLIST10                                                         
**********************************************************************          
* GETCODES - BUILD CODE LIST FROM PAR RECORD ELEMENT IN 'PARCODES'              
*            R6 ADDRESSES CURRENT PAR SECURITY ELEMENT                          
**********************************************************************          
GETCODES NTR1                                                                   
         LA    R2,BLKCODES         BLKCODES                                     
         ZICM  R3,=AL2(CODESLNQ),2                                              
         AR    R2,R3               PLUS LEN = PARCODES                          
         SR    R4,R4                                                            
         SR    R5,R5                                                            
         MVCL  R2,R4               CLEAR PARCODES                               
         LA    R2,BLKCODES         BLKCODES                                     
         ZICM  R3,=AL2(CODESLNQ),2                                              
         AR    R2,R3               PLUS LEN = PARCODES                          
*                                                                               
         XC    SETS,SETS           CLEAR SET BUFFER                             
         LA    R3,SETS             BEGINNING OF SET BUFFER                      
*                                                                               
         ZIC   R5,1(R6)            ELEM LEN                                     
         AR    R5,R6               END OF ELEM + 1                              
         LA    R6,RSECSCCL-RSECSCEL(R6)  1ST MINI-ELEM                          
GCODE050 DS    0H                                                               
         CR    R6,R5                                                            
         BNL   GCODE200            DONE WITH ELEMENT                            
*                                                                               
         OC    21(2,R9),21(R9)     SETS WITH THIS CODE TYPE?                    
         BZ    GCODE100            NO                                           
         CLI   1(R6),C'*'          SET?                                         
         BNE   GCODE100            NO                                           
*                                                                               
         ZIC   R1,0(R6)            SET LEN W/*                                  
         BCTR  R1,0                FOR *                                        
         BCTR  R1,0                FOR EX                                       
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R3),2(R6)       PUT SET IN SET LIST                          
         LA    R3,4(R3)            NEXT SPOT IN SET LIST                        
         LA    R1,2(R1)            UNDO FROM BEFORE                             
         LA    R6,1(R1,R6)         NEXT MINI-ELEM                               
         B     GCODE050                                                         
*                                                                               
GCODE100 DS    0H                                                               
         ZIC   R1,0(R6)            CODE LEN                                     
         BCTR  R1,0                FOR EX                                       
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R2),1(R6)       PUT CODE IN CODE LIST                        
         OC    0(5,R2),=5C' '      SPACE PAD CODE                               
         LA    RF,0(R1,R2)         LAST CHAR IN CODE                            
         CLI   0(RF),C'+'          SPECIAL CASE?                                
         BNE   GCODE110            NO                                           
         MVI   0(RF),C' '          REMOVE '+'                                   
         MVI   4(R2),C'+'          PUT IT IN LAST SPOT                          
GCODE110 DS    0H                                                               
         LA    R2,5(R2)            NEXT SPOT IN CODE LIST                       
         LA    R1,1(R1)            UNDO BCTR                                    
         LA    R6,1(R1,R6)         NEXT MINI-ELEM                               
         B     GCODE050                                                         
*                                                                               
GCODE200 DS    0H                  NOW EXPAND SETS INTO PARCODES LIST           
         LA    R2,BLKCODES         BLKCODES                                     
         ZICM  R3,=AL2(CODESLNQ),2                                              
         AR    R2,R3               PLUS LEN = PARCODES                          
         LA    R4,SETS                                                          
GCODE210 DS    0H                                                               
         OC    0(4,R4),0(R4)                                                    
         BZ    GCODE250            DONE WITH SETS                               
         BAS   RE,EXPSET                                                        
         LA    R4,4(R4)            NEXT SET                                     
         B     GCODE210                                                         
GCODE250 DS    0H                                                               
         B     EXIT                                                             
**********************************************************************          
* EXPSET - EXPAND SET                                                           
*          R4 = SET IN SET LIST TO BE EXPANDED INTO CODE LIST                   
*          R2 = CODE LIST TO EXPAND CODES INTO                                  
**********************************************************************          
EXPSET   NTR1                                                                   
         XC    KEY,KEY                                                          
         LA    R6,KEY                                                           
         USING RSETREC,R6                                                       
         MVI   RSETKTYP,RSETKTYQ                                                
         MVC   RSETKREP,AGENCY     MASTER REP CODE                              
         MVC   RSETKSET,21(R9)     SET TYPE FROM TABLE                          
         MVC   RSETKID,0(R4)       SET IDENTIFIER FROM SET LIST                 
         GOTOX (RFGETREC,VREPFACS),DMCB,KEY,IOAREA,0,RFBLOCK                    
         BNE   EXITL               MISSING REC - GET OUT FOR NOW                
         DROP  R6                                                               
         LA    R6,IOAREA                                                        
         MVI   ELCODE,1                                                         
         BAS   RE,GETEL                                                         
         BNE   EXPSET30                                                         
         USING RSET1DES,R6                                                      
         TM    RSET1FLG,X'08'      EXCLUSION SET?                               
         BO    EXITL               WE DON'T SUPPORT THESE                       
         TM    RSET1FLG,X'80'      SET OF SETS?                                 
         BZ    EXPSET30            NO                                           
         DROP  R6                                                               
*                                                                               
         LA    R3,4                FOR SET LIST, INCREMENT BY 4                 
         LA    R2,SETS             FIND NEXT OPEN SPOT IN SET LIST              
         OC    0(4,R2),0(R2)                                                    
         BZ    EXPSET40                                                         
         AR    R2,R3                                                            
         B     *-12                                                             
*                                                                               
EXPSET30 DS    0H                                                               
         LA    R3,5                FOR CODE LIST, INCREMENT BY 5                
         OC    0(5,R2),0(R2)       FIND NEXT SPOT IN CODE LIST                  
         BZ    EXPSET40                                                         
         AR    R2,R3                                                            
         B     *-12                                                             
*                                                                               
EXPSET40 DS    0H                                                               
         LA    R6,IOAREA                                                        
         MVI   ELCODE,X'20'                                                     
         BAS   RE,GETEL                                                         
         BNE   EXITL                                                            
         USING RSETMEMD,R6                                                      
         ZIC   R4,RSETMLEN         MEMBER LENGTH                                
         BCTR  R4,0                FOR EX LATER                                 
         ZIC   R5,1(R6)                                                         
         AR    R5,R6               END OF ELEM+1                                
         LA    R6,RSETMEMB         FIRST MEMBER                                 
         DROP  R6                                                               
EXPSET50 DS    0H                                                               
         CR    R6,R5                                                            
         BNL   EXITOK              DONE                                         
         EX    R4,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R2),0(R6)       MOVE CODE/SET TO CODE/SET LIST               
         BCTR  R3,0                FOR EX                                       
         EX    R3,*+8                                                           
         B     *+10                                                             
         OC    0(0,R2),=5C' '      SPACE PAD ENTRY                              
         LA    R3,1(R3)            UNDO BCTR                                    
         AR    R2,R3               NEXT SPOT IN CODE/SET LIST                   
         LA    R6,1(R4,R6)         NEXT MEMBER                                  
         B     EXPSET50                                                         
**********************************************************************          
* GETSEC - ROUTINE READS REP ACCESS SECURITY RECORD                             
**********************************************************************          
GETSEC   NTR1                                                                   
         XC    KEY,KEY                                                          
         LA    R6,KEY                                                           
         USING RSECREC,R6                                                       
         MVC   RSECKTYP(2),=X'1501'                                             
         MVC   RSECKREP,AGENCY                                                  
         MVC   RSECKPID,PIDNUM                                                  
         DROP  R6                                                               
         GOTOX (RFGETREC,VREPFACS),DMCB,KEY,RSECREC,0,RFBLOCK                   
         B     EXIT                                                             
**********************************************************************          
* INHERIT - ROUTINE ATTEMPTS TO INHERIT FILTER FROM OTHER FILTERS               
**********************************************************************          
INHERIT  NTR1                                                                   
         TM    25(R9),X'80'        INHERIT FROM STATION FILTER?                 
         BO    *+6                                                              
         DC    H'0'                NO OTHER INHERITANCE POSSIBILITIES           
         OC    SBSTATN,SPACES      SPACE PAD STATION FILTER                     
         CLC   SBSTATN,SPACES      DO WE HAVE A STATION FILTER                  
         BE    EXITL               NO - NOTHING TO INHERIT                      
*                                                                               
         CLI   SBSTATN,C'*'        SET                                          
         BE    EXITL               NO INHERITANCE FROM SETS                     
*                                                                               
         XC    KEY,KEY                                                          
         LA    R6,KEY                                                           
         USING RSTAREC,R6                                                       
         MVI   RSTAKTYP,X'02'                                                   
         MVC   RSTAKREP,AGENCYCT   USE SUBSIDIARY REP CODE FOR STA REC          
         MVC   RSTAKSTA,SBSTATN                                                 
         GOTOX (RFGETREC,VREPFACS),DMCB,KEY,IOAREA,0,RFBLOCK                    
         BE    *+6                                                              
         DC    H'0'                MISSING STATION RECORD                       
*                                                                               
         LA    R6,IOAREA                                                        
         MVI   ELCODE,X'51'                                                     
         BAS   RE,GETEL            MASTER STATION REC?                          
         BNE   INH050              NO - PROCEED                                 
         MVC   KEY(27),IOAREA      BUILD SUBSID STA REC KEY                     
         MVC   KEY+RSTAKREP-RSTAKEY(2),2(R6)                                    
         GOTOX (RFGETREC,VREPFACS),DMCB,KEY,IOAREA,0,RFBLOCK                    
         BE    *+6                                                              
         DC    H'0'                MISSING STATION RECORD                       
*                                                                               
INH050   DS    0H                                                               
         LA    R6,IOAREA                                                        
         CLI   RSTAELEM,X'01'      HAVE 01 ELEM?                                
         BNE   EXITL               CAN'T INHERIT ANYTHING                       
*                                                                               
         CLI   7(R9),X'10'         INHERIT GROUP?                               
         BNE   INH100                                                           
         MVC   SBGROUP(L'RSTAGRUP),RSTAGRUP                                     
         CLC   SBGROUP,SPACES                                                   
         BE    EXITL                                                            
         B     EXITOK                                                           
*                                                                               
INH100   DS    0H                                                               
         CLI   7(R9),X'30'         INHERIT OWNER?                               
         BNE   INH150                                                           
         MVC   SBOWNER(L'RSTAOWN),RSTAOWN                                       
         CLC   SBOWNER,SPACES                                                   
         BE    EXITL                                                            
         B     EXITOK                                                           
         DROP  R6                                                               
*                                                                               
INH150   DS    0H                                                               
         CLI   7(R9),X'50'         INHERIT MARKET                               
         BNE   INH200                                                           
         MVI   ELCODE,X'08'                                                     
         BAS   RE,GETEL                                                         
         BNE   EXITL                                                            
         USING RSTAXXEL,R6                                                      
         MVC   SBMARKET(L'RSTAMKTC),RSTAMKTC                                    
         CLC   SBMARKET,SPACES                                                  
         BE    EXITL                                                            
         B     EXITOK                                                           
         DROP  R6                                                               
*                                                                               
INH200   DS    0H                                                               
         CLI   7(R9),X'20'         INHERIT OFFICE?                              
         BNE   INH250                                                           
         MVI   ELCODE,X'04'                                                     
         BAS   RE,GETEL                                                         
         BNE   EXITL                                                            
         USING RSTAOTEL,R6                                                      
         MVC   SBOFFICE(L'RSTAOTOF),RSTAOTOF                                    
         CLC   SBOFFICE,SPACES                                                  
         BE    EXITL                                                            
         B     EXITOK                                                           
         DROP  R6                                                               
*                                                                               
INH250   DS    0H                                                               
         DC    H'0'                NOTHING ELSE TO INHERIT                      
**********************************************************************          
* COMMON ROUTINES                                                               
**********************************************************************          
EXITH    CLI   *,0                 SET CC HIGH                                  
         B     EXIT                                                             
EXITL    CLI   *,X'FF'                                                          
         B     EXIT                                                             
EXITNO   LTR   RB,RB               SET CC NOT EQUAL                             
         B     EXIT                                                             
OK       EQU   *                                                                
EXITOK   CR    RB,RB               SET CC EQUAL                                 
         B     EXIT                                                             
EXIT     DS    0H                  JUST EXIT                                    
         XIT1                                                                   
*                                                                               
NOHOOK   BR    RE                                                               
*                                                                               
         GETEL R6,34,ELCODE                                                     
*                                                                               
SPACES   DC    80C' '                                                           
         LTORG                                                                  
*                                                                               
WORKD    DSECT                                                                  
DUB      DS    D                                                                
DMCB     DS    6F                                                               
FULL     DS    F                                                                
RELO     DS    F                                                                
SVPARM   DS    F                                                                
SAVERD   DS    F                                                                
VREPFACS DS    A                                                                
GETFACT  DS    A                                                                
HELLO    DS    A                                                                
RFBLOCK  DS    0CL6                                                             
COMFACS  DS    A                                                                
AGENCY   DS    CL2                 REP CODE (MASTER)                            
AGENCYCT DS    CL2                 ACTUAL CONNECTED REP CODE (SUBSID)           
ELCODE   DS    X                                                                
BYTE     DS    X                                                                
PIDNUM   DS    CL2                 PASSWORD ID NUMBER                           
*                                                                               
STATUS   DS    X                   PROGRAM STATUS FLAGS                         
MASTERQ  EQU   X'80'               MASTER REP CONNECTED                         
XOWNFLG  EQU   X'40'               HIT OWNER SPCL CASE                          
*                                                                               
VASTAT   DS    X                   VALID ACCESS STATUS                          
*              X'80'               MATCHED ON GROUP                             
*              X'40'               MATCHED ON OFFICE                            
*              X'20'               MATCHED ON STATION                           
*              X'10'               MATCHED ON OWNER                             
*              X'08'               MATCHED ON MARKET                            
*                                                                               
*                                                                               
ERRCODE  DS    CL6                 CODE VALIDATION FAILED ON                    
ERRTYPE  DS    X                   ERROR CODE FOR MSG GENERATION                
*            1 = ACCESS RESTRICTED, FILTER MISSING                              
*            2 = ACCESS RESTRICTED, FILTER INVALID                              
*                                                                               
*                                                                               
WORK     DS    CL64                                                             
KEY      DS    CL32                                                             
KEYSAVE  DS    CL32                                                             
SETS     DS    CL120               30 X 4 SETS TO BE EXPANDED                   
*                                                                               
       ++INCLUDE REGENSEC                                                       
         ORG   RSECREC                                                          
SECREC   DS    XL1000                                                           
IOAREA   DS    XL1000                                                           
*                                                                               
CODESLNQ EQU   6000                                                             
BLKCODES DS    XL(CODESLNQ)        DON'T CHANGE THE ORDER OR LOCATION           
PARCODES DS    XL(CODESLNQ)        OF THESE TWO BUFFERS!                        
WORKDX   EQU   *                                                                
SECBLK   DSECT                                                                  
       ++INCLUDE REGENSBLK                                                      
         DSECT                                                                  
       ++INCLUDE REGENREPA                                                      
       ++INCLUDE REGENSTA                                                       
       ++INCLUDE REGENSET                                                       
       ++INCLUDE DDCOMFACS                                                      
       ++INCLUDE FAFACTS                                                        
       ++INCLUDE FATIOB                                                         
       ++INCLUDE REPFACSQ                                                       
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'097RECKSECS  11/17/99'                                      
         END                                                                    
