*          DATA SET ACRAPPER   AT LEVEL 004 AS OF 11/09/00                      
*CATALP ACRAPPER                                                                
         SPACE 2                                                                
*********************************************************************           
* ACRAPPER - MODULE TO MAINTAIN RECORD ACTIVITY ELEMENTS AND        *           
*                                                                   *           
* CALLER SETS RAPBLK - RAPACTN, RAPCPY, RAPRTYP, RAPACOM, AND       *           
*                      RAPAREC MUST BE SET                          *           
* CALLER FIRST MAKES AN ELEMENT ACTION CALL - THIS SAVES EXISTING   *           
* POINTER ELEMENT IN BLOCK, DELETES IT FROM RECORD, THEN GENERATES  *           
* NEW POINTER ELEMENT AND SAVES IT IN BLOCK                         *           
*                                                                   *           
* CALLER IS THEN RESPONSIBLE FOR ADDREC/PUTREC CALLS OF RECORD      *           
* CALLER SECONDLY MAKES A POINTER ACTION CALL WHICH HANDLES         *           
* DIRECTORY UPDATES FOR THE RECORD ACTIVITY POINTER                 *           
*                                                                   *           
* ON EXIT, CC=EQ IF OK, CC=NEQ IF ERROR AND RAPERROR SET            *           
*********************************************************************           
ACRAPPER TITLE 'ACRAPPER - MAINTAIN ACTIVITY ELEMENTS/POINTERS'                 
ACRAPPER CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 WORKX-WORKD,**RAPP**,RA,CLEAR=YES                                
         USING WORKD,RC                                                         
         MVC   SAVERD,4(RD)        SAVE START OF WORKING STORAGE                
         ST    R1,ARAPBLK                                                       
         LR    R9,R1               R9=A(RAPPER BLOCK)                           
         USING ACRAPD,R9                                                        
         L     R8,RAPACOM          R8=A(COMFACS)                                
         USING COMFACSD,R8                                                      
*                                                                               
         MVI   SPACES,C' '                                                      
         MVC   SPACES+1(L'SPACES-1),SPACES                                      
*                                                                               
         MVC   FILE,ACCOUNT                                                     
         CLI   RAPEMU,C'N'         TEST EMULATED ACCOUNT FILE                   
         BNE   *+10                                                             
         MVC   FILE,ACCMST                                                      
*                                                                               
         MVI   RAPERROR,0                                                       
         CLI   RAPCPY,0            TEST IF COMPANY PASSED                       
         BNE   *+12                                                             
         MVI   RAPERROR,RAPECPY                                                 
         B     RAPX                                                             
*                                                                               
         CLI   RAPRTYP,0           MAKE SURE WE HAVE A RECORD TYPE              
         BNE   *+12                                                             
         MVI   RAPERROR,RAPERTYP                                                
         B     RAPX                                                             
*                                                                               
         LA    RF,ACTIVE                                                        
         CLI   RAPACTN,RAPAELEM                                                 
         BE    RAP2                                                             
         LA    RF,POINT                                                         
         CLI   RAPACTN,RAPAPTR                                                  
         BE    RAP2                                                             
*                                                                               
         MVI   RAPERROR,RAPEACTN                                                
         B     RAPX                                                             
*                                                                               
RAP2     BASR  RE,RF               CALL PROCESSING ROUTINE FOR ACTION           
*                                                                               
RAPX     CLI   RAPERROR,0          SET CC ON EXIT                               
         B     EXIT                                                             
         EJECT                                                                  
*---------------------------------------------------------                      
*        ADD POINTER ELEMENT FOR RECORD ACTIVITY KEY                            
*---------------------------------------------------------                      
*                                                                               
ACTIVE   NTR1  ,                                                                
         XC    RAPOLDP,RAPOLDP     CLEAR SAVE POINTER AREAS IN BLOCK            
         XC    RAPNEWP,RAPNEWP                                                  
*                                                                               
         MVI   BYTE,PTRTRAP        SET POINTER TYPE FILTER                      
         GOTO1 CHELLO,DMCB,(C'G',FILE),('PTRELQ',RAPAREC),(1,BYTE)              
         CLI   12(R1),0            TEST IF ELEMENT FOUND                        
         BNE   ACTIVE10            NO                                           
*                                                                               
         L     R6,12(R1)                                                        
         USING PTRELD,R6                                                        
         MVC   RAPOLDP,PTRCODE     SAVE OLD POINTER-THEN DELETE ELEM            
         GOTO1 CHELLO,DMCB,(C'D',FILE),('PTRELQ',RAPAREC),(1,BYTE)              
*                                                                               
ACTIVE10 XC    ELEMENT,ELEMENT                                                  
         LA    R6,ELEMENT          BUILD POINTER ELEMENT IN ELEMENT             
         MVI   PTREL,PTRELQ                                                     
         MVI   PTRLN,PTRLN1Q+L'ACCKEY                                           
         MVI   PTRTYPE,PTRTRAP                                                  
*                                                                               
         LA    R5,PTRCODE                                                       
         USING RAPRECD,R5                                                       
         MVI   RAPKTYP,RAPKTYPQ                                                 
         MVC   RAPKCPY,RAPCPY                                                   
         GOTO1 CDATCON,DMCB,(5,0),(2,RAPKDATE)                                  
*                                                                               
         TIME  BIN                                                              
         SRDL  R0,32               TIME IS IN 100THS OF A SECOND                
         D     R0,=F'100'                                                       
         STCM  R1,7,RAPKTIME                                                    
*                                                                               
         MVC   RAPKRTYP,RAPRTYP                                                 
         L     RE,RAPAREC                                                       
         MVC   RAPKKEY,0(RE)                                                    
*                                                                               
         CLI   RAPRTYP,RAPKROFL    TEST FOR OFFICE LIST                         
         BNE   ACTIVE20            NO                                           
         MVC   RAPKKEY,SPACES                                                   
         MVC   RAPKOFL,OFFKOFF-OFFRECD(RE) EXTRACT OFFICE LIST CODE             
*                                                                               
ACTIVE20 MVC   RAPNEWP,RAPRECD     SAVE NEW POINTER                             
         GOTO1 CHELLO,DMCB,(C'P',FILE),RAPAREC,PTRELD                           
*                                                                               
ACTIVEX  B     EXIT                                                             
         DROP  R5,R6                                                            
         EJECT                                                                  
*---------------------------------------------------------                      
*        MAINTAIN RECORD ACTIVITY POINTERS                                      
*---------------------------------------------------------                      
*                                                                               
POINT    NTR1  ,                                                                
         CLC   RAPOLDP,RAPNEWP     TEST FOR CHANGE IN POINTERS                  
         BE    POINTX                                                           
*                                                                               
         XC    BIGKEY,BIGKEY                                                    
         LA    R2,BIGKEY                                                        
*                                                                               
POINT2   OC    RAPOLDP,RAPOLDP     TEST FOR ANY OLD POINTER                     
         BZ    POINT4                                                           
*                                                                               
         USING ACCRECD,R2                                                       
         MVC   ACCKEY,RAPOLDP                                                   
         BAS   RE,HIGHL                                                         
         CLC   ACCKEY,KEYSAVE      TEST IF RECORD FOUND                         
         BNE   POINT4              NO                                           
*                                                                               
         OI    ACCKSTA,X'80'       DELETE OLD POINTER                           
         BAS   RE,WRITE                                                         
         BE    POINT4                                                           
         DC    H'0'                                                             
*                                                                               
POINT4   L     R5,RAPAREC          R5=A(RECORD)                                 
         OC    RAPRDA,RAPRDA       TEST IF DISK ADDRESS PASSED                  
         BZ    *+14                NO                                           
         MVC   SAVEDA,RAPRDA                                                    
         B     POINT6                                                           
*                                                                               
         XC    BIGKEY,BIGKEY                                                    
         MVC   ACCKEY,0(R5)        READ MAJOR KEY DIRECTORY ENTRY               
         BAS   RE,HIGH                                                          
         CLC   ACCKEY,KEYSAVE      TEST IF NEW POINTER ON FILE                  
         BE    *+6                                                              
         DC    H'0'                                                             
         MVC   SAVEDA,DISKADD      SAVE RECORD'S DISK ADDRESS                   
*                                                                               
POINT6   XC    BIGKEY,BIGKEY                                                    
         MVC   ACCKEY,RAPNEWP      CHECK IF NEW POINTER IS ON FILE              
         BAS   RE,HIGHL                                                         
         CLC   ACCKEY,KEYSAVE      TEST                                         
         BE    POINT8                                                           
*                                                                               
         MVC   ACCKEY,RAPNEWP      RESTORE PASSIVE POINTER KEY                  
         XC    ACCKSTA,ACCKSTA                                                  
         MVC   ACCKSTA(1),ACCOSTAT(R5) COPY OLD RECORD STATUS BYTE              
         CLI   RAPEMU,C'N'         TEST FOR ACCMST RECORD                       
         BNE   *+10                                                             
         MVC   ACCKSTA,ACCRSTA-ACCRECD(R5) YES-COPY ALL OF STATUS               
         NI    ACCKSTA,X'FF'-ACTSDELT LIVE POINTER FOR DELETED RECORD           
         MVC   ACCKDA,SAVEDA       SET DISK ADDRESS IN POINTER                  
         BAS   RE,ADD                                                           
         B     POINTX                                                           
*                                                                               
POINT8   MVC   ACCKDA,SAVEDA                                                    
         XC    ACCKSTA,ACCKSTA                                                  
         MVC   ACCKSTA(1),ACCOSTAT(R5) USE OLD RECORD STATUS BYTE               
         CLI   RAPEMU,C'N'         TEST FOR ACCMST RECORD                       
         BNE   *+10                                                             
         MVC   ACCKSTA,ACCRSTA-ACCRECD(R5) YES-COPY ALL OF STATUS               
         NI    ACCKSTA,X'FF'-ACTSDELT LIVE POINTER FOR DELETED RECORD           
         BAS   RE,WRITE                                                         
*                                                                               
POINTX   B     EXIT                                                             
         DROP  R2                                                               
         EJECT                                                                  
* DATAMGR INTERFACE                                                             
*                                                                               
HIGHL    NTR1  ,                                                                
         MVC   COMMAND,DMRDHI                                                   
         MVC   KEYSAVE,BIGKEY                                                   
         MVI   DMINBTS,X'88'                                                    
         B     IO                                                               
*                                                                               
HIGH     NTR1  ,                                                                
         MVC   COMMAND,DMRDHI                                                   
         MVC   KEYSAVE,BIGKEY                                                   
         MVI   DMINBTS,X'08'                                                    
         B     IO                                                               
*                                                                               
ADD      NTR1  ,                                                                
         MVC   COMMAND,DMADD                                                    
         MVI   DMINBTS,0                                                        
         B     IO                                                               
*                                                                               
WRITE    NTR1  ,                                                                
         MVC   COMMAND,DMWRITE                                                  
         MVI   DMINBTS,0                                                        
         B     IO                                                               
*                                                                               
IO       GOTO1 CDATAMGR,DMCB,(DMINBTS,COMMAND),ACCDIR,BIGKEY,BIGKEY,0           
         MVC   DISKADD,BIGKEY+(ACCKDA-ACCRECD) GET DISK ADDRESS                 
         CLI   DMCB+8,0            TEST FOR IO ERROR                            
         B     EXIT                                                             
         EJECT                                                                  
* EXIT POINTS                                                                   
*                                                                               
EXIT     XMOD1 1                                                                
         SPACE 2                                                                
*--------------------------------------------------------------                 
* CONSTANTS                                                                     
*---------------------------------------------------------------                
*                                                                               
ACCOUNT  DC    CL8'ACCFIL'                                                      
ACCDIR   DC    CL8'ACCDIR'                                                      
ACCMST   DC    CL8'ACCMST'                                                      
DMRDHI   DC    CL8'DMRDHI'                                                      
DMADD    DC    CL8'DMADD'                                                       
DMWRITE  DC    CL8'DMWRT'                                                       
         SPACE 2                                                                
* LITERAL POOL                                                                  
*                                                                               
         LTORG                                                                  
         EJECT                                                                  
* DSECT TO COVER ACRAPPER WORKING STORAGE                                       
*                                                                               
WORKD    DSECT                                                                  
ARAPBLK  DS    A                   A(CALLER'S RAPPER BLOCK)                     
SAVERD   DS    A                                                                
*                                                                               
SPACES   DS    CL42                                                             
*                                                                               
DMCB     DS    6F                                                               
BIGKEY   DS    XL54                                                             
KEYSAVE  DS    XL54                                                             
FILE     DS    CL8                 HELLO FILE NAME                              
COMMAND  DS    CL8                 DATAMGR                                      
DMINBTS  DS    XL1                                                              
DISKADD  DS    A                                                                
*                                                                               
SAVEDA   DS    A                                                                
*                                                                               
DUB      DS    D                                                                
FULL     DS    F                                                                
BYTE     DS    XL1                                                              
ELEMENT  DS    XL256                                                            
WORKX    EQU   *                                                                
         EJECT                                                                  
* ACRAPPER CONTROL BLOCK                                                        
*                                                                               
ACRAPD   DSECT                                                                  
       ++INCLUDE ACRAPPERD                                                      
         EJECT                                                                  
* ACGENFILE                                                                     
* DDCOMFACS                                                                     
*                                                                               
         PRINT OFF                                                              
       ++INCLUDE ACGENFILE                                                      
       ++INCLUDE DDCOMFACS                                                      
         PRINT ON                                                               
         SPACE 2                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'004ACRAPPER  11/09/00'                                      
         END                                                                    
