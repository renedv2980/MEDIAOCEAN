*          DATA SET ACLABEL2   AT LEVEL 019 AS OF 12/04/03                      
*CATALP ACLABEL2                                                                
         TITLE 'ACLABLE2 - LABELS (ALL PURPOSE 7 LINES, 2 ACROSS).'             
         PRINT NOGEN                                                            
ACLABEL2 CSECT                                                                  
         SPACE 1                                                                
         NMOD1 0,**LAB2**                                                       
*                                                                               
         L     R9,0(,R1)                                                        
         CLC   =C'$OPEN',0(R9)                                                  
         BE    LABEL05                                                          
         CLC   =C'$CLOS',0(R9)                                                  
         BNE   LABEL15                                                          
         MVC   CLOSE,0(R9)                                                      
         MVI   FIRST,C'L'                                                       
         CP    ACROSS,=P'0'        IS ARRAY EMPTY                               
         BE    LABEL30             YES, DON'T FLUSH - JUST END                  
         B     LABEL45                                                          
*                                                                               
LABEL05  CLI   FIRST,C'F'          WERE WE CLOSED                               
         BNE   LABEL10             OTHERWISE GO TO SPECIAL START.               
         MVC   PRINT,4(R1)         YES, START NEW ARRAY, ETC.                   
         MVC   ALABLK,20(R1)                                                    
         MVI   SWITCH,0                                                         
*                                                                               
*        L     R2,20(R1)                                                        
*        OC    0(L'TOPROW,R2),0(R2)                                             
*        BZ    *+14                                                             
*        MVC   TOPROW,0(R2)                                                     
*        NI    TOPROW,X'0F'        CHARACTER TO BINARY CONVERSION               
         MVI   TOPROW,5                                                         
                                                                                
         ICM   RE,15,ALABLK                                                     
         BZ    LABEL06                                                          
         MVC   LABLK,0(RE)                                                      
         ZAP   PRINTED,=P'0'                                                    
         MVI   LINSW,C'F'                                                       
         BAS   RE,LINUP                                                         
         B     LABEL10                                                          
                                                                                
LABEL06  LHI   R0,#LINES                                                        
         LA    R5,ARRAYWK                                                       
LABEL08  MVI   0(R5),C'*'                                                       
         MVC   1(L'ARRAYS,R5),0(R5)                  FIRST LABEL COLUMN         
         OC    TOPROW,TOPROW                                                    
         BZ    *+14                                                             
         MVC   L'ARRAYF+L'ARRAYX(L'ARRAYS,R5),0(R5)  SECOND LABEL CLMN          
         B     *+10                                                             
         MVC   L'ARRAYF(L'ARRAYS,R5),0(R5)           SECOND LABEL CLMN          
         LA    R5,ARRAYLNQ(R5)                                                  
         BCT   R0,LABEL08                                                       
                                                                                
LABEL10  L     R2,8(R1)            HOW MANY OF EACH                             
         LTR   R2,R2                                                            
         BNZ   *+8                                                              
         LHI   R2,1                                                             
         ST    R2,NOMBRE                                                        
         CLI   FIRST,C'F'          IF NOT CLOSED, DON'T REFRESH PRINT           
         BNE   LABELX              POSITION ADDRESS, DO LINEUPS, OR             
*                                  CHANGE SPACING OPTION.                       
         L     R2,16(R1)                                                        
         CHI   R2,8                                                             
         BNE   *+12                                                             
         MVI   SWS,C'8'                                                         
         B     *+8                                                              
         MVI   SWS,C'6'                                                         
         LA    R5,ARRAYWK                                                       
         ST    R5,SVPAD                                                         
         L     R2,12(R1)                                                        
         LTR   R2,R2                                                            
         BZ    LABEL90                                                          
         CVD   R2,DOUBLE                                                        
         ZAP   CTLN,DOUBLE                                                      
         B     LABEL45                                                          
*                                                                               
         USING LINESD,R9                                                        
LABEL15  L     R9,0(,R1)                                                        
         L     R2,NOMBRE                                                        
*                                                                               
LABEL20  L     R5,SVPAD            LOAD PRINT ADDR                              
         LA    R6,LINE1                                                         
         LA    R7,#LINES                                                        
LABEL25  MVC   0(L'LINE1,R5),0(R6)                                              
         LA    R5,ARRAYLNQ(R5)                                                  
         LA    R6,L'LINE1(R6)                                                   
         BCT   R7,LABEL25                                                       
         AP    ACROSS,=P'1'                                                     
         CP    ACROSS,=P'2'        ARE WE 2 ACROSS YET                          
         BNL   LABEL45                                                          
         L     R5,SVPAD                                                         
         OC    TOPROW,TOPROW                                                    
         BZ    *+8                                                              
         LA    R5,L'ARRAYX(R5)                                                  
         LA    R5,L'ARRAYF(R5)                                                  
         ST    R5,SVPAD                                                         
*                                                                               
LABEL30  CLI   FIRST,C'L'          WAS IT A CLOSE                               
         BE    *+12                                                             
         BCT   R2,LABEL20                                                       
         B     LABELX                                                           
***UK                                                                           
         MVI   LINSW,C'L'                                                       
         BAS   RE,LINUP            DO LAST TIME DIVIDERS                        
         ZAP   ACROSS,=P'0'        ZERO OUT ACROSS COUNTER                      
         LA    R5,ARRAYWK                                                       
         MVC   PCTL,=C'BL01'       SETUP SINGLE SPACE                           
LABEL35  MVC   P+1(ARRAYLNQ),0(R5)                                              
         BAS   RE,PRT              PRINT A LINE                                 
         LA    R5,ARRAYLNQ(R5)                                                  
         AP    ACROSS,=P'1'                                                     
         CP    ACROSS,=P'7'        ALL DONE                                     
         BL    LABEL35             NO                                           
***                                                                             
         CLI   CLOSE+5,C'P'        FORCE PRINT CLOSE (CLOSP PASSED)             
         BNE   LABEL40                                                          
         GOTO1 PRINT,DMCB,=C'CLOSE'                                             
LABEL40  MVI   FIRST,C'F'          RESET FOR NEXT REQUEST                       
         B     LABELX                                                           
*                                                                               
LABEL45  ZAP   ACROSS,=P'0'        ZERO OUT ACROSS COUNTER                      
*&&UK*&& AP    PRINTED,=P'2'                                                    
         ZIC   R3,ROWCOUNT                                                      
         OC    TOPROW,TOPROW                                                    
         BZ    LABEL65                                                          
*                                                                               
LABEL50  ZIC   R7,TOPROW                                                        
         CHI   R3,#ROWS                                                         
         BNE   LABEL60                                                          
         SR    R3,R7                                                            
         MVC   PCTL,=C'BL01'       SETUP SINGLE SPACE                           
         BAS   RE,PRT                                                           
         BCT   R7,*-4                                                           
*                                                                               
LABEL60  CHI   R3,9                9 = ROW COUNT OF EACH LABEL ARRAY            
         BH    LABEL65                                                          
         MVC   PCTL,=C'BC01'       SETUP PAGE BREAK                             
         BAS   RE,PRT                                                           
         MVI   ROWCOUNT,#ROWS      RESET COUNT                                  
         ZIC   R3,ROWCOUNT                                                      
         B     LABEL50                                                          
*                                                                               
LABEL65  LA    R5,ARRAYWK                                                       
LABEL70  MVC   PCTL,=C'BL01'       SETUP SINGLE SPACE                           
LABEL75  MVC   P+1(ARRAYLNQ),0(R5)                                              
         BAS   RE,PRT                PRINT A LINE                               
         LA    R5,ARRAYLNQ(R5)                                                  
         SHI   R3,1                DECREMENT ROW COUNTER                        
         AP    ACROSS,=P'1'                                                     
         CP    ACROSS,=P'7'        ALL DONE                                     
         BL    LABEL75             NO                                           
         MVI   PCTL+3,C'2'                                                      
         MVI   P,C' '              YES, GO TO NEXT LABEL SET                    
         MVC   P+1(L'P-1),P                                                     
         BAS   RE,PRT                                                           
         SHI   R3,1                DECREMENT ROW COUNTER                        
         CLI   SWS,C'8'                                                         
         BNE   LABEL80                                                          
*                                                                               
         OC    TOPROW,TOPROW                                                    
         BZ    LABEL77                                                          
         CLI   SWITCH,1            IF 1 THEN ONLY 1 LINE SPACE                  
         BE    LABEL79                                                          
LABEL77  MVI   PCTL+3,C'3'                                                      
         BAS   RE,PRT                                                           
         SHI   R3,1                DECREMENT ROW COUNTER                        
LABEL79  XI    SWITCH,X'01'        ALTERNATE SWITCH                             
*                                                                               
         STC   R3,ROWCOUNT         SAVE ROW COUNTER                             
LABEL80  CLI   FIRST,C'F'                                                       
         BE    LABEL85                                                          
         LA    RE,ARRAYWK                                                       
         LA    RF,L'ARRAYWK                                                     
         LA    R0,SPACES                                                        
         SR    R1,R1                                                            
         ICM   R1,8,SPACES         PAD CHARACTER BLANK                          
         MVCL  RE,R0               MOVE SPACES                                  
*                                                                               
LABEL85  LA    R5,ARRAYWK                                                       
         ST    R5,SVPAD            BACK TO START FOR NEXT                       
         ZAP   ACROSS,=P'0'        DITTO ACROSS COUNTER.                        
         CLI   FIRST,C'F'          FIRST TIME                                   
         BNE   LABEL30                                                          
         SP    CTLN,=P'1'                                                       
         BNZ   LABEL70                                                          
*                                                                               
LABEL90  LA    RE,ARRAYWK                                                       
         LA    RF,L'ARRAYWK                                                     
         LA    R0,SPACES                                                        
         SR    R1,R1                                                            
         ICM   R1,8,SPACES         PAD CHARACTER BLANK                          
         MVCL  RE,R0               MOVE SPACES                                  
         MVI   FIRST,C'N'          YES,NOT ANY MORE                             
*                                                                               
LABELX   XIT1                                                                   
*                                                                               
         SPACE 2                                                                
         EJECT                                                                  
**********************************************************************          
* BLDBLK - BUILD LABEL LINEUP BLOCK                                  *          
**********************************************************************          
         SPACE 1                                                                
PRT      NTR1                                                                   
         GOTO1 PRINT,DMCB,P,PCTL                                                
         B     LABELX                                                           
         EJECT                                                                  
**********************************************************************          
* LINUP                                                              *          
**********************************************************************          
         SPACE 1                                                                
         USING LBBLKD,R2                                                        
LINUP    NTR1                                                                   
         LA    R2,LABLK            DO FIRST/LAST LABELS                         
         MVC   LINEUP03+2(34),SPACES                                            
         MVC   LINEUP04+2(34),SPACES                                            
         MVC   LINEUP05+2(34),SPACES                                            
         MVC   LINEUP06+2(34),SPACES                                            
         MVC   LINEUP03,LINEUPNN                                                
         MVC   LINEUP05+02(L'LBLOGNM),LBLOGNM                                   
         MVC   LINEUP06+02(L'LBLOGAD),LBLOGAD                                   
         CLI   LINSW,C'F'                                                       
         BNE   LIN2                                                             
*&&UK*&& MVC   LINEUP04+02(16),=C'POST PROCESSING='                             
*&&UK*&& MVC   LINEUP04+18(L'LBLOG2),LBLOG2                                     
*&&US*&& MVC   LINEUP04+02(9),=C'ORIGIN ID='                                    
*&&US*&& MVC   LINEUP04+12(L'LBORGNID),LBORGNID                                 
         MVC   LINEUP03+2(12),=C'START OF JOB'                                  
         LA    RE,LINEUP03+15                                                   
         B     LIN4                                                             
                                                                                
LIN2     MVC   LINEUP03+2(10),=C'END OF JOB'                                    
         MVC   LINEUP04,LINEUPNN                                                
         LA    R3,LINEUP04+2                                                    
         EDIT  PRINTED,(6,0(R3)),ALIGN=LEFT                                     
         LA    RE,LINEUP04+3                                                    
         AR    RE,R0                                                            
         MVC   0(14,RE),=C'LABELS PRINTED'                                      
         LA    RE,LINEUP03+13                                                   
                                                                                
LIN4     MVC   0(L'LBJNM,RE),LBJNM                                              
         LA    RE,L'LBJNM-1(RE)                                                 
         CLI   0(RE),C' '                                                       
         BH    *+8                                                              
         BCT   RE,*-8                                                           
                                                                                
         CLI   LBSTPNM,C' '                                                     
         BNH   LIN6                                                             
         MVI   1(RE),C'/'                                                       
         MVC   2(L'LBSTPNM,RE),LBSTPNM                                          
*                                                                               
LIN6     LA    R2,LINEUP01                                                      
         LA    R3,ARRAYWK                                                       
         LA    RF,7                                                             
                                                                                
         USING LABELD,R3                                                        
LIN10    MVC   LABEL1(L'LINEUP01),0(R2)                                         
         MVC   LABEL2(L'LINEUP02),0(R2)                                         
         AHI   R2,L'LINEUP01                                                    
         AHI   R3,LABELLNQ                                                      
         BCT   RF,LIN10                                                         
         DROP  R3                                                               
*                                                                               
LINX     XIT1                                                                   
         EJECT                                                                  
DUB      DS    D                                                                
DOUBLE   DS    D                                                                
SVPAD    DS    F                                                                
PRINT    DS    F                                                                
ALABLK   DS    A                                                                
DMCB     DS    6F                                                               
NOMBRE   DS    F                                                                
ACROSS   DC    PL1'0'                                                           
CTLN     DS    PL1                                                              
PRINTED  DS    PL4                                                              
FIRST    DC    C'F'                                                             
CLOSE    DS    CL6                                                              
P        DC    CL132' '                                                         
SPACES   DC    CL132' '                                                         
WORK     DS    CL80                                                             
PCTL     DC    CL4'BC01'                                                        
SWS      DS    CL1                                                              
LINSW    DS    CL1                                                              
LABLK    DS    CL(LBBLKLNQ)                                                     
SWITCH   DS    XL1                                                              
TOPROW   DC    XL1'0'                                                           
ROWCOUNT DC    XL1'3D'             INITALIZE TO 61                              
#ROWS    EQU   61                                                               
#LINES   EQU   7                                                                
LINEUPNN DC    C'**                                  **'                        
*                12345678901234567890123456789012345678                         
LINEUP01 DC    C'**************************************'                        
LINEUP02 DC    C'**************************************'                        
LINEUP03 DC    C'**                                  **'                        
LINEUP04 DC    C'**                                  **'                        
LINEUP05 DC    C'**                                  **'                        
LINEUP06 DC    C'**                                  **'                        
LINEUP07 DC    C'**************************************'                        
         SPACE 2                                                                
ARRAYWK  DS    0CL609              ARRAY WORK AREA                              
ARRAYF   DS    CL41                FIRST LABEL                                  
ARRAYX   DS    CL7                 SPACE BETWEEN LABELS                         
ARRAYS   DS    CL39                SECOND LABLE                                 
ARRAYLNQ EQU   *-ARRAYF                                                         
         DS    CL522                                                            
         SPACE 3                                                                
         LTORG                                                                  
         SPACE 2                                                                
LABELD   DSECT                                                                  
LABEL1   DS    CL41                                                             
         DS    CL7                                                              
LABEL2   DS    CL39                                                             
LABELLNQ EQU   *-LABELD                                                         
         SPACE 2                                                                
       ++INCLUDE ACLABELD                                                       
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'019ACLABEL2  12/04/03'                                      
         END                                                                    
