*          DATA SET TRLDPRGE   AT LEVEL 013 AS OF 08/11/15                      
*CATALP TRLDPRGE                                                                
         TITLE 'LDPURGE - STR - LOAD PURGE TEST'                                
*        PARAMS VIA R1                                                          
*        XL1   X'00'  RETURN  X'00'=NOPURGE  OR  X'FF'=PURGE                    
*        AL3   A(RECORD)                                                        
*                                                                               
*        UPSI  00000001 PURGE X'80' DELETES                                     
*        UPSI  00000010 PURGE X'C3' CLOSEOUTS                                   
*        UPSI  00000100 PURGE TEST AGY RECORDS                                  
*        UPSI  00001000 PURGE BUY ACTVITY RECS WITH NO EL'S, AND                
*                       DEL EL'S OLDER THAN 3 MOS.                              
         PRINT NOGEN                                                            
LDPURGE  CSECT                                                                  
         NMOD1 0,SPLDPRGE                                                       
                                                                                
PU       BC    0,PU2                                                            
         OI    PU+1,X'F0'          SET BRANCH                                   
         LR    R0,R1               SAVE R1                                      
         L     R1,=V(UPSIVAL)                                                   
         MVC   UPSI,0(R1)                                                       
         LR    R1,R0               RESTORE R1                                   
                                                                                
PU2      L     R3,0(R1)            R3 = A(RECORD)                               
         TM    UPSI,X'08'          PURGE BUY ACTV RECS?                         
         BZ    *+14                                                             
         CLC   =X'0A2E',0(R3)      TEST BUY ACTIVITY RECORD                     
         BE    ACTV                                                             
*                                                                               
         CLC   =X'0A7F',0(R3)      TEST OPTICA SAVE RECORD                      
         BNE   PU10                                                             
         MVI   0(R1),X'FF'         SET TO PURGE RECORD                          
         B     XIT                                                              
*                                                                               
PU10     TM    UPSI,X'07'          ANY PURGES                                   
         BZ    XIT                 NO                                           
         TM    15(R3),X'C0'        ANY DELETE INDS ON                           
         BNZ   ACAN                YES. A CANDIDATE                             
                                                                                
* NO DELETE INDICATORS. TEST FOR TEST AGENCY RECORD PURGE OPTION                
                                                                                
PU20     TM    UPSI,X'04'                                                       
         BO    TAG                 YES. GO TEST FOR TEST AG                     
                                                                                
XIT      XIT1                                                                   
         EJECT                                                                  
                                                                                
* THIS IS A CANDIDATE FOR A PURGE                                               
                                                                                
ACAN     TM    UPSI,X'03'          DELETE OR CLOSEOUT PURGE                     
         BZ    PU20                NO, GO TEST FOR TEST AG                      
         BO    BOTH                DELETE BOTH X'80' AND X'C0'                  
         SPACE 2                                                                
* DELETE ONE OR THE OTHER                                                       
         TM    UPSI,X'01'                                                       
         BO    O80                 ONLY X'80'                                   
         TM    UPSI,X'02'                                                       
         BO    OC0                 ONLY X'C0'                                   
         B     PU20                                                             
         SPACE 2                                                                
* DELETE ONLY X'80'                                                             
O80      TM    15(R3),X'C0'                                                     
         BO    PU20                BOTH ON, NO DELETE                           
         TM    15(R3),X'80'                                                     
         BZ    PU20                                                             
O801     MVI   0(R1),255                                                        
         B     XIT                                                              
                                                                                
* DELETE ONLY X'C0'                                                             
                                                                                
OC0      TM    15(R3),X'C0'                                                     
         BC    1,O801                                                           
         B     PU20                                                             
                                                                                
* DELETE BOTH                                                                   
                                                                                
BOTH     TM    15(R3),X'C0'                                                     
         BC    1,O801                                                           
         TM    15(R3),X'80'                                                     
         BC    1,O801                                                           
         B     PU20                                                             
                                                                                
* TEST AGENCY PURGE                                                             
                                                                                
TAG      CLC   20(2,R3),TSTAG                                                   
         BC    8,O801                                                           
         B     XIT                                                              
         EJECT                                                                  
ACTV     DS    0H                                                               
         LR    R7,R1               SAVER1                                       
         LR    R6,R3                                                            
*                                                                               
         MVI   ELCODE,X'05'                                                     
         BAS   RE,GETEL                                                         
         BNE   NOACTV                                                           
         USING TBADTAEL,R6                                                      
                                                                                
* GET TODAY'S DATE, SUBTRACT 3 MONTHS                                           
                                                                                
         OC    ACTVDATE,ACTVDATE                                                
         BNZ   ACTV2                                                            
         GOTO1 =V(DATCON),DMCB,(5,WORK),(0,WORK)                                
         GOTO1 =V(ADDAY),DMCB,(C'M',WORK),WORK+6,-3                             
         GOTO1 =V(DATCON),DMCB,(0,WORK+6),(2,ACTVDATE)                          
*                                                                               
         GOTO1 =V(ADDAY),DMCB,WORK,WORK+6,-7        GO BACK ONE WEEK            
         GOTO1 =V(DATCON),DMCB,WORK+6,(2,SV8ADATE)  SAVE 8A PTR DATE            
*                                                                               
ACTV2    NI    TBADTAAC,X'FF'-TBADTA8A  RESET 8A FLAG                           
*                                                                               
         CLC   TBADTAWK,ACTVDATE        LESS THAN CUTOFF?                       
         BNL   ACTV4                                                            
         MVI   0(R6),X'FF'                                                      
         B     ACTV6                                                            
*                                                                               
ACTV4    TM    TBADTAAC,X'80'      TEST ACTIVITY PENDING                        
         BZ    ACTV6                                                            
         CLC   TBADTAWK,SV8ADATE   AFTER 8A DATE                                
         BL    ACTV6                                                            
         OI    TBADTAAC,TBADTA8A                                                
         B     ACTV10              GO UNSET ANY MORE 8A FLAGS                   
*                                                                               
ACTV6    BAS   RE,NEXTEL                                                        
         BE    ACTV2                                                            
         B     ACTVX                                                            
*                                                                               
ACTV10   BRAS  RE,NEXTEL                                                        
         BNE   ACTVX                                                            
         NI    TBADTAAC,X'FF'-TBADTA8A  RESET 8A FLAG                           
         B     ACTV10                                                           
                                                                                
* REMOVE ALL X'FF' ELEMS, SEE IF ANY 05'S REMAIN                                
                                                                                
ACTVX    SR    RE,RE                                                            
         ICM   RE,3,13(R3)         GET REC LENGTH                               
         AR    RE,R3               POINT TO EOF                                 
         XC    0(2,RE),0(RE)       AND CLEAR BYTES AT END                       
*                                                                               
         GOTO1 =V(HELLO),DMCB,(C'D',=C'TRFFIL'),(X'FF',(R3)),0,0                
         LR    R6,R3                                                            
         MVI   ELCODE,X'05'                                                     
         BAS   RE,GETEL                                                         
         BNE   NOACTV                                                           
         B     XIT                                                              
*                                                                               
NOACTV   DS    0H                                                               
         LR    R1,R7               RESTORER1                                    
         MVI   0(R1),X'FF'         PURGE REC, NO ACTIVITY                       
         B     XIT                                                              
         DROP  R6                                                               
         EJECT                                                                  
*                                                                               
         EJECT                                                                  
*===================================================================            
* NOTE - I HAVE INCLUDED THE EXPANDED GETEL CODE HERE BECAUSE THERE             
* ARE BAD RECORDS ON SOME OF THE TRAFFIC FILES THAT CAUSE DUMPS IN              
* HELLO. NOTE THAT THE CODE HERE HAS BEEN CHANGED TO SAY THAT IF YOU            
* ENCOUNTER A NON-ZERO ELEMENT CODE WITH A ZERO LENGTH, JUST SET THE            
* ELEMENT CODE TO ZERO AND CARRY ON. AMEN.  MHER 03JUN11                        
*===================================================================            
                                                                                
GETEL    LA    R6,24(R6)                                                        
*                                                                               
FIRSTEL  CLI   0(R6),0                                                          
         JNE   *+8                                                              
         LTR   RE,RE                                                            
         BR    RE                                                               
         CLI   ELCODE,0                                                         
         BER   RE                                                               
         CLC   ELCODE,0(R6)                                                     
         BER   RE                                                               
NEXTEL   SR    RF,RF                                                            
         ICM   RF,1,1(R6)                                                       
         JNZ   NEXTEL2                                                          
         MVI   0(R6),0                                                          
         LTR   RE,RE                                                            
         BR    RE                                                               
NEXTEL2  AR    R6,RF                                                            
         J     FIRSTEL                                                          
ELCODE   DS    X                                                                
DMCB     DS    6F                                                               
UPSI     DC    X'00'                                                            
TSTAG    DC    X'EEEE'             CHANGED FROM T1 11/08/04 ABEA                
WORK     DS    CL24                                                             
ACTVDATE DC    XL2'00'                                                          
SV8ADATE DC    XL2'00'                                                          
APYDATE  DC    XL2'00'                                                          
         LTORG                                                                  
         EJECT                                                                  
       ++INCLUDE SPTRTBAE                                                       
         SPACE 2                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'013TRLDPRGE  08/11/15'                                      
         END                                                                    
