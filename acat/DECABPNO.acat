*          DATA SET DECABPNO   AT LEVEL 002 AS OF 05/01/02                      
*CATALP DECABPNO                                                                
         TITLE 'DECABPNO<==>RMDECABPNO - BASE 36 CONVERSION OF PROGRAM +        
               NUMBERS'                                                         
***********************************************************************         
*=============================== MAIN ================================*         
CABPNO   CSECT                                                                  
* CONVERTS A SINGLE FIELD 5-CHAR PGM# TO 3 SEPARATE FIELDS AND BACK             
*  ENTRY AND RETURN VALUES ARE:                                                 
*   P1  BYTE 0 : X'00'=AUTOCONVERT                                              
*                X'01'=3 FIELDS TO 1                                            
*                X'02'=1 FIELD TO 3                                             
*       BYTE1-3: A(5-CHAR PROGRAM #)  (CL5)                                     
*   P2         : A(NUMERIC PROGRAM #) (XL2)                                     
*   P3         : A(TRACK #)           (XL2)                                     
*   P4         : A(TELECAST #)        (XL2)                                     
         SPACE 2                                                                
         PRINT NOGEN                                                            
         NMOD1 WORKX-WORKD,**CBP#**                                             
         USING WORKD,RC                                                         
         ST    R1,APARM                                                         
         MVC   PARMS,0(R1)                                                      
         SPACE 2                                                                
         MVI   RETCODE,0           SET RETURN CODE                              
         BAS   RE,VALPARM          VALIDATE PARAMETERS & SET ACTION             
         BNE   MAINXNO              EXIT WITH ERROR CONDITION                   
         SPACE 1                                                                
MAIN05   CLI   ACTION,1                                                         
         BE    MAIN10                                                           
         CLI   ACTION,2                                                         
         BE    MAIN20                                                           
         DC    H'0'                                                             
*                                                                               
*--------------------------- 3 FIELDS TO 1 ---------------------------*         
*                                                                               
MAIN10   LA    R3,NPNO             CONVERT NUMERIC PRGM#                        
         LA    R0,THREE             MAX LENGTH IS 3                             
         LA    R4,CPNO+0            A(TO STORE OUTPUT)                          
         BAS   RE,CNV36                                                         
         BE    MAIN10A                                                          
         BAS   RE,CLEAR1                                                        
         B     MAINXNO                                                          
         SPACE 1                                                                
MAIN10A  LA    R3,TKNO             CONVERT TRACK #                              
         LA    R0,ONE               MAX LENGTH IS 1                             
         LA    R4,CPNO+THREE        A(TO STORE OUTPUT)                          
         BAS   RE,CNV36                                                         
         BE    MAIN10B                                                          
         BAS   RE,CLEAR1                                                        
         B     MAINXNO                                                          
         SPACE 1                                                                
MAIN10B  LA    R3,TTNO             CONVERT TELECAST #                           
         LA    R0,ONE               MAX LENGTH IS 1                             
         LA    R4,CPNO+THREE+ONE    A(TO STORE OUTPUT)                          
         BAS   RE,CNV36                                                         
         BE    MAINXYES                                                         
         BAS   RE,CLEAR1                                                        
         B     MAINXNO                                                          
*                                                                               
*---------------------------- 1 FIELD TO 3 ---------------------------*         
*                                                                               
MAIN20   LA    R2,CPNO             DO NUMERIC PROGRAM NUMBER                    
         LA    R1,THREE                                                         
         LA    R3,NPNO                                                          
         BAS   RE,CNV10                                                         
         BE    MAIN20A                                                          
         BAS   RE,CLEAR3                                                        
         B     MAINXNO                                                          
*                                                                               
MAIN20A  LA    R2,CPNO+THREE       DO TRACK NUMBER                              
         LA    R1,ONE                                                           
         LA    R3,TKNO                                                          
         BAS   RE,CNV10                                                         
         BE    MAIN20B                                                          
         BAS   RE,CLEAR3                                                        
         B     MAINXNO                                                          
*                                                                               
MAIN20B  LA    R2,CPNO+THREE+ONE   DO TELECAST NUMBER                           
         LA    R1,ONE                                                           
         LA    R3,TTNO                                                          
         BAS   RE,CNV10                                                         
         BE    MAINXYES                                                         
         BAS   RE,CLEAR3                                                        
         B     MAINXNO                                                          
*                                                                               
*----------------------------- MAIN EXIT -----------------------------*         
*                                                                               
MAINXNO  BAS   RE,STRETVAL                                                      
         L     R1,APARM                                                         
         MVC   0(1,R1),RETCODE                                                  
         B     CCNEQUAL                                                         
MAINXYES BAS   RE,STRETVAL                                                      
         L     R1,APARM                                                         
         MVC   0(1,R1),RETCODE                                                  
         B     CCEQUAL                                                          
         SPACE 2                                                                
CCEQUAL  SR    RF,RF               TO RETURN WITH CC EQUAL                      
         B     *+8                                                              
CCNEQUAL LA    RF,1                TO RETURN WITH CC NOT EQUAL                  
         LTR   RF,RF                                                            
EXIT     XIT1                                                                   
***********************************************************************         
         EJECT                                                                  
***********************************************************************         
*============================ SET VALUES =============================*         
*                                                                               
*-------------------------- CLEAR "1" FIELD --------------------------*         
*                                                                               
CLEAR1   MVI   CPNO,C'0'           INITIALIZE 5-CHAR PRGM#                      
         MVC   CPNO+1(L'CPNO-1),CPNO                                            
         BR    RE                                                               
*                                                                               
*------------------------- CLEAR "3" FIELDS --------------------------*         
*                                                                               
CLEAR3   XC    NPNO,NPNO                                                        
         XC    TKNO,TKNO                                                        
         XC    TTNO,TTNO                                                        
         BR    RE                                                               
*                                                                               
*------------------------- SET RETURN VALUES -------------------------*         
*                                                                               
STRETVAL LM    R2,R5,P1ACPNO                                                    
         MVC   0(L'CPNO,R2),CPNO                                                
         MVC   0(L'NPNO,R3),NPNO                                                
         MVC   0(L'TKNO,R4),TKNO                                                
         MVC   0(L'TTNO,R5),TTNO                                                
         BR    RE                                                               
***********************************************************************         
         EJECT                                                                  
***********************************************************************         
*======================== VALIDATE PARAMETERS ========================*         
VALPARM  NTR1                                                                   
*                                                                               
*  VALIDATE PARAMETERS AND INITIALIZE CORRESPONDING FIELDS                      
*                                                                               
*  AT EXIT,                                                                     
*     CC IS NOTEQUAL IF PARAMETERS ARE INVALID                                  
*           EQUAL    IF PARAMETERS ARE VALID                                    
*     RETCODE HAS ACTION BIT ON IF SPECIFIED, OR OFF IF AUTOCONVERT             
         SPACE 1                                                                
         LM    R2,R5,P1ACPNO       SET INPUT VALUES INTO LOCAL STRGE            
         MVC   CPNO,0(R2)           INPUT 5-CHAR PRGM#                          
         MVC   NPNO,0(R3)           INPUT 2-BYTE NUMERIC PRGM#                  
         MVC   TKNO,0(R4)           INPUT 2-BYTE TRACK#                         
         MVC   TTNO,0(R5)           INPUT 2-BYTE TELECAST#                      
         MVC   ACTION,P1ACTN        ACTION BYTE                                 
         LA    R9,CCEQUAL          ASSUME VALID PARAMETERS                      
         SPACE 1                                                                
         CLI   ACTION,X'01'        IF ACTION IS NEITHER OF                      
         BE    VP110                                                            
         CLI   ACTION,X'02'         THESE TWO CHOICES                           
         BE    VP210                THEN MAKE MY OWN CHOICE OF ACTION           
*                                                                               
*---------------------------- AUTOCONVERT ----------------------------*         
*                                                                               
* IF THE 5-CHAR PROGRAM NUMBER CONTAINS ALL VALID BASE-36 DIGITS,               
*  THEN CHOOSE ACTION=2, ELSE CHOOSE ACTION=1                                   
*                                                                               
         LA    R2,CPNO             R2-->5-CHAR PROGRAM NUMBER                   
         LA    R3,FIVE             R3=LOOP COUNTER                              
*                                                                               
VP010    MVC   DIGIT,0(R2)         VALIDATE DIGIT IN 5-CHAR PRGM#               
         MVI   VALUE,36             SET UP FOR VALIDATE                         
         BAS   RE,DGTVLU                                                        
         BNE   VP020                                                            
         LA    R2,1(R2)            BUMP TO NEXT DIGIT                           
         BCT   R3,VP010                                                         
         SPACE 1                                                                
         MVI   ACTION,X'02'        DIGITS ARE VALID, "1 FIELD TO 3"             
         B     VPX20                                                            
*                                                                               
VP020    MVI   ACTION,X'01'                                                     
         B     VPX10                                                            
*                                                                               
*----------------------------- 3 TO 1 --------------------------------*         
*                                                                               
VP110    OI    RETCODE,X'01'                                                    
         B     VPX10                                                            
*                                                                               
*----------------------------- 1 TO 3 --------------------------------*         
*                                                                               
VP210    OI    RETCODE,X'02'                                                    
         LA    R2,CPNO             R2-->5-CHAR PROGRAM NUMBER                   
         LA    R3,FIVE             R3=LOOP COUNTER                              
*                                                                               
VP220    MVC   DIGIT,0(R2)         VALIDATE DIGIT IN 5-CHAR PRGM#               
         MVI   VALUE,36             SET UP FOR VALIDATE                         
         BAS   RE,DGTVLU                                                        
         BNE   VP230               INVALID DIGIT, EXIT WITH CC <> 0             
         LA    R2,1(R2)            BUMP TO NEXT DIGIT                           
         BCT   R3,VP220                                                         
         B     VPX20                                                            
*                                                                               
VP230    LA    R9,CCNEQUAL                                                      
         OI    RETCODE,RCIDGTQ     INVALID BASE-36 DIGIT                        
         B     VPX20                                                            
*                                                                               
*------------------------- INITIALIZE & EXIT -------------------------*         
*                                                                               
VPX10    BAS   RE,CLEAR1           INITIALIZE 5-CHAR PRGM#                      
         LR    RE,R9                                                            
         BR    RE                                                               
*                                                                               
VPX20    BAS   RE,CLEAR3           INITIALIZE THE OTHERS                        
         LR    RE,R9                                                            
         BR    RE                                                               
***********************************************************************         
         EJECT                                                                  
***********************************************************************         
*=================== CONVERT TO BASE-36 ROUTINES =====================*         
*                                                                               
*---------------------------- CONTROLLER -----------------------------*         
*                                                                               
CNV36    NTR1                                                                   
*  AT ENTRY, R0 = MAX L(OUTPUT)                                                 
*            R3 = A(INPUT) (LENGTH IS 2 BYTES)                                  
*            R4 = A(TO STORE OUTPUT)                                            
*  AT EXIT,  R4-->CONVERTED #                                                   
*            CC IS SET, WITH ERROR CODE IN RETCODE                              
         SPACE 1                                                                
         XC    BASE10,BASE10                                                    
         MVC   BASE10+(L'BASE10-TWO)(TWO),0(R3)                                 
         BAS   RE,TOBASE36                                                      
         CLI   LBASE36,0           LENGTH SHOULDN'T BE ZERO,                    
         BNE   *+12                                                             
         OI    RETCODE,RCEL36Q                                                  
         B     CCNEQUAL                                                         
         CLM   R0,1,LBASE36         AND SHOULD BE <=  MAX LEN                   
         BNL   *+12                                                             
         OI    RETCODE,RCEL36Q                                                  
         B     CCNEQUAL                                                         
         SPACE 1                                                                
         AR    R4,R0               RIGHT-JUSTIFY BASE-36 # IN OUTPUT            
         ZIC   RE,LBASE36           AREA (LEFT-PAD W/ ZEROES)                   
         LR    RF,RE                                                            
         BCTR  R4,0                BUMP DESTINATION PTR BACK 1                  
         BCT   RF,*-2                                                           
         BCTR  RE,0                SET UP FOR EXMVC                             
         EXMVC RE,0(R4),BASE36                                                  
         B     CCEQUAL                                                          
         EJECT                                                                  
*----------------------------- CONVERTER -----------------------------*         
*                                                                               
TOBASE36 NTR1                                                                   
*                                                                               
*  AT ENTRY, BASE10 CONTAINS # TO BE CONVERTED                                  
*  AT EXIT,  BASE36 CONTAINS CONVERTED #, LEFT-JUSTIFIED                        
*            LBASE36 = L(CONVERTED #)                                           
         SPACE 1                                                                
         MVI   BASE36,C'0'                                                      
         MVC   BASE36+1(L'BASE36-1),BASE36                                      
         SR    R2,R2                                                            
         ICM   R3,15,BASE10                                                     
         LA    R4,BASE36                                                        
         SR    R5,R5                                                            
*                                                                               
TO3610   D     R2,=F'36'                                                        
         STC   R2,VALUE                                                         
         CLI   VALUE,36                                                         
         BL    *+6                                                              
         DC    H'0'                REMAINDER CAN'T BE >=36                      
         BAS   RE,DGTVLU                                                        
         BE    *+6                                                              
         DC    H'0'                 NO REASON FOR ERRORS                        
         SPACE 1                                                                
         LR    RF,R4               INSERT DIGIT INTO BASE36                     
         AR    RF,R5                FROM THE LEFT                               
         B     TO3615A                                                          
TO3615   BCTR  RF,0                                                             
         MVC   1(1,RF),0(RF)        BUMP A DIGIT OVER TO RIGHT                  
TO3615A  CR    R4,RF                                                            
         BE    TO3620                                                           
         BL    TO3615                                                           
         DC    H'0'                                                             
*                                                                               
TO3620   MVC   0(L'DIGIT,RF),DIGIT                                              
         LA    R5,1(R5)                                                         
         SR    R2,R2                                                            
         OR    R3,R3                                                            
         BNZ   TO3610                                                           
         STC   R5,LBASE36                                                       
         SPACE 1                                                                
         B     EXIT                                                             
***********************************************************************         
         EJECT                                                                  
***********************************************************************         
*=================== CONVERT TO BASE-10 ROUTINES =====================*         
*                                                                               
*---------------------------- CONTROLLER -----------------------------*         
*                                                                               
CNV10    NTR1                                                                   
*                                                                               
*  AT ENTRY, R1 = L(INPUT)                                                      
*            R2 = A(INPUT)                                                      
*            R3 = A(OUTPUT)                                                     
*  AT EXIT,  R3 = A(WHERE CONVERTED # IS STORED)                                
*            CC IS SET, WITH ERROR CODE IN RETCODE                              
         SPACE 1                                                                
         MVI   BASE36,C'0'                                                      
         MVC   BASE36+1(L'BASE36-1),BASE36                                      
         STC   R1,LBASE36                                                       
         BCTR  R1,0                                                             
         EXMVC R1,BASE36,0(R2)                                                  
         SPACE 1                                                                
         BAS   RE,TOBASE10                                                      
         BNE   CNV10XNO                                                         
         SPACE 1                                                                
         CLI   LBASE10,TWO                                                      
         BNH   *+12                                                             
         OI    RETCODE,RCOFLWQ                                                  
         B     CNV10XNO                                                         
         SPACE 1                                                                
         MVC   0(TWO,R3),BASE10+(L'BASE10-TWO)                                  
         B     CNV10XYS                                                         
*                                                                               
CNV10XNO B     CCNEQUAL                                                         
CNV10XYS B     CCEQUAL                                                          
         EJECT                                                                  
*----------------------------- CONVERTER -----------------------------*         
*                                                                               
TOBASE10 NTR1                                                                   
*                                                                               
*  AT ENTRY, BASE36 CONTAINS # TO CONVERT                                       
*            LBASE36 = L(# TO CONVERT)                                          
*  AT EXIT,  BASE10 CONTAINS CONVERTED #, RIGHT-JUSTIFIED                       
*            LBASE10 = L(CONVERTED #)                                           
*            CC IS SET, WITH ERROR CODE IN RETCODE                              
         SPACE 1                                                                
         XC    BASE10,BASE10                                                    
         SR    R2,R2                                                            
         SR    R3,R3                                                            
         ZIC   R5,LBASE36                                                       
         LA    R4,BASE36                                                        
*                                                                               
TO1010   MVC   DIGIT,0(R4)         GET VALUE OF DIGIT                           
         MVI   VALUE,36             SET UP DGTVLU ROUTINE                       
         BAS   RE,DGTVLU                                                        
         BE    *+6                                                              
         DC    H'0'                DIGIT SHOULD BE VALID                        
         ZIC   R0,VALUE                                                         
         AR    R3,R0                                                            
         BCT   R5,*+8                                                           
         B     TO1020                                                           
         M     R2,=F'36'                                                        
         LA    R4,1(R4)            BUMP TO NEXT DIGIT                           
         B     TO1010                                                           
*                                                                               
TO1020   OR    R2,R2               DON'T WANT OVERFLOW                          
         BZ    TO1025                                                           
         OI    RETCODE,RCOFLWQ     OVERFLOWED INTO FIRST REGISTER               
         B     TO10XNO                                                          
*                                                                               
TO1025   LR    R2,R3                                                            
TO1025A  LA    R5,1(R5)            COUNT L(CONVERTED #)                         
         SRL   R3,8                MOVE H.O.B. INTO L.O.B.                      
         OR    R3,R3                                                            
         BNZ   TO1025A                                                          
         SPACE 1                                                                
         STCM  R2,15,BASE10        R2 = CONVERTED #                             
         STC   R5,LBASE10          R5 = L(CONVERTED #)                          
         B     TO10XYES                                                         
*                                                                               
TO10XNO  B     CCNEQUAL                                                         
TO10XYES B     CCEQUAL                                                          
***********************************************************************         
         EJECT                                                                  
***********************************************************************         
*========================== DIGIT<-->VALUE ===========================*         
DGTVLU   NTR1                                                                   
*                                                                               
*  IF VALUE>=36, WE WANT TO DO DIGIT-->VALUE,                                   
*   ELSE WE WANT VALUE-->DIGIT                                                  
*                                                                               
*  AT EXIT, CC IS SET IF DIGIT OR VALUE IS INVALID                              
         SPACE 1                                                                
         CLI   VALUE,36                                                         
         BL    DV05                                                             
         MVC   BYTE,DIGIT          SET UP FOR DIGIT-->VALUE                     
         LA    R0,B36TDGT-B36TABLE                                              
         B     DV10                                                             
DV05     MVC   BYTE,VALUE          SET UP FOR VALUE-->DIGIT                     
         LA    R0,B36TVLU-B36TABLE                                              
         B     DV10                                                             
*                                                                               
DV10     LA    R4,B36TABLE                                                      
DV15     CLI   0(R4),0             AT END OF TABLE?                             
         BE    DGTVLUNO             YES, NOT GOOD                               
         LR    R2,R4                                                            
         AR    R2,R0                                                            
         CLC   BYTE,0(R2)          MATCH DIGIT/VALUE AGAINST TABLE              
         BE    DV20                 MATCHED, EXIT LOOP                          
         LA    R4,L'B36TABLE(R4)                                                
         B     DV15                                                             
*                                                                               
DV20     MVC   DIGIT(2),0(R4)      SET DIGIT & VALUE FIELDS                     
         B     DGTVLUYS                                                         
*                                                                               
DGTVLUYS B     CCEQUAL                                                          
DGTVLUNO B     CCNEQUAL                                                         
***********************************************************************         
         EJECT                                                                  
***********************************************************************         
*=========================== LITERAL POOL ============================*         
         LTORG                                                                  
***********************************************************************         
         SPACE 2                                                                
         DROP  RB,RC                                                            
         EJECT                                                                  
***********************************************************************         
*========================== BASE 36 DIGITS ===========================*         
B36TABLE DS    0CL(L'B36TDGT+L'B36TVLU)                                         
B36TDGT  DS    CL1                 BASE 36 DIGIT                                
B36TVLU  DS    AL1                 VALUE OF BASE 36 DIGIT                       
         ORG   B36TABLE                                                         
         DC    C'0',AL1(00)                                                     
         DC    C'1',AL1(01)                                                     
         DC    C'2',AL1(02)                                                     
         DC    C'3',AL1(03)                                                     
         DC    C'4',AL1(04)                                                     
         DC    C'5',AL1(05)                                                     
         DC    C'6',AL1(06)                                                     
         DC    C'7',AL1(07)                                                     
         DC    C'8',AL1(08)                                                     
         DC    C'9',AL1(09)                                                     
         DC    C'A',AL1(10)                                                     
         DC    C'B',AL1(11)                                                     
         DC    C'C',AL1(12)                                                     
         DC    C'D',AL1(13)                                                     
         DC    C'E',AL1(14)                                                     
         DC    C'F',AL1(15)                                                     
         DC    C'G',AL1(16)                                                     
         DC    C'H',AL1(17)                                                     
         DC    C'I',AL1(18)                                                     
         DC    C'J',AL1(19)                                                     
         DC    C'K',AL1(20)                                                     
         DC    C'L',AL1(21)                                                     
         DC    C'M',AL1(22)                                                     
         DC    C'N',AL1(23)                                                     
         DC    C'O',AL1(24)                                                     
         DC    C'P',AL1(25)                                                     
         DC    C'Q',AL1(26)                                                     
         DC    C'R',AL1(27)                                                     
         DC    C'S',AL1(28)                                                     
         DC    C'T',AL1(29)                                                     
         DC    C'U',AL1(30)                                                     
         DC    C'V',AL1(31)                                                     
         DC    C'W',AL1(32)                                                     
         DC    C'X',AL1(33)                                                     
         DC    C'Y',AL1(34)                                                     
         DC    C'Z',AL1(35)                                                     
         DC    X'00'                                                            
***********************************************************************         
         EJECT                                                                  
***********************************************************************         
*=========================== LOCAL STORAGE ===========================*         
WORKD    DSECT                                                                  
PARMS    DS    0CL(4*4)                                                         
P1ACTN   DS    XL1                 ACTION BYTE                                  
*                                   X'00' = AUTOCONVERT                         
*                                   X'01' = 3 FIELDS TO 1                       
*                                   X'02' = 1 FIELD TO 3                        
         ORG   P1ACTN                                                           
P1ACPNO  DS    A                   A(5-CHAR PROGRAM #)                          
P2ANPNO  DS    A                   A(2-BYTE NUMERIC PROGRAM #)                  
P3ATKNO  DS    A                   A(2-BYTE TRACK #)                            
P4ATTNO  DS    A                   A(2-BYTE TELECAST #)                         
DMCB     DS    6F                                                               
DUB      DS    D                                                                
FULL     DS    F                                                                
APARM    DS    A                   A(PARAMETER LIST)                            
HALF     DS    H                                                                
BYTE     DS    XL1                                                              
BASE36   DS    CL6                 BASE 36 REPRESENTATION OF A #                
LBASE36  DS    XL1                 L(THE BASE 36 REPRESENTATION)                
BASE10   DS    XL4                 BASE 10 OF A # IN HEX                        
LBASE10  DS    XL1                 L(THE BASE 10 REPRESENTATION)                
DIGIT    DS    CL1                 BASE 36 DIGIT                                
VALUE    DS    XL1                 VALUE OF BASE 36 DIGIT                       
ACTION   DS    XL1                                                              
RETCODE  DS    XL1                 RETURN CODE (X'01', X'02' RESERVED)          
RCIDGTQ  EQU   X'80'                INVALID BASE-36 DIGIT                       
RCOFLWQ  EQU   X'40'                BASE-36 # IS TOO BIG                        
RCEL36Q  EQU   X'20'                ERROR IN L(BASE-36 #)                       
*        EQU   X'02'                **RESERVED**                                
*        EQU   X'01'                **RESERVED**                                
*        EQU   X' 0'                **(LOW-ORDER NIBBLE) RESERVED**             
CPNO     DS    CL(FIVE)            5-CHAR PROGRAM #                             
NPNO     DS    XL(TWO)             2-BYTE NUMERICPROGRAM #                      
TKNO     DS    XL(TWO)             2-BYTE TRACK #                               
TTNO     DS    XL(TWO)             2-BYTE TELECAST #                            
WORKX    EQU   *                                                                
         SPACE 1                                                                
ONE      EQU   1                                                                
TWO      EQU   2                                                                
THREE    EQU   3                                                                
FIVE     EQU   5                                                                
***********************************************************************         
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'002DECABPNO  05/01/02'                                      
         END                                                                    
