*          DATA SET REGENDAY   AT LEVEL 003 AS OF 05/01/02                      
*CATALP REGENDAY                                                                
***********************************************************************         
*                                                                     *         
*         REGENDAY --- DAYPART VALIDATION                             *         
*                                                                     *         
* ------------------------------------------------------------------- *         
* UPDATE HISTORY:                                                     *         
*                                                                     *         
* 03APR92 (SKU) --- DATE OF BIRTH                                     *         
*                                                                     *         
***********************************************************************         
         TITLE 'REGENDAY - DAYPART VALIDATION'                                  
***********************************************************************         
*  INPUT:                                                                       
*              PARAMETER 1 =       A(PRODUCT REC)                               
*                        2 =       A(OUTPUT AREA)                               
*                        3 =       A(DATAMGR)                                   
*                        4 =       A(COMFACS)                                   
*                        5 =       A(CALLOV)                                    
*                        6 =       A(MEDGET)                                    
*                                                                               
*  OUTPUT:                                                                      
*              RETURN TWADAYPT WITH DAYPART MENU                                
*                                                                               
***********************************************************************         
REGENDAY CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 WORKX-WORKD,REGENDAY                                             
         USING WORKD,RC                                                         
*                                                                               
         MVC   APRDREC,0(R1)                                                    
         MVC   AIOAREA,4(R1)                                                    
         MVC   VDATAMGR,8(R1)                                                   
         MVC   VCOMFACS,12(R1)                                                  
         MVC   VCALLOV,16(R1)                                                   
         MVC   VMEDGET,20(R1)                                                   
*                                                                               
         MVI   DMINBTS,0           DO NOT PASS DELETED RECORDS                  
         MVI   WHEREAMI,INREP      INDICATE CURRENT SYSTEM                      
*                                                                               
         BAS   RE,GETREP           WILL SWITCH TO SPOT                          
         BNZ   NODAYPT                                                          
*                                                                               
         BAS   RE,SPOTCLI          BUILD INFO FOR DAYPART KEY                   
         BNZ   NODAYPT                                                          
*                                                                               
         BAS   RE,SPOTEST          GET EST THEN DAYPART MENU                    
         BZ    EXIT                                                             
*                                                                               
NODAYPT  DS    0H                                                               
         L     R2,AIOAREA          RETURN NULLS TO CALLER                       
         XC    0(L'WORK,R2),0(R2)  SHOULD BE AT LEAST 36 BYTES                  
*                                                                               
EXIT     DS    0H                                                               
         CLI   WHEREAMI,INREP      SWITCH BACK TO REP                           
         BE    EXXIT                                                            
         BAS   RE,SWIREP                                                        
*                                                                               
EXXIT    XMOD1                                                                  
         EJECT                                                                  
***********************************************************************         
*        YES, NO OR XIT.  CC ^0 IF NO                                           
***********************************************************************         
         PRINT GEN                                                              
         ANSR                                                                   
         PRINT NOGEN                                                            
         EJECT                                                                  
***********************************************************************         
*        GETREP --- GET THE REP AND SPOTPAK INTERFACE INFO                      
***********************************************************************         
GETREP   NTR1                                                                   
*                                                                               
         L     R2,APRDREC                                                       
         USING RPRDREC,R2                                                       
*                                                                               
         XC    KEY,KEY                                                          
         LA    R6,KEY                                                           
         USING RREPREC,R6          REP RECORD                                   
         MVI   RREPKTYP,X'01'                                                   
         MVC   RREPKREP,RPRDKREP                                                
         DROP  R2                                                               
*                                                                               
         BAS   RE,REPHIGH                                                       
         CLC   KEY(L'RREPKEY),KEYSAVE                                           
         BNE   NO                                                               
*                                                                               
         BAS   RE,REPGET                                                        
*                                                                               
         L     R6,AIOAREA                                                       
         USING RREPSPOT,R6                                                      
         MVC   DATADISP,=H'34'                                                  
         MVI   ELCODE,5            SPOTPAK INTERFACE CODES                      
         BAS   RE,GETEL                                                         
         BNE   NO                                                               
*                                                                               
         MVC   SPTAGY,RREPSPPC                                                  
         MVC   SPTMED,RREPMED                                                   
*                                                                               
         GOTO1 SPOTCT,DMCB,RREPSPPC                                             
         BNZ   NO                                                               
         BAS   RE,SWISPOT                                                       
         BNZ   NO                                                               
         GOTO1 SPOTMED,DMCB,RREPMED,RREPSPPC,WORK                               
         BNZ   NO                                                               
         B     YES                                                              
         DROP  R6                                                               
         EJECT                                                                  
***********************************************************************         
*        SPOTCT --- LOOK AT THE CONTROL FILE FOR SPOTPAK INFO                   
*                                                                               
*        P1    =    A(SPOTPAK POWER CODE)                                       
***********************************************************************         
SPOTCT   NTR1                                                                   
*                                                                               
         L     R2,0(R1)                                                         
*                                                                               
         L     R6,AIOAREA                                                       
         USING CT5KEY,R6                                                        
         XC    0(L'CT5KEY,R6),0(R6)  CLEAR KEY                                  
         MVI   CT5KTYP,CT5KTYPQ    SYSTEM ACCESS RECORD                         
         MVC   CT5KALPH,0(R2)                                                   
         GOTO1 VDATAMGR,DMCB,=C'DMREAD',=C'CTFILE',(R6),(R6),0                  
         CLI   DMCB+8,0                                                         
         BNE   NO                                                               
*                                                                               
         USING CTSYSD,R6                                                        
         MVC   DATADISP,=H'28'                                                  
         MVI   ELCODE,CTSYSELQ     X'21' ELEMENT (SYS AUTH)                     
         BAS   RE,GETEL                                                         
         BNE   NO                                                               
         CLI   CTSYSNUM,X'02'      SPOT?                                        
         BNE   NO                                                               
*                                                                               
         MVC   SPOTSYS#,CTSYSSE    SE NUMBER (FOR TASK ID)                      
         MVC   SPOTAGY#,CTSYSAGB   BINARY AGY/COMPANY                           
*                                                                               
         B     YES                                                              
         DROP  R6                                                               
         EJECT                                                                  
***********************************************************************         
*        SPOTMED --- VALIDATE AND EXPAND SPOTPAK MEDIA CODE                     
*                                                                               
*        P1      =   A(SPOTPAK MEDIA CODE)                                      
*        P2      =   A(SPOTPAK AGENCY CODE)                                     
*        P3      =   A(40 BYTE WORK AREA)                                       
*                      ON RETURN, BYTE 1 = SPOTPAK AGY/MEDIA CODE               
*                                 BYTES 2-11 = MEDIA NAME                       
*                                                                               
***********************************************************************         
SPOTMED  NTR1                                                                   
*                                                                               
         LM    R2,R4,0(R1)         GET PARAMS                                   
*                                                                               
         XC    DMCB(12),DMCB                                                    
         ST    R3,DMCB             LOAD A(AGENCY CODE)                          
         MVC   DMCB(1),0(R2)       LOAD (MEDIA CODE)                            
         MVC   DMCB+4(4),VDATAMGR                                               
         ST    R4,DMCB+8           LOAD A(WORK AREA)                            
         GOTO1 VMEDGET,DMCB                                                     
         CLI   8(R1),X'FF'                                                      
         BE    NO                                                               
         MVC   SPOTMED#,0(R4)                                                   
         B     YES                                                              
         EJECT                                                                  
***********************************************************************         
*        SPOTCLI --- VALIDATE SPOTPAK CLIENT/ASSIGN SPOTPAK INFO                
*                                                                               
*        P1      =   A(INPUT CLIENT CODE)                                       
*                                                                               
***********************************************************************         
SPOTCLI  NTR1                                                                   
*                                                                               
         L     R6,APRDREC                                                       
         USING RPRDSPOT,R6                                                      
         MVC   DATADISP,=H'34'                                                  
         MVI   ELCODE,3            SPOTPAK INTERFACE ELEMENT                    
         BAS   RE,GETEL                                                         
         BNE   NO                                                               
*                                                                               
         MVC   SPTCLT,RPRDSPCL     CLIENT CODE                                  
         MVC   SPTPRD,RPRDSPP1     PRODUCT CODE                                 
         MVC   SPTEST,RPRDSPES     ESTIMATE NUMBER                              
         DROP  R6                                                               
*                                                                               
         XC    DMCB(8),DMCB                                                     
         MVC   DMCB+4(4),=X'D9000A14'                                           
         GOTO1 VCALLOV,DMCB                                                     
         L     RF,DMCB                                                          
         GOTO1 (RF),DMCB,SPTCLT,SPOTCLI#                                        
         CLI   DMCB,0                                                           
         BNE   NO                                                               
         B     YES                                                              
         EJECT                                                                  
***********************************************************************         
*        SPOTEST --- GET SPOTPAK ESTIMATE REC                                   
*                                                                               
*        P1      =   A(INPUT PRODUCT CODE)                                      
*        P2      =   A(INPUT ESTIMATE CODE (BINARY))                            
*                                                                               
***********************************************************************         
SPOTEST  NTR1                                                                   
*                                                                               
         XC    KEY,KEY             ESTIMATE HEADER RECORD                       
         LA    R5,KEY                                                           
         USING SPEST,R5                                                         
         MVC   EKEYAM,SPOTMED#                                                  
         MVC   EKEYCLT,SPOTCLI#                                                 
         MVC   EKEYPRD,SPTPRD                                                   
         MVC   EKEYEST,SPTEST                                                   
         BAS   RE,SPOTHIGH                                                      
         CLC   KEY(L'EKEY),KEYSAVE                                              
         BNE   NO                                                               
         DROP  R5                                                               
*                                                                               
         BAS   RE,SPOTGET                                                       
*                                                                               
         L     R5,AIOAREA                                                       
         USING ESTHDR,R5                                                        
         XC    KEY,KEY             DAY HEADER RECORD                            
         LA    R4,KEY                                                           
         USING DPTKEY,R4                                                        
         MVI   DPTKTYPE,X'08'                                                   
         MVC   DPTKAGY,SPTAGY                                                   
         MVC   DPTKMED,SPTMED                                                   
         MVC   DPTKMENU,EDAYMENU                                                
         BAS   RE,SPOTHIGH                                                      
         CLC   KEY(L'DPTKEY),KEYSAVE                                            
         BNE   NO                                                               
         DROP  R4,R5                                                            
*                                                                               
         BAS   RE,SPOTGET                                                       
*                                                                               
* GET FIRST LETTER OF EACH DAYPART CODES                                        
*                                                                               
         XC    WORK,WORK           CLEAR ALL 40 BYTES,(NEED 36)                 
         LA    R3,WORK                                                          
         L     R4,AIOAREA                                                       
         USING DPTHDR,R4                                                        
         LA    R5,DPTCODES                                                      
         DROP  R4                                                               
         LA    R6,36               MAX 36 CODES                                 
*                                                                               
SPTEST10 DS    0H                                                               
         MVC   0(1,R3),0(R5)       MOVE FIRST LETTER                            
         CLC   =XL2'00',0(R5)      LAST 2 BYTES ALWAYS 00'S                     
         BE    SPTEST20                                                         
         LA    R3,1(R3)                                                         
         LA    R5,5(R5)            BUMP TO NEXT CODE                            
         BCT   R6,SPTEST10                                                      
*                                                                               
SPTEST20 DS    0H                                                               
         MVC   0(L'WORK,R4),WORK   MOVE LETTERS TO IOAREA                       
         B     YES                                                              
         EJECT                                                                  
***********************************************************************         
*        SWITCH TO SPOT                                                         
***********************************************************************         
SWISPOT  NTR1                                                                   
*                                                                               
         MVI   WHEREAMI,INSPOT     CURRENT SYSTEM IS SPOT                       
*                                                                               
         L     RE,VCOMFACS                                                      
         USING COMFACSD,RE                                                      
         L     RF,CSWITCH                                                       
         DROP  RE                                                               
*                                                                               
         XC    DMCB(8),DMCB                                                     
         MVC   DMCB(1),SPOTSYS#    SPOT SYSTEM NUMBER                           
         GOTO1 (RF),DMCB                                                        
*                                                                               
         CLI   4(R1),2             SWITCHED, BUT SYSTEM IS NOT                  
         BE    NO                    OPERATIONAL                                
*                                                                               
         CLI   4(R1),0             ALL OTHER ERRORS ARE FATAL                   
         BE    *+6                                                              
         DC    H'0'                                                             
         B     YES                                                              
         EJECT                                                                  
***********************************************************************         
*        SWITCH BACK TO REP                                                     
***********************************************************************         
SWIREP   NTR1                                                                   
*                                                                               
         L     RE,VCOMFACS                                                      
         USING COMFACSD,RE                                                      
         L     RF,CSWITCH                                                       
         DROP  RE                                                               
*                                                                               
         GOTO1 (RF),DMCB,=C'REP ',0                                             
         CLI   4(R1),0                                                          
         BE    YES                                                              
         DC    H'0'                                                             
         EJECT                                                                  
***********************************************************************         
* DATAMGR CALLS                                                                 
***********************************************************************         
REPHIGH  MVC   FILE,=C'REPDIR  '                                                
         B     *+10                                                             
SPOTHIGH MVC   FILE,=C'SPTDIR  '                                                
         MVC   KEYSAVE,KEY                                                      
*                                                                               
DIR      NTR1                                                                   
         MVC   KEYSAVE,KEY                                                      
         GOTO1 VDATAMGR,DMCB,(DMINBTS,=C'DMRDHI'),FILE,KEYSAVE,KEY,0            
         B     XIT                                                              
*                                                                               
REPGET   MVC   FILE,=C'REPFIL  '                                                
         MVC   DISKADD,KEY+28                                                   
         B     GETREC                                                           
*                                                                               
SPOTGET  MVC   FILE,=C'SPTFIL  '                                                
         MVC   DISKADD,KEY+14                                                   
*                                                                               
GETREC   NTR1                                                                   
         GOTO1 VDATAMGR,DMCB,(DMINBTS,=C'GETREC'),FILE,DISKADD,AIOAREA,X        
               DMWORK                                                           
         B     XIT                                                              
*                                                                               
         GETEL R6,DATADISP,ELCODE                                               
         EJECT                                                                  
WORKD    DSECT                                                                  
DMWORK   DS    12D                                                              
KEY      DS    CL32                                                             
KEYSAVE  DS    CL32                                                             
DMCB     DS    6F                                                               
DUB      DS    D                                                                
DMINBTS  DS    X                                                                
DISKADD  DS    XL4                                                              
FILE     DS    CL8                                                              
APRDREC  DS    A                   A(RPRDREC)                                   
VDATAMGR DS    A                   A(DATAMGR)                                   
VCOMFACS DS    A                   A(COMFACS)                                   
VCALLOV  DS    A                   A(CALLOV)                                    
VMEDGET  DS    A                   A(MEDGET)                                    
AIOAREA  DS    A                                                                
DATADISP DS    H                                                                
ELCODE   DS    X                                                                
WHEREAMI DS    C                   CURRENT SYSTEM                               
INREP    EQU   C'R'                                                             
INSPOT   EQU   C'S'                                                             
SPOTSYS# DS    X                   FACPACK SYSTEM NUMBER SPOT                   
SPOTAGY# DS    X                   SPOTPAK AGY NUMBER                           
SPOTMED# DS    X                   SPOTPAK AGY/MEDIA NUMBER                     
SPOTCLI# DS    XL2                 SPOTPAK CLIENT CODE PACKED                   
SPOTEST# DS    X                   SPOTPAK ESTIMATE CODE (BINARY)               
SPTAGY   DS    CL2                 SPOTPAK AGENCY CODE                          
SPTMED   DS    C                   SPOTPAK MEDIA                                
SPTCLT   DS    CL3                 SPOTPAK CLIENT CODE                          
SPTPRD   DS    CL3                 SPOTPAK PRODUCT CODE                         
SPTEST   DS    X                   SPOTPAK ESTIMATE NUMBER                      
WORK     DS    CL40                SPOTCLI                                      
WORKX    EQU   *                                                                
         EJECT                                                                  
* THE FOLLOWING ARE ++INCLUDE                                                   
*        REGENREPA                                                              
*        REGENPRD                                                               
*        SPGENEST                                                               
*        SPGENDAYPT                                                             
*        DDCOMFACS                                                              
*        CTGENFILE                                                              
         PRINT OFF                                                              
       ++INCLUDE REGENREPA                                                      
       ++INCLUDE REGENPRD                                                       
SPEST    DSECT                                                                  
       ++INCLUDE SPGENEST                                                       
SPDPT    DSECT                                                                  
       ++INCLUDE SPGENDAYPT                                                     
       ++INCLUDE DDCOMFACS                                                      
       ++INCLUDE CTGENFILE                                                      
         PRINT ON                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'003REGENDAY  05/01/02'                                      
         END                                                                    
