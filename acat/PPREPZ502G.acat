*          DATA SET PPREPZ502G AT LEVEL 022 AS OF 01/23/95                      
*CATALP PPZ502G                                                                 
         TITLE 'PPREPZ502 - PZ INVOICE CONVERSION'                              
         PRINT NOGEN                                                            
         SPACE                                                                  
***********************************************************************         
*                                                                               
***********************************************************************         
*                                                                     *         
         EJECT                                                                  
PPZ502   CSECT                                                                  
         NMOD1 0,*PPZ502*,R7                                                    
*                                                                               
         L     R9,0(R1)                                                         
         USING PPWORKD,R9                                                       
         MVI   RC2DSECT,C'Y'       2ND DSECT                                    
         L     RA,PPWORK2C                                                      
         USING PPWORK2D,RA                                                      
         LA    RC,SPACEND                                                       
         USING SPZ5WRKD,RC                                                      
         L     R5,=A(EZBLOCKC)                                                  
         USING EZBLOCKD,R5                                                      
*                                                                               
         CLI   MODE,PROCREQ                                                     
         BE    PROC                                                             
*                                                                               
EXIT     XIT1                                                                   
*                                                                               
EXITX    DS    0H                                                               
         L     RF,UTL                                                           
         MVC   4(1,RF),SAVSYS      RESTORE SYSTEM NUM                           
         XIT1                                                                   
         EJECT                                                                  
* REQUEST FIRST                                                                 
*                                                                               
PROC     DS    0H                                                               
         L     RF,UTL                                                           
         MVC   SAVSYS,4(RF)                                                     
         XC    HEADHOOK,HEADHOOK                                                
         MVI   FORCEHED,C'Y'                                                    
         MVI   P,C'*'                                                           
         MVC   P+1(131),P                                                       
         GOTO1 REPORT                                                           
*                                                                               
         CLI   FIRSTSW,0           TEST FIRST TIME                              
         BNE   PR10                                                             
         MVI   FIRSTSW,1                                                        
         MVI   XFF,X'FF'                                                        
         MVC   XFF+1(L'XFF-1),XFF                                               
         MVI   DASHES,C'-'                                                      
         MVC   DASHES+1(L'DASHES-1),DASHES                                      
         SPACE                                                                  
*                                                                               
PR10     DS    0H                                                               
*                                   SET EZBLOCK VALUES                          
         LA    RE,EZBLOCKD                                                      
         LA    RF,EZBLOCKL                                                      
         XCEF                                                                   
*                                                                               
         LA    R0,MPLTABLN                                                      
         LA    R1,MPLTAB                                                        
MPLSET10 CLC   QOPT5,0(R1)       SYSTEM ID - SEE SSB                            
         BE    MPLSET20                                                         
         LA    R1,MPLENTLN(,R1)           NEXT ENTRY                            
         BCT   R0,MPLSET10                                                      
         DC    H'0'                                                             
MPLSET20 MVC   CONSEMPL,8(R1)      SAVE SENUM                                   
         MVC   EZCONSE,8(R1)                  IN EZBLOCK ALSO                   
         MVI   EZOFF,C'Y'          OFF-LINE                                     
         SPACE                                                                  
         L     RE,=A(WRKRBUFF)                                                  
         ST    RE,EZWKRBUF                                                      
         LH    RF,=AL2(WRKRBEND-WRKRBUFF)                                       
         XCEF                                                                   
         L     RE,=A(WRKRREC)                                                   
         ST    RE,EZWKRREC                                                      
         LH    RF,=AL2(WRKRRECE-WRKRREC)                                        
         XCEF                                                                   
*                                                                               
         XC    EZWKRIND,EZWKRIND                                                
         MVC   EZWKRIND(2),RCORIGID                                             
         SPACE                                                                  
         GOTO1 DATAMGR,DMCB,(0,=C'GFILE'),=C'WRKFIL',EZWKRIND,EZWKRREC,C        
               EZWKRBUF                                                         
         LA    R1,EZWKRIND                                                      
         USING UKRECD,R1                                                        
         MVC   EZWKRFIL,UKUSRINF                                                
         DROP  R1                                                               
         SPACE                                                                  
         LA    RF,EZMPROC                                                       
         ST    RF,EZHOOK                                                        
         L     RF,PPFILEC                                                       
         LA    RF,PBUYREC-PPFILED(RF)                                           
         ST    RF,EZAREC                                                        
         L     RF,=V(EZRECTAB)     RECORD/FIELD TABLE                           
         ST    RF,EZARECTB                                                      
         L     RE,=A(PRTABC)       PRODUCT TABLE                                
         ST    RE,EZPRTAB                                                       
         MVC   EZPRTABL,=Y(PRTABL)                                              
         LH    RF,=AL2(PRTABE-PRTABC)                                           
         XCEF                                                                   
*                                                                               
         MVC   EZPRINT,PRINT                                                    
         MVC   EZUTL,UTL                                                        
         MVC   EZCOMFCS,VCOMFACS                                                
         MVC   EZPRNTSE,PRNTSE                                                  
*                                                                               
         MVC   EZWRITE,RCWRITE                                                  
         MVC   EZAGY,QAGENCY                                                    
         MVC   EZPROF2,PROGPROF    Z5 PROFILE                                   
*                                                                               
         MVI   ACTVSW,C'N'                                                      
         MVI   FORCEHED,C'Y'                                                    
*                                                                               
         EJECT                                                                  
*                                  SET OPTIONS                                  
*                                  -----------                                  
*                                                                               
         MVC   EZTEST,QOPT1        TEST OPTION                                  
         CLI   QOPT1,C'X'          TEST OPTION WITH DATA ERROR REPORT 9         
         BNE   *+8                                                              
         MVI   EZTEST,C'Y'         ALSO MEANS TEST OPTION                       
*                                                                               
         MVI   EZTRACE,0           TRACE OPTION                                 
         CLI   QOPT2,C'N'                                                       
         BE    PR20                                                             
         CLI   QOPT2,C' '                                                       
         BE    PR20                                                             
         CLI   QOPT2,C'A'                                                       
         BNE   *+8                                                              
         MVI   EZTRACE,X'FF'       TRACE EVERYTHING                             
         CLI   QOPT2,C'E'                                                       
         BNE   *+8                                                              
         MVI   EZTRACE,X'20'       TRACE ERRORS                                 
         CLI   QOPT2,C'M'                                                       
         BNE   *+8                                                              
         MVI   EZTRACE,X'60'       TRACE MODES AND ERRORS                       
         CLI   QOPT2,C'F'                                                       
         BNE   *+8                                                              
         MVI   EZTRACE,X'E0'       TRACE FIELDS AND ERRORS AND MODES            
         CLI   QOPT2,C'R'                                                       
         BNE   *+8                                                              
         MVI   EZTRACE,X'74'       TRACE MODES, ERRORS, AND ADDED RECS          
*                                                                               
PR20     DS    0H                                                               
         MVC   WRTOPT,QOPT3        Y=WRITE TEST                                 
         MVC   EZRPLOPT,QOPT4      Y=OK TO REPLACE INVOICES                     
*                                                                               
*                               READ WORKER FILE INDEXES                        
*                               -------------------------                       
*                                                                               
PR24     DS    0H                                                               
         XC    EZWKRIND,EZWKRIND                                                
         B     PR32                                                             
*                                                                               
PR30     DS    0H                                                               
         OI    EZWKRIND+UKFLAG-UKRECD,UKFLDAT                                   
         MVC   EZWKRIND+UKCIADDR-UKRECD(2),SVCIADDR                             
         GOTO1 DATAMGR,DMCB,=C'INDEX',EZWKRFIL,EZWKRIND,EZWKRREC,      C        
               EZWKRBUF                                                         
         TM    DMCB+8,X'80'        TEST EOF                                     
         BZ    *+6                                                              
         DC    H'0'                                                             
PR32     DS    0H                                                               
         GOTO1 DATAMGR,DMCB,=C'INDEX',EZWKRFIL,EZWKRIND,EZWKRREC,      C        
               EZWKRBUF                                                         
         TM    DMCB+8,X'80'        TEST EOF                                     
         BNZ   PR60                                                             
*                                                                               
         LA    R4,EZWKRIND                                                      
         USING UKRECD,R4                                                        
         CLI   UKDAY,X'98'         TEST AN EASI FILE                            
         BNE   PR32                                                             
*                                                                               
PR34     DS    0H                                                               
         USING EZWKRIXD,R4                                                      
*                                                                               
         CLC   EZWIUID,RCORIGID    TEST RIGHT ID                                
         BNE   PR32                                                             
*                                                                               
PR36     DS    0H                                                               
         MVC   SVCIADDR,EZWKRIND+UKCIADDR-UKRECD                                
*                                  READ FIRST RECORD                            
         GOTO1 DATAMGR,DMCB,=C'READ',EZWKRFIL,EZWKRIND,EZWKRREC,       X        
               EZWKRBUF                                                         
         TM    DMCB+8,X'80'        IF EOF ON FIRST READ                         
         BNZ   PR30                SKIP- GET NEXT INDEX                         
*                                                                               
         L     R3,EZWKRBUF         SAVE 64 BYTE DESCRIPTOR REC                  
         MVC   SVWFREC0,0(R3)                                                   
*                                                                               
         USING W_RECD,R3                                                        
         OC    QSTART(12),QSTART   ANY REQUEST DATE                             
         BZ    PR40                 NO                                          
         CLC   QSTART(12),SPACES   ANY REQUEST DATE                             
         BE    PR40                 NO                                          
         GOTO1 DATCON,DMCB,(2,W_AGELD),(0,WORK)                                 
         CLC   WORK(6),QSTART                                                   
         BL    PR30                 BYPASS                                      
         CLC   WORK(6),QEND                                                     
         BH    PR30                 BYPASS                                      
         SPACE                                                                  
PR40     DS   0H                                                                
*                                                                               
PR44     DS    0H                                                               
         MVI   UNCONVSW,C'N'       CLEAR UNCONVERTED SW                         
         MVI   CONVSW,C'N'         AND CONVERTED THIS TIME SW                   
         SPACE                                                                  
         L     RE,EZWKRBUF                                                      
         LA    RF,W_DESC-W_RECD(,RE)                                            
         MVC   EZSRCE,EZWCSRCE-EZWKRCMD(RF)                                     
         CLI   QUESTOR,C'A'                                                     
         BNH   *+10                                                             
         MVC   EZSELINV,QUESTOR                                                 
         OI    EZLOOKSW,EZL2PASS   SET FOR SECOND DETAIL PASS                   
         ORG   *-4                                                              
         BC    0,0                                                              
         SPACE                                                                  
         GOTO1 =V(PZMOD),DMCB,EZBLOCKD                                          
*                                                                               
         CLI   CONVSW,C'Y'         TEST ANY CONVERTED THIS TIME                 
         BNE   PR50                NO- NEXT INDEX                               
         CLI   EZTEST,C'Y'         TEST SUPPRESSING FILE MARKING                
         BE    PR50                 YES- SKIP                                   
         CLI   EZWRITE,C'Y'        WRITE TO FILE                                
         BNE   PR50                 NO- SKIP                                    
*                                                                               
         XC    WORK,WORK           BUILD COMMENT IN WORK                        
         SPACE                                                                  
         MVC   WORK(16),SVWFREC0+48  FROM SAVED COMMENT                         
         SPACE                                                                  
         MVI   WORK,X'80'          STOP EZMOD DROPPING BLANKS                   
         GOTO1 DATCON,DMCB,(0,TODAY),(1,WORK+1)  PWOS                           
         CLI   UNCONVSW,C'Y'       TEST ANY REMAIN UNCONVERTED                  
         BE    *+8                                                              
         OI    WORK,X'40'          SET FILE COMPLETELY CONVERTED                
*                                                                               
         L     RF,EZWKRREC         SET COMMENT                                  
         XC    0(96,RF),0(RF)                                                   
         LA    R3,28(RF)                                                        
         USING W_RECD,R3                                                        
         MVC   W_DESC,WORK                                                      
         SPACE                                                                  
         GOTO1 DATAMGR,DMCB,=C'COMMENT',EZWKRFIL,EZWKRIND,EZWKRREC,    X        
               EZWKRBUF                                                         
         SPACE                                                                  
         CLI   UNCONVSW,C'Y'       TEST ANY REMAIN UNCONVERTED                  
         BE    PR50                                                             
         SPACE                                                                  
         GOTO1 DATAMGR,DMCB,=C'KEEP',EZWKRFIL,EZWKRIND,EZWKRREC,       X        
               EZWKRBUF                                                         
         SPACE                                                                  
PR50     DS    0H                                                               
         B     PR30                NEXT INDEX                                   
*                                                                               
PR60     DS    0H                                                               
         B     EXITX                                                            
         DROP  R3,R4                                                            
         SPACE 2                                                                
         EJECT                                                                  
*        EZMOD PROCESSING ROUTINE                                               
*                                                                               
EZMPROC  NTR1                                                                   
         CLI   WRTOPT,C'Y'        WRITE TEST                                    
         BNE   EPX                                                              
         CLI   EZMODE,EZINVP       PROC INV                                     
         BNE   EP04                                                             
         MVC   SVRNO1,EZRECSEQ     SAVE INVOICE HEADER REC #                    
         B     EPX                                                              
*                                                                               
EP04     DS    0H                                                               
         CLI   EZMODE,EZINVL       INV LAST                                     
         BNE   EPX                                                              
*                                                                               
         MVC   SVRNO2,EZRECSEQ     SAVE CURRENT REC #                           
*                                                                               
         L     RF,EZWKRREC         SET FOR 'RANDOM' TO READ PREVIOUS            
         XC    0(12,RF),0(RF)                                                   
         MVC   2(2,RF),SVRNO1                                                   
         MVC   4(4,RF),=C'REC '                                                 
         GOTO1 DATAMGR,DMCB,(0,=C'RANDOM'),EZWKRFIL,EZWKRIND,EZWKRREC, X        
               EZWKRBUF                                                         
*                                                                               
         L     RF,EZWKRREC                                                      
         CLC   4(2,RF),=C'31'      SHOULD BE INVOICE HEADER                     
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
*                                                                               
         L     RF,EZWKRREC         MARK RECORD AND WRITE BACK                   
         LA    RF,7(RF)                                                         
         MVC   15(1,RF),QUESTOR+11                                              
*                                                                               
         MVC   PL(3),=C'***'                                                    
         EDIT  (B2,SVRNO1),(4,PL+4),FILL=0                                      
         L     RF,EZWKRREC                                                      
         MVC   PL+10(100),0(RF)                                                 
         GOTO1 PRINT,DMCB,PLM1,=C'BL01'                                         
*                                                                               
         GOTO1 DATAMGR,DMCB,=C'WRITE',EZWKRFIL,EZWKRIND,EZWKRREC,      X        
               EZWKRBUF                                                         
*                                                                               
         L     RF,EZWKRREC         'RANDOM' BACK TO CURRENT RECORD              
         XC    0(12,RF),0(RF)                                                   
         MVC   2(2,RF),SVRNO2                                                   
         MVC   4(4,RF),=C'REC '                                                 
         GOTO1 DATAMGR,DMCB,(0,=C'RANDOM'),EZWKRFIL,EZWKRIND,EZWKRREC, X        
               EZWKRBUF                                                         
*                                                                               
         MVC   PL(3),=C'==='                                                    
         EDIT  (B2,SVRNO2),(4,PL+4),FILL=0                                      
         L     RF,EZWKRREC                                                      
         MVC   PL+10(100),0(RF)                                                 
         GOTO1 PRINT,DMCB,PLM1,=C'BL01'                                         
*                                                                               
EPX      DS    0H                                                               
         XIT1                                                                   
*                                                                               
         SPACE 2                                                                
DIV      DIV   (R0),(RF)       RETURNS VIA RE                                   
         SPACE                                                                  
* OPEN MPL FILE AFTER FINDING OUT WHICH ONE TO OPEN *                           
         SPACE                                                                  
MPLOPN   NTR1                                                                   
         SPACE                                                                  
         ICM   RF,15,UTL           MUST BE A UTL ADDRESS                        
         BNZ   *+6                                                              
         DC    H'0'                                                             
         SPACE                                                                  
         MVC   4(1,RF),CONSEMPL                                                 
         SPACE                                                                  
         L     RF,PPFILEC                                                       
         LA    R2,PBUYREC-PPFILED(RF)   USE AS WORK AREA FOR OPEN               
         GOTO1 DATAMGR,DMCB,=C'OPEN',=CL7'MPL',FILELIST,(R2)                    
         MVI   EZMPLOPN,C'Y'                                                    
         B     EXIT                                                             
         SPACE                                                                  
FILELIST DS    0X                                                               
         DC    CL8'UMPLDIR'                                                     
         DC    CL8'UMPLFIL'                                                     
         DC    CL10'X'                                                          
CONSEMPL DC    X'05'                                                            
         SPACE                                                                  
MPLTAB   DC    C'1',CL7'MPL1',X'05' ADV 1 USE MPL1                              
         DC    C'2',CL7'MPL2',X'35'  "  2     MPL2                              
         DC    C'3',CL7'MPL3',X'55'  "  3     MPL3                              
         DC    C'4',CL7'MPL4',X'45'  "  4     MPL4                              
         DC    C'5',CL7'MPL5',X'85'  "  5     MPL5                              
         DC    C'6',CL7'MPL6',X'75'  "  6     MPL6                              
         DC    C'R',CL7'MPLQ',X'65' REP       MPLQ                              
         DC    C'T',CL7'MPL1',X'05' TST USE MPL1 ALSO                           
MPLENTLN EQU   9                                                                
MPLTABLN EQU   (*-MPLTAB)/9                                                     
         EJECT                                                                  
         LTORG                                                                  
*                                                                               
         EJECT                                                                  
*                                                                               
         DS    0D                                                               
         DC    CL8'*WKRBUF*'                                                    
WRKRBUFF DS    14336X                                                           
WRKRBEND EQU   *                                                                
         DS    0D                                                               
         DC    CL8'*WKRREC*'                                                    
WRKRREC  DS    4096X                                                            
WRKRRECE EQU   *                                                                
         DS    0D                                                               
         DC    CL8'*EZBLKF*'                                                    
EZBLOCKC DS    0X                                                               
         ORG   *+EZBLOCKL                                                       
         DS    0D                                                               
         DC    CL8'*PRTAB*'                                                     
PRTABC   DS    0X                                                               
PRTABL   EQU   24*300                                                           
         ORG   *+PRTABL                                                         
PRTABE   EQU   *                                                                
         DS    0D                                                               
SPZ5WRKD DSECT                                                                  
FIRSTSW  DS    X               0 = FIRST TIME, 01 = DRIVER INITIALIAZED         
*                              8 = AT LEAST 1 INVOICE CONVERTED                 
SAVSYS   DS    X                                                                
RERUN    DS    XL3                 RERUN DATE                                   
TRCOPT   DS    C                                                                
WRTOPT   DS    C                                                                
SVRNO1   DS    H                                                                
SVRNO2   DS    H                                                                
UNCONVSW DS    C                                                                
CONVSW   DS    C                                                                
ACTVSW   DS    CL1                                                              
*                                                                               
ADRIVER  DS    A                                                                
*                                                                               
DPGFILE  DS    CL8                                                              
DRIVER   DS    CL8                                                              
SPDRIVER DS    CL8                                                              
XFF      DS    XL20                                                             
X        DS    XL256                                                            
*                                                                               
SVKEY    DS    CL32                                                             
SVCIADDR DS    XL2                                                              
         DS    0D                                                               
SVWFREC0 DS    XL64                                                             
*                                                                               
SVMGRMKT DS    XL2                                                              
SVMGRP   DS    CL5                                                              
SVBMKT   DS    CL4                                                              
SVBBMKT  DS    XL2                                                              
SVBMKTNM DS    CL20                                                             
SVBSTA   DS    CL7                                                              
SVBDSTA  DS    CL7                 SAVED BAD (DOES NOT EXIST) STATION           
SVBDATE  DS    CL8                                                              
*VBTIM   DS    CL4                                                              
*                                                                               
RPTNO    DS    XL1                                                              
MPLKEY   DS    CL40                                                             
MPLKEYSV DS    CL40                                                             
EQVSTAW  DS   0CL5                                                              
         DS    CL4                                                              
EQVMED   DS    CL1                                                              
*                                                                               
PLM1     DS    CL1                                                              
PL       DS    CL132                                                            
*                                                                               
         SPACE 2                                                                
         PRINT OFF                                                              
       ++INCLUDE PPREPWORK                                                      
       ++INCLUDE PPREPWORK2                                                     
       ++INCLUDE PPNEWFILE                                                      
       ++INCLUDE PPMODEQU                                                       
       ++INCLUDE DDCOMFACS                                                      
MASTD    DSECT                                                                  
       ++INCLUDE DDMASTC                                                        
       ++INCLUDE FASELIST                                                       
         EJECT                                                                  
       ++INCLUDE PZBLOCK                                                        
       ++INCLUDE PPGENPZ                                                        
         PRINT ON                                                               
*                                                                               
       ++INCLUDE DMWRKFD                                                        
       ++INCLUDE DMWRKFK                                                        
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'022PPREPZ502G01/23/95'                                      
         END                                                                    
