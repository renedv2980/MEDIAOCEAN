*          DATA SET TALDRCVD   AT LEVEL 001 AS OF 03/28/12                      
*CATALP TALDRCVD                                                                
         TITLE 'LDRCVD - TALENT - RECOVERY DUMP ROUTINE'                        
                                                                                
* PARAMS VIA R1                                                                 
* XL1    X'00'=INIT,X'01'=PROCESS,X'FF'=FINAL.                                  
*        RETURN X'FF'=DONT WRITE RECORD TO DUMP TAPE                            
*        RETURN X'FE'=DELETED REC,X'FD'=RESTORED REC                            
* AL3    A(RECOVERY FILE RECORD)                                                
                                                                                
         PRINT NOGEN                                                            
LDRCVD   CSECT                                                                  
         ENTRY LDRCVDT             TABLE OF COUNTERS AND ACTIONS                
                                                                                
         NMOD1 WORKX-WORKD,ACLDRCVD                                             
         USING WORKD,RC                                                         
         LR    RA,R1               RA=A(PARAMETER LIST)                         
         ST    R1,APARM                                                         
         SR    R3,R3                                                            
         ICM   R3,7,1(R1)          R3=A(RECOVERY HEADER)                        
         USING RCVRECD,R3                                                       
         CLI   0(R1),X'01'         TEST ACTION                                  
         BL    INIT                                                             
         BE    PROCESS                                                          
         BH    FINAL                                                            
                                                                                
INIT     LA    RE,LDRCVDT          CLEAR COUNTERS ON INIT CALL                  
INIT1    CLC   0(4,RE),=4X'FF'                                                  
         BE    EXIT                                                             
         XC    0(4,RE),0(RE)                                                    
         LA    RE,L'LDRCVDT(RE)                                                 
         B     INIT1                                                            
                                                                                
FINAL    B     EXIT                FINAL CALL                                   
                                                                                
LGCLDEL  MVI   0(RA),X'FE'         RETURN RECORD IS LOGICALLY DELETED           
         B     EXIT                                                             
                                                                                
LGCLRST  MVI   0(RA),X'FD'         RETURN RECORD IS LOGICALLY RESTORED          
         B     EXIT                                                             
                                                                                
DELETE   MVI   0(RA),X'FF'         RETURN DELETE RECORD FROM DUMP TAPE          
                                                                                
EXIT     XMOD1 1                                                                
                                                                                
         EJECT                                                                  
* PROCESS A RECOVERY FILE RECORD                                                
* UPDATE TABLE OF ANY ACTIONS THAT NEED PRINTING AND ERRORS/WARNINGS            
* EXIT VIA DELETE TO REQUEST RECORD NOT WRITTEN TO RECOVERY DUMP TAPE           
                                                                                
PROCESS  CLI   RFILTY,TALFILQ      TALFIL RECOVERY RECORD                       
         BE    FIL1                                                             
         CLI   RFILTY,TALDIRQ      TALDIR RECOVERY RECORD                       
         BE    DIR1                                                             
         CLI   RFILTY,CHKFILQ      CHKFIL RECOVERY RECORD                       
         BE    CHF1                                                             
         CLI   RFILTY,CHKDIRQ      CHKDIR RECOVERY RECORD                       
         BE    CHD1                                                             
         B     EXIT                                                             
                                                                                
DIR1     CLI   RRECTY,2            TEST TALDIR CHANGE                           
         BNE   SAVE                                                             
         TM    KEYSTAT,X'80'       TEST DELETED                                 
         BZ    DIR2                                                             
         L     RF,=A(LASTRCV)                                                   
         CLI   RFILTY-RCVHDR(RF),TALDIRQ                                        
         BNE   SAVE                                                             
         CLI   RRECTY-RCVHDR(RF),1 TEST LAST RECORD TALDIR COPY                 
         BNE   SAVE                                                             
         TM    KEYSTAT-RCVHDR(RF),X'80'                                         
         BO    SAVE                                                             
         CLC   KEYKEY,KEYKEY-RCVHDR(RF)                                         
         BNE   SAVE                                                             
         L     R0,RCVDIRD          BUMP TALDIR DELETED RECORDS                  
         AHI   R0,1                                                             
         ST    R0,RCVDIRD                                                       
         MVI   0(RA),X'FE'         RETURN RECORD IS LOGICALLY DELETED           
         B     SAVE                                                             
                                                                                
DIR2     L     RF,=A(LASTRCV)      TEST IF COPY RECORD WAS ACTIVE               
         CLI   RFILTY-RCVHDR(RF),TALDIRQ                                        
         BNE   SAVE                                                             
         CLI   RRECTY-RCVHDR(RF),1                                              
         BNE   SAVE                                                             
         TM    KEYSTAT-RCVHDR(RF),X'80'                                         
         BZ    SAVE                                                             
         CLC   KEYKEY,KEYKEY-RCVHDR(RF)                                         
         BNE   SAVE                                                             
         L     R0,RCVDIRR          BUMP TALDIR RESTORED RECORDS                 
         AHI   R0,1                                                             
         ST    R0,RCVDIRR                                                       
         MVI   0(RA),X'FD'         RETURN RECORD IS LOGICALLY RESTORED          
         B     SAVE                                                             
                                                                                
FIL1     CLI   RRECTY,2            TEST TALFIL CHANGE                           
         BNE   SAVE                                                             
         TM    RECSTAT,X'80'       TEST DELETED                                 
         BZ    FIL2                                                             
         L     RF,=A(LASTRCV)                                                   
         CLI   RFILTY-RCVHDR(RF),TALFILQ                                        
         BNE   SAVE                                                             
         CLI   RRECTY-RCVHDR(RF),1 TEST LAST RECORD TALFIL COPY                 
         BNE   SAVE                                                             
         TM    RECSTAT-RCVHDR(RF),X'80'                                         
         BO    SAVE                                                             
         CLC   RECKEY,RECKEY-RCVHDR(RF)                                         
         BNE   SAVE                                                             
         L     R0,RCVFILD          BUMP TALFIL DELETED RECORDS                  
         AHI   R0,1                                                             
         ST    R0,RCVFILD                                                       
         MVI   0(RA),X'FE'         RETURN RECORD IS LOGICALLY DELETED           
         B     SAVE                                                             
                                                                                
FIL2     L     RF,=A(LASTRCV)      TEST IF COPY RECORD WAS ACTIVE               
         CLI   RFILTY-RCVHDR(RF),TALFILQ                                        
         BNE   SAVE                                                             
         CLI   RRECTY-RCVHDR(RF),1                                              
         BNE   SAVE                                                             
         TM    RECSTAT-RCVHDR(RF),X'80'                                         
         BZ    SAVE                                                             
         CLC   RECKEY,RECKEY-RCVHDR(RF)                                         
         BNE   SAVE                                                             
         L     R0,RCVFILR          BUMP TALFIL RESTORED RECORDS                 
         AHI   R0,1                                                             
         ST    R0,RCVFILR                                                       
         MVI   0(RA),X'FD'         RETURN RECORD IS LOGICALLY RESTORED          
         B     SAVE                                                             
                                                                                
CHD1     CLI   RRECTY,2            TEST CHKDIR CHANGE                           
         BNE   SAVE                                                             
         TM    CHKSTAT,X'80'       TEST DELETED                                 
         BZ    DIR2                                                             
         L     RF,=A(LASTRCV)                                                   
         CLI   RFILTY-RCVHDR(RF),CHKDIRQ                                        
         BNE   SAVE                                                             
         CLI   RRECTY-RCVHDR(RF),1 TEST LAST RECORD TALDIR COPY                 
         BNE   SAVE                                                             
         TM    CHKSTAT-RCVHDR(RF),X'80'                                         
         BO    SAVE                                                             
         CLC   CHKKEY,CHKKEY-RCVHDR(RF)                                         
         BNE   SAVE                                                             
         L     R0,RCVCHDD          BUMP TALDIR DELETED RECORDS                  
         AHI   R0,1                                                             
         ST    R0,RCVCHDD                                                       
         MVI   0(RA),X'FE'         RETURN RECORD IS LOGICALLY DELETED           
         B     SAVE                                                             
                                                                                
CHD2     L     RF,=A(LASTRCV)      TEST IF COPY RECORD WAS ACTIVE               
         CLI   RFILTY-RCVHDR(RF),CHKDIRQ                                        
         BNE   SAVE                                                             
         CLI   RRECTY-RCVHDR(RF),1                                              
         BNE   SAVE                                                             
         TM    KEYSTAT-RCVHDR(RF),X'80'                                         
         BZ    SAVE                                                             
         CLC   CHKKEY,CHKKEY-RCVHDR(RF)                                         
         BNE   SAVE                                                             
         L     R0,RCVCHDR          BUMP TALDIR RESTORED RECORDS                 
         AHI   R0,1                                                             
         ST    R0,RCVCHDR                                                       
         MVI   0(RA),X'FD'         RETURN RECORD IS LOGICALLY RESTORED          
         B     SAVE                                                             
                                                                                
CHF1     CLI   RRECTY,2            TEST CHKFIL CHANGE                           
         BNE   SAVE                                                             
         TM    CHKSTAT,X'80'       TEST DELETED                                 
         BZ    FIL2                                                             
         L     RF,=A(LASTRCV)                                                   
         CLI   RFILTY-RCVHDR(RF),CHKFILQ                                        
         BNE   SAVE                                                             
         CLI   RRECTY-RCVHDR(RF),1 TEST LAST RECORD TALFIL COPY                 
         BNE   SAVE                                                             
         TM    CHKSTAT-RCVHDR(RF),X'80'                                         
         BO    SAVE                                                             
         CLC   CHKKEY,CHKKEY-RCVHDR(RF)                                         
         BNE   SAVE                                                             
         L     R0,RCVCHFD          BUMP CHKFIL DELETED RECORDS                  
         AHI   R0,1                                                             
         ST    R0,RCVCHFD                                                       
         MVI   0(RA),X'FE'         RETURN RECORD IS LOGICALLY DELETED           
         B     SAVE                                                             
                                                                                
CHF2     L     RF,=A(LASTRCV)      TEST IF COPY RECORD WAS ACTIVE               
         CLI   RFILTY-RCVHDR(RF),CHKFILQ                                        
         BNE   SAVE                                                             
         CLI   RRECTY-RCVHDR(RF),1                                              
         BNE   SAVE                                                             
         TM    CHKSTAT-RCVHDR(RF),X'80'                                         
         BZ    SAVE                                                             
         CLC   CHKKEY,CHKKEY-RCVHDR(RF)                                         
         BNE   SAVE                                                             
         L     R0,RCVCHFR          BUMP CHKFIL RESTORED RECORDS                 
         AHI   R0,1                                                             
         ST    R0,RCVCHFR                                                       
         MVI   0(RA),X'FD'         RETURN RECORD IS LOGICALLY RESTORED          
         B     SAVE                                                             
                                                                                
SAVE     L     R0,=A(LASTRCV)      SAVE RECOVERY RECORD                         
         LHI   R1,LASTRCVL         R1=MAXIMUM RECORD LENGTH                     
         LR    RE,R3                                                            
         AHI   RE,-4                                                            
         LH    RE,0(RE)                                                         
         AHI   RE,-4               RE=ACTUAL RECORD LENGTH                      
         BM    SAVE1                                                            
         CR    RE,R1                                                            
         BNL   SAVE1                                                            
         LR    R1,RE                                                            
SAVE1    LR    RE,R3                                                            
         LR    RF,R1                                                            
         MVCL  R0,RE                                                            
         B     EXIT                                                             
                                                                                
         LTORG                                                                  
                                                                                
* THIS ENTRY POINT TABLE IS PROCESSED BY RCVDMP CALLER AFTER FINAL CALL         
* EACH ENTRY HAS A COUNTER,TEXT,TYPE,AND SPARE                                  
* ENTRIES WITH A NON-ZERO COUNTER WILL BE PRINTED                               
* ERRORS/WARNINGS WILL GO TO CONSOLE AND WILL BE EMAILED                        
         DS    0F                                                               
LDRCVDT  DS    0CL24                                                            
RCVDIRD  DC    F'0',CL16'TALDIR DELETES  ',C'I ',AL2(0)                         
RCVDIRR  DC    F'0',CL16'TALDIR RESTORES ',C'I ',AL2(0)                         
RCVFILD  DC    F'0',CL16'TALFIL DELETES  ',C'I ',AL2(0)                         
RCVFILR  DC    F'0',CL16'TALFIL RESTORES ',C'I ',AL2(0)                         
RCVCHDD  DC    F'0',CL16'CHKDIR DELETES  ',C'I ',AL2(0)                         
RCVCHDR  DC    F'0',CL16'CHKDIR RESTORES ',C'I ',AL2(0)                         
RCVCHFD  DC    F'0',CL16'CHKFIL DELETES  ',C'I ',AL2(0)                         
RCVCHFR  DC    F'0',CL16'CHKFIL RESTORES ',C'I ',AL2(0)                         
RCVERR   DC    F'0',CL16'TAL ERRORS      ',C'E ',AL2(0)                         
RCVWARN  DC    F'0',CL16'TAL WARNINGS    ',C'W ',AL2(0)                         
LDRCVDTX DC    4X'FF'                                                           
                                                                                
LASTRCV  DC    2048X'00'                                                        
LASTRCVL EQU   *-LASTRCV                                                        
                                                                                
TALDIRQ  EQU   X'71'                                                            
TALFILQ  EQU   X'72'                                                            
CHKDIRQ  EQU   X'75'                                                            
CHKFILQ  EQU   X'76'                                                            
                                                                                
* DSECT TO LOCAL WORKING STORAGE                                                
*                                                                               
WORKD    DSECT                                                                  
APARM    DS    A                                                                
WORKX    EQU   *                                                                
                                                                                
* DSECT TO COVER RECOVERY HEADER                                                
*                                                                               
RCVRECD  DSECT                                                                  
RCVHDR   DS    0XL24                                                            
*DMRCVRHDR                         RECOVERY HEADER                              
       ++INCLUDE DMRCVRHDR                                                      
                                                                                
RECREC   DS    0X                  RECORD                                       
RECKEY   DS    XL32                RECORD KEY                                   
RECLEN   DS    XL2                 RECORD LENGTH                                
RECSTAT  DS    XL2                 RECORD STATUS                                
RECDATA  DS    0X                  RECORD DATA                                  
                                                                                
         ORG   RECREC                                                           
KEYREC   DS    0XL38               KEY RECORD                                   
KEYKEY   DS    XL32                KEY KEY                                      
KEYSTAT  DS    XL2                 KEY STATUS                                   
KEYADDR  DS    XL4                 KEY DISK ADDRESS                             
                                                                                
         ORG   RECREC                                                           
CHFREC   DS    0X                  CHK RECORD                                   
CHFKEY   DS    XL32                CHK RECORD KEY                               
CHFLEN   DS    XL2                 CHK RECORD LENGTH                            
CHFSTAT  DS    XL2                 CHK RECORD STATUS                            
CHFDATA  DS    0X                  CHK RECORD DATA                              
                                                                                
         ORG   RECREC                                                           
CHKREC   DS    0XL38               CHK KEY RECORD                               
CHKKEY   DS    XL32                CHK KEY KEY                                  
CHKSTAT  DS    XL2                 CHK KEY STATUS                               
CHKADDR  DS    XL4                 CHK KEY DISK ADDRESS                         
                                                                                
*DMRCVREXT                         RECOVERY TRAILER                             
       ++INCLUDE DMRCVREXT                                                      
                                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'001TALDRCVD  03/28/12'                                      
         END                                                                    
