*          DATA SET DDPANGLOSS AT LEVEL 003 AS OF 03/20/15                      
*CATALP PANGLOSS                                                                
         TITLE 'PANGLOSS - BUILD FACILITIES PANFILE TAPE FROM PAN LIB.'         
PANGLOSS CSECT                                                                  
         PRINT NOGEN                                                            
         NBASE 0,**PANGLO,=V(REGSAVE),R9                                        
         L     RA,=V(CPRINT)                                                    
         USING DPRINT,RA                                                        
         ZAP   LINE,=PL2'99'                                                    
         MVC   TITLE+17(19),=C'PAN FILE CONVERSION'                             
         GOTO1 VPRINTER                                                         
         SPACE 2                                                                
PAN      GOTO1 VSORTER,DMCB,SORTFLD,RECTYPE,0                                   
         BAS   RE,VALC                                                          
         CLI   MODE,RUN                                                         
         BNE   PAN2                                                             
         L     R2,=A(TAPEOUT)                                                   
         OPEN  ((2),OUTPUT)                                                     
*                                                                               
PAN2     GOTO1 VSORTER,DMCB,=C'GET'                                             
         ICM   R0,15,DMCB+4        R0=A(RECORD)                                 
         BZ    PAN4                END-OF-FILE ON SORT                          
         L     R2,SORTOUTS                                                      
         LA    R2,1(R2)            INCREMENT SORT OUTPUT COUNT                  
         ST    R2,SORTOUTS                                                      
         CLI   MODE,RUN            TEST FOR RUN MODE                            
         BNE   PAN2                NO-DO NOT PUT TO TAPE                        
         L     R1,=A(TAPEOUT)      POINT TO DCB                                 
         PUT   (R1),(R0)                                                        
         L     R2,OUTRECS                                                       
         LA    R2,1(R2)            INCREMENT TAPE OUTPUT COUNT                  
         ST    R2,OUTRECS                                                       
         B     PAN2                                                             
*                                                                               
PAN4     CLI   MODE,RUN                                                         
         BNE   PAN5                                                             
         L     R2,=A(TAPEOUT)                                                   
         CLOSE ((R2),)                                                          
*                                                                               
PAN5     ZAP   LINE,=PL2'99'       FORCE PAGE BREAK                             
         MVC   MID1,SPACES                                                      
         MVC   MID2,SPACES                                                      
         MVC   MID1+10(27),=C'PANGLOSS CONVERSION SUMMARY'                      
         MVI   MID2+10,DASH                                                     
         MVC   MID2+11(26),MID2+10                                              
         GOTO1 VPRINTER                                                         
         LA    R3,BUCKTAB          R3=POINTER TO BUCKET TABLE                   
         LA    R4,BUCKETS          R4=BUCKET COUNTER                            
*                                                                               
PAN6     ICM   R2,15,0(R3)         VALUE                                        
         BZ    PAN7                DO NOT PRINT ZERO BUCKET                     
         MVC   P+10(20),4(R3)      DESCRIPTION                                  
         MVI   P+30,C'='                                                        
         EDIT  (R2),(7,P+32)                                                    
         GOTO1 VPRINTER                                                         
PAN7     LA    R3,L'BUCKTAB(R3)                                                 
         BCT   R4,PAN6                                                          
         SPACE 2                                                                
ENDJOB   XBASE                                                                  
         SPACE 2                                                                
EXIT     XIT1                                                                   
         EJECT                                                                  
* SUB-ROUTINE TO READ AND PROCESS CONTROL CARDS                                 
*                                                                               
* CARDS ARE EITHER KEYWORD-PARAMETER OPTIONS OR PAN BOOKNAMES.                  
* KEYWORD OPTION CARDS MUST PRECEDE BOOKNAMES.  UP TO 10 OPTIONS                
* OR BOOKNAMES CAN BE ON A PARTICULAR CARD.                                     
*                                                                               
VALC     NTR1                                                                   
         MVC   P+10(13),=C'CONTROL CARDS'                                       
         GOTO1 VPRINTER                                                         
         MVI   P+10,DASH           PRINT CONTROL CARD HEADING                   
         MVC   P+11(12),P+10                                                    
         GOTO1 VPRINTER                                                         
         BASR  RE,RF                                                            
*                                                                               
VALC1    GOTO1 VCARDS,DMCB,C,=C'RE00'                                           
         MVI   ERRNUM,MISERR                                                    
         CLC   C(2),=C'/*'                                                      
         BE    VALCERR             NO CARDS GIVEN                               
         MVI   BOOKSW,NO                                                        
         B     VALC4                                                            
*                                                                               
VALC2    GOTO1 VCARDS,DMCB,C,=C'RE00'                                           
         CLC   C(2),=C'/*'         TEST FOR EOF                                 
         BE    VALCN               YES                                          
*                                                                               
VALC4    LA    R0,L'C+L'HEADER     SET UP DUMMY FLD. HEADER FOR SCANNER         
         STC   R0,HEADER                                                        
         OC    C,SPACES            SPACE FILL THE CARD                          
         MVC   C+72(8),SPACES      ERASE MVS SEQUENCE NUMBERS                   
         LA    R0,72                                                            
         LA    R1,C+71             FIND DATA LEN FOR DUMMY FLD HEADER           
         CLI   0(R1),C' '                                                       
         BNE   VALC6                                                            
         BCTR  R1,0                                                             
         BCT   R0,*-10                                                          
         MVI   ERRNUM,NODATERR     NO DATA FOUND                                
         B     VALCERR                                                          
*                                                                               
VALC6    STC   R0,HEADER+5                                                      
         GOTO1 VSCANNER,DMCB,HEADER,(10,BLOCK),0                                
         MVI   ERRNUM,CONERR                                                    
         CLI   DMCB+4,0                                                         
         BE    VALCERR             CONTROL CARD IN BAD SHAPE                    
         LA    R2,BLOCK            R2=SCANNER BLOCK POINTER                     
         ZIC   R3,DMCB+4           R3=COUNT OF BLOCKS                           
*                                                                               
VALC8    MVI   ERRNUM,CONERR                                                    
         CLI   0(R2),0             TEST FOR LONE COMMA                          
         BE    VALCERR                                                          
         CLI   1(R2),0             TEST FOR PARAMETER VALUE                     
         BE    VALC12              NO-CAN ONLY BE BOOKNAME                      
         CLI   0(R2),3             KEYWORD OPTION MUST BE BETWEEN               
         BL    *+12                3 AND 8 CHARACTERS.                          
         CLI   0(R2),8                                                          
         BNH   VALC9                                                            
         LA    R1,12(R2)           POINT AT INVALID KEYWORD                     
         ST    R1,ERRPT                                                         
         MVI   ERRNUM,KEYERR                                                    
         B     VALCERR                                                          
*                                                                               
VALC9    LA    R0,KEYWORDS         R0=COUNT OF KEY OPTIONS                      
         LA    R4,KEYTAB           R4=TABLE POINTER                             
         ZIC   R1,0(R2)                                                         
         BCTR  R1,0                                                             
VALC9A   EX    R1,*+8                                                           
         B     *+10                                                             
         CLC   12(0,R2),0(R4)                                                   
         BE    VALC10              FOUND THE KEYWORD                            
         LA    R4,L'KEYTAB(R4)                                                  
         BCT   R0,VALC9A           NEXT TABLE ENTRY                             
         LA    R1,12(R2)                                                        
         ST    R1,ERRPT                                                         
         MVI   ERRNUM,KEYERR                                                    
         B     VALCERR                                                          
*                                                                               
VALC10   MVI   ERRNUM,ORDERR                                                    
         CLI   BOOKSW,YES          TEST IF ANY BOOKS PRECEDED OPTIONS           
         BE    VALCERR             YES                                          
         L     RF,8(R4)            RF=A(PARM PROCESSING ROUTINE)                
         BASR  RE,RF                                                            
         B     VALC15                                                           
*                                                                               
VALC12   CLI   0(R2),10            ASSUME BOOKNAME IS IN BLOCK                  
         BNH   VALC13              BOOKNAMES ARE UP TO 10 CHARS LONG            
         LA    R1,12(R2)                                                        
         ST    R1,ERRPT                                                         
         MVI   ERRNUM,BOOKERR                                                   
         B     VALCERR                                                          
*                                                                               
VALC13   CLI   BOOKSW,YES          TEST FOR FIRST BOOK                          
         BE    VALC14              NO                                           
         MVI   BOOKSW,YES          SET FIRST BOOK SWITCH                        
         ZAP   LINE,=PL2'99'       FORCE PAGE BREAK                             
         MVC   MID1+10(13),=C'PAN BOOK NAME'                                    
         MVC   MID1+40(10),=C'STATEMENTS'                                       
         MVI   MID2+10,DASH                                                     
         MVC   MID2+11(12),MID2+10                                              
         MVI   MID2+40,DASH                                                     
         MVC   MID2+41(9),MID2+40                                               
         GOTO1 VPRINTER                                                         
*                                                                               
VALC14   MVC   BOOKNAME,12(R2)     SET PAN BOOK NAME FOR RDPAN                  
         BAS   RE,RDPAN                                                         
*                                                                               
VALC15   LA    R2,32(R2)           NEXT SCANNER BLOCK                           
         BCT   R3,VALC8                                                         
         B     VALC2               READ NEXT CARD                               
*                                                                               
VALCN    CLI   BOOKSW,YES          TEST FOR ANY BOOKS                           
         BE    *+12                YES                                          
         MVI   ERRNUM,NOBKSERR                                                  
         B     VALCERR                                                          
         GOTO1 VPANIC,DMCB,=C'CLOSE',=C'PAN'                                    
*                                                                               
VALCX    B     EXIT                                                             
         EJECT                                                                  
* SUB-ROUTINE TO VALIDATE MODE PARAMETER                                        
*                                                                               
MODEV    ST    RE,SAVEREG          SAVE RETURN POINT                            
         CLI   1(R2),2             PARM AT LEAST 2 CHARS                        
         BL    MODEVR                                                           
         CLI   1(R2),7             PARM UP TO 7 CHARS                           
         BH    MODEVR                                                           
         LA    R0,MODES            R0=COUNTER                                   
         LA    RE,MODETAB          RE=TABLE POINTER                             
         ZIC   R1,1(R2)                                                         
         BCTR  R1,0                                                             
*                                                                               
MODEV2   EX    R1,*+8                                                           
         B     *+10                                                             
         CLC   22(0,R2),1(RE)                                                   
         BE    MODEV4              FOUND VALUE                                  
         LA    RE,L'MODETAB(RE)                                                 
         BCT   R0,MODEV2                                                        
         B     MODEVR              ERROR                                        
*                                                                               
MODEV4   MVC   MODE,0(RE)          SET MODE VALUE                               
         CLI   MODE,TRACE          TEST FOR TRACE                               
         BNE   *+16                                                             
         MVC   INPRT+4(4),=F'100'  SET DEFAULT TRACING TO 100 RECS              
         MVC   SRPRT+4(4),=F'100'                                               
         L     RE,SAVEREG                                                       
         BR    RE                                                               
*                                                                               
MODEVR   LA    R1,22(R2)                                                        
         ST    R2,ERRPT                                                         
         MVI   ERRNUM,PARMERR                                                   
         B     VALCERR                                                          
         SPACE 2                                                                
* SUB-ROUTINE TO VALIDATE NUMBER OF RECORDS PARAMETER                           
*                                                                               
NRECV    ST    RE,SAVEREG                                                       
         TM    3(R2),X'80'         TEST NUMERIC DATA                            
         BZ    NRECVR              NO                                           
         ICM   R1,15,8(R2)         GET VALUE                                    
         CH    R1,=H'999'          LIMIT OF 999 IS REASONABLE                   
         BH    NRECVR                                                           
         ST    R1,INPRT+4          SET TRACE LIMIT IN                           
         ST    R1,SRPRT+4                                                       
         L     RE,SAVEREG                                                       
         BR    RE                                                               
*                                                                               
NRECVR   MVI   ERRNUM,PARMERR                                                   
         LA    R1,22(R2)                                                        
         ST    R1,ERRPT                                                         
         B     VALCERR                                                          
         EJECT                                                                  
* CONTROL CARD ERROR EXIT                                                       
*                                                                               
* AT ENTRY, ERRNUM CONTAINS ERROR MESSAGE NUMBER AND ERRPT                      
* OPTIONALLY POINTS TO 10 BYTE TEXT TO TACK ON TO STANDARD MESSAGE              
*                                                                               
VALCERR  MVC   P+10(L'C),C         PRINT THE PROBLEM CARD                       
         GOTO1 VPRINTER                                                         
         LA    R0,ERRORS           R0=COUNTER OF ERRORS                         
         LA    R4,ERRTAB           R4=TABLE POINTER                             
         CLC   ERRNUM,0(R4)        LOCATE ERROR IN TABLE                        
         BE    VALCERR2                                                         
         LA    R4,L'ERRTAB(R4)                                                  
         BCT   R0,*-14                                                          
         DC    H'0'                DID NOT FIND MESSAGE                         
*                                                                               
VALCERR2 MVC   P+10(30),1(R4)      GET ERROR MESSAGE FROM TABLE                 
         ICM   R1,15,ERRPT         GET ERROR POINTER                            
         BZ    *+10                                                             
         MVC   P+42(10),0(R1)      TACK ON DETAIL TO MESSAGE                    
         GOTO1 VPRINTER                                                         
*                                                                               
VALCERR4 GOTO1 VPRINTER                                                         
         GOTO1 VCARDS,DMCB,C,=C'RE00'                                           
         CLC   C(2),=C'/*'         FLUSH THE READER                             
         BE    *+14                EOF ENCOUNTERED                              
         MVC   P+10(L'C),C                                                      
         B     VALCERR4                                                         
*                                                                               
         GOTO1 VPRINTER                                                         
         MVC   P+10(30),=CL30'**JOB ENDED DUE TO ERROR**'                       
         GOTO1 VPRINTER                                                         
         ABEND 4                                                                
         EJECT                                                                  
* SUB-ROUTINE TO READ PANVALET FILE AND GET ALL RECORDS FOR A BOOK              
*                                                                               
* AT ENTRY, BOOKNAME CONTAINS PAN BOOK NAME                                     
*                                                                               
RDPAN    NTR1                                                                   
         XC    BKCOUNT,BKCOUNT     CLEAR STATEMENTS IN BOOK COUNT               
         GOTO1 VPANIC,DMCB,=C'READ',=C'DIR',BOOKNAME,AINREC                     
         CLI   DMCB+8,0            TEST FOR ERROR                               
         BE    RDPAN2              YES                                          
*                                                                               
         LA    R2,P                                                             
         USING PLINED,R2                                                        
         MVC   PBOOK,BOOKNAME                                                   
         MVC   PCOUNT(18),=CL18'**BOOK NOT FOUND**'                             
         CLI   DMCB+8,X'10'                                                     
         BE    *+10                                                             
         MVC   PCOUNT(18),=CL18'**DISK ERROR**'                                 
         GOTO1 VPRINTER                                                         
         BASR  RE,RF                                                            
         CLI   DMCB+8,X'10'        TEST FOR BOOK NOT FOUND                      
         BE    RDPANX              YES                                          
         DC    H'0'                BLOW UP FOR DISK ERROR                       
*                                                                               
RDPAN2   GOTO1 VPANIC,DMCB,=C'READ',=C'PAN',BOOKNAME,AINREC                     
         CLI   DMCB+8,X'80'        TEST FOR EOF                                 
         BE    RDPAN15             YES                                          
         CLI   DMCB+8,0            TEST FOR ERROR                               
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
RDPAN4   L     R1,INRECS           BUMP PAN RECORD COUNT                        
         LA    R1,1(R1)                                                         
         ST    R1,INRECS                                                        
         L     R1,BKCOUNT          AND BOOK STATEMENT COUNTER                   
         LA    R1,1(R1)                                                         
         ST    R1,BKCOUNT                                                       
*                                                                               
RDPAN6   CLI   MODE,TRACE          TEST FOR TRACING                             
         BNE   RDPAN8                                                           
         L     R2,INPRT                                                         
         LA    R2,1(R2)                                                         
         C     R2,INPRT+4          TEST AGAINST MAX PRINT LIMIT                 
         BH    RDPAN8                                                           
         ST    R2,INPRT                                                         
         GOTO1 PRTREC,DMCB,(C'I',AINREC)                                        
*                                                                               
RDPAN8   BAS   RE,BLDREC           CONSTRUCT JCL RECORD                         
         GOTO1 VSORTER,DMCB,=C'PUT',AOUTRECH                                    
         L     R1,SORTRECS         INCREMENT COUNT OF RECORDS                   
         LA    R1,1(R1)            PUT TO SORT                                  
         ST    R1,SORTRECS                                                      
*                                                                               
RDPAN10  CLI   MODE,TRACE                                                       
         BNE   RDPAN12                                                          
         L     R2,SRPRT                                                         
         LA    R2,1(R2)                                                         
         C     R2,SRPRT+4                                                       
         BH    RDPAN12                                                          
         ST    R2,SRPRT                                                         
         GOTO1 PRTREC,DMCB,(C'S',AOUTREC)                                       
*                                                                               
RDPAN12  B     RDPAN2              GET NEXT PAN RECORD                          
*                                                                               
RDPAN15  LA    R2,P                                                             
         USING PLINED,R2                                                        
         MVC   PBOOK,BOOKNAME      PAN BOOK NAME                                
         L     R3,BKCOUNT                                                       
         EDIT  (R3),(4,PCOUNT)                                                  
         GOTO1 VPRINTER                                                         
         BASR  RE,RF                                                            
*                                                                               
RDPANX   B     EXIT                                                             
         DROP  R2                                                               
         EJECT                                                                  
* SUB-ROUTINE TO BUILD A JCL RECORD FROM PAN RECORD                             
*                                                                               
* JCL RECORD BUILT AT OUTREC WITH SORT RECORD HEADER                            
*                                                                               
BLDREC   NTR1                                                                   
         L     R2,AINREC           POINT TO PAN RECORD                          
         OC    0(80,R2),SPACES                                                  
         MVC   72(8,R2),SPACES     CLEAR OUT SEQUENCE NUMBER                    
         L     R4,AOUTREC          POINT TO AREA FOR NEW RECORD                 
         USING JCKEY,R4                                                         
         XC    0(255,R4),0(R4)     CLEAR RECORD AREA                            
         MVI   JCCODE,JCCODEQU                                                  
         MVC   JCBOOK,BOOKNAME                                                  
         CLC   =C'JC',JCBOOK       TEST FOR 'JC' PREFIX                         
         BNE   BLDREC1             NO                                           
         MVC   DUB,JCBOOK+2        YES-REMOVE THE PREFIX SO                     
         MVC   JCBOOK,SPACES       LEFT WITH E.G. AC014, SP0NR                  
         MVC   JCBOOK(L'DUB),DUB                                                
*                                                                               
BLDREC1  MVC   JCSEQ,BKCOUNT+2                                                  
         LA    R0,JCFRSTEL-JCKEY+1                                              
         STCM  R0,3,JCRLEN                                                      
*                                                                               
BLDREC2  LA    R1,72                                                            
         LA    R3,71(R2)           POINT TO END OF PAN LINE                     
         CLI   0(R3),C' '          FIND LAST DATA CHARACTER                     
         BNE   *+10                                                             
         BCTR  R3,0                                                             
         BCT   R1,*-10                                                          
*                                                                               
BLDREC4  LA    R5,JCFRSTEL                                                      
         USING PAELEM,R5                                                        
         MVI   PACODE,PACODEQU                                                  
         LA    RE,PADATA-PAELEM(R1)                                             
         STC   RE,PALEN                                                         
         LTR   R1,R1               TEST FOR ANY DATA                            
         BZ    BLDREC6                                                          
         BCTR  R1,0                                                             
         EX    R1,*+8                                                           
         B     BLDREC6                                                          
         MVC   PADATA(0),0(R2)     MOVE PAN STATEMENT TO ELEMENT                
*                                                                               
BLDREC6  SR    R1,R1                                                            
         ICM   R1,3,JCRLEN                                                      
         AR    R1,RE               UPDATE RL FOR PAN DATA ELEMENT               
         STCM  R1,3,JCRLEN                                                      
         LA    R1,4(R1)            SORT RECORD LENGTH                           
         L     RF,AOUTRECH         POINT TO SORT RECORD HEADER                  
         STH   R1,0(RF)                                                         
         B     EXIT                                                             
         DROP  R4,R5                                                            
         EJECT                                                                  
* SUB-ROUTINE TO PRINT A RECORD IN DUMP FORMAT                                  
*                                                                               
* AT ENTRY, P1 BYTE 0 = TEXT SELECTOR                                           
*              BYTES 1-3 = A(RECORD)                                            
*                                                                               
PRTREC   NTR1                                                                   
         L     R2,0(R1)            R2 POINTS TO RECORD                          
         MVC   BYTE,0(R1)                                                       
         LA    R0,PRTENT           R0=TABLE ENTRY COUNTER                       
         LA    R3,PRTTAB           R3=TABLE POINTER                             
*                                                                               
PRTREC2  CLC   BYTE,0(R3)          SEARCH ON TEXT SELECTOR                      
         BE    PRTREC4                                                          
         LA    R3,L'PRTTAB(R3)                                                  
         BCT   R0,PRTREC2                                                       
         DC    H'0'                                                             
*                                                                               
PRTREC4  SR    R5,R5                                                            
         ICM   R5,1,1(R3)          FIXED LENGTH TO PRINT                        
         BNZ   PRTREC6             YES-ITS SET IN TABLE                         
         ZIC   RE,2(R3)            GET DISPLACEMENT TO R.L.                     
         LA    RE,0(R2,RE)         POINT RE AT RECORD LENGTH                    
         SR    R5,R5                                                            
         ICM   R5,3,0(RE)          R5 CONTAINS RL                               
*                                                                               
PRTREC6  GOTO1 VPRNTBL,DMCB,(16,3(R3)),(R2),C'DUMP',(R5),=C'2D'                 
         B     EXIT                                                             
         EJECT                                                                  
* LITERAL POOL                                                                  
*                                                                               
         LTORG                                                                  
         SPACE 2                                                                
* MODULE TABLE                                                                  
*                                                                               
VCARDS   DC    V(CARDS)                                                         
VPANIC   DC    V(PANIC)                                                         
VPRINTER DC    V(PRINTER)                                                       
VPRNTBL  DC    V(PRNTBL)                                                        
VSCANNER DC    V(SCANNER)                                                       
VSORTER  DC    V(SORTER)                                                        
         SPACE 2                                                                
* TABLE OF RECORD COUNT BUCKETS                                                 
*                                                                               
         DS    0F                                                               
BUCKTAB  DS    0CL24                                                            
INRECS   DC    F'0',CL20'RECORDS IN'                                            
SORTRECS DC    F'0',CL20'RECORDS TO SORT'                                       
SORTOUTS DC    F'0',CL20'RECORDS FROM SORT'                                     
OUTRECS  DC    F'0',CL20'TAPE OUTPUT RECORDS'                                   
BUCKETS  EQU   (*-BUCKTAB)/L'BUCKTAB                                            
         SPACE 2                                                                
* COUNTERS                                                                      
*                                                                               
BKCOUNT  DS    F                                                                
INPRT    DC    F'0',F'0'                                                        
SRPRT    DC    F'0',F'0'                                                        
         SPACE 2                                                                
* TABLE OF KEYWORD OPTIONS                                                      
*                                                                               
KEYTAB   DS    0CL12                                                            
         DC    CL8'MODE',A(MODEV)                                               
         DC    CL8'NRECS',A(NRECV)                                              
KEYWORDS EQU   (*-KEYTAB)/L'KEYTAB                                              
         SPACE 2                                                                
* TABLE OF MODE VALUES                                                          
*                                                                               
MODETAB  DS    0CL8                                                             
         DC    AL1(RUN),CL7'RUN'                                                
         DC    AL1(REPORT),CL7'REPORT'                                          
         DC    AL1(TRACE),CL7'TRACE'                                            
MODES    EQU   (*-MODETAB)/L'MODETAB                                            
         SPACE 2                                                                
* TABLE OF PRINTABLE TEXTS FOR PRNTBL                                           
*                                                                               
PRTTAB   DS    0CL19                                                            
         DC    C'I',AL1(80),AL1(0),CL16'PAN INPUT RECORD'                       
         DC    C'S',AL1(0),AL1(JCRLEN-JCKEY),CL16'JCL SORT RECORD'              
PRTENT   EQU   (*-PRTTAB)/L'PRTTAB                                              
         SPACE 2                                                                
* TABLE OF CONTROL CARD ERROR MESSAGES                                          
*                                                                               
ERRTAB   DS    0CL31                                                            
         DC    AL1(MISERR),CL30'**NO CONTROL CARDS**'                           
         DC    AL1(NODATERR),CL30'**NO DATA ON CARD**'                          
         DC    AL1(CONERR),CL30'**INVALID CONTROL CARD**'                       
         DC    AL1(KEYERR),CL30'**INVALID KEYWORD ON CARD**'                    
         DC    AL1(PARMERR),CL30'**INVALID PARAMETER VALUE**'                   
         DC    AL1(ORDERR),CL30'**CARDS ARE NOT IN ORDER**'                     
         DC    AL1(BOOKERR),CL30'**BAD BOOKNAME ON CARD**'                      
         DC    AL1(NOBKSERR),CL30'**NO BOOKNAME CARDS FOR JOB**'                
ERRORS   EQU   (*-ERRTAB)/L'ERRTAB                                              
         SPACE 2                                                                
* CONSTANTS                                                                     
*                                                                               
AINREC   DC    A(INREC)                                                         
AOUTREC  DC    A(OUTREC)                                                        
AOUTRECH DC    A(OUTRECH)                                                       
SORTFLD  DC    CL80'SORT FIELDS=(5,20,A),FORMAT=BI'                             
RECTYPE  DC    CL80'RECORD TYPE=V,LENGTH=1000'                                  
         SPACE 2                                                                
* VALUES                                                                        
*                                                                               
MODE     DC    AL1(REPORT)         DEFAULT IS REPORT                            
ERRNUM   DC    X'00'                                                            
BOOKSW   DS    C                   PAN BOOK NAME CARDS FOUND (Y/N)              
BOOKNAME DC    CL10' '                                                          
ERRPT    DC    A(0)                                                             
         SPACE 2                                                                
* GENERAL STORAGE                                                               
*                                                                               
DUB      DS    D                                                                
DMCB     DS    6F                                                               
FULL     DS    F                                                                
SAVEREG  DS    A                                                                
HALF     DS    H                                                                
WORK     DS    CL80                                                             
BYTE     DS    C                                                                
         DS    0D                                                               
BLOCK    DS    CL320                                                            
*                                                                               
* CARD AREA                                                                     
*                                                                               
HEADER   DC    XL8'00'                                                          
C        DC    CL80' '                                                          
*                                                                               
* INPUT RECORD AREA                                                             
*                                                                               
         DS    0D                                                               
         DC    C'**INREC*'                                                      
INREC    DS    CL1000                                                           
*                                                                               
* OUTPUT RECORD AREA                                                            
*                                                                               
         DC    C'*OUTREC*'                                                      
OUTRECH  DS    H                                                                
         DC    H'0'                                                             
OUTREC   DS    CL1000                                                           
         EJECT                                                                  
* OUTPUT TAPE DCB                                                               
*                                                                               
TAPEOUT  DCB   DDNAME=TAPEOUT,DSORG=PS,RECFM=VB,LRECL=1100,            *        
               BLKSIZE=8200,MACRF=PM                                            
         EJECT                                                                  
* EQUATES                                                                       
*                                                                               
RUN      EQU   X'01'                                                            
REPORT   EQU   X'02'                                                            
TRACE    EQU   X'04'                                                            
YES      EQU   C'Y'                                                             
NO       EQU   C'N'                                                             
DASH     EQU   C'-'                                                             
*                                                                               
MISERR   EQU   1                                                                
NODATERR EQU   2                                                                
CONERR   EQU   3                                                                
KEYERR   EQU   4                                                                
PARMERR  EQU   5                                                                
ORDERR   EQU   6                                                                
BOOKERR  EQU   7                                                                
NOBKSERR EQU   8                                                                
         SPACE 2                                                                
* DSECT TO COVER PRINT LINE                                                     
*                                                                               
PLINED   DSECT                                                                  
         DS    CL10                                                             
PBOOK    DS    CL10                                                             
         DS    CL20                SPARE                                        
PCOUNT   DS    CL10                                                             
         SPACE 2                                                                
* DDPANFILE                                                                     
         PRINT OFF                                                              
       ++INCLUDE DDPANFILE                                                      
         PRINT ON                                                               
         SPACE 2                                                                
* DDDPRINT                                                                      
         PRINT OFF                                                              
       ++INCLUDE DDDPRINT                                                       
         PRINT ON                                                               
         SPACE 2                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'003DDPANGLOSS03/20/15'                                      
         END                                                                    
