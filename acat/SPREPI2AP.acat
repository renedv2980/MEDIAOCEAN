*          DATA SET SPREPI2AP  AT LEVEL 092 AS OF 01/31/21                      
*CATALP SPI2AP                                                                  
         TITLE 'SPI2AP - SI2 AUTOPAY MODULE'                                    
***********************************************************************         
* USER    JIRA      DATE                  CHANGE LOG                  *         
* ---- ---------- -------- ------------------------------------------ *         
* ASAX SPEC-53147 01/02/21 AUTOPAY SET UP FOR APEXTO (*A)             *         
* AKAT SPEC-52972 01/13/21 AUTOPAY PHDTOM TO $J (WAS OFFICE 40)       *         
* VGUP SPEC-46329 05/20/20 AUTOPAY SET UP FOR OPNTOA ($5)             *         
* VGUP SPEC-35665 05/13/19 AUTOPAY SET UP FOR NWOTOA ($N)             *         
*      SPEC-35708 05/13/19 CHANGE AUTOPAY PHDTOM TO PHDTOA            *         
* AKAT SPEC-34651 04/17/19 AUTOPAY OFFICE 40 FOR OU TO PHDTOA         *         
* AKAT SPEC-23878 01/11/19 SUPPORT MULTIPLE INVOICE FOR I2 BY BUY-ID  *         
* AKAT SPEC-30922 01/03/19 AUTOPAY OFFICE 26 FOR OU TO OMGROG         *         
* AKAT SPEC-25325 07/03/18 AUTOPAY OFFICE 46 FOR OU TO OMGHSC         *         
* AKAT SPEC-21375 03/07/18 REDIRECTS FOR WAVETO (*ZM) & WAVEL (*ZL)   *         
* AKAT SPEC-10585 03/15/17 REDIRECTS FOR MIEXCA ($P)                  *         
* AKAT SPEC-7767  11/11/16 SUPPORT NEW MEDIA OFFICE LIST              *         
* AKAT SPEC-7091  10/19/16 AUTOPAY OFFICE 45 FOR OU TO OMGPGCA        *         
***********************************************************************         
         PRINT NOGEN                                                            
SPI2AP   CSECT                                                                  
         NMOD1 WORKDL,**I2AP,CLEAR=YES                                          
         LR    R7,RC                                                            
         USING WORKD,R7                                                         
         USING SPWORKD,RA,R9                                                    
         LA    RC,SPACEND                                                       
         USING IMRWORK,RC                                                       
         LA    R5,MINBLOCK                                                      
         USING MINBLKD,R5                                                       
*                                                                               
         L     RF,ACOMFACS                                                      
         MVC   VMINIO,CMINIO-COMFACSD(RF)                                       
*                                                                               
         MVC   TRACEOPT,RCTRACE                                                 
         B     AP00                                                             
XYES     SR    RC,RC                                                            
XNO      LTR   RC,RC                                                            
EXIT     XIT1                                                                   
*                                                                               
**********************************************************************          
*        ADD AUTOPAY RECORD FOR SUCCESSFUL MATCHES  - SPOT                      
**********************************************************************          
AP00     DS    0H                                                               
*                                                                               
         CLI   NETPSW,C'Y'         NETPAK I2                                    
         BE    AP100                                                            
*                                                                               
AP02     L     R1,ALINV            A(LAST INV)                                  
         S     R1,AFINV            A(FIRST INV)                                 
         BNP   APX                 NO IVOICES THEN DON'T DO ANYTHING            
*                                                                               
         CLI   ALLPAID,C'N'        FLAG IF ANY UPPAID SPOTS                     
         BNE   APX                                                              
*                                                                               
**       CLC   PDGRS,MATGRS        IF EVERYTHING IS ALREADY PAID                
**       BNE   AP04                                                             
**       CLC   PDNET,MATNET                                                     
**       BE    APX                 THEN DON'T TRY TO PAY AGAIN                  
*                                                                               
AP04     GOTO1 DATCON,DMCB,(4,RCDATE),(2,CDATE)       RUN DATE                  
         XC    CDATE,=X'FFFF'       COMPLIMENT                                  
         CLI   MULTIMON,C'Y'       IF MULTIPLE MONTHS THEN NO AP                
         BE    EXIT                                                             
*                                                                               
         CLI   CANADSW,C'Y'        ONLY FOR US NOT CANADA                       
         BE    AP05                                                             
         CLI   BSTA,X'E8'          LOCAL CABLE STATIONS?                        
         BL    AP05                NO                                           
         CLI   I2PPROF+6,C'Y'      AUTOPAY CLEAR BY REP FEATURE?                
         BE    *+12                YES                                          
         CLI   I2PPROF+6,C'I'      AUTOPAY CLEAR BY REP W/INV TAX?              
         BNE   EXIT                NO - SKIP LOCAL CABLE                        
         CLI   I2PPROF+7,C'Y'      AUTOPAY LOCAL CABLE?                         
         BE    AP05                YES                                          
         CLI   I2PPROF+7,C'I'      AUTOPAY LOCAL CABLE WITH INV REP?            
         BNE   EXIT                NO - SKIP LOCAL CABLE                        
*                                                                               
AP05     MVI   BPIGGY,0            GET CORRECT PIGGY                            
         MVC   BPIGGY,SVBPRD2      IF QPRD2=PRD                                 
         CLI   BPRD2,0                                                          
         BE    *+10                                                             
         MVC   BPIGGY,BPRD2        IF QPRD2=ALL                                 
*                                                                               
         CLI   BEST,0              EST=NO=ALL                                   
         BNE   AP07                                                             
         USING IELEMD,R3           INVOICE TABLE                                
         L     R3,AFINV                                                         
AP06     C     R3,ALINV            LAST INVOICE                                 
         BNL   AP07                                                             
*                                                                               
         C     R3,AFINV            FIRST INVOICE                                
         BNE   *+10                                                             
         MVC   INVEST,IEST         IF ALL INVOICES ARE FOR THE SAME EST         
         CLC   INVEST,IEST         THEN USE THAT ESTIMATE TO PAY                
         BE    AP06NXT                                                          
         MVI   INVEST,0            IF NOT THE SAME FORCE TO 0 FOR ERROR         
         B     AP07                                                             
*                                                                               
AP06NXT  AH    R3,IELMLEN                                                       
         B     AP06                                                             
         DROP  R3                                                               
*---------------------------------------------------------------------          
* SPECIAL CODE TO CHECK FOR MATCHING ERRORS FOR COKE ONLY                       
*---------------------------------------------------------------------          
*                                                                               
AP07     MVI   PAYERRS,0                                                        
*                                                                               
         CLI   I2PPROF+3,C'N'      AUTOPAY ON FILM ANALYSIS ERR                 
         BNE   AP07A                                                            
         CLI   FILMDSCP,C'Y'       FILM ANALYSIS ERRORS?                        
         BNE   *+8                                                              
         OI    PAYERRS,APYERRS_FILMERR    X'80'                                 
*                                                                               
AP07A    CLI   I2PPROF+4,C'N'      AUTOPAY ON H/V ERR                           
         BNE   AP07B                                                            
         CLI   HVROTERR,C'Y'       ARE THERE H/V ERRORS?                        
         BNE   *+8                                                              
         OI    PAYERRS,APYERRS_HVRERR     X'20'                                 
*                                                                               
AP07B    CLI   I2PPROF+5,C'N'      AUTOPAY ON 2ND SEP ERR                       
         BNE   AP07X                                                            
         CLI   SECSEPER,C'Y'       ARE THERE H/V ERRORS?                        
         BNE   *+8                                                              
         OI    PAYERRS,APYERRS_SEPERR     X'40'                                 
*                                                                               
AP07X    CLI   I2PPROF+6,C'Y'      AUTOPAY CLEAR BY REP FEATURE?                
         BE    *+12                YES                                          
         CLI   I2PPROF+6,C'I'      AUTOPAY CLEAR BY REP W/INV TAX?              
         BNE   AP10                NO                                           
         BRAS  RE,XAPY             YES - AUTOPAY RECS ON THE XSPFILE            
         B     APX                 EXIT                                         
*                                                                               
*---------------------------------------------------------------------          
* BUILD ATUOPAY KEY                                                             
*---------------------------------------------------------------------          
*                                                                               
         USING APYRECD,R6                                                       
AP10     LA    R6,KEY                                                           
         XC    KEY,KEY                                                          
         MVI   APYKTYP,APYKTYPQ    X'0D'                                        
         MVI   APYKSUB,APYKSUBQ    X'3A'                                        
         MVC   APYKDATE,CDATE      COMPLIMENTED RUN DATE                        
         MVC   APYKAGMD,BAGYMD                                                  
         MVC   APYKCLT,BCLT        CLIENT                                       
         MVC   APYKPRD,BPRD        PRODUCT                                      
         MVC   APYKPTN,BPIGGY      PARTNER                                      
         MVC   APYKEST,BEST        ESTIMATE                                     
         CLI   BEST,0              EST=NO=ALL                                   
         BNE   *+10                                                             
         MVC   APYKEST,INVEST      USE ESTIMATE FROM INVOICES (IF ONE)          
         MVC   APYKSTA,BSTA        STATION                                      
         CLI   CANADSW,C'Y'        CABLE FOR US NOT CANADA                      
         BE    AP12                                                             
         CLI   APYKSTA,X'E8'       TEST CABLE                                   
         BL    *+8                                                              
         NI    APYKSTA+2,X'80'     TURN OFF NETWORK BITS                        
AP12     CLI   PAYERRS,0                                                        
         BE    *+8                                                              
         OI    APYKCNTL,APYRCERR   TURN ON ERROR EXISTS BIT                     
*                                                                               
*---------------------------------------------------------------------          
* BUILD AUTOPAY RECORD                                                          
*---------------------------------------------------------------------          
*                                                                               
         LA    RE,APIO             CLEAR IO AREA                                
         LHI   RF,2000                                                          
         SR    R1,R1                                                            
         MVCL  RE,R0                                                            
*                                                                               
         LA    R6,APIO                                                          
         ST    R6,AREC                                                          
         MVC   0(13,R6),KEY                                                     
         MVI   APYRLEN+1,25                                                     
*                                                                               
         CLI   PAYERRS,0                                                        
         BE    *+8                                                              
         OI    APYRCNTL,APYRCERR   TURN ON ERROR EXISTS BIT                     
*                                                                               
         USING APYEL,R8                                                         
         LA    R8,APELEM                                                        
         XC    APELEM,APELEM                                                    
         MVI   APYEL,APYELQ        X'01'                                        
         MVI   APYLEN,APYLN2Q                                                   
         MVC   APYMED,QMED         MEDIA                                        
         L     RF,ADCLT                                                         
         IC    R0,CPROF+6-CKEY(RF)                                              
         GOTO1 CLUNPK,DMCB,((R0),BCLT),APYCLT                                   
*                                                                               
         MVC   APYPRD,SVPRD        PRODUCT                                      
*                                                                               
         CLI   BPIGGY,0           ANY PIGGY                                     
         BE    AP20                                                             
         L     RF,ADCLT                                                         
         LA    RF,CLIST-CLTHDR(RF)                                              
         LA    R1,220              220 PRDS IN CLIST                            
AP15     CLI   3(RF),0             EOL                                          
         BNE   *+6                                                              
         DC    H'0'                PRD NOT IN CLTHDR                            
         CLC   BPIGGY,3(RF)                                                     
         BE    AP18                                                             
         LA    RF,4(RF)                                                         
         BCT   R1,AP15                                                          
*                                                                               
         CLI   NETPSW,C'Y'         TEST NETPAK                                  
         BE    *+6                 ONLY CLIST2 FOR NET                          
         DC    H'0'                                                             
         L     RF,ADCLT                                                         
         LA    RF,CLIST2-CLTHDR(RF)                                             
         LA    R1,35               35 PRODUCTS IN CLIST2                        
AP15A    CLI   3(RF),0             EOL                                          
         BNE   *+6                                                              
         DC    H'0'                PRD NOT IN CLTHDR                            
         CLC   BPIGGY,3(RF)                                                     
         BE    AP18                                                             
         LA    RF,4(RF)                                                         
         BCT   R1,AP15A                                                         
         DC    H'0'                                                             
*                                                                               
AP18     MVC   APYPRD2,0(RF)       SET PRD MNEMONIC                             
*                                                                               
AP20     EDIT  (B1,BEST),(3,APYEST),FILL=0                                      
         CLI   BEST,0              EST=NO=ALL                                   
         BNE   AP21                                                             
         EDIT  (B1,INVEST),(3,APYEST),FILL=0                                    
*                                                                               
AP21     XC    WORK(25),WORK                                                    
         MVC   WORK+2(3),BSTA                                                   
         GOTO1 MSUNPK,DMCB,WORK,WORK+10,WORK+15                                 
         MVC   APYSTA,WORK+15                           STATION                 
         MVC   APYMKT,QMKT                              MARKET                  
*                                                                               
         GOTO1 DATCON,DMCB,(2,BQENDP),(9,APYMONTH)      MONTH                   
*                                                                               
         MVC   APYPAYER,QUESTOR                         PAYER=REQUESTOR         
*                                                                               
         MVC   APYERRS,PAYERRS     ERRORS                                       
*                                                                               
         CLC   =C'CK',QAGY         COKE?                                        
         BNE   AP24                                                             
         CLC   SVBUYID,SPACES                                                   
         BNH   AP24                                                             
         MVC   APYACN,SVBUYID      ACN NUMBER FOR COKE PAY                      
*                                                                               
AP24     CLI   SREPSW,C'Y'                                                      
         BNE   AP25                                                             
         L     R2,SREPPARS+4       A(SREP TABLE)                                
         USING SREPD,R2                                                         
         CLI   SREPCOD,X'FF'                                                    
         BE    AP25                                                             
         XC    WORK(3),WORK                                                     
         GOTO1 VRCPACK,DMCB,(C'U',SREPCOD),WORK                                 
         TM    SVCOPT4,COP4TRD     CLIENT TRADE=T                               
         BNO   *+14                                                             
         CLC   DARREP,WORK         IS IT TRADE REP?                             
         BE    AP25                SKIP - WON'T AUTOPAY COS2 ANYWAY             
         MVC   APYSREP,WORK                                                     
         DROP  R2                                                               
*                                                                               
AP25     GOTO1 RECUP,DMCB,APIO,APELEM,APIO+24                                   
*                                                                               
         USING APYIEL,R8           INVOICE LIST ELEM                            
         LA    R8,APELEM                                                        
         XC    APELEM,APELEM                                                    
         MVI   APYIEL,APYIELQ       X'02'                                       
         MVI   APYILEN,APYIHLNQ                                                 
*                                                                               
         LA    R0,APYIHLNQ         HEADER LEN                                   
         LA    R3,APYIINV          FIRST INVOICE                                
         SR    R4,R4               COUNT INVOICES                               
*                                                                               
         L     R2,AINOLST                  TABLE OF INVOICE #S                  
         USING INOLSTD,R2                                                       
AP30     CLI   INOLINV,C' '                                                     
         BNH   AP35                                                             
*                                                                               
         OC    INOLREP,INOLREP     IS THERE A REP ON THE INVOICE?               
         BZ    AP33                NO - THEN LIST IT                            
         TM    SVCOPT4,COP4TRD     CLIENT TRADE=T                               
         BNO   AP33                                                             
         XC    WORK(3),WORK                                                     
         GOTO1 VRCPACK,DMCB,(C'U',INOLREP),WORK                                 
         CLC   DARREP,WORK         IS IT TRADE REP?                             
         BE    AP34                SKIP - WON'T AUTOPAY COS2 ANYWAY             
*                                                                               
AP33     MVC   0(L'INOLINV,R3),INOLINV     INVIOCE #                            
         AHI   R0,APYILENQ                 LENGTH OF EACH INV                   
         AHI   R3,APYILENQ                                                      
         LA    R4,1(R4)                                                         
         CHI   R4,25                       REACHED 25 INVOICES?                 
         BE    AP35                        YES - NO MORE ROOM!                  
*                                                                               
AP34     AH    R2,INOLSTQ                  NEXT                                 
         B     AP30                                                             
*                                                                               
AP35     STC   R0,APYILEN                  ELEM LENGTH                          
         STC   R0,IELEMLEN                 SAVE ELEMENT LENGTH                  
         STC   R4,APYINUM                  NUMBER OF INVOICES                   
         DROP  R2                                                               
*                                                                               
         GOTO1 RECUP,DMCB,APIO,APELEM,APIO+24+APYLN2Q                           
*                                                                               
         BRAS  RE,REDIRECT         ADD A REDIRECT ELEMENT?                      
         BNE   AP38                NO                                           
*                                                                               
         USING APYIDEL,R8          ID ELEMENT                                   
AP37     LA    R8,APELEM           BUILD ELEMENT HERE                           
         XC    APELEM,APELEM       CLEAR ELEMENT                                
         MVI   APYIDEL,APYIDELQ    X'03' ELEMENT CODE                           
         MVI   APYIDLEN,APYIDLNQ   ELEMENT LENGTH                               
         MVC   APYIDUSR,6(R3)      USER ID                                      
         MVC   APYIDID,14(R3)      PRINT QUEUE ID                               
         DROP  R8                                                               
*                                                                               
AP37A    LLC   R0,IELEMLEN         LENGTH OF X'02' ELEMENT                      
         LA    R8,APIO+24+APYLN2Q  LENGTH OF KEY + X'01' ELEMENT                
         AR    R8,R0               PUT X'03' ELEMENT HERE                       
         GOTO1 RECUP,DMCB,APIO,APELEM,(R8)                                      
*                                                                               
*---------------------------------------------------------------------          
*  SEE IF RECORD ALREADY EXISTS AND ADD NEW OR MERGE MONTHS                     
*---------------------------------------------------------------------          
*                                                                               
AP38     GOTO1 HIGH                                                             
         CLC   KEY(13),KEYSAVE                                                  
         BNE   AP50                ADD NEW RECORD                               
         L     R1,=A(APIO2)                                                     
         ST    R1,AREC                                                          
         GOTO1 GET                                                              
*                                                                               
         MVI   MULTMONS,C'N'       ASSUME NOT MULTIPLE MONTHS IN RECORD         
         L     R3,AREC             COPY 01 ELEMS FROM OLD TO NEW RECORD         
         LA    R3,24(R3)           FIRST ELEM                                   
AP40     CLI   0(R3),0                                                          
         BE    AP45                                                             
         CLI   0(R3),X'01'                                                      
         BE    AP42                                                             
AP41     ZIC   R1,1(R3)                                                         
         AR    R3,R1                                                            
         B     AP40                                                             
*                                                                               
         USING APYEL,R3                                                         
AP42     MVC   SVMONTH,APYMONTH    SEE IF MONTH ALREADY ON NEW REC              
         BAS   RE,CHKMONTH                                                      
         BNE   AP41                ALREADY IN NEW REC - NEXT ELEM               
         DROP  R3                                                               
*                                                                               
         ZIC   R1,1(R3)            COPY OLD X'01' ELEM TO NEW REC               
         AHI   R1,-1                                                            
         BM    AP41                                                             
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   APELEM(0),0(R3)                                                  
         GOTO1 RECUP,DMCB,APIO,APELEM,APIO+24                                   
         MVI   MULTMONS,C'Y'       SET FLAG                                     
         B     AP41                                                             
*                                                                               
AP45     LA    R1,APIO                                                          
         ST    R1,AREC                                                          
         CLI   MULTMONS,C'Y'                                                    
         BNE   *+8                                                              
         OI    APYRCNTL,APYRCMON   SET MULTIPLE MONTHS                          
         NI    APYRCNTL,X'FF'-APYKCPRC   TURN OFF PROCESSED                     
         GOTO1 PUT                                                              
*        BAS   RE,PRTPREC          PRINT OUT CHANGED RECORD                     
         LA    R6,KEY                                                           
         NI    APYKCNTL,X'FF'-APYKCPRC   TURN OFF PROCESSED                     
         CLI   MULTMONS,C'Y'                                                    
         BNE   *+8                                                              
         OI    APYKCNTL,APYKCMON   SET MULTIPLE MONTHS                          
         GOTO1 WRITE                                                            
         B     APX                                                              
*                                                                               
AP50     DS    0H                                                               
         MVC   KEY,KEYSAVE                                                      
         GOTO1 ADD                                                              
         B     EXIT                                                             
         EJECT                                                                  
**********************************************************************          
*        ADD AUTOPAY RECORD FOR SUCCESSFUL MATCHES  - NETPAK                    
**********************************************************************          
AP100    DS    0H                                                               
*                                                                               
         L     R1,ALINV            A(LAST INV)                                  
         S     R1,AFINV            A(FIRST INV)                                 
         BNP   APX                 NO IVOICES THEN DON'T DO ANYTHING            
*                                                                               
         GOTO1 DATCON,DMCB,(4,RCDATE),(2,CDATE)       RUN DATE                  
         XC    CDATE,=X'FFFF'       COMPLIMENT                                  
         CLI   MULTIMON,C'Y'       IF MULTIPLE MONTHS THEN NO AP                
         BE    EXIT                                                             
*                                                                               
         MVI   BPIGGY,0            GET CORRECT PIGGY                            
         MVC   BPIGGY,SVBPRD2      IF QPRD2=PRD                                 
         CLI   BPRD2,0                                                          
         BE    *+10                                                             
         MVC   BPIGGY,BPRD2        IF QPRD2=ALL                                 
*                                                                               
         CLI   BEST,0              EST=NO=ALL                                   
         BNE   AP107                                                            
         USING IELEMD,R3           INVOICE TABLE                                
         L     R3,AFINV                                                         
AP106    C     R3,ALINV            LAST INVOICE                                 
         BNL   AP107                                                            
*                                                                               
         C     R3,AFINV            FIRST INVOICE                                
         BNE   *+10                                                             
         MVC   INVEST,IEST         IF ALL INVOICES ARE FOR THE SAME EST         
         CLC   INVEST,IEST         THEN USE THAT ESTIMATE TO PAY                
         BE    AP106NXT                                                         
         MVI   INVEST,0            IF NOT THE SAME FORCE TO 0 FOR ERROR         
         B     AP107                                                            
*                                                                               
AP106NXT AH    R3,IELMLEN                                                       
         B     AP106                                                            
         DROP  R3                                                               
         EJECT                                                                  
*                                                                               
*---------------------------------------------------------------------          
*        BUILD AUTOPAY KEY BASE (W/O INVOICE NUMBER) AND SAVE IN XKEY           
*---------------------------------------------------------------------          
         USING NAPRECD,R6                                                       
AP107    LA    R6,XKEY                                                          
         XC    XKEY,XKEY                                                        
         MVI   NAPKTYP,NAPKTYPQ    X'0D'                                        
         MVI   NAPKSUB,NAPKSUBQ    X'3F'                                        
         MVC   NAPKDATE,CDATE      COMPLIMENTED RUN DATE                        
         MVC   NAPKAGMD,BAGYMD                                                  
         MVC   NAPKCLT,BCLT        CLIENT                                       
         MVC   NAPKPRD,BPRD        PRODUCT                                      
         MVC   NAPKPTN,BPIGGY      PARTNER                                      
         MVC   NAPKEST,BEST        ESTIMATE                                     
         CLI   BEST,0              EST=NO=ALL                                   
         BNE   *+10                                                             
         MVC   NAPKEST,INVEST      USE ESTIMATE FROM INVOICES (IF ONE)          
         MVC   NAPKSTA,BSTA        STATION                                      
         GOTO1 DATCON,DMCB,(2,BQENDP),(3,WORK)     MONTH                        
         MVC   NAPKMON,WORK        BINARY YEAR/MONTH                            
         DROP  R6                                                               
*                                                                               
*---------------------------------------------------------------------          
*        BUILD AUTOPAY RECORD - X'01' ELEM                                      
*---------------------------------------------------------------------          
*                                                                               
AP140    LA    RE,APIO             CLEAR IO AREA                                
         LHI   RF,2000                                                          
         SR    R1,R1                                                            
         MVCL  RE,R0                                                            
*                                                                               
         USING NAPRECD,R6                                                       
         LA    R6,APIO             BUILD REC IN APIO                            
         ST    R6,AREC                                                          
         MVC   0(L'NAPKEY,R6),XKEY                                              
         MVI   NAPRLEN+1,42        BASE LENGTH                                  
         DROP  R6                                                               
*                                                                               
         USING NAPEL,R8                                                         
         LA    R8,APELEM                                                        
         XC    APELEM,APELEM                                                    
         MVI   NAPEL,NAPELQ        X'01'                                        
         MVI   NAPLEN,NAPLENQ                                                   
         MVC   NAPMED,QMED         MEDIA                                        
         L     RF,ADCLT                                                         
         IC    R0,CPROF+6-CKEY(RF)                                              
         GOTO1 CLUNPK,DMCB,((R0),BCLT),NAPCLT                                   
*                                                                               
*        MVC   NAPPRD,SVPRD        PRODUCT                                      
         MVC   NAPPRD,PRD          PRODUCT                                      
*                                                                               
*        CLI   BPIGGY,0           ANY PIGGY                                     
*        BE    AP150                                                            
         MVC   NAPPRD2,PRD2       SET PRD MNEMONIC                              
*&&DO                                                                           
         L     RF,ADCLT                                                         
         LA    RF,CLIST-CLTHDR(RF)                                              
         LA    R1,220              220 PRDS IN CLIST                            
AP145    CLI   3(RF),0             EOL                                          
         BNE   *+6                                                              
         DC    H'0'                PRD NOT IN CLTHDR                            
         CLC   BPIGGY,3(RF)                                                     
         BE    AP148                                                            
         LA    RF,4(RF)                                                         
         BCT   R1,AP145                                                         
*                                                                               
         L     RF,ADCLT                                                         
         LA    RF,CLIST2-CLTHDR(RF)                                             
         LA    R1,35               35 PRODUCTS IN CLIST2                        
AP145A   CLI   3(RF),0             EOL                                          
         BNE   *+6                                                              
         DC    H'0'                PRD NOT IN CLTHDR                            
         CLC   BPIGGY,3(RF)                                                     
         BE    AP148                                                            
         LA    RF,4(RF)                                                         
         BCT   R1,AP145A                                                        
         DC    H'0'                                                             
*                                                                               
AP148    MVC   NAPPRD2,0(RF)       SET PRD MNEMONIC                             
*&&                                                                             
AP150    EDIT  (B1,BEST),(3,NAPEST),FILL=0                                      
         CLI   BEST,0              EST=NO=ALL                                   
         BNE   AP151                                                            
         EDIT  (B1,INVEST),(3,NAPEST),FILL=0                                    
*                                                                               
AP151    XC    WORK(25),WORK                                                    
         MVC   WORK+2(3),BSTA                                                   
         GOTO1 MSUNPK,DMCB,WORK,WORK+10,WORK+15                                 
         MVC   NAPSTA,WORK+15                           STATION                 
         MVC   NAPMKT,QMKT                              MARKET                  
*                                                                               
         GOTO1 DATCON,DMCB,(2,BQENDP),(9,NAPMONTH)      MONTH                   
*                                                                               
         MVC   NAPMNTYP,Q2USER+1       CALENDAR MONTH                           
         CLI   NAPMNTYP,C' '                                                    
         BH    *+10                                                             
         MVC   NAPMNTYP,PROGPROF+9     THEN I2 PROFILE                          
*                                                                               
         CLI   I2IPROF,C'Y'        INTEGRATION OPTIONS                          
         BNE   *+8                                                              
         MVI   NAPPTYP,C'B'        SET TO B = TIME+INT                          
         CLI   I2IPROF+1,C'Y'      MATCH ON INTEGRATION ONLY                    
         BNE   *+8                                                              
         MVI   NAPPTYP,C'I'        SET TO I= INT ONLY                           
         CLI   Q2USER+2,C' '       PROFILE OVERRIDDEN                           
         BNH   AP152                                                            
         CLI   Q2USER+2,C'I'       INT+TIME RUN                                 
         BNE   *+8                                                              
         MVI   NAPPTYP,C'B'        SET TO B = TIME+INT                          
         CLI   Q2USER+2,C'O'       INT ONLY RUN                                 
         BNE   *+8                                                              
         MVI   NAPPTYP,C'I'        SET TO I= INT ONLY                           
*                                                                               
AP152    MVC   NAPPAYER,QUESTOR                         PAYER=REQUESTOR         
*                                                                               
         CLI   MEDFILT,C' '        ANY SUB-MEDIA                                
         BNH   *+10                                                             
         MVC   NAPSMED,MEDFILT     THEN USE SUB MEDIA                           
*                                                                               
         CLI   SREPSW,C'Y'                                                      
         BNE   AP155                                                            
         L     R2,SREPPARS+4       A(SREP TABLE)                                
         USING SREPD,R2                                                         
         CLI   SREPCOD,X'FF'                                                    
         BE    AP155                                                            
         LH    R0,SREPCOD                                                       
         CVD   R0,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  NAPSREP,DUB                                                      
         DROP  R2                                                               
*                                                                               
AP155    GOTO1 RECUP,DMCB,(C'T',APIO),APELEM,APIO+42                            
         DROP  R8                                                               
*                                                                               
*---------------------------------------------------------------------          
*        BUILD AUTOPAY RECORD - X'02' ELEM - INVOICE $                          
*        LOOP THRU INVOICE TABLE - 1 RECORD PER INVOICE                         
*---------------------------------------------------------------------          
*                                                                               
AP160    DS    0H                                                               
         L     R4,AINOLST                                                       
         USING INOLSTD,R4                                                       
AP162    CLI   0(R4),C' '          TEST ANY INVOICES                            
         BNH   APX                                                              
*                                                                               
         XC    FULL,FULL           TOTAL MATCHED UNPAID GROSS                   
         XC    WORD,WORD           TOTAL MATCHED UNPAID NET                     
         USING IELEMD,R3           INVOICE TABLE                                
         L     R3,AFINV                                                         
AP165    C     R3,ALINV            LAST INVOICE                                 
         BNL   AP175                                                            
*                                                                               
         CLC   INOLNUM,IINVID      RIGHT INVOICE                                
         BNE   AP170               NO - TRY NEXT                                
         OC    IBY,IBY             MATCHED?                                     
         BZ    AP190               DETAIL NOT MATCHED =NO PAY                   
*                                  AND GO TO NEXT INVOICE                       
*                                                                               
         USING BYELEMD,R8                                                       
         SR    R8,R8               POINT TO MATCHING BUY                        
         ICM   R8,7,IBY                                                         
         A     R8,AFBY                                                          
         BCTR  R8,R0                                                            
*                                                                               
* FOR NET AUTOPAY NEED TO SEND THE FULL INVOICE AMOUNT TO PAY                   
* -IF ANYTHING IS ALREADY PAID THEY SHOULD GET AMOUNTS DON'T MATCH              
* -REPORTED BACK ON THEIR SIW                                                   
*                                                                               
***>     TM    BYSTAT,X'40'        TEST PAID                                    
***>     BO    AP170               YES - SKIP                                   
*                                                                               
         ICM   R0,15,FULL          MATCHED COST - GROSS                         
         ICM   R1,15,ICOST                                                      
         TM    ISTAT,X'01'         TEST NEG                                     
         BZ    *+6                                                              
         LCR   R1,R1                                                            
         AR    R0,R1                                                            
         STCM  R0,15,FULL                                                       
*                                                                               
         ICM   R0,15,WORD          MATCHED COST - NET                           
         ICM   R1,15,BYNET                                                      
         TM    BYSTAT,X'01'        TEST NEG                                     
         BZ    *+6                                                              
         LCR   R1,R1                                                            
         AR    R0,R1                                                            
         STCM  R0,15,WORD                                                       
*                                                                               
AP170    AH    R3,IELMLEN                                                       
         B     AP165                                                            
         DROP  R3,R8                                                            
*                                                                               
         USING NAPRECD,R6                                                       
AP175    LA    R6,APIO                                                          
         MVC   NAPKINV,INOLINV     SET INVOICE NUMBER IN RECORD                 
*                                                                               
         CLI   APIO+42+NAPLENQ,X'02'   DELETE LAST 02 ELEM                      
         BNE   AP177                                                            
         GOTO1 RECUP,DMCB,(C'T',APIO),APIO+42+NAPLENQ,0                         
*                                                                               
         USING NAPIEL,R8                                                        
AP177    LA    R8,APELEM                                                        
         XC    APELEM,APELEM                                                    
         MVI   NAPIEL,NAPIELQ      X'02'                                        
         MVI   NAPILEN,NAPILENQ                                                 
         MVC   NAPIINV,INOLINV     SET INVOICE NUMBER IN ELEM                   
         MVC   NAPIDOLS,FULL                                                    
         MVC   NAPINET,WORD                                                     
         GOTO1 RECUP,DMCB,(C'T',APIO),APELEM,APIO+42+NAPLENQ                    
         DROP  R8                                                               
*                                                                               
*---------------------------------------------------------------------          
*        READ FOR RECORD                                                        
*---------------------------------------------------------------------          
*                                                                               
         MVI   RECEXIST,C'N'                                                    
         MVC   XKEY(32),0(R6)      SET KEY WITH INV NUMBER                      
         MVC   XKEYSAVE,XKEY                                                    
         GOTO1 DATAMGR,DMCB,DMRDHI,=C'XSPDIR',XKEY,XKEY                         
         CLC   XKEY(L'NAPKEY),XKEYSAVE      RECORD EXIST?                       
         BNE   AP180                                                            
         MVI   RECEXIST,C'Y'                                                    
         B     AP190               <----                                        
*                                                                               
AP180    MVC   XKEY,XKEYSAVE                                                    
         CLI   RCWRITE,C'N'                                                     
         BE    AP190                                                            
         GOTO1 DATAMGR,DMCB,ADDREC,=C'XSPFIL',XKEY,APIO,DMWORK                  
         CLI   8(R1),0                                                          
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
AP190    AH    R4,INOLSTQ          NEXT INVOICE                                 
         B     AP162                                                            
         DROP  R4                                                               
*                                                                               
APX      B     EXIT                                                             
         EJECT                                                                  
**********************************************************************          
*        ROUTINES                                                               
*                                                                               
*---------------------------------------------------------------------          
* CHECK NEW RECORD FOR X'01' ELEM FOR MONTH IN SVMONTH                          
*---------------------------------------------------------------------          
CHKMONTH NTR1                                                                   
         LA    R3,APIO             NEW REOCRD                                   
         LA    R3,24(R3)           FIRST ELEM                                   
CM10     CLI   0(R3),0                                                          
         BE    XYES                NO MATCH FOUND = ADD ELEM TO NEW REC         
         CLI   0(R3),X'01'                                                      
         BE    CM20                                                             
CM15     ZIC   R1,1(R3)                                                         
         AR    R3,R1                                                            
         B     CM10                                                             
*                                                                               
         USING APYEL,R3                                                         
CM20     CLC   SVMONTH,APYMONTH    SEE IF MONTH ALREADY ON NEW REC              
         BE    XNO                 ALREADY IN NEW REC - DON'T ADD AGAIN         
         B     CM15                CHECK NEXT ELEM                              
         DROP  R3                                                               
*---------------------------------------------------------------------          
* PRINT RECORD                                                                  
*---------------------------------------------------------------------          
*                                                                               
PRTAREC  NTR1                                                                   
         MVC   P(6),=C'ADDREC'                                                  
         B     PREC10                                                           
PRTPREC  NTR1                                                                   
         MVC   P(6),=C'PUTREC'                                                  
PREC10   DS    0H                                                               
         GOTO1 HEXOUT,DMCB,(R6),P+10,50,=C'N'                                   
         GOTO1 REPORT                                                           
         LA    R2,APIO                                                          
         LA    R2,24(R2)                                                        
PREC20   CLI   0(R2),0                                                          
         BE    EXIT                                                             
         ZIC   R1,0(R2)                                                         
         AHI   R1,-1                                                            
         BM    PREC30                                                           
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   P(0),0(R2)                                                       
         GOTO1 REPORT                                                           
PREC30   LA    R2,1(R1,R2)                                                      
         B     PREC20                                                           
         EJECT                                                                  
*---------------------------------------------------------------------          
*        CHECK FILM TABLE TO MAKE SURE COMML RECS ARE FOR RIGHT PRD             
*---------------------------------------------------------------------          
*                                                                               
*&&DO                                                                           
FILMERR  NTR1                                                                   
         L     R2,AFLMTAB                                                       
         USING FLMTABD,R2                                                       
         ICM   R3,15,FLMTPARS+8    NO OF ENTRIES                                
         BZ    FEX                                                              
FE10     OC    FLMCOD,SPACES                                                    
         XC    KEY,KEY             READ FOR FILM                                
         MVC   KEY(2),=X'0A21'                                                  
         MVC   KEY+2(1),BAGYMD                                                  
         OC    KEY+3(2),SVTRACLT   USE TRAFFIC OVERRIDE CLIENT                  
         BNZ   *+10                IF ANY                                       
         MVC   KEY+3(2),BCLT                                                    
         MVC   KEY+5(8),FLMCOD     FILM CODE                                    
         GOTO1 HIGH                                                             
         CLC   KEY(13),KEYSAVE                                                  
         BNE   FE60                                                             
*        LA    R1,APIO2                                                         
         L     R1,=A(APIO2)                                                     
         ST    R1,AREC                                                          
         GOTO1 GET                                                              
*        LA    R4,APIO2                                                         
         L     R4,=A(APIO2)                                                     
         LA    R4,24(R4)           FIRST ELEM                                   
FE20     CLI   0(R4),0                                                          
         BE    FE60                                                             
         CLI   0(R4),X'20'         PRODUCT LIST ELEM                            
         BE    FE50                                                             
         ZIC   R1,1(R4)                                                         
         AR    R4,R1                                                            
         B     FE20                                                             
*                                                                               
FE50     ZIC   R1,1(R4)            ELEM LEN                                     
         AHI   R1,-2               SUBTRACT ELEM CODE AND LEN = # PRDS          
         LA    R4,2(R4)                                                         
FE55     CLC   BPRD,0(R4)                                                       
         BE    FE60                COMML INCLUDES PRD = OK - GET NEXT           
         LA    R4,1(R4)                                                         
         BCT   R1,FE55                                                          
         OI    PAYERRS,PAYERRS_INVPRD    FILM CODE NOT VALID FOR PRD            
*                                                                               
FE60     LA    R2,FLMTABEL(R2)                                                  
         BCT   R3,FE10                                                          
*                                                                               
FEX      XIT1                                                                   
         DROP  R2                                                               
         EJECT                                                                  
*&&                                                                             
*---------------------------------------------------------------------          
* THIS ROUTINE INITIALIZES MINIO VARIABLES                                      
*---------------------------------------------------------------------          
*                                                                               
MNINIT   NTR1                                                                   
         LA    R0,MINBLOCK         CLEAR MINBLOCK                               
         LA    R1,MINBLKL                                                       
         SR    RF,RF                                                            
         MVCL  R0,RE                                                            
*                                                                               
         MVC   MINCOMF,ACOMFACS    A(COMFACS)                                   
         MVC   MINRECUP,RECUP      A(RECUP)                                     
         MVI   MINOPEN,C'N'        SET NOT OPEN                                 
         MVC   MINFIL,=CL8'XSPFIL'     FILE NAME                                
         MVC   MINDIR,=CL8'XSPDIR'     DIR NAME                                 
         MVI   MINFKLEN,L'MSRKEY   KEY LENGTH                                   
         MVI   MINEKLEN,L'MSRKMINK   ELEMENT KEY LENGTH                         
         MVI   MINEKDSP,L'MSRKMAST   DISPLACEMENT TO ELEMENT KEY                
         MVI   MINNCTL,L'MSRDSTAT  NUMBER OF CONTROL BYTES                      
         MVC   MINFRCLM,=AL2(3975) MAXIMUM RECORD LENGTH                        
         LA    RF,MBUFFR           A(MINIO BUFFER)                              
         ST    RF,MINBUFF                                                       
         MVI   MINNBUF,2           USE ONE BUFFER                               
*                                                                               
         LA    R1,MRECTAB          A(AREA FOR RECORD TABLE)                     
         ST    R1,MINRTAB                                                       
         MVC   MINRTABL,=Y(MRECTABL)  LENGTH OF RECORD TABLE                    
*                                                                               
         LA    RE,MELEM            A(AREA FOR ELEM OR CLUSTER)                  
         ST    RE,MINELEM                                                       
         MVC   MINMAXEL,=Y(L'MELEM)   MAX LENGTH OF ELEM OF CLUSTER             
         XC    0(L'MELEM,RE),0(RE)   CLEAR MINELEM AREA                         
*                                                                               
         CLI   RCWRITE,C'N'                                                     
         BNE   *+8                                                              
         MVI   MINWRITE,C'N'                                                    
*                                                                               
         MVI   MINDELSW,C'N'       BYPASS DELETES                               
*                                                                               
MINITX   B     EXIT                                                             
         EJECT                                                                  
*---------------------------------------------------------------------          
*        CHECK MSR REC FOR FLIGHT OR BUDGET ERRORS - COKE                       
*        NO MORE COKE - THIS CODE IS COMMENTED OUT !                            
*---------------------------------------------------------------------          
*                                                                               
*&&DO                                                                           
COKERRS  NTR1                                                                   
         BAS   RE,MNINIT           INIT MINIO                                   
*                                                                               
         XC    MINMKEY,MINMKEY     READ FOR MSR REC                             
         LA    R2,MINMKEY                                                       
         USING MSRKEYD,R2                                                       
         MVI   MSRKTYPE,MSRKTYPQ   X'0E'                                        
         MVI   MSRKSUB,MSRKSUBQ    X'04'                                        
         MVC   MSRKAM,BAGYMD                                                    
         MVC   MSRKCLT,BCLT                                                     
         MVC   MSRKPRD,BPRD                                                     
         MVC   MSRKEST,BEST                                                     
         CLI   BEST,0              EST=NO=ALL                                   
         BNE   *+10                                                             
         MVC   MSRKEST,INVEST      USE ESTIMATE FROM INVOICES (IF ONE)          
         MVC   MSRKMKT,BMKT                                                     
         MVC   MSRKSTA,BSTA                                                     
         CLI   CANADSW,C'Y'        CABLE FOR US NOT CANADA                      
         BE    CE05                                                             
         CLI   MSRKSTA,X'E8'       TEST CABLE                                   
         BL    *+8                                                              
         NI    MSRKSTA+2,X'80'     TURN OFF NETWORK BITS                        
*                                                                               
CE05     GOTO1 DATCON,DMCB,(2,BQENDP),(3,WORK)        GET YMD BINARY            
         MVI   WORK+2,1            FORCE TO DAY 1                               
         GOTO1 DATCON,DMCB,(3,WORK),(2,WORK+3)        COMPRESS                  
         MVC   MSRKMOS,WORK+3      MOS                                          
         XC    MSRKMOS,=X'FFFF'    COMPLIMENTED                                 
*                                                                               
         GOTO1 VMINIO,DMCB,('MINOPN',MINBLKD)                                   
         CLI   MINERR,0                                                         
         BNE   CEX                 NO RECORD TO CHECK                           
*                                                                               
         XC    MINEKEY,MINEKEY                                                  
         MVI   MINEKEY,MSRSTELQ    X'10' MATCHING STATUS ELEM                   
         GOTO1 VMINIO,DMCB,('MINHI',MINBLKD)                                    
         B     CE10                                                             
CE10SEQ  GOTO1 VMINIO,DMCB,('MINSEQ',MINBLKD)                                   
CE10     CLI   MINERR,0            TREAT ALL ERRS HERE AS EOF                   
         BNE   CE50                                                             
         CLI   MELEM,MSRSTELQ      STILL X'10' ELEMS                            
         BNE   CE10SEQ                                                          
         LA    R8,MELEM                                                         
         USING MSRSTELD,R8                                                      
         MVC   TEMPBYTE,MSRSTBGS                                                
         NI    TEMPBYTE,X'FE'      TURN OFF X'01'                               
*                                                                               
*        REMOVE THIS AFTER NEW CD REPORT GOES LIVE                              
***7/28  NI    TEMPBYTE,X'FF'-X'20'  IGNORE BUYS NO BUDGET ERROR                
*                                                                               
         OC    PAYERRS,TEMPBYTE    ADD ANY BUY/GOAL ERRORS                      
*                                                                               
         NI    MSRMPERR,X'FF'-MSRMPFPR   SET FILM ERRS IN INCH REC              
         TM    PAYERRS,PAYERRS_INVPRD    FILM CODE NOT VALID FOR PRD            
         BNO   *+8                                                              
         OI    MSRMPERR,MSRMPFPR                                                
*                                                                               
         NI    MSRMPERR,X'FF'-MSRMPFIN                                          
         TM    PAYERRS,PAYERRS_FILMINV   INVALID FILM CODE                      
         BNO   *+8                                                              
         OI    MSRMPERR,MSRMPFIN                                                
*                                                                               
         NI    MSRMPERR,X'FF'-MSRMPFRR                                          
         TM    PAYERRS,PAYERRS_RELREC    RELEASE/RECALL ERROR                   
         BNO   *+8                                                              
         OI    MSRMPERR,MSRMPFRR                                                
*                                                                               
         GOTO1 VMINIO,DMCB,('MINWRT',MINBLKD)                                   
         B     CE10SEQ                      LIKE OVERBUDGET/NO BUDGET           
         DROP  R8                                                               
*                                                                               
CE50     LA    R8,MELEM                                                         
         USING MSRICELD,R8         X'13' INCH STATUS                            
         XC    MINEKEY,MINEKEY                                                  
         MVI   MINEKEY,MSRICELQ    X'13'                                        
         GOTO1 VMINIO,DMCB,('MINHI',MINBLKD)                                    
         CLI   MELEM,MSRICELQ      MAKE SURE IT'S A X'13'                       
         BE    CE55                                                             
*                                                                               
         XC    MELEM,MELEM         NEED TO ADD ELEM                             
         MVI   MSRICEL,X'13'                                                    
         MVI   MSRICLEN,MSRICLNQ                                                
         MVI   MSRICST,C'H'        ASSUME HOLD                                  
         CLI   PAYERRS,0                                                        
         BNE   *+8                                                              
         MVI   MSRICST,C'A'        IF NO ERRORS THEN A=APPROVED                 
         GOTO1 DATCON,DMCB,(4,RCDATE),(0,MSRICDAT)    RUN DATE                  
         MVC   MSRICPER(13),=C'AUTO APPROVED'                                   
         GOTO1 VMINIO,DMCB,('MINADD',MINBLKD)                                   
         CLI   MINERR,0                                                         
         BE    CE60                                                             
         B     CE100                                                            
*                                                                               
CE55     MVI   MSRICST,C'H'        ASSUME HOLD                                  
         CLI   PAYERRS,0                                                        
         BNE   *+8                                                              
         MVI   MSRICST,C'A'        IF NO ERRORS THEN A=APPROVED                 
         GOTO1 DATCON,DMCB,(4,RCDATE),(0,MSRICDAT)    RUN DATE                  
         MVC   MSRICPER,SPACES                                                  
         MVC   MSRICPER(13),=C'AUTO APPROVED'                                   
         GOTO1 VMINIO,DMCB,('MINWRT',MINBLKD)                                   
         CLI   MINERR,0                                                         
         BE    CE60                                                             
         B     CE100                                                            
         DROP  R8                                                               
*                                                                               
CE60     CLI   PAYERRS,0           DO WE NEED TO UPDATE PAY STATUS              
         BNE   CE100               NOT IF ERRORS                                
         LA    R8,MELEM                                                         
         USING MSRIPELD,R8         X'14' PAY  STATUS                            
         XC    MINEKEY,MINEKEY                                                  
         MVI   MINEKEY,MSRIPELQ    X'14'                                        
         GOTO1 VMINIO,DMCB,('MINHI',MINBLKD)                                    
         CLI   MELEM,MSRIPELQ      MAKE SURE IT'S A X'14'                       
         BE    CE65                                                             
*                                                                               
         XC    MELEM,MELEM         NEED TO ADD ELEM                             
         MVI   MSRIPEL,X'14'                                                    
         MVI   MSRIPLEN,MSRIPLNQ                                                
         MVI   MSRIPST,C'Y'        WILL AUTOPAY                                 
         GOTO1 DATCON,DMCB,(4,RCDATE),(0,TEMPDATE)   RUN DATE                   
         GOTO1 ADDAY,DMCB,TEMPDATE,MSRIPDAT,1        ADD 1 FOR PAY DATE         
         GOTO1 VMINIO,DMCB,('MINADD',MINBLKD)                                   
         B     CE100                                                            
*                                                                               
CE65     MVI   MSRIPST,C'Y'        WILL AUTOPAY                                 
         GOTO1 DATCON,DMCB,(4,RCDATE),(0,TEMPDATE)   RUN DATE                   
         GOTO1 ADDAY,DMCB,TEMPDATE,MSRIPDAT,1        ADD 1 FOR PAY DATE         
         GOTO1 VMINIO,DMCB,('MINWRT',MINBLKD)                                   
*                                                                               
CE100    GOTO1 VMINIO,DMCB,('MINCLS',MINBLKD)                                   
*                                                                               
CEX      XIT1                                                                   
         EJECT                                                                  
*&&                                                                             
**********************************************************************          
*        LTORG                                                                  
**********************************************************************          
         LTORG                                                                  
         EJECT                                                                  
**********************************************************************          
*        IO AREAS                                                               
**********************************************************************          
APIO     DS    CL2000                                                           
APIO2    DS    CL2000                                                           
*                                                                               
REDIRECT NTR1  BASE=*,LABEL=*                                                   
*                                                                               
         L     R8,ADCLT            A(CLIENT RECORD)                             
         USING CLTHDR,R8           CLIENT RECORD DSECT                          
*                                                                               
         L     R5,ADCONLST         A(ADCONS)                                    
         USING SPADCONS,R5         ADCONS DSECT FOR VOFFICER                    
*                                                                               
         LAY   R3,RDTABLE          AGENCY REDIRECT TABLE                        
*                                                                               
RD10     CLI   0(R3),X'FF'         END OF TABLE?                                
         JE    RDNO                YES - NO REDIRECT                            
         CLC   QAGY,0(R3)          DOES THIS AGENCY HAVE A REDIRECT?            
         BNE   RD20                NO - CHECK NEXT ENTRY                        
*                                                                               
         XC    WORK,WORK           CLEAR WORK                                   
         LA    R4,WORK             R2 = WORK                                    
         USING OFFICED,R4          OFFICER DSECT                                
         MVI   OFCSYS,C'S'         SYSTEM = SPOT                                
         MVC   OFCAGY,QAGY         AGENCY                                       
         MVC   OFCLMT,2(R3)        OFFICE OR OFFICE LIST                        
         MVC   OFCOFC,COFFICE      CLIENT OFFICE                                
*                                                                               
         GOTO1 VOFFICER,DMCB,(C'N',WORK),ACOMFACS                               
*                                                                               
         CLI   0(R1),0             INCLUDE THIS CLIENT?                         
         JE    RDYES               YES - ADD THE X'03' ELEMENT                  
*                                                                               
RD20     LA    R3,16(R3)           NO - BUMP TO NEXT TABLE ENTRY                
         B     RD10                PROCESS NEXT TABLE ENTRY                     
*                                                                               
RDYES    SR    RC,RC               SET CC EQU                                   
*                                                                               
RDNO     LTR   RC,RC               SET CC NEQ                                   
*                                                                               
         XIT1  REGS=(R3)           RETURN BUT LEAVE R3 AS IS                    
*                                                                               
         DROP  R4,R5,R8            DROP USINGS                                  
*                                                                               
XAPY     NTR1  BASE=*,LABEL=*                                                   
*                                                                               
         MVI   NUMINV,0            SET NUMBER OF X'10' ELEMS TO 0               
         XC    REPCODE,REPCODE     NO REP                                       
         XC    PACKREP,PACKREP     NO PACKED REP                                
         LA    R4,1                PROCESS ONE RECORD                           
         CLI   I2PPROF+7,C'I'      AUTOPAY LOCAL CABLE WITH INV REP?            
         BNE   APX9A               NO                                           
         CLI   BSTA,X'E8'          YES - IS THIS LOCAL CABLE?                   
         BL    APX9A               NO                                           
*                                                                               
         MVI   NUMREPS,0           NO REPS IN TABLE YET                         
         XC    REPTAB,REPTAB       CLEAR THE REP TABLE                          
         L     R3,AINOLST          TABLE OF INVOICE NUMBER AND REPS             
         USING INOLSTD,R3          INVOICE NUMBER LIST DSECT                    
         XR    RF,RF               CLEAR RF                                     
*                                                                               
APX00    CLI   INOLINV,C' '        ANY INVOICES LEFT?                           
         BNH   APX04               NO - DONE                                    
*                                                                               
         LA    R2,REPTAB           REP TABLE                                    
         ICM   RF,1,NUMREPS        ANY REPS IN TABLE THUS FAR?                  
         BZ    APX02               NO - ADD THE REP                             
APX01    CLC   INOLREP,1(R2)       THIS REP ALREADY IN THE TABLE?               
         BE    APX03               YES - DON'T ADD IT TWICE!                    
         LA    R2,3(R2)            BUMP TO NEXT TABLE ENTRY                     
         BCT   RF,APX01            LOOP BACK AND CHECK NEXT TABLE ENTRY         
*                                                                               
APX02    OC    INOLREP,INOLREP     ANY REP?                                     
         BZ    APX02D              NO                                           
         L     R5,AFINV            A(FIRST INV)                                 
         USING IELEMD,R5           INVOICE ELEMENT DSECT                        
*                                                                               
APX02A   C     R5,ALINV            DONE PROCESSING THIS INVOICE?                
         BNL   APX02D              YES - EXCLUDE THIS INVOICE                   
         CLC   INOLNUM,IINVID      MATCH ON INTERNAL ID?                        
         BE    APX02C              YES - CHECK THE MATCHING BUY'S REP           
APX02B   AH    R5,IELMLEN          NEXT INVOICE ITEM                            
         B     APX02A              PROCESS NEXT INVOICE DETAIL                  
*                                                                               
APX02C   XR    R1,R1               CLEAR R1                                     
         ICM   R1,7,IBY            HAVE POINTER TO MATCHING BUY?                
         BZ    APX02B              NO, GO TO NEXT INVOICE DETAIL                
         A     R1,AFBY             YES - ADD A(FIRST BUY) TO OFFSET             
         BCTR  R1,0                POINT TO MATCHING BUY                        
         USING BYELEMD,R1          BUY ELEMENT DSECT                            
         CLC   BYSREP,INOLREP      MATCH ON REP OR NO REP?                      
         BE    *+8                 YES                                          
         OI    0(R2),APX_PREP      NO - PAY THIS REP WITH A "P"                 
         DROP  R1,R5               DROP BUY AND INV BUFFER USINGS               
*                                                                               
APX02D   MVC   1(2,R2),INOLREP     ADD THE REP TO THE REP TABLE                 
         IC    RF,NUMREPS          CURRENT NUMBER OF REPS                       
         LA    RF,1(RF)            ADD ONE                                      
         CHI   RF,MAXREPS          BLEW REP TABLE?                              
         BNH   *+6                 NO                                           
         DC    H'0'                YES - DEATH                                  
         STC   RF,NUMREPS          NEW NUMBER OF REPS                           
*                                                                               
APX03    AH    R3,INOLSTQ          NEXT INVOICE NUMBER                          
         B     APX00               LOOP BACK AND CHECK FOR REP                  
         DROP  R3                  DROP R3                                      
*                                                                               
APX04    MVI   CABLEREP,C'N'       DEFAULT TO NO REPS ON INVOICE                
         OC    REPTAB,REPTAB       ANY REPS?                                    
         BZ    APX9A               NO - USE BUY REPS!                           
         MVI   CABLEREP,C'Y'       HAVE REPS ON INVOICE                         
         LA    R2,REPTAB           REP TABLE                                    
         IC    R4,NUMREPS          NUMBER OF REPS                               
*                                                                               
APX05    MVI   NUMINV,0            SET NUMBER OF X'10' ELEMS TO 0               
         MVC   PACKREP,1(R2)       SAVE PACKED REP                              
         XC    REPCODE,REPCODE     DEFAULT TO NO REP                            
         OC    PACKREP,PACKREP     HAVE A PACKED REP?                           
         BZ    APX10               NO                                           
         GOTO1 VRCPACK,DMCB,(C'U',PACKREP),REPCODE                              
         B     APX10               ADD XAUTOPAY REC WITH THIS REP               
*                                                                               
APX9A    CLI   SREPSW,C'Y'         HAVE ANY REPS?                               
         BNE   APX10               NO                                           
         ICM   RF,15,SREPPARS+8    HAVE ANY REPS IN TABLE?                      
         BZ    APX10               NO                                           
         LR    R4,RF               R4 = NUMBER OF REPS TO PROCESS               
         L     R2,SREPPARS+4       A(SREP TABLE)                                
*                                                                               
         USING SREPD,R2            REP TABLE DSECT                              
APX9B    XC    REPCODE,REPCODE     DEFAULT TO NO REP                            
         XC    PACKREP,PACKREP     NO PACKED REP                                
         MVI   NUMINV,0            SET NUMBER OF X'10' ELEMS TO 0               
         CLI   SREPCOD,X'FF'       NO REP ENTRY?                                
         BE    APX10               YES                                          
         MVC   PACKREP,SREPCOD     SAVE PACKED REP                              
         GOTO1 VRCPACK,DMCB,(C'U',SREPCOD),REPCODE                              
         DROP  R2                                                               
*                                                                               
         USING APXRECD,R6          AUTOPAY DSECT                                
APX10    LA    R6,XKEY             R6=XKEY                                      
         XC    XKEY,XKEY           CLEAR THE KEY                                
         MVI   APXKTYP,APXKTYPQ    X'0D'                                        
         MVI   APXKSUB,APXKSUBQ    X'3A'                                        
         MVC   APXKDATE,CDATE      COMPLIMENTED RUN DATE                        
         MVC   APXKAGMD,BAGYMD     A/M                                          
         MVC   APXKCLT,BCLT        CLIENT                                       
         MVC   APXKPRD,SVPRD       3-CHAR PRODUCT                               
         XC    THREE,THREE         CLEAR PIGGY                                  
         CLI   BPIGGY,0            HAVE PIGGYBACK PRODUCT?                      
         BE    *+14                NO                                           
         BAS   RE,GETPIGGY         YES - GET 3-CHAR PIGGY PRODUCT               
         MVC   APXKPTN,THREE       3-CHAR PIGGY                                 
         MVC   APXKEST,BEST        ESTIMATE                                     
         CLI   BEST,0              EST=NO/ALL?                                  
         BNE   *+10                NO                                           
         MVC   APXKEST,INVEST      YES - USE EST FROM INVOICES (IF ONE)         
         MVC   APXKMKT,BMKT        MARKET                                       
         MVC   APXKSTA,BSTA        STATION                                      
         CLI   CANADSW,C'Y'        CANADIAN?                                    
         BE    APX12               YES                                          
         CLI   APXKSTA,X'E8'       US CABLE?                                    
         BL    APX12               NO                                           
         NI    APXKSTA+2,X'80'     YES - TURN OFF NETWORK BITS                  
*                                                                               
APX12    MVC   APXKMON,BQENDP      PACKED DATE                                  
         MVC   APXKREP,REPCODE     REP CODE                                     
         L     RE,ADCLT            A(CLIENT RECORD)                             
         MVC   APXKOFC,COFFICE-CKEY(RE) CLIENT OFFICE                           
         CLI   PAYERRS,0           ANY PAY ERRORS?                              
         BE    *+8                 NO                                           
         OI    APXKCNTL,APXKCERR   TURN ON ERROR EXISTS BIT                     
*                                                                               
*---------------------------------------------------------------------          
* BUILD AUTOPAY RECORD                                                          
*---------------------------------------------------------------------          
*                                                                               
         LAY   RE,APIO             RE = AIO AREA                                
         LHI   RF,2000             RF = 2000 BYTES TO CLEAR                     
         SR    R1,R1               R1 = 0 FOR MVCL                              
         MVCL  RE,R0               CLEAR IO AREA                                
*                                                                               
         LAY   R6,APIO             R6 = AIO AREA                                
         ST    R6,AREC             AREC = APIO                                  
         MVC   0(32,R6),XKEY       MOVE THE KEY TO THE RECORD                   
         MVI   APXRLEN+1,42        RECORD LENGTH                                
*                                                                               
         CLI   PAYERRS,0           ANY PAY ERRORS?                              
         BE    *+8                 NO                                           
         OI    APXRCNTL,APXRCERR   YES - TURN ON ERROR EXISTS BIT               
         DROP  R6                  DROP R6                                      
*                                                                               
         USING APXEL,R8            X'01' ELEMENT DSECT                          
         LA    R8,APELEM           BUILD X'01' ELEM HERE                        
         XC    APELEM,APELEM       CLEAR                                        
         MVI   APXEL,APXELQ        X'01'                                        
         MVI   APXLEN,APXLNQ       LENGTH                                       
         MVC   APXMED,QMED         MEDIA                                        
         L     RF,ADCLT            A(CLIENT RECORD)                             
         IC    R0,CPROF+6-CKEY(RF) AAN FIELD                                    
         GOTO1 CLUNPK,DMCB,((R0),BCLT),APXCLT                                   
*                                                                               
         MVC   APXPRD,SVPRD        3-CHAR PRODUCT                               
         MVC   APXPTN,THREE        3-CHAR PIGGY                                 
         EDIT  (B1,BEST),(3,APXEST),FILL=0                                      
         CLI   BEST,0              EST=NO/ALL?                                  
         BNE   APX12A              NO - REQUEST EST SET                         
         EDIT  (B1,INVEST),(3,APXEST),FILL=0                                    
*                                                                               
APX12A   XC    WORK(25),WORK                                                    
         MVC   APXMKT,QMKT         EBCIDIC MARKET                               
         XC    WORK(25),WORK       CLEAR WORK                                   
         MVC   WORK+2(3),BSTA      BINARY STATION FOR MSUNPK                    
         GOTO1 MSUNPK,DMCB,WORK,WORK+10,WORK+15                                 
         MVC   APXSTA,WORK+15      EBCEDIC STATION                              
         GOTO1 DATCON,DMCB,(2,BQENDP),(9,APXMONTH)                              
         MVC   APXPAYER,QUESTOR    PAYER=REQUESTOR                              
         MVC   APXERRS,PAYERRS     ERRORS                                       
         MVC   APXSREP,REPCODE     REP CODE                                     
         CLI   I2PPROF+7,C'I'      AUTOPAY LOCAL CABLE WITH INV REP?            
         BNE   APX12B              NO                                           
         CLI   BSTA,X'E8'          YES - IS THIS LOCAL CABLE?                   
         BL    APX12B              NO                                           
         CLI   CABLEREP,C'Y'       HAVE REPS ON INVOICE?                        
         BNE   APX12B              NO                                           
         OC    APXFLAGS,0(R2)      PAY THIS REP WITH A "P" IF FLAG SET          
         DROP  R8                  DROP R8                                      
*                                                                               
APX12B   LAY   R0,APIO             A(AUTOPAY RECORD)                            
         AHI   R6,42                                                            
         GOTO1 RECUP,DMCB,(C'T',(R0)),APELEM,(R6)                               
*                                                                               
         AHI   R6,APXLNQ           PUT NEXT ELEMENT HERE                        
*                                                                               
         BRAS  RE,REDIRECT         ADD A REDIRECT ELEMENT?                      
         BNE   APX15               NO                                           
*                                                                               
         USING APXIDEL,R8          ID ELEMENT                                   
         LA    R8,APELEM           BUILD ELEMENT HERE                           
         XC    APELEM,APELEM       CLEAR ELEMENT                                
         MVI   APXIDEL,APXIDELQ    X'03' ELEMENT CODE                           
         MVI   APXIDLEN,APXIDLNQ   ELEMENT LENGTH                               
         MVC   APXIDUSR,6(R3)      USER ID                                      
         MVC   APXIDID,14(R3)      PRINT QUEUE ID                               
         DROP  R8                                                               
*                                                                               
         LAY   R0,APIO             A(AUTOPAY RECORD)                            
         GOTO1 RECUP,DMCB,(C'T',(R0)),APELEM,(R6)                               
*                                                                               
         AHI   R6,APYIDLNQ         PUT NEXT ELEMENT HERE                        
*                                                                               
APX15    L     R3,AINOLST          TABLE OF INVOICE NUMBER AND REPS             
         USING INOLSTD,R3          INVOICE NUMBER LIST DSECT                    
APX20    CLI   INOLINV,C' '        ANY INVOICE?                                 
         BNH   APX40               NO - DONE                                    
*                                                                               
         CLI   I2PPROF+7,C'I'      AUTOPAY LOCAL CABLE WITH INV REP?            
         BNE   APX21               NO                                           
         CLI   BSTA,X'E8'          YES - IS THIS LOCAL CABLE?                   
         BL    APX21               NO                                           
         CLI   CABLEREP,C'Y'       HAVE REPS ON INVOICE?                        
         BNE   APX21               NO                                           
         CLC   PACKREP,INOLREP     PROCESS THIS INVOICE?                        
         BNE   APX24A              NO - BUMP TO NEXT INVOICE                    
         B     APX25               YES                                          
*                                                                               
APX21    L     R5,AFINV            A(FIRST INV)                                 
         USING IELEMD,R5           INVOICE ELEMENT DSECT                        
*                                                                               
APX22    C     R5,ALINV            DONE PROCESSING THIS INVOICE?                
         BNL   APX24A              YES - EXCLUDE THIS INVOICE                   
         CLC   INOLNUM,IINVID      MATCH ON INTERNAL ID?                        
         BE    APX24               YES - CHECK THE MATCHING BUY'S REP           
APX23    AH    R5,IELMLEN          NEXT INVOICE ITEM                            
         B     APX22               PROCESS NEXT INVOICE DETAIL                  
*                                                                               
APX24    XR    R1,R1               CLEAR R1                                     
         ICM   R1,7,IBY            HAVE POINTER TO MATCHING BUY?                
         BZ    APX23               NO, GO TO NEXT INVOICE DETAIL                
         A     R1,AFBY             YES - ADD A(FIRST BUY) TO OFFSET             
         BCTR  R1,0                POINT TO MATCHING BUY                        
         USING BYELEMD,R1          BUY ELEMENT DSECT                            
         CLC   BYSREP,PACKREP      MATCH ON REP OR NO REP?                      
         BE    APX25               YES - FIND THESE INVOICES                    
         DROP  R1,R5               DROP BUY AND INV BUFFER USINGS               
*                                                                               
APX24A   AH    R3,INOLSTQ          NEXT INVOICE NUMBER                          
         B     APX20               LOOP BACK AND CHECK FOR REP                  
*                                                                               
APX25    BAS   RE,BUILDINV         BUILD INVOICE ELEMENT                        
*                                                                               
         LAY   R0,APIO             A(AUTOPAY RECORD)                            
         GOTO1 RECUP,DMCB,(C'T',(R0)),APELEM,(R6)                               
         LLC   R0,NUMINV           NUMBER OF X'10' ELEMS THUS FAR               
         AHI   R0,1                PLUS THE ONE WE JUST ADDED                   
         CHI   R0,13               DID WE JUST ADD THE 13TH X'10' ELEM          
         BE    APX40               YES - DONE (ONLY ROOM FOR 13 IN PAY)         
         STC   R0,NUMINV           NO - SAVE THE NUMBER OF X'10' ELEMS          
*                                                                               
         AHI   R6,APXILNQ          PUT NEXT ELEMENT HERE                        
         B     APX24A              PROCESS NEXT INVOICE                         
         DROP  R3                  DROP R3                                      
*                                                                               
APX40    MVC   XKEYSAVE,XKEY       SAVE OFF THE AUTOPAY KEY                     
         GOTO1 DATAMGR,DMCB,DMRDHI,=C'XSPDIR',XKEY,XKEY                         
         CLC   XKEY(32),XKEYSAVE   RECORD ALREADY EXISTS?                       
         BNE   APX45               NO                                           
*                                                                               
         LAY   R0,APIO2            PUT THE EXISTING REC INTO APIO2              
         GOTO1 DATAMGR,DMCB,GETREC,=C'XSPFIL',XKEY+36,(R0),DMWORK               
*                                                                               
         CLI   RCWRITE,C'N'        WRITE BACK TO FILE?                          
         BE    APX50               NO                                           
         CLI   CANADSW,C'Y'        CANADIAN?                                    
         BNE   APX41               NO                                           
         CLC   SVBUYID,SPACES      RUNNING I2 BY BUY-ID?                        
         BNH   APX41               NO                                           
         LAY   R6,APIO2            A(EXISTING AUTOPAY RECORD)                   
         USING APXRECD,R6          AUTOPAY DSECT                                
         TM    APXRCNTL,APXRCPRC   ALREADY PROCESSED?                           
         BO    APX41               YES A DIFFERENT I2 CREATED THIS              
         DROP  R6                  DROP AUTOPAY RECORD USING                    
*                                                                               
         XR    R5,R5               USED FOR COUNTING X'10' ELEMENTS             
         MVI   ELCODE,APXIELQ      X'10' ELEMENT                                
         BAS   RE,GETEL            HAVE A X'10' ELEMENT?                        
         B     *+8                 GO TEST CC                                   
APX40A   BAS   RE,NEXTEL           HAVE ANOTHER X'10' ELEMENT?                  
         BNE   APX40B              NO                                           
         AHI   R5,1                INCRIMENT X'10' ELEM COUNTER                 
         B     APX40A              GET NEXT X'10' ELEMENT                       
*                                                                               
APX40B   CHI   R5,13               ARE WE MAXED OUT ON X'10' ELEMENTS?          
         BNL   APX41               YES - CAN'T ADD MORE X'10' ELEMENTS          
         STC   R5,NUMINV           CURRENT NUMBER OF X'10' ELEMENTS             
         LR    R5,R6               INSERT ADDTIONAL X'10' ELEMENTS HERE         
*                                                                               
         LAY   R6,APIO             A(NEW AUTOPAY RECORD)                        
         MVI   ELCODE,APXIELQ      X'10' ELEMENT                                
         BAS   RE,GETEL            HAVE A X'10' ELEMENT?                        
         B     *+8                 GO TEST CC                                   
APX40C   BAS   RE,NEXTEL           HAVE ANOTHER X'10' ELEMENT?                  
         BNE   APX40D              NO                                           
*                                                                               
         LAY   R0,APIO2            UPDATE APIO2                                 
         GOTO1 RECUP,DMCB,(C'T',(R0)),(R6),(R5)                                 
*                                                                               
         LLC   R0,1(R5)            LENGTH OF NEWLY ADDED X'10' ELEM             
         AR    R5,R0               BUMP R5                                      
*                                                                               
         LLC   R0,NUMINV           NUMBER OF X'10' ELEMS THUS FAR               
         AHI   R0,1                PLUS THE ONE WE JUST ADDED                   
         CHI   R0,13               DID WE JUST ADD THE 13TH X'10' ELEM          
         BE    APX40D              YES - DONE (ONLY ROOM FOR 13 IN PAY)         
         STC   R0,NUMINV           NO - SAVE THE NUMBER OF X'10' ELEMS          
         B     APX40C              GET NEXT X'10' ELEMENT                       
*                                                                               
APX40D   LAY   R0,APIO2            UPDATED APIO2                                
         B     APX42               USE UPDATED APIO2 FOR PUTREC                 
*                                                                               
APX41    LAY   R0,APIO             A(LATEST AUTOPAY RECORD)                     
*                                                                               
APX42    GOTO1 DATAMGR,DMCB,PUTREC,=C'XSPFIL',XKEY+36,(R0),DMWORK               
         CLI   8(R1),0             ANY ERRORS?                                  
         BE    APX50               NO                                           
         DC    H'0'                YES - DEATH                                  
*                                                                               
APX45    MVC   XKEY,XKEYSAVE       RESTORE AUTOPAY KEY                          
         CLI   RCWRITE,C'N'        WRITE BACK TO FILE?                          
         BE    APX50               NO                                           
         LAY   R0,APIO             A(AUTOPAY RECORD)                            
         GOTO1 DATAMGR,DMCB,ADDREC,=C'XSPFIL',XKEY,(R0),DMWORK                  
         CLI   8(R1),0             ANY ERRORS?                                  
         BE    *+6                 NO                                           
         DC    H'0'                YES - DEATH                                  
*                                                                               
APX50    CLI   I2PPROF+7,C'I'      AUTOPAY LOCAL CABLE WITH INV REP?            
         BNE   APX55               NO                                           
         CLI   BSTA,X'E8'          YES - IS THIS LOCAL CABLE?                   
         BL    APX55               NO                                           
         CLI   CABLEREP,C'Y'       HAVE REPS ON INVOICE?                        
         BNE   APX55               NO - PROCESSING BUY REPS                     
         LA    R2,3(R2)            BUMP TO NEXT REP IN TABLE                    
         BCT   R4,APX05            LOOP BACK AND PROCESS NEXT REP               
         J     EXIT                DONE ADDING AUTOPAY RECORDS                  
*                                                                               
APX55    AHI   R2,SREPL            BUMP SREP TABLE                              
         BCT   R4,APX9B            LOOP BACK AND PROCESS NEXT REP               
         J     EXIT                DONE ADDING AUTOPAY RECORDS                  
         EJECT                                                                  
*                                                                               
GETPIGGY L     RF,ADCLT            A(CLIENT RECORD)                             
         LA    RF,CLIST-CLTHDR(RF) RF = CLIST                                   
         LA    R1,220              220 PRDS IN CLIST                            
*                                                                               
GP10     CLI   3(RF),0             EOL?                                         
         BNE   *+6                 NO                                           
         DC    H'0'                YES - PRD NOT IN CLTHDR                      
         CLC   BPIGGY,3(RF)        MATCH ON BINARY PRODUCT?                     
         BE    GP20                YES                                          
         LA    RF,4(RF)            NO - BUMP TO NEXT ENTRY IN CLIST             
         BCT   R1,GP10             LOOP BACK AND TEST NEXT PRD                  
         DC    H'0'                                                             
*                                                                               
GP20     MVC   THREE,0(RF)         SET PRD MNEMONIC                             
         BR    RE                                                               
*                                                                               
BUILDINV NTR1                                                                   
*                                                                               
         USING INOLSTD,R3          INVOICE NUMBER LIST DSECT                    
         XC    FULL,FULL           TOTAL MATCHED UNPAID GROSS                   
         XC    WORD,WORD           TOTAL MATCHED UNPAID NET                     
         XC    DUB,DUB             TOTAL MATCHED UNPAID TAX                     
*                                                                               
         USING IELEMD,R4           INVOICE TABLE                                
         L     R4,AFINV            A(FIRST INV)                                 
BINV10   C     R4,ALINV            PAST LAST INV?                               
         BNL   BINV30              YES - ADD THE X'10' ELEMENT                  
*                                                                               
         CLC   IINVID,INOLNUM      SAME INTERNAL INVOICE NUMBER?                
         BNE   BINV20              NO - NEXT INVOICE                            
*                                                                               
         SR    R8,R8               CLEAR R8                                     
         ICM   R8,7,IBY            HAVE POINTER TO MATCHING BUY?                
         BZ    BINV20              NO, GO TO NEXT INVOICE                       
         A     R8,AFBY             YES - ADD A(FIRST BUY) TO OFFSET             
         BCTR  R8,0                POINT TO MATCHING BUY                        
         USING BYELEMD,R8          BUY ELEMENT DSECT                            
*                                                                               
         ICM   R1,15,BYXAPYGN      COS2 DOLS IN BYCOST/BYNET?                   
         BZ    BINV15              NO                                           
         ICM   R0,15,FULL          RUNNING TOTALS FOR GROSS                     
         LA    RE,FULL             SAVE A(FULL)                                 
         CLI   A0GROSSN,C'G'       PAYING BY GROSS?                             
         BE    *+12                YES                                          
         ICM   R0,15,WORD          NO - RUNNING TOTALS FOR NET                  
         LA    RE,WORD             SAVE A(WORD)                                 
         TM    BYSTAT,X'01'        NEGATIVE?                                    
         BZ    *+6                 NO                                           
         LCR   R1,R1               YES - MAKE VALUE POSITIVE                    
         AR    R0,R1               ADD TO TOTAL                                 
         STCM  R0,15,0(RE)         SAVE RUNNING TOTAL                           
         B     BINV16              GO SAVE TAX AMOUNT                           
*                                                                               
BINV15   CLI   I2PPROF+6,C'I'      AUTOPAY CLEAR BY REP W/INV TAX?              
         BNE   BINV15A             NO                                           
         MVC   WORD,INOLAMT        NET+TAX AMOUNT                               
         B     BINV30              ONLY PROCESS ONCE                            
*                                                                               
BINV15A  ICM   R0,15,FULL          MATCHED COST - GROSS                         
         ICM   R1,15,BYCOST        GROSS VALUE FOR THIS DETAIL                  
         TM    BYSTAT,X'01'        NEGATIVE?                                    
         BZ    *+6                 NO                                           
         LCR   R1,R1               YES - MAKE VALUE POSITIVE                    
         AR    R0,R1               ADD TO TOTAL                                 
         STCM  R0,15,FULL          SAVE RUNNING GROSS TOTAL IN FULL             
*                                                                               
         ICM   R0,15,WORD          MATCHED COST - NET                           
         ICM   R1,15,BYNET         NET VALUE FOR THIS BUY                       
         TM    BYSTAT,X'01'        NEGATIVE?                                    
         BZ    *+6                 NO                                           
         LCR   R1,R1               YES - MAKE VALUE POSITIVE                    
         AR    R0,R1               ADD TO TOTAL                                 
         STCM  R0,15,WORD          SAVE RUNNING NET TOTAL IN WORD               
*                                                                               
BINV16   ICM   R0,15,DUB           MATCHED TAX                                  
         ICM   R1,15,BYTAX         TAX VALUE FOR THIS BUY                       
         AR    R0,R1               ADD TO TOTAL                                 
         STCM  R0,15,DUB           SAVE RUNNING TAX TOTAL IN DUB                
*                                                                               
BINV20   AH    R4,IELMLEN          BUMP TO NEXT DETAIL                          
         B     BINV10              GO PROCESS NEXT DETAIL                       
         DROP  R4,R8               DROP USINGS                                  
*                                                                               
         USING APXIEL,R8           INVOICE ELEM                                 
BINV30   LA    R8,APELEM           BUILD INVOICE ELEMENT HERE                   
         XC    APELEM,APELEM       CLEAR THE ELEMENT                            
         MVI   APXIEL,APXIELQ      X'10' ELEMENT                                
         MVI   APXILEN,APXILNQ     ELEMENT LENGTH                               
         MVC   APXIINV,INOLINV     INVOICE NUMBER                               
         L     R1,FULL             MATCHED COST - GROSS                         
         CVD   R1,DOUBLE           CONVERT GROSS TO PACKED                      
         ZAP   APXIDOLS,DOUBLE     GROSS DOLLARS                                
         L     R1,WORD             MATCHED COST - NET                           
         CVD   R1,DOUBLE           CONVERT NET TO PACKED                        
         ZAP   APXINET,DOUBLE      NET DOLLARS                                  
         L     R1,DUB              MATCHED TAX                                  
         CVD   R1,DOUBLE           CONVERT TAX TO PACKED                        
         ZAP   APXITAX,DOUBLE      TAX DOLLARS                                  
         DROP  R3,R8               DROP USINGS                                  
*                                                                               
         J     EXIT                EXIT                                         
*                                                                               
         GETEL R6,42,ELCODE        GETEL MACRO                                  
*                                                                               
         LTORG                                                                  
***                                                                             
* AGENCY OFFICE/OFFICE LIST REDIRECT (USER-ID/PRINT QUEUE ID) TABLE             
***                                                                             
*&&DO                                                                           
RDTABLE  DC    C'HY',C'$',C'S',C'  ',C'MAXTOA  ',X'41B1'                        
*&&                                                                             
RDTABLE  DC    C'HY',C'$',C'A',C'  ',C'MNSTOA  ',X'435A'                        
         DC    C'HY',C'$',C'Q',C'  ',C'MNSQU   ',X'435B'                        
         DC    C'HY',C'$',C'D',C'  ',C'MECEL   ',X'41B4'                        
         DC    C'HY',C'$',C'I',C'  ',C'MECEM   ',X'41B3'                        
         DC    C'HY',C'$',C'P',C'  ',C'MIEXCA  ',X'496B'                        
***      DC    C'HY',C'*',X'65',C'  ',C'SIXTOA  ',X'48C2'                       
         DC    C'HY',C'*',X'4E',C'  ',C'WAVETO  ',X'4C1F'                       
         DC    C'HY',C'*',X'78',C'  ',C'WAVEL   ',X'4C59'                       
         DC    C'UB',C'$',C'D',C'  ',C'CARPAY  ',X'40F7'                        
         DC    C'TH',C'$',C'S',C'  ',C'DFZRES  ',X'2F07'                        
         DC    C'TB',C'*',C'G',C'  ',C'ZOGO    ',X'3CC7'                        
         DC    C'OU',C'*',X'73',C'  ',C'OMGPGCA ',X'481E'                       
         DC    C'OU',C'*',X'74',C'  ',C'OMGHSC  ',X'49D5'                       
         DC    C'OU',C'*',X'78',C'  ',C'OMGROG  ',X'4E61'                       
         DC    C'OU',C'$',C'J',C'  ',C'PHDTOM  ',X'39F9'                        
         DC    C'HY',C'$',C'N',C'  ',C'NWOTOA  ',X'4CEA'                        
         DC    C'HY',C'$',C'5',C'  ',C'OPNTOA  ',X'5183'                        
         DC    C'TB',C'*',C'A',C'  ',C'APEXTO  ',X'4EBF'                        
         DC    X'FF'                                                            
*                                                                               
**********************************************************************          
*        WORKING STORAGE                                                        
**********************************************************************          
WORKD    DSECT                                                                  
SAVR1    DS    F                   CONTENTS OF R1 ON ENTRY                      
SAVERD   DS    F                   CONTENTS OF RD ON ENTRY                      
VMINIO   DS    V                                                                
TRACEOPT DS    CL1                                                              
CDATE    DS    CL2                                                              
TEMPDATE DS    CL6                                                              
SVMONTH  DS    CL6                                                              
MULTMONS DS    X                                                                
BPIGGY   DS    XL1                                                              
INVEST   DS    XL1                 ESTIMATE FROM INVOICES IF ALL SAME           
TEMPBYTE DS    XL1                                                              
PAYERRS  DS    XL1                 DON'T PAY ERRORS (COKE)                      
IELEMLEN DS    XL1                 X'02' ELEMENT LENGTH                         
*                                                                               
APELEM   DS    CL250                                                            
REPCODE  DS    CL3                 REP CODE                                     
PACKREP  DS    XL2                 PACKED REP CODE                              
NUMINV   DS    XL1                 NUMBER OF X'10' INV ELEMS (MAX 13)           
*                                                                               
RECEXIST DS    CL1                                                              
*                                                                               
CABLEREP DS    CL1                 REP ON INV FOR CABLE FLAG                    
*                                                                               
REPTAB   DS    CL30                REP TABLE                                    
NUMREPS  DS    XL1                 NUMBER OF REPS IN TABLE                      
MAXREPS  EQU   L'REPTAB/3          MAX REPS                                     
         DS    0D                                                               
         DC    CL8'*MELEM*'                                                     
MELEM    DS    XL256               MINIO ELEMENT                                
         DS    0D                                                               
         DC    CL8'*MBLOCK*'                                                    
MINBLOCK DS    CL(MINBLKL)                                                      
*                                                                               
MRECTABL EQU   2000                                                             
         DS    0D                                                               
         DC    CL8'*MRTAB*'                                                     
MRECTAB  DS    XL(MRECTABL)                                                     
*                                                                               
         DS    0D                                                               
         DC    CL8'*MBUFF*'                                                     
MBUFFR   DS    XL(3975*2)          ROOM FOR 2 BUFFERS                           
*                                                                               
WORKDL   EQU   *-WORKD                                                          
*                                                                               
       ++INCLUDE SPGENAPY                                                       
       ++INCLUDE SPGENXAPY                                                      
       ++INCLUDE NEGENAPY                                                       
         PRINT OFF                                                              
       ++INCLUDE DDMINBLK                                                       
       ++INCLUDE SPGENMSR                                                       
       ++INCLUDE SPGENCLT                                                       
       ++INCLUDE SPREPWORKD                                                     
       ++INCLUDE SPREPI2WKN                                                     
       ++INCLUDE DDCOMFACS                                                      
       ++INCLUDE DDOFFICED                                                      
         PRINT ON                                                               
*                                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'092SPREPI2AP 01/31/21'                                      
         END                                                                    
