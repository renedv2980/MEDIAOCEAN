*          DATA SET DDCHOPPER  AT LEVEL 009 AS OF 09/11/07                      
*CATALP CHOPPER                                                                 
         TITLE 'CHOPS UP A CONTINOUS STRING INTO FIXED BLOCK'                   
CHOPPER  CSECT                                                                  
         PRINT NOGEN                                                            
         USING CHOPD,RC                                                         
         NMOD1 CHOPX-CHOPD,**CHOP**                                             
         LM    R2,R3,0(R1)         PARAMETER 1 BYTE 0   L'STRING                
*                                              BYTE 1-3 A(STRING)               
         LLC   R4,0(R1)            PARAMETER 2 BYTE 0   WIDTH OF BLOCK          
*                                              BYTE 1-3 A(BLOCK)                
         LLC   R5,4(R1)            PARAMETER 3 MAX NUMBER OF LINES              
         L     R6,8(R1)                        SET TO ACTUAL LINES USED         
         LA    R6,0(R6)            CLEAR HOB                                    
         XC    DISP,DISP                                                        
         XC    ASEP,ASEP                                                        
         MVC   DISP+1(1),8(R1)     BYTE 0   0=CONTIGUOUS                        
         CLI   8(R1),C'P'                   N=DISPLACEMENT                      
         BNE   *+8                          C'P'=132                            
         MVI   DISP+1,132                                                       
         LTR   R4,R4               WAS LENGTH SUPPLIED IN P1(1)                 
         BNZ   CHSTART             YES, SO START PROCESSING                     
         CLC   =C'LEN=',12(R1)     PARAMETER 4 C'LEN='                          
         BE    CH08                YES SO LENGTH IN P5                          
         CLC   =C'DLM=',12(R1)     PARAMETER 4 C'DLM='                          
         BNE   EXIT                LENGTH WAS ZERO, SO NO GOOD                  
         ICM   RE,15,20(R1)        A(C'DDDDD'X'FF')                             
         BZ    CH08                NO ADDRESS SUPPLIED IGNORE IT THEN           
         ST    RE,ASEP                                                          
CH08     L     R4,16(,R1)          PARAMETER 5 FULLWORD LENGTH                  
                                                                                
*---------------------------------------------------------------------          
*                                                                               
* REGISTERS   R2 A(STRING)                                                      
* ---------   R3 A(BLOCK)                                                       
*             R4 L'STRING                                                       
*             R5 WIDTH OF BLOCK                                                 
*             R6 MAXIMUM LINES                                                  
*             R7 FIELD POINTER                                                  
*             R8 FIELD LENGTH                                                   
*             R9 LINES SO FAR                                                   
*---------------------------------------------------------------------          
         EJECT                                                                  
*---------------------------------------------------------------------          
*              CHECK INPUT AND CLEAR BLOCK                                      
*---------------------------------------------------------------------          
CHSTART  SR    R9,R9                                                            
         LTR   R4,R4               ANY LENGTH SUPPLIED?                         
         BZ    EXIT                NO                                           
         CHI   R5,2                                                             
         BL    EXIT                                                             
         LTR   R6,R6                                                            
         BZ    EXIT                                                             
*                                                                               
         LR    R7,R3                                                            
         LR    R8,R5                                                            
         SHI   R8,2                                                             
         LR    R9,R6                                                            
*                                                                               
CHCLEAR  MVI   0(R7),C' '                                                       
         EX    R8,LINECLR                                                       
         OC    DISP,DISP                                                        
         BZ    *+12                                                             
         AH    R7,DISP                                                          
         B     *+6                                                              
         AR    R7,R5                                                            
         BCT   R9,CHCLEAR                                                       
         B     CHLOOP                                                           
*                                                                               
LINECLR  MVC   1(0,R7),0(R7)                                                    
         EJECT                                                                  
*---------------------------------------------------------------------          
*              MAIN DIVIDING LOOP                                               
*---------------------------------------------------------------------          
CHLOOP   CLI   0(R2),C' '          IGNORE LEADING SPACES                        
         BNE   CH2                 AT THE BEGINNING OF A LINE                   
         LA    R2,1(R2)                                                         
         BCT   R4,CHLOOP                                                        
         B     EXIT                                                             
*                                                                               
CH2      CR    R4,R5               IF REMAINING BYTES CAN FIT                   
         BH    CH4                 IN A LINE THEN THIS IS THE LAST              
         BCTR  R4,R0                                                            
         EX    R4,MOVELINE                                                      
         LA    R9,1(R9)                                                         
         B     EXIT                                                             
*                                                                               
CH4      LA    R7,0(R5,R2)         SET UP TO SCAN BACKWARDS                     
         LR    R8,R5               FOR A DIVIDING CHARACTER                     
*                                                                               
CH6      DS    0H                                                               
         OC    ASEP,ASEP           DO WE HAVE USER-DEFINED SEP LIST?            
         BZ    CH6DEF              NO - USE DEFAULT                             
*                                                                               
         ICM   RF,15,ASEP                                                       
CH6A     CLI   0(RF),X'FF'         END OF DELIMITER LIST?                       
         BE    CH7                 CURRENT CHARACTER NOT A DELIMITER            
         CLC   0(1,RF),0(R7)                                                    
         BE    CH8                                                              
         LA    RF,1(RF)                                                         
         B     CH6A                                                             
*                                                                               
CH6DEF   DS    0H                  DEFAULT DELIMITERS HERE                      
         CLI   0(R7),C' '                                                       
         BE    CH8                                                              
         CLI   0(R7),C'-'                                                       
         BE    CH8                                                              
         CLI   0(R7),C','                                                       
         BNE   CH7                                                              
         CLI   1(R7),C'0'          IGNORE INTEGER,INTEGER                       
         BL    CH8                                                              
         LR    RE,R7                                                            
         BCTR  RE,0                                                             
         CLI   0(RE),C'0'                                                       
         BL    CH8                                                              
CH7      BCTR  R7,R0                                                            
         BCT   R8,CH6              IF WE DONT FIND THEN ITS TOUGH               
         LR    R8,R5               WE'LL SET FIELD LENGTH = WIDTH               
         BCTR  R8,0                                                             
*                                                                               
CH8      CR    R8,R5                                                            
         BNE   *+14                                                             
         CLI   0(R7),C','          IGNORE COMMA AT WIDTH+1                      
         BE    CH7                 TO PRECLUDE LINE STARTING WIDTH ONE          
         BCTR  R8,R0                                                            
         EX    R8,MOVELINE                                                      
         LA    R9,1(R9)                                                         
         CR    R9,R6               HAVE WE RUN OUT OF LINES                     
         BE    EXIT                                                             
         LA    R8,1(R8)                                                         
         AR    R2,R8               BUMP STRING BY FIELD LENGTH                  
         SR    R4,R8               REDUCE LENGTH BY FIELD LENGTH                
         OC    DISP,DISP                                                        
         BZ    *+12                                                             
         AH    R3,DISP             BUMP TO NEXT PRINT LINE OR ...               
         B     *+6                                                              
         AR    R3,R5               BUMP TO NEXT LINE IN BLOCK                   
         B     CHLOOP                                                           
*                                                                               
MOVELINE MVC   0(0,R3),0(R2)                                                    
*                                                                               
EXIT     ST    R9,8(R1)            RETURN NO OF LINES USED                      
         XMOD1 1                                                                
         SPACE 1                                                                
CHOPD    DSECT                                                                  
ASEP     DS    A                                                                
DISP     DS    H                                                                
CHOPX    EQU   *                                                                
*                                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'009DDCHOPPER 09/11/07'                                      
         END                                                                    
