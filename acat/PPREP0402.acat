*          DATA SET PPREP0402  AT LEVEL 028 AS OF 05/01/02                      
*CATALP PP0402N                                                                 
         TITLE 'PP0402 - ORIGINAL BILLING'                                      
PP0402   CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 0,PP0402                                                         
         SPACE 2                                                                
         L     RA,0(R1)                                                         
         USING PPWORKD,RA                                                       
         L     RC,PPFILEC                                                       
         LA    R9,4095(RC)                                                      
         LA    R9,1(R9)                                                         
         USING PPFILED,RC,R9                                                    
         LA    R8,SPACEND                                                       
         USING PBILWRKD,R8                                                      
         SPACE 2                                                                
*                                                                               
         CLI   MODE,PROCBUY                                                     
         BE    PRBY                                                             
         CLI   MODE,FBUYEST                                                     
         BE    FBYE                                                             
         CLI   MODE,FBUYPRO                                                     
         BE    FBYP                                                             
         CLI   MODE,FBUYCLI                                                     
         BE    FBYC                                                             
         CLI   MODE,LBUYEST                                                     
         BE    LBYE                                                             
         CLI   MODE,LBUYPRO                                                     
         BE    LBYP                                                             
         CLI   MODE,LBUYDIV                                                     
         BE    LBYV                                                             
         CLI   MODE,LBUYCLI                                                     
         BE    LBYC                                                             
         CLI   MODE,LBUYREQ                                                     
         BE    LBYQ                                                             
*                                                                               
         CLI   MODE,FBUYREQ                                                     
         BE    FBYQ                                                             
         CLI   MODE,RUNFRST                                                     
         BE    RFRST                                                            
         CLI   MODE,RUNLAST                                                     
         BE    RLAST                                                            
*                                                                               
EXIT     DS    0H                                                               
         XIT1                                                                   
         EJECT                                                                  
RFRST    DS    0H                                                               
         RELOC RELO                                                             
FBYQ     DS    0H                                                               
FBYC     DS    0H                                                               
FBYP     DS    0H                                                               
FBYE     DS    0H                                                               
LBYQ     DS    0H                                                               
*                                                                               
         L     R3,RELO                                                          
         GOTO1 =V(START),RR=RB                                                  
*                                                                               
         CLI   MODE,FBUYPRO                                                     
         BNE   EXIT                                                             
         OC    MANGRS(8),MANGRS                                                 
         BZ    EXIT                                                             
         BAS   RE,MANBIL                                                        
         B     EXIT                                                             
         SPACE 2                                                                
PRBY     DS    0H                                                               
*                                                                               
         CLI   MASTPRD,0                                                        
         BNE   PRBY2                                                            
         XC    WORK(7),WORK                                                     
         GOTO1 AGETFORM,DMCB,WORK  GET MASTHDR                                  
*                                                                               
PRBY2    DS    0H                                                               
         TM    PBUYCNTL,X'80'                                                   
         BNZ   EXIT                BYPASS DELETES                               
         MVC   BUYGRS(12),GROSS                                                 
*                                  DATE                                         
         LA    R3,MASTPRD                                                       
         USING HDRKEYD,R3                                                       
         CLC   PBDBDATE,BEND                                                    
         BH    EXIT                                                             
         CLC   PBDBDATE,BSTART                                                  
         BNL    PRBY4                                                           
         CLC   PBDBDATE(2),BSTART                                               
         BNL   EXIT                                                             
         CLI   PRIORSW,C'S'                                                     
         BL    EXIT                NO PRIOR                                     
         BE    PRBY4               PRIOR SEPARATE                               
*                                  IF PRIOR TOGETHER                            
*                                  SKIP ALL BEFORE EFF DATE                     
*                                  UNLESS REQ DATE IS LESS                      
         CLC   PBDBDATE(2),BILADAT                                              
         BNL   PRBY4                                                            
         CLC   BSTART(2),BILADAT                                                
         BNL   EXIT                                                             
PRBY4    DS    0H                                                               
         GOTO1 APOST,DMCB,PBUYREC                                               
         B     EXIT                                                             
         SPACE 2                                                                
LBYE     DS    0H                  LAST FOR EST                                 
*                                                                               
         CLI   ESTSW,C'A'          EST = ALL                                    
         BNE   EXIT                                                             
         BAS   RE,ORINV                                                         
         B     EXIT                                                             
         SPACE 3                                                                
LBYP     DS    0H                  LAST FOR PROD                                
*                                                                               
         CLI   PRDSW,C'P'                                                       
         BH    EXIT                                                             
*                                  IF PRD = ALL OR PRD, DO INV NOW              
         BAS   RE,ORINV                                                         
         B     EXIT                                                             
         SPACE 2                                                                
LBYV     DS    0H                  LAST FOR DIVISION                            
         CLI   QDIV,C' '                                                        
         BE    EXIT                                                             
         BAS   RE,ORINV                                                         
         B     EXIT                                                             
         SPACE 2                                                                
LBYC     DS    0H                  LAST FOR CLIENT                              
         BAS   RE,ORINV                                                         
         B     EXIT                                                             
         SPACE 2                                                                
RLAST    DS    0H                                                               
         L     R3,RELO                                                          
         GOTO1 =V(INVREG),RR=RB                                                 
         B     EXIT                                                             
         EJECT                                                                  
         SPACE 2                                                                
ORINV    NTR1                                                                   
         SPACE 2                                                                
         CLC   QSORT,SPACES                                                     
         BE    ORINV0                                                           
         CLI   MASTPRD,0           IF SORTING AND NO BUYS                       
         BE    ORINVX              CANNOT DO ANYTHING HERE                      
ORINV0   DS    0H                                                               
         CLI   DONESW,C'D'                                                      
         BE    ORINVX                                                           
*                                                                               
         CLI   MASTPRD,0           CHECK HAVE MAST HDR                          
         BNE   ORINV1                                                           
         XC    WORK(7),WORK                                                     
         GOTO1 AGETFORM,DMCB,WORK  GET MASTHDR                                  
*                                                                               
ORINV1   DS    0H                                                               
         OC    MANGRS(8),MANGRS                                                 
         BNZ   ORINV1B                                                          
         GOTO1 ABLDREV             BUILD LIST OF REVERSALS                      
*                                                                               
ORINV1B  DS    0H                                                               
         MVI   WORK+3,X'FF'                                                     
         GOTO1 APOST,DMCB,WORK     ROLL UP TOTALS                               
*                                                                               
         CLI   PRIORSW,C'S'        PRIOR MONTHS SEP                             
         BNE   *+8                 NO                                           
         BAS   RE,RESEQ            YES - RESEQ ACC'S BY YM                      
*                                                                               
         XC    NXTYM,NXTYM                                                      
ORINV2   DS    0H                                                               
         MVI   ADBSW,0                                                          
         GOTO1 AADBIL                                                           
*                                                                               
         MVI   CMNTCD,C'R'                                                      
         CLI   ADBSW,0                                                          
         BE    ORINV4                                                           
*                                                                               
         MVI   FORCEHED,C'Y'                                                    
         MVC   PAGE,=H'1'                                                       
         GOTO1 APRTACC                                                          
*                                                                               
         GOTO1 AGETNUM             GET NEXT INV NO.                             
ORINV4   DS    0H                                                               
         CLI   NXTYM,X'FF'                                                      
         BNE   ORINV2                                                           
*                                                                               
         XC    BSNUM,BSNUM         RESET TABLE                                  
         MVI   DONESW,C'D'         INVOICE DONE                                 
         MVI   MASTPRD,0           CLEAR MAST HDR                               
ORINVX   DS    0H                                                               
         B     EXIT                                                             
         EJECT                                                                  
*                             RESEQUENCE ACCUMS                                 
         SPACE 2                                                                
RESEQ    NTR1                                                                   
         SPACE 2                                                                
         LM    R2,R4,BSTAB         TRB,NUM,LEN                                  
         LTR   R3,R3                                                            
         BZ    EXIT                NO ENTRIES                                   
RSQ2     DS    0H                                                               
         MVC   WORK(3),0(R2)       SWAP PRD-YM                                  
         MVC   0(2,R2),3(R2)                                                    
         MVC   2(3,R2),WORK                                                     
         AR    R2,R4                                                            
         BCT   R3,RSQ2                                                          
*                                                                               
         XC    DMCB(24),DMCB                                                    
         MVC   DMCB(16),BSTAB                                                   
         GOTO1 XSORT,DMCB                                                       
*                                                                               
         LM    R2,R4,BSTAB                                                      
RSQ4     DS    0H                                                               
         MVC   WORK(3),2(R2)       RESWAP PRD-YM                                
         MVC   3(2,R2),0(R2)                                                    
         MVC   0(3,R2),WORK                                                     
         AR    R2,R4                                                            
         BCT   R3,RSQ4                                                          
*                                                                               
         B     EXIT                                                             
         EJECT                                                                  
*                                  HANDLE MANUAL AMOUNTS                        
         SPACE 2                                                                
*                                  HANDLE MANUAL AMOUNTS                        
         SPACE 2                                                                
MANBIL   NTR1                                                                   
         SPACE 2                                                                
         MVI   FCRDBUY,C'N'        SUPPRESS REEADING BUYS                       
         MVC   BUYGRS,MANGRS                                                    
         MVC   BUYCD,MANCD                                                      
         L     RF,MANGRS                                                        
         M     RE,=F'85'                                                        
         SLDL  RE,1                                                             
         D     RE,=F'100'                                                       
         LTR   RF,RF                                                            
         BM    *+8                                                              
         A     RF,=F'1'                                                         
         SRA   RF,1                RF = NET                                     
         LCR   RF,RF                                                            
         A     RF,MANGRS                                                        
         ST    RF,BUYAC                                                         
         MVI   PBUYREC+3,X'20'                                                  
         MVC   PBDBDATE,BSTART                                                  
         MVC   PBUYKPRD,PPRDKPRD                                                
         PACK  DUB,QEST                                                         
         CVB   R0,DUB                                                           
         STH   R0,HALF                                                          
         MVC   PBUYKEST,HALF                                                    
         GOTO1 APOST,DMCB,PBUYREC                                               
*                                                                               
         B     EXIT                                                             
         SPACE 3                                                                
         LTORG                                                                  
         EJECT                                                                  
*                                                                               
       ++INCLUDE PBILWRK                                                        
*                                                                               
         PRINT OFF                                                              
       ++INCLUDE PPNEWFILE                                                      
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'028PPREP0402 05/01/02'                                      
         END                                                                    
