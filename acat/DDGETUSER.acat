*          DATA SET DDGETUSER  AT LEVEL 030 AS OF 10/26/10                      
*CATALP GETUSER                                                                 
         TITLE 'GETUSER- ROUTINE FOR HANDLING SPOT/PRINT USER FIELDS'           
*                                                                               
*  PARAMETER LIST                                                               
*                                                                               
*  PARAM 1  BYTE  0      C'S' = SPOT                                            
*                        C'B' = SPOT BILLING                                    
*                        C'P' = PRINT                                           
*           BYTES 1-3    A(CLIENT HEADER)                                       
*                                                                               
*  PARAM 2  BYTE  0      C'P'   PRODUCT RECORD                                  
*                        C'E'   ESTIMATE RECORD                                 
*           BYTES 1-3    A(RECORD)                                              
*                                                                               
*  PARAM-3  BYTE  0      X'00' = RETURN UDEF1 AND VALUE1                        
*                        IN FIXED PRINTING FORMAT                               
*                        UDEF1(20),SPACE, FOLLOWED BY VALUE1(32)                
*                        (LENGTH IS 53 BYTES)                                   
*                        C'ANY CHAR' = RETURN UDEF1 AND VALUE1                  
*                        LEFT JUSTIFIED WITH DESIGNATED CHARACTER               
*                        IN BETWEEN FOLLOWED BY A SPACE                         
*                    EX. UDEF1: VALUE1  (LENGTH IS 54)                          
*           BYTES 1-3    A(USER1 OUTPUT - 54 BYTES)                             
*                                                                               
****** NOTE - IF PARAMETER 3 IS MISSING DON'T RETURN USER1                      
*                                                                               
*  PARAM-4  BYTE  0      X'00' = RETURN UDEF2 AND VALUE2                        
*                        IN FIXED PRINTING FORMAT                               
*                        UDEF2(20),SPACE, FOLLOWED BY VALUE2(16)                
*                        (LENGTH IS 37 BYTES)                                   
*                        C'ANY CHAR' RETURNS UDEF2 AND VALUE2                   
*                        LEFT JUSTIFIED WITH DESIGNATED CHARACTER               
*                        IN BETWEEN FOLLOWED BY A SPACE THEN VALUE2             
*                    EX. UDEF2: VALUE2  (LENGTH IS 38)                          
*           BYTES 1-3    A(USER2 OUTPUT - 38 BYTES)                             
*                                                                               
****** NOTE - IF PARAMETER 4 IS MISSING DON'T RETURN USER2                      
*                                                                               
****************************************************                            
****   PARAMETERS ON RETURN                                                     
****************************************************                            
*                                                                               
*                                                                               
*** ON RETURN PARAM 1 IS SET TO X'FF' IF AN ERROR IS FOUND                      
*                                                                               
*** ON RETURN PARAM 2 IS SET TO FILTER VALUES                                   
**                                                                              
*           FIRST BYTE            PRINTPAK                    SPOTPAK           
*           USER1 FILTER1 X'40'   P52 - ESTIMATES             A2                
*                         X'20'   PEC - ESTIMATE CHG REPORT   A3                
*                         X'10'   PB1 - BILLING               B1                
*           SECOND BYTE                                                         
*           USER1 FILTER2                                                       
*                                                                               
*           THIRD BYTE            PRINTPAK                    SPOTPAK           
*           USER2 FILTER1 X'40'   P52 - ESTIMATES             A2                
*                         X'20'   PEC - ESTIMATE CHG REPORT   A3                
*                         X'10'   PB1 - BILLING               B1                
*           FOURTH BYTE                                                         
*           USER2 FILTER2                                                       
*                                                                               
***** NOTE - FILTER VALUE IS SET TO X'00' IF THE USER DATA                      
*****        DOES NOT EXIST ON THE PRODUCT/ESTIMATE                             
*                                                                               
***********************************************************                     
*                                                                               
*        CHANGE LOG                                                             
*                                                                               
*    BPLA 1/29/04  IF UDEF FIELD IS MISSING, DON'T TRY                          
*                  TO FLOAT IT IN FRONT OF FIELD DATA                           
*                  CHANGE MADE SINCE SOME RECORDS HAD                           
*                  USER DATA BUT THE FIELD DEFINITIONS                          
*                  HAD BEEN REMOVED.                                            
*                                                                               
         EJECT                                                                  
         PRINT NOGEN                                                            
GETUSER  CSECT                                                                  
         NMOD1 24,GETUSER                                                       
         USING WORKD,RC                                                         
         SPACE 3                                                                
         LM    R2,R5,0(R1)                                                      
*                                                                               
         MVI   SPACES,C' '       SET SPACES                                     
         MVC   SPACES+1(L'SPACES-1),SPACES                                      
*                                                                               
         LTR   R4,R4              SEE IF USER1 NEEDED                           
         BZ    GETU5                                                            
         MVI   0(R4),C' '        CLEAR USER1 OUTPUT                             
         MVC   1(52,R4),0(R4)                                                   
*                                                                               
GETU5    DS    0H                                                               
         LTR   R5,R5             SEE IF USER2 NEEDED                            
         BZ    GETU10                                                           
         MVI   0(R5),C' '        CLEAR USER2 OUTPUT                             
         MVC   1(36,R5),0(R5)                                                   
*                                                                               
GETU10   DS    0H                                                               
         XC    MYP1F1(8),MYP1F1   CLEAR FILTER VALUES                           
         XC    MYFLAG1(2),MYFLAG1  FLAGS FOR BILLING                            
*                                                                               
PGETU    DS    0H                                                               
         CLI   0(R1),C'S'          SEE IF SPOT                                  
         BE    SGETU                                                            
         CLI   0(R1),C'B'          SEE IF SPOT BILLING                          
         BE    SGETU                                                            
         CLI   0(R1),C'P'          SEE IF PRINT                                 
         BNE   GETUERR                                                          
         SPACE 3                                                                
*                                  PRINTPAK                                     
         USING PCLTRECD,R2                                                      
         CLI   PCLTKRCD,X'02'      BE SURE IT'S A CLIENT HEADER                 
         BNE   GETUERR                                                          
*                                                                               
         LA    R7,PCLTREC+33                                                    
         MVI   ELCODE,X'20'                                                     
         BAS   RE,NEXTEL                                                        
         BNE   GETUX                                                            
         USING PCLTUDEF,R7                                                      
         MVC   MYPUD1,PCLTPU1                                                   
         MVC   MYPUD2,PCLTPU2                                                   
         MVC   MYEUD1,PCLTEU1                                                   
         MVC   MYEUD2,PCLTEU2                                                   
*                                                                               
         OC    MYPUD1,SPACES                                                    
         OC    MYPUD2,SPACES                                                    
         OC    MYEUD1,SPACES                                                    
         OC    MYEUD2,SPACES                                                    
*                                                                               
         MVC   MYP1F1,PCLTP1F1     REPORT FILTER VALUE                          
         MVC   MYP1F2,PCLTP1F2     REPORT FILTER VALUE                          
         MVC   MYP2F1,PCLTP2F1     REPORT FILTER VALUE                          
         MVC   MYP2F2,PCLTP2F2     REPORT FILTER VALUE                          
*                                                                               
         MVC   MYE1F1,PCLTE1F1     REPORT FILTER VALUE                          
         MVC   MYE1F2,PCLTE1F2     REPORT FILTER VALUE                          
         MVC   MYE2F1,PCLTE2F1     REPORT FILTER VALUE                          
         MVC   MYE2F2,PCLTE2F2     REPORT FILTER VALUE                          
*                                                                               
         DROP  R2                                                               
         DROP  R7                                                               
*                                                                               
PPRD     CLI   4(R1),C'P'         SEE IF RECORD IS PRODUCT                      
         BNE   PEST                                                             
         XC    4(4,R1),4(R1)       CLEAR PARAMETER 2                            
         USING PPRDRECD,R3                                                      
         CLI   PPRDKRCD,X'06'      BE SURE IT'S A PRODUCT HEADER                
         BNE   GETUERR                                                          
*                                                                               
         LA    R7,PPRDREC+33                                                    
         MVI   ELCODE,X'08'                                                     
         BAS   RE,NEXTEL                                                        
         BNE   GETUX                                                            
         USING PPRDUDEF,R7                                                      
         MVC   MYUSER1,PUSER1                                                   
         MVC   MYUSER2,PUSER2                                                   
         OC    MYUSER1,SPACES                                                   
         OC    MYUSER2,SPACES                                                   
*                                                                               
         DROP  R3                                                               
         DROP  R7                                                               
*                                                                               
         LA    R8,MYPUD1                                                        
         LA    R9,MYP1F1                                                        
         B     PGETU20                                                          
*                                                                               
PEST     CLI   4(R1),C'E'         SEE IF RECORD IS ESTIMATE                     
         BNE   GETUERR                                                          
         XC    4(4,R1),4(R1)       CLEAR PARAMETER 2                            
         USING PESTRECD,R3                                                      
         CLI   PESTKRCD,X'07'      BE SURE IT'S AN ESTIMATE HEADER              
         BNE   GETUERR                                                          
*                                                                               
         LA    R7,PESTREC+33                                                    
         MVI   ELCODE,X'08'                                                     
         BAS   RE,NEXTEL                                                        
         BNE   GETUX                                                            
         USING PESTUDEF,R7                                                      
         MVC   MYUSER1,PEUSER1                                                  
         MVC   MYUSER2,PEUSER2                                                  
         OC    MYUSER1,SPACES                                                   
         OC    MYUSER2,SPACES                                                   
*                                                                               
         DROP  R3                                                               
         DROP  R7                                                               
*                                                                               
         LA    R8,MYEUD1                                                        
         LA    R9,MYE1F1                                                        
         B     PGETU20                                                          
*                                                                               
PGETU20  DS    0H                 R8 POINTS TO DEF1                             
*                                 R9 POINTS TO FILTER VALUES                    
         LTR   R4,R4             SEE IF USER1 NEEDED                            
         BZ    PGETU40                                                          
         CLC   MYUSER1,SPACES                                                   
         BNE   PGETU22                                                          
         XC    0(2,R9),0(R9)       SET OFF FILTER VALUES                        
         B     PGETU40             FOR USER 1                                   
*                                                                               
PGETU22  MVC   0(20,R4),0(R8)      MOVE DEF1                                    
         LA    RE,21(R4)                                                        
         CLI   8(R1),0         IS DELIMITER CHARACTER REQUESTED?                
         BE    PGETU28                                                          
         CLC   0(20,R4),SPACES   IS FIELD DEFINITION PRESENT?                   
         BNH   PGETU28          CAN'T FLOAT AFTER                               
*                                                                               
PGETU23  CLI   0(RE),C' '       SCAN BACK FOR FIRST NON-SPACE                   
         BNE   PGETU25                                                          
         BCT   RE,PGETU23                                                       
*                                                                               
PGETU25  LA    RE,1(RE)                                                         
         MVC   0(1,RE),8(R1)                                                    
         LA    RE,2(RE)            FOR CHARACTER AND SPACE                      
*                                                                               
PGETU28  MVC   0(32,RE),MYUSER1                                                 
*                                                                               
PGETU40  LTR   R5,R5              SEE IF USER2 NEEDED                           
         BZ    PGETUX                                                           
         CLC   MYUSER2,SPACES                                                   
         BNE   PGETU42                                                          
         XC    2(2,R9),2(R9)       SET OFF FILTER VALUES                        
         B     PGETUX              FOR USER 2                                   
*                                                                               
PGETU42  MVC   0(20,R5),20(R8)     MOVE DEF2                                    
         LA    RE,21(R5)                                                        
         CLI   12(R1),0         IS DELIMITER CHARACTER PRESENT?                 
         BE    PGETU48                                                          
         CLC   0(20,R5),SPACES   IS FIELD DEFINITION PRESENT?                   
         BNH   PGETU48          CAN'T FLOAT AFTER                               
*                                                                               
PGETU43  CLI   0(RE),C' '       SCAN BACK FOR FIRST NON-SPACE                   
         BNE   PGETU45                                                          
         BCT   RE,PGETU43                                                       
*                                                                               
PGETU45  LA    RE,1(RE)                                                         
         MVC   0(1,RE),12(R1)                                                   
         LA    RE,2(RE)              FOR CHARACTER AND SPACE                    
*                                                                               
PGETU48  MVC   0(16,RE),MYUSER2                                                 
*                                                                               
PGETUX   DS    0H                                                               
         MVC   4(4,R1),0(R9)       RETURN FILTER VALUES                         
         MVC   8(2,R1),MYFLAG1     FILTERS FOR BILLING                          
         B     GETUXIT                                                          
*                                                                               
SGETU    DS    0H                  SPOTPAK                                      
         USING SCLTRECD,R2                                                      
         MVC   MYPUD1,CPU1                                                      
         MVC   MYPUD2,CPU2                                                      
         MVC   MYEUD1,CEU1                                                      
         MVC   MYEUD2,CEU2                                                      
         OC    MYPUD1,SPACES                                                    
         OC    MYPUD2,SPACES                                                    
         OC    MYEUD1,SPACES                                                    
         OC    MYEUD2,SPACES                                                    
*                                                                               
         MVC   MYP1F1,CPU1FLG1     REPORT FILTER VALUE                          
         MVC   MYP1F2,CPU1FLG2     REPORT FILTER VALUE                          
         MVC   MYP2F1,CPU2FLG1     REPORT FILTER VALUE                          
         MVC   MYP2F2,CPU2FLG2     REPORT FILTER VALUE                          
*                                                                               
         MVC   MYE1F1,CEU1FLG1     REPORT FILTER VALUE                          
         MVC   MYE1F2,CEU1FLG2     REPORT FILTER VALUE                          
         MVC   MYE2F1,CEU2FLG1     REPORT FILTER VALUE                          
         MVC   MYE2F2,CEU2FLG2     REPORT FILTER VALUE                          
*                                                                               
         DROP  R2                                                               
*                                                                               
SPRD     CLI   4(R1),C'P'         SEE IF RECORD IS PRODUCT                      
         BNE   SEST                                                             
         XC    4(4,R1),4(R1)       CLEAR PARAMETER 2                            
         USING SPRDRECD,R3                                                      
         MVC   MYUSER1,SPUSER1                                                  
         MVC   MYUSER2,SPUSER2                                                  
         CLI   0(R1),C'B'         IF CALLED BY BILLING CHK                      
         BNE   SPRD50             IF ANYTHING SPECIAL                           
         XC    MYUSER1,MYUSER1                                                  
         XC    MYUSER2,MYUSER2                                                  
         CLC   =C'#R#',SPUSER1     APPLICABLE ONLY TO MAIN BILL                 
         BNE   *+18                                                             
         MVC   MYUSER1(L'SPUSER1-3),SPUSER1+3                                   
         MVI   MYFLAG1,C'R'                                                     
         B     SPRD20                                                           
*                                                                               
         CLC   =C'#F#',SPUSER1     APPLICABLE ONLY TO COMM BILL                 
         BNE   *+18                                                             
         MVC   MYUSER1(L'SPUSER1-3),SPUSER1+3                                   
         MVI   MYFLAG1,C'F'                                                     
         B     SPRD20                                                           
*                                                                               
         MVC   MYUSER1,SPUSER1                                                  
*                                                                               
SPRD20   CLC   =C'#R#',SPUSER2     APPLICABLE ONLY TO MAIN BILL                 
         BNE   *+18                                                             
         MVC   MYUSER2(L'SPUSER2-3),SPUSER2+3                                   
         MVI   MYFLAG2,C'R'                                                     
         B     SPRD50                                                           
*                                                                               
         CLC   =C'#F#',SPUSER2     APPLICABLE ONLY TO COMM BILL                 
         BNE   *+18                                                             
         MVC   MYUSER2(L'SPUSER2-3),SPUSER2+3                                   
         MVI   MYFLAG2,C'F'                                                     
         B     SPRD50                                                           
*                                                                               
         MVC   MYUSER2,SPUSER2                                                  
*                                                                               
SPRD50   OC    MYUSER1,SPACES                                                   
         OC    MYUSER2,SPACES                                                   
*                                                                               
         DROP  R3                                                               
*                                                                               
         LA    R8,MYPUD1                                                        
         LA    R9,MYP1F1                                                        
         B     PGETU20                                                          
*                                                                               
SEST     CLI   4(R1),C'E'         SEE IF RECORD IS ESTIMATE                     
         BNE   GETUERR                                                          
         XC    4(4,R1),4(R1)       CLEAR PARAMETER 2                            
         USING SESTRECD,R3                                                      
         MVC   MYUSER1,EUSER1                                                   
         MVC   MYUSER2,EUSER2                                                   
         CLI   0(R1),C'B'         IF CALLED BY BILLING CHK                      
         BNE   SEST50             IF ANYTHING SPECIAL                           
         XC    MYUSER1,MYUSER1                                                  
         XC    MYUSER2,MYUSER2                                                  
         CLC   =C'#R#',EUSER1      APPLICABLE ONLY TO MAIN BILL                 
         BNE   *+18                                                             
         MVC   MYUSER1(L'EUSER1-3),EUSER1+3                                     
         MVI   MYFLAG1,C'R'                                                     
         B     SEST20                                                           
*                                                                               
         CLC   =C'#F#',EUSER1      APPLICABLE ONLY TO COMM BILL                 
         BNE   *+18                                                             
         MVC   MYUSER1(L'EUSER1-3),EUSER1+3                                     
         MVI   MYFLAG1,C'F'                                                     
         B     SEST20                                                           
*                                                                               
         MVC   MYUSER1,EUSER1                                                   
*                                                                               
SEST20   CLC   =C'#R#',EUSER2     APPLICABLE ONLY TO MAIN BILL                  
         BNE   *+18                                                             
         MVC   MYUSER2(L'EUSER2-3),EUSER2+3                                     
         MVI   MYFLAG2,C'R'                                                     
         B     SEST50                                                           
*                                                                               
         CLC   =C'#F#',EUSER2     APPLICABLE ONLY TO COMM BILL                  
         BNE   *+18                                                             
         MVC   MYUSER2(L'EUSER2-3),EUSER2+3                                     
         MVI   MYFLAG2,C'F'                                                     
         B     SEST50                                                           
*                                                                               
         MVC   MYUSER2,EUSER2                                                   
*                                                                               
SEST50   OC    MYUSER1,SPACES                                                   
         OC    MYUSER2,SPACES                                                   
*                                                                               
         DROP  R3                                                               
*                                                                               
         LA    R8,MYEUD1                                                        
         LA    R9,MYE1F1                                                        
         B     SGETU20                                                          
*                                                                               
SGETU20  DS    0H                 R8 POINTS TO DEF1                             
*                                 R9 POINTS TO FILTER VALUES                    
         LTR   R4,R4             SEE IF USER1 NEEDED                            
         BZ    SGETU40                                                          
         CLC   MYUSER1,SPACES                                                   
         BNE   SGETU22                                                          
         XC    0(2,R9),0(R9)    SET OFF USER FILTER VALUES FOR USER1            
         B     SGETU40                                                          
*                                                                               
SGETU22  MVC   0(20,R4),0(R8)      MOVE DEF1                                    
         LA    RE,21(R4)                                                        
         CLI   8(R1),0         IS DELIMITER CHARACTER REQUESTED?                
         BE    SGETU28                                                          
         CLC   0(20,R4),SPACES   IS FIELD DEFINITION PRESENT?                   
         BNH   PGETU28          CAN'T FLOAT AFTER                               
*                                                                               
SGETU23  CLI   0(RE),C' '       SCAN BACK FOR FIRST NON-SPACE                   
         BNE   SGETU25                                                          
         BCT   RE,SGETU23                                                       
*                                                                               
SGETU25  LA    RE,1(RE)                                                         
         MVC   0(1,RE),8(R1)                                                    
         LA    RE,2(RE)           FOR CHARACTER AND SPACE                       
*                                                                               
SGETU28  MVC   0(32,RE),MYUSER1                                                 
*                                                                               
SGETU40  LTR   R5,R5              SEE IF USER2 NEEDED                           
         BZ    SGETUX                                                           
         CLC   MYUSER2,SPACES                                                   
         BNE   SGETU42                                                          
         XC    2(2,R9),2(R9)    SET OFF USER FILTER VALUES FOR USER2            
         B     SGETUX                                                           
*                                                                               
SGETU42  MVC   0(20,R5),20(R8)     MOVE DEF2                                    
         LA    RE,21(R5)                                                        
         CLI   12(R1),0          IS DELIMITER CHARACTER PRESENT?                
         BE    SGETU48                                                          
         CLC   0(20,R5),SPACES   IS FIELD DEFINITION PRESENT?                   
         BNH   PGETU48          NO - THEN CAN'T FLOAT AFTER                     
*                                                                               
SGETU43  CLI   0(RE),C' '       SCAN BACK FOR FIRST NON-SPACE                   
         BNE   SGETU45                                                          
         BCT   RE,SGETU43                                                       
*                                                                               
SGETU45  LA    RE,1(RE)                                                         
         MVC   0(1,RE),12(R1)                                                   
         LA    RE,2(RE)           FOR CHARACTER AND SPACE                       
*                                                                               
SGETU48  MVC   0(16,RE),MYUSER2                                                 
*                                                                               
SGETUX   DS    0H                                                               
         MVC   4(4,R1),0(R9)       RETURN FILTER VALUES                         
         MVC   8(2,R1),MYFLAG1     FILTERS FOR BILLING                          
         B     GETUXIT                                                          
*                                                                               
NEXTEL   ZIC   R0,1(R7)                                                         
         AR    R7,R0                                                            
         CLI   0(R7),0                                                          
         BE    NEXTEL2                                                          
         CLC   ELCODE,0(R7)                                                     
         BER   RE                                                               
         B     NEXTEL                                                           
*                                                                               
NEXTEL2  DS    0H                                                               
         LTR   R7,R7               SET CC NOT EQUAL                             
         BR    RE                                                               
*                                                                               
GETUERR  DS    0H                                                               
         MVI   0(R1),X'FF'      SET ERROR RETURN                                
GETUX    DS    0H              HERE IF NO USER DATA IN CLT PRD OR EST           
         XC    4(4,R1),4(R1)     CLEAR FILTER VALUES                            
GETUXIT  DS    0H                                                               
EXIT     XMOD1 1                                                                
         SPACE 3                                                                
*                                                                               
         LTORG                                                                  
         SPACE 3                                                                
WORKD    DSECT                                                                  
MYPUD1   DS    CL20                                                             
MYPUD2   DS    CL20                                                             
MYEUD1   DS    CL20                                                             
MYEUD2   DS    CL20                                                             
*                                                                               
*                      ** PRODUCT USER FILTERS **                               
MYP1F1   DS    CL1     USER1 - FILTER 1                                         
MYP1F2   DS    CL1             FILTER 2                                         
MYP2F1   DS    CL1     USER2 - FILTER 1                                         
MYP2F2   DS    CL1             FILTER 2                                         
*                                                                               
*                      ** ESTIMATE USER FILTERS **                              
MYE1F1   DS    CL1     USER1 - FILTER 1                                         
MYE1F2   DS    CL1             FILTER 2                                         
MYE2F1   DS    CL1     USER2 - FILTER 1                                         
MYE2F2   DS    CL1             FILTER 2                                         
*                                                                               
ELCODE   DS    CL1                                                              
SPACES   DS    CL32                                                             
*                                                                               
MYUSER1  DS    CL32                                                             
MYUSER2  DS    CL16                                                             
MYFLAG1  DS    CL1      TO HOLD REGULAR OR FEE UDEF                             
MYFLAG2  DS    CL1      TO HOLD REGULAR OR FEE UDEF                             
*                                                                               
         EJECT                                                                  
PCLTRECD DSECT                                                                  
       ++INCLUDE PCLTREC                                                        
PPRDRECD DSECT                                                                  
       ++INCLUDE PPRDREC                                                        
PESTRECD DSECT                                                                  
       ++INCLUDE PESTREC                                                        
         EJECT                                                                  
SCLTRECD DSECT                                                                  
       ++INCLUDE SPGENCLT                                                       
SPRDRECD DSECT                                                                  
*PREFIX=S                                                                       
       ++INCLUDE SPGENPRD                                                       
*PREFIX=                                                                        
*                                                                               
SESTRECD DSECT                                                                  
       ++INCLUDE SPGENEST                                                       
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'030DDGETUSER 10/26/10'                                      
         END                                                                    
