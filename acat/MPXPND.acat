*          DATA SET MPXPND     AT LEVEL 006 AS OF 05/01/02                      
*CATALP MPXPND                                                                  
         TITLE 'MPXPND  - VALIDATE AND EXPAND PAIRINGS'                         
         PRINT NOGEN                                                            
MPXPND   CSECT                                                                  
         SPACE 2                                                                
*                                                                               
*        INPUT IS FIVE  FULLWORDS :-                                            
*        PARAM 1 - PROGRAM NUMBER                                               
*        PARAM 2 - ADDRESS OF PROCESSING TABLE                                  
*        PARAM 3 - ADDRESS OF MPDITAB                                           
*        PARAM 4 - ADDRESS OF ERROR FLAG                                        
*        PARAM 5 - NUMBER OF VEHICLES IN SCHEDULE (VXV ONLY), OR                
*                - ADDRESS OF ISSUE COUNT TABLE(REACH ONLY)                     
*        PARAM 6 - ADDRESS OF END OF PROCESSING TABLE AREA                      
*        OUTPUT IS RETURN CODE OR EXPANDED PROCESSING TABLE                     
*                                                                               
         SPACE 3                                                                
         NMOD1 0,MPXPND                                                         
         SH    RC,=H'04'                                                        
         L     RC,0(RC)                                                         
         USING GEND,RC                                                          
         EJECT                                                                  
         LR    R8,R1               SAVE PARMS ADDRESS                           
         L     R6,0(R1)            PROGRAM NUMBER                               
         L     R4,4(R1)            A(PROCESSING TABLE)                          
*                                                                               
MPX01    DS    0H                                                               
         BAS   RE,PAIRVAL          VALIDATE BASE/QUALIFIER/REP/SCALE            
*                                                                               
         BAS   RE,EXPAND           EXPAND THE PROCESSING TABLE                  
*                                                                               
         LA    R4,PCLEN(R4)        BUMP TO NEXT B/Q PAIRING                     
         CLI   0(R4),0             CHECK FOR END OF PROC TABLE                  
         BNE   MPX01               BACK FOR NEXT                                
*                                  NOW CHECK FOR A VXV PROC TABLE               
         LA    R0,MPVXV                                                         
         LH    R1,2(R8)            PROG NUMBER FROM CALLER                      
         CR    R0,R1               VXV REPORT ????                              
         BNE   MPX02               NO - TRY REACH                               
         CLI   19(R8),0            PROCESSING PAIRWISE ENTRIES ??               
         BE    MPX03               NO -EXIT                                     
*                                                                               
         BAS   RE,VXVPAIRS         EXTEND PROCTAB FOR PAIRWISE ITEMS            
         B     MPX03                                                            
MPX02    DS    0H                                                               
         LA    R0,MPREACH                                                       
         CR    R0,R1               RCH REPORT ????                              
         BNE   MPX03               NO - EXIT                                    
         ICM   R3,15,16(R8)        PROCESSING PAIRWISE ENTRIES ??               
         BZ    MPX03               NO -EXIT                                     
         BAS   RE,RCHPAIRS                                                      
*                                                                               
MPX03    DS    0H                                                               
         B     XIT                                                              
         EJECT                                                                  
*                                                                               
*                                                                               
***********************************************************************         
*                                                                     *         
*        PAIRING ROUTINE CHECKS- 1 BASE/QUALIFER PAIR EXISTS          *         
*                                2 BASE/QUALIFER VALID FOR REP        *         
*                                3 USER OVERRIDE SCALINGS EXIST       *         
*        RETURNS IF VALID WITH R3 - A(CONTAB) ENTRY                   *         
*                              R5 - A(OPBLOCK) ENTRY                  *         
*                                                                     *         
***********************************************************************         
         SPACE 2                                                                
PAIRVAL  DS    0H                                                               
         L     R3,8(R8)            ADDRESS OF MPDITAB                           
         A     R3,8(R3)            ADDRESS OF CONTAB                            
         USING MPDIPD,R3                                                        
         USING PCMDD,R4                                                         
PV02     DS    0H                                                               
         CLI   MPPLEN,X'FF'        CHECK FOR END OF TABLE                       
         LA    R7,INVBQREP         INVALID BASE/QUALIFIER FOR REPORT            
         BE    TRAPERR                                                          
         CLC   PCBASE(2),MPPBCOD   VALIDATE BASE/QUALIFER                       
         BE    PV04                FOUND                                        
PV03     DS    0H                                                               
         ZIC   R1,MPPLEN           LENGTH OF MPDITAB ENTRY                      
         AR    R3,R1               BUMP TO NEXT MPDITAB                         
         B     PV02                                                             
PV04     DS    0H                                                               
         SR    R1,R1                                                            
         LH    R1,MPPVALF          PROGRAM VALIDITY FLAGS                       
         NR    R1,R6               IS PROGRAM SET ON ???                        
         BZ    PV03                BACK FOR NEXT IF NOT SET                     
         TM    MPPCTLF,MPSUFFR     SUFFIX REQUIRED ???                          
         BO    PV04A               YES - VALIDATE SUFFIX VALUE                  
         TM    MPPCTLF,MPSUFFO     SUFFIX OPTIONAL ???                          
         BO    PV04B               YES -NO VALIDATION BUT LEAVE SET UP          
*                                       (CALC METHOD NOT AFFECTED               
*                                        BY SUFFIX)                             
         OC    PCSFX,PCSFX         ANY SUFFIX ????                              
         BNZ   PV03                YES-CAN'T USE THIS ENTRY - PCMD HAS          
*                                  SUFFIX BUT DITAB ENTRY DOESN'T               
*                                  SUPPORT ONE.                                 
         B     PV04C               NO-USE THIS MPDITAB ENTRY                    
*                                                                               
PV04A    DS    0H                  SUFFIX IS REQUIRED TO DEFINE ENTRY           
         CLC   PCSFX+1(1),MPPSUFF  CORRECT SUFFIX ???                           
         BNE   PV03                                                             
         B     PV04C                                                            
*                                                                               
PV04B    DS    0H                  SUFFIX IS OPTIONAL                           
         OC    PCSFX,PCSFX         HAS USER SET SUFFIX ???                      
         BZ    PV03 >>>>>>>>>>>>>  THIS CODE CHANGES THE MEANING OF             
* OPTIONAL SUFFIX TO BE 'MUST BE DEFINED BUT DOESN'T AFFECT FORUMLA'            
* FOLLOWING TWO INSTRUCTIONS GAVE A DEFAULT. HENCE RCH ON IT'S OWN              
* IS NO LONGER VALID - UNLESS MPDIVAL IS PASSED A FORCED SUFFIX                 
* THIS SHOULD SOLVE PROBLEM OF REACH AND VXV FIXED/PAIRED DATA                  
******   BNZ   PV04C               YES - LEAVE USER VALUE                       
******   MVC   PCSFX+1(1),MPSUFF   ELSE INSERT DEFAULT VALUE                    
*                                                                               
PV04C    DS    0H                                                               
         ICM   R5,15,MPPOBAD       OFFSET ADDR OF DEFAULT O/P BLOCK             
         DROP  R3                                                               
         A     R5,8(R8)            + TABLE BASE ADDR                            
         USING MPDIOD,R5                                                        
         CLI   PCDSPSCF,X'FF'      USE DEFAULT SCALING ?                        
         BE    PVXIT               YES - END                                    
PV05     DS    0H                                                               
         CLC   PCDSPSCF(2),MPOSCF  CHECK USER DEFINED SCALING OK                
         BE    PVXIT               YES - END                                    
         ZIC   R1,MPOLEN           LENGTH OF O/P BLOCK ENTRY                    
         AR    R5,R1               BUMP TO NEXT O/P BLOCK                       
         CLI   MPOLEN,X'FF'        EOT ?                                        
         DROP  R5                                                               
         DROP  R4                                                               
         BNE   PV05                TRY NEXT ENTRY                               
         LA    R7,INVSCBQ          INVALID SCALING FOR BASE/QUAL                
         B     TRAPERR                                                          
PVXIT    DS    0H                                                               
         BR    RE                                                               
         EJECT                                                                  
***********************************************************************         
*                                                                     *         
*        EXPAND SUBROUTINE                                            *         
*                                                                     *         
*        INPUT REGS :-                                                *         
*              R3  -  CONTAB ENTRY                                    *         
*              R4  -  PROC TABLE                                      *         
*              R5  -  OUTPUT BLOCK ENTRY                              *         
*                                                                     *         
***********************************************************************         
         SPACE 2                                                                
EXPAND   DS    0H                                                               
         USING PCMDD,R4                                                         
         USING MPDIOD,R5                                                        
         USING MPDIPD,R3                                                        
*                                                                               
*        MOVE DATA FROM FIXED TABLES TO PROCESSING COMMAND TABLE                
*                                                                               
*              PCBASE              ALREADY SET UP                               
*              PCQUAL              ALREADY SET UP                               
         MVC   PCWGT,MPPWGT                                                     
*              PCSFX               ALREADY SET UP                               
         MVC   PCTYP,MPPVTYP       INSERT VECTOR TYPE                           
         MVC   PCDATFMT,MPPINTD                                                 
         MVC   PCDATLEN,MPPDLEN                                                 
         MVC   PCDATSCF,MPPDSCF                                                 
         MVC   PCDSPSCF,MPOSCF                                                  
         MVC   PCDSPDEC,MPODEC                                                  
         MVC   PCDWDTH,MPODLEN                                                  
         MVC   PCDEDT,MPOEDIT                                                   
         MVC   PCTOTFLG,MPPTOTF                                                 
         MVC   PCHWDTH,MPOTWID                                                  
         MVC   PCHPLNS,MPOTDEP                                                  
         L     R1,8(R8)            GET MPDITAB ADDRESS                          
         ICM   R0,15,MPOTXAD       GET TEXT OFFSET                              
         AR    R1,R0               GENERATE TEXT ADDRESS                        
         STCM  R1,15,PCHATXT       SAVE ADDR                                    
         SPACE                                                                  
         CLI   MPOTEXT,C'S'        'STUB' TYPE OUTPUT BLOCK ?                   
         BNE   EX010               NO - BRANCH PAST                             
         MVC   PCSWDTH,MPOTWID     YES - GET WIDTH                              
         L     R1,8(R8)            GET MPDITAB ADDRESS                          
         ICM   R0,15,MPOTXAD       GET TEXT OFFSET                              
         AR    R1,R0               GENERATE TEXT ADDRESS                        
         STCM  R1,15,PCSATXT       SAVE ADDR                                    
         SPACE                                                                  
         CH    R6,=AL2(MPREACH)                                                 
         BE    *+16                                                             
         MVI   PCHWDTH,10          SET HEADER WIDTH AND LINES ..                
         MVI   PCHPLNS,2           .. FOR VXV REPORT                            
         B     *+12                                                             
         MVI   PCHWDTH,4           SET HEADER WIDTH AND LINES ..                
         MVI   PCHPLNS,1           .. FOR REACH REPORT                          
         XC    PCHATXT,PCHATXT                                                  
EX010    DS    0H                                                               
         CLI   MPPHOW,MPOPSUBR     IS THE 'FORMULA' A(SUBROUTINE) ???           
         BNE   EX040               NO - SET UP FORMULA                          
         MVC   PCFHOW,MPPHOW       ELSE - DEFINE AS A SUBR                      
         MVC   PCFASUBR,MPPASUBR   DEFINE ITS ADDRESS (IF GENERAL)              
*                                  OR SUBR NUMBER (IF LOCAL)                    
         B     EX050                                                            
EX040    DS    0H                                                               
         MVC   PCFB1,MPPFOP1B                                                   
         MVC   PCFQ1,MPPFOP1Q                                                   
         MVC   PCFOP1,MPPFO1                                                    
*              PCFINDX1            WILL BE SET IN DICHECK                       
         MVC   PCFB2,MPPFOP2B                                                   
         MVC   PCFQ2,MPPFOP2Q                                                   
         MVC   PCFOP2,MPPFO2                                                    
*              PCFINDX2            WILL BE SET IN DICHECK                       
         MVC   PCFB3,MPPFOP3B                                                   
         MVC   PCFQ3,MPPFOP3Q                                                   
*              PCFINDX3            WILL BE SET IN DICHECK                       
EX050    DS    0H                                                               
         BR    RE                                                               
         EJECT                                                                  
***********************************************************************         
*                                                                     *         
*        VXVPAIRS                                                     *         
*        ROUTINE TO EXTEND THE PROCESSING TABLE FOR A VXV REPORT      *         
*        EACH SUFFIXED ITEM MUST BE REPEATED FOR EACH VEHICLE IN THE  *         
*        SCHEDULE.                                                    *         
*                                                                     *         
***********************************************************************         
*                                                                               
VXVPAIRS DS    0H                                                               
         LR    R5,R4               SAVE CURRENT END OF PROC TABLE               
         L     R2,4(R8)            DEFINE START OF PAIRWISE FIELDS              
         LA    R1,PCLEN            DEFINE EXECUTABLE MOVE LENGTH                
         BCTR  R1,R0                                                            
         L     R6,16(R8)           DEFINE NUMBER OF VEHICLES                    
         CH    R6,=H'2'            MAKE SURE AT LEAST 2 VEHICLES!!!             
         BL    VPXIT               SHOULDN'T BE HERE ANYWAY                     
         BCTR  R6,0                NUMBER OF PAIRS TO ADD                       
         LA    RF,2                INIT SUFFIX NUMBER                           
*                                                                               
VP03     DS    0H                                                               
         LR    R3,R2               POINT TO START OF SUFFIX=1                   
VP04     DS    0H                                                               
         CR    R3,R5               DONE ALL SUFFIX=1 ENTRIES ????               
         BNL   VP05                YES - SET FOR NEXT                           
         EX    R1,*+8              MOVE THIS ENTRY TO END OF TABLE              
         B     *+10                                                             
         MVC   0(0,R4),0(R3)                                                    
*                                                                               
         CLC   PCSFX,=H'1'         MAKE SURE SUFFIX IS STILL REQ VALUE          
         BE    *+6                                                              
         DC    H'0'                ELSE DIE                                     
*                                                                               
         STCM  RF,3,PCSFX          STICK IN NEW SUFFIX                          
         LA    R3,PCLEN(R3)        POINT TO NEXT SUFFIX=1 ENTRY                 
         LA    R4,PCLEN(R4)        POINT TO NEW END OF TABLE                    
         LA    R0,PCLEN(R4)        POINT TO END OF NEW ENTRY                    
         C     R0,20(R8)           CHECK STILL IN TABLE AREA                    
         BNH   VP04                BACK FOR NEXT ENTRY                          
         LA    R7,CANTDO           PROC TAB TOO SMALL                           
         CLI   OFFLINE,C'Y'                                                     
         BNE   TRAPERR             ONLINE - INFORM USER                         
         DC    H'0'                OFFLINE - DIE - PROC TAB TOO SMALL           
VP05     DS    0H                                                               
         LA    RF,1(RF)            SET FOR NEXT SUFFIX                          
         BCT   R6,VP03             BACK FOR NEXT SUFFIX GENERATION              
*                                                                               
         MVI   0(R4),0             FLAG NEW END OF TABLE                        
VPXIT    DS    0H                                                               
         BR    RE                                                               
         EJECT                                                                  
***********************************************************************         
*                                                                     *         
*        RCHPAIRS                                                     *         
*        ROUTINE TO EXTEND THE PROCESSING TABLE FOR AN RCH REPORT     *         
*        EACH SUFFIXED ITEM MUST BE REPEATED FOR EACH ISSUE COUNT     *         
*        IN THE TABLE.                                                *         
*                                                                     *         
***********************************************************************         
*                                                                               
RCHPAIRS DS    0H                                                               
         LR    R5,R4               SAVE CURRENT END OF PROC TABLE               
         LA    R1,PCLEN-1          DEFINE EXECUTABLE MOVE LENGTH                
         L     R6,16(R8)           ADDRESS ISSUE COUNT TABLE                    
*                                                                               
RP03     DS    0H                                                               
         LA    R6,2(R6)            NEXT ISSUE COUNT ENTRY                       
         CLC   0(2,R6),=H'0'       END OF TABLE ?                               
         BE    RP05                YES - EXIT                                   
         L     R3,4(R8)            POINT TO START OF SUFFIX=1                   
RP04     DS    0H                                                               
         CR    R3,R5               DONE ALL ISSUE=1 ENTRIES ????                
         BNL   RP03                YES - SET FOR NEXT                           
         EX    R1,*+8              MOVE THIS ENTRY TO END OF TABLE              
         B     *+10                                                             
         MVC   0(0,R4),0(R3)                                                    
*                                                                               
         MVC   PCSFX,0(R6)         STICK IN NEW SUFFIX                          
         LA    R3,PCLEN(R3)        POINT TO NEXT SUFFIX=1 ENTRY                 
         LA    R4,PCLEN(R4)        POINT TO NEW END OF TABLE                    
         LA    RF,PCLEN(R4)        POINT TO END OF NEW ENTRY                    
         C     RF,20(R8)           CHECK STILL IN TABLE AREA                    
         BNH   RP04                BACK FOR NEXT ENTRY                          
         LA    R7,CANTDO           PROC TAB TOO SMALL                           
         CLI   OFFLINE,C'Y'                                                     
         BNE   TRAPERR             ONLINE - INFORM USER                         
         DC    H'0'                OFFLINE - DIE - PROC TAB TOO SMALL           
RP05     DS    0H                                                               
*                                                                               
         MVI   0(R4),0             FLAG NEW END OF TABLE                        
RPXIT    DS    0H                                                               
         BR    RE                                                               
         EJECT                                                                  
*                                                                               
***********************************************************************         
XIT      DS    0H                                                               
         ST    R4,4(R8)                                                         
         XMOD1 1                                                                
         SPACE 2                                                                
TRAPERR  DS    0H                                                               
         L     R1,12(,R8)                                                       
         STC   R7,0(,R1)           SET RETURN CODE                              
         B     XIT                                                              
         EJECT                                                                  
         LTORG                                                                  
         SPACE 3                                                                
*THE FOLLOWING DSECTS ARE NOT PRINTED                                           
*DDSPLWORKD,MPDITABD,MPPCMDD,MPEQUATES                                          
         PRINT OFF                                                              
       ++INCLUDE DDSPLWORKD                                                     
       ++INCLUDE MPDITABD                                                       
       ++INCLUDE MPPCMDD                                                        
       ++INCLUDE MPEQUATES                                                      
         PRINT ON                                                               
         SPACE 2                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'006MPXPND    05/01/02'                                      
         END                                                                    
