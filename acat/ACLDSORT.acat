*          DATA SET ACLDSORT   AT LEVEL 002 AS OF 06/05/90                      
*CATALP ACLDSORT                                                                
LDSORT   TITLE '- DIRECTORY SORT ROUTINES'                                      
**********************************************************************          
* PARAMS VIA R1                                                      *          
* AL4   A(ACTION) - C'PUT' OR C'GET'                                 *          
* AL4   A(RECORD) - C'PUT'=INPUT, C'GET'=OUTPUT                      *          
**********************************************************************          
         SPACE 1                                                                
LDSORT   CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 WRKX-WRKD,ACLDDCNT                                               
         USING WRKD,RC                                                          
         LR    RA,R1               SAVE CALLER'S PLIST ADDRESS                  
*                                                                               
         L     RF,0(R1)                                                         
         CLC   0(1,RF),SORTPUT     TEST PUT RECORD TO SORT                      
         BE    LDPUT                                                            
         CLC   0(1,RF),SORTGET     TEST GET RECORD FROM SORT                    
         BE    LDGET                                                            
         DC    H'0'                                                             
*                                                                               
LDX      XIT1  ,                                                                
         EJECT                                                                  
**********************************************************************          
* PUT A RECORD TO SORT - SPECIAL POINTERS ARE PUT WITH INVERSE KEYS  *          
**********************************************************************          
         SPACE 1                                                                
LDPUT    L     R2,4(R1)                                                         
         USING CHDRECD,R2                                                       
         MVC   DIRIO,0(R2)                                                      
         LA    R2,DIRIO                                                         
         CLC   CHDKAPTR,EFFS       TEST FOR A SPECIAL POINTER                   
         BNE   LDPUT2                                                           
         CLI   CHDKCPY,X'40'       TEST CONTRA HEADER RECORD                    
         BNH   LDPUT2                                                           
         CLC   CHDKCULC,SPACES                                                  
         BE    LDPUT2                                                           
         OC    CHDKNULL,CHDKNULL                                                
         BNZ   LDPUT2                                                           
         XC    CHDKCULA,CHDKCULC   INVERT KEYS FOR INITIAL SORT                 
         XC    CHDKCULC,CHDKCULA                                                
         XC    CHDKCULA,CHDKCULC                                                
         XC    CHDKDA,CHDKDA       CLEAR POINTER DISK ADDRESS                   
*                                                                               
LDPUT2   GOTO1 SORTER,PARM,SORTPUT,CHDRECD                                      
         MVI   MODE,GETFRST                                                     
         B     LDX                                                              
         EJECT                                                                  
**********************************************************************          
* GET A RECORD FROM SORT - ON THE FIRST TIME THROUGH ALL RECORDS PUT *          
* TO SORT ARE READ - CONTRA HEADER KEYS ARE INVERTED AND THE DISK    *          
* ADDRESS SET. THE RECORDS ARE THEN SORTED INTO LOAD SEQUENCE AND    *          
* RETURNED ONE AT A TIME TO THE GENERALISED LOAD PROGRAM.            *          
**********************************************************************          
         SPACE 1                                                                
LDGET    CLI   MODE,GETFRST        TEST FIRST GET                               
         BNE   LDGET9                                                           
         XC    ACTIO,ACTIO         CLEAR ACCOUNT SAVE AREA                      
         OPEN  (ACLDWORK,(OUTPUT))                                              
*                                                                               
LDGET1   GOTO1 SORTER,PARM,SORTGET                                              
         ICM   R2,15,4(R1)         R2=A(RECORD)                                 
         BNZ   *+12                                                             
         LA    R2,EFFS             ON EOF POINT TO X'FF'S                       
         B     LDGET2                                                           
*                                                                               
         USING ACTRECD,R2          PROCESS ACCOUNT RECORDS                      
         CLI   ACTKCPY,X'40'                                                    
         BNH   LDGET5                                                           
         CLC   ACTKACT,SPACES      TEST COMPANY/UNIT/LEDGER                     
         BE    LDGET5                                                           
         CLC   ACTKEY+ACTKEND(L'ACTKEY-ACTKEND),SPACES                          
         BNE   LDGET4                                                           
*                                                                               
LDGET2   OC    ACTIO,ACTIO         TEST ACCOUNT SAVED                           
         BZ    LDGET3                                                           
         PUT   ACLDWORK,ACTIO      YES - PUT LAST ACCOUNT TO WORK FILE          
*                                                                               
LDGET3   CLC   EFFS,ACTRECD        TEST ANY MORE SORT RECORDS                   
         BE    LDGET7                                                           
         MVC   ACTIO,ACTRECD       YES - SAVE THIS ACCOUNT HEADER               
         B     LDGET6                                                           
*                                                                               
         USING CHDRECD,R2                                                       
LDGET4   CLC   CHDKAPTR,EFFS       TEST FOR A SPECIAL POINTER                   
         BNZ   LDGET5                                                           
         OC    CHDKNULL,CHDKNULL                                                
         BNZ   LDGET5                                                           
         XC    CHDKCULC,CHDKCULA   INVERT KEYS TO LOAD SEQUENCE                 
         XC    CHDKCULA,CHDKCULC                                                
         XC    CHDKCULC,CHDKCULA                                                
         CLC   CHDKCULC,ACTIOKEY   TEST ACCOUNT MATCHES LAST ACCOUNT            
         BNE   LDGET5                                                           
         MVC   CHDKDA,ACTIODA      YES - SET DISK ADDRESS IN POINTER            
         OI    ACTIOSTA,ACTSCHDR   AND SET ACCOUNT REFERENCE BIT ON             
*                                                                               
LDGET5   PUT   ACLDWORK,(R2)       PUT RECORD TO WORK FILE                      
*                                                                               
LDGET6   MVI   MODE,GETSORT        GET NEXT SORT RECORD                         
         B     LDGET1                                                           
*                                                                               
LDGET7   CLOSE (ACLDWORK)          CLOSE WORK FILE                              
         CLI   MODE,GETSORT                                                     
         BE    *+6                                                              
         DC    H'0'                                                             
         OPEN  (ACLDWORK,(INPUT))  OPEN WORK FILE & INITIALISE SORT             
         GOTO1 SORTER,PARM,SORTSRT,SORTREC,0,0                                  
         GOTO1 ,(R1),SORTPUT,DIRIO                                              
*                                                                               
LDGET8   GET   ACLDWORK,DIRIO      READ WORK FILE AND RESORT RECORDS            
         GOTO1 SORTER,PARM                                                      
         B     LDGET8                                                           
*                                                                               
LDGET9   CLI   MODE,GETSORT        GET FIRST/NEXT SORT RECORD                   
         BNE   LDX                                                              
         GOTO1 SORTER,PARM,SORTGET                                              
         ICM   R1,15,4(R1)                                                      
         BNZ   *+8                                                              
         MVI   MODE,INITIAL        NO - RESET MODE TO INIT                      
         STCM  R1,15,4(RA)         SET RECORD ADDRESS IN CALLER'S PLIST         
         B     LDX                                                              
         EJECT                                                                  
         LTORG                                                                  
         SPACE 1                                                                
SORTER   DC    V(SORTER)                                                        
MODE     DC    AL1(INITIAL)                                                     
INITIAL  EQU   0                                                                
GETFRST  EQU   1                                                                
GETSORT  EQU   2                                                                
SORTPUT  DC    C'PUT'                                                           
SORTGET  DC    C'GET'                                                           
SORTSRT  DC    C'SORT FIELDS=(1,42,A),FORMAT=BI,WORK=1 '                        
SORTREC  DC    C'RECORD TYPE=F,LENGTH=54 '                                      
EFFS     DC    X'FFFFFFFFFFFFFFFF'                                              
SPACES   DC    CL(L'ACCKEY)' '                                                  
ACTIO    DS    0XL(ACCKLEN)                                                     
ACTIOKEY DC    XL(L'ACCKEY)'00'                                                 
ACTIOSTA DC    XL(L'ACCKSTA)'00'                                                
ACTIODA  DC    XL(L'ACCKDA)'00'                                                 
DIRIO    DC    XL(ACCKLEN)'00'                                                  
         SPACE 1                                                                
ACLDWORK DCB   DDNAME=ACLDWORK,DSORG=PS,MACRF=(PM,GM),EODAD=LDGET9,    *        
               RECFM=FB,LRECL=54,BLKSIZE=6102                                   
         SPACE 1                                                                
WRKD     DSECT                                                                  
PARM     DS    6F                                                               
WRKX     EQU   *                                                                
         SPACE 1                                                                
* ACLDEQUS                                                                      
         PRINT OFF                                                              
       ++INCLUDE ACLDEQUS                                                       
         PRINT ON                                                               
         SPACE 1                                                                
* ACGENFILE                                                                     
         PRINT OFF                                                              
       ++INCLUDE ACGENFILE                                                      
         PRINT ON                                                               
         SPACE 1                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'002ACLDSORT  06/05/90'                                      
         END                                                                    
