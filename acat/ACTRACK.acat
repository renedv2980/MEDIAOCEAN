*          DATA SET ACTRACK    AT LEVEL 006 AS OF 08/25/10                      
*CATALP ACTRACK                                                                 
*                                                                               
********************************************************************            
* ACTRACK - CREATE TRACKING RECORDS FOR POSTINGS & MARKINGS        *            
*                                                                  *            
* THIS MODULE INCLUDED IN: ACREP2102                               *            
*                          ACREP2702                               *            
*                          ACREPNB02                               *            
*                                                                  *            
* AT ENTRY - P1 BYTE 0   = STATUS                                  *            
*                          X'40' = TEST RUN                        *            
*                          X'20' = SOON                            *            
*               BYTE 1-3 = A(ACWORKD)                              *            
*          - P2 BYTE 1-3 = A(POSTINGS)                             *            
*          - P3 BYTE 1-3 = A(MARKINGS)                             *            
*          - P4 BYTE 1-3 = A(RECORDS)                              *            
*          - P5 BYTE 1-3 = A(WKID)                                 *            
* POSTINGS, MARKINGS, AND RECORDS MUST BE DEFINED AS PL8           *            
********************************************************************            
*                                                                               
         TITLE 'ACTRACK - MODULE TO CREATE TRACKING RECORDS'                    
ACTRACK  CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 WORKX-WORKD,**TRAK**,CLEAR=YES                                   
         USING WORKD,RC                                                         
         USING ACWORKD,RA                                                       
         ST    R1,APARM                                                         
         MVC   PARMS(PARML),0(R1)                                               
***********************************************************************         
*        GET DATA AND BUILD RECORD HERE                               *         
***********************************************************************         
         L     RA,AACWORKD          A(ACWORKD)                                  
                                                                                
         USING COMFACSD,RE                                                      
         L     RE,ADCOMFAC          A(COMFACS)                                  
         MVC   VSMFOUT,CSMFOUT                                                  
         DROP  RE                                                               
                                                                                
         USING TRAKD,TRECORD                                                    
         XC    TRECORD,TRECORD               CLEAR TRACK RECORD                 
         MVC   TRAKLN,=YL2(TRAKLNQ)          INITIALIZE KEY                     
         MVI   TRAKTYP,TRAKTYPP                                                 
*                                                                               
         USING MASTD,R2                                                         
         L     R2,ADMASTC                                                       
         MVC   TSENUM,MCIDSENO               SE NUMBER                          
         MVC   TRAKJIB,MCJOB                 JOB NAME                           
         DROP  R2                                                               
*                                                                               
         GOTO1 DATCON,PARM,(5,0),(10,TRAKDAT)   TODAY'S DATE                    
*                                                                               
         TIME  DEC                           TIME                               
         LR    R1,R0                                                            
         SRL   R1,8                                                             
         SLL   R1,4                                                             
         AHI   R1,12                                                            
*                                                                               
         ST    R1,TDUB+4                                                        
         MVC   EWORK(L'ETIME),ETIME                                             
         ED    EWORK(L'ETIME),TDUB+4                                            
         MVC   TRAKTIM,EWORK+2                                                  
*                                                                               
         MVC   TRAKSTAT,STATUS               STATUS                             
*                                                                               
         L     R2,APOST                                                         
         ZAP   TRAKPOS,0(8,R2)               TOTAL POSTED                       
*                                                                               
         L     R2,AMARK                                                         
         ZAP   TRAKMRK,0(8,R2)               TOTAL MARKED                       
*                                                                               
         L     R2,ARECS                                                         
         ZAP   TRAKREC,0(8,R2)               TOTAL RECORDS                      
*                                                                               
         USING WKRECD,R2                                                        
         L     R2,AWKID                                                         
         MVC   TRAKPRG,WKSYSPRG              PROGRAM NAME                       
         MVC   TRAKSUB,WKSUBPRG              SUB PROGRAM                        
         CLI   TRAKSUB,C' '                                                     
         BH    *+8                                                              
         MVI   TRAKSUB,C'*'                  (* IF NO SUB PROGRAM)              
         UNPK  TRAKDAY(3),WKDAY(2)           DAY                                
         MVC   TRAKCLS,WKCLASS               CLASS                              
*                                                                               
         SR    RF,RF                                                            
         ICM   RF,3,WKFILNO                                                     
         CVD   RF,TDUB                                                          
         OI    TDUB+7,X'0F'                                                     
         UNPK  TRAKSEQ,TDUB+4(4)             SEQUENCE NUMBER                    
*                                                                               
         USING CTIREC,R3                                                        
         LA    R3,CXREC                      READ CONTROL FILE                  
         XC    CTIKEY,CTIKEY                                                    
         MVI   CTIKTYP,CTIKTYPQ                                                 
         MVC   CTIKNUM,0(R2)                 PASS IT ORIGIN NUMBER              
         GOTO1 DATAMGR,PARM,DMREAD,CTFILE,(R3),(R3)                             
         CLI   8(R1),0                       ANY ERRORS?                        
         BNE   ERROR                         YES                                
*                                                                               
         LA    R3,CTIDATA                    NO, LOOK FOR ELEMENT               
         SR    RE,RE                                                            
*                                                                               
GET02    CLI   0(R3),0                       END OF RECORD?                     
         BE    ERROR                                                            
         CLI   0(R3),X'02'                                                      
         BE    *+14                                                             
         IC    RE,1(R3)                                                         
         AR    R3,RE                                                            
         B     GET02                                                            
*                                                                               
         MVC   TRAKUID,2(R3)                 USER ID                            
*                                                                               
         USING CTWREC,R3                                                        
         LA    R3,CXREC                      READ CONTROL FILE AGAIN            
         XC    CTWKEY,CTWKEY                                                    
         MVI   CTWKTYP,CTWKTYPQ                                                 
         MVI   CTWKREC,C'S'                  INDICATE SYSTEM LIST               
         MVI   CTWKSYSN,CTWKACC                                                 
         GOTO1 DATAMGR,PARM,DMREAD,CTFILE,(R3),(R3)                             
         CLI   8(R1),0                       ANY ERRORS?                        
         BNE   ERROR                         YES                                
*                                                                               
         LA    R3,CTWDATA                    NO, LOOK FOR ELEMENT               
         SR    RE,RE                                                            
*                                                                               
         USING SYSELD,R3                                                        
GETA4    CLI   0(R3),0                       END OF RECORD?                     
         BE    ERROR                                                            
         CLI   0(R3),SYSELQ                                                     
         BE    GETA404                                                          
*                                                                               
GETA402  IC    RE,1(R3)                                                         
         AR    R3,RE                                                            
         B     GETA4                                                            
*                                                                               
GETA404  CLC   SYSSEN,TSENUM                 MATCH ON SE NUMBER                 
         BNE   GETA402                                                          
         MVC   TRAKFIL,SYSNAME               FILE NAME                          
*                                                                               
***********************************************************************         
*        DO SMF CALL WITH RECORD HERE                                 *         
***********************************************************************         
         GOTOR VSMFOUT,SMFPARM,7,TRECORD                                        
         B     OKEXIT                                                           
         EJECT                                                                  
***********************************************************************         
*        EXIT ROUTINES                                                *         
***********************************************************************         
*                                                                               
ERROR    L     R1,APARM                                                         
         MVI   4(R1),1                                                          
         B     EXIT                                                             
*                                                                               
OKEXIT   L     R1,APARM                                                         
         MVI   4(R1),0                                                          
*                                                                               
EXIT     XMOD1                                                                  
         EJECT                                                                  
CTFILE   DC    C'CTFILE '                                                       
ETIME    DC    X'402120204B20204B2020'                                          
         EJECT                                                                  
         LTORG                                                                  
         EJECT                                                                  
WORKD    DSECT                                                                  
*                                                                               
APARM    DS    A                   A(PARAMETER LIST)                            
*                                                                               
PARMS    DS    0C                                                               
STATUS   DS    X                   STATUS                                       
         ORG   STATUS                                                           
AACWORKD DS    A                   A(ACWORKD)                                   
APOST    DS    A                   A(POSTINGS)                                  
AMARK    DS    A                   A(MARKINGS)                                  
ARECS    DS    A                   A(RECORDS)                                   
AWKID    DS    A                   A(WKID)                                      
PARML    EQU   *-PARMS                                                          
*                                                                               
TDUB     DS    PL8                                                              
PARM     DS    6F                                                               
SMFPARM  DS    6F                                                               
*                                                                               
VSMFOUT  DS    A                                                                
TSENUM   DS    X                   SE NUMBER                                    
*                                                                               
EWORK    DS    CL(L'ETIME)         EDIT WORK AREA                               
CXREC    DS    CL2048                                                           
*                                                                               
TRECORD DS     XL(TRAKLNQ)                                                      
*                                                                               
WORKX    EQU   *                                                                
         EJECT                                                                  
       ++INCLUDE DMWRKRD                                                        
         EJECT                                                                  
       ++INCLUDE ACTRACKD                                                       
         EJECT                                                                  
*ACREPWORKD                                                                     
         PRINT OFF                                                              
       ++INCLUDE ACREPWORKD                                                     
         PRINT ON                                                               
*DDCOMFACS                                                                      
         PRINT OFF                                                              
       ++INCLUDE DDCOMFACS                                                      
         PRINT ON                                                               
*CTGENFILE                                                                      
         PRINT OFF                                                              
       ++INCLUDE CTGENFILE                                                      
         PRINT ON                                                               
*DDMASTD                                                                        
         PRINT OFF                                                              
       ++INCLUDE DDMASTD                                                        
         PRINT ON                                                               
*DDSYSELD                                                                       
         PRINT OFF                                                              
       ++INCLUDE DDSYSELD                                                       
         PRINT ON                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'006ACTRACK   08/25/10'                                      
         END                                                                    
