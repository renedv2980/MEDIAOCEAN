*          DATA SET DEGETNET   AT LEVEL 055 AS OF 09/14/11                      
*PROCESS USING(WARN(15))           ENABLE ALL USING STATEMENT WARNINGS          
*CATALP DEGETNET                                                                
         TITLE 'DEGETNET - NETWORK FILES READ CONTROLLER'                       
*                                                                               
* NOV17/03 - FOR CABLE NAD TIME PERIOD REQUEST, SHIFT TO FIRST WEEK OF          
*            THE MONTH. THE MONTHLY AVERAGES ARE ON FILE AS WEEKLY              
*            BOOKS. SHIFT LOGIC WAS INITIALLY IN THE RESEARCH WRITER.           
*                                                                               
GETNET   CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 260,**GETNET,RA,CLEAR=YES                                        
         LR    R9,RC                                                            
         LR    R7,R1                                                            
         L     RC,0(R1)                                                         
         USING DEGETD,RC           RC=A(W/S)                                    
         USING MYWRK,R9            GETNET'S TEMP WRK STORAGE                    
         USING DBLOCKD,R6          R6=A(DBLOCK)                                 
         USING COMFACSD,R8         R8=A(COMFACS)                                
         LA    R1,SPANAD                                                        
         STCM  R1,15,DBSPANAD      WORK AREA FOR SPANNED RECORDS                
         L     R4,DBAREC                                                        
         CLI   0(R7),0             1ST TIME IN                                  
         BNE   *+10                                                             
         XC    0(30,R4),0(R4)      CLEAR USER WORK AREA                         
*                                                                               
         MVC   SVDBASRC,DBACTSRC   SAVE INCOMING VALUES                         
         MVC   SVDBIMED,DBINTMED   (CHANGED IN THE HOOK)                        
         MVC   SVDBIFIL,DBINTFIL                                                
*                                                                               
         MVC   SVDBSTYP,DBSTYPE                                                 
*                                                                               
         CLI   DBFUNCT,DBGETTOT    ALLOWED FOR REP-NHT CALL                     
         BNE   GETEXTND                                                         
         MVC   DBACTSTA,=C'HUT  '                                               
         TM    SPNETFLG,SPNHTQ                                                  
         BNO   *+8                                                              
         MVI   DBACTSTA+4,C'H'                                                  
         TM    SPNETFLG,SPNTIQ                                                  
         BNO   *+8                                                              
         MVI   DBACTSTA+4,C'T'                                                  
         TM    SPNETFLG,SPCBLQ                                                  
         BNO   *+8                                                              
         MVI   DBACTSTA+4,C'Q'     <--FORCE QTR HR LK UP                        
*                                                                               
GETEXTND DS    0H                 INIT DBEXTEND BLOCK FIELDS                    
         MVI   EXTNDLP,C'N'        NOT IN LOOP                                  
         L     RE,DBEXTEND                                                      
         LTR   RE,RE                                                            
         BZ    GETEXTDX                                                         
*                                                                               
         XC    AODTLIST,AODTLIST                                                
         ICM   RE,15,ADTLIST       'DYTM' EXTEND RECEIVED?                      
         BNZ   *+8                  USE IT                                      
         BAS   RE,GETODYT           CK FOR 'ODYT' EXTEND (NETW CABLE)           
*                                                                               
         CLI   SPNETFLG,0           FOR SPOT REQUESTS ONLY                      
         BE    GETEXTDX                                                         
         MVI   EXTFLG,0                                                         
*                                                                               
GETMSTA  L     RE,DBEXTEND         MULTI STATIONS?                              
         LTR   RE,RE                                                            
         BZ    GETMBKS                                                          
         USING DBXMSD,RE                                                        
         CLC   DBXMSID,=C'MSTA'                                                 
         BE    *+12                                                             
         ICM   RE,15,DBXMSNXT                                                   
         B     *-20                                                             
         MVC   DBSELSTA,DBXMSTA    SET FIRST STATION                            
         MVI   DBXMSIDX,0                                                       
         OI    EXTFLG,EXTMSTQ                                                   
*                                                                               
GETMBKS  L     RE,DBEXTEND         MULTI BOOKS?                                 
         LTR   RE,RE                                                            
         BZ    GETDYTM                                                          
         USING DBXMBID,RE                                                       
         CLC   DBXMBID,=C'MBKS'                                                 
         BE    *+12                                                             
         ICM   RE,15,DBXMBNXT                                                   
         B     *-20                                                             
         MVC   DBSELBK,DBXMBKS     SET FIRST BOOK                               
         MVI   DBXMBIDX,0                                                       
         OI    EXTFLG,EXTMBKQ                                                   
*                                                                               
GETDYTM  L     RE,DBEXTEND          MULTI DAY/TIMES?                            
         LTR   RE,RE                                                            
         BZ    GETEXTDX                                                         
         USING DBXTLID,RE                                                       
         CLC   DBXTLID,=C'DYTM'                                                 
         BE    *+12                                                             
         ICM   RE,15,DBXTLNXT                                                   
         B     *-20                                                             
         MVC   DBSELDAY(5),DBXTLIST    SET FIRST DAY/TM                         
         MVI   DBXTLIDX,0                                                       
         OI    EXTFLG,EXTDYTQ                                                   
         DROP  RE                                                               
*                                                                               
GETEXTDX DS    0H                  DONE PROCESSING EXTEND FIELDS                
*                                                                               
GETUNV   L     R4,DBAREC                                                        
         CLI   DBSELSTA+4,X'88'    LOWER CASE 'H'= WEIGHTED HISP                
         BE    *+8                                                              
         CLI   DBSELSTA+4,C'H'     REGULAR HISP                                 
         BNE   GOTUNIV                                                          
         TM    SPNETFLG,SPNHTQ     SPOT-NHT CALL?                               
         BO    *+12                                                             
         CLI   DBSELSRC,C'N'                                                    
         BNE   GOTUNIV                                                          
         CLC   DBSELSTA(4),=C'UUUU'                                             
         BE    GETXXX                                                           
         L     RE,DBAREC                                                        
         MVC   0(L'DBKEY,RE),DBKEY                                              
         XC    DBKEY,DBKEY                                                      
         MVC   DBKEY(8),=C'PNNUUUUH'                                            
         CLI   DBSELSTA+4,X'88'    LOWER CASE 'H'= WEIGHTED HISP                
         BNE   *+8                                                              
         MVI   DBKEY+7,X'88'       READ WEIGHTED UNIVERSES INSTEAD              
         MVC   DBKEY+1(1),DBSELMED                                              
         MVC   DBKEY+10(2),DBSELBK                                              
         MVI   IOFLAG,DIR+HIGH                                                  
         GOTO1 AIO,IOFLAG                                                       
         BNE   GOTUNIV                                                          
         CLC   DBKEY+10(2),DBSELBK                                              
         BNE   GOTUNIV                                                          
         CLC   DBKEY(8),KEYSAVE                                                 
         BNE   GOTUNIV                                                          
*                                                                               
         ICM   RE,15,DBSPANAD                                                   
         MVC   0(4,RE),=C'NTHU'    NHT LABEL FOR SPAN AREA                      
         LA    RE,4(RE)                                                         
         ST    RE,DBAREC           UNIV RECD AFTER LABEL                        
         MVC   DIRSAVE,DBKEY                                                    
         MVI   IOFLAG,FILE+READ                                                 
         GOTO1 AIO,IOFLAG                                                       
         ICM   RE,15,DBSPANAD                                                   
         LA    RE,4(RE)                                                         
         USING PRKEY,RE                                                         
         LA    RE,PRFRSTEL                                                      
         SR    RF,RF                                                            
GETNHTU1 CLI   0(RE),0             TEMP FIX FOR ELEMENT CODES                   
         BE    GOTUNIV                                                          
         CLI   0(RE),X'41'                                                      
         BNE   *+8                                                              
         MVI   0(RE),X'4B'                                                      
         CLI   0(RE),X'42'                                                      
         BNE   *+8                                                              
         MVI   0(RE),X'4C'                                                      
         IC    RF,1(RE)                                                         
         AR    RE,RF                                                            
         B     GETNHTU1                                                         
         DROP  RE                                                               
*                                                                               
GOTUNIV  ST    R4,DBAREC           RESTORE ORIG DBAREC                          
         CLI   EXTNDLP,C'Y'                                                     
         BE    *+14                                                             
         LR    R1,R7                                                            
         CLI   0(R1),0                                                          
         BNE   GETNET2                                                          
         ZIC   R1,DBFUNCT          SAVE THE REQUESTED FUNCTION                  
         MVI   DBFUNCT,DBTSTACS    SET FOR VALIDATE                             
         GOTO1 ATSTACS             CALL TO VALIDATE ACCESS                      
         STC   R1,DBFUNCT          RESTORE ORIGINAL REQUEST                     
         CLI   DBERROR,0           EXIT IF NOT VALID                            
         BE    AUTHOK              ACCESS OKAY                                  
*-->>                                                                           
         CLI   SPNETFLG,0                                                       
         BE    GETX                                                             
         OC    DBEXTEND,DBEXTEND                                                
         BZ    GETX                                                             
         TM    EXTFLG,EXTMBKQ      MULTI BOOKS PRESENT?                         
         BNO   GETX                                                             
         B     GETNETX             LOOP THRU EXTENDS                            
*-->>                                                                           
*                                                                               
AUTHOK   LA    R1,DBSELBK          SET THE CONTOL TABLE                         
         L     RF,AGETCTRL                                                      
         OC    ACONHDR,ACONHDR                                                  
         BNZ   *+6                                                              
         BASR  RE,RF                                                            
*                                                                               
         CLI   DBSELSTA+4,C'D'     NTI DAILIES?                                 
         BE    *+8                                                              
         CLI   DBSELSTA+4,C'S'     SYNDICATORS AND                              
         BE    *+8                                                              
         CLI   DBSELSTA+4,C'T'     REG NETWORKS ONLY                            
         BNE   GETNETA                                                          
         CLC   DBSELBK,=X'5724'    PRIOR TO 8736 HAS DIARY                      
         BL    GETNETA                                                          
         CLC   DBSELBK,=X'5731'    OUT OF ORDER WEEKS                           
         BE    GETNETA                                                          
         CLC   DBSELBK,=X'5732'                                                 
         BE    GETNETA                                                          
         CLC   DBSELBK,=X'5733'                                                 
         BE    GETNETA                                                          
         CLI   DBBTYPE,C'D'        KILL DIARY AFTER THESE DATES                 
         BNE   *+8                                                              
         MVI   DBBTYPE,X'FE'                                                    
         CLI   DBBTYPE,0           ALLOW USER TO SPECIFY TYPE                   
         BNE   GETNETA                                                          
         MVI   DBBTYPE,C'A'        DEFAULT TO PM DATA                           
*                                                                               
GETNETA  CLI   DBBTYPE,C'D'        FIX UP DIARY                                 
         BNE   *+8                                                              
         MVI   DBBTYPE,0           TO USE DEFAULT BOOK                          
         TM    SPNETFLG,SPNADQ                                                  
         BO    GETNADQ             SPOT-NAD FILE LOOK UP?                       
         CLC   DBFILE,=CL3'NAD'                                                 
         BE    GETNADQ                                                          
         CLI   DBFUNCT,DBVLNBK                                                  
         BNE   *+10                                                             
         CLC   DBSELSTA,=C'WB  S'  FOR SYNDICATION                              
         BNE   *+8                                                              
         MVI   DBSTYPE,0           ALWAYS VALIDATE FOR REGULAR PROGR            
         B     GETNETB             ALL OTHER FILES                              
         EJECT                                                                  
**********************************************************************          
*GETNETB - THIS AREA FOR ALL FILES EXCEPT NAD                                   
*        READS THRU P-RECD TIME PERD RECDS BUMPING DAY AND SQH IN KEY           
**********************************************************************          
GETNETB  DS    0H                                                               
         LA    R2,DBKEY                                                         
         USING PRKEY,R2            R2=A(KEY)                                    
         XC    PRKMAJOR,PRKMAJOR   READ P-RECDS                                 
         LA    R3,DBDQTAB                                                       
DBDQ     USING DBDQD,R3                                                         
         MVC   DUBSV,DUB                                                        
         MVC   WORKSV+0(1),DBDQ.DBDQSQH                                         
         MVC   WORKSV+1(1),DBDQ.DBDQEQH                                         
         MVC   WORKSV+2(1),DBSELDAY                                             
         MVC   WORKSV+3(4),DBSELTIM                                             
         DROP  DBDQ                                                             
         MVI   PRCODE,PRCODEQU                                                  
         MVC   PRMEDIA,DBINTMED                                                 
         TM    SPNETFLG,SPCBLQ                                                  
         BNO   *+8                                                              
         MVI   PRMEDIA,C'C'        PCN FOR CABLE                                
         CLI   PRMEDIA,C'H'                                                     
         BNE   *+8                                                              
         MVI   PRMEDIA,C'N'                                                     
         CLI   DBMEDOVR,0          OVERRIDE MEDIA                               
         BE    *+10                                                             
         MVC   PRMEDIA,DBMEDOVR                                                 
         CLI   DBSELMED,C'W'                                                    
         BNE   *+8                                                              
         MVI   PRMEDIA,C'W'                                                     
         MVC   PRSRC,DBACTSRC                                                   
         TM    SPNETFLG,SPNHTQ+SPNTIQ+SPCBLQ  SPOT NHT/NTI/CBL CALL?            
         BZ    *+8                                                              
         MVI   PRSRC,C'N'                                                       
         MVC   PRSTAT,DBACTSTA                                                  
         OC    DBACTSTA,DBACTSTA                                                
         BNZ   *+10                                                             
         MVC   PRSTAT,DBSELSTA                                                  
         OC    DBCABNUM,DBCABNUM   CABLE NUMBER PRESENT - POST OCT/93           
         BZ    *+10                                                             
         MVC   PRSTAT(4),DBCABNUM  YES- USE IT A STATION                        
         MVC   PRBOOK,DBACTBK                                                   
         OC    DBACTBK,DBACTBK                                                  
         BNZ   *+10                                                             
         MVC   PRBOOK,DBSELBK                                                   
         MVC   PRSTYP,DBSTYPE                                                   
         TM    SPNETFLG,SPNHTQ     SPOT-NHT CALL?-BKTYPE USED FOR MKTBK         
         BO    *+10                                                             
         MVC   PRBTYP,DBBTYPE                                                   
*                                                                               
         CLI   DBSELSTA+4,C'C'     CABLE                                        
         BNE   *+8                                                              
         CLI   DBBTYPE,C'N'        NAD                                          
         BNE   *+8                                                              
         BAS   RE,SHIFTWK                                                       
*                                                                               
         MVI   IOFLAG,DIR+READ     READ/DIRECTORY                               
         GOTO1 AIO,IOFLAG                                                       
         BL    GETX                                                             
         BNE   GETDLY                                                           
         MVC   DBACTBK,PRBOOK                                                   
         MVI   DBERROR,0                                                        
         B     GETNETC                                                          
         EJECT                                                                  
*                                                                               
GETDLY   DS    0H                                                               
         LA    R1,KEYSAVE                                                       
         CLI   PRSTAT+4-PRKEY(R1),C'C'   WAS SEARCH FOR NTI DATA                
         BE    GETCUNV                                                          
         CLI   DBSTYPE,X'00'       ONLY SWITCH ON 1ST ITERATION                 
         BNE   GETNETX                                                          
         LA    R1,KEYSAVE                                                       
         CLI   PRSTAT+4-PRKEY(R1),C'T'   WAS SEARCH FOR NTI DATA                
         BNE   GETNETX                                                          
         CLC   DBKEY(PRSTYP-PRKEY),0(R1)   YES, DID WE MATCH STN & BK?          
         BE    GETNETX             YES, JUST CHG STYPE FIELD                    
         BAS   RE,TSTDYL                                                        
         BNE   GETNETX                                                          
         MVC   DBACTSTA,DBSELSTA   NO DATA, ARE THEIR DAILIES?                  
         MVI   DBACTSTA+4,C'D'                                                  
         B     GETNETB                                                          
*                                                                               
GETCUNV  CLI   DBFUNCT,DBVLNBK                                                  
*        BNE   GETNETX                  FOR CABLE NAD,                          
         BE    GETCUNV9                 EXIT WHEN ALL STYPE'S HAVE              
         LA    R1,KEYSAVE               BEEN PROCESSED                          
         CLI   PRSTAT+4-PRKEY(R1),C'C'  CABLE ...                               
         BNE   GETNETX                                                          
         CLI   DBBTYPE,C'N'               ... NAD                               
         BNE   GETNETX                                                          
         B     GETNETX2                 NO DIR ENTRY.DONE W/ALL STYP'S          
*                                                                               
GETCUNV9 CLC   DBKEY(PRSTAT+3-PRKEY),0(R1)    DID WE MATCH STN?                 
         BNE   GETNETX             YES, JUST CHG BAND FIELD                     
         MVC   DBACTSTA(4),PRSTAT  NO DATA, ARE THERE UNIVS?                    
         MVI   DBACTSTA+4,C'U'                                                  
         B     GETNETB                                                          
*                                                                               
*                                                                               
GETNETX  DS    0H                                                               
         MVC   DBKEY,KEYSAVE       RESTORE KEY TRIED TO READ                    
*                                                                               
         CLI   PRSTAT+4,C'C'       CABLE HAS DIFFERENT FORMAT                   
*        BE    GETNETX2                                                         
         BNE   GETNETX0                                                         
         CLI   DBBTYPE,C'N'        CABLE NAD                                    
         BNE   GETNETX2            REGULAR CABLE                                
         CLI   DBFUNCT,DBVLNBK     VALIDATE?                                    
         BE    GETNETX2            STATION NOT FOUND. JUST EXIT                 
         B     GETNETX1                                                         
*                                                                               
GETNETX0 CLI   PRSTAT+4,C'U'       CABLE HAS DIFFERENT FORMAT                   
         BE    GETNETX2                                                         
         CLI   PRSTAT+4,C'Q'                                                    
         BE    GETNETX2                                                         
*                                                                               
GETNETX1 CLI   SPNETFLG,0                                                       
         BE    *+14                                                             
         OC    DBEXTEND,DBEXTEND   IF DOING EXTEND, INIT FLDS                   
         BNZ   *+12                                                             
         CLI   PRBOOK,X'5E'        BYPASS LOGIC PRIOR TO '94                    
         BL    GETNETX2            MOST FILES START W/95 EXCEPT NAD             
         MVI   DBLSTNXT,0          DO INIT'S                                    
         XC    DBMINKEY,DBMINKEY   CLEAR PASSIVE KEY FOR 1ST RDHI               
         XC    DBDQTAB,DBDQTAB                                                  
         XC    DBACTBK,DBACTBK                                                  
         MVI   DBERROR,0                                                        
         MVC   DUB,DUBSV                                                        
         LA    R3,DBDQTAB          RESTORE ORIG REQUEST DAYS/QHRS               
DBDQ     USING DBDQD,R3                                                         
         MVC   DBDQ.DBDQSQH,WORKSV                                              
         MVC   DBDQ.DBDQEQH,WORKSV+1                                            
         MVC   DBSELDAY,WORKSV+2                                                
         MVC   DBSELTIM(4),WORKSV+3                                             
         DROP  DBDQ                                                             
         CLI   PRBOOK,X'5E'        BYPASS LOGIC PRIOR TO '94                    
         BL    GETNETX2                                                         
*                                                                               
         CLI   DBSELSTA+4,C'C'     CABLE                                        
         BNE   GETNETXA                                                         
         CLI   DBBTYPE,C'N'        FOR CABLE NAD,                               
         BNE   GETNETXA            INCREASE STYP FOR SPANNING RECORDS           
         ZIC   R0,PRSTYP                                                        
         AHI   R0,1                                                             
         STC   R0,DBSTYPE                                                       
         B     GETNETB                                                          
*                                                                               
GETNETXA CLI   PRSTYP,X'00'        REG TIME PERD READ?                          
         BNE   *+12                                                             
         MVI   DBSTYPE,C'B'        READ FOR BREAKOUTS                           
         B     GETNETB             START OVER SEARCHING FOR BRKS                
         CLI   PRSTYP,C'B'         NO BREAKOUTS?                                
         BNE   *+12                                                             
         MVI   DBSTYPE,C'S'        TRY SPECIALS                                 
         B     GETNETB             START OVER SEARCHING FOR BRKS                
GETNETX2 DS    0H                                                               
*                                                                               
         CLI   SPNETFLG,0          DBEXTEND ONLY VALID WITH SPOT LK UP          
         BE    GETNTX20                                                         
         OC    DBEXTEND,DBEXTEND                                                
         BZ    GETNTX20                                                         
         MVC   WORK(1),DBACTMED                                                 
         MVC   WORK+1(1),DBACTSRC                                               
         MVC   WORK+2(2),DBACTAGY                                               
         XC    DBACTUAL,DBACTUAL                                                
         MVC   DBACTMED,WORK                                                    
         MVC   DBACTSRC,WORK+1                                                  
         MVC   DBACTAGY,WORK+2                                                  
         MVI   DBSTYPE,0           RESET B/S INDICATOR                          
         MVI   EXTNDLP,C'Y'                                                     
*                                                                               
NXTDYTM  TM    EXTFLG,EXTDYTQ      IS THERE A DYTM BLOCK?                       
         BNO   NXTMBK                                                           
         L     RE,DBEXTEND         CHECK FOR MULIPLE DAY/TIME                   
         LTR   RE,RE               NO MULTI D/T   - END IT                      
         BZ    NXTMBK                                                           
         USING DBXTLID,RE                                                       
         CLC   DBXTLID,=C'DYTM'    THIS IS THE ONE WE WANT                      
         BE    *+12                                                             
         ICM   RE,15,DBXTLNXT                                                   
         B     *-20                                                             
         ZIC   RF,DBXTLIDX                                                      
         LA    RF,1(RF)                                                         
         STC   RF,DBXTLIDX                                                      
         MH    RF,=H'5'                                                         
         LA    RF,DBXTLIST(RF)                                                  
         CLI   0(RF),0                                                          
         BE    NXTMBK              END OF DAY TIME LIST, DO NXT MBK             
         MVC   DBSELDAY(5),0(RF)                                                
         B     GETEXTDX            START A NEW ASEARCH                          
*                                                                               
NXTMBK   TM    EXTFLG,EXTMBKQ      IS THERE A MBKS BLOCK?                       
         BNO   NXTMSTA                                                          
         L     RE,DBEXTEND         CHECK FOR MULIPLE BOOKS                      
         LTR   RE,RE               NO MULTI BOOKS - END IT                      
         BZ    NXTMSTA                                                          
         USING DBXMBID,RE                                                       
         CLC   DBXMBID,=C'MBKS'    THIS IS THE ONE WE WANT                      
         BE    *+12                                                             
         ICM   RE,15,DBXMBNXT                                                   
         B     *-20                                                             
         ZIC   RF,DBXMBIDX                                                      
         LA    RF,1(RF)                                                         
         STC   RF,DBXMBIDX                                                      
         SLL   RF,1                                                             
         LA    RF,DBXMBKS(RF)                                                   
         CLI   0(RF),0                                                          
         BE    NXTMSTA             END OF BOOKS IN MBK LIST                     
         MVC   DBSELBK,0(RF)                                                    
         B     GETDYTM             RESET DAYS/TIMES AND RE-SEARCH               
*                                                                               
NXTMSTA  TM    EXTFLG,EXTMSTQ      WAS THERE A MSTA BLOCK?                      
         BNO   GETNTX20                                                         
         L     RE,DBEXTEND         CHECK FOR MULIPLE STATIONS                   
         LTR   RE,RE                                                            
         BZ    GETNTX20            DONE                                         
         USING DBXMSD,RE                                                        
         CLC   DBXMSID,=C'MSTA'                                                 
         BE    *+12                                                             
         ICM   RE,15,DBXMSNXT                                                   
         B     *-20                                                             
         ZIC   RF,DBXMSIDX                                                      
         LA    RF,1(RF)                                                         
         STC   RF,DBXMSIDX                                                      
         MH    RF,=Y(L'DBXMSTA)                                                 
         LA    RF,DBXMSTA(RF)                                                   
         CLI   0(RF),0                                                          
         BE    GETNTX20            DONE - END OF STATION LIST                   
         MVC   DBSELSTA,0(RF)                                                   
         XC    DBDQPTR(5),DBDQPTR                                               
         B     GETMBKS                                                          
*                                                                               
GETNTX20 MVI   DBERROR,EOF                                                      
         B     GETX                ELSE REALLY EXIT                             
*                                                                               
         EJECT                                                                  
**********************************************************************          
*GETNETC - RETURNS HERE UPON RE-ENTRY. FILTERS RECDS ON DAYS AND TIMES          
*        READS SEQ TIME PERIOD FILE (P-RECDS) BUMPING DAY AND SQH               
**********************************************************************          
GETNETC  DS    0H                                                               
*2/7/03                                                                         
****     MVI   BACKDUP,C'N'        NOT BACKED UP YET                            
*                                                                               
         CLI   DBFUNCT,DBGETTLB    TEST FOR NETW BK VALID FROM SPOT             
         BE    *+12                NO                                           
         CLI   DBFUNCT,DBVLNBK     TEST FOR NETW BOOK VALIDATION                
         BNE   *+16                NO                                           
         MVI   DBRECTYP,DBRECNBK   YES-RETURNING VALID BOOK                     
         MVI   DBMODE,DBMFRST                                                   
         B     GETX                                                             
         OC    DBSELPUR,DBSELPUR   TEST FOR PURE NUMBER                         
         BNZ   GETNET1                                                          
*                                                                               
         CLI   DBSELDAY,0          TEST FOR DEFAULT DAY                         
         BNE   *+8                                                              
         MVI   DBSELDAY,X'7F'      SET TO M-SUN                                 
         OC    DBSELTIM,DBSELTIM   TEST FOR DEFAULT TIME                        
         BNZ   *+16                                                             
         MVC   DBSELTIM(2),=H'600' RANGE IS 6A - 545A                           
         MVC   DBSELTIM+2(2),=H'545'                                            
         SR    R1,R1               PASS TO GETDQ,  0= REG TABLE                 
*                                                                               
*        TM    SPNETFLG,SPNHTQ     SPOT-NHT CALL?                               
*        BNO   *+18                                                             
*        CLC   DBSELSTA(5),=C'HUT H'                                            
*        BNE   *+8                                                              
*        LA    R1,2                PICK UP OTHER DQTAB                          
*                                  BUILD LIST OF DAYS/QTR HOURS                 
         OC    AODTLIST,AODTLIST                                                
         BZ    *+12                FROM 'ODYT' EXTENTION                        
         BAS   RE,GETODQ           OPTIMIZING DAY/TIME TABLE                    
         B     GETNET2                                                          
*                                                                               
         GOTO1 AGETDQ              FROM 'DYTM' EXTENTION,IF ANY                 
         B     GETNET2             REGULAR DAY/TIME TABLE                       
         SPACE 1                                                                
GETNET1  DS    0H                  PURE NUMBER FILE READ                        
         XC    DBFACTOR,DBFACTOR                                                
         MVI   IOFLAG,FILE+READ                                                 
         MVC   DBMINKEY,DBSELPUR   PURE NUMBER IS MINOR KEY                     
         GOTO1 AIO,IOFLAG                                                       
         BNE   GETNETX                                                          
         BAS   RE,GETNETD                                                       
         B     GETNET12                                                         
         SPACE 1                                                                
GETNET2  DS    0H                                                               
         XC    DBFACTOR,DBFACTOR                                                
         LA    R3,DBDQTAB          POINT TO DAY/QUARTER HOUR TABLE              
DBDQ     USING DBDQD,R3                                                         
         ST    R3,ADQNTRY                                                       
         CLI   DBLSTNXT,0          TEST FOR FIRST ENTRY TO RTN                  
         BE    *+16                                                             
         CLI   DBLSTNXT,1          TEST FOR READ HIGH NEXT                      
         BE    GETNET4                                                          
         B     GETNET5             OTHERWISE READ SEQUENTIAL                    
*                                                                               
         LA    R1,DUB              BUILD A NETWORK CONTROL ENTRY                
         USING NETDQD,R1                                                        
         MVC   NETRSQH,DBDQ.DBDQSQH     REQUESTED SQH AND EQH                   
         MVC   NETREQH,DBDQ.DBDQEQH                                             
         MVC   NETCSQH,DBDQ.DBDQSQH     CURRENT SQH                             
         MVC   NETRDAY,DBSELDAY    REMAINING DAYS FOR CURRENT SQH               
         DROP  R1,DBDQ                                                          
*                                                                               
         USING NETDQD,R3                                                        
         MVC   0(NETDQLEN,R3),DUB  USE DQTAB FOR NETWORK CONTROL DATA           
         LA    R4,2                FIRST TIME FOR READ HIGH                     
         MVC   DBMINKEY(1),NETCSQH                                              
         MVI   DBMINKEY+1,0        DAY                                          
         SPACE 1                                                                
         MVI   FRSTHIGH,C'Y'                                                    
GETNET3  DS    0H                                                               
         MVI   IOFLAG,FILE+HIGH    FIRST READ HIGH                              
         GOTO1 AIO,IOFLAG                                                       
         BL    GETX                DISK ERROR                                   
         BE    *+14                GOOD RECORD                                  
         LTR   R4,R4               EOF - CHECK IF FIRST TIME                    
         BM    GETNETX                                                          
         B     GETNET3A                YES - TRY FOR ANOTHER RECORD             
*                                                                               
         CLI   NETRSQH,0           TEST FOR SQH=6AM                             
         BE    GETNET6             SKIP OVER BACKING UP LOGIC                   
         BAS   RE,GETNETD          EXTRACT DATA FROM RECORD                     
         CLC   DUB(1),NETCSQH      TEST RECORD SQH AGAINST START SQH            
         BNH   GETNET6                                                          
GETNET3A DS    0H                                                               
         LCR   R4,R4               TEST FOR SECOND                              
         BP    GETNET6             CONTINUE PROCESSING RECORD                   
*2/7/03                                                                         
****     CLI   BACKDUP,C'Y'        ALREADY BACKED UP?                           
****     BE    GETNET6             YES. CONTINUE PROCESSING RECD                
*                                                                               
         ZIC   R1,NETCSQH          BACK UP START QH 15 MINUTES AND              
         SH    R1,=H'1'            TRY AGAIN SINCE MOST NETWORK SHOWS           
         BM    GETNETX             ARE AT LEAST 1/2 HOUR (E.G. 1145)            
         STC   R1,DBMINKEY                                                      
         MVI   DBMINKEY+1,0                                                     
****     MVI   BACKDUP,C'Y'                                                     
         B     GETNET3                                                          
         SPACE 1                                                                
GETNET4  DS    0H                  READ HIGH LOGIC                              
         CLI   DBSTYPE,C'B'        DO READ SEQ FOR BRK & SPCLS                  
         BE    GETNET5                                                          
         CLI   DBSTYPE,C'S'                                                     
         BE    GETNET5                                                          
         ZIC   R1,NETCSQH          INCREMENT CURRENT SQH                        
         LA    R1,1(R1)                                                         
         STC   R1,NETCSQH                                                       
         STC   R1,DBMINKEY                                                      
         MVI   DBMINKEY+1,0                                                     
         MVC   NETRDAY,DBSELDAY    REFRESH DAY MASK                             
         MVI   FRSTHIGH,C'N'                                                    
         MVI   IOFLAG,FILE+HIGH                                                 
         GOTO1 AIO,IOFLAG                                                       
         BNE   GETNETX             DISK ERROR OR EOF                            
         B     GETNET6             PROCESS RECORD                               
         SPACE 1                                                                
GETNET5  DS    0H                  READ SEQUENTIAL LOGIC                        
         MVI   IOFLAG,FILE+SEQ                                                  
         GOTO1 AIO,IOFLAG                                                       
         BNE   GETNETX                                                          
****     B     GETNET6                                                          
         CLI   FRSTHIGH,C'Y'                                                    
         BNE   GETNET6                                                          
         CLI   NETRDAY,0           SEE IF ALL DAYS FOUND FOR THIS QH            
         BE    GETNET6             YES. GO AND READ HIGH FOR NEXT QH            
         CLI   NETRSQH,0           TEST FOR SQH=6AM                             
         BE    GETNET6             SKIP OVER BACKING UP LOGIC                   
         BAS   RE,GETNETD          EXTRACT DATA FROM RECORD                     
         CLC   DUB(1),NETCSQH      TEST RECORD SQH AGAINST START SQH            
         BNH   GETNET6                                                          
         MVI   FRSTHIGH,C'N'                                                    
         B     GETNET3A                                                         
         SPACE 1                                                                
GETNET6  DS    0H                                                               
         GOTO1 ATSTMRKT            TEST FILE ACCESS                             
         BNE   GETX                                                             
*                                                                               
         BAS   RE,GETNETD          EXTRACT DATA FROM REC TO DUB                 
         LA    R1,DBDQTAB                                                       
*--                                                                             
*        MVI   SPTDBDQ,X'01'                                                    
*        MVC   SPTDBDQ+1(1),DUB+2                                               
*        MVC   SPTDBDQ+2(1),DUB+0                                               
*        MVC   SPTDBDQ+3(1),DUB+6                                               
*        MVC   SPTDBDQ+4(1),DBDQDAYW-DBDQD(R1)                                  
*--                                                                             
*&&DO                                                                           
*2/7/03                                                                         
*BACK UP ONE QH                                                                 
         CLI   BACKDUP,C'N'        IF NOT BACKED UP YET, DO IT                  
         BNE   GETNET6A                                                         
         CLC   DUB(1),NETCSQH      TEST RECORD SQH AGAINST START SQH            
         BNH   GETNET6A                                                         
         LCR   R4,R4               TEST FOR SECOND                              
         BP    GETNET6A            CONTINUE PROCESSING RECORD                   
         ZIC   R1,NETCSQH          BACK UP START QH 15 MINUTES AND              
         SH    R1,=H'1'            TRY AGAIN SINCE MOST NETWORK SHOWS           
         BM    GETNET6A            ARE AT LEAST 1/2 HOUR (E.G. 1145)            
         STC   R1,DBMINKEY                                                      
         MVI   DBMINKEY+1,0                                                     
         MVI   BACKDUP,C'Y'                                                     
         B     GETNET3                                                          
         SPACE 1                                                                
*&&                                                                             
*                                                                               
GETNET6A CLC   DUB(1),NETREQH      TEST RECORD SQH AGAINST REQSTD EQH           
         BH    GETNETX                                                          
         CLC   DUB+6(1),NETRSQH    TEST RECORD EQH AGAINST REQSTD SQH           
         BL    GETNET5             NO OVERLAP-READ NEXT RECORD                  
         CLC   DUB(1),NETCSQH      TEST FOR SAME SQH AS CONTROL                 
         BE    *+16                                                             
         MVC   NETCSQH,DUB         UPDATE CURRENT SQH                           
         MVC   NETRDAY,DBSELDAY    REFRESH DAYS REMAINING MASK                  
*                                                                               
         CLC   DBSELBK,=X'5D25'    START OF NEW CABLE DATA                      
         BL    GTNET7C1             HAS M-F/M-SU DATA (BYPASS IT)               
         CLI   DBSELSTA+4,C'Q'     CABLE QURTER HOUR                            
         BE    *+8                                                              
         CLI   DBSELSTA+4,C'C'     CABLE                                        
         BE    GTNET7C2                                                         
*                                                                               
GTNET7C1 TM    SPNETFLG,SPNHTQ     FOR ALL NHT TEST DAYS                        
         BO    GTNETNHT                                                         
         CLC   DBSELSTA(3),=C'HUT'  ELSE ONLY TEST DAYS FOR HUT RCD             
         BE    GTNETNHX                                                         
         B     GETNET7             IF NOT HUT, LET THRU                         
*                                                                               
GTNETNHT CLI   DBSELDAY,X'7F'      M-SUN REQUESTED?                             
         BNE   *+16                NO                                           
         CLI   DUB+2,X'00'         YES: BYPASS M-F,ONLY TAKE SING DAYS          
         BE    GETNET5             COMPOSE M-SU FROM INDIV DAYS ONLY            
         B     GETNET8             TAKE THIS RECD                               
                                                                                
GTNETNHX DS    0X                                                               
*                                                                               
*** IGNORE P+S2 SETTING FROM DCON RECORD                                        
*** IT IS NO LONGER INFORCED FOR NATIONAL (PER JACK LUSHER FEB09)               
***      CLI   DBSATLIT,C'Y'       INDIVIDUAL DAYS AUTHORIZED                   
***      BNE   GTNETNH5                                                         
*                                                                               
         CLI   DBDAYOPT,C'A'       ALL AVG + DAYS                               
         BE    GETNET8             DON'T DO FILTERING                           
         CLI   DBDAYOPT,C'I'       INDIVIDUAL DAYS ONLY                         
         BNE   GTNETNH5            NO - DO POCKETPIECE LINES                    
         CLI   DUB+2,X'10'         M-F                                          
         BL    GETNET5             SKIP IT                                      
         CLI   DUB+2,X'80'         M-S OR VAR                                   
         BNL   GETNET5                                                          
         B     GETNET8             INDIV. DAY - USE IT                          
GTNETNH5 CLI   DBSELDAY,X'7C'      TEST FOR M-F REQUESTED                       
         BNE   *+16                NO                                           
         CLI   DUB+2,X'00'         YES-TEST FOR M-F HUT REC                     
         BE    GETNET8             YES-TAKE ONLY M-F, SKIP ALL OTHERS           
         B     GETNET5                                                          
                                                                                
         CLI   DBSELDAY,X'7F'      TEST FOR M-SUN REQUESTED                     
         BNE   *+16                NO                                           
         CLI   DUB+2,X'80'         TEST FOR M-SUN HUT REC                       
         BE    GETNET8             YES-ONLY TAKE M-SUN RECS                     
         B     GETNET5             SKIP ALL OTHERS                              
*                                                                               
GTNET7C2 CLI   DUB+2,X'00'         FOR SINGLE DAY READS                         
         BE    GETNET5             SKIP M-F AND MON-SUN                         
         CLI   DUB+2,X'80'                                                      
         BE    GETNET5                                                          
         B     GETNET8                                                          
         SPACE 1                                                                
GETNET7  DS    0H                                                               
         CLI   DBBTYPE,0           DIARY DATA                                   
         BE    GETNET7A            OPTIONS DON'T APPLY                          
         SPACE 1                                                                
         CLC   DBACTBK,=X'5721'    OPTIONAL DATA STARTS 87/33                   
         BL    GETNET7B                                                         
*                                                                               
*** IGNORE P+S2 SETTING FROM DCON RECORD                                        
*** IT IS NO LONGER INFORCED FOR NATIONAL (PER JACK LUSHER FEB09)               
***      CLI   DBSATLIT,C'Y'       INDIVIDUAL DAYS AUTHORIZED                   
***      BNE   GETNET7A            NO - BYPASS OPTION                           
*                                                                               
         CLI   DBDAYOPT,C'A'       ALL AVG + DAYS                               
         BE    GETNET7B            DON'T DO FILTERING                           
         CLI   DBDAYOPT,C'I'       INDIVIDUAL DAYS ONLY                         
         BNE   GETNET7A            NO - DO POCKETPIECE LINES                    
         CLI   DUB+2,X'10'         M-F                                          
         BL    GETNET5             SKIP IT                                      
         CLI   DUB+2,X'80'         M-S OR VAR                                   
         BNL   GETNET5                                                          
         B     GETNET7B            INDIV. DAY - USE IT                          
         SPACE 1                                                                
GETNET7A CLI   DUB+2,X'00'         M-F                                          
         BNE   *+8                 ALLOW MULTIPLE M-F                           
         MVI   WORK+4,C'N'                                                      
*                            FRIDAY NIGHT VIDEOS START AFTER                    
*                            ALL OTHER NBC PROGRAMS SO THERE                    
*                            ARE NO SAT OR SUN PROGS TO RESET                   
*                            THE INDIV. DAY BYPASS                              
*                            THEREFORE THE FOLLOWING 3 INSTRUCTIONS             
         CLC   WORK+5(1),DUB       PREV END LT OR EQ CURR START  *              
         BH    *+8                                               *              
         MVI   WORK+4,C'N'         RESET M-F FLAGS               *              
*                                                                               
         CLI   WORK+4,C'Y'         HAD A M-F PREVIOUSLY                         
         BNE   *+12                                                             
         CLI   DUB+2,X'50'         BYPASS INDIV. DAY                            
         BNH   GETNET5                                                          
         MVI   WORK+4,C'N'                                                      
         CLI   DUB+2,X'00'         M-F                                          
         BNE   GETNET7B                                                         
         MVC   WORK+5(1),DUB+6     SAVE M-F END TIME                            
         MVI   WORK+4,C'Y'         SET FOR BYPASS                               
*                                                                               
GETNET7B LA    R1,X'40'            DEFAULT IS REJECT DURATION LT 2              
         CLI   DBSELSTA+4,C'Q'     CABLE QURTER HOUR                            
         BE    *+8                                                              
         CLI   DBSELSTA+4,C'C'     CABLE                                        
         BE    *+8                                                              
         CLI   DBSELDUR,X'FF'      TEST SELECT ALL DURATIONS                    
         BNE   *+8                                                              
         LA    R1,X'00'            SET MASK TO NO-OP BRANCH                     
         CLI   DBSELDUR,X'FE'      TEST SELECT ONLY DURATION LT 2               
         BNE   *+8                                                              
         LA    R1,X'B0'            SET REJECT MASK TO BNL                       
         CLI   DUB+3,2             TEST FOR HALF HOUR DURATION                  
         EX    R1,NETBRAN1         REJECT RECORD USING MASK                     
         CLI   DUB+2,X'90'         TEST FOR VAR RECORD                          
         BE    GETNET5             SKIP IT                                      
         SPACE 1                                                                
GETNET8  DS    0H                  FIND DAY BITS FOR RECORD                     
         L     RE,ADQPNNS          NETWORK DAY TABLE                            
         LA    RE,DQHDRLN(RE)      POINT TO FIRST ENTRY                         
         USING DQDTAD,RE                                                        
         CLC   DUB+2(1),DQKDAY     TEST RECORD DAY VS. TABLE                    
         BE    *+12                                                             
         LA    RE,DQDTALN(RE)      NEXT ENTRY                                   
         B     *-14                                                             
         MVC   WORK(1),DQDAY       EXTRACT BIT SETTING FOR DAY                  
         DROP  RE                                                               
         SPACE 1                                                                
GETNET9  DS    0H                                                               
         ZIC   R1,NETRDAY          DAYS REMAINING FOR QH CONTROL                
         LR    R0,R1                                                            
         EX    R1,*+8              TEST RECORD DAY BITS VS. CONTROL             
         B     *+8                 BITS FOR ANY OVERLAP                         
         TM    WORK,0                                                           
         BZ    GETNET5             NO OVERLAP-NEXT RECORD                       
         ZIC   RF,WORK             RECORD BITS                                  
         NR    RF,R1               COMMON BITS (RECD-MASK) IN RF                
         LR    R1,RF               R1 NOW HAS DAY/DAYS MATCHED                  
*                                                                               
         CLI   DBSELSTA+4,C'S'     FOR SYND. ALWAYS GET THE                     
         BE    *+8                 OVERLAPS                                     
*                                                                               
         CLI   DBBEST,C'A'         TEST FOR ALL RECORDS FOR SQH                 
         BE    *+10                YES-TAKE ALL RECDS W ANY DAY OVERLAP         
         XR    R0,R1               TURN OFF THOSE BITS IN REMAINING MSK         
         STC   R0,NETRDAY          UPDATED MASK                                 
*                                                                               
         SR    R1,R1               CLEAR COUNT OF DAYS HIT                      
         LA    R0,7                MAXIMUM                                      
         SLL   RF,25                                                            
GETNET10 SR    RE,RE                                                            
         SLDL  RE,1                                                             
         LTR   RE,RE               TEST FOR ON BIT                              
         BZ    *+8                                                              
         LA    R1,1(R1)            BUMP COUNT                                   
         BCT   R0,GETNET10                                                      
         MVI   DBLSTNXT,1          SET TO READ HIGH NEXT                        
         CLI   DBSTYPE,C'B'                                                     
         BE    *+12                                                             
         CLI   DBSTYPE,C'S'                                                     
         BNE   *+8                                                              
         MVI   DBLSTNXT,2          READ SEQ                                     
         CLI   NETRDAY,0           TEST FOR ANY DAYS LEFT                       
         BE    *+8                                                              
         MVI   DBLSTNXT,2          YES-KEEP READING SEQUENTIAL                  
         B     GETNET15                                                         
         SPACE 1                                                                
GETNET12 DS    0H                  PURE NUMBER LOGIC                            
         MVI   DBLSTNXT,0          NO RE-ENTRY IN THIS CASE                     
         LA    R1,5                SET DAYS IN R1                               
         CLI   DUB+2,X'00'         TEST FOR M-F RECORD                          
         BE    GETNET15                                                         
         LA    R1,7                                                             
         CLI   DUB+2,X'80'         TEST FOR M-SU RECORD                         
         BE    GETNET15                                                         
         LA    R1,1                                                             
         SPACE 1                                                                
GETNET15 DS    0H                                                               
         STC   R1,DAYWGT                                                        
         MVC   DAYCODE,DUB+2                                                    
         CLC   DUB+6(1),NETREQH    CALCULATE AMOUNT OF QH OVERLAP WITH          
         BL    *+10                REQUESTED PERIOD                             
         MVC   DUB+6(1),NETREQH                                                 
         ZIC   R0,DUB+6            END QH                                       
         CLC   DUB(1),NETRSQH      TEST REC SQH AGAINST REQD SQH                
         BH    *+10                                                             
         MVC   DUB(1),NETRSQH                                                   
         MVC   DBACTSQC(1),DUB     SET CURRENT START QHR                        
         MVC   DBACTEQC(1),DUB+6      AND END QHR                               
         ZIC   RF,DUB              START QH                                     
         SR    R0,RF               EQH-SQH                                      
         AH    R0,=H'1'            PLUS 1 FOR DURATION                          
         MR    R0,R0               DAY WEIGHT X DURATION                        
         STH   R1,DBFACTOR                                                      
         AH    R1,DBDIVSOR         UPDATE DIVISOR                               
         STH   R1,DBDIVSOR                                                      
         MVI   DBRECTYP,DBRECDEM                                                
         MVI   DBMODE,DBMFRST                                                   
         OC    DBSELPUR,DBSELPUR   TEST FOR PURE NUMBER                         
         BNZ   *+8                                                              
         MVI   DBMODE,DBMNEXT      SET SEQUENTIAL MODE--THIS SETS RDHI          
         MVC   WORKSV+10(2),DBINTFIL   SAVE BEFORE HOOK                         
*                                                                               
         CLI   DBSELSTA+4,X'88'    NTHI DATA?                                   
         BE    *+8                 LOWER CASE 'H' = WEIGHTED HISP               
         CLI   DBSELSTA+4,C'H'     REGULAR HISP                                 
         BNE   *+16                                                             
         TM    DBCNTRL+2,X'04'     AUTHORIZED TO SPAN DOM DATA?                 
         BO    *+8                 YES                                          
         BAS   RE,DELSPDM          NO, DELETE OPTNL DATA FROM RECD              
*                                                                               
         MVI   RSTMB,0             LK UP MKT BRK FLAG INIT TO NO                
         CLI   SPNETFLG,0          SPOT->NETWORK LOOK UP?                       
         BE    USRHK                                                            
         LA    RE,SVDBDQ+1         ORIG DBDQTAB ENTRY                           
         LA    R1,DBDQTAB          SETUP DBDQTAB ENTRY FOR SPOT                 
DBDQ     USING DBDQD,R1                                                         
         MVC   SVDBDQ,DBDQPTR      SAVE ORIGINAL ENTRY                          
         XC    DBDQ.DBDQTAB(DBDQLEN),DBDQ.DBDQTAB                               
         MVI   DBDQPTR,1           ONLY ONE ENTRY IN TABLE                      
         MVC   DBDQ.DBDQSQH,NETRSQH-NETDQD(RE)                                  
         MVC   DBDQ.DBDQEQH,NETREQH-NETDQD(RE)                                  
         MVC   DBDQ.DBDQDAYW,DAYWGT                                             
         MVC   DBDQ.DBDQKDAY,DAYCODE                                            
         DROP  DBDQ                                                             
*                                                                               
         CLI   SPNETFLG,0          SPOT-NETWORK CALL?                           
         BE    USRHK                                                            
         MVC   SVACTBK,DBACTBK                                                  
         MVC   SVACTSTA,DBACTSTA                                                
         L     RE,DBAREC           PT TO RECD TO PASS BACK                      
         MVC   DBACTBK,PRBOOK-PRKEY(RE)                                         
         MVC   DBACTSTA,PRSTAT-PRKEY(RE)                                        
         TM    SPNETFLG,SPNHTQ     SPOT-NHT CALL?                               
         BNO   USRHK                                                            
*        CLI   DBBTYPE,0           BOOK TYPE REQUESTED?                         
*        BE    *+8                                                              
         BAS   RE,GETMB            GET MARKET BREAK RECORD                      
*                                                                               
*---------------------------------------------------------                      
USRHK    BAS   RE,GETX             GOING OFF TO USERS HOOK                      
*---------------------------------------------------------                      
*                                                                               
         CLI   SPNETFLG,0          SPOT-NHT CALL?                               
         BE    *+22                                                             
         MVC   DBDQPTR(5),SVDBDQ   RESTORE DQTAB ENTRY                          
         MVC   DBACTBK,SVACTBK     RESTORE ORIGINAL VALUE                       
         MVC   DBACTSTA,SVACTSTA   RESTORE ORIGINAL VALUE                       
         MVC   DBINTFIL(2),WORKSV+10  SAVE BEFORE HOOK                          
         CLI   RSTMB,C'Y'                                                       
         BNE   *+8                                                              
         BAS   RE,RSETMB           RESET POINTER                                
         OC    DBSELPUR,DBSELPUR   TEST FOR PURE NUMBER                         
         BNZ   GETNETX             YES, LOOK FOR SPEC/BRKOUT                    
         CLI   DBLSTNXT,2          TEST FOR READ SEQUENTIAL=(2)                 
         BE    GETNET5             READ SEQ                                     
         B     GETNET4             READ HIGH                                    
         SPACE 1                                                                
GETNET20 DS    0H                  EOF EXIT                                     
         MVI   DBERROR,EOF                                                      
         B     GETX                                                             
         DROP  R3                                                               
         SPACE 2                                                                
NETBRAN1 BC    0,GETNET5           EXECUTED                                     
         EJECT                                                                  
**********************************************************************          
* GET VALUES FROM AN NTI DEMO RECORD. (P-RECD FOR TIME PERIOD)                  
* ON EXIT DUB+0(1)=START QTR HOUR OF THIS RECORD                                
*         DUB+1(1)=DAY/WEEK OF THIS RECORD                                      
*         DUB+2(1)=DAY OF THIS RECORD                                           
*         DUB+3(1)=DURATION IN QTR HOURS                                        
*         DUB+4(1)=START QTR HOUR OF PREVIOUS RECORD                            
*         DUB+5(1)=DAY/WEEK OF PREVIOUS RECORD                                  
*         DUB+6(1)=END QTR HOUR OF RECORD                                       
**********************************************************************          
GETNETD  L     RF,DBAREC                                                        
         MVC   DUB(2),PRKMINOR-PRKEY(RF)                                        
         MVC   DUB+2(1),DUB+1                                                   
         NI    DUB+2,X'F0'                                                      
         AH    RF,DBDTADSP                                                      
         USING PHELEM,RF                                                        
         SR    R0,R0                                                            
*                                                                               
         OC    DBCABNUM,DBCABNUM   CHECK FOR CABLE                              
         BZ    GETNETD2                                                         
         ST    RF,DUB+4                                                         
GETCBD2  CLI   0(RF),0             FIX SCREWED UP 5E ELEMENTS                   
         BE    GETCBDX                                                          
         CLI   0(RF),X'5E'         FIND THE SOURCE/BOOK ELEMENT                 
         BE    GETCBD4                                                          
         IC    R0,1(RF)                                                         
         AR    RF,R0                                                            
         B     GETCBD2                                                          
GETCBD4  CLC   2(3,RF),=C'CNN'     MAKE SURE ITS CABLE                          
         BNE   GETCBDX                                                          
         CLC   DBSELBK,=X'5D25'                                                 
         BE    *+10                                                             
         MVC   5(2,RF),DBSELBK     SET TO INPUT BOOK                            
GETCBDX  L     RF,DUB+4                                                         
*                                                                               
GETNETD2 CLI   PHCODE,0            FIND QTR HOUR ELEMENT                        
         BNE   *+6                                                              
         DC    H'0'                                                             
         CLI   PHCODE,PHCODEQ                                                   
         BE    *+14                                                             
         IC    R0,1(RF)                                                         
         AR    RF,R0                                                            
         B     GETNETD2                                                         
         ST    RF,DBAQUART                                                      
         MVC   DUB+3(1),PHDUR                                                   
         MVC   DUB+4(1),PHPRVST                                                 
         MVC   DUB+5(1),PHPRVDW                                                 
         ZIC   RF,DUB+3                                                         
         ZIC   R0,DUB                                                           
         AR    RF,R0                                                            
         BCTR  RF,0                                                             
         STC   RF,DUB+6            SET END QTR HOUR                             
         BR    RE                                                               
         DROP  RF                                                               
         EJECT                                                                  
**********************************************************************          
*GETNADQ -  FOR NAD FILES: READ Q-RECS WHICH CONTAIN TIME PERD DATA             
**********************************************************************          
GETNADQ  DS    0H                                                               
         LA    R2,DBKEY            BUILD 'Q' RECORD KEY                         
         USING PMKEY,R2                                                         
         XC    PMKMAJOR,PMKMAJOR                                                
         MVI   PMCODE,PMCODEQU                                                  
         MVC   PMMEDIA,INTMED                                                   
         CLI   PMMEDIA,C'H'                                                     
         BNE   *+8                                                              
         MVI   PMMEDIA,C'N'                                                     
         CLI   DBMEDOVR,0          OVERRIDE MEDIA                               
         BE    *+10                                                             
         MVC   PMMEDIA,DBMEDOVR                                                 
         CLI   DBSELMED,C'W'                                                    
         BNE   *+8                                                              
         MVI   PMMEDIA,C'W'                                                     
         MVC   PMSRC,DBACTSRC                                                   
         CLI   PMSRC,SRCNAD                                                     
         BNE   *+8                                                              
         MVI   PMSRC,C'N'          SPT-NAD LK UP->SRC=NIELSEN                   
         MVC   PMBOOK,DBACTBK                                                   
         OC    DBACTBK,DBACTBK                                                  
         BNZ   *+10                                                             
         MVC   PMBOOK,DBSELBK                                                   
         MVC   PMSTAT,DBACTSTA                                                  
         OC    DBACTSTA,DBACTSTA                                                
         BNZ   *+10                                                             
         MVC   PMSTAT,DBSELSTA                                                  
         MVC   PMBTYP,DBBTYPE      SET BOOK TYPE                                
         MVC   DBMINKEY,DBSELPUR                                                
         MVI   IOFLAG,DIR+READ                                                  
         OC    DBSELPUR,DBSELPUR   TEST FOR PURE NUMBER                         
         BNZ   *+8                                                              
         MVI   IOFLAG,DIR+HIGH    SWITCH TO READ HIGH                           
         GOTO1 AIO,IOFLAG                                                       
         BNE   NADQ01              RECD NOT FOUND                               
         CLC   PMKEY(PMSTYP-PMKEY),KEYSAVE                                      
         BE    *+14                MATCH ON MEDIA,SOURCE,BOOK,STATION           
NADQ01   MVC   DBKEY,KEYSAVE       RESTORE SEARCH KEY                           
         B     NADQ20                                                           
*                                                                               
         CLI   DBFUNCT,DBVLNBK     VALIDATING NET/BOOK                          
         BNE   NADQ03                                                           
         CLI   DBSELSTA+4,C'S'                                                  
         BE    NADQ03                                                           
         CLC   KEYSAVE(PMSTYP-PMKEY),DBKEY                                      
         BNE   NADQ02                                                           
         CLI   PMBTYP,C'C'         CK MONITOR PLUS BTYP FOUND                   
         BNE   NADQ03                                                           
         CLI   DBBTYPE,C'C'        ALLOW IT ONLY IF REQ IS SPECIFIC             
         BE    NADQ03                                                           
NADQ02   MVC   DBKEY,KEYSAVE       RESTORE SEARCH KEY                           
         B     NADQ20                                                           
*                                                                               
NADQ03   CLI   DBFUNCT,DBGETTLB    LATEST BK LOOK UP?                           
         BNE   NADQ05                                                           
         MVC   DBACTBK,PMBOOK                                                   
         MVI   DBRECTYP,DBRECNBK                                                
         MVI   DBMODE,DBMFRST                                                   
         B     GETX                                                             
*                                                                               
NADQ05   MVC   DBNDXDA2,DBNDXDA    SAVE D/A                                     
         MVI   IOFLAG,FILE+HIGH    NOW READ THE FILE                            
         MVC   DBMINKEY,DBSELPRG                                                
         OC    DBSELPRG,DBSELPRG   READ FOR SPECIFIC RECD                       
         BZ    *+8                                                              
         MVI   IOFLAG,FILE+READ                                                 
         GOTO1 AIO,IOFLAG                                                       
         BE    *+14                RECD FOUND                                   
         MVC   DBKEY,KEYSAVE       NOT FOUND: RESTORE SEARCH KEY                
         B     NADQ20                                                           
         MVC   DBACTBK,PMBOOK                                                   
*                                                                               
NADQ10   DS    0H                                                               
         CLI   DBLSTNXT,1          RE-ENTRY ON ALL PRG FOR BK/STA               
         BNE   NADQ30                                                           
NADQ15   MVI   IOFLAG,FILE+SEQ     RETURN HERE FOR SIMPLE SEQUNTIAL             
         GOTO1 AIO,IOFLAG                                                       
         BE    NADQ30                                                           
*                                                                               
NADQ20   XC    PMBTYP+1(PMDATA-PMBTYP+1),PMBTYP+1  CLEAR KEY FIELDS             
         MVI   DBERROR,EOF                                                      
         XC    DBACTBK,DBACTBK                                                  
         CLI   PMSTYP,X'00'        REGULAR PRMS?                                
         BNE   *+12                                                             
         MVI   PMSTYP,C'B'         YES, NOW READ FOR BREAKOUTS                  
         B     NADQ25                                                           
         CLI   PMSTYP,C'B'         FINISHED READING FOR BREAKOUTS?              
         BNE   GETX                IF 'S' OR ANYTHING ELSE -> EOF               
         MVI   PMSTYP,C'S'         NOW READ FOR SPECIALS                        
         B     NADQ25                                                           
*                                                                               
NADQ25   DS    0H                                                               
         MVI   IOFLAG,DIR+HIGH     RDHIGH ON NEW KEY                            
         GOTO1 AIO,IOFLAG                                                       
         MVC   DBNDXDA2,DBNDXDA    SAVE D/A                                     
         L     R1,DBAREC                                                        
         CLC   PMKEY(PMBTYP+1-PMKEY),KEYSAVE                                    
         BE    *+14                NO BRKOUTS/SPCLS FOR THIS BOOK               
         MVC   DBKEY,KEYSAVE       RESTORE KEY USED IN CALL                     
         B     NADQ20                                                           
         MVI   IOFLAG,FILE+HIGH                                                 
         MVC   DBMINKEY,DBSELPUR                                                
         OC    DBSELPUR,DBSELPUR   READ FOR SPECIFIC RECD                       
         BZ    *+8                                                              
         MVI   IOFLAG,FILE+READ                                                 
         GOTO1 AIO,IOFLAG                                                       
         BNE   NADQ20              NO RECDS OR NO SPEC PRGM RECD                
         MVC   DBACTBK,PMBOOK                                                   
*                                                                               
NADQ30   DS    0H                                                               
         CLI   DBFUNCT,DBVLNBK     TEST FOR NETWORK BOOK VALIDATION             
         BNE   *+16                                                             
         MVI   DBRECTYP,DBRECNBK   YES-RETURN VALID BOOK                        
         MVI   DBMODE,DBMFRST                                                   
         B     GETX                DONE                                         
*                                                                               
         CLI   DBSELDAY,0          TEST FOR DEFAULT DAY                         
         BNE   *+8                                                              
         MVI   DBSELDAY,X'7F'      SET TO M-SUN                                 
         OC    DBSELTIM,DBSELTIM   TEST FOR DEFAULT TIME                        
         BNZ   *+16                                                             
         MVC   DBSELTIM(2),=H'600' RANGE IS 6A - 545A                           
         MVC   DBSELTIM+2(2),=H'545'                                            
         GOTO1 AGETDQ,0            BUILD LIST OF DAYS/QTR HOURS                 
         GOTO1 ATSTMRKT            TEST FILE ACCESS                             
         BNE   GETX                                                             
         XC    DBAQUART,DBAQUART                                                
         BAS   RE,GETNTID          EXTRACT INFO FROM REC'S DATA ELEMS           
         CLC   DUB(1),DBDQEQH      TEST REC EQH VS RQST                         
         BH    NADQ15              REJECT READ NEXT RECD                        
         CLC   DUB+6(1),DBDQSQH    TEST REC SQH VS RQST                         
         BL    NADQ15              REJECT READ NEXT RECD                        
         L     RE,ADQPNNS          NETWORK DAY TABLE                            
         LA    RE,DQHDRLN(RE)      POINT TO FIRST ENTRY                         
         USING DQDTAD,RE                                                        
         CLC   DUB+2(1),DQKDAY     TEST RECORD DAY VS. TABLE                    
         BE    *+12                                                             
         LA    RE,DQDTALN(RE)      NEXT ENTRY                                   
         B     *-14                                                             
         MVC   WORK(1),DQDAY       EXTRACT BIT SETTING FOR DAY                  
         DROP  RE                                                               
*                                                                               
         CLC   DBSELSTA(3),=C'HUT'                                              
         BNE   NADQ35                                                           
         CLI   DBSELDAY,X'7C'      M-F HUT RECDS ONLY?                          
         BNE   *+16                                                             
         CLI   DUB+2,X'00'         DID WE GET A M-F RECD?                       
         BE    NADQ45              YES                                          
         B     NADQ15              NO, READ ANOTHER RECD                        
         CLI   DBSELDAY,X'7F'      M-S HUT RECDS ONLY?                          
         BNE   *+16                                                             
         CLI   DUB+2,X'80'         DID WE GET A M-F RECD?                       
         BE    NADQ45              YES                                          
         B     NADQ15              NO, READ ANOTHER RECD                        
         CLI   DUB+2,X'00'         SINGLE DAY READS, SKIP M-F & M-S             
         BE    NADQ15                                                           
         CLI   DUB+2,X'80'                                                      
         BE    NADQ15                                                           
         B     NADQ45              ACCEPT INDIV DAY                             
*                                                                               
NADQ35   CLI   DBBEST,C'L'         EXACT MATCH?                                 
         BNE   *+14                                                             
         CLC   WORK(1),DBSELDAY                                                 
         BNE   NADQ15              REJECT RECD                                  
         ZIC   R1,WORK             MATCH BITS                                   
         EX    R1,*+8                                                           
         B     *+8                                                              
         TM    DBSELDAY,0          EXEC TM                                      
         BZ    NADQ15                                                           
*                                                                               
         CLI   DBBTYPE,X'00'       NOT APPLICABLE TO DIARY                      
         BE    NADQ45                                                           
         CLC   PMBOOK,=X'5721'     OPTIONAL DATA STARTS 87/33                   
         BL    NADQ45                                                           
*                                                                               
*** IGNORE P+S2 SETTING FROM DCON RECORD                                        
*** IT IS NO LONGER INFORCED FOR NATIONAL (PER JACK LUSHER FEB09)               
*        CLI   DBSATLIT,C'Y'       INDIVIDUAL DAYS AUTHORIZED                   
*        BNE   NADQ40              NO - BYPASS OPTION                           
*                                                                               
         CLI   DBDAYOPT,C'A'       ALL AVG + DAYS                               
         BE    NADQ45              DON'T DO FILTERING                           
         CLI   DBDAYOPT,C'I'       INDIVIDUAL DAYS ONLY                         
         BNE   NADQ40              NO - DO POCKETPIECE LINES                    
         CLI   DUB+2,X'10'         M-F                                          
         BL    NADQ15              SKIP IT                                      
         CLI   DUB+2,X'80'         M-S OR VAR                                   
         BNL   NADQ15                                                           
         B     NADQ45              INDIV. DAY - USE IT                          
         SPACE 1                                                                
NADQ40   CLI   DUB+2,X'00'         M-F                                          
         BE    *+12                                                             
         CLI   DUB+2,X'80'         M-S OR VAR                                   
         BL    *+14                                                             
         MVC   WORK+4(2),DBACTPRG  SAVE PROGRAM NUMBER                          
         B     NADQ45                                                           
         CLI   DUB+2,X'70'         TEST INDIV. DAYS (MON THRU SUN)              
         BH    *+14                (BOGUS TEST)                                 
         CLC   WORK+4(2),DBACTPRG                                               
         BE    NADQ15              BYPASS INDIV. DAY                            
         XC    WORK+4(2),WORK+4                                                 
*                                                                               
NADQ45   MVC   DBACTDAY,DUB+2      RETURN DAY CODE FOR DEFINE                   
         ZIC   R1,DUB+3                                                         
         STH   R1,DBFACTOR                                                      
         AH    R1,DBDIVSOR                                                      
         STH   R1,DBDIVSOR                                                      
         MVI   DBMODE,DBMFRST                                                   
         MVI   DBRECTYP,DBRECNTI                                                
         MVI   DBMODE,DBMNEXT      SEQUENTIAL MODE                              
         MVI   DBLSTNXT,1          DBMNEXT=1 SET FOR RE-ENTRY(NO HOOK)          
         MVC   WORKSV+10(2),DBINTFIL   SAVE BEFORE HOOK                         
         BAS   RE,GETX             OUT TO USER FOR ALL PRGMS                    
         MVC   DBINTFIL(2),WORKSV+10  SAVE BEFORE HOOK                          
*        BAS   RE,GETX             HOOK TO USER FOR ALL PROGRAMS                
         OC    DBSELPRG,DBSELPRG   TEST FOR PROGRAM NUMBER READ                 
         BNZ   NADQ20              READ 'B' & 'S' RECDS FOR PRGM                
         B     NADQ10              READ SEQ NEXT RECD                           
         EJECT                                                                  
* ********************************************************************          
* GETNTID - GET VALUES FROM AN NETWORK PROGRAM RECORD (-Q-).                    
* ON EXIT DUB+0(1)=START QTR HOUR OF THIS RECORD                                
*         DUB+2(1)=DAY CODE FOR RECORD                                          
*         DUB+3(1)=DURATION IN QTR HOURS                                        
*         DUB+6(1)=END QTR HOUR OF RECORD                                       
* ********************************************************************          
GETNTID  L     RF,DBAREC                                                        
         MVC   DBACTPRG,PMPNUM-PMKEY(RF)                                        
*        MVC   WORK+18(1),PMSTYP-PMKEY(RF)                                      
         ICM   RF,15,DBAQUART                                                   
         BNZ   *+16                                                             
         L     RF,DBAREC                                                        
         AH    RF,DBDTADSP                                                      
         ST    RF,DBAQUART         <-----INITIALIZE                             
         MVI   DUB+3,1             <-----INITIALIZE                             
         SR    R0,R0                                                            
         USING PHTELEM,RF                                                       
*                                                                               
         OC    DBCABNUM,DBCABNUM   CHECK FOR CABLE                              
         BZ    GETNTID2                                                         
         ST    RF,DUB+4                                                         
GETCID2  CLI   0(RF),0             FIX SCREWED UP 5E ELEMENTS                   
         BE    GETCIDX                                                          
         CLI   0(RF),X'5E'         FIND THE SOURCE/BOOK ELEMENT                 
         BE    GETCID4                                                          
         IC    R0,1(RF)                                                         
         AR    RF,R0                                                            
         B     GETCID2                                                          
GETCID4  CLC   2(3,RF),=C'CNN'     MAKE SURE ITS CABLE                          
         BNE   GETCIDX                                                          
         MVC   5(2,RF),DBSELBK     SET TO INPUT BOOK                            
GETCIDX  L     RF,DUB+4                                                         
*                                                                               
GETNTID2 CLI   0(RF),0             FIND 2 ELEMENTS                              
         BNE   *+6                                                              
         DC    H'0'                                                             
         USING PHELEM,RF                                                        
         CLI   PHCODE,PHCODEQ      FIND QTR HOUR ELEMENT (X'20')                
         BNE   GETNTID4                                                         
         ST    RF,DBAQUART                                                      
         MVC   DUB+3(1),PHDUR      QUARTER HOUR DURATION                        
         B     GETNTID5                                                         
         USING NTELEM,RF                                                        
GETNTID4 CLI   NTCODE,NTCODEQU     FIND RUN TIME ELEMENT (X'22')                
         BE    GETNTID6                                                         
GETNTID5 IC    R0,1(RF)                                                         
         AR    RF,R0                                                            
         B     GETNTID2                                                         
*                                                                               
GETNTID6 MVC   DUB(1),NTSQH                                                     
         MVC   DUB+6(1),NTEQH                                                   
         SR    R1,R1                                                            
         CLI   PMSTAT+4,X'88'      LOWER CASE 'H'= WEIGHTED HISP                
         BE    *+8                                                              
         CLI   PMSTAT+4,C'H'       REGULAR HISP                                 
         BNE   GETNTD6A                                                         
         IC    R1,DUB+6                                                         
         BCTR  R1,0                                                             
         STC   R1,DUB+6                                                         
GETNTD6A MVI   DUB+2,X'90'         SET DAY CODE TO VAR                          
         TM    NTDAY,X'80'         TEST FOR INVALID DATE IN ELEM                
         BO    GETNTID8                                                         
         L     R1,ADQPNNS          FIND DAY CODE FOR BIT SETTING                
         LA    R1,DQHDRLN(R1)                                                   
         USING DQDTAD,R1                                                        
*                                                                               
GETNTID7 CLI   DQDAY,0             TEST FOR END OF TABLE                        
         BE    GETNTID8                                                         
         CLC   NTDAY,DQDAY         TEST FOR MATCH AGAINST TABLE                 
         BE    *+12                                                             
         LA    R1,DQDTALN(R1)                                                   
         B     GETNTID7                                                         
         MVC   DUB+2(1),DQKDAY     EXTRACT DAY CODE FOR BIT SETTING             
*                                                                               
GETNTID8 BR    RE                                                               
         DROP  R1,R2,RF                                                         
         EJECT                                                                  
***********************************************************************         
*TSTDYL- TEST ACCESS TO DAILY DATA (DCON SECURITY)                              
*        IF GRANTED, CONDITION CODE NONZERO                                     
***********************************************************************         
         SPACE 1                                                                
TSTDYL   NTR1  ,                                                                
         OC    ALET,ALET           SSBTBLET RESOLVED?                           
         BNZ   TSTDYL02            YES                                          
         XC    DUB,DUB             SPECIAL CALL FOR SYSFACS                     
         MVC   DUB(4),=XL4'FEFFFFFF'                                            
         OC    CSWITCH,CSWITCH                                                  
         BNZ   TSTDYL01                                                         
         ICM   RF,15,CMASTC                                                     
         ICM   RF,15,MCSSB-MASTD(RF)       RF=ASSB                              
         MVC   ALET,SSBTBLET-SSBD(RF)                                           
         B     TSTDYL02                                                         
*                                                                               
TSTDYL01 L     RF,CSWITCH                                                       
         GOTO1 (RF),DUB                                                         
         L     RF,0(R1)            RF=V(SYSFACS)                                
         L     RF,VSSB-SYSFACD(RF) RF=V(SSB)                                    
         MVC   ALET,SSBTBLET-SSBD(RF)                                           
*                                                                               
TSTDYL02 L     R2,ACONHDR          R2=A(CONTROL TABLE)                          
         USING CONHDRD,R2                                                       
         LAM   AR2,AR2,ALET                                                     
         LA    R3,CONHDRD+CONHDRLN                                              
         CPYA  AR3,AR2                                                          
         SAC   512                                                              
         USING CONDTAD,R3                                                       
         MVI   DEMOFLAG,0                                                       
         LA    R4,3                                                             
         XR    R5,R5                                                            
         ICM   R5,7,CONAET                                                      
         CR    R3,R5               NO LISTS?? --JUST DISALLOW                   
         BE    TSTDYLX                                                          
*                                                                               
TSTDYL04 CLI   CONINDIC,0          PROCESS VALID MARKET LIST                    
         BNE   TSTDYLX             NOT VALID                                    
         OI    DEMOFLAG,X'80'                                                   
         CLC   CONMRKT,=H'221'     DAILY MKT VALID?                             
         BE    *+10                                                             
         CLC   CONMRKT,=H'222'     DAILY MKT VALID?                             
         BNE   *+12                                                             
         OI    DEMOFLAG,X'40'                                                   
         B     *+8                                                              
         BXLE  R3,R4,TSTDYL04                                                   
*                                                                               
TSTDYLX  LAM   AR2,AR3,=2F'0'                                                   
         SAC   0                                                                
         CLI   DEMOFLAG,X'C0'      TEST MARKET NUMBERS VALID                    
         B     XIT                                                              
         EJECT                                                                  
*********************************************************************           
*GETX -  EXIT TO CALLER AND RETURN OR EXIT FOR GOOD IF EOF OR ERROR             
*********************************************************************           
GETX     CLI   DBERROR,0           TEST IF ERROR SET                            
         BE    *+14                                                             
         XC    DBAQUART,DBAQUART   YES - CLEAR QTR HOUR ADDRESS                 
         MVI   DBMODE,DBMFRST      AND RESET SEQUENTIAL MODE                    
         MVC   DBLSTFNC,DBFUNCT    EQUATE LAST FUNCTION TO THIS                 
         OC    AUSERRTN,AUSERRTN   TEST IF USER HOOK SUPPLIED                   
         BZ    GETXX               NO - RETURN TO CALLER                        
         CLI   DBERROR,0           TEST IF ERROR SET                            
         BNE   GETXX               YES - RETURN TO CALLER                       
         CLI   DBMODE,DBMNEXT      TEST IF SEQUENTIAL MODE                      
         BE    *+8                                                              
         LA    RE,GETXX            NO - SET RETURN TO EXIT                      
*                                  SAVE REGS & GO TO USER HOOK                  
         NTR1                                                                   
         L     RF,AUSERRTN                                                      
         L     RE,ROOTRD                                                        
         LR    R0,RE               R0=A(CALLER'S RD)                            
         LM    R1,RC,24(RE)        R1-RC=CALLERS REGISTERS                      
         BASR  RE,RF               GO TO USER HOOK                              
         XIT1                                                                   
*                                                                               
GETXX    DS    0H                                                               
         CLI   SPNETFLG,0          SPOT/NET RQST USES STD LK UP                 
         BNE   GETXXX              AND DOESNT USE ADTLIST                       
         ICM   RE,15,ADTLIST       CHECK FOR DAY/TIME LIST                      
*        BZ    GETXXX                                                           
         BNZ   *+12                                                             
         ICM   RE,15,AODTLIST      OR OPTIMIZING DAY/TIME LIST                  
         BZ    GETXXX                                                           
         USING DBXTLD,RE                                                        
         ZIC   R1,DBXTLIDX         BUMP THE INDEX                               
         LA    R1,1(R1)                                                         
         STC   R1,DBXTLIDX                                                      
         MH    R1,=H'5'                                                         
         AR    R1,RE                                                            
         CLI   DBXTLIST-DBXTLID(R1),0                                           
         BE    GETXXX                                                           
*                                  MORE DAYS AND TIMES TO PROCESS               
         CLI   DBERROR,0                                                        
         BE    GETXX5                                                           
         TM    IOFLAG,DIR          DIRECTORY READ ERROR (KEY NOT FOUND)         
         BNO   GETXX5                                                           
*                                                                               
         CLI   DBSELSTA+4,C'C'     FOR CABLE NAD,                               
         BNE   *+8                 NO DIR ENTRY MEANS NO STATION TYPE           
         CLI   DBBTYPE,C'N'        IT'S OK BECAUSE STYP IS USED FOR             
         BNE   *+14                RECORD SPANNING                              
         MVC   DBSTYPE,SVDBSTYP    GO READ DIRECTORY AGAIN STARTING             
         B     GETNETB             WITH INITIAL STYP                            
*                                                                               
         OC    AODTLIST,AODTLIST   MAYBE SHOULD ALWAYS DO THIS                  
         BNZ   GETNETB             READ DIR AGAIN INSTEAD OF FILE SEQ           
*                                                                               
****     CLI   DBBTYPE,C'V'        REMOVED 9/16/04. EXIT FOR ALL                
         BE    GETXXX              EXIT FOR BTYPE 'V'(TCAR)                     
*                                                                               
GETXX5   MVI   DBERROR,0           RESET ERROR                                  
         MVI   DBLSTNXT,0          RESET FIRST TIME                             
*                                                                               
GETXX10  CLC   DBFILE,=C'NAD'                                                   
         BE    NADQ10                                                           
         B     GETNETC                                                          
         DROP  RE                                                               
         SPACE 2                                                                
XIT      DS    0H                                                               
GETXXX   XIT1                      EXIT TO CALLER OR DEMAND                     
         EJECT                                                                  
**********************************************************************          
*GETODYT - LOOK FOR EXTEND 'ODYT'                                               
*          USED FOR OPTIMIZING DAY/TIME FILTERING                               
*          EXTEND IS SET UP IN THE RESEARCH WRITER (NEPOD01)                    
**********************************************************************          
GETODYT  NTR1                                                                   
         ICM   RE,15,DBEXTEND                                                   
GETODT5  BZ    GETODTX            GO THROUGH LIST STARTING AT DBEXTEND          
         CLC   =C'ODYT',0(RE)     AND LOOK FOR ID                               
         BE    GOTODT                                                           
         ICM   RE,15,4(RE)                                                      
         B     GETODT5                                                          
*                                                                               
GOTODT   ST    RE,AODTLIST        EXTENSION FOUND, PASS BACK ADDRESS            
         USING DBXTLD,RE                                                        
         MVI   DBXTLIDX,0         START WITH FIRST ENTRY                        
*                                                                               
GETODTX  B     XIT                                                              
         EJECT                                                                  
**********************************************************************          
*GETODQ - SET DBSELDAY/DBSELTIM USING 'ODYT' TABLE                              
*         CALLS GETDQ(FROM DEGET), EXCEPT IT USES 'ODYT', NOT 'DYTM'            
**********************************************************************          
GETODQ   NTR1                                                                   
         MVC   DBACTSRC,SVDBASRC   RESTORE VALUES CHANGED IN THE HOOK           
         MVC   DBINTMED,SVDBIMED   THEY ARE USED IN GETDQ                       
         MVC   DBINTFIL,SVDBIFIL                                                
*                                                                               
         MVC   ADTLIST,AODTLIST    FAKE IT. SEND 'ODYT' TABLE INSTEAD           
*                                                                               
         GOTO1 AGETDQ      --->    SET DAY/TIME FIELDS (DBSELDAY/TIM)           
*                                                                               
         XC    ADTLIST,ADTLIST                                                  
*                                                                               
         B     XIT                                                              
         EJECT                                                                  
***********************************************************************         
*DELSPDM- REMOVE OPTIONAL SPANISH DOMINANAT DATA FROM RECORD                    
*         NAMELY: 51/52 PRG PUT ELEMS FROM 23/03/05 MKT BRK                     
***********************************************************************         
DELSPDM  NTR1                                                                   
         SR    R0,R0                                                            
         L     R2,DBAREC                                                        
         USING PRKEY,R2                                                         
         CLI   0(R2),C'P'          P-RECD?                                      
         BNE   DELSPD5                                                          
         CLI   PRSTAT+4,X'88'      LOWER CASE 'H'= WEIGHTED HISP                
         BE    *+8                                                              
         CLI   PRSTAT+4,C'H'       REGULAR HISP                                 
         BNE   DELSPDX                                                          
         CLC   PRSTAT(3),=C'TEL'                                                
         BE    *+10                                                             
         CLC   PRSTAT(3),=C'TF '                                                
         BE    *+14                                                             
         CLC   PRSTAT(3),=C'UNI'                                                
         BNE   DELSPDX                                                          
         LA    R2,PRFRSTEL                                                      
         B     DELSPD10                                                         
         DROP  R2                                                               
*                                                                               
DELSPD5  CLI   0(R2),C'Q'          Q-RECD?                                      
         BNE   DELSPDX                                                          
         USING PMKEY,R2                                                         
         CLI   PMSTAT+4,X'88'      LOWER CASE 'H'= WEIGHTED HISP                
         BE    *+8                                                              
         CLI   PMSTAT+4,C'H'       REGULAR HISP                                 
         BNE   DELSPDX             NOT HISP RECORD                              
         LA    R2,PMDATA                                                        
         DROP  R2                                                               
*                                                                               
DELSPD10 CLI   0(R2),0             EOF                                          
         BE    DELSPD50                                                         
         CLI   0(R2),X'23'                                                      
         BNE   *+8                                                              
         MVI   DUB,0                                                            
         CLC   0(3,R2),=X'230305'                                               
         BNE   *+8                                                              
         MVI   DUB,1               WITHIN SPDM MKT BRK                          
         CLI   DBKEY,C'Q'          IF Q-RECD, ONLY DELETE PUTS                  
         BE    DELSPD25                                                         
         CLI   DBKEY,C'P'          IF P-RECD, DELETE ENTIRE BRK                 
         BE    DELSPD30              NOT A P-RECD -- DON'T MESS W/IT            
         B     DELSPD40                                                         
*                                                                               
DELSPD25 CLI   0(R2),X'51'         ONLY DELETE PUTS                             
         BE    DELSPD30                                                         
         CLI   0(R2),X'52'                                                      
         BE    DELSPD30                                                         
         B     DELSPD40                                                         
*                                                                               
DELSPD30 CLI   DUB,1                                                            
         BNE   *+8                                                              
         MVI   0(R2),X'FF'         MARK ELEM FOR DELETE                         
*                                                                               
DELSPD40 IC    R0,1(R2)                                                         
         AR    R2,R0                                                            
         B     DELSPD10                                                         
*                                                                               
DELSPD50 DS    0H                  REMOVE X'FF' ELEMS FROM RECD                 
         GOTO1 CHELLO,DMCB,(C'D',=C'PAVFIL  '),(X'FF',DBAREC),0,0               
*                                                                               
DELSPDX  B     XIT                                                              
*                                                                               
**********************************************************************          
*GETMB  - GET MARKET BREAK SECTION LEAD ELEM AS REQSTD IN DBBTYPE               
**********************************************************************          
GETMB    NTR1                                                                   
         L     R2,DBAREC                                                        
         USING PRKEY,R2                                                         
         MVC   FILKEY,0(R2)        SAVE ORIGINAL KEY                            
         XC    DBAQUART,DBAQUART                                                
GETMB2   LA    RE,NHTBRKS          TRANSLATE BKTYPE TO INTERNAL CODE            
         MVC   FILMB,DBBTYPE                                                    
*        CLI   DBBTYPE,0           HUT STATION TOT SAMPLE=X'01'                 
*        BNE   *+10                                                             
*        CLC   PRSTAT,=C'HUT H'                                                 
*        BNE   *+12                                                             
*        MVI   FILMB,X'01'                                                      
*        B     GETMB5                                                           
         TM    SPNETFLG,SPNHTQ                                                  
         BNO   GETMB5                                                           
GETMB3   CLI   0(RE),X'FF'         EOT                                          
         BE    GETMB5               -USE DDBTYPE STRAIGHT                       
         CLC   DBBTYPE,0(RE)                                                    
         BE    *+12                                                             
         LA    RE,L'NHTBRKS(RE)                                                 
         B     GETMB3                                                           
         MVC   FILMB,1(RE)                                                      
*                                                                               
GETMB5   L     R2,DBAREC                                                        
         LA    R1,PRFRSTEL                                                      
         MVI   INBRK,C'N'                                                       
         SR    RE,RE                                                            
*                                                                               
GETMB10  CLI   0(R1),0             LOOP THRU LKING FOR X'23'                    
         BE    GETMB40             TO COMPARE ON MKT BREAK                      
         CLI   0(R1),X'23'         GET SECTION LEAD ELEM                        
         BNE   GETMB20                                                          
         CLC   FILMB,2(R1)         MATCH MKT BREAK ON SECTN LEAD                
         BE    GETMB18                                                          
         CLI   FILMB,0             0 & 1 ARE INTERCHANGABLE                     
         BNE   GETMB20                                                          
         CLI   2(R1),1                                                          
         BNE   GETMB20                                                          
*                                                                               
GETMB18  ST    R1,DBAQUART         SAVE START OF MKT BRK ELEMS                  
         MVI   INBRK,C'Y'                                                       
         B     GETMB20                                                          
*                                                                               
GETMB20  DS    0H                                                               
         CLI   INBRK,C'Y'          ARE WE WITHIN MKT BRK SECTION?               
         BNE   GETMB25                                                          
         CLI   0(R1),X'5E'         END OF GOOD SECTION                          
         BNE   GETMB30                                                          
         MVI   INBRK,C'N'                                                       
         B     GETMB30             DON'T MARK FOR DELETION                      
*                                                                               
GETMB25  CLI   0(R1),X'23'                                                      
         BL    *+8                 DON'T DELETE THE NAME/TIME ELEMS             
         MVI   0(R1),X'FF'         MARK OTHER MKT BRK ELEMS FOR DEL             
*                                                                               
GETMB30  IC    RE,1(R1)            BUMP TO NEXT ELEM IN RECD                    
         AR    R1,RE                                                            
         B     GETMB10                                                          
*                                                                               
GETMB40  CLC   DBAQUART,BZEROS     COMPARE TO BINARY ZEROS                      
         BNE   GETMB50                                                          
         LA    R2,DBKEY                                                         
         ZIC   R1,PRBTYP           BUMP BOOKTYPE FIELD IN KEY                   
         LA    R1,1(R1)                                                         
         STC   R1,PRBTYP                                                        
         XC    PRKSTAT(5),PRKSTAT                                               
         MVI   RSTMB,C'Y'          WILL NEED TO RESET FILE PTR LATER            
         MVI   IOFLAG,DIR+READ     READ/DIRECTORY                               
         GOTO1 AIO,IOFLAG                                                       
         BL    GETX                                                             
         BNE   GETMBNX             MKT BRK NOT AVAILABLE/NOT FOUND              
         MVI   IOFLAG,FILE+READ    READ/FILE                                    
         GOTO1 AIO,IOFLAG                                                       
         BE    GETMB2                                                           
         BNE   GETMBNX             MKT BRK NOT AVAILABLE/NOT FOUND              
*                                                                               
GETMB50  DS    0H                  DEL X'FF' ELEMS FROM RECD                    
         GOTO1 CHELLO,DMCB,(C'D',=C'PAVFIL  '),(X'FF',DBAREC),0,0               
         CLI   DMCB+12,0                                                        
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         ICM   R2,15,DBSPANAD      FIND MKT BRK UNIVS AND PUT ON RECD           
         LA    R2,4(R2)            BUMP PASS LABEL                              
         USING PRKEY,R2                                                         
         LA    R2,PRFRSTEL                                                      
         SR    RF,RF                                                            
         MVI   INBRK,C'N'                                                       
         CLI   FILMB,0                                                          
         BNE   *+8                                                              
         MVI   FILMB,X'01'         UNIV RECD TOT-SMPL IS X'01'                  
GETMB60  CLI   0(R2),0                                                          
         BE    GETMB80             NO UNIVS FOUND                               
         CLI   0(R2),X'23'                                                      
         BNE   GETMB62                                                          
         MVI   INBRK,C'N'          RE-SET DEFAULT                               
         CLC   FILMB,2(R2)         GET CORRECT MKT BREAK UNIVS                  
         BL    GETMB80             IT' NOT GOING TO BE THERE--EXIT              
         BH    GETMB65             KEEP LKG                                     
         MVI   INBRK,C'Y'          WE ARE W/IN THE CORRECT MKT BRK              
         B     GETMB65             BUMP TO NEXT ELEM                            
*                                                                               
GETMB62  CLI   INBRK,C'Y'          ARE WE W/IN THE BRK?                         
         BNE   GETMB65                                                          
         CLI   0(R2),X'4B'         YES: XFER 4B & 4C ELEMS ONLY                 
         BE    *+12                                                             
         CLI   0(R2),X'4C'                                                      
         BNE   GETMB65                                                          
         GOTO1 CHELLO,DMCB,(C'P',=C'PAVFIL  '),DBAREC,(R2),0                    
         CLI   0(R2),X'4C'                                                      
         BE    GETMB80             DONE XFERING                                 
         SR    RF,RF                                                            
*                                                                               
GETMB65  IC    RF,1(R2)            LK FOR MKT BRK MATCH OR 4B/4C                
         AR    R2,RF                                                            
         B     GETMB60                                                          
         DROP  R2                                                               
*                                                                               
GETMB80  L     R2,DBAREC                                                        
         USING PRKEY,R2                                                         
         LA    R1,PRFRSTEL                                                      
         SR    RE,RE                                                            
GETMB85  CLI   0(R1),0                                                          
         BE    GETMBNX                                                          
         CLI   0(R1),X'20'                                                      
         BE    *+14                                                             
         IC    RE,1(R1)                                                         
         AR    R1,RE                                                            
         B     GETMB85                                                          
         ST    R1,DBAQUART                                                      
         B     GETMBKX                                                          
*                                                                               
GETMBNX  DS    0H                  MKT BREAK RECD NOT FOUND                     
         MVI   RSTMB,C'F'          FUDGE A RECD W/NO DEMOS                      
         BAS   RE,RSETMB                                                        
*                                                                               
GETMBKX  OC    DBAQUART,DBAQUART   TEST IF A RECD WAS FOUND                     
         B     XIT                                                              
         EJECT                                                                  
**********************************************************************          
*RSETMB - RESET POINTER TO '00' MKT BREAK WHERE WE WERE BEFORE GETMBK           
**********************************************************************          
RSETMB   NTR1                                                                   
         MVC   DBKEY,FILKEY        RESET DIR KEY                                
         LA    R2,DBKEY                                                         
         XC    PRKSTAT(5),PRKSTAT                                               
         MVI   IOFLAG,DIR+READ     READ/DIRECTORY                               
         GOTO1 AIO,IOFLAG                                                       
         BL    GETX                                                             
         BE    *+6                                                              
         DC    H'0'                                                             
         MVC   DBMINKEY(2),FILKEY+18                                            
         MVI   IOFLAG,FILE+READ    READ/FILE                                    
         GOTO1 AIO,IOFLAG                                                       
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         L     R2,DBAREC           FUDGE RECD W/JUST PRG INFO IN IT             
         CLI   RSTMB,C'F'          MKTBRK NOT FOUND, FUDGE RECD?                
         BNE   RSETMBX             NO                                           
         LA    R1,PRFRSTEL                                                      
         SR    RE,RE                                                            
         SR    RF,RF                                                            
*                                                                               
RSET20   CLI   0(R1),0             DELETE DEMO ELEMS SAVE PRG ELEMS             
         BE    RSET35                                                           
         CLI   0(R1),X'20'                                                      
         BNE   *+8                                                              
         ST    R1,DBAQUART                                                      
         CLI   0(R1),X'23'         DELETE LEAD ELEM TOO                         
         BE    RSET25                                                           
         CLI   0(R1),X'5E'         END OF SECTION --> EXIT                      
         BNE   RSET22                                                           
         LTR   RF,RF               DID WE GET ONE 5E ALREADY?                   
         BNZ   RSET25              YES, DELETE THE REST                         
         LA    RF,1                                                             
         B     RSET30              SAVE THIS ONE                                
RSET22   CLI   0(R1),X'30'                                                      
         BL    RSET30              SAVE HEADER ELEMS                            
RSET25   MVI   0(R1),X'FF'         DELETE ELEM                                  
*                                                                               
RSET30   IC    RE,1(R1)            BUMP TO NEXT ELEM IN RECD                    
         AR    R1,RE                                                            
         B     RSET20                                                           
*                                                                               
RSET35   GOTO1 CHELLO,DMCB,(C'D',=C'PAVFIL  '),(X'FF',DBAREC),0,0               
*                                                                               
         ICM   R2,15,DBSPANAD      PUT UNIV ON RECD                             
         LA    R2,4(R2)            BUMP PASS LABEL                              
         USING PRKEY,R2                                                         
         LA    R2,PRFRSTEL                                                      
         SR    RF,RF                                                            
         MVI   INBRK,C'N'                                                       
         CLI   FILMB,0                                                          
         BNE   *+8                                                              
         MVI   FILMB,X'01'         UNIV RECD TOT-SMPL IS X'01'                  
RSET60   CLI   0(R2),0                                                          
         BE    RSETMBX             NO UNIVS FOUND                               
         CLI   0(R2),X'23'                                                      
         BNE   RSET62                                                           
         MVI   INBRK,C'N'          RE-SET DEFAULT                               
         CLC   FILMB,2(R2)         GET CORRECT MKT BREAK UNIVS                  
         BL    RSETMBX             IT' NOT GOING TO BE THERE--EXIT              
         BH    RSET65              KEEP LKG                                     
         MVI   INBRK,C'Y'          WE ARE W/IN THE CORRECT MKT BRK              
         B     RSET65              BUMP TO NEXT ELEM                            
*                                                                               
RSET62   CLI   INBRK,C'Y'          ARE WE W/IN THE BRK?                         
         BNE   RSET65                                                           
         CLI   0(R2),X'4B'         YES: XFER 4B & 4C ELEMS ONLY                 
         BE    *+12                                                             
         CLI   0(R2),X'4C'                                                      
         BNE   RSET65                                                           
         GOTO1 CHELLO,DMCB,(C'P',=C'PAVFIL  '),DBAREC,(R2),0                    
         CLI   0(R2),X'4C'                                                      
         BE    RSETMBX             DONE XFERING                                 
         SR    RF,RF                                                            
*                                                                               
RSET65   IC    RF,1(R2)            LK FOR MKT BRK MATCH OR 4B/4C                
         AR    R2,RF                                                            
         B     RSET60                                                           
*                                                                               
RSETMBX  MVI   RSTMB,0                                                          
         B     XIT                                                              
         EJECT                                                                  
**********************************************************************          
*SHIFTWK - SHIFT TO FIRST WEEK OF THE MONTH                                     
**********************************************************************          
SHIFTWK  NTR1                                                                   
         USING PRKEY,R2                                                         
*                                                                               
         ICM   RF,15,DBCOMFCS                                                   
         ICM   RF,15,CDEMTABS-COMFACSD(RF)                                      
         GOTO1 (RF),DMCB,NENACWKS  GET NIELSEN MONTH<->WEEK TABLE               
         ICM   RE,15,DMCB          A(TABLE) RETURNED IN P1                      
         BNZ   *+6                 TEMP                                         
         DC    H'0'                BAD TABLEID PASSED                           
*                                                                               
         USING NEWKSD,RE            MONTH TO WEEK TABLE                         
SHIFTW1  CLC   PRBOOK(1),NEWKSYR    COMPARE ON YEAR                             
         BNE   SHIFTW10                                                         
         CLC   PRBOOK+1(1),NEWKSLST COMPARE ON END WEEK                         
         BH    SHIFTW10                                                         
         CLC   PRBOOK+1(1),NEWKSFRS COMPARE ON START WEEK                       
         BL    SHIFTW10                                                         
         MVC   PRBOOK+1(1),NEWKSFRS MOVE IN FIRST WEEK OF THE MONTH             
         B     SHIFTWX                                                          
*                                                                               
SHIFTW10 LA    RE,NEWKSQ(RE)        NEXT ENTRY                                  
         CLI   0(RE),X'FF'                                                      
         BNE   SHIFTW1                                                          
*                                                                               
SHIFTWX  B     XIT                                                              
         DROP  R2                                                               
         DROP  RE                                                               
         EJECT                                                                  
**********************************************************************          
*LTORG AND CONSTANTS AND DSECTS ETC                                             
**********************************************************************          
         LTORG                                                                  
*                                                                               
NHTBRKS  DS    0CL2         WHEN SPTNHT ACTIVE, ALLOW CHAR BKTYPES              
         DC    C'1',X'01'          CONVERT CHAR TO NUMERIC                      
         DC    C'5',X'05'                                                       
         DC    C'6',X'06'                                                       
         DC    C'7',X'07'                                                       
         DC    C'H',X'05'          SET UP LETTER EQUATES                        
         DC    C'E',X'06'                                                       
         DC    C'B',X'07'                                                       
         DC    X'FFFFF'                                                         
*                                                                               
         DS    0F                                                               
BZEROS   DC    XL20'00'                                                         
*                                                                               
MYWRK    DSECT                     MY OWN WORK AREA                             
SPANAD   DS    CL2000              STRG FOR DBSPAN FOR UNIVS                    
FILKEY   DS    XL23                DBKEY BEFORE WE GO LKG AROUND                
INBRK    DS    C                   FLAG=N IF NOT IN DESIRED MKT BRK             
RSTMB    DS    C                   RESET PTR TO X'00' MKT BRK                   
FILMB    DS    X                   INTERNAL MKT BREAK VALUE ON FILE             
SVDBDQ   DS    CL5                 RESTORE OLD DBDQ FOR PROCESSING              
DAYWGT   DS    X                   DAY WEIGHT                                   
DAYCODE  DS    X                   DAY BITS                                     
SVACTBK  DS    XL2                 SAVE ORIG DBLOCK BEFORE USERHOOK             
SVACTSTA DS    CL5                                                              
SVDBASRC DS    C                   DBACTSRC                                     
SVDBIMED DS    C                   DBINTMED                                     
SVDBIFIL DS    C                   DBINTFIL                                     
SVDBSTYP DS    X                   DBSTYPE                                      
AODTLIST DS    A                   A('ODYT' DBEXTEND BLOCK)                     
EXTNDLP  DS    X                   LOOPING THRU DBEXTENDS                       
EXTFLG   DS    X                   DBEXTEND BLOCKS SET                          
EXTDYTQ  EQU   X'01'                 DYTM SET                                   
EXTMBKQ  EQU   X'02'                 MBKS SET                                   
EXTMSTQ  EQU   X'04'                 MSTA SET                                   
*BACKDUP  DS    C                                                               
FRSTHIGH DS    C                                                                
MYWRKX   EQU   2080-(*-MYWRK)                                                   
*                                                                               
         EJECT                                                                  
       ++INCLUDE DEGETD                                                         
          PRINT OFF                                                             
       ++INCLUDE FASSB                                                          
       ++INCLUDE FASYSFAC                                                       
       ++INCLUDE DEDBEXTRAD                                                     
       ++INCLUDE DDMASTD                                                        
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'055DEGETNET  09/14/11'                                      
         END                                                                    
                                                                                
