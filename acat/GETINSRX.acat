*          DATA SET GETINSRX   AT LEVEL 006 AS OF 05/01/02                      
*CATALP GETINS                                                                  
*                                                                               
*  RELOCATABLE VERSION OF GETINS - CORERESIDENT SOURCE IS GETINS                
*  NOTE - ANY CHANGE IN THIS PHASE SHOULD ALSO BE MADE IN GETINS                
*         UNTIL ALL PROGRAMS USE CORERESIDENT VERSION OF GETINS                 
*                                                                               
           TITLE 'LOG OF PGM CHANGES'                                           
*                                                                               
* BPLA 2/97     CHANGES FOR HST                                                 
*                                                                               
* BOBY JAN94    ADD PST CALCULATIONS                                            
* BPLA 7/9/92   REDO COMMISSION BILLING OPTIONS TO INCLUDE NET                  
*               PARS+4 = 'C' OR 'N'                                             
*               ALSO SPECIAL HANDLING OF UFC COMM AND NET                       
*               BILLING ELEMENTS (UNLESS PARS+4 = 'C' OR 'N')                   
*                                                                               
* BPLA 4/29/92  OPTION TO RETURN COMMISSION BILLING                             
*               'C' IN BYTE 0 PARAMETER 2                                       
* BPLA 4/23/91 IN LEFTCOMP DON'T GIVE THE PRODUCT A PENNY REMAINDER             
*              IF SHARE IS ZERO                                                 
* ROSA 2/7/91 CLEAR ALL GST FIELDS IF A COMMISSION BUY                          
*                                                                               
*                                                                               
* BPLA 2/5/91  BUG FIXED IN GETX                                                
*              IMPROPER ALLOCATION OF GSTTAXPD                                  
*                                                                               
* BPLA 1/22/91 ADD GST BILLED CALCULATION                                       
*              ALSO USE GVALUES DSECT TO COVER RETURNED                         
*              GST INFO. *NOTE THAT BILLED GST IS "PAYABLE GST"                 
*                                                                               
* BPLA/ROSA 11/16/90-1/10/91 CHANGES FOR GST                                    
*      IF PARAMETER 4 POINTS TO CONSTANT                                        
*      'BOTH' PASS COUNT AND GST DATA ----                                      
*      'COUNT' PASS ONLY PAY/BILLED COUNTS ONLY                                 
*      'GST'   PASS ONLY GST INFO      ---                                      
*      IN ALL 3 CASES REPLACE THE ADDRESS OF THE FOURTH PARAMETER               
*       WITH THE ADDRESS OF GSPAYCNT.                                           
*====================================================================           
*  GST TAX CALCUALTED AS (GROSS-A/C)*GST TAX %                    !             
*  TAX (SALES OR PROVINCIAL) CALCULATED AS                        !             
*           (GROSS-A/C+GST TAX)* SALES TAX PCT                    !             
*  TAX (SALES OR PROVINCIAL) IS CALCULATED ON NET IF                            
*             GSTCODE IS 'T'                                                    
*  GROSS DOLLARS INCLUDES SALES TAX // NOT GST \\                 !             
*               ---------------------- -------                    !             
*====================================================================           
*                                                                               
*      PBUYREC NOW HAS PBDCNDA = X'80' IF CANADIAN AGY                          
*      AND PBDGST IS GST CODE FROM PUBREC NAME ELEM                             
*                                                                               
* ROSA 1/12/88  ADD THREE OPTIONS TO BYTE 0 AND ADD NEW PARAMETER               
*      BYTES 12-15.  THIS WORD WILL POINT TO A START AND END DATE               
*      IN BINARY.                                                               
*      IF BYTE 0 IS A C'P' ALL PAID ELEMENTS MUST FALL ON OR BTWN               
*              START AND END DATES                                              
*      IF BYTE 0 C'B' BILLING ELEMENTS MUST PASS SAME TESTS                     
*      IF BYTE O C'T' BOTH BILLING AND PAID MUST PASS TEST                      
*                                                                               
*      NEW VALUE FOR PBDCOSIN - C'C' COMMISSION RATE                            
*                                                                               
*          EVEN THOUGH PBDACP CAN BE SET TO ANYTHING THE USER                   
*          DESIRES, GETINS WILL CALCULATE AGYCOM AS IF IT WERE                  
*          100.0 (TO INSURE ZERO NET ON REPORTS).                               
*          BILL AND PAY ELEMS ALSO REFELECT 100.0 PCT AC.                       
*                                                                               
*          $BUY NOTES - 'C' CAN NOT BE ADDED/REMOVED FROM RATE                  
*                       AFTER BILLING/PAYING                                    
*                       PBDACP CANNOT BE CHANGED AFTER BILL/PAY                 
*                       PBDCD SHOULD BE 0.0                                     
*                       PBDTAX SHOULD ALSO BE 0                                 
*                                                                               
*          IF VALUE TABLE HAS =C'CRAT' GETINS WILL USE PBDACP                   
*          THIS PARAMETER IS ONLY NEEDED FOR THOSE REPORTS                      
*          THAT NEED TO SHOW 'TRUE AC'  - CURRENTLY ONLY THE                    
*          L1 AND THE NEW WRITER AND MAYBE BILLING                              
*                                                                               
*                                                                               
         TITLE 'GETINS - DERIVE ORD/PAID/BILLED BUY DATA'                       
* PARAMETER LIST IS                                                             
*                                                                               
*  PARAMETER 1                                                                  
*  BYTE 0      C'X' IF BILL/PAY DATA NOT REQUIRED                               
*  BYTE 0      C'P' PAID ELEMENTS MUST PASS DATE TEST                           
*  BYTE 0      C'B' BILL ELEMENTS MUST PASS DATE TEST                           
*  BYTE 0      C'T' BOTH PAID AND BILLED MUST PASS DATE TEST                    
*                                                                               
*       1-3    A(BUY RECORD)                                                    
*----------->                                                                   
*  PARAMETER 2                                                                  
*       0      C'O' DO OPEN RATE ORDERED AND BILLING                            
*              (IF OPEN RATE ELEM EXISTS)                                       
*                                                                               
*       THE FOLLOWING 2 OPTIONS REALLY ONLY NEEDED FOR BILLING                  
*                                                                               
*              C'C' NORMAL ORDERED/PAID AMOUNTS BUT UFC COMM BILLING            
*                   (PBLILST X'01') REPORTED AS IT IS STORED                    
*                                                                               
*              C'N' NORMAL ORDERED/PAID AMOUNTS BUT UFC NET BILLING             
*                   (PBLILST X'02') REPORTED AS IT IS STORED                    
*                                                                               
*       1-3    A(VALUE TABLE)                                                   
*              (IF CONTAINS C'CRAT' PROCESS 'C' RATE BUYS USING                 
*              PBDACP INSTEAD OF 100.0 PCT)                                     
*              *NOTE - MUST CALCULATE 'TRUE AC' FROM BILL/PAY                   
*              ELEMENTS, SINCE THEY HAVE GROSS=AC                               
*----------->                                                                   
*  PARAMETER 3                                                                  
*       0      C'Y' OTHER AGENCY OPTION                                         
*       1-3    A(PRD CODE)                                                      
*                                                                               
*----------->                                                                   
*  PARAMETER 4                                                                  
*       1-3    A(START/END DATES)    3 BYTE BINARY                              
*                                                                               
*----------->                                                                   
*  PARAMETER 5                                                                  
*              OPTIONALLY PASS BACK COUNT, GST DATA (ORDERED & PAID)            
*              OR BOTH                                                          
*       1-3    A(=C'COUNT')                                                     
*              ON RETURN THIS PARAMETER IS SET TO ADDRESS OF                    
*              2 FULL WORD BIN COUNTERS  - PAY ELEMS/BILL ELEMS                 
*       1-3    A(=C'GST')                                                       
*              ON RETURN THIS PARAMETER IS SET TO ADDRESS OF                    
*              OPTIONS FIELD AND WILL PASS BACK GST DATA (SEE DSECT)            
*       1-3    A(=C'PST')                                                       
*              ON RETURN THIS PARAMETER IS SET TO ADDRESS OF                    
*              OPTIONS FIELD AND WILL PASS BACK GST & PST DATA                  
*              (SEE DSECT)                                                      
*       1-3    A(=C'BOTH')                                                      
*                                                                               
*              ON RETURN THIS PARAMETER IS SET TO ADDRESS OF                    
*              OPTIONS FIELD AND WILL PASS COUNT AND GST/PST DATA               
*-----------------------------------------------------------------              
*                                                                               
         SPACE 2                                                                
GETINS   CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 GETWORKL,GETINS,CLEAR=YES                                        
         USING GETWORK,RC                                                       
         MVC   PARS(20),0(R1)       SAVE PARAMTER CALL ARGUMENTS                
         L     RA,0(R1)                                                         
         USING REC,RA                                                           
*                                                                               
*        INIT GST, PST AND COUNT RELATED FIELDS                                 
*                                                                               
         MVI   GSTSW,0                                                          
         XC    GSPAYCNT,GSPAYCNT   CLEAR                                        
         XC    GSBILCNT,GSBILCNT   CLEAR                                        
         XC    GVALUES(GVALUESL),GVALUES  INIT GST FIELDS                       
*                                                                               
         LA    R0,10               10 PROVINCES                                 
         LA    RF,PSTAREA          CLEAR PST AREA                               
*                                                                               
         XC    0(PSTAREAL,RF),0(RF)                                             
         LA    RF,PSTAREAL(RF)                                                  
         BCT   R0,*-10                                                          
*                                                                               
NOGST    MVI   COUNTYN,0                                                        
         L     RF,PARS+16                                                       
*                                                                               
         CLC   0(3,RF),=C'GST'     GST OPTION                                   
         BNE   *+12                                                             
         MVI   GSTSW,C'G'                                                       
         B     YISGST              LOAD ADDRESS TO CALLER                       
*                                                                               
         CLC   0(3,RF),=C'PST'     PST OPTION                                   
         BNE   NOGSTA                                                           
         MVI   GSTSW,C'G'                                                       
         MVI   PSTSW,C'P'                                                       
         B     YISGST              LOAD ADDRESS TO CALLER                       
*                                                                               
NOGSTA   CLC   0(4,RF),=C'BOTH'    PST/GST AND COUNT                            
         BNE   CKCOUNT                                                          
         MVI   GSTSW,C'G'                                                       
         MVI   PSTSW,C'P'                                                       
         B     YISBOTH                                                          
*                                                                               
CKCOUNT  CLC   0(5,RF),=C'COUNT'                                                
         BNE   NOCOUNT                                                          
*                                                                               
YISBOTH  MVI   COUNTYN,C'Y'                                                     
*                                                                               
YISGST   LA    RE,GSPAYCNT         POINT TO TOTAL TO BE PASSED                  
         ST    RE,16(R1)                                                        
*                                                                               
NOCOUNT  DS    0H                                                               
         USING ELEM,R5                                                          
*                                                                               
         L     R2,4(R1)                                                         
         MVI   CRSW,0                                                           
         XC    STAX,STAX                                                        
         USING PVALUESD,R2                                                      
         CLC   0(4,R2),=C'CRAT'     CRATE                                       
         BNE   *+8                                                              
         MVI   CRSW,C'C'                                                        
*                                                                               
         XC    PVALUES(PVALUESX-PVALUES),PVALUES                                
         XC    FULL,FULL                                                        
*                                                                               
         MVC   SAVEAC,PBDACP      SAVE PBUYREC'S PBDACP                         
         CLI   PBDCOSIN,C'C'      SEE IF 'C' RATE BUY                           
         BNE   GET0                                                             
         CLI   CRSW,C'C'       SPECIAL C RATE PROCESSING                        
         BE    GET0                                                             
         ZAP   PBDACP,=P'-1'       SET TO 100.00 PCT                            
*                                                                               
GET0     L     RE,8(R1)                                                         
         MVC   PRDCODE,0(RE)                                                    
         CLI   PARS+8,C'Y'         TEST OTHER AGY OPTION                        
         BE    GT1                                                              
         CLI   PRDCODE,C'*'        SKIP O/A PRODUCT                             
         BE    GETX                                                             
*                                                                               
GT1      DS    0H                                                               
         EJECT                                                                  
***********************************************************************         
*                                                                     *         
*        FOR CANADIAN BUYS FIND GST CODE, PERCENT AND BASIS           *         
*        ALSO SIMILAR INFORMATION FOR EACH PROVINCE WITH PST          *         
*                                                                     *         
***********************************************************************         
         SPACE 2                                                                
GETCAN   DS    0H                                                               
*                                                                               
         TM    PBDCNDA,X'80'       SKIP IF NOT CANADIAN BUY                     
         BZ    GETCANX                                                          
*                                                                               
         MVC   FULL(1),PBDGST      TAKE GST CODE FROM BUY                       
*                                                                               
         CLI   FULL,0              IF CODE NOT ENTERED IN BUY                   
         BNE   *+8                                                              
         MVI   FULL,C'S'              DEFAULT TO STANDARD                       
*                                                                               
*---------------------------> VERIFY GST CODE TO TABLE                          
*                                                                               
*        GST/PST DETERMINED BY INSERT DATE UNLESS PAID                          
*           THEN WE USE PAID DATE FOR TABLE SEARCHES                            
*                                                                               
         MVC   WGSTDATE,PBUYKDAT   INIT GST DATE WITH INSERT DATE               
         MVI   WPSTIND,0           INIT PAID BUY INDICATOR FOR PST              
*                                                                               
         L     R6,PARS             GET A(BUYREC)                                
         LA    R6,33(R6)           POINT TO FIRST ELEMENT IN RECORD             
         SR    R0,R0                                                            
*                                                                               
GTCDTELP DS    0H                                                               
*                                                                               
         USING ELEM,R6             ESTABLISH AS PAY ELEMENT                     
*                                                                               
         CLI   0(R6),0             DONE AT END OF RECORD                        
         BE    GTCDTEDN                                                         
*                                                                               
         CLI   0(R6),X'25'         WANT PAY ELEMENT                             
         BNE   GTCDTECN                                                         
         OC    PPDDATE,PPDDATE     THAT HAS BEEN PAID                           
         BZ    GTCDTECN                                                         
*                                                                               
         MVI   WPSTIND,C'P'        INDICATE PAID BUY                            
*                                                                               
         MVC   WGSTDATE,PPDDATE    SET TO USE PAID DATE                         
*                                                                               
         CLC   WGSTDATE,=AL1(94,6,1)  IF PAID JUNE 1,1994                       
         BNE   *+10                                                             
         MVC   WGSTDATE,=AL1(94,5,31)    USE MAY 31,1994 AS DATE                
*                                                                               
         CLC   PBUYKDAT,WGSTDATE   IF INSERT DATE PRIOR TO PAY DATE             
         BNL   *+10                                                             
         MVC   WGSTDATE,PBUYKDAT      USE IT FOR TABLE SEARCH                   
*                                                                               
         B     GTCDTEDN                                                         
*                                                                               
GTCDTECN DS    0H                                                               
*                                                                               
         IC    R0,1(R6)            GET ELEMENT LENGTH                           
         AR    R6,R0               POINT TO NEXT ELEMENT                        
         B     GTCDTELP                                                         
*                                                                               
GTCDTEDN DS    0H                                                               
*                                                                               
         DROP  R6                                                               
*                                                                               
*        TABLE IS CL1   GST CODE                                                
*                 XL1   SALES TAX BASIS X'00' NET+GST                           
*                                       X'01' NET                               
*                 XL2   GST PERCENT TO 3 DECIMALS                               
*                 XL3   START DATE BINARY                                       
*                 XL1   SPARE                                                   
*                                                                               
         LA    R5,PGSTTAB                                                       
*                                                                               
GTCGSTLP DS    0H                                                               
*                                                                               
         CLC   0(1,R5),FULL        FIND GST CODE IN TABLE                       
         BE    GTCGSTFD                                                         
*                                                                               
GTCGSTCN DS    0H                                                               
*                                                                               
         LA    R5,8(R5)            BUMP TO NEXT TABLE ENTRY                     
         CLI   0(R5),X'FF'         IF NOT END OF TABLE                          
         BNE   GTCGSTLP               GO CHECK NEXT TABLE ENTRY                 
*                                                                               
         DC    H'0'                BAD GST CODE                                 
*                                                                               
GTCGSTFD DS    0H                                                               
*                                                                               
         OC    2(2,R5),2(R5)      SKIP IF NO GST TAX PCT                        
         BZ    GTCGSTX                                                          
*                                                                               
         CLC   WGSTDATE,4(R5)     IF GST/PST DATE BEFORE START-UP               
         BL    GTCGSTX               SKIP GST                                   
*                                                                               
         MVC   GSTBASIS,1(R5)      SAVE BASIS BYTE                              
         MVC   GSTCODE(1),0(R5)    SAVE CODE                                    
         MVC   GSTPCT,2(R5)        SAVE PERCENT                                 
*                                                                               
GTCGSTX  DS    0H                                                               
*                                                                               
*        INIT PST AREA                                                          
*                                                                               
GTCPST   DS    0H                                                               
*                                                                               
         LA    R9,PSTAREA          ESTABLISH PSTAREA                            
         USING PSTAREA,R9                                                       
*                                                                               
         L     R6,PARS             GET A(BUYREC)                                
         LA    R6,33(R6)           POINT TO FIRST ELEMENT IN RECORD             
         SR    R0,R0                                                            
*                                                                               
GTCPSTLP DS    0H                                                               
*                                                                               
         CLI   0(R6),0             EOR - NO PST ELEMENT                         
         BE    GTCPSTX                SKIP PST CALCULATIONS                     
*                                                                               
         CLI   0(R6),PBYPSTQ       LOOKING FOR BUY PST ELEMENT                  
         BE    GTCPSTFD                                                         
*                                                                               
GTCPSTCN DS    0H                                                               
*                                                                               
         IC    R0,1(R6)            GET ELEMENT LENGTH                           
         AR    R6,R0               POINT TO NEXT ELEMENT                        
         B     GTCPSTLP                                                         
*                                                                               
GTCPSTFD DS    0H                                                               
*                                                                               
         MVC   WRKPSTC,PBYPSTC-PBYPSTEL(R6)   SAVE PST CODES                    
*                                                                               
*        FIND PROVINCE WITH PST CODE                                            
*                                                                               
         LA    R7,1                PROVINCE COUNTER                             
         LA    R6,WRKPSTC          PST CODES BY PROVINCE                        
*                                                                               
GTCPSTPL DS    0H                                                               
*                                                                               
         CLI   0(R6),0             SKIP IF NO PST CODE PRESENT                  
         BE    GTCPSTPC                                                         
*                                                                               
*        FIND PROVINCIAL CODE                                                   
*                                                                               
         LR    R1,R7               COPY PROVINCE COUNTER                        
         BCTR  R1,0                DECREMENT FOR INDEXING                       
         SLL   R1,1                EACH ENTRY IS 2 BYTES                        
         LA    R1,PROVTAB(R1)      POINT TO PROVINCE CODE                       
*                                                                               
*        FIND PST TABLE ENTRY FOR PROVINCE AND PST CODE                         
*                                                                               
GTCPSTT  DS    0H                                                               
*                                                                               
         LA    R8,PPSTTAB          POINT TO PST TABLE                           
         LA    RE,PPSTTABN         NUMBER OF ENTRIES IN TABLE                   
*                                                                               
GTCPSTTL DS    0H                                                               
*                                                                               
         CLC   0(2,R8),0(R1)       MATCH ON PROVINCE CODE                       
         BE    *+10                                                             
         CLC   0(2,R8),=C'ZZ'      OR DEFAULT CODE                              
         BNE   GTCPSTTC                                                         
*                                                                               
         CLC   2(1,R8),0(R6)       MATCH ON PST CODE                            
         BNE   GTCPSTTC                                                         
*                                                                               
         CLC   WGSTDATE,5(R8)      PST/GST MUST BE AFTER INIT DATE              
         BL    GTCPSTTC                                                         
*                                                                               
         B     GTCPSTTF            TABLE ENTRY FOUND                            
*                                                                               
GTCPSTTC DS    0H                                                               
*                                                                               
         LA    R8,PPSTTABL(R8)     NEXT TABLE ENTRY                             
         BCT   RE,GTCPSTTL                                                      
*                                                                               
         B     GTCPSTTD            SKIP PST SEARCH                              
*                                                                               
GTCPSTTF DS    0H                  GOT PST CODE ENTRY                           
*                                                                               
         MVC   PSTPROV,0(R1)       SET PROVINCE CODE                            
         MVC   PSTCODE,0(R6)       SET PST CODE                                 
*                                                                               
         CLI   PSTCODE,C'H'        SEE IF HST CODE FOUND                        
         BNE   *+10                                                             
         XC    GSTPCT,GSTPCT       CLEAR GST PERCENTAGE                         
*                                  SO GST WILL ALWAYS BE ZERO                   
*                                                                               
         MVC   PSTPCT,3(R8)        SET PST PER CENT                             
         MVC   PSTBASIS,8(R8)      SET PST BASIS                                
*                                                                               
         LA    R9,PSTAREAL(R9)     BUMP TO NEXT PST AREA                        
*                                                                               
GTCPSTTD DS    0H                                                               
*                                                                               
GTCPSTPC DS    0H                                                               
*                                                                               
         LA    R6,1(R6)            BUMP CODE POINTER                            
         LA    R7,1(R7)            BUMP PROVINCE COUNTER                        
         CH    R7,=H'10'           MAX 10 PROVINCES                             
         BNH   GTCPSTPL                                                         
*                                                                               
GTCPSTPD DS    0H                                                               
*                                                                               
GTCPSTX  DS    0H                                                               
*                                                                               
GETCANX  DS    0H                                                               
*                                                                               
         DROP  R9                                                               
*                                                                               
         EJECT                                                                  
*------------------->  GROSS COST                                               
         MVI   BILELCOD,X'26'                                                   
         MVC   GPBDCOS,PBDCOS      COST OF BUY                                  
         MVC   GPBDCTYP,PBDCOSTY   COST TYPE                                    
         MVI   GETPASS,0           SET FOR FIRST PASS                           
         SPACE 3                                                                
*----------------------------------------------------------------*              
* NOTE -- GETPASS HAS TWO SETTINGS 0 AND 1.  0 IS USED FOR NORMAL*              
* PROCESSING AND 1 IS USED TO CALCULATE OPEN RATES (WHEN         *              
*      PARAMATER 2 BYTE 0 ='O').                                 *              
*                                                                *              
*----------------------------------------------------------------*              
         SPACE 3                                                                
GT2      ZAP   DUB,GPBDCOS         CONVERT COST TO BIN                          
         CVB   R1,DUB              LOAD COST TO REG 1                           
         ZAP   DUB,PBDUNITS        CONVERT UNITS TO BINARY                      
         CVB   R0,DUB              LOAD TO 0                                    
         ST    R0,UNITS            SAVE BIN UNITS                               
         CLI   GPBDCTYP,C'U'       IF COST TYPE IS UTHEN 5 DECIMALS             
         BNE   GET2                                                             
*-------------------------------  BRING TO 2 DECIMALS AND ROUND                 
         ZAP   P11,PBDUNITS                                     !               
         MP    P11,GPBDCOS                                      !               
         DP    P11,=P'1000'                                     !               
         CP    P11+8(3),=P'0'      SEE IF NEGATIVE              !               
         BL    GT3                                              !               
         CP    P11+8(3),=P'500'    ROUNDING                     !               
         BL    *+10                                             !               
         AP    P11(8),=P'1'                                     !               
         B     GT4                                              !               
*                                                               !               
GT3      CP    P11+8(3),=P'-500'                                !               
         BH    GT4                                              !               
         SP    P11(8),=P'1'                                     !               
*                                                               !               
GT4      DS    0H                                               !               
         ZAP   DUB,P11(8)                                       !               
         CVB   R1,DUB            COST IN R1                     !               
*---------------------------------------------------------------^               
         SPACE 3                                                                
*                                                                               
*--------- SEE IF UNITS IN 2 DECIMALS ---IF SO CONVERT -----------*             
*                                                                 !             
         CLI   PBDUIND,X'89'       LOWER CASE I                   !             
         BNE   GET2                                               !             
*                                  MEANS UNITS TO 2 DECIMALS      !             
*                                  MUST DIVIDE COST BY 100 TO GET CENTS         
         DP    DUB,=P'100'                                        !             
         CP    DUB+6(2),=P'0'      SEE IF NEGATIVE                !             
         BL    GT6                                                !             
         CP    DUB+6(2),=P'50'                                    !             
         BL    GT7                                                !             
         AP    DUB(6),=P'1'                                       !             
         B     GT7                                                !             
*                                                                 !             
GT6      CP    DUB+6(2),=P'-50'                                   !             
         BH    GT7                                                !             
         SP    DUB(6),=P'1'                                       !             
GT7      ZAP   DUB,DUB(6)                                         !             
         CVB   R1,DUB                                             !             
*-----------------------------------------------------------------^             
         SPACE 3                                                                
*--------------------------------> PREMIUM COST                                 
GET2     ZAP   DUB,PBDPRCOS                                                     
         CVB   RF,DUB                                                           
         ST    RF,PREMIUM          SAVE PREMIUM COST                            
         AR    R1,RF               AND ADD TO GROSS                             
         ST    R1,GROSS            SAVE COST                                    
         LR    RF,R1                                                            
         SPACE 3                                                                
*                                                                               
*--------------------------------> CALCULATE AGENCY COMMISSION    !             
*                                                                 !             
         CLI   PBDCOSIN,C'S'       BUY MADE WHERE COST = NET      !             
         BE    GET4                NO AC                          !             
         OC    PBDACP,PBDACP       AGENCY COMMISSION OVERRIDE     !             
         BZ    GET3                ASSUME 15%                     !             
         CP    PBDACP,=P'-1'       = 100 PCT                      !             
         BNE   GET3B                                              !             
         SR    RF,RF               ZERO NET                       !             
         B     GET4                                               !             
*                                                                 !             
GET3     DS    0H                                                 !             
         ZAP   PBDACP,=P'15000'                                   !             
GET3B    DS    0H                                                 !             
         ZAP   DUB,PBDACP                                         !             
         BZ    GET4                NO AC                          !             
         CVB   R0,DUB                                             !             
         L     RF,=F'100000'                                      !             
         SR    RF,R0                                              !             
         MR    RE,R1                                              !             
         SLDL  RE,1                                               !             
         D     RE,=F'100000'                                      !             
         LTR   RF,RF                                              !             
         BM    *+8                                                !             
         AH    RF,=H'1'                                           !             
         SRA   RF,1                RF=NET                         !             
*                                                                 !             
GET4     DS    0H                                                 !             
         LR    R0,R1                                              !             
         SR    R0,RF                                              !             
         ST    R0,AGYCOM                                          !             
*-----------------------------------------------------------------^             
*                                                                               
         SPACE 3                                                                
*--------------------------------->  CALCULATE CASH DISCOUNT                    
GET4B    DS    0H                                                               
         ZAP   DUB,PBDCD                                                        
         CVB   RE,DUB                                                           
         MR    RE,RE                                                            
         SLDL  RE,1                                                             
         D     RE,=F'1000'                                                      
         LTR   RF,RF                                                            
         BM    *+8                                                              
         AH    RF,=H'1'                                                         
         SRA   RF,1                                                             
         CLI   GETPASS,1           WHAT PASS                                    
         BE    GET4D          DON'T STORE CD FOR PASS 1                         
         ST    RF,CSHDSC                                                        
         B     GET5                                                             
*--------------------------------->  END CALCULATE CASH DISCOUNT                
*                                                                               
         SPACE 3                                                                
GET4D    L     RF,CSHDSC   FOR GETPASS=1 USE STORED CD FROM PASS=0              
*                                                                               
GET5     SR    R1,RF               GROSS-CSHDSC                                 
         ST    R1,BLABLE           * BILLABLE DOLLARS                           
         S     R1,AGYCOM           BLABLE-AGYCOM                                
         ST    R1,PYABLE           * PAYABLE DOLLARS                            
*              SAVE BILLABLE DATE                                               
         OC    BLBLDT,BLBLDT                                                    
         BNZ   *+10                                                             
         MVC   BLBLDT,PBDBDATE                                                  
         SPACE 3                                                                
*-------------------->   CALCULATE TAX AND ADD TO GROSS                         
*                                  BLABLE, AND PYABLE                           
GET6     DS    0H                                                               
**       CLI   PBDELEM+1,105                                                    
**       BL    GET18               ONLY ON NEW PBDELEMS                         
*                                  TAX CHECK NO-OPED                            
***      OC    PBDTAX,PBDTAX       CHK FOR TAX                                  
***      BZ    GET18                                                            
         CLI   GETPASS,0        FOR GETPASS=1 LEAVE TAX ALONE                   
         BE    GET10                                                            
         MVC   FULL,STAX           USE STAX FROM GETPASS=0                      
         B     GET15                                                            
*                                                                               
GET10    L     RF,GROSS                                                         
         S     RF,AGYCOM                                                        
         ST    RF,WRKNET           SAVE WORKING NET                             
*                                                                               
*---------------------------> CALCULATE GST                                     
*        ALWAYS CALCULATE FOR CANADIAN BUY                                      
*          BECAUSE PROVINCIAL SALES TAX MAY DEPEND ON GST                       
*                                                                               
         TM    PBDCNDA,X'80'       SKIP IF NOT CANADIAN                         
         BZ    GET10X                                                           
*                                                                               
         SR    RE,RE                                                            
         ICM   RE,3,GSTPCT         GST PER CENT                                 
         BZ    GET10F              NO TAX                                       
*                                                                               
         MR    RE,RE               RF HAS NET                                   
         SLDL  RE,1                                                             
         D     RE,=F'100000'        GST TAX HAS 3 DECIMALS                      
         LTR   RF,RF                                                            
         BM    *+8                                                              
         AH    RF,=H'1'                                                         
         SRA   RF,1                                                             
*                                                                               
         STCM  RF,15,GSTTAX        RETURN TAX                                   
*                                                                               
GET10F   DS    0H                                                               
*                                                                               
*        CALCULATE PST IF NEEDED                                                
*                                                                               
GTPPST   DS    0H                                                               
*                                                                               
         LA    R9,PSTAREA          ESTABLISH PSTAREA                            
         USING PSTAREA,R9                                                       
*                                                                               
         LA    R0,10               10 PROVINCES                                 
*                                                                               
GTPPSTLP DS    0H                                                               
*                                                                               
         OC    PSTPROV,PSTPROV     DONE IF NO MORE PROVINCES                    
         BZ    GTPPSTDN                                                         
*                                                                               
         L     RF,GROSS            CALCULATE NET=GROSS-AGYCOM                   
         S     RF,AGYCOM                                                        
*                                                                               
         TM    PSTBASIS,X'01'      SKIP PST ON NET ONLY                         
         BO    *+8                                                              
         A     RF,GSTTAX           ELSE ON NET + GST TAX                        
*                                                                               
         LR    RE,RF               UPDATE BASIS TAX CALCULATED ON               
         A     RE,PST$BS                                                        
         ST    RE,PST$BS                                                        
*                                                                               
         SR    RE,RE                                                            
         ICM   RE,3,PSTPCT         GET PST PERCENT                              
         MR    RE,RE               CALCULATE PST TAX                            
*                                                                               
         SLDL  RE,1                DOUBLE RESULT                                
         D     RE,=F'100000'       PER CENT HAS 3 DECIMALS                      
         LTR   RF,RF               CHECK FOR NEGATIVE NUMBER                    
         BM    *+8                                                              
         AH    RF,=H'1'            PREPARE FOR ROUNDING                         
         SRA   RF,1                ROUND                                        
*                                                                               
         STCM  RF,15,PSTTAX        SAVE TAX                                     
*                                                                               
GTPPSTCN DS    0H                                                               
*                                                                               
         LA    R9,PSTAREAL(R9)     BUMP TO NEXT PSTAREA                         
         BCT   R0,GTPPSTLP                                                      
*                                                                               
GTPPSTDN DS    0H                                                               
*                                                                               
GTPPSTX  DS    0H                                                               
*                                                                               
         DROP  R9                                                               
*                                                                               
*        CALCULATE SALES TAX                                                    
*                                                                               
         L     RF,GROSS                                                         
         S     RF,AGYCOM                                                        
*                                                                               
         TM    GSTBASIS,X'01'                                                   
         BO    GET10X             SALES TAX IS ON NET                           
         A     RF,GSTTAX          SALES TAX IS ON NET + GST                     
*                                                                               
GET10X   XC    FULL,FULL                                                        
         CLI   PBDELEM+1,105                                                    
         BL    GET13                                                            
         OC    PBDTAX,PBDTAX                                                    
         BZ    GET13                                                            
         MVC   FULL+1(3),PBDTAX                                                 
         L     RE,FULL                                                          
         MR    RE,RE                                                            
         SLDL  RE,1                                                             
         D     RE,=F'1000000'        PBDTAX HAS 4 DECIMALS                      
         LTR   RF,RF                                                            
         BM    *+8                                                              
         AH    RF,=H'1'                                                         
         SRA   RF,1                                                             
         ST    RF,FULL                                                          
         MVC   TAX,FULL                                                         
         MVC   STAX,FULL                                                        
*                                                                               
GET13    DS    0H                                                               
*                                                                               
GET15    L     R0,GROSS                                                         
         A     R0,FULL                                                          
         ST    R0,GROSS                                                         
         L     R0,BLABLE            BILLABLE = GROSS -CD                        
         A     R0,FULL                                                          
         ST    R0,BLABLE                                                        
         L     R0,PYABLE            PAYABLE = AGYCOM                            
         A     R0,FULL                                                          
         ST    R0,PYABLE                                                        
*                                                                               
GET16    DS    0H                                                               
*                                                                               
GET18    CLI   GETPASS,1                                                        
         BE    GET20                                                            
         CLI   PARS+4,C'O'          SEE IF DOING OPEN RATES                     
         BNE   GET19                                                            
         LA    R5,PBDELEM                                                       
         MVI   ELCODE,X'30'         SEARCH FOR OPEN RATE ELEM                   
         BAS   R9,NEXTEL                                                        
         BNE   GET20                NOT FOUND - DONE AS REGULAR                 
         MVC   GPBDCTYP,2(R5)    OVERLAY COST TYPE AND GROSS WITH               
         MVC   GPBDCOS,3(R5)     OPEN RATE DATA                                 
         MVI   BILELCOD,X'28'       SEARCH FOR OPEN BILL ELEMS                  
         MVI   GETPASS,1         GO BACK AND PROCESS OPEN GROSS/TYPE            
         B     GT2                                                              
*                                                                               
GET19    DS    0H                                                               
         EJECT                                                                  
*----------------------> COLLECT PAID DATA                                      
*                                                                               
GET20    CLI   PARS,C'X'           TEST IF REQUESTED                            
         BE    GETX           NO IF C'X'                                        
*                                                                               
         LM    R6,R8,PGROSS                                                     
         MVI   ELCODE,X'25'                                                     
         LA    R5,PBDELEM                                                       
GETP2    BAS   R9,NEXTEL                                                        
         BNE   GETPX                                                            
         OC    PPDDATE,PPDDATE                                                  
         BZ    GETP2                                                            
* TEST TO SEE IF DATE TEST IS TO BE MADE FOR PAID DATA                          
         CLI   PARS,C'T'   BOTH BILLING AND PAID                                
         BE    YBOTH                                                            
         CLI   PARS,C'P'   PAID ONLY                                            
         BNE   NOPTEST                                                          
YBOTH    L     R9,PARS+12   LOAD ADD OF START AND END DATE                      
         CLC   PPDDATE,0(R9)  PAID TO START                                     
         BL    GETP2         BYPASS                                             
         CLC   PPDDATE,3(R9)  PAID TO END                                       
         BH    GETP2                                                            
*************                                                                   
NOPTEST  MVC   WORK(12),PPGROSS                                                 
         CLI   PBDCOSIN,C'C'            SEE IF 'C' RATE                         
         BNE   GETP5                    NO                                      
         CLI   CRSW,C'C'             SPECIAL HANDLING                           
         BNE   GETP5                                                            
         BAS   R9,FIXAC                                                         
GETP5    A     R6,WORK                                                          
         A     R7,WORK+4                                                        
         A     R8,WORK+8                                                        
         CLI   GSTSW,C'G'          SEE IF GETTING GST                           
         BNE   GETP10                                                           
         MVI   GETGSTSW,C'P'       SET DOING PAID GST                           
         BAS   RE,GETGST                                                        
GETP10   L     RF,GSPAYCNT                                                      
         LA    RF,1(RF)                                                         
         ST    RF,GSPAYCNT                                                      
         B     GETP2                                                            
*                                                                               
*                                                                               
GETPX    STM   R6,R8,PGROSS                                                     
         SR    R6,R7                                                            
         SR    R6,R8                                                            
         ST    R6,PAID                                                          
         B     GETB                                                             
*                                                                               
         EJECT                                                                  
*----------------------> COLLECT BILLED DATA                                    
*                                                                               
*                                                                               
GETB     MVC   ELCODE,BILELCOD                                                  
         LA    R5,PBDELEM                                                       
         LM    R6,R8,BGROSS                                                     
GETB2    BAS   R9,NEXTEL                                                        
         BNE   GETBX                                                            
         OC    PBLDATE,PBLDATE                                                  
         BZ    GETB2                                                            
*                                                                               
         CLI   PARS+4,C'C'         UFC COMMISSION BILLING                       
         BNE   GETB2C                                                           
         TM    PBBILST,X'01'       CHK RIGHT TYPE                               
         BZ    GETB2                                                            
         B     GETB2F                                                           
*                                                                               
GETB2C   CLI   PARS+4,C'N'         UFC NET BILLING                              
         BNE   GETB2F                                                           
         TM    PBBILST,X'02'       CHK RIGHT TYPE                               
         BZ    GETB2                                                            
*                                                                               
GETB2F   DS    0H                                                               
         CLC   PRDCODE,REC+7       REQUESTED PRD TO KEY                         
         BE    *+14                                                             
         CLC   PRDCODE,PBPRD       REQUESTED PRD TO ELEM                        
         BNE   GETB2                                                            
*                                                                               
* TEST TO SEE IF DATE TEST IS TO BE MADE FOR BILLING DATA                       
*                                                                               
         CLI   PARS,C'T'   BOTH BILLING AND PAID                                
         BE    YBOTHA                                                           
         CLI   PARS,C'B'   BILLING ONLY                                         
         BNE   NOBTEST                                                          
YBOTHA   L     R9,PARS+12   LOAD ADD OF START AND END DATE                      
         CLC   PBLDATE,0(R9)  BILLED TO START                                   
         BL    GETB2         BYPASS                                             
         CLC   PBLDATE,3(R9) BILLED TO END                                      
         BH    GETB2         BYPASS                                             
*************                                                                   
NOBTEST  MVC   WORK(12),PBGROSS                                                 
         CLI   PBDCOSIN,C'C'            SEE IF 'C' RATE                         
         BNE   GETB5                    NO                                      
         CLI   CRSW,C'C'             SPECIAL HANDLING                           
         BNE   GETB5                                                            
         BAS   R9,FIXAC                                                         
GETB5    DS    0H                                                               
         CLI   PARS+4,C'C'           DON'T DO SPECIAL HANDLING                  
         BE    GETB8                 FOR UFC COMM OR NET BILLING                
         CLI   PARS+4,C'N'                                                      
         BE    GETB8                                                            
*                                                                               
*        SPECIAL HANDLING OF UFC COMM AND NET BILL ELEMS                        
*        UNLESS PARS+4 = 'C' OR 'N'                                             
*                                                                               
         TM    PBBILST,X'01'          SEE IF UFC COMM BILLING                   
         BZ    GETB7                                                            
         MVC   WORK(4),WORK+4         SET GROSS TO AC                           
         XC    WORK+8(8),WORK+8       CLEAR CD                                  
*                                     "EFFECTIVE GROSS" = AC                    
         B     GETB8                   (NET WILL BE ZERO)                       
*                                                                               
GETB7    TM    PBBILST,X'02'          SEE IF UFC NET BILLING                    
         BZ    GETB8                                                            
         L     R0,WORK                                                          
         S     R0,WORK+4              SET GROSS TO NET (GROSS - AC)             
         ST    R0,WORK                AND LEAVE AC AND CD ALONE                 
         XC    WORK+4(4),WORK+4       CLEAR AC (SO NET WILL BE NET)             
*                                     "EFFECTIVE GROSS" = NET                   
GETB8    A     R6,WORK                                                          
         A     R7,WORK+4                                                        
         A     R8,WORK+8                                                        
*                                                                               
         CLI   GSTSW,C'G'           SEE IF GETTING GST                          
         BNE   GETB10                                                           
         MVI   GETGSTSW,C'B'        GET GST BILLED FROM ELEMENT                 
         BAS   RE,GETGST                                                        
*                                                                               
GETB10   L     RF,GSBILCNT                                                      
         LA    RF,1(RF)                                                         
         ST    RF,GSBILCNT                                                      
         B     GETB2                                                            
*                                                                               
GETBX    STM   R6,R8,BGROSS                                                     
         SR    R6,R7                                                            
         SR    R6,R8                                                            
         ST    R6,BILLED                                                        
         EJECT                                                                  
GETX     DS    0H                                                               
         MVC   PBDACP,SAVEAC         RESTORE PBUYREC'S PBDACP                   
*                                                                               
         CLC   PRDCODE,REC+7         IF NOT EQ DO POL ALLOCATIN                 
         BE    GETX8                                                            
*                                                                               
* SAVE PAID DATA.  BILL DATA WILL BE REPLACED WITH GST TAX DATA                 
*  THAT WILL BE SPLIT BY PRODUCT.  AT THE END OF ALLOCATION BILLING             
*  DATA WILL BE RESTORED.                                                       
*                                                                               
         MVC   SAVE2W(8),BGROSS                                                 
         MVC   BGROSS(8),GSTTAX    GST DATA OVERLAYING BILLED                   
*                                                                               
         IC    R3,PBDWTSUM         WEIGHTING FACTOR FOR BUY                     
         N     R3,=F'127'                                                       
         BAS   RE,REMCOMP                                                       
         SPACE 3                                                                
*                                                                               
*------------------> FIND PRDELEM TO CALCULATE PRODUCT SHARE                    
*                                                                               
         MVI   ELCODE,X'21'                                                     
         LA    R5,PBDELEM                                                       
GETX2    EQU   *                                                                
*                                                                               
         BAS   R9,NEXTEL           FIND PRODUCT ALLOCATION                      
         BE    GETX3                                                            
*                                                                               
         XC    PVALUES(PVALUESX-PVALUES),PVALUES  CLEAR - NO PRODUCT            
         XC    SAVE2W(8),SAVE2W                                                 
*                                                                               
         LA    R0,10               10 PROVINCES                                 
         LA    RF,PSTAREA          CLEAR PST AREA                               
*                                                                               
         XC    0(PSTAREAL,RF),0(RF)                                             
         LA    RF,PSTAREAL(RF)                                                  
         BCT   R0,*-10                                                          
*                                                                               
         B     GETX8A                                                           
*                                                                               
GETX3    IC    R4,PPRCOST          COST SHARE IN UNITS                          
         N     R4,=F'127'                                                       
         BAS   RE,LEFTCOMP                                                      
         CLC   PPRCODE,PRDCODE                                                  
         BNE   GETX2                                                            
         BAS   R9,NEXTEL           TEST IF AT LAST PRODUCT                      
         BE    GETX3PDN            NO                                           
         MVC   WORK+1(14),WORK+29      YES- MOVE LEFTOVERS TO THIS SHR          
*                                                                               
*        MOVE PST LEFTOVERS TO THIS SHARE                                       
*                                                                               
         LA    RE,10               10 PROVINCES                                 
         LA    R8,PSTWORK          LEFTOVERS SAVEAREA                           
*                                                                               
GETX3PLP DS    0H                                                               
*                                                                               
         LA    R6,PSTFLDNM         SET NUMBER OF FIELDS TO PROCESS              
*                                                                               
GETX3PTL DS    0H                                                               
*                                                                               
         LR    RF,R6               CALCULATE INDEX                              
         BCTR  RF,0                                                             
         MH    RF,=H'3'            3 BYTES OF WORK FOR EACH FIELD               
         LA    RF,0(RF,R8)                                                      
         MVC   0(1,RF),2(RF)       USE PST LEFTOVERS                            
*                                                                               
GETX3PTC DS    0H                                                               
*                                                                               
         BCT   R6,GETX3PTL                                                      
*                                                                               
GETX3PTD DS    0H                                                               
*                                                                               
GETX3PCN DS    0H                                                               
*                                                                               
         LA    R8,L'PSTWORK(R8)    NEXT PST WORKAREA                            
         BCT   RE,GETX3PLP                                                      
*                                                                               
GETX3PDN DS    0H                                                               
*                                                                               
         BAS   RE,GETSHR                                                        
*                                                                               
GETX8A   DS    0H                                                               
         MVC   GSTTAX(8),BGROSS    MOVE ALLOCATED TAX                           
         MVC   BGROSS(8),SAVE2W                                                 
*                                                                               
GETX8    DS    0H                                                               
         CLI   PBDCOSIN,C'C'         SEE IF COMMISSION RATE BUY                 
         BNE   GETX9                                                            
*                                                                               
         XC    GSTTAX,GSTTAX         COMMISSION - CLEAR TAX                     
         XC    GSTTAXPD,GSTTAXPD     CLEAR PAID                                 
         XC    GSTTAXBL,GSTTAXBL     CLEAR BILLED                               
         XC    PYABLE,PYABLE         CLEAR NET ORDERED                          
         XC    PAID,PAID             CLEAR NET PAID                             
         XC    BILLED,BILLED         CLEAR NET BILLED                           
*                                                                               
         LA    R0,10               10 PROVINCES                                 
         LA    R9,PSTAREA          CLEAR PST TAXES                              
         USING PSTAREA,R9          ESTABLISH PSTAREA                            
*                                                                               
         XC    PSTTAX,PSTTAX       CLEAR TAX                                    
         XC    PSTTAXPD,PSTTAXPD   CLEAR TAX PAID                               
         XC    PSTTAXBL,PSTTAXBL   CLEAR TAX BILLED                             
         XC    PST$BS,PST$BS       CLEAR TAX DOLLAR BASIS                       
         XC    PST$BSPD,PST$BSPD   CLEAR TAX PAID DOLLAR BASIS                  
         XC    PST$BSBL,PST$BSBL   CLEAR TAX BILLED DOLLAR BASIS                
         LA    RF,PSTAREAL(RF)                                                  
         BCT   R0,*-22                                                          
*                                                                               
GETX9    DS    0H                                                               
*                                                                               
         B     EXIT                                                             
*                                                                               
         DROP  R9                                                               
*                                                                               
*  NOTE: TAX REMAINS AS CALCULATED ON NET FOR COMMISSION RATE BUY               
*  NOTE: PROGRAMS USING 'CRAT' IN VALUES USE PYABLE, PAID,AND                   
*        BILLED FOR THEIR NETS - NOT SUBTRACT AGYCOM FROM GROSS                 
*        TO GET NET.                                                            
*                                                                               
*EXIT     CLI   COUNTYN,C'Y'                                                    
*         BNE   EXITX                                                           
*         LA    RF,GSPAYCNT                                                     
*         ST    RF,PARS+16                                                      
*                                                                               
EXIT     XMOD1 1                                                                
*                                                                               
         EJECT                                                                  
***********************************************************************         
*                                                                     *         
*        CALCULATE GST AND PST ON INDIVIDUAL PAY AND BILL ELEMENTS    *         
*                                                                     *         
***********************************************************************         
GETGST   NTR1                                                                   
*                                                                               
         XC    WRKGST,WRKGST       INIT GST AMOUNT                              
*                                                                               
         TM    PBDCNDA,X'80'       SKIP IF NOT CANADIAN                         
         BZ    GETGSTX                                                          
*                                                                               
         L     RF,WORK         GROSS                                            
         S     RF,WORK+4       MINUS AGYCOM                                     
*                                                                               
         ST    RF,WRKNET           SAVE AS NET                                  
*                                                                               
         OC    PBDTAX,PBDTAX       SEE IF I HAVE TAX                            
         BZ    GETPG20                                                          
*                                                                               
*                          FOR TAX BUYS WORK INCLUDES TAX                       
*                                                                               
GETPG10G DS    0H          EXTRACT NET ELEMENT                                  
*                     RETURNS NET IN RF  FOR GETPG10G                           
         L     R0,WORK                 PPGROSS                                  
         S     R0,WORK+4              MINUS PPAGYCOM                            
         ST    R0,DUB                                                           
         XC    FULL,FULL                                                        
         MVC   FULL+1(3),PBDTAX                                                 
         L     RF,FULL           RE = SALES TAX PCT                             
         SR    RE,RE                                                            
*                                                                               
         TM    GSTBASIS,X'01'     SEE IF SALES TAX IS ON NET                    
         BO    GETPG25                                                          
*                                                                               
         XC    FULL,FULL                                                        
         MVC   FULL+2(2),GSTPCT   GSTTAX PCT                                    
         L     R0,FULL                                                          
         A     R0,=F'100000'                                                    
         ST    R0,FULL                                                          
*                                                                               
         M     RE,FULL                                                          
         D     RE,=F'100000'                                                    
*                                                                               
GETPG25  A     RF,=F'1000000'                                                   
         ST    RF,FULL                                                          
*                                                                               
         L     RF,DUB                                                           
         M     RE,=F'1000000'                                                   
         SLDL  RE,1                                                             
         D     RE,FULL                                                          
         LTR   RF,RF                                                            
         BM    *+8                                                              
         AH    RF,=H'1'                                                         
         SRA   RF,1                                                             
*                                                                               
*                                   NOTE - RF HAS NET AT THIS POINT             
GETPG20  DS    0H                                                               
*                                                                               
         ST    RF,WRKNET           SAVE NET                                     
*                                                                               
         CLI   GETGSTSW,C'P'       SEE IF GETTING PAID GST                      
         BNE   GETPG5                                                           
*                                                                               
         TM    PBDCNDA,X'01'       SEE IF PAID WITH GST                         
         BZ    GETPGX              NO - DON'T CALCULATE GST PAID                
*                                                                               
GETPG5   DS    0H                                                               
*                                                                               
         XC    FULL,FULL                                                        
         MVC   FULL+2(2),GSTPCT                                                 
         L     RE,FULL             GST PERCENTAGE                               
*                                                                               
         MR    RE,RE                                                            
         SLDL  RE,1                                                             
         D     RE,=F'100000'       GST TAX HAS 3 DECIMALS                       
         LTR   RF,RF                                                            
         BM    *+8                                                              
         AH    RF,=H'1'                                                         
         SRA   RF,1                                                             
         ST    RF,FULL             ROUNDED GST                                  
         ST    RF,WRKGST           SAVE GST                                     
*                                                                               
GETPGX   DS    0H                                                               
*                                                                               
*        CALCULATE PST IF NEEDED                                                
*                                                                               
GETPST   DS    0H                                                               
*                                                                               
         CLI   PSTSW,C'P'          SKIP IF PST NOT NEEDED                       
         BNE   GETPSTX                                                          
*                                                                               
         CLI   GETGSTSW,C'P'       SEE IF GETTING PAID GST                      
         BNE   GETPST01                                                         
*                                                                               
         TM    PBDCNDA,X'02'       SEE IF PAID WITH PST                         
         BZ    GETPSTX             NO - DON'T CALCULATE PST PAID                
*                                                                               
GETPST01 DS    0H                                                               
*                                                                               
         LA    R9,PSTAREA          ESTABLISH PSTAREA                            
         USING PSTAREA,R9                                                       
*                                                                               
         LA    R0,10               10 PROVINCES                                 
*                                                                               
GETPSTLP DS    0H                                                               
*                                                                               
         OC    PSTPROV,PSTPROV     SKIP IF NO MORE PROVINCES WITH PST           
         BZ    GETPSTDN                                                         
*                                                                               
         L     RF,WRKNET           COPY NET                                     
*                                                                               
         TM    PSTBASIS,X'01'      SKIP PST ON NET ONLY                         
         BO    *+8                                                              
         A     RF,WRKGST           NET + GSTTAX                                 
*                                                                               
         ST    RF,WRK$BS           SAVE PST DOLLAR BASIS                        
*                                                                               
         SR    RE,RE                                                            
         ICM   RE,3,PSTPCT         GET PST PERCENT                              
         MR    RE,RE               CALCULATE PST TAX                            
*                                                                               
         SLDL  RE,1                DOUBLE RESULT                                
         D     RE,=F'100000'       PER CENT HAS 3 DECIMALS                      
         LTR   RF,RF               CHECK FOR NEGATIVE NUMBER                    
         BM    *+8                                                              
         AH    RF,=H'1'            PREPARE FOR ROUNDING                         
         SRA   RF,1                ROUND                                        
*                                                                               
         CLI   GETGSTSW,C'P'       IF LOOKING FOR PAID PST                      
         BNE   GETPST40                                                         
*                                                                               
         L     RE,PST$BSPD            ACCUMULATE PAID PST $ BASIS               
         A     RE,WRK$BS                                                        
         ST    RE,PST$BSPD                                                      
*                                                                               
         L     RE,PSTTAXPD                                                      
         AR    RE,RF                  ACCUMULATE PAID PST                       
         ST    RE,PSTTAXPD                                                      
         B     GETPST50                                                         
*                                                                               
GETPST40 DS    0H                                                               
*                                                                               
         CLI   GETGSTSW,C'B'       IF LOOKING FOR BILLED PST                    
         BNE   GETPST50                                                         
*                                                                               
         L     RE,PST$BSBL            ACCUMULATE BILLED PST $ BASIS             
         A     RE,WRK$BS                                                        
         ST    RE,PST$BSBL                                                      
*                                                                               
         L     RE,PSTTAXBL                                                      
         AR    RE,RF                  ACCUMULATE BILLED PST                     
         ST    RE,PSTTAXBL                                                      
*                                                                               
GETPST50 DS    0H                                                               
*                                                                               
GETPSTCN DS    0H                                                               
*                                                                               
         LA    R9,PSTAREAL(R9)     BUMP TO NEXT PSTAREA                         
         BCT   R0,GETPSTLP                                                      
*                                                                               
GETPSTDN DS    0H                                                               
*                                                                               
GETPSTX  DS    0H                                                               
*                                                                               
         DROP  R9                                                               
*                                                                               
         MVC   FULL,WRKGST         RESTORE GST                                  
*                                                                               
GETPG30  DS    0H                                                               
*                                                                               
         CLI   GETGSTSW,C'P'                                                    
         BNE   GETPG40                                                          
*                                                                               
         L     RE,GSTTAXPD         ACCUMLATE GST PAID                           
         A     RE,FULL                                                          
         ST    RE,GSTTAXPD                                                      
         B     GETGSTX                                                          
*                                                                               
GETPG40  CLI   GETGSTSW,C'B'                                                    
         BNE   GETPSTX                                                          
         L     RE,GSTTAXBL         ACCUMLATE GST BILLED                         
         A     RE,FULL                                                          
         ST    RE,GSTTAXBL                                                      
         B     GETGSTX                                                          
GETGSTX  XIT1                      RETURN                                       
*                                                                               
         EJECT                                                                  
*--------------------------->  BREAK OUT COSTS FOR POOL PRODUCTS                
*                                                                               
GETSHR   NTR1                                                                   
*                                                                               
         LA    R7,GROSS                                                         
         LA    R6,14                ***** NOW 14       X                        
*                                                                               
GETSHR2  EQU   *                                                                
*                                                                               
         L     RF,0(R7)            AMT                                          
         SR    R0,R0                                                            
         IC    R0,WORK(R6)                                                      
         SR    RE,RE                                                            
         LTR   RF,RF                                                            
         BZ    GETSHR6                                                          
         BP    *+10                                                             
         LCR   R0,R0                                                            
         L     RE,=F'-1'                                                        
         DR    RE,R3               / WTSUM (NO ROUND)                           
         MR    RE,R4               X THIS SHARE                                 
         AR    RF,R0               ADD SHARE OF PENNIES                         
         ST    RF,0(R7)                                                         
GETSHR6  EQU   *                                                                
         LA    R7,4(R7)                                                         
         BCT   R6,GETSHR2                                                       
*                                                                               
*        REMAINDERS FOR PST                                                     
*                                                                               
GSHPST   DS    0H                                                               
*                                                                               
         LA    R2,10               10 PROVINCES                                 
         LA    R9,PSTAREA          ESTABLISH PSTAREA                            
         USING PSTAREA,R9                                                       
         LA    R8,PSTWORK          POINT TO PST WORKAREA                        
*                                                                               
GSHPSTLP DS    0H                                                               
*                                                                               
         LA    R6,PSTFLDNM         NUMBER OF PST FIELDS TO PROCESS              
*                                                                               
GSHPSTTL DS    0H                                                               
*                                                                               
         LR    R1,R6               CALCULATE INDEX                              
         BCTR  R1,0                                                             
         MH    R1,=H'3'            3 BYTES OF WORK FOR EACH FIELD               
         LA    R1,0(R1,R8)                                                      
*                                                                               
         SR    R0,R0                                                            
         IC    R0,0(R1)                                                         
*                                                                               
         LR    R7,R6               CALCULATE INDEX                              
         BCTR  R7,0                                                             
         MH    R7,=H'4'            4 BYTES FOR EACH FIELD                       
         LA    R7,PSTFLDT(R7)      A(NEXT FIELD TO PROCESS)                     
*                                                                               
         L     R7,0(R7)            DISPLACEMENT OF NEXT FIELD                   
         LA    R7,0(R7,R9)         POINT TO NEXT FIELD                          
*                                                                               
         SR    RE,RE                                                            
         ICM   RF,15,0(R7)         NEXT AMT                                     
         BZ    GSHPSTTC                                                         
         BP    *+10                                                             
         LCR   R0,R0                                                            
         L     RE,=F'-1'                                                        
*                                                                               
         DR    RE,R3               / WTSUM (NO ROUND)                           
         MR    RE,R4               X THIS SHARE                                 
         AR    RF,R0               ADD SHARE OF PENNIES                         
*                                                                               
         ST    RF,0(R7)                                                         
*                                                                               
GSHPSTTC DS    0H                                                               
*                                                                               
         BCT   R6,GSHPSTTL                                                      
*                                                                               
GSHPSTTD DS    0H                                                               
*                                                                               
GSHPSTCN DS    0H                                                               
*                                                                               
         LA    R9,PSTAREAL(R9)     NEXT PSTAREA                                 
         LA    R8,L'PSTWORK(R8)    NEXT PST WORKAREA                            
         BCT   R2,GSHPSTLP                                                      
*                                                                               
GSHPSTDN DS    0H                                                               
         XIT1                                                                   
*                                                                               
         DROP  R9                                                               
*                                                                               
         EJECT                                                                  
NEXTEL   CLI   0(R5),0                                                          
         BE    NEXTELX                                                          
         SR    R0,R0                                                            
NEXTEL2  DS    0H                                                               
         IC    R0,1(R5)                                                         
         LTR   R0,R0                                                            
         BNP   NEXTELX                                                          
         AR    R5,R0                                                            
         CLC   ELCODE,0(R5)                                                     
         BCR   8,R9                RETURN WITH CC =                             
         CLI   0(R5),0                                                          
         BNE   NEXTEL2                                                          
NEXTELX  LTR   R5,R5               SET CC TO NOT =                              
         BR    R9                                                               
         EJECT                                                                  
*                                  CALCULATE REMAINDER (AMT/WTSUM)              
*                                  STC REMAINDER IN WORK(R6) AND                
*                                  WORK+14(R6)                                  
REMCOMP  NTR1                                                                   
         LA    R6,14               ***** NOW 14                                 
         LA    R7,GROSS                                                         
REM2     EQU   *                                                                
         L     RF,0(R7)                                                         
         M     RE,=F'1'                                                         
         DR    RE,R3                                                            
         LPR   RE,RE                                                            
         STC   RE,WORK(R6)                                                      
         STC   RE,WORK+14(R6)                                                   
         LA    R7,4(R7)                                                         
         BCT   R6,REM2                                                          
*                                                                               
*        REMAINDERS FOR PST                                                     
*                                                                               
REMPST   DS    0H                                                               
*                                                                               
         LA    R2,10               10 PROVINCES                                 
         LA    R9,PSTAREA          ESTABLISH PSTAREA                            
         USING PSTAREA,R9                                                       
         LA    R8,PSTWORK          POINT TO PST WORKAREA                        
*                                                                               
REMPSTLP DS    0H                                                               
*                                                                               
         LA    R6,PSTFLDNM         NUMBER OF FIELDS TO PROCESS                  
*                                                                               
REMPSTTL DS    0H                                                               
*                                                                               
         LR    R7,R6               CALCULATE INDEX                              
         BCTR  R7,0                                                             
         MH    R7,=H'4'            4 BYTES FOR EACH FIELD                       
         LA    R7,PSTFLDT(R7)      A(NEXT FIELD TO PROCESS)                     
*                                                                               
         L     R7,0(R7)            DISPLACEMENT OF NEXT FIELD                   
         LA    R7,0(R7,R9)         POINT TO NEXT FIELD                          
*                                                                               
         L     RF,0(R7)                                                         
         M     RE,=F'1'                                                         
         DR    RE,R3                                                            
         LPR   RE,RE                                                            
*                                                                               
         LR    R1,R6               CALCULATE INDEX                              
         BCTR  R1,0                                                             
         MH    R1,=H'3'            3 BYTES OF WORK FOR EACH FIELD               
*                                                                               
         STC   RE,0(R1,R8)                                                      
         STC   RE,1(R1,R8)         SINCE THERE ARE 2 PST FLDS                   
*                                                                               
REMPSTTC DS    0H                                                               
*                                                                               
         BCT   R6,REMPSTTL                                                      
*                                                                               
REMPSTTD DS    0H                                                               
*                                                                               
REMPSTCN DS    0H                                                               
*                                                                               
         LA    R9,PSTAREAL(R9)     NEXT PSTAREA                                 
         LA    R8,L'PSTWORK(R8)    NEXT PST WORKAREA                            
         BCT   R2,REMPSTLP                                                      
*                                                                               
REMPSTDN DS    0H                                                               
         XIT1                                                                   
*                                                                               
         DROP  R9                                                               
*                                                                               
         EJECT                                                                  
*                                  DETERMINE PART OF REMAINDER FOR              
*                                  THIS SHARE                                   
*                                                                               
LEFTCOMP NTR1                                                                   
         MVC   WORK+29(14),WORK+1      SAVE CURRENT LEFTOVERS                   
         LA    R6,14                                                            
LEFT2    SR    R1,R1                                                            
         IC    R1,WORK+14(R6)      ORIG. REM                                    
         LTR   R1,R1                                                            
         BZ    LEFT4               NONE                                         
         SR    RF,RF                                                            
         IC    RF,WORK(R6)         LEFT OVER                                    
         LTR   RF,RF                                                            
         BZ    LEFT4               NONE LEFT                                    
         MR    R0,R4               X THIS SHARE                                 
         SLDL  R0,1                                                             
         DR    R0,R3               / WTSUM                                      
         AH    R1,=H'1'                                                         
         SRA   R1,1                                                             
         LTR   R1,R1                                                            
         BNZ   LEFT3                                                            
*                                  NEW CODE 4/23/91                             
         LTR   R4,R4               SEE IF ZERO SHARE                            
         BZ    LEFT3               YES - THEN NO PENNY                          
*                                                                               
         LA    R1,1                AT LEAST 1                                   
LEFT3    LR    R0,RF                                                            
         SR    RF,R1                                                            
         BNM   *+8                                                              
         SR    RF,RF                                                            
         LR    R1,R0                                                            
         STC   RF,WORK(R6)         SAVE LEFT                                    
         CLC   PPRCODE,PRDCODE     UNLESS THIS IS OUR PRODUCT                   
         BNE   LEFT4                                                            
         STC   R1,WORK(R6)         THEN SAVE THIS SHARE                         
LEFT4    EQU   *                                                                
         BCT   R6,LEFT2                                                         
*                                                                               
*        REMAINDERS FOR PST                                                     
*                                                                               
LFTPST   DS    0H                                                               
*                                                                               
         LA    R2,10               10 PROVINCES                                 
         LA    R9,PSTAREA          ESTABLISH PSTAREA                            
         USING PSTAREA,R9                                                       
         LA    R8,PSTWORK          POINT TO PST WORKAREA                        
*                                                                               
LFTPSTLP DS    0H                                                               
*                                                                               
         LA    R6,PSTFLDNM         DO PSTTAX & PSTTAXPD                         
*                                                                               
LFTPSTTL DS    0H                                                               
*                                                                               
         ST    R6,FULL             SAVE COUNTER                                 
         BCTR  R6,0                CALCULATE INDEX                              
         MH    R6,=H'3'            3 BYTES OF WORK FOR EACH FIELD               
         SR    R1,R1                                                            
*                                                                               
         IC    R1,0(R6,R8)         CURRENT LEFTOVER                             
         STC   R1,2(R6,R8)         SAVE                                         
*                                                                               
         IC    R1,1(R6,R8)         ORIG. REM                                    
*                                                                               
         LTR   R1,R1                                                            
         BZ    LFTPSTTC            NONE                                         
*                                                                               
         SR    RF,RF                                                            
         IC    RF,0(R6,R8)         LEFT OVER                                    
         LTR   RF,RF                                                            
         BZ    LFTPSTTC            NONE LEFT                                    
*                                                                               
         MR    R0,R4               X THIS SHARE                                 
         SLDL  R0,1                                                             
         DR    R0,R3               / WTSUM                                      
         AH    R1,=H'1'                                                         
         SRA   R1,1                                                             
         LTR   R1,R1                                                            
         BNZ   LFTPSTT3                                                         
*                                  NEW CODE 4/23/91                             
         LTR   R4,R4               SEE IF ZERO SHARE                            
         BZ    LFTPSTT3            YES - THEN NO PENNY                          
*                                                                               
         LA    R1,1                AT LEAST 1                                   
LFTPSTT3 LR    R0,RF                                                            
         SR    RF,R1                                                            
         BNM   *+8                                                              
         SR    RF,RF                                                            
         LR    R1,R0                                                            
         STC   RF,0(R6,R8)         SAVE LEFT                                    
         CLC   PPRCODE,PRDCODE     UNLESS THIS IS OUR PRODUCT                   
         BNE   LFTPSTTC                                                         
         STC   R1,0(R6,R8)         THEN SAVE THIS SHARE                         
LFTPSTTC EQU   *                                                                
*                                                                               
         L     R6,FULL             RESTORE COUNTER                              
         BCT   R6,LFTPSTTL                                                      
*                                                                               
LFTPSTTD DS    0H                                                               
*                                                                               
LFTPSTCN DS    0H                                                               
*                                                                               
         LA    R9,PSTAREAL(R9)     NEXT PSTAREA                                 
         LA    R8,L'PSTWORK(R8)    NEXT PST WORKAREA                            
         BCT   R2,LFTPSTLP                                                      
*                                                                               
LFTPSTDN DS    0H                                                               
         XIT1                                                                   
*                                                                               
         DROP  R9                                                               
*                                                                               
         EJECT                                                                  
FIXAC    EQU   *            ALTERS AGYCOM FROM BILL/PAY ELEM                    
*                           TO REFLECT PBDACP - FOR SPECIAL                     
*                           HANDLING OF 'C' RATE BUYS                           
         SR    R0,R0                                                            
         ZAP   DUB,PBDACP       AC=0.0                                          
         BZ    FIXACX5                                                          
         CVB   R0,DUB                                                           
         L     R1,WORK          GROSS FROM BILL/PAY ELEM                        
         L     RF,=F'100000'                                                    
         SR    RF,R0                                                            
         MR    RE,R1                                                            
         SLDL  RE,1                                                             
         D     RE,=F'100000'                                                    
         LTR   RF,RF                                                            
         BM    *+8                                                              
         AH    RF,=H'1'                                                         
         SRA   RF,1                RF=NET                                       
*                                                                               
FIXACX   DS    0H                                                               
         LR    R0,R1                                                            
         SR    R0,RF                                                            
FIXACX5  ST    R0,WORK+4          STORE 'FIXED' BILL/PAY AGYCOM                 
         BR    R9                                                               
*                                                                               
         LTORG                                                                  
       ++INCLUDE PGSTTAB                                                        
*                                                                               
*        TABLE OF DISPLACEMENTS OF PST FIELDS                                   
*        THAT NEED TO HAVE PENNIES ALLOCATED WHEN COSTS ARE                     
*        ALLOCATED BETWEEN PRODUCTS                                             
*                                                                               
PSTFLDT  DS    0A                  TABLE OF PST FLDS FOR PENNY ALLOC            
         DC    AL4(PSTTAX-PSTAREA)   PST TAX PAYABLE                            
         DC    AL4(PSTTAXPD-PSTAREA) PST TAX PAID                               
         DC    AL4(PST$BS-PSTAREA)   PST TAX PAYABLE DOLLAR BASIS               
         DC    AL4(PST$BSPD-PSTAREA) PST TAX PAID    DOLLAR BASIS               
PSTFLDNM EQU   (*-PSTFLDT)/4       NUMBER OF FIELDS IN TABLE                    
*                                                                               
         EJECT                                                                  
GETWORK  DSECT                                                                  
*                                                                               
DUB      DS    D                                                                
FULL     DS    F                                                                
HALF     DS    H                                                                
HALF2    DS    H                                                                
NET      DS    F                                                                
PARS     DS    5A                                                               
SAVE2W   DS    2F                                                               
P11      DS    PL11                                                             
ELCODE   DS    C                                                                
GETPASS  DS    C                                                                
PRDCODE  DS    CL3                                                              
GPBDCTYP DS    CL1                                                              
GPBDCOS  DS    PL5                                                              
STAX     DS    F                PAYABLE SALES TAX                               
BILELCOD DS    CL1                                                              
SAVEAC   DS    PL3               SAVED AND RESTORED TO PBDACP                   
CRSW     DS    CL1              'C' FOR SPECIAL 'C' RATE HANDLING               
GSTSW    DS    CL1              'G' IF FINDING GST                              
PSTSW    DS    CL1              'P' IF FINDING GST                              
GETGSTSW DS    CL1              C'P'= PAID GST, C'B'= BILLED GST                
*                               GETGSTSW IS FOR GSTGST ROUTINE                  
*                                                                               
COUNTYN  DS    CL1                                                              
WGSTDATE DS    XL3                 DATE USED FIR PST/GST TABLE                  
WPSTIND  DS    CL1                 C'P' BUY HAS BEEN PAID                       
*                                                                               
*        THESE FIELDS BELOW ARE RETURNED TO CALLER                              
*        IF PARAMETER 5 IS POINTING TO =C'COUNT' OR =C'BOTH'                    
WORK     DS    15F                                                              
WRKNET   DS    F                   WORKING NET                                  
WRKGST   DS    F                   WORKING GST                                  
WRKPST   DS    F                   WORKING PST                                  
WRK$BS   DS    F                   WORKING PST $ BASIS                          
WRKPSTC  DS    XL(L'PBYPSTC)       WORKING PST CODES FROM BUY                   
*                                                                               
PSTWORK  DS    XL(PSTFLDNM*3)      3 ITEMS FOR EACH PST ITEM                    
*                                  'PSTFLDNM' PST ITEMS PROCESSED               
         ORG   PSTWORK                                                          
         DS    10XL(L'PSTWORK)     10 PROVINCES                                 
*                                                                               
       ++INCLUDE GVALUES                                                        
GETWORKL EQU   *-GETWORK           WORKAREA LENGTH                              
*                                                                               
         EJECT                                                                  
PVALUESD DSECT                                                                  
       ++INCLUDE PVALUES                                                        
         EJECT                                                                  
REC      DSECT                                                                  
       ++INCLUDE PBUYREC                                                        
         EJECT                                                                  
       ++INCLUDE PBDELEM                                                        
         EJECT                                                                  
ELEM     DSECT                                                                  
*                                                                               
       ++INCLUDE PPAYELEM                                                       
         SPACE 2                                                                
         ORG   ELEM                                                             
       ++INCLUDE PBILELEM                                                       
         ORG   ELEM                                                             
       ++INCLUDE PPRELEM                                                        
PBYPSTD  DSECT PST ELEMENT                                                      
PBYPSTQ  EQU   X'84'               PST ELEMENT ID                               
       ++INCLUDE PBYPSTEL                                                       
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'006GETINSRX  05/01/02'                                      
         END                                                                    
