*          DATA SET MPRDR00UK  AT LEVEL 016 AS OF 05/01/02                      
*CATALP MPRDR00                                                                 
         TITLE 'T51000 - PLANNING - Q FILE MAINTENANCE CONTROLLER'              
T51000   CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 (TWORKL),T51000,RR=R2                                            
         LA    R7,2048(RB)                                                      
         LA    R7,2048(R7)                                                      
*                                                                               
         USING T51000+4096,R7                                                   
         USING SPOOLD,R8                                                        
         USING GEND,RC                                                          
         USING SYSD,R9             SYSTEM SPECIFIC WORK                         
         USING CONHEADH-64,RA      TWA                                          
*                                                                               
         ST    R2,RELO                                                          
         LR    R8,RC                                                            
         LA    RC,SPOOLEND                                                      
         LA    R9,IO               START OF IO 1                                
         AH    R9,=Y(IOLEN)        PLUS TOTAL IOA AREA LENGTH                   
*                                                                               
         LR    RE,R8                                                            
         LH    RF,=Y(TWORKL)       CLEAR STORAGE                                
         XCEF                                                                   
*                                                                               
         ST    R1,SYSPARMS                                                      
         L     RA,4(R1)                                                         
         ST    RA,ATWA                                                          
         MVI   SPACES,C' '                                                      
         MVC   SPACES+1(131),SPACES                                             
*                                                                               
         BAS   RE,SYSINIT          INITIALIZE SYSTEM DEPENDENT                  
*                                                                               
         ST    RB,SYSRB            SYSTEM BASE REGS                             
         BAS   RE,SETRD            SET RD SO GENCON WILL RETURN                 
*                                                                               
         L     R1,SYSPARMS                                                      
         L     R2,8(R1)            A(SYSLIST)                                   
*                                                                               
         L     RF,4(R2)            A(CALLOV)                                    
         XC    DMCB,DMCB                                                        
         MVC   DMCB+4(4),=X'D9000A30'                                           
         LA    R1,DMCB                                                          
         BASR  RE,RF               GO TO CALLOV                                 
         MVC   GENCON,DMCB         SET A(GENERAL CONTROLLER)                    
*                                                                               
         XC    DMCB,DMCB                                                        
         MVC   DMCB+4(4),=X'D9000A70'                                           
         LA    R1,DMCB                                                          
         BASR  RE,RF               GO TO CALLOV                                 
         MVC   NODIO,DMCB          SET A(NODIO)                                 
*                                                                               
         BAS   RE,SETHEADS         ADJUST VARIOUS HEADLINE FIELDS               
         BAS   RE,CHKLIST          HANDLE ANY SPECIAL LIST FUNCTIONS            
         BNZ   XIT                 MAY RETURN WITHOUT USING GENCON              
*                                                                               
         GOTO1 GENCON,DMCB,(R8)    GO THERE - PASS A(WORKING STORAGE)           
         EJECT                                                                  
*        ROUTINES FOR AFTER GENCON                                              
*        -------------------------                                              
         SPACE 2                                                                
EP       DS    0H                  NOTE - GENCON ALWAYS RETURNS HERE            
         ICM   R2,15,CURSFLD       CURSOR POSITIONING                           
         BZ    EP4                                                              
         OI    6(R2),X'40'         SET CURSOR                                   
         XC    CURSFLD,CURSFLD                                                  
*                                  UNSET ANYWHERE LATER                         
EP2      DS    0H                                                               
         BAS   RE,BUMPU                                                         
         BE    EP2B                                                             
         NI    6(R2),X'BF'                                                      
         B     EP2                                                              
*                                                                               
EP2B     DS    0H                                                               
EP4      DS    0H                                                               
*                            **NOTE LOCAL USE OF R6 FOR END OF TWA              
         LA    R6,2048(RA)                                                      
         LA    R6,2048(R6)                                                      
         USING CONHEADH-64+4096,R6                                              
*                                                                               
         CLI   NOPEND,C'Y'         SET TO CLEAR MOVE PENDING                    
         BNE   EP5                                                              
*                                                                               
         OC    MVPENDS,MVPENDS     TEST ANYTHING PENDING                        
         BZ    EP5                                                              
         XC    MVPENDS,MVPENDS     YES CLEAR IT                                 
*                                                                               
EP5      DS    0H                                                               
         OC    MVMNOD(8),MVMNOD    TEST ANY MOVE PENDING                        
         BZ    EP6                                                              
         MVI   MSGNUM,PENDMSG                                                   
         GOTO1 MSGSET                                                           
*                                                                               
EP6      DS    0H                                                               
         CLI   LISTPSW,C'Y'        LIST + PRINT SW                              
         BNE   EP7                                                              
         MVC   CONACT(8),=C'LISTP   '                                           
         OI    CONACTH+6,X'80'                                                  
*                                                                               
EP7      DS    0H                                                               
         B     XIT                 THEN WE'RE THROUGH                           
         DROP  R6                                                               
         SPACE 2                                                                
SETRD    NTR1                      SET SYSRD SO GENCON WILL                     
         ST    RD,SYSRD            ALWAYS RETURN TO ME                          
         B     XIT                                                              
         EJECT                                                                  
*              INITIALIZE SYSTEM DEPENDENT VALUES                               
         SPACE 3                                                                
SYSINIT  NTR1                                                                   
         LA    R2,SYSV                                                          
         LA    R3,SYSVCON                                                       
         LA    R4,VCOUNT                                                        
         SPACE 1                                                                
SYS2     L     R1,0(R3)            RELOCATE SYSTEM VTYPES                       
         A     R1,RELO                                                          
         ST    R1,0(R2)                                                         
         LA    R2,4(R2)                                                         
         LA    R3,4(R3)                                                         
         BCT   R4,SYS2                                                          
         SPACE 1                                                                
         LA    R2,VCOMMON          SET UP COMMON ENTRIES                        
         SR    R3,R3                                                            
         LA    R4,SYSCOMM                                                       
         LA    R5,VCOUNT                                                        
         SPACE 1                                                                
SYS4     ST    R2,0(R4)                                                         
         STC   R3,0(R4)                                                         
         LA    R3,4(R3)                                                         
         LA    R4,4(R4)                                                         
         BCT   R5,SYS4                                                          
         SPACE 1                                                                
*                                  SET AREA FOR NODIO AND MPQFAC WORK           
         LA    RF,SYSD                                                          
         AH    RF,=Y(SYCOMSV2-SYSD)                                             
         MVC   0(8,RF),=C'*NODBLK*'                                             
         LA    RF,8(RF)                                                         
         ST    RF,VNODBLK                                                       
         LA    RF,NODWKL(RF)                                                    
         LA    RF,7(RF)                                                         
         SRL   RF,3                                                             
         SLL   RF,3                                                             
         MVC   0(8,RF),=C'*MPQBLK*'                                             
         LA    RF,8(RF)                                                         
         ST    RF,VMPQBLK                                                       
*                                                                               
*                                  SET SYSTEM DEPENDENT VALUES                  
         SPACE 1                                                                
         LA    R1,STARTSAV                                                      
         ST    R1,ASTARTSV                                                      
*                                                                               
         MVI   SYSTEM,C'Q'         PERSONNEL                                    
         MVI   GETMSYS,21          USES GETMSG FOR SYSTEM 21                    
         MVI   GETMSYS2,5          BUT SYSTEM 5 FOR MSGS 1-60                   
*                                                                               
         MVI   MAXIOS,3            USES 3 I/O AREAS                             
         MVC   SIZEIO,=F'2000'     EACH I/O IS 2000 BYTES                       
         MVI   NTWA,2              2 SAVED PAGES                                
         MVC   SYSDUMMY,VDUMMY     END OF SYSTEM BASE                           
         MVC   GETUSER,GETAGY      ROUTINE TO GET USER NAME AND ADDRESS         
*                                                                               
         MVC   LKEY,=H'32'         DETAILS OF DIRECTORY AND KEY                 
         MVC   LSTATUS,=H'4'                                                    
         MVC   DATADISP,=H'42'                                                  
         MVC   SYSFIL,=C'MPLFIL  '                                              
         MVC   SYSDIR,=C'MPLDIR  '                                              
*                                                                               
         MVC   LWORK,=A(TWORKL)    TOTAL SIZE OF NMOD AREA                      
         MVC   RCPROG(2),=C'MP'    PREFIX FOR REPORT NO.                        
         MVC   SYSPHASE,=X'D9051000'    PRESET FOR SYSTEM CALLOVS               
         LA    R1,RECACT           RECORD/ACTION DIRECTORY                      
         ST    R1,ARECACT                                                       
         SPACE 3                                                                
*        SET UP CERTAIN ROUTINE ADDRESSES-  (CANT WAIT FOR GENCON)              
         SPACE 1                                                                
         L     R1,SYSPARMS                                                      
         LM    R2,R4,8(R1)         A(SYSLIST) A(TIA) A(COMFACS)                 
         ST    R4,ACOMFACS                                                      
         USING COMFACSD,R4                                                      
         ST    R3,ATIA                                                          
         MVC   CALLOV,CCALLOV                                                   
         MVC   CASHVAL,CCASHVAL                                                 
         MVC   DATAMGR,CDATAMGR                                                 
         MVC   GETMSG,CGETMSG                                                   
         MVC   HELLO,CHELLO                                                     
         MVC   SCANNER,CSCANNER                                                 
*                                                                               
         MVC   NOVAL,=X'80000000'  CONVENTION FOR NO VALUE                      
         B     XIT                                                              
         SPACE 3                                                                
*        ADJUST VARIOUS HEADLINE FIELDS                                         
*                                                                               
SETHEADS DS    0H                                                               
*                            **NOTE LOCAL USE OF R6 FOR END OF TWA              
         LA    R6,2048(RA)                                                      
         LA    R6,2048(R6)                                                      
         USING CONHEADH-64+4096,R6                                              
*                                                                               
*                                  REMOVE PRINT FIELDS IF IRRELEVANT            
         MVI   LISTPSW,C'N'        LIST + PRINT SW OFF                          
         CLC   =C'REP',CONACT      NEEDED FOR REPORT                            
         BE    SHD4                                                             
         CLC   =C'RUN',CONACT      RUN                                          
         BE    SHD4                                                             
         CLC   =C'LISTP',CONACT    AND LISTP                                    
         BNE   SHD3                                                             
         MVI   LISTPSW,C'Y'        LIST + PRINT SW ON                           
         B     SHD4                                                             
*                                     ELSE REMOVE                               
SHD3     DS    0H                                                               
         TM    CONWHENH-15+1,X'0C'    TEST ALREADY NIL                          
         BNZ   SHD6                                                             
         OI    CONWHENH-15+1,X'0C'    SET NIL INTENSITY                         
         OI    CONOUTH-15+1,X'0C'                                               
         OI    CONDESTH-15+1,X'0C'                                              
         OI    CONOTHH-15+1,X'0C'                                               
*                                                                               
         XC    CONWHEN,CONWHEN                                                  
         OI    CONWHENH+6,X'80'    TRANSMIT                                     
         MVI   CONWHENH+5,0                                                     
         OI    CONWHENH+1,X'20'    PROTECT                                      
*                                                                               
         XC    CONOUT,CONOUT                                                    
         OI    CONOUTH+6,X'80'     TRANSMIT                                     
         MVI   CONOUTH+5,0                                                      
         OI    CONOUTH+1,X'20'     PROTECT                                      
*                                                                               
         XC    CONDEST,CONDEST                                                  
         OI    CONDESTH+6,X'80'    TRANSMIT                                     
         MVI   CONDESTH+5,0                                                     
         OI    CONDESTH+1,X'20'    PROTECT                                      
*                                                                               
         XC    CONOTH,CONOTH                                                    
         OI    CONOTHH+6,X'80'     TRANSMIT                                     
         MVI   CONOTHH+5,0                                                      
         OI    CONOTHH+1,X'20'     PROTECT                                      
         B     SHD6                                                             
*                                                                               
SHD4     DS    0H                                                               
         TM    CONWHENH-15+1,X'0C'    TEST ALREADY NORMAL                       
         BZ    SHD6                                                             
         NI    CONWHENH-15+1,X'F3'    RETURN TO NORMAL INTENSITY                
         OI    CONWHENH-15+6,X'80'    TRANSMIT                                  
         NI    CONOUTH-15+1,X'F3'                                               
         OI    CONOUTH-15+6,X'80'                                               
         NI    CONDESTH-15+1,X'F3'                                              
         OI    CONDESTH-15+6,X'80'                                              
         NI    CONOTHH-15+1,X'F3'                                               
         OI    CONOTHH-15+6,X'80'                                               
         NI    CONWHENH+1,X'DF'       UN-PROTECT                                
         OI    CONWHENH+6,X'80'       TRANSMIT                                  
         NI    CONOUTH+1,X'DF'                                                  
         OI    CONOUTH+6,X'80'                                                  
         NI    CONDESTH+1,X'DF'                                                 
         OI    CONDESTH+6,X'80'                                                 
         NI    CONOTHH+1,X'DF'                                                  
         OI    CONOTHH+6,X'80'                                                  
*                                                                               
SHD6     DS    0H              RESET RECORD FIELD AFTER QSPEC SELECT            
         TM    AQLIST,X'80'        TEST DOING QSPEC LIST                        
         BZ    SHD7                                                             
         CLC   =C'SEL',CONACT      AFTER A SELECT                               
         BNE   SHD7                                                             
         MVC   CONREC,=C'QSPEC   ' RESET RECORD TO QSPEC                        
         OI    CONRECH+6,X'80'                                                  
         MVI   TWALREC,RECQSP                                                   
*                                                                               
SHD7     DS    0H                                                               
         BR    RE                                                               
         DROP  R6                                                               
         EJECT                                                                  
*              SYSTEM ROUTINES ENTERABLE FROM BASE OR OVERLAY                   
         SPACE 3                                                                
         DS    0H                                                               
VCOMMON  NTR1  BASE=SYSRB                                                       
         LA    R7,2048(RB)         2ND BASE REG                                 
         LA    R7,2048(R7)                                                      
         ST    RD,COMMRD                                                        
         SRL   RF,24                                                            
         B     VBRANCH(RF)                                                      
         SPACE 1                                                                
VBRANCH  B     VUSER                                                            
         B     VFREE                                                            
         B     DFREE                                                            
         B     VSURV                                                            
         B     VWAVE                                                            
         B     DWAVE                                                            
         B     VQSP                                                             
         B     VNUM                                                             
         B     VMULTIF                                                          
         B     VCLRSCR                                                          
         B     VNDERRP                                                          
         B     VMSGSET                                                          
         B     VSURVXT                                                          
         B     VTARG                                                            
         B     VSPEC                                                            
         B     DCOUNT                                                           
         B     VMATCH                                                           
         B     VWGT                                                             
         B     VADDRSET                                                         
         SPACE 1                                                                
VCOUNT   EQU   (*-VBRANCH)/4                                                    
         EJECT                                                                  
*              USER AGENCY                                                      
         SPACE 3                                                                
VUSER    DS    0H                                                               
         LA    R9,IO               POINT R9 TO SYSD                             
         AH    R9,=Y(IOLEN)                                                     
*                                                                               
         CLI   SVUSRNAM,C' '       DO ONE TIME ONLY                             
         BH    XIT                                                              
*                                  AGENCY NAME & ADDRESS FROM ID REC.           
         MVC   FILENAME,=CL8'CTFILE'                                            
         MVI   USEIO,C'Y'                                                       
         XC    KEY,KEY                                                          
         LA    R4,KEY                                                           
         USING CTIKEY,R4                                                        
         MVI   CTIKTYP,C'I'                                                     
         MVC   CTIKID+8(2),TWAORIG                                              
         GOTO1 READ                                                             
         L     R4,AIO                                                           
         LA    R6,CTIDATA                                                       
         MVI   ELCODE,X'36'                                                     
         MVI   SVUSRNAM,C'*'                                                    
         BAS   RE,FIRSTEL                                                       
         BNE   VUSER2                                                           
         USING CTORGD,R6                                                        
         MVC   USERNAME,CTORGNAM                                                
         MVC   USERADDR,CTORGADD                                                
         MVC   SVUSRNAM,USERNAME                                                
         MVC   SVUSRADR,USERADDR                                                
         SPACE 1                                                                
VUSER2   XC    FILENAME,FILENAME                                                
         MVI   USEIO,0                                                          
         B     XIT                                                              
         EJECT                                                                  
*              HANDLE COMMENT TYPE ELEMENTS                                     
*              VALIFREE AND DISPFREE ARE CONTROLLED BY-                         
*                FREEKEY- 'KEY' FOR THE ELEMENT                                 
*                FREEKLN- LENGTH OF 'KEY'                                       
*                FREECTL- CONTROL BYTE, X'80'=REQUIRED                          
*                                       X'40'=RETURN 1 IN WORK                  
*                FREEMAX- MAXIMUM LINES/ELEMS                                   
*                FREEFRST- FIRST TIME FOR DISPS                                 
         SPACE 3                                                                
VFREE    CLI   MODE,DISPREC                                                     
         BE    DFREE                                                            
         LR    R5,R2               SAVE FIRST FIELD                             
         ZIC   R0,FREEMAX          MAX FIELDS                                   
         SR    R4,R4               SEQUENCE NUMBER                              
*                               DELETE ELEMS WITH RIGHT 'KEY'                   
         ZIC   R6,FREEKLN                                                       
         GOTO1 HELLO,DMCB,(C'D',SYSFIL),(ELCODE,AIO),((R6),FREEKEY)             
         SPACE 1                                                                
VFREE2   DS    0H                                                               
         CLI   5(R2),0                                                          
         BE    VFREE4                                                           
         LA    R4,1(R4)            BUMP SEQUENCE                                
*                                                                               
         MVC   ELEMENT(1),ELCODE   BUILD AN ELEMENT                             
         ZIC   R1,FREEKLN                                                       
         LA    R1,ELEM+2(R1)       SEQ AFTER 'KEY'                              
         STC   R4,0(R1)            SEQUENCE NO.                                 
         ZIC   R3,5(R2)                                                         
         BCTR  R3,0                                                             
         EX    R3,*+8                                                           
         B     *+10                                                             
         MVC   1(0,R1),8(R2)       MOVE IN DATA                                 
*                                                                               
         LA    R3,4(R3)                                                         
         CLI   FREEKLN,0                                                        
         BE    VFREE3                                                           
         ZIC   R1,FREEKLN                                                       
         BCTR  R1,R0                                                            
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   ELEM+2(0),FREEKEY   SET KEY                                      
         LA    R3,1(R3,R1)         ADD IN KEY LENGTH                            
*                                                                               
VFREE3   DS    0H                                                               
         STC   R3,ELEMENT+1          LENGTH OF L'DATA+L'KEY+3                   
         GOTO1 ADDELEM                                                          
         BAS   RE,BUMPU                                                         
         BCT   R0,VFREE2                                                        
*                                                                               
VFREE4   DS    0H                                                               
         STC   R4,ACTUAL           SET COUNT OF ELEMS                           
         TM    FREECTL,X'80'       TEST INPUT REQUIRED                          
         BZ    VFREE6                                                           
         LTR   R4,R4                                                            
         BP    VFREE6                                                           
         LR    R2,R5               RESTORE R2 TO FIRST FIELD                    
         MVI   ERROR,MISSING                                                    
         B     TRAPERR                                                          
*                                                                               
VFREE6   DS    0H                                                               
         B     XIT                                                              
         SPACE 2                                                                
DFREE    DS    0H                  --DISPLAY--                                  
         TM    FREECTL,X'40'       IF RETURNING DATA IN WORK                    
         BZ    DFREE1                                                           
         LA    R0,255              NO MAX                                       
         CLI   FREEFRST,C'N'       TEST FIRST TIME                              
         MVI   FREEFRST,C'N'       RESET                                        
         BNE   DFREE1D             YES                                          
         L     R6,FREELAST         NO-START AFTER LAST ELEM DONE                
         B     DFREE2                                                           
*                                                                               
DFREE1   DS    0H                                                               
         ZIC   R0,FREEMAX          DISPLAY UP TO MAX FIELDS                     
*                                                                               
DFREE1D  DS    0H                                                               
         SR    R4,R4               FOR ELEM COUNT                               
         L     R6,AIO              USER SUPPLIED ELCODE                         
         BAS   RE,GETEL                                                         
         B     *+8                                                              
         SPACE 1                                                                
DFREE2   BAS   RE,NEXTEL                                                        
         BNE   DFREE4                                                           
         CLI   FREEKLN,0           IF NO 'KEY' - OK                             
         BE    DFREE3                                                           
         ZIC   R1,FREEKLN          ELSE MUST MATCH                              
         BCTR  R1,R0                                                            
         EX    R1,*+8                                                           
         B     *+10                                                             
         CLC   2(0,R6),FREEKEY                                                  
         BNE   DFREE2                                                           
*                                                                               
DFREE3   DS    0H                                                               
         LA    R4,1(R4)                                                         
         STC   R4,ACTUAL           ELEM COUNT                                   
         MVC   WORK,SPACES                                                      
         ZIC   R1,1(R6)                                                         
         ZIC   RF,FREEKLN                                                       
         SR    R1,RF               LESS KEY LENGTH                              
         LA    RE,3(R6,RF)         POINT TO DATA                                
         SH    R1,=H'4'                                                         
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   WORK(0),0(RE)       PICK OUT DATA                                
         ST    R6,FREELAST         A(LAST ELEM DONE)                            
         TM    FREECTL,X'40'       OPTION TO RETURN 1 FLD IN WORK               
         BNZ   XIT                                                              
         BAS   RE,GENDISP                                                       
         BAS   RE,BUMPU                                                         
         BCT   R0,DFREE2                                                        
*                                                                               
         B     XIT                                                              
         SPACE 1                                                                
DFREE4   MVC   WORK,SPACES         CLEAR REST                                   
         TM    FREECTL,X'40'       OPTION TO RETRUN 1 FIELD                     
         BNZ   XIT                                                              
         BAS   RE,GENDISP                                                       
         BAS   RE,BUMPU                                                         
         BCT   R0,DFREE4                                                        
         B     XIT                                                              
         EJECT                                                                  
*        ADDRSET- RESET MOVABLE ADDRESSES                                       
         SPACE 2                                                                
VADDRSET DS    0H                                                               
         L     R5,VNODBLK                                                       
         USING NODBLKD,R5                                                       
         MVC   NDRECUP,RECUP                                                    
         MVC   NDTSCAN,TSCAN                                                    
         MVC   NDIOA,AIO1                                                       
         MVC   NDIOA2,AIO2                                                      
*                                                                               
         L     R5,VMPQBLK                                                       
         USING MPQBLKD,R5                                                       
         MVC   MPQBNDIO,NODIO                                                   
         MVC   MPQBQFAC,MPQFAC                                                  
         MVC   MPQBQTRN,MPQTRAN                                                 
         MVC   MPQBNDBL,VNODBLK                                                 
         MVC   MPQBIOA,AIO2        USE 2ND IOA                                  
         LH    RF,=Y(MQSTACK-SYSD)                                              
         AR    RF,R9                                                            
         ST    RF,MPQBSTK          A(STACK AREA)                                
         B     XIT                                                              
         DROP  R5                                                               
*                                                                               
         EJECT                                                                  
*              SYSTEM VALIDATION ROUTINES                                       
         SPACE 3                                                                
*        NOTE- FOR KEY VALIDATION ROUTINES R4 POINTS TO KEY                     
*        NOTE- ALL OVERLAYS USE VALSURV                                         
*                                                                               
         SPACE 2                                                                
VSURV    DS    0H                                                               
         MVC   KEY,SVQSDKEY        USE SAVED KEY                                
         TM    4(R2),X'20'         TEST ALREADY VALIDATED                       
         BNZ   VSURV11                                                          
         USING QSDKEY,R4                                                        
         MVI   QSDKSYS,C'Q'                                                     
         MVI   QSDKRCD,X'24'       QSPEC MASTER                                 
         CLI   5(R2),0                                                          
         BNE   VSURV3                                                           
*                                                                               
         CLI   ACTNUM,ACTLIST      IF NO DATA, MUST BE LIST                     
         BNE   VSURV3                                                           
         CLI   RECNUM,RECSUR       OF SURVEY ITSELF                             
         BNE   VSURV3                                                           
         MVC   SVQSDKEY,KEY                                                     
         B     XIT                                                              
*                                                                               
VSURV3   DS    0H                                                               
         GOTO1 ANY                                                              
         MVC   QSDKFCD,WORK                                                     
*                                                                               
         CLI   RECNUM,RECSUR       SPECIALS FOR SURVEY RECORD                   
         BNE   VSURV9                                                           
         CLI   ACTNUM,ACTLIST      FOR LIST                                     
         BNE   VSURV8                                                           
         CLI   ACTNUM,ACTREST      AND RESTORE                                  
         BNE   VSURV8                                                           
         MVC   SVQSDKEY,KEY        JUST SAVE KEY AND EXIT                       
         B     VSURV11                                                          
*                                                                               
VSURV8   DS    0H                                                               
         CLI   ACTNUM,ACTADD       FOR ADD                                      
         BNE   VSURV9                                                           
         XC    SVQSDKEY,SVQSDKEY   CLEAR KEY AND EXIT                           
         BAS   RE,NODSET                                                        
         B     VSURV11                                                          
*                                                                               
VSURV9   DS    0H                                                               
         CLC   QSDKEY,SVQSDKEY     DO WE ALREADY HAVE                           
         BE    VSURV9B                                                          
*                                                                               
         GOTO1 HIGH                                                             
         CLC   QSDKEY,KEYSAVE                                                   
         BE    *+12                                                             
         MVI   ERROR,NOTFOUND                                                   
         B     TRAPERR                                                          
*                                                                               
         MVC   SVQSDKEY,KEY                                                     
*                                                                               
         ST    R2,KCHGFLD          SAVE HEADER OF CHGD KEY FLD                  
         MVI   KEYCHG,C'Y'         SET HAVE KEY CHANGE                          
*                                                                               
         XC    SQDCLR,SQDCLR       CLEAR QSPEC DATA                             
         XC    SQTOPD,SQTOPD                                                    
         XC    SQQUED,SQQUED                                                    
         XC    SQANSD,SQANSD                                                    
         BAS   RE,NODSET           SET NODIO AND READ MASTER                    
         GOTO1 SURVXT              EXTRACT SURVEY DATA                          
*                                                                               
VSURV9B  DS    0H                                                               
         OI    4(R2),X'20'         SET VALIDATED                                
*                                                                               
VSURV11  DS    0H                                                               
         B     XIT                                                              
         SPACE 3                                                                
VWAVE    DS    0H                  VALIDATE WAVE DATE EXPRESSION                
         MVI   ERROR,0                                                          
         CLI   OPTION2,C'V'        TST ALREADY HAVE COMPRESSED DATE             
         BE    VWVE3                                                            
         XC    WAVDATE,WAVDATE     RETURN DATE IN WAVDATE                       
         CLI   OPTION2,C'W'        DATA IS IN WORK, NOT SCREEN FLD              
         BE    VWVE2                                                            
*                                                                               
         CLI   5(R2),0                                                          
         BE    XIT                                                              
         GOTO1 ANY                                                              
*                                                                               
VWVE2    DS    0H                                                               
         GOTO1 DATVAL,DMCB,(2,WORK),DUB                                         
         OC    DMCB(4),DMCB        TEST VALID                                   
         BNZ   *+12                                                             
         MVI   ERROR,INVDATE                                                    
         B     VWVE91                                                           
         MVC   DUB+4(2),=C'01'     SET TO FIRST OF MONTH                        
         GOTO1 DATCON,DMCB,DUB,(2,WAVDATE) CMPRESSED DATE IN WAVDATE            
*                                                                               
*                                  FIND DATE IN WAVE LIST                       
         CLI   OPTION,C'N'        TEST TO SKIP                                  
         BE    XIT                                                              
*                                                                               
VWVE3    DS    0H                                                               
         LA    R4,SURWAVS                                                       
         USING SURWVD,R4                                                        
         CLI   WAVDATE,0           IF ZERO USE FIRST WAVE                       
         BE    VWVE6                                                            
         LA    R0,MAXWAVS                                                       
*                                                                               
VWVE4    DS    0H                                                               
         CLC   WAVDATE,SURWVDT                                                  
         BE    VWVE6                                                            
         LA    R4,SURWVDL(R4)                                                   
         BCT   R0,VWVE4                                                         
         MVI   ERROR,WAVNFERR      WAVE NOT FOUND                               
         B     VWVE91                                                           
*                                                                               
VWVE6    DS    0H                                                               
         MVC   WAVDESC,SURWVDSC                                                 
*                                                                               
         L     R5,VMPQBLK          SET IN MPQBLK                                
         USING MPQBLKD,R5                                                       
         CLC   MPQBWVDT,SURWVDT    TEST WAVE DATE CHANGED                       
         BE    *+8                                                              
         MVI   FORCESPC,C'Y'       YES- FORCE RE-VALIDATE SPECS                 
         MVC   MPQBWVDT,SURWVDT    WAVE DATE                                    
         MVC   MPQBQVD,SURWVDN     INVERTED FILE DIRECTORY                      
         MVC   MPQBQVF,SURWVFN     INVERTED FILE                                
         MVC   MPQBNRES,SURWVRC    RESP COUNT                                   
         DROP  R4                                                               
         DROP  R5                                                               
*                                                                               
         B     XIT                                                              
*                                                                               
VWVE91   DS    0H                                                               
         CLI   OPTION2,C'W'        IF DATA FROM WORK                            
         BE    XIT                 JUST EXIT                                    
         B     TRAPERR             ELSE DO ERROR NOW                            
         SPACE 2                                                                
*        DWAVE  - DISPLAY WAVE DATE (IN WAVDATE)                                
         SPACE 1                                                                
DWAVE    DS    0H                                                               
         MVC   WORK(10),SPACES                                                  
         GOTO1 DATCON,DMCB,(2,WAVDATE),(6,WORK)                                 
         B     XIT                                                              
         SPACE 3                                                                
*        VALWGT- VALIDATE WEIGHT CODE   (CODE IS IN WORK)                       
         SPACE 2                                                                
VWGT     DS    0H                                                               
         L     R3,VMPQBLK          SET MPQBLK WEIGHT INFO                       
         USING MPQBLKD,R3                                                       
*                                                                               
         XC    MPQBWCRD(5),MPQBWCRD  CLEAR TO START                             
         MVI   MPQBWPRE,0                                                       
         MVI   MPQBWDLN,0                                                       
*                                                                               
         CLC   =C'NONE',WORK       UNLESS 'NONE'                                
         BE    VWGT4                                                            
         CLI   WORK,C' '          IF WEIGHT PRESENT VALIDATE                    
         BH    VWGT1D                                                           
         LA    R4,SURDFWGT         ELSE USE DEFAULT WEIGHT                      
         B     VWGT3                                                            
*                                                                               
VWGT1D   DS    0H                                                               
         LA    R4,SURWGTS          WEIGHT LIST                                  
         USING SURWD,R4                                                         
         LA    R5,MAXWGTS                                                       
*                                                                               
VWGT2    DS    0H                                                               
         CLC   SURWGTC,WORK                                                     
         BE    VWGT3                                                            
         LA    R4,SURWDL(R4)                                                    
         BCT   R5,VWGT2                                                         
*                                                                               
         MVI   ERROR,NOTFOUND                                                   
         B     TRAPERR                                                          
*                                                                               
VWGT3    DS    0H                                                               
         MVC   MPQBWCRD,SURWCRD                                                 
         MVC   MPQBWCOL,SURWCOL                                                 
         MVC   MPQBWLEN,SURWFLN                                                 
         MVC   MPQBWPRE,SURWPRE                                                 
         MVC   MPQBWDLN,SURWBLN                                                 
         DROP  R3                                                               
*                                                                               
VWGT4    DS    0H                                                               
         B     XIT                                                              
         EJECT                                                                  
         SPACE 2                                                                
VNUM     DS    0H                  VALIDATE NUMERIC DATA IN WK                  
         ZIC   R0,LWK              LENGTH                                       
         LA    R1,WK                                                            
*                                                                               
VNUM4    DS    0H                                                               
         CLI   0(R1),C'0'                                                       
         BL    TRAPERR                                                          
         CLI   0(R1),C'9'                                                       
         BH    TRAPERR                                                          
         LA    R1,1(R1)                                                         
         BCT   R0,VNUM4                                                         
*                                                                               
         ZIC   R1,LWK                                                           
         BCTR  R1,R0                                                            
         EX    R1,VNPACK                                                        
         CVB   R0,DUB                                                           
         ST    R0,FULL                                                          
         B     XIT                                                              
*                                                                               
VNPACK   PACK  DUB,WK(0)                                                        
         SPACE 3                                                                
*        MULTI FIELD CHECK- LOOK AT MAX UNPROTECTED FIELDS                      
*        IF ALL EMPTY OR ALL NON-EMPTY RETURN WITH CC SET                       
*        ELSE ERROR- MISSING DATA AT FIRST EMPTY FIELD                          
*                                                                               
VMULTIF  DS    0H                                                               
         ZIC   R4,MAX              NUMBER OF FIELDS TO CHECK                    
         SR    R5,R5                                                            
         SR    R6,R6                                                            
         B     *+8                                                              
*                                                                               
VMUF2    DS    0H                                                               
         BAS   RE,BUMPU                                                         
         CLI   5(R2),0                                                          
         BZ    *+12                                                             
         LA    R5,1(R5)            BUMP NON-EMPTY COUNT                         
         B     VMUF6                                                            
*                                                                               
         LTR   R6,R6               SAVE A(1ST EMTPY FIELD)                      
         BNZ   *+6                                                              
         LR    R6,R2                                                            
*                                                                               
VMUF6    DS    0H                                                               
         CLI   OPTION,1            TEST ANY SKIPPING                            
         BNH   VMUF7                                                            
         ZIC   R0,OPTION                                                        
         BCTR  R0,R0                                                            
         BAS   RE,BUMPU                                                         
         BCT   R0,*-4                                                           
*                                                                               
VMUF7    DS    0H                                                               
         BCT   R4,VMUF2            NEXT FIELD                                   
*                                                                               
         LTR   R5,R5               IF ALL EMPTY                                 
         BZ    ZEROX               EXIT WITH CC =                               
         ZIC   R4,MAX                                                           
         CR    R5,R4               IF ALL NON-EMPTY                             
         BE    NONZX               EXIT WITH CC NOT =                           
*                                                                               
         LR    R2,R6               ELSE POINT TO 1ST EMTPY FIELD                
         MVI   ERROR,MISSING                                                    
         B     TRAPERR                                                          
         SPACE 3                                                                
*        CLRSCR- CLEAR SCREEN FIELDS.                                           
*                PARAM1  BYTE 0      P(PROTECTED),U(UNPROT),A(ALL)              
*                        BYTES 1-3   A(FIRST FIELD)                             
*                PARAM2  BYTES 1-3   A(LAST FIELD) OR NUMBER OF FIELDS          
*                                                                               
VCLRSCR  DS    0H                                                               
         L     R2,0(R1)            A(FIRST FIELD)                               
         LA    R2,0(R2)            CLEAR HOB                                    
         L     R3,4(R1)            A(LAST FIELD) OR NUMBER OF FIELDS            
         CR    R2,R3               IF R2 LESS R3, R3 IS LAST FIELD              
         BL    CLRS2B                                                           
*                                                                               
         L     R4,4(R1)            ELSE IS NUMBER OF FIELDS                     
         LTR   R4,R4               IF ZERO                                      
         BNZ   *+8                                                              
         LA    R4,1000             CLEAR TO END OF SCREEN                       
         LA    R3,4095(R2)                                                      
         B     CLRS2B                                                           
*                                                                               
CLRS2    DS    0H                                                               
         CR    R2,R3               TEST VS USERS LAST                           
         BNL   CLRSX                                                            
         CLI   0(R2),9             TEST EOS                                     
         BL    CLRSX                                                            
         BAS   RE,BUMP                                                          
*                                                                               
CLRS2B   DS    0H                                                               
         CLI   0(R1),C'A'          ALL FIELDS                                   
         BE    CLRS3                                                            
         CLI   0(R1),C'P'          PROTECTED ONLY                               
         BNE   *+16                                                             
         TM    1(R2),X'20'                                                      
         BZ    CLRS4               SKIP                                         
         B     CLRS3                                                            
*                                                                               
         TM    1(R2),X'20'         UNPROTECTED ONLY                             
         BNZ   CLRS4                                                            
*                                                                               
CLRS3    DS    0H                                                               
         ZIC   RF,0(R2)                                                         
         SH    RF,=H'9'                                                         
         EX    RF,CLRSOC           OR WITH SPACES                               
         EX    RF,CLRSCLC          COMPARE TO SPACES                            
         BE    CLRS4                                                            
         EX    RF,CLRSMVC          SET TO SPACES                                
         OI    6(R2),X'80'         TRANSMIT                                     
*                                                                               
CLRS4    DS    0H                                                               
         BCT   R4,CLRS2                                                         
*                                                                               
CLRSX    DS    0H                                                               
         B     XIT                                                              
*                                                                               
CLRSOC   OC    8(0,R2),SPACES                                                   
CLRSCLC  CLC   8(0,R2),SPACES                                                   
CLRSMVC  MVC   8(0,R2),SPACES                                                   
         SPACE 2                                                                
*        NODSET- ESTABLISH NODIO DSECT                                          
         SPACE 2                                                                
NODSET   NTR1                                                                   
         L     R5,VNODBLK                                                       
         USING NODBLKD,R5                                                       
*                                                                               
         LR    RE,R5                                                            
         LA    RF,NODWKL                                                        
         XCEF                                                                   
*                                                                               
         MVC   NDKEY,SVQSDKEY                                                   
         MVC   NDKLEN,LKEY                                                      
         MVC   NDELSTRT,DATADISP                                                
         MVC   NDRMAX,SIZEIO+2                                                  
         MVC   NDCNTL,=H'34'                                                    
         MVC   NDDISK,=H'36'                                                    
         MVC   NDRLEN,=H'32'                                                    
*                                                                               
         MVC   NDDIRNAM,SYSDIR                                                  
         MVC   NDFILNAM,SYSFIL                                                  
         MVI   NDWRITE,C'Y'                                                     
         MVI   NDDELIM,C'.'                                                     
         MVI   NDMXLEVS,3                                                       
*                                                                               
NODS8    DS    0H                  ADDRESSES SET EACH TIME                      
         MVC   NDDMGR,DATAMGR                                                   
         MVC   NDRECUP,RECUP                                                    
         MVC   NDTSCAN,TSCAN                                                    
*                                                                               
         OC    SVQSDKEY,SVQSDKEY   UNLESS ADDING MASTER                         
         BZ    NODS9                                                            
*                                  DO A DUMMY NODIO TO 'OPEN' AND               
*                                  TO GET MASTER (SURVEY DEF REC)               
*                                                                               
         MVC   NDIOA2,AIO1         THIS WILL GET MASTER IN AIO1                 
         MVC   NDIOA,AIO2                                                       
         MVC   WORK,SPACES                                                      
         GOTO1 NODIO,DMCB,VNODBLK,=C'READ',WORK                                 
         CLI   NDERR,0                                                          
         BE    *+6                                                              
         DC    H'0'                NO ERROR SHOULD HAPPEN HERE                  
*                                                                               
NODS9    DS    0H                                                               
         MVC   NDIOA,AIO1          NORMALLY USE 1ST AND 2ND IOAREAS             
         MVC   NDIOA2,AIO2                                                      
*                                                                               
         B     XIT                                                              
*                                                                               
         DROP  R6                                                               
         EJECT                                                                  
*        SURVXT- EXTRACT SURVEY DEFINITION DATA                                 
*                AND SET MPQBLKD                                                
         SPACE 2                                                                
VSURVXT  DS    0H                                                               
         L     R5,VMPQBLK                                                       
         USING MPQBLKD,R5                                                       
*                                                                               
         LA    R4,SVQSDKEY                                                      
         USING QSDKEY,R4                                                        
         MVC   MPQBFCD,QSDKFCD     FILE CODE                                    
         DROP  R4                                                               
*                                                                               
         MVC   MPQBQSD,SYSDIR      QSPEC DIR                                    
         MVC   MPQBQSF,SYSFIL      QSPEC FILE                                   
         MVC   MPQBAGY,TWAAGY      AGENCY CODE                                  
*                                                                               
         L     R6,AIO                                                           
         MVI   ELCODE,X'10'        DEFINITION ELEMENT                           
         BAS   RE,GETEL                                                         
         USING QSDEFEL,R6                                                       
         BE    *+6                                                              
         DC    H'0'                DEFINITION ELEMENT MISSING                   
*                                                                               
         MVC   MPQBNCRD,QSDNCRDS   HIGH CARD NO                                 
         MVC   MPQBNCOL,QSDNCOLS   HIGH COL NO                                  
*                                                                               
         MVC   MPQBDMGR,DATAMGR                                                 
         MVC   MPQBNDIO,NODIO                                                   
         MVC   MPQBQTRN,MPQTRAN                                                 
         MVC   MPQBQFAC,MPQFAC                                                  
*                                                                               
         MVC   MPQBNDBL,VNODBLK                                                 
         MVC   MPQBIOA,AIO2        USE IOAREA 2 FOR RECS                        
         LH    RF,=Y(MQSTACK-SYSD) STACK AREA IS AT END OF SYSD                 
         AR    RF,R9                AFTER SAVED AREAS                           
         ST    RF,MPQBSTK                                                       
         LH    RF,=Y(SYSDEND-MQSTACK)  LENGTH OF STACK AREA                     
         ST    RF,MPQBSTLN                                                      
*                                                                               
*                                  NB- WEIGHTS TO BE SET LATER                  
*                                  NB- ACCESS NEEDS TO BE SET                   
*                                                                               
         MVC   SURWMRGE,QSDWMRGE   WAVES MERGED CONTROL                         
*                                                                               
*                                  GET SURVEY TITLE                             
         MVI   FREEMAX,1                                                        
         MVI   FREECTL,X'40'       RETURN IN WORK                               
         MVI   FREEKEY,0           NO 'KEY'                                     
         MVI   FREEKLN,1                                                        
         MVI   FREEFRST,C'Y'                                                    
         MVI   ELCODE,X'C1'                                                     
         GOTO1 DISPFREE                                                         
         MVC   SURDESC,WORK                                                     
*                                                                               
*                                                                               
         L     R6,AIO                                                           
         MVI   ELCODE,X'13'        WAVE ELEMENT                                 
         BAS   RE,GETEL                                                         
         B     *+8                                                              
*                                                                               
SURX3    DS    0H                                                               
         BAS   RE,NEXTEL                                                        
         BNE   SURX3D                                                           
         USING QSDWAVEL,R6                                                      
*                                  FOR NOW JUST USE FIRST WAVE                  
*                                  (MAY BE RESET ELSEWHERE)                     
         MVC   MPQBQVD,QSDWVDN     INVERTED FILE DIRECTORY                      
         MVC   MPQBQVF,QSDWVFN     INVERTED FILE                                
         MVC   MPQBWVDT,QSDWVDAT   WAVE DATE                                    
         MVC   MPQBNRES,QSDWVRC    RESP COUNT                                   
*                                                                               
*                                  BUILD LIST OF SURVEY WEIGHTS                 
SURX3D   DS    0H                                                               
         XC    SURWGTS,SURWGTS                                                  
         XC    SURDFWGT,SURDFWGT                                                
         LA    R4,MAXWGTS          MAX NUMBER                                   
         LA    R3,SURWGTS                                                       
         USING SURWD,R3                                                         
         MVI   ELCODE,X'12'                                                     
         L     R6,AIO                                                           
         BAS   RE,GETEL                                                         
         B     *+8                                                              
*                                                                               
SURX4    DS    0H                                                               
         BAS   RE,NEXTEL                                                        
         BNE   SURX6                                                            
*                                                                               
         USING QSDWGTEL,R6                                                      
         CLI   QSDWDFLT,C'Y'       IF DEFAULT WEIGHT                            
         BNE   *+10                                                             
         MVC   SURDFWGT,QSDWGTCD   SET IN DEFAULT AREA                          
*                                                                               
         MVC   SURWGTC(SURWDL),QSDWGTCD   MOVE ALL DATA                         
         LA    R3,SURWDL(R3)                                                    
         MVI   0(R3),X'FF'         SET EOL                                      
         BCT   R4,SURX4                                                         
*                                                                               
         CLI   SURDFWGT,C' '       TEST HAVE A DEFAULT                          
         BNH   SURX6               NO                                           
         MVC   WORK(4),SURDFWGT    YES-LET VALWGT                               
         GOTO1 VALWGT              SET MPQBLKD                                  
*                                  BUILD LIST OF SURVEY WAVE DATES              
SURX6    DS    0H                                                               
         LA    RE,SURWAVS          CLEAR                                        
         LA    RF,L'SURWAVS                                                     
         XCEF                                                                   
         LA    R4,MAXWAVS          MAX NUMBER                                   
         LA    R3,SURWAVS                                                       
         USING SURWVD,R3                                                        
         MVI   ELCODE,X'13'                                                     
         L     R6,AIO                                                           
         BAS   RE,GETEL                                                         
         B     *+8                                                              
*                                                                               
SURX7    DS    0H                                                               
         BAS   RE,NEXTEL                                                        
         BNE   SURX8                                                            
*                                                                               
         USING QSDWAVEL,R6                                                      
         MVC   SURWVDT(22),QSDWVDAT   MOVE DATE,DIR,FIL,RESPC                   
         MVC   SURWVDSC,QSDWVDSC      DESCRIPTION                               
*                                                                               
         LA    R3,SURWVDL(R3)                                                   
         MVI   0(R3),X'FF'         SET EOL                                      
         BCT   R4,SURX7                                                         
*                                                                               
SURX8    DS    0H                                                               
         B     XIT                                                              
         EJECT                                                                  
*        VALQSP- VALIDATE QSPEC AND SET SQDATA                                  
*                (**NB- QSPEC MUST BE IN WORK, CALLER TO DO 'ANY')              
         SPACE 2                                                                
VQSP     DS    0H                                                               
         TM    4(R2),X'20'         TEST VALIDATED                               
         BC    0,VQS30             **NB- NO-OP'D TEMPORARILY                    
*                                                                               
         L     R5,VNODBLK                                                       
         USING NODBLKD,R5                                                       
*                                                                               
         XC    SQDCLR,SQDCLR       CLEAR CERTAIN QSPEC CONTROLS                 
         CLI   WORK,C' '           ANY INPUT                                    
         BH    VQS3                YES                                          
*                                                                               
         XC    SQTOPD,SQTOPD       NO- CLEAR ALL DATA                           
         XC    SQQUED,SQQUED                                                    
         XC    SQANSD,SQANSD                                                    
         B     XIT                                                              
*                                                                               
VQS3     DS    0H                                                               
         MVC   SQSAV,WORK          SAVE QSPEC AS ENTERED                        
*                                  FIND END OF QSPEC                            
         LA    RF,WORK+29                                                       
         CLI   0(RF),C' '                                                       
         BH    *+8                                                              
         BCT   RF,*-8              RF AT END                                    
*                                                                               
         CLI   0(RF),C'.'          SHOULD NOT END IN DELIMITER                  
         BE    VQS91                                                            
*                                  NOW CHECK FOR INVALID                        
         LA    R3,WORK             INTERNAL CHARACTERS                          
         LA    R4,1                AND COUNT LEVELS                             
*                                                                               
VQS8     DS    0H                                                               
         CR    R3,RF               TEST AT END                                  
         BH    VQS9                YES- DONE                                    
*                                                                               
         CLI   0(R3),C' '          IMBEDDED BLANKS ARE INVALID                  
         BE    VQS91                                                            
         CLI   0(R3),C','          ALSO COMMAS                                  
         BE    VQS91                                                            
         CLI   0(R3),C')'          AND )                                        
         BE    VQS91                                                            
         CLI   0(R3),C'('          AND (                                        
         BE    VQS91                                                            
         CLI   0(R3),C'='          AND =                                        
         BE    VQS91                                                            
         CLI   0(R3),C'?'          AND QUESTION MARK                            
         BE    VQS91                                                            
         CLI   0(R3),C'!'          AND EXCLAMATION POINT                        
         BE    VQS91                                                            
*                                                                               
         CLI   0(R3),C'.'          DELIMITER                                    
         BNE   *+8                                                              
         LA    R4,1(R4)            BUMP LEVEL COUNT                             
*                                                                               
         LA    R3,1(R3)            NEXT CHARACTER                               
         B     VQS8                                                             
*                                                                               
VQS9     DS    0H                                                               
         STC   R4,SQNLEVS          COUNT OF LEVELS                              
         MVC   SQSPEC,WORK         SAVE AMENDED QSPEC                           
*                                                                               
         LA    RF,VQSPHK                                                        
         ST    RF,NDHOOK                                                        
         MVI   NDREREAD,C'Y'       **NB- TEMP- FORCE NODIO REREAD               
*                                  SET COMMAND                                  
         LA    R4,=C'READ'         READ IF REC NOT A QSPEC                      
         CLI   RECNUM,RECQSP                                                    
         BH    VQS20                                                            
         LA    R4,=C'DEL'          DELETE                                       
         CLI   ACTNUM,ACTDEL                                                    
         BE    VQS20                                                            
         LA    R4,=C'RES'          RESTORE                                      
         CLI   ACTNUM,ACTREST                                                   
         BE    VQS20                                                            
         LA    R4,=C'HIGH'         HIGH                                         
         CLI   ACTNUM,ACTLIST                                                   
         BE    VQS20                                                            
         LA    R4,=C'READ'         READ FOR ALL OTHER ACTIONS                   
*                                                                               
VQS20    DS    0H                                                               
         GOTO1 NODIO,DMCB,VNODBLK,(R4),SQSPEC                                   
*                                                                               
         OC    SQSPEC,SPACES                                                    
         CLI   NDERR,0                                                          
         BNE   VNDERRP                                                          
*                                                                               
VQS30    DS    0H                                                               
         OI    4(R2),X'20'         SET VALIDATED                                
         B     XIT                                                              
         SPACE 2                                                                
VQSPHK   NTR1                                                                   
         L     R3,NDLEVPTR         POINT TO CURRENT LEVEL                       
         USING NDLVTABD,R3                                                      
*                                                                               
         CLI   NDERR,0                                                          
         BE    VQH3                NO ERROR                                     
         CLI   NDERR,NDRNFERR      REC NOT FOUND ERROR                          
         BE    VQH40                                                            
*                                                                               
         B     VNDERRP             HANDLE OTHER NODIO ERROR                     
*                                                                               
VQH3     DS    0H                  -NO ERROR-                                   
         CLI   NDMODE,NDVAL        VALIDATE                                     
         BE    VQH4                                                             
         CLI   NDMODE,NDPROC       PROCESS                                      
         BE    VQH20                                                            
         CLI   NDMODE,NDEND                                                     
         BE    VQH30                                                            
         B     XIT                 RETURN TO NODIO                              
*                                                                               
*                                  MODE = NDVAL- VALIDATE                       
*                                  ------------                                 
VQH4     DS    0H                                                               
         MVC   KEY,NDLVKEY         SAVE LASTEST KEY                             
         LA    RF,KEY                                                           
         AH    RF,LKEY                                                          
         AH    RF,LSTATUS                                                       
         MVC   0(4,RF),NDLVDA      AND DISK ADDRESS                             
         MVC   SVQSPKEY,KEY                                                     
*                                                                               
         L     R6,NDIOA                                                         
         MVI   ELCODE,X'10'        QSPEC TYPE ELEM                              
         BAS   RE,GETEL                                                         
         BNE   VQS94                                                            
         USING QSPELEM,R6                                                       
*                                                                               
         MVC   SQTYP,QSPTYP                                                     
         CLI   NDLEV,1             IF AT LEVEL 1                                
         BNE   VQH7                                                             
         XC    SQTOPD,SQTOPD       CLEAR T,Q,A                                  
         XC    SQQUED,SQQUED                                                    
         XC    SQANSD,SQANSD                                                    
*                                                                               
         CLI   QSPTYP,C'T'         TOPIC                                        
         BE    VQH10                                                            
         CLI   QSPTYP,C'Q'         QUESTION                                     
         BE    VQH13                                                            
         CLI   QSPTYP,C'A'         ANSWER                                       
         BE    VQH16                                                            
         B     VQS94                                                            
*                                                                               
VQH7     DS    0H                                                               
         CLI   NDLEV,2             IF AT LEVEL 2                                
         BNE   VQH8                                                             
         CLI   QSPTYP,C'T'         TOPIC IS ERROR                               
         BE    VQS94                                                            
         CLI   QSPTYP,C'Q'         IF Q AT LEVEL 2                              
         BNE   VQH7D                                                            
         CLI   SQTCOD,C' '         TOPIC MUST BE DEFINED                        
         BNH   VQS94                                                            
         B     VQH13               PROCESS QUESTION                             
*                                                                               
VQH7D    DS    0H                                                               
         CLI   QSPTYP,C'A'         MUST BE ANSWER                               
         BNE   VQS94                                                            
         CLI   SQTCOD,C' '         AND MUST HAVE TOPIC                          
         BH    VQH16                                                            
         CLI   SQQCOD,C' '         OR QUESTION                                  
         BH    VQH16                                                            
         B     VQS94                                                            
*                                                                               
VQH8     DS    0H                  AT LEVEL 3                                   
         CLI   QSPTYP,C'A'         MUST BE ANSWER                               
         BNE   VQS94                                                            
         CLI   SQTCOD,C' '         AND MUST HAVE TOPIC                          
         BH    VQH16                                                            
         CLI   SQQCOD,C' '         OR QUESTION                                  
         BH    VQH16                                                            
         B     VQS94                                                            
*                                                                               
VQH10    DS    0H                                                               
         MVC   SQTCOD,NDLVCOD      SET TOPIC CODE                               
         MVC   SQTLEV,NDLEV        LEVEL                                        
         MVC   SQTDA,NDLVDA        DISK ADDRESS                                 
         MVC   SQTCBPG,QSPCBPG     CODE BOOK PAGE                               
         MVC   SQTQPG,QSPQUPG      QUESTIONNARE PAGE                            
         MVC   SQTWGT,QSPWGTC      WEIGHT CODE                                  
         XC    SQQUED,SQQUED       CLEAR QUEST DATA                             
         XC    SQANSD,SQANSD       CLEAR ANSWER DATA                            
         B     XIT                 RETURN TO NODIO                              
*                                                                               
VQH13    DS    0H                                                               
         MVC   SQQCOD,NDLVCOD      SET QUESTION CODE                            
         MVC   SQQLEV,NDLEV                                                     
         MVC   SQQDA,NDLVDA        DISK ADDRESS                                 
         MVC   SQQCBPG,QSPCBPG     CODE BOOK PAGE                               
         MVC   SQQQPG,QSPQUPG      QUESTIONNARE PAGE                            
         MVC   SQQWGT,QSPWGTC      WEIGHT CODE                                  
         MVC   SQQSTYP,QSPQTYP     N=NUMERIC QUESTION FIELD                     
         XC    SQANSD,SQANSD       CLEAR ANSWER DATA                            
         B     XIT                 RETURN TO NODIO                              
*                                                                               
VQH16    DS    0H                                                               
         MVC   SQACOD,NDLVCOD      SET ANSWER CODE                              
         MVC   SQALEV,NDLEV                                                     
         MVC   SQADA,NDLVDA        DISK ADDRESS                                 
         MVC   SQACBPG,QSPCBPG     CODE BOOK PAGE                               
         MVC   SQAQPG,QSPQUPG      QUESTIONNARE PAGE                            
         MVC   SQAWGT,QSPWGTC      WEIGHT CODE                                  
         MVC   SQASTYP,QSPQTYP     SPEC TYPE (N=NUM FIELD)                      
         CLC   SQALEV,SQQLEV       IF ANS LEVEL                                 
         BH    *+10                NOT HIGHER THAN Q                            
         XC    SQQUED,SQQUED       THEN CLEAR LEFT OVER Q                       
         CLC   SQALEV,SQTLEV       IF ANS LEVEL                                 
         BH    *+10                NOT HIGHER THAN TOPIC                        
         XC    SQTOPD,SQTOPD       THEN CLEAR LEFT OVER TOPIC                   
         B     XIT                 RETURN TO NODIO                              
*                                                                               
*                                  MODE = NDPROC                                
*                                  -------------                                
*                                                                               
VQH20    DS    0H                                                               
         B     VQH4                THEN SAME AS VALIDATE                        
*                                                                               
*                                  MODE = NDEND                                 
*                                  ------------                                 
VQH30    DS    0H                                                               
         CLI   ACTNUM,ACTADD       ARE WE ADDING                                
         BNE   VQH31                                                            
         CLI   RECNUM,RECANS       AND IS IT A T,Q, OR A                        
         BNH   VQH35                                                            
*                                  NO-SET SQTYP AND EXIT                        
VQH31    DS    0H                                                               
         MVI   SQTYP,C'A'          ANS                                          
         CLI   SQACOD,C' '         IF ANS IS PRESENT                            
         BH    VQH33                                                            
         MVI   SQTYP,C'Q'          QUESTION                                     
         CLI   SQQCOD,C' '         IF Q IS PRESENT                              
         BH    VQH33                                                            
         MVI   SQTYP,C'T'          ELSE TOPIC                                   
*                                                                               
VQH33    DS    0H                                                               
         B     XIT                                                              
*                                                                               
VQH35    DS    0H                                                               
         MVI   NDERR,NDADDERR      YES- IF WE GOT THRU READ WITHOUT             
         B     VNDERRP             ERROR THE REC IS ALREADY THERE               
*                                                                               
VQH40    DS    0H                  -REC NOT FOUND ERROR-                        
*                                  ---------------------                        
*                                                                               
         CLI   ACTNUM,ACTADD       UNLESS ADDING                                
         BNE   VNDERRP             ERROR                                        
         CLC   NDLEV,SQNLEVS       MUST BE AT LAST LEVEL                        
         BL    VNDERRP                                                          
*                                                                               
         CLI   RECNUM,RECQSP       MUST BE T,Q, OR A ADD                        
         BH    VNDERRP                                                          
*                                                                               
         CLI   RECNUM,RECTOP       IF TOPIC ADD                                 
         BNE   VQH45                                                            
         CLI   NDLEV,1             MUST BE AT LEVEL 1                           
         BNE   VQS92                                                            
         B     VQH48                                                            
*                                                                               
VQH45    DS    0H                                                               
         CLI   RECNUM,RECQUE       IF QUESTION ADD                              
         BNE   VQH46                                                            
         CLI   NDLEV,2             LEVEL MAY BE 1                               
         BL    VQH48                                                            
         BH    VQS92               BUT NOT 3                                    
         CLI   SQTCOD,C' '         2 WITH TOPIC DEFINED                         
         BNH   VQS92                                                            
         B     VQH48                                                            
*                                                                               
VQH46    DS    0H                                                               
         CLI   RECNUM,RECANS       IF ANSWER ADD                                
         BNE   VQH48                                                            
         CLI   NDLEV,2             LEVEL 1 OR 3 OK                              
         BNE   VQH48                                                            
         CLI   SQTCOD,C' '         OR 2 WITH TOPIC                              
         BH    VQH48                                                            
         CLI   SQQCOD,C' '         OR QUEST DEFINED                             
         BNH   VQS92                                                            
*                                                                               
VQH48    DS    0H                                                               
         MVI   NDERR,0             CLEAR ERROR                                  
         LR    RD,R0                                                            
         B     XIT                 AND DONT RETURN TO NODIO                     
*                                                                               
         SPACE 2                                                                
VQS91    MVI   ERROR,QSFMTERR      QSPEC FORMAT                                 
         B     TRAPERR                                                          
VQS92    MVI   ERROR,QSTYPERR      QSPEC TYPE                                   
         B     TRAPERR                                                          
VQS94    MVI   ERROR,QSRECERR      BAD QSPEC RECORD                             
         B     TRAPERR                                                          
         SPACE 3                                                                
VNDERRP  DS    0H                  HANDLE NODIO ERRORS                          
         L     R1,VNODBLK                                                       
         ZIC   RF,NDERR-NODBLKD(R1)                                             
         SH    RF,=Y(NDERRFST)     RF HAS RELATIVE NODIO ERROR                  
         IC    R0,NODERRTB(RF)                                                  
         STC   R0,ERROR                                                         
         B     TRAPERR                                                          
         SPACE 2                                                                
*        POSITIONAL TRANSLATE TABLE FOR NODIO ERRORS                            
         SPACE 2                                                                
NODERRTB DS    0X                                                               
         DC    AL1(QSFMTERR)       NDLEVERR                                     
         DC    AL1(QSFMTERR)       NDCDLERR                                     
         DC    AL1(NOTFOUND)       NDRNFERR                                     
         DC    AL1(DUPLICAT)       NDADDERR                                     
         DC    AL1(RENAMERR)       NDRENERR                                     
         DC    AL1(LOCNFERR)       NDPMDERR                                     
         DC    AL1(TOOLONG)        NDOVFERR                                     
         DC    AL1(0)                                                           
         DC    AL1(RESTERR)        NDRESERR                                     
         DC    AL1(NODIOERR)       NDLIBERR                                     
         DC    AL1(NODIOERR)       NDANFERR                                     
         DC    AL1(NODIOERR)       NDAMXERR                                     
         SPACE 3                                                                
VMSGSET  DS    0H                  SET MY OWN MESSAGE                           
         XC    CONHEAD,CONHEAD                                                  
         OI    CONHEADH+6,X'80'    TRANSMIT                                     
         ZIC   RF,MSGNUM                                                        
         LTR   RF,RF               IF MSGNUM IS ZERO                            
         BNZ   MSGS4                                                            
         MVC   CONHEAD,MSGTXT      JUST SET MSG AND XIT                         
         B     XIT                                                              
*                                                                               
MSGS4    DS    0H                                                               
         CLI   MSGNUM,MSGHI                                                     
         BNH   *+8                                                              
         LA    RF,MSGHI                                                         
*                                                                               
         SLL   RF,1                                                             
         LH    RF,MSGDSPS-2(RF)                                                 
         LA    RF,MSGS(RF)                                                      
         ZIC   R1,0(RF)                                                         
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   CONHEAD(0),1(RF)                                                 
*                                                                               
         ST    R2,CURSFLD          SET CURSOR                                   
         CLI   MSGNUM,MSGHI                                                     
         BL    XIT                                                              
         LA    R4,CONHEAD+11                                                    
         EDIT  (B1,MSGNUM),(3,0(R4)),FILL=0                                     
         B     XIT                                                              
         SPACE 3                                                                
MSGDSPS  DS    0H                                                               
         DC    AL2(MSENTM-1-MSGS)  01                                           
         DC    AL2(MSADDM-1-MSGS)  02                                           
         DC    AL2(MSPENM-1-MSGS)  03                                           
         DC    AL2(MSMOVM-1-MSGS)  04                                           
         DC    AL2(MSISLM-1-MSGS)  05                                           
         DC    AL2(MSMERM-1-MSGS)  06                                           
         DC    AL2(MSDELM-1-MSGS)  07                                           
         DC    AL2(XXXMSG-1-MSGS)  08                                           
MSGHI    EQU   (*-MSGDSPS)/2       HIGHEST MESSAGE NUMBER                       
         DC    X'FFFF'                                                          
         SPACE 2                                                                
MSGS     DS    0C                                                               
         DC    AL1(L'MSENTM)                                                    
MSENTM   DC    C'PLEASE ENTER FIELDS AS REQUIRED'                               
         DC    AL1(L'MSADDM)                                                    
MSADDM   DC    C'RECORD HAS BEEN ADDED TO FILE'                                 
         DC    AL1(L'MSPENM)                                                    
MSPENM   DC    C'MOVE PENDING, HIT ENTER TO CONTINUE LIST'                      
         DC    AL1(L'MSMOVM)                                                    
MSMOVM   DC    C'MOVE COMPLETED, HIT ENTER TO CONTINUE LIST'                    
         DC    AL1(L'MSISLM)                                                    
MSISLM   DC    C'INVALID SELECT ENTRY OR COMBINATION'                           
         DC    AL1(L'MSMERM)                                                    
MSMERM   DC    C'LOCATION NOT VALID FOR RECORD BEING MOVED'                     
         DC    AL1(L'MSDELM)                                                    
MSDELM   DC    C'RECORD HAS BEEN DELETED'                                       
         DC    AL1(L'XXXMSG)                                                    
XXXMSG   DC    C'** MESSAGE NNN'                                                
*                                                                               
         EJECT                                                                  
*        VALTARG- VALIDATE TARGET                                               
         SPACE 2                                                                
VTARG    DS    0H                                                               
         MVC   KEY,SVTRGKEY        USE SAVED KEY                                
         TM    4(R2),X'20'         IF PREVIOUSLY VALIDATED                      
         BNZ   VTARG11                                                          
*                                                                               
         LA    R4,KEY                                                           
         USING QTGKEY,R4                                                        
         MVC   QTGKEY,SVQSDKEY     START WITH SURVEY KEY                        
         CLI   TWAAGY,C' '         **NB- TEMP AGY SET FOR OFFLINE               
         BH    *+10                                                             
         MVC   TWAAGY,=C'T1'                                                    
         MVC   QTGKAGY,TWAAGY                                                   
         MVI   QTGKRCD,X'25'                                                    
*                                                                               
         CLI   5(R2),0             ANY INPUT                                    
         BNE   VTRG4                                                            
         CLI   ACTNUM,ACTLIST      IF NOT LIST OF TARGET RECS                   
         BNE   VTRG4               ERROR                                        
         CLI   RECNUM,RECTRG                                                    
         BNE   VTRG4                                                            
         MVC   SVTRGKEY,KEY                                                     
         B     XIT                                                              
*                                                                               
VTRG4    DS    0H                  BUILD KEY                                    
         GOTO1 ANY                                                              
         MVC   QTGKTRG,WORK                                                     
*                                                                               
VTARG6   DS    0H                                                               
         CLI   RECNUM,RECTRG       IF DOING TARGET ITSELF                       
         BNE   VTARG7                                                           
         CLI   ACTNUM,ACTADD       FOR ADD                                      
         BNE   VTARG6D                                                          
         XC    SVTRGKEY,SVTRGKEY   JUST CLEAR SAVE KEY                          
         B     VTARG11                                                          
*                                                                               
VTARG6D  DS    0H                                                               
         CLI   ACTNUM,ACTLIST      FOR LIST                                     
         BNE   VTARG7                                                           
         CLI   ACTNUM,ACTREST      AND RESTORE                                  
         BNE   VTARG7                                                           
         MVC   SVTRGKEY,KEY        JUST SAVE KEY                                
         B     VTARG11                                                          
*                                                                               
VTARG7   DS    0H                                                               
         CLC   QTGKEY,SVTRGKEY     DO WE ALREADY HAVE                           
         BE    VTARG8                                                           
*                                                                               
         GOTO1 HIGH                                                             
         CLC   QTGKEY,KEYSAVE                                                   
         BE    *+12                                                             
         MVI   ERROR,NOTFOUND                                                   
         B     TRAPERR                                                          
*                                                                               
         MVC   SVTRGKEY,KEY                                                     
         ST    R2,KCHGFLD          SET KEY CHG FIELD                            
         MVI   KEYCHG,C'Y'                                                      
*                                                                               
VTARG8   DS    0H                                                               
         OI    4(R2),X'20'         SET VALIDATED                                
*                                                                               
VTARG11  DS    0H                                                               
         B     XIT                                                              
         EJECT                                                                  
*        VALSPEC- VALIDATE SPECIFICATION                                        
*                 VSPOPT- X'80'=TEST ONLY, X'40'=SUPRESS COUNTS                 
         SPACE 2                                                                
VSPEC    DS    0H                                                               
         L     R2,0(R1)            A(FIRST SCREEN FIELD)                        
         ST    R2,SVFLDH           SAVE IT                                      
         ICM   R4,15,4(R1)         A(OUTPUT)                                    
         BNZ   *+8                 IF NONE                                      
         LA    R4,DUB              USE DUB                                      
*                                                                               
         CLI   MODE,VALREC         TEST VALIDATING                              
         BE    VALSP3              YES                                          
         TM    VSPOPT,X'40'        NO- TEST COUNTS SUPPRESSED                   
         BNZ   VALSP9B             YES-DONT PROCESS-JUST SET VAL'D              
*                                                                               
VALSP3   DS    0H                  TEST ANY MODIFIED                            
         CLI   FORCESPC,C'Y'       FORCE RE-VALIDATE                            
         MVI   FORCESPC,C'N'       RESET                                        
         BE    VALSP4B                                                          
*                                                                               
         CLI   SURWMRGE,C'Y'       FOR NON-MERGED WAVE SURVEYS                  
         BE    VALSP3B                                                          
         TM    VSPOPT,X'40'        IF COUNTING NOT SUPRESSED                    
         BZ    VALSP4B             MUST PROCESS SPECS IN ANY CASE               
*                                                                               
VALSP3B  DS    0H                                                               
         ZIC   R3,FREEMAX          NUMBER OF FIELDS TO CHECK                    
*                                                                               
VALSP4   DS    0H                                                               
         TM    4(R2),X'20'         TEST VALIDATED                               
         BZ    VALSP4B                                                          
         BAS   RE,BUMPU                                                         
         BCT   R3,VALSP4                                                        
         B     VALSPX              ALL VALIDATED                                
*                                                                               
*                                  VALIDATION                                   
VALSP4B  DS    0H                                                               
         MVC   0(4,R4),NOVAL       CLEAR OUTPUT COUNTS                          
         MVC   4(4,R4),NOVAL       (SET TO NO VALUE)                            
         L     R6,AIO              FIND FIRST ELEM                              
         BAS   RE,GETEL            NOTE- ELCODE SET BY CALLER                   
         B     *+8                                                              
*                                                                               
VALSP5   DS    0H                                                               
         BAS   RE,NEXTEL                                                        
         BNE   VALSPX                                                           
         ZIC   RF,FREEKLN          TEST RIGHT ELEM 'KEY'                        
         BCTR  RF,R0                                                            
         EX    RF,*+8                                                           
         B     *+10                                                             
         CLC   FREEKEY(0),2(R6)                                                 
         BNE   VALSP5                                                           
*                                                                               
         MVI   SPECTYP,0           CLEAR TYPE                                   
         CLC   =C'NOT DEF',6(R6)   DONE IF NOT-DEFINED                          
         BE    VALSP9B                                                          
*                                                                               
         L     R3,VMPQBLK                                                       
         USING MPQBLKD,R3                                                       
*                                                                               
         L     R5,VNODBLK          FOR THIS USE LET NODIO                       
         USING NODBLKD,R5                                                       
         MVC   NDIOA,AIO2          HAVE AIO2                                    
         MVC   NDIOA2,AIO3         AND AIO3                                     
*                                                                               
         LA    R0,X'80'            SET TO TRANSLATE                             
         TM    VSPOPT,X'80'        TEST TO SUPRESS EXECUTE                      
         BZ    *+8                                                              
         LA    R0,X'C0'            TRANSLATE ONLY                               
*                                                                               
         GOTO1 MPQFAC,DMCB,((R0),=C'EXEC'),VMPQBLK,(R6),0                       
         CLI   MPQBERR,0                                                        
         BNE   VALSP7                                                           
         MVC   SPECTYP,DMCB+8      SPEC TYPE (N,L,Q)                            
         LTR   R4,R4               ANY OUTPUT AREA                              
         BZ    VALSP9              NO- NO COUNTS                                
         TM    VSPOPT,X'C0'        TEST COUNTS NEEDED                           
         BNZ   VALSP9              NO                                           
*                                                                               
         GOTO1 MPQFAC,DMCB,=C'XMULT',,(C'B',MPQBSTK),0                          
*                                                                               
         MVC   0(8,R4),MPQBSAM     SAMPLE AND POP IN OUTPUT                     
         CLI   MPQBERR,0                                                        
         BE    VALSP9                                                           
*                                                                               
VALSP7   DS    0H                                                               
         MVC   NDIOA,AIO1          RESTORE NORMAL NODIO IOS                     
         MVC   NDIOA2,AIO2                                                      
         MVC   CONHEAD(40),MPQBEMSG                                             
         GOTO1 ERREX2                                                           
*                                                                               
VALSP9   DS    0H                  SET COUNTS                                   
         MVC   NDIOA,AIO1          RESTORE NORMAL NODIO IOS                     
         MVC   NDIOA2,AIO2                                                      
*                                  SET FIELDS VALIDATED                         
*                                                                               
VALSP9B  DS    0H                                                               
         L     R2,SVFLDH           A(FIRST FIELD)                               
         ZIC   R3,FREEMAX          NUMBER OF FIELDS TO SET                      
*                                                                               
VALSP9D  DS    0H                                                               
         OI    4(R2),X'20'         SET VALIDATED                                
         BAS   RE,BUMPU                                                         
         BCT   R3,VALSP9D                                                       
*                                                                               
VALSPX   DS    0H                                                               
         B     XIT                                                              
         EJECT                                                                  
*        DSPCOUNT- DISPLAY SAMPLE AND POP COUNTS                                
         SPACE 2                                                                
DCOUNT   DS    0H                                                               
         TM    VSPOPT,X'40'        TEST COUNTS SUPRESSED                        
         BNZ   DCOUNTX                                                          
*                                                                               
         L     R6,0(R1)            A(COUNTS)                                    
         L     R2,4(R1)            A(SAMPLE FIELD)                              
         L     R5,8(R1)            A(POP FIELD)                                 
*                                                                               
         MVC   SCANWRK(60),SPACES  USE SCANWRK AS WORK AREA                     
         LA    R4,SCANWRK                                                       
*                                                                               
         CLC   0(4,R6),NOVAL       TEST NO VALUE                                
         BE    DCOUNT3                                                          
         MVC   0(12,R4),=C'SAMPLE SIZE='                                        
         EDIT  (B4,0(R6)),(13,12(R4)),COMMAS=YES,ALIGN=LEFT,           X        
               ZERO=NOBLANK                                                     
*                                                                               
         MVC   WORK(25),SCANWRK                                                 
         BAS   RE,GENDISP                                                       
*                                                                               
DCOUNT3  DS    0H                                                               
         L     R3,VMPQBLK          TEST ANY WEIGHTS                             
         USING MPQBLKD,R3                                                       
         OC    MPQBWCRD,MPQBWCRD                                                
         BZ    DCOUNT4             NO- DONT SHOW POPULATION                     
         CLC   4(4,R6),NOVAL       TEST NO VALUE                                
         BE    DCOUNT4                                                          
         DROP  R3                                                               
*                                                                               
         MVC   SCANWRK(60),SPACES  USE SCANWRK AS WORK AREA                     
         LR    R2,R5               A(POP FIELD)                                 
         MVC   0(12,R4),=C'POPULATION ='                                        
         EDIT  (B4,4(R6)),(13,12(R4)),COMMAS=YES,ALIGN=LEFT,           X        
               ZERO=NOBLANK                                                     
*                                                                               
         MVC   WORK(25),SCANWRK                                                 
         BAS   RE,GENDISP                                                       
*                                                                               
DCOUNT4  DS    0H                                                               
DCOUNTX  DS    0H                                                               
         B     XIT                                                              
         EJECT                                                                  
*        MATCH- CODE MATCHING FOR FILTERING                                     
         SPACE 2                                                                
VMATCH   DS    0H                                                               
         L     R4,4(R1)            FILTER                                       
         CLI   0(R4),C'='          IF DOES NOT START WITH =                     
         BNE   VMT20               USE TSCAN TO SEARCH                          
*                                  ELSE DO EXACT PATTERN MATCH                  
         L     R3,0(R1)            CODE                                         
         LA    R4,1(R4)            BYPASS =                                     
         ZIC   R5,4(R1)            FILTER LENGTH                                
         BCT   R5,*+8              IF ONLY =, THEN PASS                         
         B     ZEROX                                                            
*                                                                               
VMT4     DS    0H                                                               
         CLI   0(R4),C'*'          IGNORE *'S                                   
         BE    VMT6                                                             
         CLC   0(1,R3),0(R4)                                                    
         BNE   NONZX               REJECT ON MISMATCH                           
*                                                                               
VMT6     DS    0H                                                               
         LA    R3,1(R3)                                                         
         LA    R4,1(R4)                                                         
         BCT   R5,VMT4                                                          
         B     ZEROX               PASSES FILTER                                
*                                                                               
*                                  USE TSCAN TO SEARCH FOR FILTER               
VMT20    DS    0H                                                               
         MVC   DMCB+8(4),4(R1)     SEARCH STRNG AND LEN                         
         MVC   DMCB+12(4),4(R1)    AND REPLACE (SAME)                           
         ZIC   RF,0(R1)            LENGTH OF TEXT                               
         ST    RF,DMCB+4                                                        
         MVC   DMCB(4),0(R1)       A(TEXT)                                      
*                                                                               
         GOTO1 TSCAN,DMCB                                                       
         OC    DMCB+16(4),DMCB+16  TEST ANY MATCH                               
         BZ    NONZX                                                            
         B     ZEROX                                                            
*                                                                               
         EJECT                                                                  
*        VARIOUS SHORT ROUTINES                                                 
*        ----------------------                                                 
         SPACE 1                                                                
GENDISP  ZIC   R1,0(R2)                                                         
         SH    R1,=H'9'                                                         
         EX    R1,GENCLC           IS DATA ON SCREEN ALREADY                    
         BER   RE                  YES- RETURN (CC=)                            
         EX    R1,GENMVC           NO - SO MOVE IT THERE                        
         OI    6(R2),X'80'              AND TRANSMIT                            
         BR    RE                  RETURN (CC NOT=)                             
         SPACE 1                                                                
GENCLC   CLC   8(0,R2),WORK                                                     
GENMVC   MVC   8(0,R2),WORK                                                     
         SPACE 2                                                                
         PRINT GEN                                                              
         GETEL (R6),DATADISP,ELCODE                                             
         PRINT NOGEN                                                            
         SPACE 2                                                                
NONZX    LTR   RB,RB               EXIT CC NOT=                                 
         B     XIT                                                              
         SPACE 1                                                                
ZEROX    CR    RB,RB               EXIT CC=                                     
         B     XIT                                                              
         SPACE 2                                                                
BUMP     ZIC   RF,0(R2)            BUMP TO NEXT SCREEN FIELD                    
         AR    R2,RF                                                            
         CLI   0(R2),0             EOS- RETURN =                                
         BR    RE                                                               
         SPACE 2                                                                
BUMPU    ZIC   RF,0(R2)            BUMP TO NEXT UNPROTECTED FIELD               
         AR    R2,RF                                                            
         CLI   0(R2),0             EOS- RETRUN =                                
         BER   RE                                                               
         TM    1(R2),X'20'                                                      
         BNZ   BUMPU                                                            
         LTR   RE,RE               NOT EOS- RETURN NOT =                        
         BR    RE                                                               
         SPACE 1                                                                
TRAPERR  GOTO1 ERREX                                                            
         SPACE 2                                                                
XIT      XIT1                                                                   
         EJECT                                                                  
*        CHKLIST- HANDLE SPECIAL LIST FUNCTIONS                                 
*        FOR QSPEC LIST (MOVES) AND TARGET BUILD                                
*        (LOGICAL SELECTS)                                                      
         SPACE 2                                                                
CHKLIST  NTR1                                                                   
*                            **NOTE LOCAL USE OF R6 FOR END OF TWA              
         LA    R6,2048(RA)                                                      
         LA    R6,2048(R6)                                                      
         USING CONHEADH-64+4096,R6                                              
*                                                                               
         CLI   TWASCR,X'EA'        MUST ALREADY HAVE DONE LIST                  
         BNE   CKL85                                                            
         MVI   CKLSW,C'Q'          SET QSPEC/LIST                               
         CLC   =C'LIST',CONACT                                                  
         BE    CKL3                                                             
         CLC   =C'LAST',CONACT                                                  
         BE    CKL3                                                             
         CLC   =C'NEXT',CONACT                                                  
         BE    CKL3                                                             
         CLC   =C'FIRST',CONACT                                                 
         BE    CKL3                                                             
         CLC   =C'RESET',CONACT                                                 
         BNE   CKL4                                                             
*                                                                               
CKL3     DS    0H                                                               
         CLC   =C'QSP',CONREC                                                   
         BE    CKL6                                                             
         CLC   =C'TOP',CONREC                                                   
         BE    CKL6                                                             
         CLC   =C'QUE',CONREC                                                   
         BE    CKL6                                                             
         CLC   =C'ANS',CONREC                                                   
         BE    CKL6                                                             
*                                                                               
CKL4     DS    0H                                                               
         MVI   NOPEND,C'Y'         SET TO CLEAR PENDING                         
         CLC   =C'TAR',CONREC      TARG/BUILD                                   
         BNE   CKLZX               (NB- ONLY A VERY LITTLE                      
         CLC   =C'BUILD',CONACT    TARG BUILD CODE HERE)                        
         BNE   CKLZX                                                            
         MVI   CKLSW,C'T'          SET DOING TARGET BUILD                       
*                                                                               
CKL6     DS    0H                                                               
         CLC   =C'RESET',CONACT    RESET MEANS                                  
         BNE   CKL6D                                                            
         XC    MVPENDS,MVPENDS     CLEAR ALL PENDINGS                           
         MVC   CONACT,=C'LIST     '                                             
         OI    CONACTH+6,X'80'                                                  
*                                                                               
CKL6D    DS    0H                                                               
         LA    R2,QSLSELH          FIRST SELECT FIELD                           
         LA    R3,1                COUNT FIEDLS                                 
         XC    CLCTLS,CLCTLS       CLEAR CONTROLS                               
*                                                                               
         OI    CONACTH+1,X'01'     FORCE MODIFIED                               
         OI    CONACTH+6,X'80'                                                  
*                                                                               
CKL7     DS    0H                                                               
         CLI   5(R2),0             ANY INPUT                                    
         BE    CKL40               NO- NEXT FIELD                               
*                                                                               
         MVC   WORK(3),8(R2)                                                    
         OC    WORK(3),=3C' '                                                   
*                                                                               
         CLI   WORK,C'*'           CLEAR SEL FIELDS WITH *                      
         BNE   CKL7D                                                            
         MVC   8(3,R2),=3C' '                                                   
         OI    6(R2),X'80'         TRANSMIT                                     
         MVI   5(R2),0             LENGTH                                       
         B     CKL40                                                            
*                                                                               
CKL7D    DS    0H                                                               
         CLC   =C'M  ',WORK        MOVE                                         
         BNE   CKL8                                                             
         CLI   CLSMOV,0            DO WE ALREADY HAVE A MOVE                    
         BNE   CKL91               YES ERROR                                    
         CLI   CKLSW,C'Q'          MUST BE Q/L                                  
         BNE   CKL91                                                            
         CLI   CLSOTH,C'Y'         MUST NOT HAVE OTHER TYPES                    
         BE    CKL91                                                            
         STC   R3,CLSMOV           SAVE FIELD NUMBER                            
         ST    R2,CLMOVF           A(FIELD)                                     
         BAS   RE,CKLRESTR         RESTORE SAVED CORE                           
*                                  FIND QLIST ENTRY                             
         LR    R4,R3                                                            
         BCTR  R4,R0                                                            
         MH    R4,=Y(QLISTDL)                                                   
         ICM   RF,15,AQLIST        QLIST DISPLACEMENT                           
         LA    RF,SYSUSAV(RF)      'RELOCATE'                                   
         AR    R4,RF                                                            
         USING QLISTD,R4                                                        
         OC    QLNODE,QLNODE       MUST HAVE A NODE                             
         BZ    CKL91                                                            
         MVC   MVMNOD,QLNODE       SAVE NODE                                    
         MVC   MVMCOD,QLQSPEC      SAVE FULL QSPEC                              
         B     CKL40                                                            
*                                                                               
CKL8     DS    0H                                                               
         CLC   =C'A  ',WORK        AFTER                                        
         BNE   CKL9                                                             
         MVI   MVBA,C'A'                                                        
         B     CKL9D                                                            
*                                                                               
CKL9     DS    0H                                                               
         CLC   =C'B  ',WORK        BEFORE                                       
         BNE   CKL10                                                            
         MVI   MVBA,C'B'                                                        
*                                                                               
CKL9D    DS    0H                                                               
         CLI   CLSLOC,0            DO WE ALREADY HAVE A LOCATION                
         BNE   CKL91               YES ERROR                                    
         CLI   CKLSW,C'Q'          MUST BE Q/L                                  
         BNE   CKL91                                                            
         CLI   CLSOTH,C'Y'         MUST NOT HAVE OTHER TYPES                    
         BE    CKL91                                                            
         STC   R3,CLSLOC           SAVE FIELD NUMBER                            
         ST    R2,CLLOCF           A(FIELD)                                     
         BAS   RE,CKLRESTR         MAY HAVE TO RESTORE SAVED AREAS              
*                                  FIND QLIST ENTRY                             
         LR    R4,R3                                                            
         BCTR  R4,R0                                                            
         MH    R4,=Y(QLISTDL)                                                   
         ICM   RF,15,AQLIST        QLIST DISPLACEMENT                           
         LA    RF,SYSUSAV(RF)      'RELOCATE'                                   
         AR    R4,RF                                                            
         USING QLISTD,R4                                                        
         OC    QLNODE,QLNODE       MUST HAVE A NODE                             
         BZ    CKL91                                                            
         MVC   MVLNOD,QLNODE       SAVE NODE                                    
*                                                                               
         MVI   WORK,C'.'                                                        
         MVC   WORK+1(L'QLQSPEC),QLQSPEC                                        
         LA    RF,WORK+1+L'QLQSPEC                                              
         CLI   0(RF),C'.'                                                       
         BE    *+8                                                              
         BCT   RF,*-8                                                           
         MVC   MVLCOD,1(RF)        SAVE LAST LEVEL OF QSPEC                     
         B     CKL30                                                            
*                                                                               
CKL10    DS    0H                                                               
         CLC   =C'AND',WORK                                                     
         BE    CKL12                                                            
         CLC   =C'OR ',WORK                                                     
         BE    CKL12                                                            
         CLC   =C'EOR',WORK                                                     
         BNE   CKL28                                                            
*                                                                               
CKL12    DS    0H                  HAVE LOGICAL SELECTION                       
         CLI   CKLSW,C'T'          MUST BE TARGET BUILD                         
         BNE   CKL91                                                            
         MVI   CLSLOG,C'Y'         SET HAVE LOGICAL                             
         B     CKL40                                                            
*                                                                               
CKL28    DS    0H                  HAVE OTHER SELECT INPUT                      
         CLI   CKLSW,C'T'          ERROR IF TARGET BUILD                        
         BE    CKL91                                                            
         CLI   CLSMOV,0            OR IF HAVE MOVE                              
         BNE   CKL91                                                            
         CLI   CLSLOC,0            OR LOCATION                                  
         BNE   CKL91                                                            
         MVI   CLSOTH,C'Y'         SET HAVE OTHER                               
*                                                                               
CKL30    DS    0H                                                               
CKL40    DS    0H                                                               
         LA    R3,1(R3)            BUMP FIELD COUNT                             
         BAS   RE,BUMPU            NEXT SELECT FIELD                            
         BNE   CKL7                                                             
*                                                                               
*                                  END OF SCREEN                                
*                                  -------------                                
*                                                                               
         CLI   CLSOTH,C'Y'         TEST HAVE 'OTHER' LIST ACTIONS               
         BE    CKL85                                                            
         CLI   CKLSW,C'T'          TARGET BUILD                                 
         BNE   CKL50                                                            
         CLI   CLSLOG,C'Y'         ANY LOGICALS                                 
         BNE   CKLZX               NO- LET GENCON CONTINUE LIST                 
         B     CKLZX               **NB- NEED CODE TO HANDLE LOGICALS           
*                                                                               
CKL50    DS    0H                  QSPEC/LIST MOVES                             
         CLI   CLSMOV,0            DO WE HAVE A MOVE                            
         BNE   CKL54               YES                                          
         CLI   CLSLOC,0            DO WE HAVE A LOCATION                        
         BE    CKLZX               NO- LET GENCON CONTINUE LIST                 
*                                  HAVE LOCATION                                
         OC    MVMNOD,MVMNOD       IS MOVE PENDING                              
         BZ    CKL80               NO- THEN LOC IS                              
         B     CKL56                                                            
*                                                                               
CKL54    DS    0H                  HAVE MOVE                                    
         OC    MVLNOD,MVLNOD       IS LOCATION PENDING                          
         BZ    CKL80               NO- THEN MOVE IS                             
*                                                                               
CKL56    DS    0H                                                               
         CLC   MVMNOD,MVLNOD       ARE NODES THE SAME                           
         BE    *+14                                                             
         XC    MVLNOD,MVLNOD                                                    
         B     CKL92                                                            
*                                                                               
         BAS   RE,CKLMOV                                                        
         XC    MVPENDS,MVPENDS     CLEAR PENDINGS                               
         B     CKL82                                                            
*                                                                               
CKL80    DS    0H                                                               
         MVI   MSGNUM,PENDMSG                                                   
         GOTO1 MSGSET                                                           
*                                                                               
CKL82    DS    0H                                                               
         BAS   RE,CKLFADJ          RESET MOVE/LOC FIELDS                        
         LA    R2,QSLQSPH          CURSOR                                       
         B     CKLNZX              DONT USE GENCON ON RETURN                    
*                                                                               
CKL85    DS    0H                  CLEAR ANY PENDINGS AND RETURN                
         MVI   NOPEND,C'Y'         SET TO CLEAR PENDING                         
*                                                                               
CKLZX    DS    0H                                                               
         B     ZEROX               USE GENCON ON RETURN                         
         SPACE 3                                                                
CKL91    MVI   MSGNUM,INSELMSG                                                  
         B     CKL999                                                           
CKL92    MVI   MSGNUM,MVERMSG                                                   
         L     R2,CLLOCF                                                        
         B     CKL999                                                           
CKL999   DS    0H                                                               
         GOTO1 MSGSET                                                           
*                                                                               
CKLNZX   DS    0H                                                               
         OI    6(R2),X'40'         CURSOR                                       
         B     NONZX               DONT USE GENCON ON RETURN                    
         SPACE 3                                                                
CKLMOV   NTR1                                                                   
         L     R5,VNODBLK                                                       
         USING NODBLKD,R5                                                       
         XC    NDHOOK,NDHOOK       NO HOOK                                      
         GOTO1 NODIO,DMCB,VNODBLK,(MVBA,=C'MOVE'),MVMCOD,(8,MVLCOD)             
         CLI   NDERR,0                                                          
         BNE   CKL92               ERROR                                        
         MVI   MSGNUM,MOVMSG                                                    
         MVI   NOPEND,C'Y'         SET TO CLEAR PENDING                         
         GOTO1 MSGSET                                                           
         BAS   RE,CKLSAV           AFTER MOVE SAVE NODIO TABLES                 
*                                                                               
         B     XIT                                                              
         DROP  R5                                                               
         SPACE 2                                                                
CKLFADJ  DS    0H                  ADJUST MOVE/LOC FIELD                        
         ICM   RF,15,CLMOVF        MOVE COMMAND FIELD                           
         BZ    CKLFA4                                                           
         MVC   10(1,RF),9(RF)      MOVE COMMAND OVER                            
         MVC   9(1,RF),8(RF)                                                    
         MVI   8(RF),C'*'                                                       
         MVI   7(RF),3             SET OUTPUT LENGTH                            
         OI    6(RF),X'80'         TRANSMIT                                     
*                                                                               
CKLFA4   DS    0H                                                               
         ICM   RF,15,CLLOCF        LOCATION FIELD                               
         BZ    CKLFA5                                                           
         MVC   10(1,RF),9(RF)      MOVE COMMAND OVER                            
         MVC   9(1,RF),8(RF)                                                    
         MVI   8(RF),C'*'                                                       
         OI    6(RF),X'80'         TRANSMIT                                     
*                                                                               
CKLFA5   DS    0H                                                               
         BR    RE                                                               
         SPACE 2                                                                
CKLRESTR DS    0H                                                               
         CLI   CLRESTR,C'Y'        HAVE WE ALREADY RESTORED                     
         BER   RE                                                               
*                                                                               
         NTR1                                                                   
         SR    R0,R0                                                            
         ICM   R0,1,NTWA                                                        
         BZ    CKLRSX                                                           
*                                                                               
         L     R4,ASTARTSV                                                      
         LA    R3,1                                                             
*                                                                               
CKLRS4   DS    0H                                                               
         STC   R3,DMCB+8           PAGE NUMBER                                  
         MVI   DMCB+9,0                                                         
         MVC   DMCB+10(2),TWATRM                                                
         GOTO1 DATAMGR,DMCB,=C'DMRDIR',=C'TEMPSTR',,(R4),0                      
         CLI   DMCB+8,0                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
         LA    R4,2304(R4)                                                      
         LA    R3,1(R3)                                                         
         BCT   R0,CKLRS4                                                        
*                                                                               
CKLRSX   DS    0H                                                               
         MVI   CLRESTR,C'Y'        SET HAVE RESTORED                            
         MVI   CKLRS4,0            **TEMP** CATCH DOUBLE RESTORES               
         B     XIT                                                              
         SPACE 2                                                                
CKLSAV   NTR1                                                                   
         LA    R0,1                NEED DO ONLY 1 PAGE                          
         L     R4,ASTARTSV                                                      
         LA    R4,2304(R4)         DO PAGE 2 ONLY                               
         LA    R3,2                                                             
*                                                                               
CKLSV4   DS    0H                                                               
         STC   R3,DMCB+8           PAGE NUMBER                                  
         MVI   DMCB+9,0                                                         
         MVC   DMCB+10(2),TWATRM                                                
         GOTO1 DATAMGR,DMCB,=C'DMWRT ',=C'TEMPSTR',,(R4),0                      
         CLI   DMCB+8,0                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
         LA    R4,2304(R4)                                                      
         LA    R3,1(R3)                                                         
         BCT   R0,CKLSV4                                                        
*                                                                               
         B     XIT                                                              
         DROP  R6                                                               
         EJECT                                                                  
*              CONSTANTS TABLES ETC                                             
         SPACE 3                                                                
RELO     DS    A                                                                
         SPACE 1                                                                
SYSVCON  DS    0F                                                               
         DC    A(0)            SPARE (WAS NODIO)                                
         DC    V(MPQFAC)                                                        
         DC    V(MPQTRAN)                                                       
         DC    V(RECUP)                                                         
         DC    V(TSCAN)                                                         
         DC    V(DUMMY)                                                         
         SPACE 1                                                                
NVTYPES  EQU   (*-SYSVCON)/4                                                    
         EJECT                                                                  
         LTORG                                                                  
         EJECT                                                                  
*              DIRECTORY OF RECORDS AND ACTIONS                                 
         SPACE 3                                                                
RECACT   DS    0D                                                               
         SPACE 1                                                                
*                                  X'01' ENTRIES ARE AVAILABLE RECORDS          
*                                  CL8 EXPANDED RECORD NAME                     
*                                  CL1 RECORD NUMBER                            
*                                  CL1 PHASE NUMBER FOR DATA DICTIONARY         
*                                  CL1 PHASE NUMBER FOR HELP SCREEN             
         SPACE 1                                                                
         DC    X'01',C'SURVEY  ',AL1(02),X'00DF'                                
         DC    X'01',C'WAVE    ',AL1(03),X'00DF'                                
         DC    X'01',C'WEIGHT  ',AL1(04),X'00DF'                                
         DC    X'01',C'CCP     ',AL1(08),X'00DF'                                
         DC    X'01',C'TOPIC   ',AL1(10),X'00DF'                                
         DC    X'01',C'QUESTION',AL1(11),X'00DF'                                
         DC    X'01',C'ANSWER  ',AL1(12),X'00DF'                                
         DC    X'01',C'QSPEC   ',AL1(13),X'00DF'                                
         DC    X'01',C'QSPECWGT',AL1(14),X'00DF'                                
         DC    X'01',C'TARGET  ',AL1(21),X'00DF'                                
         DC    X'01',C'TARGWGT ',AL1(22),X'00DF'                                
         DC    X'01',C'VEHICLE ',AL1(24),X'00DF'                                
         DC    X'01',C'PROTO   ',AL1(25),X'00DF'                                
         DC    X'01',C'SCHEDULE',AL1(26),X'00DF'                                
         DC    X'01',C'RDRSHP  ',AL1(27),X'00DF'                                
         DC    X'01',C'PROGRAM ',AL1(30),X'00DF'                                
         DC    X'01',C'HELP    ',AL1(00),X'00CF'                                
         SPACE 2                                                                
*                                  X'02' ENTRIES ARE AVAILABLE ACTIONS          
*                                  CL8 EXPANDED ACTION NAME                     
*                                  CL1 ACTION NUMBER                            
*                                  CL1 ACTION EQUATE                            
*                                  CL1 SPARE                                    
         SPACE 1                                                                
         DC    X'02',C'ADD     ',AL1(01,01,00)                                  
         DC    X'02',C'CHANGE  ',AL1(02,01,00)                                  
         DC    X'02',C'DISPLAY ',AL1(03,01,00)                                  
         DC    X'02',C'DELETE  ',AL1(04,01,00)                                  
         DC    X'02',C'SELECT  ',AL1(05,01,00)                                  
         DC    X'02',C'RESTORE ',AL1(06,01,00)                                  
         DC    X'02',C'RENAME  ',AL1(07,02,00)                                  
         DC    X'02',C'COPY    ',AL1(08,03,00)                                  
         DC    X'02',C'ATTACH  ',AL1(09,04,00)                                  
         DC    X'02',C'LIST    ',AL1(10,10,00)                                  
         DC    X'02',C'REPORT  ',AL1(12,12,00)                                  
         DC    X'02',C'HELP    ',AL1(00,00,00)                                  
         EJECT                                                                  
*              DIRECTORY OF PHASES FOR SELECTED RECORD/ACTION                   
         SPACE 3                                                                
*                                  X'03' ENTRIES ARE OK REC/ACT COMBOS          
*                                  CL1 RECORD NUMBER                            
*                                  CL1 ACTION EQUATE                            
*                                  CL1 PHASE NUMBER FOR SCREEN                  
*                                  CL1 PHASE NUMBER FOR EDIT                    
*                                  CL1 PHASE NUMBER FOR SPECS                   
*                                  CL1 PHASE NUMBER FOR REPORT                  
*                                  CL1 WHEN OK BITS 80=SCREEN 40=NOW            
*                                      20=SOON 10=OV 08=DDS                     
*                                  CL2 CODE FOR REPORTS                         
*                                  CL2 CODE FOR EOD HANDLING                    
         SPACE 1                                                                
         DC    X'03',AL1(02,01),X'F202000080',C'    '  SURVEY    MAINT          
         DC    X'03',AL1(02,10),X'E202000280',C'    '            LIST           
         DC    X'03',AL1(03,01),X'F303000080',C'    '  WAVE      MAINT          
         DC    X'03',AL1(03,10),X'E303000380',C'    '            LIST           
         DC    X'03',AL1(04,01),X'F404000080',C'    '  WEIGHT    MAINT          
         DC    X'03',AL1(04,10),X'E404000480',C'    '            LIST           
         DC    X'03',AL1(08,10),X'E8080008C0',C'    '  CCP       LIST           
         DC    X'03',AL1(10,01),X'FA0A000080',C'    '  TOPIC     MAINT          
         DC    X'03',AL1(10,02),X'EB0A000080',C'    '            RENAME         
         DC    X'03',AL1(10,03),X'EC0A000080',C'    '            COPY           
         DC    X'03',AL1(10,10),X'EA0A000AC0',C'    '            LIST           
         DC    X'03',AL1(11,01),X'FB0A000080',C'    '  QUESTION  MAINT          
         DC    X'03',AL1(11,02),X'EB0A000080',C'    '            RENAME         
         DC    X'03',AL1(11,03),X'EC0A000080',C'    '            COPY           
         DC    X'03',AL1(11,10),X'EA0A000AC0',C'    '            LIST           
         DC    X'03',AL1(12,01),X'FC0A000080',C'    '  ANSWER    MAINT          
         DC    X'03',AL1(12,02),X'EB0A000080',C'    '            RENAME         
         DC    X'03',AL1(12,03),X'EC0A000080',C'    '            COPY           
         DC    X'03',AL1(12,10),X'EA0A000AC0',C'    '            LIST           
         DC    X'03',AL1(13,01),X'FD0A000080',C'    '  QSPEC     MAINT          
         DC    X'03',AL1(13,02),X'EB0A000080',C'    '            RENAME         
         DC    X'03',AL1(13,03),X'EC0A000080',C'    '            COPY           
         DC    X'03',AL1(13,10),X'EA0A000AC0',C'    '            LIST           
         DC    X'03',AL1(14,01),X'FE0E000080',C'    '  QSPECWGT  MAINT          
         DC    X'03',AL1(14,10),X'EE0E000EC0',C'    '            LIST           
         DC    X'03',AL1(13,04),X'ED0D000080',C'    '  QSPEC     ATTACH         
         DC    X'03',AL1(21,01),X'A121000080',C'    '  TARGET    MAINT          
         DC    X'03',AL1(21,03),X'A321000080',C'    '            COPY           
         DC    X'03',AL1(21,10),X'B1210021C0',C'    '            LIST           
         DC    X'03',AL1(21,12),X'A1210021F8',C'AABB'            REPORT         
         DC    X'03',AL1(22,01),X'A222000080',C'    '  TARGWGT   MAINT          
         DC    X'03',AL1(22,10),X'B2220022C0',C'    '            LIST           
         DC    X'03',AL1(24,01),X'A424000080',C'    '  VEHICLE   MAINT          
         DC    X'03',AL1(24,10),X'B424002480',C'    '            LIST           
         DC    X'03',AL1(24,12),X'B4240024F8',C'    '            REPORT         
         DC    X'03',AL1(25,01),X'A525000080',C'    '  PROTO     MAINT          
         DC    X'03',AL1(25,10),X'B525002580',C'    '            LIST           
         DC    X'03',AL1(26,01),X'A626000080',C'    '  SCHEDULE  MAINT          
         DC    X'03',AL1(26,10),X'B626002680',C'    '            LIST           
         DC    X'03',AL1(26,12),X'B6260026F8',C'    '            REPORT         
         DC    X'03',AL1(26,03),X'AF26002680',C'    '            COPY           
         DC    X'03',AL1(27,01),X'A727000080',C'    '  RDRSHP    MAINT          
         DC    X'03',AL1(27,10),X'B727002780',C'    '            LIST           
         DC    X'03',AL1(30,01),X'C030000080',C'    '  PROGRAM   MAINT          
         DC    X'03',AL1(30,10),X'D130003080',C'    '            LIST           
         DC    X'03',AL1(30,12),X'C0300030F8',C'    '            REPORT         
         DC    X'FF'                                                            
         EJECT                                                                  
       ++INCLUDE DDSPOOLD                                                       
         EJECT                                                                  
       ++INCLUDE DDSPLWORKD                                                     
         EJECT                                                                  
       ++INCLUDE MPRDRFFD                                                       
         SPACE 2                                                                
       ++INCLUDE DDGENTWA                                                       
         PRINT OFF                                                              
         ORG   CONTAGH                                                          
       ++INCLUDE MPRDREAD                                                       
         PRINT ON                                                               
         EJECT                                                                  
       ++INCLUDE MPQBLKD                                                        
       ++INCLUDE MPQERRQS                                                       
         EJECT                                                                  
       ++INCLUDE DDNODBLKD                                                      
         SPACE 2                                                                
NODWKL   EQU   NODBLKL+(4*NDLVTABL)   AREA NEEDED FOR NODIO                     
         EJECT                                                                  
       ++INCLUDE MPRDRWORKD                                                     
*                                                                               
IOLEN    EQU   3*(2000+8)       TOTAL IO AREA LENGTH                            
*                               3 X (2000 BYTE IO AREA + 8 BYTE LABEL)          
*                                                                               
TWORKL   EQU   SPOOLEND-SPOOLD+8+GENDEND-GEND+8+IOLEN+SYSDEND-SYSD              
         SPACE 2                                                                
         ORG   SYSUNON             NON SAVED AREA                               
CKLSW    DS    C                                                                
*                                                                               
         DS    0F                                                               
CLCTLS   DS    0XL20               SPECIAL LIST FUNCTION CONTROL                
CLMOVF   DS    A                                                                
CLLOCF   DS    A                                                                
CLSMOV   DS    X                                                                
CLSLOC   DS    X                                                                
CLSLOG   DS    C                                                                
CLSOTH   DS    C                                                                
CLRESTR  DS    C                                                                
         DS    XL7                 SPARE                                        
         EJECT                                                                  
       ++INCLUDE MPGENQS                                                        
         EJECT                                                                  
         PRINT OFF                                                              
       ++INCLUDE CTGENFILE                                                      
       ++INCLUDE DDCOMFACS                                                      
         PRINT ON                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'016MPRDR00UK 05/01/02'                                      
         END                                                                    
