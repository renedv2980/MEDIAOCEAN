*          DATA SET FATASKER   AT LEVEL 013 AS OF 03/23/19                      
*CATALP FATASKER                                                                
         TITLE 'COMBINED TOR/AOR SCHEDULER/TASK CONTROLLER'                     
ADNEXT   CSECT                                                                  
         PRINT NOGEN                                                            
         ENTRY ADFIRST                                                          
         ENTRY ADFIRREG                                                         
         ENTRY ADWAIT                                                           
         ENTRY SCWAIT                                                           
         ENTRY TKWAIT                                                           
         ENTRY ADIRPT                                                           
         ENTRY MQWAIT                                                           
         ENTRY VSWAIT                                                           
         ENTRY TIMERECB                                                         
         ENTRY POSTECB                                                          
         ENTRY UTLZERO                                                          
         ENTRY ABNDECB                                                          
         ENTRY DSPACECB                                                         
         ENTRY DSOPSECB                                                         
         ENTRY ECBLST                                                           
         ENTRY AUTOSRTB                                                         
         ENTRY SMFREC                                                           
         EJECT                                                                  
***********************************************************************         
* MAIN PROGRAM ENTRY POINT                                            *         
***********************************************************************         
         NBASE 0,TASKNEXT,WORK=TSKWRK                                           
         SAC   0                                                                
         L     R9,MTSKC                                                         
         USING TASKCNST,R9                                                      
         L     RA,VSYSFX                                                        
         USING SYSFACD,RA                                                       
         L     R8,VSSB                                                          
         USING SSBD,R8                                                          
         BRAS  RE,ARSOFF           RESET ARS                                    
*                                                                               
         SAM31                                                                  
         TM    SSBSTAT5,SSB5ESPY   FORCE ESPIE RESET?                           
         BZ    MAIN02              NO                                           
         NI    SSBSTAT5,255-SSB5ESPY                                            
*                                                                               
         ESPIE RESET,=F'0'                                                      
         L     R2,VABENDPC         SET MVS ESPIE EXIT                           
         ESPIE SET,(2),((1,7),9,11,12,15)                                       
*&&UK*&& WTO   'AUTONOTE*EU-FAC-TEAM:ESPIE WAS RESET'                           
*&&US*&& WTO   'AUTONOTE*US-MF_FAC_NOTIFY:ESPIE WAS RESET'                      
         EJECT                                                                  
***********************************************************************         
* SUSPEND TIMER SUPPORT AND CLEAR SSB TASK ADDRESS                    *         
***********************************************************************         
MAIN02   GOTO1 VTICTOC,DUB,C'SSET'                                              
         XC    SSBTKADR,SSBTKADR   CLEAR SSBTKADR                               
*                                                                               
SEFST    BRAS  RE,ARSOFF           RESET ARS                                    
         SAM31                                                                  
         MVC   TRACERTN,=CL8'SEFST'                                             
         CLI   TORFLAG,C'Y'                                                     
         BE    TORFST                                                           
         B     AORFST                                                           
*                                                                               
MTSKC    DC    A(TASKCNST)                                                      
TSKWRK   DC    A(TASKWORK)                                                      
         EJECT                                                                  
***********************************************************************         
* RESET ACCESS REGISTERS & TEST TIMER EXPIRY                          *         
***********************************************************************         
TORFST   MVI   MODE,0              SET NORMAL MODE                              
         NOP   *+12                                                             
         OI    *-3,X'F0'                                                        
         OI    MODE,X'80'          SET FIRST TIME MODE                          
         TM    SSBT2,X'80'                                                      
         BZ    *+8                                                              
         OI    MODE,X'02'          SET MAJOR TIMER EXPIRED MODE                 
         TM    SSBSTAT4,SSBNOGO    DO NOT DISPATCH NEW TRANSACTIONS             
         BO    VTAMDSPC                                                         
         EJECT                                                                  
***********************************************************************         
* FIND THE MOST APPROPRIATE TRANSACTION TO PROCESS                    *         
***********************************************************************         
         BRAS  RE,SCHEDULE         SCHEDULING ALGORITHM                         
         USING SELISTD,R7          RETURNS A(SELIST ENTRY) IN R7                
         BNE   VTAMDSPC            NO TRANSACTIONS TO PROCESS                   
         LTR   R7,R7                                                            
         JZ    *+2                 DIE IF NO SELIST ENTRY                       
         EJECT                                                                  
***********************************************************************         
* FIND THE BEST AVAILABLE TASK TO PROCESS TRANSACTION                 *         
***********************************************************************         
         BRAS  RE,PCOMPS           CHECK AOR FOR COMPLETIONS                    
         BNE   SEFST                                                            
         BRAS  RE,TASKFILL                                                      
         BE    SEFST               AOR WILL PROCESS TRANSACTION                 
         BL    VTAMDSPC            NO FREE TASKS (CHECK COMPLETIONS)            
*                                                                               
         BRAS  RE,ARSOFF                                                        
         LAM   AR2,AR2,SSBALET     TOR MUST PROCESS TRANSACTION                 
         ICM   R2,15,SSBATOR                                                    
         SAC   512                                                              
         AHI   R2,TORFACLQ+6       FIRST ENTRY IS ALWAYS FOR TOR                
         USING SBEXCHD,R2                                                       
*                                                                               
         ICM   R0,15,SBTSKMAX                                                   
         CPYA  AR3,AR2                                                          
         LA    R3,SBTSKBLK         FIND NEW TASK TO PROCESS                     
         USING EXCHNGD,R3                                                       
*                                                                               
TFST02   CLI   EXCFLAG,EXCNEW      NEW TASK?                                    
         BE    TFST04              YES                                          
         AHI   R3,EXCHNGLQ                                                      
         BCT   R0,TFST02                                                        
         DC    H'0'                                                             
*                                                                               
TFST04   L     R4,VTCB             MATCH TCB SLOT TO EXCHANGE SLOT              
         LH    RE,0(R4)                                                         
         L     RF,2(R4)                                                         
         LA    R4,6(R4)                                                         
         USING TCBD,R4                                                          
         CLC   EXCIDENT,TCBID                                                   
         BE    TFST06                                                           
         BXLE  R4,RE,*-10          TCB IS MESSED UP                             
         DC    H'0'                                                             
*                                                                               
TFST06   ST    R4,SSBTKADR         SET A(TASK) IN SSB                           
         MVI   EXCFLAG,EXCBUSY     SET THIS TASK NOW PROCESSING                 
*                                                                               
         LH    RF,EXCUTL           PICK UP TNUM & INDEX INTO UTL LIST           
         AHI   RF,-1                                                            
         L     RE,VUTL                                                          
         MH    RF,0(RE)                                                         
         LA    R5,6(RE,RF)         R5=A(UTL)                                    
         BRAS  RE,DISPATCH                                                      
         DC    H'0'                                                             
         DROP  R2,R3,R4                                                         
         EJECT                                                                  
***********************************************************************         
* FIND AND INITIALISE NEXT UNIT OF WORK TO PROCESS                    *         
***********************************************************************         
AORFST   TM    SSBSTAT4,SSBNOGO    DO NOT DISPATCH NEW TRANSACTIONS             
         BO    IOWAIT                                                           
         BRAS  RE,TASKFIND                                                      
         BNE   MQDSPC              NO NEW WORK                                  
*                                                                               
         ICM   R3,15,SSBTKADR                                                   
         USING TCBD,R3                                                          
         L     R5,TCBUTL                                                        
         L     R7,TCBSE                                                         
         BRAS  RE,DISPATCH         DISPATCH VIA MONITOR/SCRUNCH                 
         DC    H'0'                                                             
         EJECT                                                                  
***********************************************************************         
* NOTHING TO DISPATCH - SEE IF VTAM HAS ANYTHING                      *         
***********************************************************************         
VTAMDSPC BRAS  RE,ARSOFF                                                        
         MVC   TRACERTN,=CL8'VTAMDSPC'                                          
         TM    POSTECB,POSTED      CHECK AOR COMPLETIONS FIRST                  
         BZ    VTDS02                                                           
         BRAS  RE,PCOMPS           PROCESS ALL AOR COMPLETIONS                  
         BNE   SEFST               REQUEUED TRANSACTIONS                        
*                                                                               
VTDS02   TM    ABNDECB,POSTED      THEN TEST AOR ABENDED                        
         BZ    VTDS04                                                           
         NI    ABNDECB,255-POSTED                                               
         GOTO1 VTORRCV,TO24=Y      RECOVER ABENDING AOR                         
         B     SEFST               SEFST GETS BACK INTO XA                      
*                                                                               
VTDS04   L     R7,VRPLLST          POINT TO VTAM RPLLST                         
         USING FARPLD,R7                                                        
*                                                                               
VTDS06   L     RE,VLCMECB          TEST LINE CONTROL ECB                        
         TM    0(RE),POSTED                                                     
         BO    VTDS08                                                           
*                                                                               
         L     RE,FARPLRPL         POINT TO RPL                                 
         TM    8(RE),POSTED        TEST VTAM ECB POSTED                         
         BO    VTDS08              YES                                          
         ICM   R7,15,FARPLNXT      POINT TO NEXT RPL                            
         BNZ   VTDS06                                                           
         B     MQDSPC              DISPATCH MQ EVENT                            
                                                                                
***********************************************************************         
* COMPLETION HAS BEEN POSTED - CALL LCM TO PROCESS IT                 *         
***********************************************************************         
VTDS08   GOTO1 VLCM,DUB,VTWAIT                                                  
         B     SEFST               SEE IF ANY NEW SE'S TO DISPATCH              
                                                                                
***********************************************************************         
* CHECK MQIO EVENT POSTED                                             *         
***********************************************************************         
MQDSPC   CLI   MQIOFLAG,0          TEST FOR MQIO ACTIVE                         
         BE    IOWAIT                                                           
                                                                                
         MVC   TRACERTN,=CL8'MQDSPC  '                                          
         L     RF,VSSB                                                          
X        USING SSBD,RF                                                          
         ICM   RE,15,X.SSBMQMAX    ARE THERE BOUNDS ON THE NUM OF MQS           
         BZ    *+14                NO                                           
         CLC   X.SSBMQNUM,X.SSBMQMAX                                            
         BNL   MQDS12              IGNORE FLAG UNTIL WE CAN PROCESS IT          
         DROP  X                                                                
*                                                                               
         ICM   RE,15,VMQIOECB      TEST MQIO ECB                                
         BZ    MQDS12                                                           
         TM    0(RE),POSTED                                                     
         BZ    MQDS12                                                           
         NI    0(RE),255-POSTED                                                 
         GOTO1 VMQIO,PARM,INPUT,TO24=Y PROCESS MQIO INPUT                       
         B     SEFST                                                            
*                                                                               
MQDS12   L     RF,VSSB             DETACH ANY ATTACHED SUB-TASKS                
         L     R5,SSBAATC-SSBD(RF)                                              
         LH    R6,0(R5)                                                         
         L     R7,2(R5)                                                         
         LA    R5,6(R5)                                                         
         USING ATCD,R5             R5=A(ATC)                                    
*                                                                               
MQDS14   CLI   ATCTYPE,ATCTMQIO    TEST FAMQIO SUB TASK                         
         BNE   MQDS18              NO                                           
         TM    ATCOSECB,POSTED     TEST FOR O/S POSTED COMPLETION               
         BZ    MQDS18                                                           
         NI    ATCOSECB,255-POSTED                                              
         TM    ATCSTAT1,ATCSRSET   TEST FAMQIO SUB TASK RESET FLAG              
         BNZ   MQDS16                                                           
         TM    ATCSTAT1,ATCSATCH   TEST FAMQIO SUB TASK ATTACHED                
         BZ    MQDS18                                                           
         NI    ATCSTAT1,255-ATCSATCH                                            
         OI    ATCSTAT1,ATCSERRS   SET ERROR OCCURRED IN TASK                   
         OI    ATCSTAT2,ATCSUDER                                                
         LA    R1,ATCOSTCB         DETACH TASK                                  
         DETACH (1),STAE=NO                                                     
*                                                                               
         GOTO1 VMQIO,PARM,END,TO24=Y PROCESS FAMQIO ENDED                       
         B     SEFST                                                            
*                                                                               
MQDS16   MVI   ATCSTAT1,0                                                       
         MVC   ATCARECB,VMQIOECB                                                
         MVC   ATCAIOPL,VMQIOLST                                                
         LA    R4,SSBMQIO                                                       
         LA    R6,ATCOSECB                                                      
         ATTACH EPLOC=(R4),PARAM=((R5)),ECB=(R6),SZERO=YES                      
         ST    R1,ATCOSTCB         SAVE A(OPERATING SYSTEM TCB)                 
         B     SEFST                                                            
*                                                                               
MQDS18   BXLE  R5,R6,MQDS14                                                     
         B     IOWAIT                                                           
         DROP  R5                                                               
         EJECT                                                                  
***********************************************************************         
* NOTHING TO DO - WAIT ON DISK I/O COMPLETIONS IF ANY PENDING AND     *         
* MQ PUT PENDING IF ANY                                               *         
***********************************************************************         
IOWAIT   LA    R0,ECBLST           CLEAR ECBLST - FOR DEBUGGING CLARITY         
         LHI   R1,ECBLSTLQ                                                      
         XR    RF,RF                                                            
         MVCL  R0,RE                                                            
*                                                                               
         MVC   TRACERTN,=CL8'IOWAIT  '                                          
         XC    OLDWAIT,OLDWAIT     FOR TIMED WAITS                              
         MVI   IOECB,NO            YES WE HAVE NORMAL ECB                       
*                                                                               
         L     R3,VTCB             START AT FIRST TASK                          
         LH    R4,0(R3)                                                         
         L     R5,2(R3)                                                         
         LA    R3,6(R3)            R3=A(FIRST TASK)                             
         USING TCBD,R3                                                          
         CLI   TORFLAG,YES                                                      
         BE    *+6                                                              
         AR    R5,R4               AOR HAS EXTRA TASK FOR OWN WORK              
*                                                                               
         LHI   R0,-1               R0=TIME TO WAIT FOR DSPACE OR $WAIT          
         LA    R1,ECBLST           R1=A(NEXT ECBLIST ENTRY)                     
         SAM31                                                                  
*                                                                               
IOWT02   TM    TCBFLAG3,TCBKILL    HAS A FORCED ABEND BEEN REQUESTED            
         BZ    IOWT03                                                           
         NI    TCBFLAG3,255-TCBKILL                                             
         CLI   TCBSEN,0            DONT KILL AN EMPTY TASK (MAXIDUMP)           
         BE    IOWT03                                                           
         XC    TCBSVECB,TCBSVECB   REMOVE ANY WAIT INDICATORS                   
         XC    TCBSVRD,TCBSVRD                                                  
         XC    TCBTIMEW,TCBTIMEW                                                
         ST    R3,SSBTKADR         ABEND THIS TASK ONLY                         
         DC    H'0'                                                             
*                                                                               
IOWT03   XR    RE,RE                                                            
         ICM   RE,7,TCBSVECB+1     CAN I WAIT ON THIS TASK                      
         BZ    IOWT10              NO                                           
*                                                                               
         CLI   TCBSVECB,X'FA'      TEST 31-BIT ECB ADDRESS                      
         JNE   IOWT03A             USED FOR SHRMEM RECOVERY                     
         L     RE,TCBSE                                                         
         L     RE,SERCVDTF-SELISTD(RE)  POINT TO DTF                            
         L     RE,DBLK-DTFPHD(RE)       POINT TO SHRMEM BUFFER                  
         LA    RE,8(RE)                 POINT TO ECB                            
         TM    0(RE),POSTED                                                     
         BZ    IOWT09                                                           
         B     TASK300                                                          
*                                                                               
IOWT03A  CLI   TCBSVECB,X'FB'      TEST 31-BIT ECB ADDRESS                      
         JNE   IOWT03B             USED FOR ISGENQ WAITS                        
         XR    RE,RE                                                            
         ICM   RE,7,TCBSVECB+1                                                  
         A     RE,SSBXAISG         ADD SSBXA ADDR                               
         TM    0(RE),POSTED                                                     
         BZ    IOWT09                                                           
         B     TASK300                                                          
*                                                                               
IOWT03B  CLI   TCBSVECB,X'FC'      TEST WHAT TYPE OF ECB                        
         BL    IOWT04              NORMAL                                       
         CLI   TCBSVECB,X'FF'      TEST WHAT TYPE OF ECB                        
         BE    IOWT06              NORMAL                                       
         B     IOWT08              DATASPACE-WAIT TYPE ECB                      
*                                                                               
IOWT04   LR    R6,R1               SAVE ECBLST                                  
         GOTO1 VSHIPFIX,TCBD,TO24=Y FIX SHIPIT POST IF IT GOT LOST              
         SAM31                                                                  
         LR    R1,R6                                                            
         XR    RE,RE                                                            
         ICM   RE,7,TCBSVECB+1                                                  
         TM    0(RE),POSTED        TEST COMPLETION ON NORMAL ECB                
         BO    TASK300             GO DISPATCH                                  
*                                                                               
         MVI   IOECB,YES           YES WE HAVE NORMAL ECB                       
         ST    RE,0(R1)            PUT NORMAL ECBS IN ECBLIST                   
         LA    R1,4(R1)                                                         
         B     IOWT14                                                           
*                                                                               
IOWT06   TM    0(RE),POSTED        TEST COMPLETION ON DO-NOT-WAIT ECB           
         BO    TASK300             YES - PROCESS COMPLETION                     
         B     IOWT09              SET A TIMER                                  
*                                                                               
IOWT08   BRAS  RE,GETALET                                                       
         XR    RE,RE               ECB CONTAINS INDEX TO DATASPACE ECB          
         ICM   RE,7,TCBSVECB+1                                                  
         AR    R2,RE               INDEX TO DATASPACE ECB                       
         TM    0(R2),POSTED        TEST POSTED COMPLETE                         
         SAC   0                                                                
         BO    TASK300             PROCESS COMPLETION                           
*                                                                               
IOWT09   CL    R0,T1TENTH          DATASPACE - WAIT 1/10 SECONDS                
         BL    IOWT14                                                           
         L     R0,T1TENTH          DATASPACE - WAIT 1/10 SECONDS                
         B     IOWT14                                                           
*                                                                               
IOWT10   ICM   RE,15,TCBTIMEW      TEST FOR TASK WAIT                           
         BZ    IOWT14                                                           
         STM   R0,R1,DUB           PROTECT R0,R1 FROM TIME MACRO                
         TIME  TU                                                               
         LR    RE,R0               RE=TIME NOW (TUS)                            
         LM    R0,R1,DUB                                                        
*                                                                               
         ICM   RF,15,TCBTIMEW                                                   
         SLR   RF,RE               RF=TUS UNTIL WAKEUP TIME                     
         BM    IOWT12                                                           
         CLR   R0,RF               R0=TIME TO GO UNTIL POP                      
         BL    IOWT14                                                           
         LR    R0,RF               SAVE SMALLEST TIME                           
         B     IOWT14                                                           
                                                                                
IOWT12   ICM   RF,15,OLDWAIT       ANY READY TO RUN ALREADY SET?                
         BNZ   *+12                                                             
         ST    R3,OLDWAIT                                                       
         B     IOWT14                                                           
*                                                                               
         CLC   TCBTIMEW,TCBTIMEW-TCBD(RF)                                       
         BH    IOWT14                                                           
         ST    R3,OLDWAIT          SET NEW EARLIEST TIMER POP                   
         B     IOWT14                                                           
*                                                                               
IOWT14   BXLE  R3,R4,IOWT02        BACK FOR NEXT TCB ENTRY                      
                                                                                
***********************************************************************         
* NOW START TO RUN TASK OR WAIT ON ECBS                               *         
***********************************************************************         
         ICM   R3,15,OLDWAIT                                                    
         BZ    *+14                                                             
         MVC   TCBTIMEW,MINUSONE   SET TO FFS IF READY TO RUN                   
         B     TASK300                                                          
         LTR   R0,R0               ANY TIMER TO SET?                            
         BNZ   *+8                                                              
         L     R0,T1SEC                                                         
*                                                                               
IOWT16   OC    ECBLST(4),ECBLST    ANY TASK IO WAIT ENTRIES IN ECBLIST          
         BZ    POSTWAIT            NO                                           
         LR    R0,R1                                                            
         GOTO1 VTICTOC,DUB,C'WSET',TO24=Y START AN EVENT TIMER                  
         SAM31                                                                  
         LR    R1,R0                                                            
*                                                                               
         LA    RF,TIMERECB         ADD TIMER ECB                                
         BRAS  RE,ADDTOLST                                                      
         LA    RF,POSTRECB         ADD POST ECB                                 
         ST    RF,0(R1)                                                         
         LA    R1,4(R1)                                                         
         LA    RF,DSPACECB         DSPACE ECB                                   
         BRAS  RE,ADDTOLST                                                      
         LA    RF,DSOPSECB         DSPACE OPS COMMAND ECB                       
         BRAS  RE,ADDTOLST                                                      
*                                                                               
IOWT18   MVC   0(4,R1),SSBOPECB    ADD OPER ECB TO LIST                         
         OI    0(R1),X'80'         SET EOL FLAG                                 
         LA    R1,ECBLST                                                        
         WAIT  ECBLIST=(1)         WAIT FOR DISK I/O COMPLETION                 
*                                                                               
         TM    POSTRECB,POSTED     TEST POST                                    
         BO    SEFST                                                            
         TM    TIMERECB,POSTED     TEST TIMER                                   
         BO    SEFST                                                            
         L     RF,TRACETIM                                                      
         LA    RF,1(,RF)                                                        
         ST    RF,TRACETIM                                                      
*                                                                               
         TM    DSPACECB,POSTED     TEST FOR DSPACE INTERUPT                     
         BO    SEFST                                                            
         TM    POSTRECB,POSTED     TEST FOR WAKE UP POST                        
         BO    SEFST                                                            
*                                                                               
         L     R1,SSBOPECB         TEST FOR OPERATOR INTERUPT                   
         TM    0(R1),POSTED                                                     
         BO    SEFST               GO PROCESS OPERATOR COMMAND                  
         B     TASK300             GO PROCESS WORK                              
         EJECT                                                                  
***********************************************************************         
* NO DISK I/OS PENDING - WAIT ON VTAM,INTERVAL TIMER,OR DSPACE POST   *         
***********************************************************************         
POSTWAIT TM    SSBSTAT1,SSBSEOJ    TEST EOJ PENDING                             
         BZ    PWT02                                                            
         C     R0,MINUSONE         TEST WAITING FOR ANYTHING                    
         BNE   PWT02                                                            
         L     RF,AADLAST          COMPLETE EOJ HOUSE KEEPING                   
         BASR  RE,RF                                                            
         LM    RB,RE,ADFIRREG      RESTORE RETURN REGISTERS                     
         BR    RE                                                               
*                                                                               
PWT02    TM    SSBSTAT4,SSBNOGO                                                 
         BZ    *+12                                                             
         BRAS  RE,RBLDCORE                                                      
         B     SEFST                                                            
*                                                                               
         MVC   TRACERTN,=CL8'POSTWAIT'                                          
         OI    SSBSTAT1,SSBSVTAM   SET AWAITING VTAM                            
         C     R0,MINUSONE         FFS MEANS NO TIMER3                          
         BE    PWT04                                                            
*        XR    R0,R0                                                            
         GOTO1 VTICTOC,DUB,C'3SET',(R0),TO24=Y  SET T3                          
         SAM31                                                                  
*                                                                               
PWT04    GOTO1 VTICTOC,DUB,C'WSET',TO24=Y START AN EVENT TIMER                  
         SAM31                                                                  
         CLI   MQIOFLAG,0          TEST FOR MQIO ACTIVE                         
         BE    PWT05                                                            
         GOTO1 VMQIO,PARM,RESET,TO24=Y RESET MQIO INPUT TRIGGER MONITOR         
         SAM31                                                                  
*                                                                               
PWT05    LA    R1,ECBLST           BUILD ECBLST FOR EXTERNAL WAIT               
         CLI   TORFLAG,C'N'                                                     
         BE    PWT06               TOR WAIT FOR VTAM AOR WAIT FOR POST          
*                                                                               
         L     RE,VLCMECB          LCM CONTROL ECB                              
         ST    RE,0(R1)                                                         
         LA    R1,4(R1)                                                         
*                                                                               
PWT06    LA    RF,POSTRECB         POST ECB                                     
         ST    RF,0(R1)                                                         
         LA    R1,4(R1)                                                         
         LA    RF,TIMERECB         TIMER ECB                                    
         BRAS  RE,ADDTOLST                                                      
         LA    RF,DSPACECB         DSPACE ECB                                   
         BRAS  RE,ADDTOLST                                                      
         LA    RF,DSOPSECB         DSPACE OPS COMMAND ECB                       
         BRAS  RE,ADDTOLST                                                      
*                                                                               
         CLI   TORFLAG,C'N'        AOR COMPLETIONS FOR TOR ONLY                 
         BE    PWTMQ                                                            
         LA    RF,POSTECB          AOR COMPLETION ECB                           
         BRAS  RE,ADDTOLST                                                      
         LA    RF,ABNDECB          AOR ABEND ECB                                
         BRAS  RE,ADDTOLST                                                      
         EJECT                                                                  
***********************************************************************         
* ADD MQ ECBS                                                         *         
***********************************************************************         
PWTMQ    CLI   MQIOFLAG,0          TEST MQIO ACTIVE                             
         BE    PWT20                                                            
         ICM   RE,15,VMQIOECB      MQIO ECB                                     
         BZ    PWT10                                                            
         ST    RE,0(R1)                                                         
         LA    R1,4(R1)                                                         
*                                                                               
PWT10    L     R5,SSBAATC          ADD OS ECB OF FAMQIO ATTACHED TASK           
         LH    R6,0(R5)                                                         
         L     R7,2(R5)                                                         
         LA    R5,6(R5)                                                         
         USING ATCD,R5             R5=A(ATC)                                    
PWT11    CLI   ATCTYPE,ATCTMQIO    TEST FAMQIO SUB-TASK                         
         BNE   PWT12               NO                                           
         TM    ATCSTAT1,ATCSATCH   TEST ATTACHED                                
         BZ    PWT12               NO                                           
*                                                                               
         LA    RE,ATCOSECB                                                      
         ST    RE,0(R1)                                                         
         LA    R1,4(R1)                                                         
PWT12    BXLE  R5,R6,PWT11                                                      
         DROP  R3,R5                                                            
*                                                                               
         L     R5,VTCB             ADD MQIO TASK WAIT ECB'S                     
         LH    R6,0(R5)                                                         
         L     R7,2(R5)                                                         
         LA    R5,6(R5)                                                         
         USING TCBD,R5                                                          
         CLI   TORFLAG,C'Y'                                                     
         BE    *+6                                                              
         AR    R5,R4               AOR HAS EXTRA TASK FOR OWN WORK              
*                                                                               
PWT13    TM    TCBFLAG3,TCBMQWT    TEST TASK WAITING ON MQIO                    
         BZ    PWT14                                                            
         ICM   RE,15,TCBMQERT                                                   
         BZ    PWT14                                                            
         ST    RE,0(R1)                                                         
         LA    R1,4(R1)                                                         
*                                                                               
PWT14    BXLE  R5,R6,PWT13         BACK FOR NEXT TCB ENTRY                      
         DROP  R5                                                               
         EJECT                                                                  
***********************************************************************         
* COMPLETE ECB LIST AND WAIT                                          *         
***********************************************************************         
PWT20    CLI   TORFLAG,C'N'        VTAM ECBS FOR TOR ONLY                       
         BE    PWT40                                                            
*                                                                               
         L     R7,VRPLLST          VTAM RPL ECB'S ARE REST OF LIST              
         USING FARPLD,R7                                                        
PWT30    L     RE,FARPLRPL         POINT TO RPL                                 
         LA    RE,8(RE)            POINT TO INTERNAL ECB                        
         ST    RE,0(R1)                                                         
         LA    R1,4(R1)                                                         
         ICM   R7,15,FARPLNXT      POINT TO NEXT RPL                            
         BNZ   PWT30                                                            
*                                                                               
PWT40    L     RE,VSSB                                                          
         MVC   0(4,R1),SSBOPECB    ADD OPERATOR ECB TO LIST AT END              
         OI    0(R1),X'80'         SET EOL FLAG                                 
         MVC   TRACEDMP,=CL8'PW-WAIT '                                          
         LA    R1,ECBLST                                                        
         WAIT  ECBLIST=(1)         WAIT ON VTAM/TIMER                           
         L     RF,TRACEPOP                                                      
         LA    RF,1(,RF)                                                        
         ST    RF,TRACEPOP                                                      
         MVC   TRACEDMP,=CL8'PW-POST '                                          
*                                                                               
         NI    SSBSTAT1,255-SSBSVTAM                                            
*                                                                               
         TM    DSOPSECB,POSTED     TEST FOR DSPACE OPER                         
         BO    SEFST                                                            
         TM    TIMERECB,POSTED     TEST FOR TIMER INTERUPT                      
         BO    SEFST                                                            
         TM    DSPACECB,POSTED     TEST FOR DSPACE INTERUPT                     
         BO    SEFST                                                            
         TM    POSTRECB,POSTED     TEST FOR WAKE UP POST                        
         BO    SEFST                                                            
*                                                                               
         L     R1,SSBOPECB         TEST FOR OPERATOR INTERUPT                   
         TM    0(R1),POSTED                                                     
         BO    SEFST               GO PROCESS IT                                
*                                                                               
         GOTO1 VTICTOC,DUB,C'SSET',TO24=Y SUSPEND TIMERS                        
         SAM31                                                                  
*                                                                               
         CLI   TORFLAG,C'Y'        TEST TOR                                     
         BE    VTAMDSPC            YES,TRY VTAM                                 
         B     MQDSPC              NO, TRY MQ INSTEAD                           
         EJECT                                                                  
***********************************************************************         
* DISK I/O COMPLETION POSTED - FIND WHICH TASK                        *         
***********************************************************************         
         USING TCBD,R3             NOTE R4 HAS LEN,R5 HAS END ADDR              
TASK300  BRAS  RE,ARSOFF                                                        
         MVC   TRACERTN,=CL8'TASK300 '                                          
         L     R3,LASTTCB          GET LAST TCB POSTED                          
         B     TASK350             BUMP TO NEXT                                 
*                                                                               
TASK320  XR    R1,R1                                                            
         ICM   R1,7,TCBSVECB+1     GET IOB ADDRESS                              
         BZ    TASK340                                                          
*                                                                               
         CLI   TCBSVECB,X'FA'                                                   
         JNE   TASK321                                                          
         SAM31                                                                  
         L     R1,TCBSE                 GET SELIST ENTRY                        
         L     R1,SERCVDTF-SELISTD(R1)  POINT TO RECOVERY DTF                   
         L     R1,DBLK-DTFPHD(R1)       POINT TO SHRMEM                         
         LA    R1,8(R1)                 POINT TO ECB                            
         TM    0(R1),POSTED                                                     
         BO    TASK322                                                          
         SAM24                                                                  
         B     TASK350                                                          
*                                                                               
TASK321  CLI   TCBSVECB,X'FB'      FB IS ISGENQ XA ECB                          
         JNE   TASK325                                                          
         SAM31                                                                  
         XR    R1,R1                                                            
         ICM   R1,7,TCBSVECB+1                                                  
         A     R1,SSBXAISG         ADD ISGENQ XA OFFSET                         
         TM    0(R1),POSTED                                                     
         BO    TASK323                                                          
         SAM24                                                                  
         B     TASK350                                                          
*                                                                               
TASK322  MVI   0(R1),0             CLEAR THE ECB NOW                            
TASK323  SAM24                                                                  
         B     TASK400                                                          
*                                                                               
TASK325  CLI   TCBSVECB,X'FC'      TEST DATASPACE-WAIT                          
         BL    TASK330                                                          
         CLI   TCBSVECB,X'FE'      TEST DATASPACE-WAIT                          
         BH    TASK330                                                          
*                                                                               
         BRAS  RE,GETALET          GET CORRECT DSPACE ALET                      
         AR    R2,R1               INDEX TO DATASPACE ECB                       
         TM    0(R2),POSTED        TEST POSTED COMPLETE                         
         BRAS  RE,ARSOFF                                                        
         BO    TASK400             DISPATCH IF FREE                             
         B     TASK350                                                          
*                                                                               
TASK330  TM    0(R1),POSTED        TEST POSTED COMPLETE                         
         BO    TASK400             YES                                          
         B     TASK350                                                          
*                                                                               
TASK340  OC    TCBSVRD,TCBSVRD     IS RD SAVED                                  
         BZ    TASK350             NO                                           
         CLC   TCBTIMEW,MINUSONE   IS IT TIME TO WAKE UP                        
         BE    TASK400                                                          
         OC    TCBTIMEW,TCBTIMEW   ARE WE WAITING FOR WAKE UP                   
         BNZ   TASK350                                                          
         OC    TCBLOCK+1(3),TCBLOCK+1                                           
         BZ    TASK400             DISPATCH IF DATA NOW FREE                    
*                                                                               
TASK350  BXLE  R3,R4,TASK320                                                    
         L     R3,VTCB             NOW START AT TOP OF LIST                     
         LA    R3,6(R3)                                                         
         B     TASK320             LOOP BACK                                    
         EJECT                                                                  
***********************************************************************         
* THIS TASK IS READY TO RUN - TEST IF CANCEL PENDING FROM TERMINAL    *         
***********************************************************************         
TASK400  L     RE,TCBUTL           TEST FOR CANCEL PENDING THIS TASK            
         TM    TSTAT2-UTLD(RE),TSTATCAN                                         
         BZ    TASK500                                                          
         CLI   TCBSEN,1            IGNORE IF S/R PROGRAM                        
         BE    TASK500                                                          
         OC    TCBRCVC,TCBRCVC     IGNORE IF RECS IN RECOVERY FILE              
         BNZ   TASK500                                                          
         TM    TCBFLAG1,TCBENQPQ+TCBENQWK IGNORE IF ENQUEUE ISSUED              
         BNZ   TASK500                                                          
         TM    TCBFLAG2,TCBENQFW+TCBENQCT+TCBENQMZ                              
         BNZ   TASK500                                                          
         TM    TCBFLAG1,TCBIRALC   IGNORE IF IRDR ALLOCATED                     
         BNZ   TASK500                                                          
*                                                                               
         L     R0,VMNBRPRG                                                      
         L     RF,TCBWRKA                                                       
         L     RF,8(RF)            POINT TO MONITOR'S REGISTERS                 
         CLM   R0,7,13(RF)                                                      
         BNE   TASK500             IGNORE IF RE NOT VIA MONBRPRG                
         MVC   TCBSVRD,8(RF)       SET RD TO RETURN DIRECT TO MONITOR           
         XC    TCBPSW(72),TCBPSW   NEGATE TIMER POP EXIT EFFECTOR               
         MVC   TSVCREQ-UTLD(2,RE),$CAN                                          
         B     TASK600             EXIT BACK TO MONITOR WITH $CAN               
         EJECT                                                                  
***********************************************************************         
* THIS TASK IS READY TO RUN - TEST IF PRIORITIZED TASK                *         
***********************************************************************         
TASK500  C     R3,LASTTCB          DISPATCH IF THIS TASK SAME AS LAST           
         BE    TASK600                                                          
         ST    R3,LASTTCB          SAVE TCB ENTRY ADDRESS                       
         USING TCBD,R3                                                          
         SR    R2,R2               SET R2 TO PROGRAM PRIORITY                   
         ICM   R2,1,TCBPRTY                                                     
         BZ    TASK600                                                          
         TBIN  MILLI               GET TIME IN MILLI SECS                       
         MR    R0,R0                                                            
         ALR   R1,R0                                                            
         SRL   R1,5                SQUARE,FOLD,EXTRACT                          
         N     R1,=X'0000000F'                                                  
         L     RF,SSBPRTST                                                      
         LA    RF,1(RF)                                                         
         ST    RF,SSBPRTST                                                      
         CR    R1,R2               COMPARE TIME TO PRIORITY                     
         BH    TASK300             IF HIGH DONT DISPATCH THIS TIME              
         L     RF,SSBPRPAS                                                      
         LA    RF,1(RF)                                                         
         ST    RF,SSBPRPAS                                                      
         B     TASK600                                                          
         EJECT                                                                  
***********************************************************************         
* THIS TASK IS READY TO RUN - RETURN CONTROL BACK TO HIM              *         
***********************************************************************         
TASK600  L     RD,TCBSVRD          GET SAVED REG AND CLEAR SAVE AREA            
         XC    TCBSVECB(8),TCBSVECB                                             
         OC    TCBPSW,TCBPSW       TEST WAIT CAUSED BY TIMER POP                
         BNZ   TPOP                                                             
*                                                                               
         ST    R3,SSBTKADR                                                      
         USING TCBD,R3                                                          
         GOTO1 VDTFIOA,DUB,(C'R',(R3)),TO24=Y RESTORE DTF SAVE DATA             
         SAM31                                                                  
*                                                                               
         CLC   TCBTIMEW,MINUSONE   TEST TIME TO WAKE UP                         
         BNE   *+14                                                             
         XC    TCBTIMEW,TCBTIMEW   CLEAR TIME FIELD                             
         B     TASK620                                                          
*                                                                               
         TM    TCBFLAG3,TCBSCPWT   TEST SCRIPT COMPLETION                       
         BZ    *+12                                                             
         NI    TCBFLAG3,255-TCBSCPWT                                            
         B     TASK620                                                          
*                                                                               
         TM    TCBFLAG3,TCBJ3WT    TEST SMTP COMPLETION                         
         BZ    *+12                                                             
         NI    TCBFLAG3,255-TCBJ3WT                                             
         B     TASK620                                                          
*                                                                               
TASK610  TM    TCBFLAG3,TCBMQWT    TEST MQIO COMPLETION                         
         BZ    TASK630                                                          
         NI    TCBFLAG3,255-TCBMQWT                                             
*                                                                               
TASK620  GOTO1 VTICTOC,DUB,C'1SET',1,TO24=Y                                     
         SAM31                                                                  
*                                                                               
         L     R0,TCBTKWT          TKWAIT LOOP CHECK                            
         C     R0,MAXTKWTS                                                      
         BH    TASK625                                                          
         XC    TCBIRPTS,TCBIRPTS   RESET TIMER INTERRUPT COUNTER                
         MVC   TCBTIME,DUB         SET EVENT TIME                               
         TIMEUSED STORADR=TCBCPUSE,LINKAGE=SYSTEM,CPU=MIC                       
         B     DIOEXIT                                                          
*                                                                               
TASK625  STCM  R0,7,MAXTKS         DIE IF TASK HAS EXCEEDED MAX TK'S            
         DC    H'0'                                                             
*                                                                               
MAXTKS   DC    XL3'00',CL13'MAX TKWAITS  '                                      
*                                                                               
TASK630  CLI   TCBPRTY,0           TEST IF PRIORITIZED TASK                     
         BE    TASK640                                                          
         TM    TCBPRTYF,X'80'      TEST PREVIOUSLY EXCEEDED                     
         BO    TASK640                                                          
         CLC   TCBIOCNT+1(2),SSBPRIO TEST I/O THRESHOLD EXCEEDED                
         BL    TASK640                                                          
         LLC   RF,TCBPRTY          DECREMENT TASK PRIORITY                      
         SRL   RF,1                                                             
         CH    RF,SSBPRMIN                                                      
         BNL   *+8                                                              
         LH    RF,SSBPRMIN                                                      
         STC   RF,TCBPRTY                                                       
         OI    TCBPRTYF,X'80'      SET TASK EXCEEDED I/O THRESHOLD              
*                                                                               
TASK640  GOTO1 VTICTOC,DUB,C'1SET',1,TO24=Y                                     
         SAM31                                                                  
         XC    TCBIRPTS,TCBIRPTS   RESET TIMER INTERRUPT COUNTER                
         XC    TCBTKWT,TCBTKWT     RESET TKWAIT COUNTER                         
         MVC   TCBTIME,DUB         SET EVENT TIME                               
         TIMEUSED STORADR=TCBCPUSE,LINKAGE=SYSTEM,CPU=MIC                       
         SR    R0,R0                                                            
         ICM   R0,3,SSBMAXIO                                                    
         CLM   R0,7,TCBIOCNT       TEST I/O COUNT WITH NORMAL MAX               
         BNL   DIOEXIT                                                          
*                                                                               
         L     R1,TCBUTL           GET A(TASK UTL ENTRY)                        
X        USING UTLD,R1                                                          
         XR    RF,RF                                                            
         TM    X.TSTAT6,TST6SCRP   TEST SCRIPT EXECUTING                        
         BO    DIOEXIT                                                          
         TM    X.TSTATU,TSTATMIO   TEST PROGRAM WANTS TO EXCEED                 
         BO    DIOEXIT                                                          
         OC    X.TSVCREQ,X.TSVCREQ                                              
         BZ    TASK650                                                          
         CLC   X.TSVCREQ,$WKR      TEST SPECIAL SERVICE REQUESTS                
         BE    DIOEXIT                                                          
         CLC   X.TSVCREQ,$BC                                                    
         BE    DIOEXIT                                                          
         CLC   X.TSVCREQ,$$BYE                                                  
         BE    DIOEXIT                                                          
         CLC   X.TSVCREQ,$UNWIND                                                
         BE    DIOEXIT                                                          
         CLC   X.TSVCREQ,$ABEND                                                 
         BE    DIOEXIT                                                          
         ICM   RF,7,X.TASVC        IF S/R - POINT TO S/R PGMLST ENTRY           
         BNZ   TASK660                                                          
         B     TASK670                                                          
*                                                                               
TASK650  ICM   RF,7,X.TAPRG        GET A(PGM LIST ENTRY)                        
         BZ    TASK670                                                          
         XR    RE,RE                                                            
         ICM   RE,7,X.TARSYS                                                    
         BZ    TASK670                                                          
         ICM   RE,15,SEPGMS-SELISTD(RE)                                         
         AR    RF,RE                                                            
         DROP  X                                                                
*                                                                               
TASK660  TM    PGMIND-PGMLSTD(RF),PGMIIOB TEST IF PGM I/O BOUND                 
         BZ    TASK670                                                          
         L     R0,MAXMAXIO         NORMAL MAXIMUM I/O COUNT                     
         CLI   TCBSWSOV,6                                                       
         BNE   *+8                                                              
         L     R0,ACCMAXIO         ACCOUNT MAXIMUM I/O COUNT                    
         CLI   TCBSWSOV,7                                                       
         BNE   *+8                                                              
         L     R0,TALMAXIO         TALENT  MAXIMUM I/O COUNT                    
         CLM   R0,7,TCBIOCNT                                                    
         BNL   DIOEXIT                                                          
*                                                                               
TASK670  MVC   MAXIOS,TCBIOCNT     STORE ACTUAL NUMBER OF I/OS                  
         L     RF,TCBLIOCT                                                      
         A     RF,TCBLIOC                                                       
         ST    RF,TCBLIOCT         BUMP TOTAL LOGICAL I/OS                      
         XC    TCBLIOC,TCBLIOC                                                  
         XC    TCBIOCNT,TCBIOCNT                                                
         DC    H'0'                DIE IF TASK HAS EXCEEDED MAX I/0'S           
*                                                                               
MAXIOS   DC    XL3'00',CL13'MAX IOS      '                                      
*                                                                               
DIOEXIT  LAM   AR0,ARF,TCBIARS     RESTORE ACCESS REGISTERS (ADWAIT)            
         B     EXIT                EXIT BACK TO WHERE TASK ISSUED I/O           
         EJECT                                                                  
***********************************************************************         
* THIS PROCESSING USED WHEN WAIT CAUSED BY TIMER POP                  *         
***********************************************************************         
TPOP     L     RD,ATSKWK           POINT TO WORK AREA FOR SUBR ENTRY            
         ST    R3,SSBTKADR                                                      
         GOTO1 VDTFIOA,DUB,(C'R',(R3)),TO24=Y RESTORE DTF SAVE DATA             
         SAM31                                                                  
*                                                                               
         L     RE,VSSB                                                          
         CLI   TCBPRTY,0           TEST IF PRIORITIZED TASK                     
         BE    TPOP02                                                           
         TM    TCBPRTYF,POSTED     TEST PREVIOUSLY EXCEEDED                     
         BO    TPOP02                                                           
         CLC   TCBCPUTM+2(2),SSBPRCPU                                           
         BL    TPOP02                                                           
         LLC   RF,TCBPRTY          DECREMENT TASK PRIORITY                      
         SRL   RF,1                                                             
         CH    RF,SSBPRMIN                                                      
         BNL   *+8                                                              
         LH    RF,SSBPRMIN                                                      
         STC   RF,TCBPRTY                                                       
         OI    TCBPRTYF,POSTED     SET TASK EXCEEDED CPU THRESHOLD              
*                                                                               
TPOP02   L     RE,VABNDWHY                                                      
         MVI   0(RE),C'X'          SET TASKER EXIT FLAG FOR PC ROUTINE          
         DC    H'0'                AND EXIT VIA PROGRAM CHECK                   
         DROP  R3                                                               
         EJECT                                                                  
***********************************************************************         
* DISPATCH THE TASK VIA MONITOR/SCRUNCH                               *         
* R5=A(UTL)                                                           *         
* R7=A(SELIST ENTRY)                                                  *         
* R8=A(SSB)                                                           *         
* A(TCB) IN SSBTKADR                                                  *         
***********************************************************************         
         USING UTLD,R5                                                          
         USING SELISTD,R7                                                       
         USING SSBD,R8                                                          
DISPATCH NTR1  BASE=*,LABEL=*                                                   
         BRAS  RE,ARSOFF                                                        
         MVC   TRACERTN,=CL8'DISPATCH'                                          
         L     R3,SSBTKADR                                                      
         USING TCBD,R3                                                          
*                                                                               
         ST    R5,TCBRUTL          SET A(UTL IN XA FOR RETURN)                  
         CLI   TORFLAG,C'Y'        IS THIS THE TOR                              
         BNE   *+12                                                             
         TM    TTORAOR,TTARCVR     RECOVERED TRANSACTIONS CANNOT RUN IN         
         BO    DSPT02              THE TOR TO HELP TOR INTEGRITY                
         TM    SEIND,SEISTRT       IS SYSTEM OPERATIONAL                        
         BO    DSPT04              YES - DISPATCH                               
*                                  NO- REQUEUE TRANSACTION TO SE1               
DSPT02   MVC   TSVCREQ,$SYSNOP                                                  
         L     R7,VSELIST                                                       
         LA    R7,6(,R7)           SE#1 IS ALWAYS FIRST                         
*                                                                               
DSPT04   ST    RA,TCBSYSF          A(SYSFAC)                                    
         ST    R7,TCBSE            A(SELIST ENTRY)                              
         OI    TSTAT2,TSTATTIP                                                  
         ICM   RF,15,TXTWIN        SET TWIN IN PROCESS ALSO                     
         BZ    *+8                                                              
         OI    TSTAT2-UTLD(RF),TSTATTIP                                         
*                                                                               
         LLC   RF,SETASK           BUMP NUMBER OF ACTIVE TASKS                  
         LA    RF,1(RF)                                                         
         STC   RF,SETASK                                                        
*                                                                               
         MVC   TCBDTFS(8),SEFILES  SET FILE INFO INTO TCB                       
         XC    TCBRCV,TCBRCV       INITIALISE RECOVERY COUNTS/DSKADRS           
         XC    TCBSWNUM(1+(TCBSWMAX*TCBSWLEN)),TCBSWNUM                         
         XC    TCBXTINF,TCBXTINF                                                
         XC    TCBLKSE,TCBLKSE     INITIALISE LOCKER SE LIST                    
         XC    TCBBILL,TCBBILL     INITIALISE BILLING REF                       
         XC    TCBSRMSG,TCBSRMSG                                                
         XC    TCBTKWT,TCBTKWT     INITIALISE TKWAIT COUNTER                    
         XC    TCBPERF1,TCBPERF1   INITIALISE PERFORMANCE DATA                  
         XC    TCBPERF2,TCBPERF2                                                
*                                                                               
         GOTO1 VDTFIOA,DUB,(C'I',(R3)),TO24=Y                                   
         SAM31                     INTIALISES DTF SAVE DATA                     
*                                                                               
         MVI   TCBFLAG1,0          SET NO SYSTEM GLOBAL RESOURCES USED          
         MVI   TCBFLAG4,0          RESET DMGR XFLAG STUFF                       
         XC    TCBIRDR,TCBIRDR     INITIALISE IRDR INFO                         
         XC    TCBDMSTA,TCBDMSTA   INITIALISE FOR VSAM DEMOS                    
*                                                                               
DSPT06   OC    TSVCREQ,TSVCREQ     TASK IS FOR A SERVICE REQUEST                
         BZ    DSPT08                                                           
         MVI   TCBSEN,1                                                         
         MVC   TCBPRG,TSVCREQ+1                                                 
         MVI   TCBOVSYS,1                                                       
         MVI   TCBPRTY,0                                                        
         MVI   TCBPRTYF,0                                                       
         B     DSPT12                                                           
*                                                                               
DSPT08   MVC   TCBSEN,TSYS         TASK IS FOR A REGULAR PROGRAM                
         MVC   TCBPRG,TPRG                                                      
         MVC   TCBOVSYS,TOVSYS                                                  
         MVI   TCBPRTY,0           DEFAULT PRIORITY                             
         MVI   TCBPRTYF,0                                                       
*                                                                               
         XR    RF,RF               SET INITIAL PRTY TO PROGRAM VALUE            
         ICM   RF,7,TAPRG                                                       
         BZ    DSPT12                                                           
         XR    RE,RE                                                            
         ICM   RE,7,TARSYS         REAL SYSTEM                                  
         BZ    DSPT12                                                           
         ICM   RE,15,SEPGMS-SELISTD(RE)                                         
         AR    RF,RE                                                            
*                                                                               
DSPT10   MVC   TCBPRTY,PGMPRTY-PGMLSTD(RF)                                      
*                                                                               
DSPT12   XC    TCBSCRPT,TCBSCRPT   SET TASK SCRIPT DATA                         
         NI    TCBFLAG3,255-TCBSCPWT                                            
         MVI   TCBSCPTK,0                                                       
*                                                                               
         TM    TSTAT6,TST6SCRP     TEST IF GOING TO RUN A SCRIPT                
         BZ    DSPT14                                                           
         ICM   RF,15,TBUFF         BUFFER HAS DETAILS OF SCRIPT                 
         BZ    DSPT14              UNLESS NO BUFFER                             
         ICM   RF,15,TBHATCB-TBHDATA(RF)                                        
         BZ    DSPT14              UNLESS NO BUFFER                             
         MVC   TCBSCPTK-TCBD(1,RF),TCBID+6                                      
         ST    RF,TCBSCRPT         SET A(TASK WAITING FOR US)                   
         ICM   RF,15,TCBSIN-TCBD(RF)                                            
         B     DSPT16                                                           
*                                                                               
DSPT14   L     RF,SSBSIN           GET SYSTEM INPUT NUMBER                      
*                                                                               
DSPT16   STCM  RF,15,TSIN          SET SIN IN UTL & TCB ENTRIES                 
         STCM  RF,15,TCBSIN                                                     
*                                                                               
         MVC   TCBTASK,TCBID+6                                                  
         XC    TCBOVCNT(4),TCBOVCNT CLEAR TCBOVCNT AND TCBIOCNT                 
         XC    TCBVSCNT,TCBVSCNT    CLEAR VSAM I/O COUNT                        
         XC    TCBLIOC(8),TCBLIOC   CLEAR LOGICAL I/O COUNTS                    
         XC    TCBCPUTM,TCBCPUTM    CLEAR ACCUMULATED CPU                       
         XC    TCBCPUTK,TCBCPUTK    CLEAR ACCUMULATED TASK CPU USED             
         XC    TCBMSGI(4),TCBMSGI   CLEAR INPUT AND OUTPUT MSG LENGTHS          
         XC    TCBINDS1(8),TCBINDS1 CLEAR STATS INDS1/INDS2/UDATA               
         NI    TCBFLAG3,255-TCBMQWT CLEAR MQ WAIT FLAG                          
         XC    TCBARIRP,TCBARIRP    RESET AR INTERRUPTS                         
         XC    TGIN,TGIN            CLEAR GIN (GLOBAL INPUT NUMBER)             
*                                                                               
         TM    TTYPE2,TTYPEDUM                                                  
         BO    DSPT18                                                           
         ICM   RE,15,TBUFF         SET INPUT MSG LEN FROM BUFFER HDR            
         BZ    DSPT18                                                           
         AHI   RE,-TBHL                                                         
         MVC   TCBMSGI(2),TBHMSGL-TBHD(RE)                                      
*                                                                               
DSPT18   GOTO1 VTICTOC,DUB,C'1SET'                                              
         TIMEUSED STORADR=TCBCPUSE,LINKAGE=SYSTEM,CPU=MIC                       
         MVC   TCBINTM,TTIMETU     SET TRANSACTION INPUT TIME                   
         L     R1,DUB                                                           
         ST    R1,TCBSTTM          SET TRANSACTION START TIME                   
         ST    R1,TCBTIME          SET CURRENT EVENT TIME                       
*                                                                               
         SL    R1,TTIME            COMPUTE TRANSACTION RATE                     
         SRL   R1,11               <== WAS 12                                   
         LLC   RF,TRATE                                                         
         SR    RF,R1               DECR RATE BY DELTA TIME FACTOR               
         BNM   *+6                                                              
         SR    RF,RF               IF NEG SET RATE ZERO                         
         LA    RF,1(RF)            BUMP RATE FOR NEW TRANSACTION                
         STC   RF,TRATE            NEW RATE                                     
         STC   RF,TCBOVCNT         COPY TO UNUSED OVCNT FOR REPORTING           
*                                                                               
         MVC   TTIME,DUB           SET LAST EVENT TIME TO NOW                   
         MVC   TDATEB,SSBDATEB     SET LAST EVENT DATE TO TODAY                 
*                                                                               
         TM    TTYPE2,TTYPEDUM     DUMMY TERMINAL?                              
         BZ    *+10                NO                                           
         MVC   TCBINTM,DUB         SET ARRIVAL TIME TO NOW FOR DUMMIES          
         MVC   TCBSYM,TSYM                                                      
*                                                                               
         TM    TSTATC,TSTCMQ                                                    
         BO    *+10                                                             
         XC    TMOCNTS,TMOCNTS                                                  
*                                                                               
         TM    TSTAT6,TST6DBUG     IF TERMINAL IS DEBUG                         
         BZ    *+8                                                              
         OI    TCBFLAG3,TCBDEBUG   SET TASK TO DEBUG                            
*                                                                               
         L     R0,TCBLUTL          PICK UP A(LOCAL UTL)                         
         LR    RE,R5                                                            
         L     R1,VUTL                                                          
         LH    R1,0(R1)                                                         
         LR    RF,R1                                                            
         MVCL  R0,RE               COPY XA UTL TO LOCAL UTL                     
*                                                                               
         L     R5,TCBLUTL          NOW HANG FROM LOCAL UTL ENTRY                
         ST    R5,TCBUTL                                                        
*                                                                               
         MVC   TCBRBUFF,TBUFF      SAVE A(REAL TBUFF)                           
         MVC   TBUFF,TCBTBUFF      HAVE LOCAL UTL POINT TO TBUFF COPY           
*                                                                               
         ICM   RE,15,TCBRBUFF                                                   
         AHI   RE,-32                                                           
         ICM   R0,15,TCBTBUFF                                                   
         AHI   R0,-32                                                           
         LHI   R1,32+TBUFFL                                                     
         LHI   RF,32+TBUFFL                                                     
         OC    TCBRBUFF,TCBRBUFF   SPECIAL AUTO S/R - HAS NO TBUFF              
         BNZ   *+6                                                              
         XR    RF,RF               SET TO CLEAR LOCAL TBUFF                     
         MVCL  R0,RE               COPY TO OR CLEAR LOCAL TBUFF                 
*                                                                               
         ICM   RE,15,TCBRBUFF      TEST SPECIAL AUTO S/R - HAS NO TBUFF         
         BZ    DSPT20              ($DONE SOMETIMES DOES THIS TOO)              
         TM    TSTAT6,TST6SCRP     TEST IF GOING TO RUN A SCRIPT                
         BZ    DSPT20              NO                                           
*                                                                               
         ICM   RF,15,TCBRBUFF      SEE IF SCRIPT IS IN TBUFF                    
         USING TBHDATA,RF                                                       
         LA    R0,TBHSCREC-TBHDATA(RF)                                          
         C     R0,TBHSCAS                                                       
         BNE   DSPT20              SCRIPT NOT IN TBUFF                          
*                                                                               
         ICM   RF,15,TBUFF         ADJUST A(SCRIPT) FOR SCRUNCH                 
         LA    R0,TBHSCREC-TBHDATA(RF)                                          
         STCM  R0,15,TBHSCAS                                                    
*                                                                               
DSPT20   CLC   =CL8'NONENONE',TLUID  SPECIAL AUTO S/R                           
         BNE   *+10                                                             
         XC    TNUM,TNUM           CLEAR LOCAL TNUM FOR AUTO S/R                
*                                                                               
         TM    SSBSTAT5,SSB5WLM    WLM ENABLED?                                 
         BZ    DSPT21              NO                                           
         TM    SSBSTAT1,SSBUII     USER INPUT INHIBITED?                        
         BO    DSPT20A             NO ENCLAVES TILL GO ISSUED                   
         CLC   TSVCREQ,$EOJ        SHUTDOWN ?                                   
         BNE   DSPT20B             NO,CONTINUE AS NORMAL                        
*                                                                               
DSPT20A  L     R2,TCBWLMBF         R2=A(TASK WLM WORK AREA)                     
         USING WLMD,R2                                                          
         XC    WLMTIME,WLMTIME     DO NOT CREATE ENCLAVE                        
         B     DSPT21                                                           
                                                                                
***********************************************************************         
* CALL WORK LOAD MANAGER SERVICES ROUTINE TO CLASSIFY AND CREATE AN   *         
* ENCLAVE FOR THIS NEW WORK REQUEST (TRANSACTION).                    *         
* THE ENCLAVE REPRESENTS A BUSINESS TRANSACTION.                      *         
* FOR EACH TRANSACTION A UNIQUE COPY OF WLMPARM NEEDS TO BE           *         
* MAINTAINED FOR WLM ENCLAVE PROCESSING TO WORK CORRECTLY.            *         
* COPY THE STATIC STRUCTURE (WLMPARMS) TO THIS TRANSACTIONS TCB WORK  *         
* AREA. POPULATE THE TRANSACTIONS STRUCTURE WITH THE TRANSACTION NAME *         
* AND STORE CLOCK VALUE REPRESENTING THE START OF THIS TRANSACTION.   *         
***********************************************************************         
DSPT20B  L     R2,TCBWLMBF         R2=A(TASK WLM WORK AREA)                     
         USING WLMD,R2                                                          
         ICM   R0,15,WLMTIME       CHECK FOR RESIDUAL ENCLAVE ?                 
         BZ    DSPT20M             NO,SO CREATE ONE                             
                                                                                
***********************************************************************         
* BEFORE CREATING AN ENCLAVE FOR THIS TRANSACTION CHECK FOR A RESIDUAL*         
* ENCLAVE. IN THE EVENT A TRANSACTION ABENDED AND THE ENCLAVE DID NOT *         
* GET PROPERLY DELETED                                                *         
***********************************************************************         
         WTO  'FATASKER - POSSIBLE RESIDUAL ENCLAVE'                            
         LA    R1,ELCVPARM         R1=A(ENCLAVE WORK AREA)                      
         USING WLMED,R1                                                         
         GOTO1 VWLMQE,(R1),C'WLQE'                                              
         LM    RF,R0,ECLVRETC      GET RETURN AND REASON CODE                   
         CHI   RF,4                IS TOKEN A STOKEN                            
         JE    DSPT20C             YES,CLEAN UP                                 
*                                                                               
         CHI   RF,0                IS ENCLAVE STILL ACTIVE ?                    
         JNE   DSPT20M             NO, CONTINUE                                 
         JE    DSPT20C             YES,CLEAN UP                                 
         DROP  R1                                                               
                                                                                
***********************************************************************         
* REMOVE AND CLEAN UP OLD ENCLAVE AND CLEAR TIME STAMP                *         
***********************************************************************         
DSPT20C  GOTO1 VWLMDE,WLMPARM,C'WLMD'                                           
         XC    WLMTIME,WLMTIME      CLEAR TIME STAMP                            
         J     DSPT20M              CONTINUE                                    
                                                                                
***********************************************************************         
* CREATE NEW ENCLAVE INSTANCE                                         *         
***********************************************************************         
DSPT20M  L     R2,TCBWLMBF         R2=A(WLM WORK AREA)                          
         MVC   0(WLMLNQ,R2),WLMPARMS COPY STATIC WORK AREA                      
         STCK  WLMTIME             IDENTIFY ENCLAVE START TIME                  
         MVC   WLMTRXNAME,=C'TYPE1   '                                          
         MVC   WLMFUNCT,=C'FUNCTION'                                            
         XC    WLMETOKEN,WLMETOKEN CLEAR ENCLAVE TOKEN                          
         XC    FWORD,FWORD                                                      
         MVC   FWORD+2(1),TCBPRG                                                
         ICM   R0,15,FWORD                                                      
         CVD   R0,BYTES8                                                        
         UNPK  BYTES8(5),BYTES8+5(3)                                            
         MVC   WLMPRG,BYTES8+1     PROGRAM IDENTIFIER                           
         GOTO1 VWLMCE,WLMPARM,C'WLME'                                           
                                                                                
***********************************************************************         
* AFTER CREATING AN ENCLAVE FOR THIS TRANSACTION, AN ENCLAVE TOKEN    *         
* WILL BE RETURNED BY THE CREATE ENCLAVE SERVICE.                     *         
* SAVE THE ENCLAVE TOKEN IN THE ZIP WORK AREA FOR USE BY THE ZIP      *         
* COMPRESSION ADDRESS SPACE.                                          *         
***********************************************************************         
         L     R1,TCBAZIP          ADDRESS OF ZIP WORK AREA                     
         USING ZIPHDRD,R1                                                       
         MVC   ZIPETOKN,WLMETOKEN  STORE ENCLAVE TOKEN                          
         DROP  R1,R2                                                            
                                                                                
***********************************************************************         
* NOW JOIN THE ENCLAVE WE JUST CREATED, BEFORE DISPATCHING THE NEW    *         
* WORK REQUEST (TRANSACTION).                                         *         
* IF WLMTIME IS ZEROES, THE WLM ENQUEUE HAS BEEN CANCELLED.           *         
***********************************************************************         
         L     R1,TCBWLMBF         R1=A(WORK AREA)                              
         USING WLMD,R1                                                          
         LA    R2,ELCVPARM         R2=A(ENCLAVE WORK AREA)                      
         USING WLMED,R2                                                         
         XC    ELCVPARM,ELCVPARM                                                
         MVC   ECLVTOKEN,WLMETOKEN COPY ENCLAVE TOKEN                           
         XC    FWORD,FWORD                                                      
         MVC   FWORD+2(1),TCBPRG                                                
         ICM   R0,15,FWORD                                                      
         CVD   R0,BYTES8                                                        
         UNPK  BYTES8(5),BYTES8+5(3)                                            
         MVC   ECLPRG,BYTES8+1     PROGRAM IDENTIFIER                           
         GOTO1 VWLMJN,ELCVPARM,C'WLJ1'                                          
         DROP  R1,R2                                                            
*                                                                               
DSPT21   OC    TCBSCRPT,TCBSCRPT   IS TASK RUNNING A SCRIPT?                    
         JNZ   DSPT21A2                                                         
         CLC   =CL8'NONENONE',TLUID SPECIAL AUTO S/R?                           
         JE    DSPT21A2                                                         
*                                                                               
         OC    TAGY,TAGY           ATTRIBUTED TO AN AGENCY?                     
         JZ    DSPT21A2                                                         
         OC    TPERSON,TPERSON     ATTRIBUTED TO A PERSON?                      
         JZ    DSPT21A2                                                         
*                                                                               
         TIME  BIN                 GET DATE/TIME IN 1/100 SEC UNITS             
         STCM  R0,15,SMFITME       TIME IN BINARY 1/100 SECOND                  
         STCM  R1,15,SMFIDTE       DATE IN PACKED JULIAN 0CYYDDD+               
*                                                                               
         CLI   SMFISID,0           ONLY SET THIS INFO ONCE                      
         JNE   DSPT21A0                                                         
*                                                                               
         L     R1,X'10'(,R0)       GET CVT                                      
         USING CVTMAP,R1                                                        
         L     R1,CVTSMCA          GET SMCA                                     
         MVC   SMFISID,SMCASID-SMCABASE(R1)                                     
*                                                                               
         L     R1,PSATOLD-PSA(,R0) GET CURRENT TCB                              
         USING TCB,R1                                                           
         L     R1,TCBTIO           GET ADDRESS OF TIOT                          
         USING TIOT1,R1                                                         
         MVC   SMFDJOB,TIOCNJOB    GET THE JOB NAME FROM TIOT                   
         MVC   SMFDSTEP,TIOCSTEP   GET THE STEP/PROC NAME FROM TIOT             
*                                                                               
         LA    R2,SMFDJES                                                       
         IAZXJSAB READ,WORKID=(R2) GET THE JES NAME                             
         DROP  R1                                                               
*                                                                               
DSPT21A0 XC    SMFDATA,SMFDATA                                                  
         LA    R1,SMFDATA-2                                                     
         USING METD,R1                                                          
         MVI   METTYP,METTYPQ                                                   
         MVC   METDSP,SSBDSPAC                                                  
         MVC   METAGY,TAGY                                                      
         MVC   METUIDN,TUSER                                                    
         MVC   METSAGY,TAGYSEC                                                  
         MVC   METPAGY,TAGYPER                                                  
         MVC   METPIDN,TPERSON                                                  
         MVC   METSYS,TOVSYS                                                    
         MVC   METMFP,TPRG                                                      
         OC    TSVCREQ,TSVCREQ     SERVICE REQUEST?                             
         JZ    DSPT21A1            NO                                           
         MVI   METSYS,1            YES                                          
         MVC   METMFP,TSVCREQ+1                                                 
DSPT21A1 MVC   METXPT,TXPTYPE                                                   
         MVC   METXPP,TXPNUM                                                    
         MVC   METXPV,TXPVER                                                    
         DROP  R1                                                               
*                                                                               
         SMFWTM SMFREC                                                          
         ST     RF,SMFERR          SAVE SMF RETURN CODE                         
*                                                                               
DSPT21A2 L     RD,TCBWRKA          POINT TO WORK AREA                           
         LA    R1,TCBD                                                          
         XR    R2,R2               SET NORMAL CALL TO MONITOR                   
         L     RF,VMONITR                                                       
         OC    TCBSCRPT,TCBSCRPT   TEST IF THIS TASK RUNNING A SCRIPT           
         BZ    *+8                                                              
         L     RF,VSCRNCH          YES - GO TO SCRIPT EXECUTOR                  
         BASSM RE,RF                                                            
                                                                                
***********************************************************************         
* REMOVE THIS ENCLAVE INSTANCE.                                       *         
* WE MUST USE THE CORRECT STORAGE STRUCTURE THAT REPRESENTS THIS      *         
* TRANSACTIONS UNIQUE COPY OF WLMPARM.                                *         
* OTHERWISE WE WILL DELETE THE WRONG ENCLAVE                          *         
***********************************************************************         
         SAM31                                                                  
         TM    SSBSTAT5,SSB5WLM    WLM ACTIVE?                                  
         BZ    DSPT21B             NO                                           
         CLC   TSVCREQ,$EOJ        SHUTDOWN ?                                   
         BE    DSPT21B             NO,BYPASS REMOVING TOKEN                     
*                                                                               
DSPT21A  L     R2,TCBWLMBF         GET ADDRESS OF WORK AREA                     
         USING WLMD,R2                                                          
         ICM   R0,15,WLMTIME       DID WE CREATE AN ENCLAVE ?                   
         BZ    DSPT21B             NO,BYPASS                                    
         XC    FWORD,FWORD                                                      
         MVC   FWORD+2(1),TCBPRG                                                
         ICM   R0,15,FWORD                                                      
         CVD   R0,BYTES8                                                        
         UNPK  BYTES8(5),BYTES8+5(3)                                            
         MVC   WLMPRG,BYTES8+1     PROGRAM IDENTIFIER                           
         GOTO1 VWLMDE,WLMPARM,C'WLMD'                                           
         XC    WLMTIME,WLMTIME     CLEAR TIME STAMP                             
         DROP  R2                                                               
*                                                                               
DSPT21B  BRAS  RE,ARSOFF           CLEAN UP AND GET OUT OF XA                   
         SAM24                                                                  
         ICM   RF,15,VCHAIN                                                     
         BZ    *+6                                                              
         BASR  RE,RF               TRACE CHAINS                                 
         ICM   RF,15,VCRAPPER                                                   
         BZ    *+6                                                              
         BASR  RE,RF               TRACE CRAPPERS                               
         SAM31                     BACK INTO XA                                 
*                                                                               
         NI    TCBFLAG3,255-TCBTBND                                             
         XC    TCBTBSEC,TCBTBSEC                                                
*                                                                               
         TIME  TU                  SET END TIME AND CPU TIME                    
         ST    R0,TCBNDTM                                                       
         L     R1,TCBTIME                                                       
         CLR   R0,R1                                                            
         BNL   *+8                                                              
         AL    R0,MIDNIGHT         ADJUST FOR MIDNIGHT                          
         SLR   R0,R1                                                            
         AL    R0,TCBCPUTM                                                      
         ST    R0,TCBCPUTM                                                      
*                                                                               
         TIMEUSED STORADR=CPUTIME,LINKAGE=SYSTEM,CPU=MIC                        
         LG    GR1,CPUTIME                                                      
         SLG   GR1,TCBCPUSE        GR1=CPU USED BY TASK THIS GO ROUND           
         AL    R1,TCBCPUTK                                                      
         ST    R1,TCBCPUTK         BUMP CPU USED BY THIS TASK                   
*                                                                               
         L     RF,TCBLIOCT         BUMP TOTAL LOGICAL I/O COUNT                 
         A     RF,TCBLIOC                                                       
         ST    RF,TCBLIOCT                                                      
         XC    TCBLIOC,TCBLIOC                                                  
         NI    TTORAOR,255-TTARCVR CLEAR AOR RECOVERY FLAG                      
*                                                                               
         TM    SSBSTAT4,SSBSAOR    SPECIAL FOR AOR IF JUST RUN $T2              
         BZ    DSPT24                                                           
         CLC   TSVCREQ+1(1),$T2+1                                               
         BNE   DSPT24                                                           
*                                                                               
         LAM   AR2,AR2,SSBALET                                                  
         ICM   R2,15,SSBATOR                                                    
         AHI   R2,TORFACLQ                                                      
         SAC   512                                                              
         USING SBEXCHD,R2                                                       
         LH    RE,0(,R2)                                                        
         L     RF,2(,R2)                                                        
         AHI   R2,6                                                             
         CLC   ASTOKEN,SBSTOKEN    IS THIS OUR ROW?                             
         BE    DSPT22                                                           
         BXLE  R2,RE,*-10                                                       
         DC    H'0'                WHERE IS OUR DSPACE ENTRY?                   
*                                                                               
DSPT22   MVI   SBAVLBL,SBYES       SET AOR AVAILABLE                            
         BRAS  RE,ARSOFF                                                        
*                                                                               
DSPT24   LA    R1,TCBD             POINT R1 TO TCB ENTRY                        
         GOTO1 VLOGGR,(R1),TO24=Y  PUT RECORD TO RECORDER FILE                  
         SAM31                                                                  
*                                                                               
         OC    TCBSCRPT,TCBSCRPT                                                
         BNZ   DSPT25              THAT'S ALL FOR SCRIPTS                       
         OC    TSVCREQ,TSVCREQ                                                  
         BNZ   DSPT25              THAT'S ALL FOR SERVICE REQUESTS              
*                                                                               
         TM    TSTAT1,TSTATBIL     TEST IF BILLING ACTIVE                       
         BZ    DSPT25              NO                                           
         XC    DUB,DUB             YES OUTPUT FACPAK SYSTEM DATA                
         MVI   DUB,C'S'                                                         
         GOTO1 VBILLIT,DUB,TO24=Y                                               
         SAM31                                                                  
                                                                                
***********************************************************************         
* SPECIAL PROCESSING FOR MQ SERVICE REQUESTS                          *         
* THESE GRAB A UTL AND A TBUFF THAT BOTH NEED TO BE RELEASED          *         
***********************************************************************         
DSPT25   TM    TSTATC,TSTCMQ       IS THIS AN MQ TYPE SERVICE REQUEST?          
         BZ    DSPT30              NO                                           
*                                                                               
DSPT26   ICM   RE,15,SSBMQNUM      DECREMENT CURRENT ACTIVE COUNT               
         BZ    DSPT28                                                           
         LR    RF,RE                                                            
         AHI   RF,-1                                                            
         CS    RE,RF,SSBMQNUM                                                   
         BNE   DSPT26                                                           
*                                                                               
DSPT28   L     R5,TCBRUTL          BACK TO USING REAL (XA) UTL ENTRY            
         CLI   TSVCREQ+1,X'73'     SPECIAL REP ONE - NO TBUFF                   
         BE    DSPT29                                                           
         ICM   R0,15,TBUFF         FREE UP TBUFF IN USE                         
         BZ    DSPT29                                                           
         GOTO1 VLCM,PARM,VTRELBUF,(R0)                                          
*                                                                               
DSPT29   XC    TBUFF,TBUFF                                                      
         B     DSPT34              REUSE CODE TO FREE UP THE UTL                
                                                                                
***********************************************************************         
* NOTE THAT AN AUTOMATIC S/R IS SPECIAL.                              *         
* WE JUST FREE UP THE UTL AS THERE IS NO TBUFF TO WRITE               *         
* THE NEXT TIME A VTGETUTL IS ISSUED, THIS ENTRY WILL BE ELIGIBLE     *         
***********************************************************************         
DSPT30   CLC   =CL8'NONENONE',TLUID                                             
         BNE   DSPT36                                                           
         OC    TSVCREQ,TSVCREQ     TEST SPECIAL AUTO S/R                        
         BZ    DSPT36                                                           
*                                                                               
         LA    RF,AUTOSRTB         LIST OF AUTO S/R WE SUPPORT                  
DSPT32   CLC   0(1,RF),TSVCREQ+1                                                
         BNE   *+12                                                             
         MVI   1(RF),C'N'          SET AUTO S/R NOT ACTIVE                      
         B     DSPT34                                                           
*                                                                               
         AHI   RF,L'AUTOSRTB                                                    
         CLI   0(RF),0             WHILE                                        
         BNE   DSPT32                                                           
*                                                                               
DSPT34   L     R5,TCBRUTL          BACK TO USING REAL (XA) UTL ENTRY            
         XC    TCBUTL,TCBUTL                                                    
         XC    TCBRUTL,TCBRUTL                                                  
*&&US*&& MVC   TLUID,=CL8'FREEFREE'                                             
*&&UK*&& MVC   TLUID,=CL8'FREENTRY'                                             
         NI    TTYPE2,255-TTYPEDUM                                              
         NI    TSTAT2,255-TSTATTIP                                              
         OI    TSTATU,TSTATAVA     RELEASE UTL SO IT CAN BE REUSED              
         B     CHECK000            NO WRITE FOR AUTO S/R                        
         EJECT                                                                  
***********************************************************************         
* ON RETURN NEED TO COPY LOCAL UTL BACK TO XA AND NEED TO COPY TBUFF  *         
* BACK ALSO IF THERE AND NOT BINARY                                   *         
***********************************************************************         
DSPT36   L     R0,TCBLUTL          RESTORE LOCAL UTL COPY TO XA                 
         AHI   R0,L'TNUM                                                        
         L     RE,TCBRUTL                                                       
         AHI   RE,L'TNUM                                                        
         L     R1,VUTL                                                          
         LH    R1,0(R1)                                                         
         AHI   R1,-L'TNUM                                                       
         LR    RF,R1                                                            
         MVCL  RE,R0               COPY ALL EXCEPT TNUM BACK TO XA              
*                                                                               
         L     R5,TCBRUTL          START USING XA UTL ENTRY                     
         XC    TCBUTL,TCBUTL                                                    
         XC    TCBRUTL,TCBRUTL                                                  
*                                                                               
         ICM   RF,15,TCBRBUFF      TEST 'REAL' TBUFF EXISTS                     
         BZ    DSPT40              NO                                           
         TM    TTYPE2,TTYPE2SC     TEST CHAINED WRITE (BINARY XFR)              
         BZ    DSPT38              NO                                           
*                                                                               
         AHI   RF,-9               CHAINED WRITES MANIPULATE REAL TBUFF         
         ICM   RE,15,TBUFF                                                      
         BZ    DSPT38                                                           
         AHI   RE,-9                                                            
         MVC   0(1,RF),0(RE)       SO JUST PRESERVE WRITE INFO                  
         B     DSPT40                                                           
*                                                                               
DSPT38   ICM   RE,15,TCBRBUFF      COPY LOCAL TBUFF INTO XA                     
         AHI   RE,-32              REMEMBER HEADERS ON TBUFFS                   
         ICM   R0,15,TBUFF                                                      
         AHI   R0,-32                                                           
         LHI   R1,32+TBUFFL                                                     
         LR    RF,R1                                                            
         MVCL  RE,R0                                                            
*                                                                               
DSPT40   MVC   TBUFF,TCBRBUFF      RESTORE GLOBAL TBUFF ADDRESS                 
         XC    TCBRBUFF,TCBRBUFF                                                
*                                                                               
         TM    TTORAOR,TTAGOBAK    TEST REQUEUE TRANSACTION                     
         BO    DSPT42              YES                                          
         ICM   RF,15,TXTWIN        VIRTUAL UTL ENTRY?                           
         BZ    DSPT42              NO                                           
X        USING UTLD,RF             RF=TWIN (REAL) XA UTL ENTRY                  
*                                                                               
         MVC   X.TBUFF,TBUFF       RESTORE TBUFF TO ORIGINAL UTL                
         XC    TBUFF,TBUFF         CLEAR OUT A(TBUFF) FROM HERE                 
         NI    TSTAT2,255-TSTATTIP SET VIRTUAL NOT IN PROGRESS                  
         LR    R5,RF               USE REAL UTL FOR THE WRITE                   
         DROP  X                                                                
*                                                                               
DSPT42   GOTO1 VWRTOUT,PARM,UTLD,0,0                                            
*                                                                               
         OC    TCBSCRPT,TCBSCRPT                                                
         BNZ   CHECK08             THAT'S ALL FOR SCRIPTS                       
*                                                                               
         NI    TSTAT2,255-TSTATTIP RESET TRM IN PROCESS                         
         MVC   SESIN,TSIN          SET COMPLETED SIN                            
         CLI   TSVCREQ+1,$DONE                                                  
         BE    DSPT44                                                           
         TM    TSTAT5,TST5PSWD     TEST PASSWORD INPUT PENDING                  
         BO    DSPT44                                                           
         NI    TSTAT2,255-TSTATNIT                                              
*                                                                               
DSPT44   TM    TSTAT2,TSTATBCS+TSTATBCP TEST IF BRDCAST SENT THIS TIME          
         BNO   *+12                                                             
         NI    TSTAT2,255-TSTATBCP-TSTATBCS                                     
         OI    TSTAT2,TSTATNIT     SET TRM NOT INIT TO FORCE $RE                
*                                                                               
         CLC   TSVCREQ+1(1),$HELP+1 TEST IF HELP SREEN ON TERMINAL              
         B     *+8                 NOP THIS IS NOT USED EVER                    
         OI    TSTAT2,TSTATNIT     YES FORCE $RE NEXT TIME                      
*                                                                               
         CLC   TSVCREQ+1(1),$EOJ+1                                              
         BNE   CHECK000                                                         
         OI    SSBSTAT1,SSBSEOJ    SET EOJ PENDING                              
         B     CHECK06                                                          
         EJECT                                                                  
***********************************************************************         
* WRITE CHKPT RECORDS IF FLAGGED                                      *         
***********************************************************************         
CHECK000 LR    R1,RD               POINT TO WORK AREA                           
         LA    RD,32(RD)           SET FOR NEXT NMOD                            
         TM    SSBSTAT4,SSBSAOR                                                 
         BO    CHECK04             DON'T CHECKPOINT AORS                        
         TM    SSBSTAT1,SSBSCHK1                                                
         BZ    CHECK02                                                          
*                                                                               
         NI    SSBSTAT1,255-SSBSCHK1                                            
         XC    0(32,R1),0(R1)                                                   
         MVC   0(4,R1),VWTID                                                    
         L     RE,VCHKPT1                                                       
         ST    RE,4(R1)                                                         
         L     RF,VCHKPT1X                                                      
         SR    RF,RE                                                            
         ST    RF,8(R1)                                                         
         MVC   12(4,R1),VPRGMS                                                  
         LLC   RF,SSBSYSID                                                      
         LA    RF,1(RF,RF)                                                      
         STH   RF,20(R1)                                                        
         MVC   22(2,R1),=X'0100'                                                
         LA    RE,20(R1)                                                        
         ST    RE,16(R1)                                                        
         XR    R0,R0               SAVE AND CLEAR TASK I/O COUNT                
         ICM   R0,7,TCBIOCNT                                                    
         XC    TCBIOCNT,TCBIOCNT                                                
         GOTO1 VDADDS,(R1),TO24=Y                                               
         SAM31                                                                  
*                                                                               
         AHI   R0,1                RESTORE NEW TASK I/O COUNT                   
         STCM  R0,7,TCBIOCNT                                                    
         OC    8(2,R1),8(R1)                                                    
         JNZ   *+2                                                              
*                                                                               
CHECK02  TM    SSBSTAT1,SSBSCHK2   TEST CHKPT2 UPDATED                          
         BZ    CHECK04                                                          
         NI    SSBSTAT1,255-SSBSCHK2                                            
         XC    0(32,R1),0(R1)                                                   
         MVC   0(4,R1),VWTID                                                    
         L     RE,VCHKPT2                                                       
         ST    RE,4(R1)                                                         
         L     RF,VCHKPT2X                                                      
         SR    RF,RE                                                            
         ST    RF,8(R1)                                                         
         MVC   12(4,R1),VPRGMS                                                  
         LLC   RF,SSBSYSID                                                      
         LA    RF,2(RF,RF)                                                      
         STH   RF,20(R1)                                                        
         MVC   22(2,R1),=X'0100'                                                
         LA    RE,20(R1)                                                        
         ST    RE,16(R1)                                                        
*                                                                               
         XR    R0,R0               SAVE AND CLEAR TASK I/O COUNT                
         ICM   R0,7,TCBIOCNT                                                    
         XC    TCBIOCNT,TCBIOCNT                                                
         GOTO1 VDADDS,(R1),TO24=Y                                               
         SAM31                                                                  
*                                                                               
         AHI   R0,1                RESTORE NEW TASK I/O COUNT                   
         STCM  R0,7,TCBIOCNT                                                    
         OC    8(2,R1),8(R1)                                                    
         JNZ   *+2                                                              
*                                                                               
CHECK04  TM    TTEST,TTESTTAC      TEST IF TSTTAB REFERENCED                    
         BO    *+14                                                             
         CLC   TSVCREQ+1(1),$TEST+1                                             
         BNE   CHECK06                                                          
*                                                                               
         XC    0(24,R1),0(R1)      WRITE TSTTAB TO TSTRCVR FILE                 
         MVC   0(4,R1),VWTID                                                    
         L     RE,VTSTTAB                                                       
         ST    RE,4(R1)                                                         
         L     RF,2(RE)                                                         
         LA    RF,1(RF)                                                         
         SR    RF,RE                                                            
         ST    RF,8(R1)                                                         
         MVC   12(4,R1),VTSTRCVR                                                
         LA    RE,20(R1)                                                        
         ST    RE,16(R1)                                                        
         MVC   20(4,R1),=X'00010100'                                            
         XR    R0,R0               SAVE AND CLEAR TASK I/O COUNT                
         ICM   R0,7,TCBIOCNT                                                    
         XC    TCBIOCNT,TCBIOCNT                                                
         GOTO1 VDADDS,(R1),TO24=Y                                               
         SAM31                                                                  
*                                                                               
         AHI   R0,1                RESTORE NEW TASK I/O COUNT                   
         STCM  R0,7,TCBIOCNT                                                    
         OC    8(2,R1),8(R1)                                                    
         JNZ   *+2                                                              
*                                                                               
CHECK06  CLI   TCBSEN,1            TEST SE 1 COMPLETION                         
         BE    *+8                                                              
         ST    R7,LASTSE           SET LAST SE COMPLETION                       
*                                                                               
CHECK08  ICM   R0,15,TCBSTTM       SAVE FOR =TASK SO WE KNOW MAX TASKS          
         XC    TCBSTATS,TCBSTATS   SET TASK IDLE                                
         STCM  R0,15,TCBSTTM                                                    
         XC    TCBLNSYS,TCBLNSYS                                                
         XC    TCBLNPRG,TCBLNPRG                                                
         XC    TCBSVRD,TCBSVRD                                                  
         XC    TCBSVECB,TCBSVECB                                                
         NI    TCBFLAG3,255-TCBDEBUG                                            
*                                                                               
         LLC   R0,SETASK                                                        
         AHI   R0,-1                                                            
         BM    *+8                                                              
         STC   R0,SETASK           DECR NUMBER OF ACTIVE TASKS                  
*                                                                               
         CLI   TORFLAG,C'N'                                                     
         BE    CHECKAOR                                                         
         EJECT                                                                  
***********************************************************************         
* TAKE TASK OUT OF DSPACE                                             *         
***********************************************************************         
         BRAS  RE,ARSOFF           FIND EXCHANGE BLOCK FOR TOR/AOR WORK         
         LAM   AR2,AR2,SSBALET                                                  
         ICM   R2,15,SSBATOR                                                    
         AHI   R2,TORFACLQ+6       FIRST ONE IS ALWAYS PRIMARY                  
         SAC   512                                                              
         USING SBEXCHD,R2                                                       
         ICM   R0,15,SBTSKMAX                                                   
         CPYA  AR4,AR2                                                          
         LA    R4,SBTSKBLK         FIND NEW TASK TO PROCESS                     
         USING EXCHNGD,R4                                                       
*                                                                               
CHECK10  CLC   TCBID,EXCIDENT      FIND THIS TASK IN EXCHANGE BLOCK             
         BE    CHECK12                                                          
         AHI   R4,EXCHNGLQ                                                      
         BCT   R0,CHECK10                                                       
         DC    H'0'                YOU IS REALLY MESSED UP                      
*                                                                               
CHECK12  XC    EXCUTL,EXCUTL       FREE EXCHANGE BLOCK SLOT                     
         XC    EXCTIME,EXCTIME                                                  
         MVI   EXCFLAG,EXCFREE                                                  
*                                                                               
CHECK14  ICM   RE,15,SBTSKNOW      DECREMENT ACTIVE TASK COUNT                  
         BZ    CHECK16                                                          
         LR    RF,RE                                                            
         AHI   RF,-1                                                            
         CS    RE,RF,SBTSKNOW                                                   
         BNE   CHECK14                                                          
*                                                                               
CHECK16  BRAS  RE,ARSOFF                                                        
         L     RF,AADNEXT                                                       
         BR    RF                                                               
         DROP  R2,R3,R4,R5,R7                                                   
         EJECT                                                                  
***********************************************************************         
* MOVE BACK COMPLETED TRANSACTION TO TOR UTL AND TBUFF                *         
***********************************************************************         
CHECKAOR OC    MODE,MODE           SRTIM INITIALISATION RUN?                    
         BNZ   *+8                 YES - BACK TO ADNEXT                         
         BRAS  RE,TORBACK          COPY INFO BACK TO TOR                        
         L     RF,AADNEXT                                                       
         BR    RF                                                               
         EJECT                                                                  
***********************************************************************         
* SCHEDULE BEST QUALIFYING ENTRY IN SELIST                            *         
* R8=A(SSB)                                                           *         
* R9=A(TASKCNST)                                                      *         
* RA=V(SYSFAC)                                                        *         
* DUB(4) = TIME IN TUS - FROM SSET                                    *         
* EXIT CC EQ =ENTRY TO DISPATCH - ENTRY RETURNED IN R7                *         
*      CC NEQ=NO ENTRY TO DISPATCH                                    *         
***********************************************************************         
SCHEDULE NTR1  BASE=*,LABEL=*                                                   
         TIME  TU                                                               
         ST    R0,DUB              SET SCHEDULE START TIME                      
*                                                                               
         L     R3,VSELIST          R3=A(FIRST SELIST ENTRY)                     
         LH    R4,0(R3)            BXLE ON R3,R4,R5                             
         L     R5,2(R3)                                                         
         AHI   R3,6                                                             
         USING SELISTD,R3                                                       
                                                                                
***********************************************************************         
* DISPATCH SPECIAL S/R IF NECESSARY                                   *         
***********************************************************************         
         CLI   OPSACTV+1,C'Y'      $OPS ALREADY ACTIVE?                         
         BE    SCHD02              YES                                          
*                                                                               
         TM    SSBOPECB,X'80'      TEST SROPS IN PROGRESS                       
         BO    SCHD02                                                           
         TM    DSOPSECB,POSTED     TEST DSPACE OPS ECB POSTED                   
         BO    *+16                                                             
         L     RF,SSBOPECB         TEST OPERATOR ECB POSTED                     
         TM    0(RF),POSTED                                                     
         BZ    SCHD02                                                           
*                                                                               
         XC    DSOPSECB,DSOPSECB                                                
         MVI   OPSACTV+1,C'Y'                                                   
         XR    RF,RF                                                            
         ICM   RF,15,OPSACTV+2                                                  
         AHI   RF,1                                                             
         STCM  RF,15,OPSACTV+2                                                  
         MVC   OPSACTV+6(4),DUB                                                 
         MVC   DUB+4(2),$OPS       PROCESS OPER INPUT                           
         B     SCHD20                                                           
*                                                                               
SCHD02   CLI   TIMACTV+1,C'Y'      $TIM ALREADY ACTIVE?                         
         BE    SCHD04              YES                                          
         CLI   MODE,0              TEST TIMER EXPIRED MODE                      
         BE    SCHD04                                                           
*                                                                               
         MVI   TIMACTV+1,C'Y'                                                   
         ICM   RF,15,TIMACTV+2                                                  
         AHI   RF,1                                                             
         STCM  RF,15,TIMACTV+2                                                  
         MVC   TIMACTV+6(4),DUB                                                 
         MVC   DUB+4(2),$T2                                                     
         NI    SSBT2,X'7F'                                                      
         B     SCHD20                                                           
*                                                                               
SCHD04   CLI   VTLACTV+1,C'Y'      $VTL ALREADY ACTIVE?                         
         BE    SCHD06              YES                                          
         TM    SSBVTFL1,SSBVTTBP+SSBVTTCP TEST TERMBLD PENDING                  
         BZ    SCHD06                                                           
*                                                                               
         MVI   VTLACTV+1,C'Y'                                                   
         ICM   RF,15,VTLACTV+2                                                  
         AHI   RF,1                                                             
         STCM  RF,15,VTLACTV+2                                                  
         MVC   VTLACTV+6(4),DUB                                                 
         MVC   DUB+4(2),$VTL                                                    
         B     SCHD20                                                           
*                                                                               
SCHD06   CLI   EXTACTV+1,C'Y'      $EXT ALREADY ACTIVE?                         
         BE    SCHD08              YES                                          
         TM    SSBJFLAG,SSBJFEXT   TEST EXTRACT UPDATE PENDING                  
         BZ    SCHD08                                                           
*                                                                               
         MVI   EXTACTV+1,C'Y'                                                   
         ICM   RF,15,EXTACTV+2                                                  
         AHI   RF,1                                                             
         STCM  RF,15,EXTACTV+2                                                  
         MVC   EXTACTV+6(4),DUB                                                 
         MVC   DUB+4(2),$EXT                                                    
         B     SCHD20                                                           
*                                                                               
SCHD08   CLI   WKRACTV+1,C'Y'      $WKR ALREADY ACTIVE?                         
         BE    SCHD10              YES                                          
         TM    SSBJFLAG,SSBJFWKR   TEST FACWRK RECOVERY UPDATE PENDING          
         BZ    SCHD10                                                           
*                                                                               
         MVI   WKRACTV+1,C'Y'                                                   
         MVC   WKRACTV+6(4),DUB                                                 
         ICM   RF,15,WKRACTV+2                                                  
         AHI   RF,1                                                             
         STCM  RF,15,WKRACTV+2                                                  
         MVC   WKRACTV+6(4),DUB                                                 
         MVC   DUB+4(2),$WKR                                                    
         B     SCHD20                                                           
*                                                                               
SCHD10   CLI   SCRACTV+1,C'Y'      $SCR ALREADY ACTIVE?                         
         BE    SCHD12              YES                                          
         TM    SSBJFLG2,SSBJFSCP   TEST WRKF SCRIPT PENDING                     
         BZ    SCHD12                                                           
*                                                                               
         MVI   SCRACTV+1,C'Y'                                                   
         ICM   RF,15,SCRACTV+2                                                  
         AHI   RF,1                                                             
         STCM  RF,15,SCRACTV+2                                                  
         MVC   SCRACTV+6(4),DUB                                                 
         MVC   DUB+4(2),$SCR                                                    
         B     SCHD20                                                           
*                                                                               
SCHD12   DS    0H                                                               
*&&UK*&& B     SCHD22              NO UK DARE S/R'S                             
*&&US                                                                           
         TM    SSBDARFL,SSBDRUPD   DOES THIS SYSTEM USE $DARE?                  
         BZ    SCHD22              NO                                           
*                                                                               
         CLI   RDRACTV+1,C'Y'      $REPDARE ALREADY ACTIVE?                     
         BE    SCHD14              YES                                          
         TM    SSBDARFL,SSBDRRPQ   CALL $REPDARE IF ENTRY IN TABLE              
         BZ    SCHD14                                                           
*                                                                               
         MVI   RDRACTV+1,C'Y'                                                   
         ICM   RF,15,RDRACTV+2                                                  
         AHI   RF,1                                                             
         STCM  RF,15,RDRACTV+2                                                  
         MVC   RDRACTV+6(4),DUB                                                 
         MVC   DUB+4(2),$REPDARE                                                
         B     SCHD20                                                           
*                                                                               
SCHD14   CLI   SPTACTV+1,C'Y'      $SPTDARE ALREADY ACTIVE?                     
         BE    SCHD16              YES                                          
         TM    SSBDARFL,SSBDRSDR   CALL $SPTDARE IF ENTRY IN TABLE              
         BZ    SCHD16                                                           
*                                                                               
         MVI   SPTACTV+1,C'Y'                                                   
         ICM   RF,15,SPTACTV+2                                                  
         AHI   RF,1                                                             
         STCM  RF,15,SPTACTV+2                                                  
         MVC   SPTACTV+6(4),DUB                                                 
         MVC   DUB+4(2),$SPTDARE                                                
         B     SCHD20                                                           
*                                                                               
SCHD16   CLI   MKGACTV+1,C'Y'      $MKGDARE ALREADY ACTIVE?                     
         BE    SCHD18              YES                                          
         TM    SSBDARFL,SSBDRSPQ   CALL $MKGDARE IF ENTRY IN TABLE              
         BZ    SCHD18                                                           
*                                                                               
         MVI   MKGACTV+1,C'Y'                                                   
         ICM   RF,15,MKGACTV+2                                                  
         AHI   RF,1                                                             
         STCM  RF,15,MKGACTV+2                                                  
         MVC   MKGACTV+6(4),DUB                                                 
         MVC   DUB+4(2),$MKGDARE                                                
         B     SCHD20                                                           
*&&                                                                             
SCHD18   B     SCHD22              ADD NEW AUTO S/R ENTRIES HERE                
*                                                                               
SCHD20   GOTO1 VLCM,PARM,VTGETUTL  GET A UTL ENTRY FOR THIS S/R                 
         LTR   R2,R1                                                            
         BNZ   *+12                OUT OF UTL ENTRIES                           
         BRAS  RE,NOUTL                                                         
         B     SCHD22                                                           
*                                                                               
         USING UTLD,R2                                                          
         MVC   TLUID,=CL8'NONENONE'                                             
         MVC   TXNEXTIN,SEFIRST    SET LINK TO NEXT UTL ENTRY                   
         MVC   TNEXTIN,SEFIRST+1   .....                                        
         MVC   TAGYB,MODE          SET MODE                                     
         MVC   TSVCREQ,DUB+4       SET SPECIAL S/R                              
         MVC   TTIMETU,DUB         SET ARRIVAL TIME TO NOW                      
*                                                                               
         STCM  R2,15,SEFIRST       SET AT TOP OF SE1 QUEUE                      
         XR    RF,RF                                                            
         ICM   RF,3,SEQLEN                                                      
         BNZ   *+8                                                              
         STCM  R2,15,SELAST                                                     
         AHI   RF,1                                                             
         STH   RF,SEQLEN                                                        
         B     SCHDXE              DISPATCH SPECIAL S/R                         
         DROP  R2                                                               
         EJECT                                                                  
***********************************************************************         
* CHECK SERVICE REQUESTS FIRST                                        *         
***********************************************************************         
SCHD22   ICM   RE,15,SEFIRST       TEST IF SERVICE QUEUE EMPTY                  
         BZ    SCHD24                                                           
         TM    SEIND,SEISTRT+SEINOP SYSTEM IS INHIBITED                         
         BO    SCHD24                                                           
         TM    TSTAT5-UTLD(RE),TST5TBP                                          
         BO    SCHD24              TERMINAL NOT YET BUILT - DELAY SE1           
*                                                                               
         CLC   SETASK,SETASKMX     DISPATCH IF LESS THAN MAX TASKS              
         BL    SCHDXE                                                           
*NOP     B     SCHDXE              USE THIS IF SERVICE QUEUE TOO BUSY           
*                                                                               
         ICM   RE,15,SEFIRST       POINT TO FIRST TERMINAL IN SE1 QUEUE         
         TM    TSTAT1-UTLD(RE),TSTATDDS                                         
         BNZ   SCHDXE              DISPATCH IF DDS TERMINAL                     
                                                                                
***********************************************************************         
* CHECK SE0 (SCRIPTS) NEXT                                            *         
***********************************************************************         
SCHD24   ICM   R3,15,VSELIST0      POINT TO SCRIPT SYSTEM                       
         BZ    SCHD26                                                           
         OC    SEFIRST,SEFIRST     TEST IF SCRIPT QUEUE EMPTY                   
         BZ    SCHD26                                                           
         TM    SEIND,SEISTRT+SEINOP SYSTEM IS INHIBITED                         
         BO    SCHD26                                                           
         CLC   SETASK,SETASKMX     DISPATCH IF LESS THAN MAX TASKS              
         BL    SCHDXE                                                           
                                                                                
***********************************************************************         
* CHECK CONTROL SYSTEM NEXT                                           *         
***********************************************************************         
SCHD26   ICM   R3,15,VSELISTC      POINT TO CONTROL SYSTEM                      
         BZ    SCHD28                                                           
         OC    SEFIRST,SEFIRST     TEST IF CONTROL QUEUE EMPTY                  
         BZ    SCHD28                                                           
         TM    SEIND,SEISTRT+SEINOP SYSTEM IS INHIBITED                         
         BO    SCHD28                                                           
         CLC   SETASK,SETASKMX     DISPATCH IF LESS THAN MAX TASKS              
         BL    SCHDXE                                                           
         EJECT                                                                  
***********************************************************************         
* NO SERVICE OR CONTROL SYSTEM WORK                                   *         
* START AT SYSTEM AFTER LAST ONE TO COMPLETE                          *         
***********************************************************************         
SCHD28   XC    DUB,DUB             CLEAR OPTIMUM SYSTEM INFO                    
         L     R3,LASTSE           GET LAST ONE                                 
         B     SCHD32              BUMP TO NEXT                                 
*                                                                               
SCHD30   BRAS  RE,CHKMAX                                                        
*                                                                               
SCHD32   BXLE  R3,R4,SCHD30                                                     
                                                                                
***********************************************************************         
* NOW START AT TOP AND GO THRU TO LASTSE                              *         
***********************************************************************         
         L     R5,LASTSE           SET BXLE END PARAM                           
         LA    R5,0(R4,R5)                                                      
         AHI   R5,-1                                                            
         L     R3,VSELIST                                                       
         LA    R3,6(R3,R4)         POINT TO SECOND ENTRY                        
*                                                                               
SCHD34   BRAS  RE,CHKMAX                                                        
*                                                                               
SCHD36   BXLE  R3,R4,SCHD34                                                     
         ICM   R3,15,DUB           DISPATCH SE WITH LONGEST QUEUE               
         BNZ   SCHDXE                                                           
                                                                                
***********************************************************************         
* DISPATCH SYSTEM WITH LOWEST SIN IF ALL SYSTEMS ARE AT MAX TASKS     *         
***********************************************************************         
         L     R3,VSELIST                                                       
         LH    R4,0(R3)                                                         
         L     R5,2(R3)                                                         
         LA    R3,6(R4,R3)         POINT TO SECOND ENTRY                        
*                                                                               
SCHD38   ICM   R2,15,SEFIRST                                                    
         BZ    SCHD40                                                           
         USING UTLD,R2                                                          
*&&UK*&& CLI   SETASK,7            MAX 7 TASKS FOR UK                           
*&&UK*&& BNL   SCHD40              ALLOW 1 TO SWITCH IN                         
*&&US*&& CLI   SETASK,9            MAX 10 TASKS FOR VERMONT                     
*&&US*&& BNL   SCHD40              ALLOW 1 TO SWITCH IN                         
         ST    R3,DUB              SAVE A(SELIST ENTRY)                         
         MVC   DUB+4(4),TSIN       SAVE SIN                                     
         B     SCHD44                                                           
*                                                                               
SCHD40   BXLE  R3,R4,SCHD38                                                     
         B     SCHD46              NOTHING TO DISPATCH                          
*                                                                               
SCHD42   ICM   R2,15,SEFIRST                                                    
         BZ    SCHD44                                                           
         CLC   DUB+4(4),TSIN                                                    
         BL    SCHD44                                                           
         ST    R3,DUB              SAVE A(SELIST ENTRY)                         
         MVC   DUB+4(4),TSIN       SAVE SIN                                     
*                                                                               
SCHD44   BXLE  R3,R4,SCHD42                                                     
         L     R3,DUB                                                           
         B     SCHDXE                                                           
*                                                                               
SCHD46   XC    SSBTKADR,SSBTKADR   RESET TASK ADDRESS                           
         B     SCHDXL                                                           
         EJECT                                                                  
***********************************************************************         
* FIND TASK WITH LONGEST INPUT QUEUE                                  *         
***********************************************************************         
CHKMAX   OC    SEFIRST,SEFIRST     ANYTHING IN QUEUE                            
         BZR   RE                  NO                                           
         CLC   SETASKMX,SETASK     MAX TASKS IN PROCESS                         
         BNHR  RE                  YES                                          
*&&UK*&& CLI   SETASK,6            MORE THAN 6 IN PROCESS                       
*&&UK*&& BHR   RE                  YES - IGNORE                                 
*&&US*&& CLI   SETASK,8            MORE THAN 9 IN PROCESS                       
*&&US*&& BHR   RE                  YES - IGNORE                                 
         TM    SEIND,SEISTRT+SEINOP SYSTEM IS INHIBITED                         
         BOR   RE                                                               
         CLI   SETASK,0            ANY TASKS IN PROCESS                         
         BE    SCHDXE              NO - DISPATCH                                
         CLC   SEQLEN,DUB+4        LONGEST QUEUE YET FOUND?                     
         BCR   12,RE               NO                                           
         MVC   DUB+4(2),SEQLEN                                                  
         ST    R3,DUB                                                           
         BR    RE                                                               
         DROP  R2                                                               
*                                                                               
SCHDXE   CR    RB,RB               SET CC EQUAL                                 
         B     SCHDX                                                            
*                                                                               
SCHDXL   CLI   *,255               SET CC LOW                                   
         B     SCHDX                                                            
*                                                                               
SCHDX    LR    R7,R3               SELIST ENTRY RETURNED IN R7                  
         B     EXITR7                                                           
         DROP  R3                                                               
         EJECT                                                                  
***********************************************************************         
* FIND BEST AVAILABLE TASK                                            *         
* R7=SELIST ENTRY TO PROCESS                                          *         
* CC HI=TOR MUST PROCESS TRANSACTION IN OWN TASKS                     *         
* CC EQ=AOR POSTED TO PROCESS TRANSACTION                             *         
* CC LO=NO TASKS FREE IN EITHER TOR OR AOR                            *         
***********************************************************************         
         USING SELISTD,R7                                                       
TASKFILL NTR1  BASE=*,LABEL=*                                                   
         BRAS  RE,ARSOFF                                                        
         SAM31                                                                  
*                                                                               
         ICM   R3,15,SEFIRST       GET FIRST UTL IN THIS LIST                   
         USING UTLD,R3                                                          
         MVI   TASKMASK,PGMIRTOR   DEFAULT ACTION - RUN IN TOR                  
         OC    TSVCREQ,TSVCREQ     SERVICE REQUEST?                             
         BZ    TFL02               NO                                           
*                                                                               
         XR    RF,RF                                                            
         ICM   RF,7,TASVC          DO WE HAVE A REAL PGMTAB ENTRY?              
         BNZ   TFL04               YES                                          
         LA    RF,SVCDUM           SET US UP WITH A DUMMY PGMTAB ENTRY          
         STCM  RF,7,TASVC                                                       
         B     TFL04                                                            
*                                                                               
TFL02    XR    RF,RF               TEST CONNECTED TO A PROGRAM                  
         ICM   RF,7,TAPRG                                                       
         BZ    TFL08                                                            
*                                                                               
         XR    RE,RE               GET A(PGMTAB ENTRY)                          
         ICM   RE,7,TARSYS                                                      
         BZ    TFL08                                                            
         ICM   RE,15,SEPGMS-SELISTD(RE)                                         
         AR    RF,RE                                                            
*                                                                               
         USING PGMLSTD,RF                                                       
TFL04    TM    PGMIND3,PGMITSKS    TEST ANY PROGRAM ROUTING OVERRIDES           
         BZ    TFL06               NONE                                         
         MVC   TASKMASK,PGMIND3    SET ROUTING FROM PGMIND3 OVERRIDE            
         NI    TASKMASK,PGMITSKS                                                
         B     TFL08                                                            
         DROP  RF                                                               
*                                                                               
TFL06    ICM   R1,15,SEFILES       TEST GLOBAL SYSTEM                           
         BZ    TFL08                                                            
         AHI   R1,16                                                            
         TM    ISFFLAG-ISDTF(R1),ISFGLOB                                        
         BZ    *+8                                                              
         MVI   TASKMASK,PGMITSKS   GLOBALS CAN RUN IN EITHER AOR OR TOR         
*                                                                               
TFL08    TM    TTEST,TTESTTAC      CONNECTED TO A TEST BUFFER?                  
         BZ    *+8                 NO                                           
         MVI   TASKMASK,PGMIRTOR   YES - MUST RUN IN TOR                        
         TM    SSBSTAT1,SSBUII     USER INPUT INHIBITED?                        
         BNO   *+8                 NO                                           
         MVI   TASKMASK,PGMIRTOR   YES - MUST RUN IN TOR                        
*                                                                               
         BRAS  RE,ARSOFF           NOW GET TOR ENTRY                            
         LAM   AR2,AR2,SSBALET                                                  
         ICM   R2,15,SSBATOR                                                    
         SAC   512                                                              
         AHI   R2,TORFACLQ                                                      
         XR    R4,R4                                                            
         ICM   R4,03,0(R2)         BXLE ON R4,R5                                
         ICM   R5,15,2(R2)                                                      
         AHI   R2,6                R2=A(TOR ENTRY)                              
         USING SBEXCHD,R2                                                       
*                                                                               
         TM    TSTATU,TSTATRER     REQUEUED DUE TO PREVIOUS ERROR?              
         BZ    *+12                NO                                           
         MVI   TASKMASK,PGMIRTOR   YES - MUST RUN IN TOR                        
         B     TFL26                                                            
*                                                                               
         TM    TSTAT7,TST7DIR      DIRECT TO A PARTICULAR REGION?               
         BO    TFL10               YES                                          
         OC    TSVCREQ,TSVCREQ     SERVICE REQUEST?                             
         BZ    TFL10               NO                                           
         MVI   TASKMASK,PGMIRTOR   SET MUST RUN IN TOR                          
         B     TFL26               TRY TO SCHEDULE                              
*                                                                               
TFL10    TM    TSTAT7,TST7DIR      DIRECT TO A PARTICULAR REGION?               
         BZ    TFL26               NO                                           
         MVC   DIRECT,TTORAOR      FIND REGION DESIRED                          
         NI    DIRECT,255-(TTAXTRA)                                             
         BNZ   TFL12               NON-ZERO - MEANS ROUTE TO AOR                
*                                                                               
         TM    TASKMASK,PGMIRTOR   CAN WE ROUTE THIS TO TOR?                    
         BO    TFL14               YES                                          
         NI    TTORAOR,TTAXTRA                                                  
         OI    TTORAOR,1           ERROR 1 - PRG WILL NOT RUN IN REGION         
         B     TFL22                                                            
*                                                                               
TFL12    TM    TASKMASK,PGMIRAOR   CAN WE ROUTE THIS TO AN AOR?                 
         BO    TFL14               YES                                          
         NI    TTORAOR,TTAXTRA                                                  
         OI    TTORAOR,1           ERROR 1 - PRG WILL NOT RUN IN REGION         
         B     TFL22                                                            
*                                                                               
TFL14    LLC   RF,DIRECT           RF=ZERO BASED REGION NUMBER                  
         MSR   RF,R4               INDEX INTO FACPAK BLOCKS                     
         AR    RF,R2                                                            
         CPYA  ARF,AR2             SET RF TO ROW IN BLOCK                       
SEL      USING SBEXCHD,RF                                                       
         OC    SEL.SBSTOKEN,SEL.SBSTOKEN                                        
         BNZ   TFL16                                                            
         NI    TTORAOR,TTAXTRA                                                  
         OI    TTORAOR,2           ERROR 2 - NO FACPAK IN ROW                   
         B     TFL22                                                            
*                                                                               
TFL16    CLI   SEL.SBSTRTD,SBYES   FACPAK WAS INITIALISED?                      
         BE    TFL18               YES                                          
         NI    TTORAOR,TTAXTRA                                                  
         OI    TTORAOR,3           ERROR 3 - FACPAK NOT STARTED                 
         B     TFL22                                                            
*                                                                               
TFL18    CLI   SEL.SBAVLBL,SBYES   FACPAK STILL RUNNING?                        
         BE    TFL20               YES                                          
         NI    TTORAOR,TTAXTRA                                                  
         OI    TTORAOR,4           ERROR 4 - FACPAK NOT AVAILABLE               
         B     TFL22                                                            
         DROP  SEL                                                              
*                                                                               
TFL20    LAM   ARF,ARF,ARZERO      YOU HAD BETTER RESET THIS                    
         LR    R2,RF               R2=A(CORRECT REGION)                         
*                                                                               
         CLC   SBTSKNOW,SBTSKMAX   IS THERE A FREE TASK IN THE AOR?             
         BL    TFL34               YES                                          
         NI    TTORAOR,255-TTAXTRA                                              
         OI    TTORAOR,5           ERROR 5 - AOR IS BUSY                        
         B     TFL22                                                            
         EJECT                                                                  
***********************************************************************         
* HANDLE ROUTING ERRORS BY REQUEUEING TERMINAL TO SE#1 $TOR.          *         
* FLAG TST8RERR TO INDICATE AN ERROR CONDITION OCCURRED               *         
* BITS IN TTORAOR INDICATE THE ERROR CONDITION WHICH WAS HANDLED      *         
* EXIT WITH CC EQ: THIS MEANS THAT THE TOR DOES NOTHING.              *         
* IN DUE COURSE THE REQUEUED TRANSACTION WILL RUN AS NORMAL           *         
***********************************************************************         
TFL22    MVC   SEFIRST,TXNEXTIN    REMOVE THIS UTL FROM CURRENT QUEUE           
         XC    TXNEXTIN,TXNEXTIN                                                
         XC    TNEXTIN,TNEXTIN     .....                                        
*                                                                               
         LH    RE,SEQLEN           UPDATE QUEUE COUNTER                         
         AHI   RE,-1                                                            
         BP    *+10                                                             
         XC    SELAST,SELAST                                                    
         STH   RE,SEQLEN                                                        
*                                                                               
         OI    TSTATU,TSTATRER     SET ERROR & REQUEUE TO SE#1                  
         NI    TSTAT2,255-TSTATTIP                                              
         MVC   TSVCREQ,$TOR        THIS HANDLES ERROR CONDITIONS                
*                                                                               
         L     R7,VSELIST                                                       
         AHI   R7,6                SE#1 IS ALWAYS FIRST                         
         OC    SEFIRST,SEFIRST     ANYTHING IN QUEUE?                           
         BNZ   TFL24               YES                                          
         ST    R3,SEFIRST                                                       
         ST    R3,SELAST                                                        
         LHI   R0,1                                                             
         STH   R0,SEQLEN                                                        
         B     TFILE               SET CC EQUAL AND EXIT BACK                   
*                                                                               
TFL24    L     RE,SELAST                                                        
         ST    R3,TXNEXTIN-TNUM(RE)                                             
         STCM  R3,7,TNEXTIN-TNUM(RE) .....                                      
         ST    R3,SELAST                                                        
         LH    RE,SEQLEN                                                        
         AHI   RE,1                                                             
         STH   RE,SEQLEN                                                        
         B     TFILE                                                            
         EJECT                                                                  
***********************************************************************         
* TRY TO ROUTE THE TRANSACION TO BEST REGION (TOR OR AOR)             *         
* INTENTION IS TO ALWAYS TRY TO RUN IN SYSTEM WITH LOWEST ACTIVE      *         
* TASKS UNLESS PROGRAM HAS TO BE ROUTED TO A SPECIFIC REGION          *         
***********************************************************************         
TFL26    LAM   ARF,ARF,ARZERO      RESET THIS JUST IN CASE                      
         TM    TASKMASK,PGMIRAOR   TRANSACTION WILL RUN IN AOR?                 
         BZ    TFL32               NO                                           
*                                                                               
         CPYA  AR6,AR2                                                          
         CPYA  AR3,AR2             SET R3 TO FIRST AOR ROW IN BLOCK             
         LR    R3,R2                                                            
AOR      USING SBEXCHD,R3          R3=CURRENT AOR BLOCK                         
         CPYA  ARF,AR2                                                          
         XR    RF,RF               RF=BEST QUALIFYING AOR                       
BEST     USING SBEXCHD,RF                                                       
         B     TFL30                                                            
*                                                                               
TFL28    CLI   AOR.SBAVLBL,SBYES   AOR AVAILABLE FOR WORK?                      
         BNE   TFL30               NO                                           
         CLC   AOR.SBTSKNOW,AOR.SBTSKMAX                                        
         BNL   TFL30               AOR AT MAX TASKS                             
*                                                                               
         LTR   RF,RF                                                            
         BNZ   *+6                                                              
         LR    RF,R3               SAVE A(1ST AOR ENTRY WITH FREE TASK)         
                                                                                
***********************************************************************         
* PERFORMANCE INFORMATION PROCESSING FOR SCHEDULER SELECTION WILL     *         
* HAVE TO GO HERE EVENTUALLY                                          *         
* FOR NOW THE ASSUMPTION IS THAT THE AOR WITH THE LEAST NUMBER OF     *         
* ACTIVE TASKS IS THE LEAST BUSY - THIS MAY NOT ALWAYS BE THE CASE    *         
***********************************************************************         
                                                                                
         CLC   AOR.SBTSKNOW,BEST.SBTSKNOW                                       
         BNL   *+6                                                              
         LR    RF,R3               BEST (LEAST BUSY) FACPAK IN RF               
*                                                                               
TFL30    BXLE  R3,R4,TFL28                                                      
         LTR   RF,RF               AOR AVAILABLE?                               
         BZ    TFL32               NO                                           
*                                                                               
         TM    TASKMASK,PGMIRTOR   ABLE TO RUN IN TOR?                          
         BO    *+10                YES                                          
         LR    R2,RF                                                            
         B     TFL34               RUN IN AOR                                   
*                                                                               
         CLC   SBTSKNOW,BEST.SBTSKNOW                                           
         BL    TFL34               TOR IS LESS BUSY THAN AOR                    
         LR    R2,RF                                                            
         B     TFL34               AOR IS LESS BUSY THAN TOR                    
         DROP  BEST,AOR                                                         
*                                                                               
TFL32    TM    TASKMASK,PGMIRTOR   ABLE TO RUN IN TOR?                          
         BZ    *+14                NO                                           
         CLC   SBTSKNOW,SBTSKMAX   TOR AT MAX CAPACITY?                         
         BL    TFL34               NO                                           
         LAM   ARF,ARF,ARZERO                                                   
         B     TFILL                                                            
*                                                                               
TFL34    LAM   ARF,ARF,ARZERO                                                   
         ICM   RE,15,SBTSKNOW      INCREMENT ACTIVE TASK COUNT                  
         LA    RF,1(RE)                                                         
         CS    RE,RF,SBTSKNOW                                                   
         BNE   *-8                                                              
         CLC   SBTASKMX,SBTSKNOW   SAVE MAX TASK FOR STATS                      
         JNL   *+10                                                             
         MVC   SBTASKMX,SBTSKNOW                                                
*                                                                               
         ICM   R0,15,SBTSKMAX      FIND FIRST FREE TASK SLOT                    
         CPYA  AR3,AR2                                                          
         LA    R3,SBTSKBLK                                                      
         USING EXCHNGD,R3                                                       
*                                                                               
TFL36    CLI   EXCFLAG,EXCFREE     FREE TASK?                                   
         BE    TFL38               YES                                          
         AHI   R3,EXCHNGLQ         NEXT TASK                                    
         BCT   R0,TFL36                                                         
         DC    H'0'                HOW CAN DISPATCH IF NOTHING FREE?            
*                                                                               
TFL38    L     R5,SEFIRST          POINT TO UTL ENTRY                           
         USING UTLD,R5                                                          
         OI    TSTAT2,TSTATTIP     SET TRM IN PROC                              
         MVC   SEFIRST,TXNEXTIN                                                 
         XC    TXNEXTIN,TXNEXTIN                                                
         XC    TNEXTIN,TNEXTIN     .....                                        
*                                                                               
         LH    RE,SEQLEN           UPDATE QUEUE COUNTER                         
         AHI   RE,-1                                                            
         BP    *+10                                                             
         XC    SELAST,SELAST                                                    
         STH   RE,SEQLEN           SE QUEUE LENGTH                              
         NI    TSTAT8,255-TST8INQ                                               
         LA    RE,TSIN             RE NEED FOR CODE BELOW                       
*                                                                               
         MVC   EXCUTL,TNUM         SET TERMINAL NUMBER FOR TASK                 
         MVC   EXCTIME,TTIMETU     SET ARRIVAL TIME OF TRANSACTION              
*                                                                               
         LAM   AR5,AR5,SSBTBLET    USE R5 TO POINT TO STATS BLOCK               
         XR    R5,R5                                                            
         USING FATABSD,R5                                                       
*                                                                               
         L     R5,TABSCOMM         GET COMMON AREA IN TABS DATASPACE            
         USING TCOMMOND,R5                                                      
TFL38A   L     RF,TCORSIN                                                       
         LR    R1,RF               R1 = CURRENT SIN, USE FOR CS LOCK            
         AHI   RF,1                RF = UP THE COUNT FOR NEW VALUE              
         CLFI  R1,X'00FFFFFE'      CHECK LOCK VALUE, DON'T WANT FFFFFF          
         BNE   *+8                 NO, DON'T RESET SIN TO 2                     
         LA    RF,2                AVOID -1, 0 AND 1 VALUES                     
         CS    R1,RF,TCORSIN       UPDATE THE SIN WITH VALUE IN RF              
         BNE   TFL38A              DIDN'T GET THE LOCK, TRY AGAIN               
         ST    RF,SSBSIN                                                        
         ST    RF,0(RE)            SAVE NEW SIN IN TSIN (UTL)                   
         L     RF,SSBTTRN#         TRANSACTION COUNT FOR ADV/REP                
         AHI   RF,1                                                             
         ST    RF,SSBTTRN#         LOCAL VALUE (ONLY STORED IN TOR)             
*                                                                               
         MVI   EXCFLAG,EXCNEW      SET NEW WORK SO AOR CAN PICKUP               
*                                                                               
* PROCESS STATS FOR SE QUEUE                                                    
*                                                                               
         LH    RE,SEQLEN           RELOAD SE QUEUE LENGTH                       
         XR    R5,R5               RESET BACK TO TOP OF TABS                    
         USING FATABSD,R5                                                       
         ICM   R5,15,TABSSTAT                                                   
         JZ    TFL39                                                            
         A     R5,=A(DSSLOGQL-DSSLOGD) SET TO SE QUEUES                         
         LLC   RF,SESYS                                                         
         SLL   RF,2                ADD SYS*4                                    
         AR    R5,RF                                                            
         USING DSLLOGQL,R5                                                      
         STH   RE,DSLSELQN         SAVE QUEUE NOW                               
         CH    RE,DSLSEMAX                                                      
         JL    *+8                                                              
         STH   RE,DSLSEMAX         SAVE LONGEST QUEUE                           
         DROP  R5                                                               
*                                                                               
TFL39    LAM   AR5,AR5,ARZERO                                                   
*                                                                               
TFL40    CLC   SBWAKE,TORWAKE      IS THIS THE TOR?                             
         BE    TFILH               SET CC HIGH IF WORK FOR TOR                  
*                                                                               
         LH    R5,SBASID           GET ASCB FOR THE AOR                         
         ICM   R6,15,SBWAKE        SET A(WAKEUP ECB) IN R6                      
         SAC   0                                                                
         LOCASCB ASID=(R5)                                                      
         LTR   RF,RF               ASCB RETURNED IN R1 OK?                      
         BZ    TFL42               NO                                           
         BRAS  RE,FACDWN                                                        
         B     TFILE                                                            
                                                                                
***********************************************************************         
* MIGHT NEED A BETTER CHECK THAN THIS FOR AOR AVAILABILITY            *         
* IN CASE AOR IS ABENDING WHILST PROCESSING TAKING PLACE              *         
***********************************************************************         
TFL42    LR    R4,R1               SET ASCB IN R4                               
         POST (R6),99,ASCB=(R4),LINKAGE=SYSTEM,ECBKEY=8,MF=(E,POSTAO)           
         LTR   RF,RF                                                            
         BZ    TFILE               SET CC EQUAL IF AOR POSTED                   
         BRAS  RE,FACDWN                                                        
         B     TFILE                                                            
*                                                                               
POSTAO   POST  ECBKEY=YES,MF=L                                                  
*                                                                               
TFILE    CR    RB,RB                                                            
         B     TFILX                                                            
*                                                                               
TFILH    CLI   *,0                                                              
         B     TFILX                                                            
*                                                                               
TFILL    CLI   *,FF                                                             
         B     TFILX                                                            
*                                                                               
TFILX    BRAS  RE,ARSOFF                                                        
         XMOD1                                                                  
         DROP  R2,R7                                                            
         EJECT                                                                  
***********************************************************************         
* PROCESS COMPLETED TRANSACTIONS FROM AOR FACPAKS                     *         
* R9=A(TASKCNST)                                                      *         
* RA=A(SYSFACS)                                                       *         
***********************************************************************         
PCOMPS   NTR1  BASE=*,LABEL=*                                                   
         XC    PCMPRQ,PCMPRQ                                                    
         BRAS  RE,ARSOFF                                                        
         LAM   AR2,AR2,SSBALET                                                  
         ICM   R2,15,SSBATOR                                                    
         AHI   R2,TORFACLQ         INDEX INTO FACPAK GRID FOR US                
         SAC   512                                                              
*                                                                               
         XR    R4,R4                                                            
         ICM   R4,3,0(R2)                                                       
         ICM   R5,15,2(R2)                                                      
         AHI   R2,6                                                             
         USING SBEXCHD,R2          R2=A(FACPAK GRID)                            
         CPYA  AR3,AR2                                                          
         B     PCOMP16             FIRST ENTRY IS TOR                           
*                                                                               
PCOMP02  TM    SBDONE,POSTED       COMPLETION POST FOR THIS FACPAK?             
         BZ    PCOMP16             NO                                           
         NI    SBDONE,255-POSTED   TURN OFF FLAG                                
         ICM   R0,15,SBTSKMAX      THIS FACPAK HAS TASKS?                       
         BZ    PCOMP16             NO                                           
*                                                                               
         LA    R3,SBTSKBLK         REMEMBER THIS IS IN THE DATASPACE            
         USING EXCHNGD,R3                                                       
PCOMP04  CLI   EXCFLAG,EXCDONE     IS THIS TRANSACTION FINISHED?                
         BNE   PCOMP14             NO                                           
*                                                                               
PCOMP06  ICM   RE,15,SBTSKNOW      DECREMENT ACTIVE TASK COUNT                  
         BZ    PCOMP08                                                          
         LR    RF,RE                                                            
         AHI   RF,-1                                                            
         CS    RE,RF,SBTSKNOW                                                   
         BNE   PCOMP06                                                          
*                                                                               
PCOMP08  ICM   RE,15,VUTL          RE=A(UTL BLOCK)                              
         LH    RF,EXCUTL           RF=TERMINAL NUMBER                           
         AHI   RF,-1               ZERO BASED                                   
*&&US                                                                           
         CHI   RF,6000             DEBUG TEST                                   
         JH    *+2                                                              
*&&                                                                             
         MH    RF,0(RE)            INDEX INTO BLOCK                             
         LA    R6,6(RE,RF)                                                      
         USING UTLD,R6                                                          
         XC    EXCUTL,EXCUTL       CLEAR EXCHANGE SLOT                          
         XC    EXCTIME,EXCTIME                                                  
         MVI   EXCFLAG,EXCFREE                                                  
*                                                                               
         STAR  CLEAR=ARZERO                                                     
         TM    TTORAOR,TTAGOBAK    UTL NEEDS REQUEUING?                         
         BZ    *+8                 NO                                           
         MVI   PCMPRQ,1            FLAG REQUEUED STUFF TO RUN                   
         GOTO1 VWRTOUT,PCMPLST,UTLD,0,0                                         
         REAR                                                                   
*                                                                               
PCOMP14  AHI   R3,EXCHNGLQ         NEXT TASK WITHIN THIS FACPAK                 
         BCT   R0,PCOMP04                                                       
         DROP  R6                                                               
*                                                                               
PCOMP16  BXLE  R2,R4,PCOMP02                                                    
*                                                                               
PCOMPX   BRAS  RE,ARSOFF           CLEAR ACCESS REGISTERS                       
         CLI   PCMPRQ,0            SET CC EQ IF NOTHING REQUEUED                
         B     EXIT                                                             
         DROP  R2,R3                                                            
         EJECT                                                                  
***********************************************************************         
* FIND FIRST UNPROCESSED WORK & INITIALISE CORRESPONDING TASK         *         
* R9=A(TASKCNST)                                                      *         
* RA=V(SYSFACS)                                                       *         
* EXIT SSBTKADR=A(NEW TASK CONTROL BLOCK),TCBUTL=A(UTL ENTRY)         *         
* R7=A(DUMMY SELIST ENTRY)                                            *         
* R5=A(UTL COPY)                                                      *         
***********************************************************************         
         USING TASKCNST,R9                                                      
         USING SYSFACD,RA                                                       
         USING SSBD,R8                                                          
TASKFIND NTR1  BASE=*,LABEL=*                                                   
         TM    SSBOPECB,X'80'      SPECIALS THAT RUN IN SPRTSK HERE             
         BO    TFN08               SROPS IN PROGRESS                            
*                                                                               
         LA    R5,UTLZERO          POINT TO DUMMY UTL ENTRY                     
         USING UTLD,R5                                                          
         TM    DSOPSECB,POSTED     TEST DSPACE OPS ECB POSTED                   
         BO    TFN02                                                            
         L     RF,SSBOPECB                                                      
         TM    0(RF),POSTED        OPERATOR REQUEST TO HANDLE                   
         BO    TFN02                                                            
         B     TFN04                                                            
*                                                                               
TFN02    TM    TSTAT2,TSTATTIP     TEST BUSY WITH MAIN INITIALISATION           
         BO    TFN08               YES - CAN'T DO ANYTHING ELSE                 
*                                                                               
         XC    DSOPSECB,DSOPSECB                                                
         MVC   SSBTKADR,SPRTSK     SET A(SPARE TASK)                            
         MVC   DUB+4(2),$OPS                                                    
         B     TFN06                                                            
*                                                                               
TFN04    TM    TSTAT2,TSTATTIP     TEST BUSY WITH MAIN INITIALISATION           
         BO    EXITL               YES - CAN'T DO ANYTHING ELSE                 
         MVI   MODE,0              SET NORMAL MODE                              
         NOP   *+12                                                             
         OI    *-3,X'F0'                                                        
         OI    MODE,X'80'          SET FIRST TIME MODE                          
*                                                                               
         CLI   MODE,0              TIMER SET?                                   
         BE    TFN08               NO                                           
         MVC   DUB+4(2),$T2                                                     
*                                                                               
TFN06    MVC   SSBTKADR,SPRTSK     RUN IN SPARE TASK                            
         L     R7,VSELIST          GET A(DUMMY SELIST ENTRY)                    
         USING SELISTD,R7                                                       
         LH    RE,0(R7)                                                         
         L     RF,2(R7)                                                         
         LA    R7,6(R7)                                                         
         CLI   SESYS,1             MATCH SYSTEM (SERVICE=SE#1)                  
         BE    *+10                                                             
         BXLE  R7,RE,*-8                                                        
         DC    H'0'                WHERE SELIST FOR SERVICE?                    
*                                                                               
         L     R6,SSBTKADR                                                      
         USING TCBD,R6                                                          
         ST    R7,TCBSE            SET A(SELIST ENTRY)                          
         ST    R5,TCBUTL           SAVE A(UTL)                                  
         MVC   TXNEXTIN,SEFIRST                                                 
         MVC   TNEXTIN,SEFIRST+1   .....                                        
         ST    R5,SEFIRST          PUT THIS UTL TO FRONT OF QUEUE               
*                                                                               
         LH    RF,SEQLEN                                                        
         AHI   RF,1                                                             
         STH   RF,SEQLEN           INCREMENT QUEUE                              
*                                                                               
         MVC   TAGYB,MODE          SET MODE                                     
         MVC   TSVCREQ,DUB+4       SET SPECIAL S/R                              
         NI    SSBT2,X'7F'                                                      
         MVC   TTIMETU,DUB         SET ARRIVAL TIME TO NOW                      
         B     EXITOK              GO PROCESS TRANSACTION                       
         DROP  R6,R7                                                            
*                                                                               
TFN08    BRAS  RE,ARSOFF           NORMAL TRANSACTIONS HERE                     
         LAM   AR3,AR3,SSBALET                                                  
         ICM   R3,15,SSBATOR                                                    
         SAC   512                                                              
         USING TORFACD,R3                                                       
         CLI   TORSTRTD,SBYES      TOR INITIALISED?                             
         BE    *+12                NO - GO TO SLEEP                             
         BRAS  RE,ARSOFF                                                        
         B     EXITL                                                            
*                                                                               
         BRAS  RE,ARSOFF                                                        
         GOTO1 VAORSLT,TFLIST,TO24=Y GET OUR SLOT IN TOR BLOCK                  
         BRAS  RE,ARSOFF                                                        
         SAM31                                                                  
         LAM   AR3,AR3,SSBALET                                                  
         ICM   R3,15,0(R1)                                                      
         SAC   512                                                              
         USING SBEXCHD,R3                                                       
*                                                                               
         ICM   R0,15,SBTSKMAX      NUMBER OF TASKS IN AOR                       
         CPYA  AR4,AR3                                                          
         LA    R4,SBTSKBLK                                                      
         USING EXCHNGD,R4                                                       
*                                                                               
         L     R5,VUTL                                                          
         LH    RE,0(R5)                                                         
*NOP     XR    RF,RF                                                            
*NOP     IC    RF,SSBTASKS         INDEX INTO START FOR REAL UTLS               
*NOP     MSR   RF,RE                                                            
*NOP     AR    R5,RF                                                            
         AHI   R5,6                R5=A(LOCAL XA UTL BLOCK)                     
XA       USING UTLD,R5                                                          
*                                                                               
TFN10    CLI   EXCFLAG,EXCNEW      NEW TRANSACTION TO PROCESS?                  
         BE    TFN12               YES                                          
         AHI   R4,EXCHNGLQ                                                      
         AR    R5,RE                                                            
         BCT   R0,TFN10                                                         
         BRAS  RE,ARSOFF                                                        
         XC    POSTRECB,POSTRECB   CLEAR POSTRECB NOW                           
         B     EXITL               NO WORK TO DO                                
         DROP  R3                                                               
*                                                                               
TFN12    L     R6,VTCB             MATCH TCB SLOT TO EXCHANGE SLOT              
         LH    RE,0(R6)                                                         
         L     RF,2(R6)                                                         
         AHI   R6,6                                                             
         USING TCBD,R6                                                          
         CLC   EXCIDENT,TCBID                                                   
         BE    *+10                                                             
         BXLE  R6,RE,*-10                                                       
         DC    H'0'                                                             
         MVI   EXCFLAG,EXCBUSY     SET TASK IS NOW RUNNING                      
         ST    R6,SSBTKADR         SET A(ACTIVE TASK)                           
*                                                                               
         ICM   R3,15,SSBATOR       POINT R3 BACK TO TOR BLOCK                   
         USING TORFACD,R3                                                       
         ICM   R3,15,TORUTL        PICK UP A(UTL BLOCK) IN TOR                  
         DROP  R3                                                               
*                                                                               
         LAM   AR3,AR3,SSBTLET     R3 POINTS INTO TOR                           
         LH    RF,EXCUTL           PICK UP TNUM & INDEX INTO UTL LIST           
         AHI   RF,-1                                                            
         LH    RE,0(,R3)                                                        
         MSR   RF,RE                                                            
         LA    R3,6(RF,R3)         R3=A(UTL IN TOR)                             
TR       USING UTLD,R3                                                          
*                                                                               
         MVC   TFXAUTL,XA.TUTLXADR SAVE A(XA BLOCK IN AOR)                      
*                                                                               
         LR    RF,RE               RF=LENGTH OF UTL                             
         LR    R1,RF               R1=LENGTH OF UTL                             
         LR    R0,R5               R0=A(XA.UTLD) IN AOR                         
         LR    RE,R3               RE=A(TR.UTLD) IN TOR                         
         CPYA  ARE,AR3             POINT RE INTO TOR REGION FOR COPY            
         MVCL  R0,RE               COPY UTL ENTRY FROM TOR TO AOR               
         LAM   ARE,ARE,ARZERO                                                   
*                                                                               
         MVC   XA.TUTLXADR,TFXAUTL                                              
         ST    R5,TCBUTL           SET A(UTL) IN TCB                            
*                                                                               
         ICM   R1,15,XA.TUTLXADR                                                
         USING XAUTLD,R1                                                        
         MVC   SAVE1,XAAPASSR                                                   
         MVC   SAVE2,XAASWTAB                                                   
         DROP  R1                                                               
*                                                                               
         CPYA  ARE,AR3             POINT RE INTO TOR REGION FOR COPY            
         ICM   RE,15,TR.TUTLXADR   COPY OUT XA PORTION OF UTL                   
TX       USING XAUTLD,RE                                                        
*                                                                               
         ICM   R0,15,XA.TUTLXADR   INTO LOCAL XA BLOCK                          
         ICM   R1,15,TX.XALEN                                                   
         LR    RF,R1                                                            
         MVCL  R0,RE                                                            
         LAM   ARE,ARE,ARZERO                                                   
*                                                                               
         ICM   R1,15,XA.TUTLXADR                                                
         USING XAUTLD,R1                                                        
         MVC   XAAPASSR,SAVE1                                                   
         MVC   XAASWTAB,SAVE2                                                   
         DROP  R1,R6,TR,TX                                                      
*                                                                               
         GOTO1 VLCM,TFLIST,VTGETBUF GET FREE LOCAL BUFFER (RTN IN R1)           
         SAFE  CLEAR=ARZERO                                                     
         SAM31 ,                   SWITCH INTO XA                               
*                                                                               
         ICM   R6,15,XA.TBUFF      SAVE A(TBUFF) IN TOR                         
         AHI   R6,-32              POINT TO HEADER                              
         LAY   R7,32+TBUFFXL                                                    
         LR    RE,R1                                                            
         STCM  RE,15,XA.TBUFF      SET A(LOCAL TBUFF) IN TBUFF                  
         AHI   RE,-32                                                           
         LR    RF,R7                                                            
*                                                                               
         LAM   AR6,AR6,SSBTLET                                                  
         MVCL  RE,R6               COPY TOR TBUFF LOCALLY                       
         BRAS  RE,ARSOFF                                                        
         DROP  R4                                                               
*                                                                               
         L     R6,SSBTKADR                                                      
         USING TCBD,R6                                                          
         L     R7,VSELIST          GET A(SELIST ENTRY)                          
         USING SELISTD,R7                                                       
         LH    RE,0(R7)                                                         
         L     RF,2(R7)                                                         
         LA    R7,6(R7)                                                         
*                                                                               
         LHI   R0,1                SE#1 FOR SERVICE REQUESTS                    
         OC    XA.TSVCREQ,XA.TSVCREQ                                            
         BNZ   *+8                                                              
         ICM   R0,1,XA.TSYS                                                     
         CLM   R0,1,SESYS          MATCH SYSTEM                                 
         BE    *+10                                                             
         BXLE  R7,RE,*-8                                                        
         DC    H'0'                                                             
*                                                                               
         ST    R7,TCBSE            SET SELIST ENTRY                             
         STCM  R7,7,TASYS          SET LOCAL SELIST ENTRY IN UTL                
         STCM  R7,7,TARSYS         SET REAL SELIST ENTRY IN UTL                 
         MVC   SSBSIN,TSIN         SET SIN TO THIS ONE                          
         BRAS  RE,ARSOFF                                                        
         B     EXITOK              SET CC EQUAL                                 
         DROP  XA,R6,R7                                                         
         EJECT                                                                  
***********************************************************************         
* COPY COMPLETED TASK INFORMATION BACK INTO TOR                       *         
* R5=A(UTL TO USE)                                                    *         
***********************************************************************         
         USING UTLD,R5                                                          
TORBACK  NTR1  BASE=*,LABEL=*                                                   
         BRAS  RE,ARSOFF                                                        
         OC    TNUM,TNUM                                                        
         BZ    TBACKX              DUMMY TERMINALS DON'T GO BACK                
         CLC   TLUID(5),=C'DUMMY'                                               
         BE    TBACKX              SCRIPT TERMINALS DON'T GO BACK               
*                                                                               
         L     R6,SSBTKADR                                                      
         USING TCBD,R6                                                          
         C     R6,SPRTSK                                                        
         BE    TBACKX              NOTHING IN SPARE TASK COPIED BACK            
*                                                                               
         LAM   AR3,AR3,SSBALET                                                  
         ICM   R3,15,SSBATOR                                                    
         SAC   512                                                              
         USING TORFACD,R3          R3=A(TOR REGION)                             
         CPYA  AR4,AR3                                                          
         LA    R4,TORFACLQ(,R3)                                                 
         USING SBEXCHD,R4          R4=A(FACPAK GRID ENTRY)                      
         LH    RE,0(,R4)                                                        
         L     RF,2(,R4)                                                        
         LA    R4,6(,R4)                                                        
         CLC   ASTOKEN,SBSTOKEN    FIND OUR GRID ENTRY?                         
         BE    *+10                                                             
         BXLE  R4,RE,*-10                                                       
         DC    H'0'                WHERE IS OUR GRID ENTRY?                     
*                                                                               
         ICM   R0,15,SBTSKMAX      R0=# TASKS IN THIS AOR                       
         LA    R7,SBTSKBLK                                                      
         CPYA  AR7,AR4                                                          
         USING EXCHNGD,R7          R7=A(TASK CONTROL BLOCK)                     
*                                                                               
TBACK02  CLC   EXCUTL,TNUM         THIS TASK SLOT?                              
         BE    TBACK04             YES                                          
         AHI   R7,EXCHNGLQ                                                      
         BCT   R0,TBACK02                                                       
         DC    H'0'                WHERE SLOT?                                  
*                                                                               
TBACK04  CLI   TORAVLBL,SBYES      TOR STILL THERE?                             
         BE    *+16                YES                                          
         OI    SBDONE,POSTED                                                    
         MVI   EXCFLAG,EXCDONE     NO - FLAG TASK COMPLETE & EXIT               
         B     TBACKX                                                           
         LAM   AR8,AR8,SSBTLET                                                  
         ICM   R8,15,TORUTL        R8=A(TOR UTL LIST)                           
         SAM31                                                                  
*                                                                               
         XR    RF,RF                                                            
         ICM   RF,3,TNUM                                                        
         AHI   RF,-1               RF=TNUM-1                                    
         LH    RE,0(,R8)                                                        
         MSR   RF,RE                                                            
         LA    R8,6(RF,R8)         R8=A(TOR UTL ENTRY)                          
TR       USING UTLD,R8                                                          
*                                                                               
         MVC   TBKBUFF,TR.TBUFF    PRESERVE A(TBUFF)                            
         MVC   TBKASYS,TR.TASYS    PRESERVE TASYS                               
         MVC   TBKARSYS,TR.TARSYS  PRESERVE TARSYS                              
         MVC   TBKUXAD,TR.TUTLXADR PRESERVE TUTLXADR                            
*                                                                               
         LR    RF,RE               RF=LENGTH OF UTL                             
         LR    R1,RF               R1=LENGTH OF UTL                             
         LR    RE,R8               RE=A(TR.UTLD) IN TOR                         
         LR    R0,R5               R0=A(XA.UTLD) IN AOR                         
         CPYA  ARE,AR8             POINT RE INTO TOR REGION FOR COPY            
         MVCL  RE,R0               COPY UTL ENTRY FROM AOR TO TOR               
         LAM   ARE,ARE,ARZERO                                                   
*                                                                               
         MVC   TR.TBUFF,TBKBUFF    RESTORE SAVED VALUES                         
         MVC   TR.TASYS,TBKASYS                                                 
         MVC   TR.TARSYS,TBKARSYS                                               
         MVC   TR.TUTLXADR,TBKUXAD                                              
*                                                                               
         ICM   RE,15,TR.TBUFF      COPY BACK TBUFF TO TOR                       
         AHI   RE,-32                                                           
         ICM   R0,15,TBUFF                                                      
         AHI   R0,-32                                                           
         LAY   R1,32+TBUFFXL                                                    
         LR    RF,R1                                                            
         CPYA  ARE,AR8             POINT RE INTO TOR ADDRESS SPACE              
         MVCL  RE,R0                                                            
*                                                                               
         ICM   RE,15,TR.TUTLXADR                                                
         USING XAUTLD,RE                                                        
         MVC   SAVE1,XAAPASSR                                                   
         MVC   SAVE2,XAASWTAB                                                   
         DROP  RE                                                               
*                                                                               
         ICM   RE,15,TR.TUTLXADR   COPY BACK XA PORTION OF UTL                  
         ICM   R0,15,TUTLXADR                                                   
         ICM   R1,15,XALEN-XAUTLD(RE)                                           
         LR    RF,R1                                                            
         MVCL  RE,R0                                                            
*                                                                               
         ICM   RE,15,TR.TUTLXADR                                                
         USING XAUTLD,RE                                                        
         MVC   XAAPASSR,SAVE1                                                   
         MVC   XAASWTAB,SAVE2                                                   
         LAM   ARE,ARE,ARZERO                                                   
         DROP  RE                                                               
*                                                                               
         OI    SBDONE,POSTED       SET AOR HAS COMPLETED WORK                   
         MVI   EXCFLAG,EXCDONE     FLAG TASK COMPLETED                          
         ICM   R0,15,TBUFF         GET A(TBUFF) AND FREE IT                     
         GOTO1 VLCM,TBKLIST,VTRELBUF,(R0)                                       
         SAFE  CLEAR=ARZERO                                                     
*                                                                               
         ICM   R4,15,TORDONE       GET A(COMPLETION ECB IN TOR)                 
         XR    R5,R5                                                            
         ICM   R5,3,TORASID        GET ASID FOR TOR IN R4                       
*                                                                               
         BRAS  RE,ARSOFF                                                        
         DROP  R3,R4,R5,TR                                                      
*                                                                               
         LOCASCB ASID=(R5)         USE ASID TO GET ASCB                         
         LR    R2,R1                                                            
         LTR   RF,RF                                                            
         JNZ   *+2                 TEMP FOR DEBUGGING                           
*                                                                               
         USING ASCB,R2             MAKE SURE ASCB IS OURS                       
         CLC   ASCBASCB,=C'ASCB'                                                
         JNE   *+2                 FIND A BETTER TEST                           
         DROP  R2                                                               
*                                                                               
         POST (R4),911,ASCB=(R2),LINKAGE=SYSTEM,ECBKEY=8,MF=(E,POSTTO)          
         LTR   RF,RF                                                            
         BZ    TBACKX                                                           
                                                                                
***********************************************************************         
* MIGHT NEED TO DO SOMETHING HERE ON ERROR                            *         
***********************************************************************         
TBACKX   B     EXIT                RETURN TO CALLER AFTER XM POST               
         DROP  R6,R7,R9                                                         
*                                                                               
POSTTO   POST  ECBKEY=YES,MF=L                                                  
         EJECT                                                                  
***********************************************************************         
* I/O WAITS ENTER HERE                                                *         
***********************************************************************         
ADWAIT   NTR1  BASE=*,LABEL=*                                                   
         SAC   0                                                                
         L     R9,ADWCNST                                                       
         USING TASKCNST,R9                                                      
         L     RA,VSYSFX                                                        
         USING SYSFACD,RA                                                       
         L     R8,VSSB                                                          
         USING SSBD,R8                                                          
         SAM24                     NEED TO ENSURE 24 BIT MODE                   
*                                                                               
         ICM   RF,15,VCHAIN        LOGGING LONGEST CHAIN?                       
         BZ    *+6                                                              
         BASR  RE,RF                                                            
         ICM   RF,15,VCRAPPER      TRACING CRAPPER?                             
         BZ    *+6                                                              
         BASR  RE,RF                                                            
*                                                                               
         CLI   SSBMTIND,0          TEST MULTI-TASKING ACTIVE                    
         BNZ   ADWAIT02                                                         
         WAIT  ECB=(1)                                                          
         B     EXIT                                                             
*                                                                               
ADWAIT02 L     R3,SSBTKADR         POINT TO TCB ENTRY                           
         USING TCBD,R3                                                          
         ST    RD,TCBSVRD          SAVE CURRENT RD AND ECB ADDR                 
         ST    R1,TCBSVECB                                                      
         STAM  AR0,ARF,TCBIARS     SAVE ACCESS REGISTERS                        
*                                                                               
         CLI   TCBSVECB,X'FA'      TEST SHARED MEMORY WAIT                      
         JE    ADWAIT06                                                         
         CLI   TCBSVECB,X'FB'      TEST ISGENQ WAIT                             
         JE    ADWAIT06                                                         
         CLI   TCBSVECB,X'FC'      TEST DO-NOT-WAIT/DATASPACE-WAIT              
         BNL   *+8                                                              
         MVI   TCBSVECB,0          CLEAR HOB OF NORMAL ECBS                     
*                                                                               
ADWAIT06 GOTO1 VDTFIOA,PARM,(C'S',(R3))  SAVE DTF DATA  (WLM)                   
*                                                                               
         TIME  TU                  ACCUMULATE CPU TIME                          
         L     R1,TCBTIME                                                       
         CLR   R0,R1                                                            
         BNL   *+8                                                              
         AL    R0,MIDNIGHT         ADJUST FOR MIDNIGHT                          
         SLR   R0,R1                                                            
         AL    R0,TCBCPUTM                                                      
         ST    R0,TCBCPUTM                                                      
*                                                                               
         TIMEUSED STORADR=CPUTIME,LINKAGE=SYSTEM,CPU=MIC                        
         LG    GR1,CPUTIME                                                      
         SLG   GR1,TCBCPUSE        GR1=CPU USED BY TASK THIS GO ROUND           
         AL    R1,TCBCPUTK                                                      
         ST    R1,TCBCPUTK         BUMP CPU USED BY THIS TASK                   
*                                                                               
         CLI   TCBSVECB,X'FC'      TEST DO-NOT-WAIT/DATASPACE-WAIT              
         BNL   ADWAIT08            YES                                          
         CLI   TCBSVECB,X'FA'      TEST SHRMEM RCVR WAIT                        
         JE    ADWAIT08                                                         
         CLI   TCBSVECB,X'FB'      TEST ISGENQ WAIT                             
         JE    ADWAIT08                                                         
*                                                                               
         XR    R1,R1               NORMAL ECB - BUMP I/O COUNTER                
         ICM   R1,7,TCBIOCNT                                                    
         AHI   R1,1                                                             
         STCM  R1,7,TCBIOCNT                                                    
*                                                                               
         L     R1,TCBLIOCT         ADD LOGICAL I/O COUNT TO TOTAL               
         A     R1,TCBLIOC                                                       
         ST    R1,TCBLIOCT                                                      
         XC    TCBLIOC,TCBLIOC     CLEAR LOGICAL I/O COUNT                      
*                                                                               
*NOP     L     RF,=V(TEMPIOS)      I/O TRACE                                    
*NOP     BASR  RE,RF                                                            
*                                                                               
ADWAIT08 L     RF,AADNEXT                                                       
         BR    RF                                                               
*                                                                               
ADWCNST  DC    A(TASKCNST)                                                      
         DROP  R3,R8,R9,RA,RB                                                   
         EJECT                                                                  
***********************************************************************         
* VSAM I/O WAITS ENTER HERE MAINLY DUE TO DIFFERENT I/O COUNTER       *         
***********************************************************************         
VSWAIT   NTR1  BASE=*,LABEL=*                                                   
         SAC   0                                                                
         L     R9,VSWCNST                                                       
         USING TASKCNST,R9                                                      
         L     RA,VSYSFX                                                        
         USING SYSFACD,RA                                                       
         L     R8,VSSB                                                          
         USING SSBD,R8                                                          
         SAM24                     NEED TO ENSURE 24 BIT MODE                   
*                                                                               
         ICM   RF,15,VCHAIN        LOGGING LONGEST CHAIN?                       
         BZ    *+6                                                              
         BASR  RE,RF                                                            
         ICM   RF,15,VCRAPPER      TRACING CRAPPER?                             
         BZ    *+6                                                              
         BASR  RE,RF                                                            
*                                                                               
         CLI   SSBMTIND,0          TEST MULTI-TASKING ACTIVE                    
         BNZ   VSWAIT02                                                         
         WAIT  ECB=(1)                                                          
         B     EXIT                                                             
*                                                                               
VSWAIT02 L     R3,SSBTKADR         POINT TO TCB ENTRY                           
         USING TCBD,R3                                                          
         ST    RD,TCBSVRD          SAVE CURRENT RD AND ECB ADDR                 
         ST    R1,TCBSVECB                                                      
         STAM  AR0,ARF,TCBIARS     SAVE ACCESS REGISTERS                        
*                                                                               
         GOTO1 VDTFIOA,PARM,(C'S',(R3))  SAVE DTF DATA  (WLM)                   
*                                                                               
         TIME  TU                  ACCUMULATE CPU TIME                          
         L     R1,TCBTIME                                                       
         CLR   R0,R1                                                            
         BNL   *+8                                                              
         AL    R0,MIDNIGHT         ADJUST FOR MIDNIGHT                          
         SLR   R0,R1                                                            
         AL    R0,TCBCPUTM                                                      
         ST    R0,TCBCPUTM                                                      
*                                                                               
         TIMEUSED STORADR=CPUTIME,LINKAGE=SYSTEM,CPU=MIC                        
         LG    GR1,CPUTIME                                                      
         SLG   GR1,TCBCPUSE        GR1=CPU USED BY TASK THIS GO ROUND           
         AL    R1,TCBCPUTK                                                      
         ST    R1,TCBCPUTK         BUMP CPU USED BY THIS TASK                   
*                                                                               
         L     R1,TCBVSCNT         BUMP VSAM I/O WAIT COUNTER                   
         AHI   R1,1                                                             
         ST    R1,TCBVSCNT                                                      
*                                                                               
         L     R1,TCBLIOCT         ADD LOGICAL I/O COUNT TO TOTAL               
         A     R1,TCBLIOC                                                       
         ST    R1,TCBLIOCT                                                      
         XC    TCBLIOC,TCBLIOC     CLEAR LOGICAL I/O COUNT                      
*                                                                               
         L     RF,AADNEXT                                                       
         BR    RF                                                               
*                                                                               
VSWCNST  DC    A(TASKCNST)                                                      
         DROP  R3,R8,R9,RA,RB                                                   
         EJECT                                                                  
***********************************************************************         
* SCRIPT WAITS ENTER HERE - R1 POINTS TO CALLER'S ECB                 *         
***********************************************************************         
SCWAIT   NTR1  BASE=*,LABEL=*                                                   
         SAC   0                                                                
         L     R9,SCWCNST                                                       
         USING TASKCNST,R9                                                      
         L     RA,VSYSFX                                                        
         USING SYSFACD,RA                                                       
         L     R8,VSSB                                                          
         USING SSBD,R8                                                          
         SAM24                     NEED TO ENSURE 24 BIT MODE                   
*                                                                               
         ICM   RF,15,VCHAIN        LOGGING LONGEST CHAIN?                       
         BZ    *+6                                                              
         BASR  RE,RF                                                            
         ICM   RF,15,VCRAPPER      TRACING CRAPPER?                             
         BZ    *+6                                                              
         BASR  RE,RF                                                            
*                                                                               
         L     R3,SSBTKADR                                                      
         USING TCBD,R3                                                          
         OI    TCBFLAG3,TCBSCPWT   SET AWAITING SCRIPT COMPLETION               
         ST    RD,TCBSVRD          SAVE CURRENT RD AND ECB ADDR                 
         ST    R1,TCBSVECB                                                      
         STAM  AR0,ARF,TCBIARS     SAVE ACCESS REGISTERS                        
*                                                                               
         CLI   TCBSVECB,X'FA'      TEST SHRMEM RECOVERY                         
         JE    SCWAIT02                                                         
         CLI   TCBSVECB,X'FB'      TEST ISGENQ WAIT                             
         JE    SCWAIT02                                                         
         CLI   TCBSVECB,X'FC'      TEST DO-NOT-WAIT/DATASPACE-WAIT              
         BNL   *+8                 YES                                          
         MVI   TCBSVECB,0          CLEAR HOB OF NORMAL ECB                      
*                                                                               
SCWAIT02 GOTO1 VDTFIOA,PARM,(C'S',(R3))  SAVE DTF DATA                          
*                                                                               
         TIME  TU                  ACCUMULATE CPU TIME                          
         L     R1,TCBTIME                                                       
         CLR   R0,R1                                                            
         BNL   *+8                                                              
         AL    R0,MIDNIGHT         ADJUST FOR MIDNIGHT                          
         SLR   R0,R1                                                            
         AL    R0,TCBCPUTM                                                      
         ST    R0,TCBCPUTM                                                      
*                                                                               
         TIMEUSED STORADR=CPUTIME,LINKAGE=SYSTEM,CPU=MIC                        
         LG    GR1,CPUTIME                                                      
         SLG   GR1,TCBCPUSE        GR1=CPU USED BY TASK THIS GO ROUND           
         AL    R1,TCBCPUTK                                                      
         ST    R1,TCBCPUTK         BUMP CPU USED BY THIS TASK                   
*                                                                               
         L     RF,AADNEXT                                                       
         BR    RF                                                               
*                                                                               
SCWCNST  DC    A(TASKCNST)                                                      
         DROP  R3,R8,R9,RA,RB                                                   
         EJECT                                                                  
***********************************************************************         
* MQIO WAITS ENTER HERE - R1 POINTS TO CALLER'S ECB                   *         
***********************************************************************         
MQWAIT   NTR1  BASE=*,LABEL=*                                                   
         SAC   0                                                                
         L     R9,MQWCNST                                                       
         USING TASKCNST,R9                                                      
         L     RA,VSYSFX                                                        
         USING SYSFACD,RA                                                       
         L     R8,VSSB                                                          
         USING SSBD,R8                                                          
         SAM24                                                                  
*                                                                               
         ICM   RF,15,VCHAIN        LOGGING LONGEST CHAIN?                       
         BZ    *+6                                                              
         BASR  RE,RF                                                            
         ICM   RF,15,VCRAPPER      TRACING CRAPPER?                             
         BZ    *+6                                                              
         BASR  RE,RF                                                            
*                                                                               
         L     R3,SSBTKADR                                                      
         USING TCBD,R3                                                          
         OI    TCBFLAG3,TCBMQWT    SET AWAITING MQIO COMPLETION                 
         ST    RD,TCBSVRD          SAVE CURRENT RD AND ECB ADDR                 
         ST    R1,TCBSVECB                                                      
         STAM  AR0,ARF,TCBIARS     SAVE ACCESS REGISTERS                        
         CLI   TCBSVECB,X'FC'      TEST DO-NOT-WAIT/DATASPACE-WAIT              
         BNL   *+8                 YES                                          
         MVI   TCBSVECB,0          CLEAR HOB OF NORMAL ECB                      
*                                                                               
         GOTO1 VDTFIOA,PARM,(C'S',(R3))  SAVE DTF DATA                          
*                                                                               
         TIME  TU                  ACCUMULATE CPU TIME                          
         L     R1,TCBTIME                                                       
         CLR   R0,R1                                                            
         BNL   *+8                                                              
         AL    R0,MIDNIGHT         ADJUST FOR MIDNIGHT                          
         SLR   R0,R1                                                            
         AL    R0,TCBCPUTM                                                      
         ST    R0,TCBCPUTM                                                      
*                                                                               
         TIMEUSED STORADR=CPUTIME,LINKAGE=SYSTEM,CPU=MIC                        
         LG    GR1,CPUTIME                                                      
         SLG   GR1,TCBCPUSE        GR1=CPU USED BY TASK THIS GO ROUND           
         AL    R1,TCBCPUTK                                                      
         ST    R1,TCBCPUTK         BUMP CPU USED BY THIS TASK                   
*                                                                               
         L     RF,AADNEXT                                                       
         BR    RF                                                               
*                                                                               
MQWCNST  DC    A(TASKCNST)                                                      
         DROP  R3,R8,R9,RA,RB                                                   
         EJECT                                                                  
***********************************************************************         
* TIMER WAITS ENTER HERE - R1=LENGTH OF TIME TO WAIT FOR              *         
***********************************************************************         
TKWAIT   NTR1  BASE=*,LABEL=*                                                   
         SAC   0                                                                
         L     R9,TKWCNST                                                       
         USING TASKCNST,R9                                                      
         L     RA,VSYSFX                                                        
         USING SYSFACD,RA                                                       
         L     R8,VSSB                                                          
         USING SSBD,R8                                                          
         SAM24                     NEED TO ENSURE 24 BIT MODE                   
*                                                                               
         ST    R1,TKFULL           SAVE TIME INTERVAL                           
         ICM   RF,15,VCHAIN        LOGGING LONGEST CHAIN?                       
         BZ    *+6                                                              
         BASR  RE,RF                                                            
         ICM   RF,15,VCRAPPER      TRACING CRAPPER?                             
         BZ    *+6                                                              
         BASR  RE,RF                                                            
         CLI   SSBMTIND,0          TEST MULTI-TASKING ACTIVE                    
         BNE   TKWAIT2             YES                                          
*                                                                               
         GOTO1 VTICTOC,DUB,C'SSET'                                              
         STIMER WAIT,TUINTVL=TKFULL                                             
         B     EXIT                                                             
*                                                                               
TKWAIT2  L     R3,VSSB                                                          
         L     R3,SSBTKADR                                                      
         USING TCBD,R3                                                          
         ST    RD,TCBSVRD          SAVE CURRENT RD                              
         ST    R1,TCBTIMEW         SET WAKE UP IME                              
         STAM  AR0,ARF,TCBIARS     SAVE ACCESS REGISTERS                        
*                                                                               
         L     R0,TCBTKWT          TKWAIT LOOP COMPLETION                       
         AHI   R0,1                                                             
         ST    R0,TCBTKWT                                                       
         TIME  TU                  GET TIME NOW                                 
         LR    R1,R0                                                            
         A     R1,TCBTIMEW         ADD WAIT TIME                                
         ST    R1,TCBTIMEW         SET WAKE UP IME                              
*                                                                               
         GOTO1 VDTFIOA,PARM,(C'S',(R3))  SAVE DTF DATA                          
*                                                                               
         L     R1,TCBTIME                                                       
         CLR   R0,R1                                                            
         BNL   *+8                                                              
         AL    R0,MIDNIGHT         ADJUST FOR MIDNIGHT                          
         SLR   R0,R1                                                            
         AL    R0,TCBCPUTM                                                      
         ST    R0,TCBCPUTM                                                      
*                                                                               
         TIMEUSED STORADR=CPUTIME,LINKAGE=SYSTEM,CPU=MIC                        
         LG    GR1,CPUTIME                                                      
         SLG   GR1,TCBCPUSE        GR1=CPU USED BY TASK THIS GO ROUND           
         AL    R1,TCBCPUTK                                                      
         ST    R1,TCBCPUTK         BUMP CPU USED BY THIS TASK                   
*                                                                               
TKWAIT3  L     RF,AADNEXT                                                       
         BR    RF                                                               
*                                                                               
TKWCNST  DC    A(TASKCNST)                                                      
         DROP  R3,R9,RA,RB                                                      
         EJECT                                                                  
***********************************************************************         
* TIMER INTERRUPTED TASKS ENTER HERE (FROM ABENDIT)                   *         
***********************************************************************         
ADIRPT   BASR  RB,0                ESTABLISH ADDRESSABILITY                     
         USING *,RB                                                             
         L     R9,ADICNST                                                       
         USING TASKCNST,R9                                                      
         L     RA,VSYSFX                                                        
         USING SYSFACD,RA                                                       
         L     R8,VSSB                                                          
         USING SSBD,R8                                                          
         L     RD,ATSKWK           POINT TO REGSAVE AREA                        
         SAM24                     NEED TO ENSURE 24 BIT MODE                   
*                                                                               
         ICM   RF,15,VCHAIN        LOGGING LONGEST CHAIN?                       
         BZ    *+6                                                              
         BASR  RE,RF                                                            
         ICM   RF,15,VCRAPPER      TRACING CRAPPER?                             
         BZ    *+6                                                              
         BASR  RE,RF                                                            
*                                                                               
         L     R3,SSBTKADR         GET TCB ENTRY ADDRESS                        
         USING TCBD,R3                                                          
         TM    TCBFLAG3,TCBDBWAI   TEST DEBUG WAIT                              
         BO    ADIRP06                                                          
         TM    TCBINDS1,TCBABPEN   OR ABEND PENDING                             
         BO    ADIRP06                                                          
*                                                                               
         L     RF,VABNDITR         GET ADDRESS OF TIMER IRPT REGS               
         MVC   TCBPSW(72),0(RF)    AND SAVE PSW/REGS IN TCB                     
         MVC   TCBIARS,72(RF)      AND SAVE ACCESS REGS                         
         MVC   TCBIGRS,136(RF)                                                  
         LA    R0,TCBSVRD          NOW DUMMY UP AN ECB ADDRESS                  
         ST    R0,TCBSVECB                                                      
         MVI   TCBSVRD+0,X'7F'                                                  
*                                                                               
         LH    R1,TCBIRPTS         BUMP TIMER INTERRUPT COUNTER                 
         LA    R1,1(R1)                                                         
         STH   R1,TCBIRPTS                                                      
         TM    TCBFLAG3,TCBTBND    RUNNING AS TIME BOUND                        
         BO    ADIRP04             YES                                          
*                                                                               
         CH    R1,SSBPOPMX         IF EXCEEDS MAXIMUM ASSUME LOOP               
         BNH   ADIRP06                                                          
         L     RE,VABNDWHY         POINT TO ABEND CAUSE BYTE                    
         MVI   0(RE),C'I'          AND INDICATE LOOP (INTVL TIMER)              
         DC    H'0'                THEN CAUSE A PROGRAM CHECK                   
*                                                                               
ADIRP04  TIME  TU                  TIME NOW - START TIME                        
         S     R0,TCBSTTM                                                       
         SRDL  R0,32                                                            
         D     R0,T1SEC            CONVERT TO WHOLE SECONDS                     
*                                                                               
         XR    R0,R0                                                            
         ICM   R0,1,TCBTBSEC       CHECK WITHIN BOUNDS ALLOWED                  
         BNZ   *+8                                                              
         LHI   R0,10                                                            
         CR    R0,R1               STILL OK?                                    
         BH    ADIRP06             YES                                          
*                                                                               
         L     RE,VABNDWHY         POINT TO ABEND CAUSE BYTE                    
         MVI   0(RE),C'I'          AND INDICATE LOOP (INTVL TIMER)              
         DC    H'0'                THEN CAUSE A PROGRAM CHECK                   
*                                                                               
ADIRP06  GOTO1 VDTFIOA,PARM,(C'S',(R3))  SAVE DTF DATA                          
*                                                                               
         TIME  TU                  ACCUMULATE CPU TIME                          
         L     R1,TCBTIME                                                       
         CLR   R0,R1                                                            
         BNL   *+8                                                              
         AL    R0,MIDNIGHT         ADJUST FOR MIDNIGHT                          
         SLR   R0,R1                                                            
         AL    R0,TCBCPUTM                                                      
         ST    R0,TCBCPUTM                                                      
*                                                                               
         TIMEUSED STORADR=CPUTIME,LINKAGE=SYSTEM,CPU=MIC                        
         LG    GR1,CPUTIME                                                      
         SLG   GR1,TCBCPUSE        GR1=CPU USED BY TASK THIS GO ROUND           
         AL    R1,TCBCPUTK                                                      
         ST    R1,TCBCPUTK         BUMP CPU USED BY THIS TASK                   
*                                                                               
         L     RF,AADNEXT                                                       
         BR    RF                                                               
*                                                                               
ADICNST  DC    A(TASKCNST)                                                      
         DROP  R3,R8,R9,RA,RB                                                   
         EJECT                                                                  
***********************************************************************         
* FASTART PASSES CONTROL TO V(ADFIRST)                                *         
* INTIALISE FOR ENTRY INTO ADNEXT                                     *         
***********************************************************************         
         USING *,RF                                                             
ADFIRST  L     R9,ADFCNST                                                       
         USING TASKCNST,R9                                                      
         L     RA,VSYSFX                                                        
         USING SYSFACD,RA                                                       
         STM   RB,RE,ADFIRREG      SAVE RETURN REGS RB,RC,RD,RE                 
         B     ADF00                                                            
*                                                                               
ADFCNST  DC    A(TASKCNST)                                                      
*                                                                               
         DROP  RF                                                               
ADF00    NBASE 0,TASKFRST,=A(TASKWORK),SAVE=YES                                 
         SAC   0                                                                
         LAM   AR0,ARF,ARZERO                                                   
         L     R8,VSSB                                                          
         USING SSBD,R8                                                          
         TM    SSBSTAT4,SSBSAOR    ARE WE TOR OR AOR                            
         BO    AORFIRST                                                         
         EJECT                                                                  
***********************************************************************         
* ADFIRST ACTIONS FOR TOR                                             *         
***********************************************************************         
TORFIRST MVI   TORFLAG,C'Y'        IDENTIFY TOR                                 
*                                                                               
         EXTRACT ASIDFLD,'S',FIELDS=(ASID)                                      
         ALESERV EXTRACTH,STOKEN=ASTOKEN                                        
*                                                                               
         L     R2,ASIDFLD                                                       
         LOCASCB ASID=(R2)         LOCATE AND SAVE ASCB                         
         ST    R1,ASCBAD                                                        
         ST    R1,SSBAASCB                                                      
         MVC   SSBSTOKN,ASTOKEN    SAVE STOKEN IN SSB                           
         TM    SSBSTAT5,SSB5WLM    WLM INSTALLED?                               
         BZ    ADF01               NO                                           
                                                                                
***********************************************************************         
* BUILD THE STATIC COPY OF WLM REQUEST BLOCK                          *         
***********************************************************************         
         L     R2,ASCBAD           R2=A(ASCB)                                   
         USING ASCB,R2                                                          
         L     R2,ASCBJBNS         R2=A(STC JOB NAME)                           
         LA    R1,WLMPARMS         R1=A(CONNECT/DISCONNECT RB)                  
         USING WLMD,R1                                                          
         XC    WLMPARMS,WLMPARMS   CLEAR REQUEST BLOCK                          
         MVC   WLMSYSNAM,00(R2)    SET SUBSYSTEM NAME                           
         MVC   WLMSYSTYP,=C'FAC '  SET SUBSYSTEM TYPE                           
         DROP  R1,R2                                                            
*                                                                               
ADF01    LAM   AR2,AR2,SSBALET                                                  
         ICM   R2,15,SSBATOR                                                    
         JZ    *+2                 DIE IF NOT DEFINED                           
         SAC   512                                                              
*                                                                               
         USING TORFACD,R2          INITIALISE TOR INFORMATION AREA              
         MVC   TOREYE(4),=CL4'TOR-'                                             
         MVC   TOREYE+4(4),SSBSYSN4                                             
         MVC   TORSTOKN,ASTOKEN    PUBLISH TOR STOKEN                           
         L     RF,ASIDFLD                                                       
         STH   RF,TORASID          PUBLISH TOR ASID                             
         LA    RF,POSTECB                                                       
         STCM  RF,15,TORDONE       PUBLISH A(COMPLETION POST ECB)               
         MVC   TORUTL,VUTL         PUBLISH A(UTL ENTRIES)                       
         LA    RF,ABNDECB                                                       
         STCM  RF,15,TORAORDN      PUBLISH A(AOR ABEND ECB)                     
         MVI   TORSTRTD,SBYES      SET TOR STARTED                              
         MVI   TORAVLBL,SBYES      SET TOR AVAILABLE                            
*                                                                               
         AHI   R2,TORFACLQ         SET EXCHANGE GRID SIZE INFORMATION           
         USING SBEXCHD,R2                                                       
         LHI   R4,SBEXCHLQ         SET LENGTH OF SINGLE ROW                     
         STCM  R4,3,0(R2)                                                       
         LA    R5,SBFACMAX         MAX ALLOWABLE FACPAKS                        
         MSR   R5,R4               * LENGTH OF SINGLE ROW                       
         LA    R5,5(R5,R2)         + BXLE OVERHEAD - 1                          
         STCM  R5,15,2(R2)         = A(END OF GRID -1)                          
         LA    R2,6(,R2)                                                        
         LR    R0,R2               SAVE A(FIRST FACPAK ENTRY)                   
*                                                                               
         LA    R3,1(,R5)                                                        
         SR    R3,R2               R3=LENGTH OF GRID                            
         XR    RE,RE                                                            
         XR    RF,RF                                                            
         MVCL  R2,RE               CLEAR EXCHANGE GRID                          
         LR    R2,R0               RESTORE R2                                   
*                                                                               
         LA    RF,1                INITIALISE EXCHANGE GRID INFORMATION         
         STH   RF,SBFACID          SET FACPAK ID NUMBER                         
         LA    RF,1(RF)                                                         
         BXLE  R2,R4,*-8                                                        
*                                  PUBLISH TOR FACPAK DETAILS                   
         LR    R2,R0               TOR IS ALWAYS FIRST ENTRY                    
         MVC   SBSTOKEN,ASTOKEN    PUBLISH TOR STOKEN                           
         L     RF,ASIDFLD                                                       
         STH   RF,SBASID           PUBLISH TOR ASID                             
         MVC   SBSSB,VSSB                                                       
         MVC   SBTCB,VTCB                                                       
         MVC   SBWAKE,TORWAKE      TOR REQUIRES NO WAKE-UP ECB                  
         MVI   SBSTRTD,SBYES       SET TOR STARTED                              
         MVI   SBAVLBL,SBYES       SET TOR AVAILABLE                            
*                                                                               
         CPYA  AR6,AR2                                                          
         LA    R6,SBTSKBLK         INITIALISE TOR TASK DETAILS                  
         USING EXCHNGD,R6                                                       
*                                                                               
         L     R3,VTCB                                                          
         LH    R4,0(R3)                                                         
         L     R5,2(R3)                                                         
         LA    R3,6(R3)                                                         
         USING TCBD,R3                                                          
         XR    RF,RF                                                            
*                                                                               
ADF02    TM    TCBFLAG2,TCBNOPED+TCBAORED TEST END OF USABLE TCBS               
         BNZ   ADF22               YES                                          
         CLI   TCBID,C'*'          IGNORE IF NO *TASK HEADER                    
         BNE   ADF04                                                            
*                                                                               
         MVC   EXCIDENT,TCBID      MOVE IN TCB IDENTIFIER                       
         LA    RF,1(RF)                                                         
         ST    RF,SBTSKMAX         BUMP MAX TASK COUNT                          
         LA    R6,EXCHNGLQ(,R6)    NEXT TASK BLOCK WITHIN ROW                   
*                                                                               
ADF04    BXLE  R3,R4,ADF02                                                      
         B     ADF22                                                            
         DROP  R2,R3,R6                                                         
         EJECT                                                                  
***********************************************************************         
* ADFIRST ACTIONS FOR AOR                                             *         
***********************************************************************         
AORFIRST MVI   TORFLAG,C'N'        IDENTIFY AOR                                 
         MVC   NOTOR+4(3),SSBSYSNA                                              
*                                                                               
         LAM   AR2,AR2,SSBALET                                                  
         ICM   R2,15,SSBATOR                                                    
         JZ    *+2                 DIE IF NOT DEFINED                           
         SAC   512                                                              
         USING TORFACD,R2                                                       
         CLI   TORAVLBL,SBYES      IS TOR AVAILABLE?                            
         BE    ADF06               YES                                          
         SAC   0                                                                
         WTO   TEXT=NOTORMSG                                                    
         ABEND 900                                                              
*                                                                               
ADF06    MVC   TSTOKEN,TORSTOKN    GET TOR STOKEN LOCALLY                       
         SAC   0                                                                
*                                                                               
         XC    ADFCARD,ADFCARD     SVC 247 NEEDS CHANGING                       
         MVC   ADFCARD+00(04),=C'PALE'                                          
         MVC   ADFCARD+04(12),=C'TESTDATAMGRT'                                  
         MVC   ADFCARD+28(08),TSTOKEN                                           
         LA    R0,21                                                            
         LNR   R0,R0                                                            
         LA    R1,ADFCARD                                                       
         SVC   247                                                              
         LTR   RF,RF                                                            
         BZ    ADF08                                                            
*                                                                               
         CVD   RF,DUB                                                           
         OI    DUB+L'DUB-1,X'0F'                                                
         UNPK  RCMSG+4(4),DUB                                                   
         WTO   TEXT=NOTORMSG                                                    
         ABEND 901                                                              
*                                                                               
ADF08    L     RF,ADFCARD+24       SAVE ALET FOR TOR IN SSB & LOCALLY           
         ST    RF,AALET                                                         
         ST    RF,SSBTLET                                                       
*                                                                               
         EXTRACT ASIDFLD,'S',FIELDS=(ASID)                                      
         ALESERV EXTRACTH,STOKEN=ASTOKEN                                        
*                                                                               
         L     R3,ASIDFLD                                                       
         LOCASCB ASID=(R3)         LOCATE AND SAVE ASCB                         
         ST    R1,ASCBAD                                                        
         ST    R1,SSBAASCB                                                      
         MVC   SSBSTOKN,ASTOKEN    SAVE STOKEN IN SSB                           
         TM    SSBSTAT5,SSB5WLM    WLM INSTALLED?                               
         BZ    ADF09                                                            
                                                                                
***********************************************************************         
* BUILD THE STATIC COPY OF WLM REQUEST BLOCK                          *         
***********************************************************************         
         L     R3,ASCBAD           ADDRESS ASCB                                 
         USING ASCB,R3                                                          
         L     R3,ASCBJBNS         ADDRESS OF STC JOB NAME                      
         LA    R1,WLMPARMS         CONNECT/DISCONNECT RB                        
         USING WLMD,R1                                                          
         XC    WLMPARMS,WLMPARMS   CLEAR REQUEST BLOCK                          
         MVC   WLMSYSNAM,0(R3)     SET SUBSYSTEM NAME                           
         MVC   WLMSYSTYP,=C'FAC '  SET SUBSYSTEM TYPE                           
         DROP  R1,R3                                                            
*                                                                               
ADF09    AHI   R2,TORFACLQ         INDEX TO FACPAK EXCHANGE GRID                
         SAC   512                                                              
         USING SBEXCHD,R2                                                       
         LH    R4,0(,R2)                                                        
         L     R5,2(,R2)                                                        
*                                                                               
         LLC   R1,SSBSYSIX         USE SYSIX TO INDEX TO AOR                    
         SRL   R1,4                                                             
         MH    R1,0(,R2)                                                        
         LA    R2,6(,R2)                                                        
         AR    R2,R1                                                            
*                                                                               
         OC    SBSTOKEN,SBSTOKEN   SLOT FREE?                                   
         BZ    ADF10               THIS WILL NEED TO CHANGE (CPU++)=2           
*                                                                               
         SAC   0                                                                
         WTO   'THIS AOR IS ALREADY STARTED '                                   
         ABEND 902                                                              
*                                                                               
ADF10    MVC   SBSTOKEN,ASTOKEN    SAVE STOKEN FOR THIS AOR                     
         L     RF,ASIDFLD                                                       
         STH   RF,SBASID           SET ASID FOR THIS AOR                        
         LA    RF,POSTRECB                                                      
         STCM  RF,15,SBWAKE        SET A(WAKEUP ECB) FOR THIS AOR               
         MVC   SBSSB,VSSB                                                       
         MVC   SBTCB,VTCB                                                       
         MVI   SBSTRTD,SBYES       SET FACPAK STARTED                           
         MVI   SBAVLBL,SBNO                                                     
*                                                                               
         L     R3,VSELIST                                                       
         LH    R4,0(R3)                                                         
         L     R5,2(R3)                                                         
         LA    R3,6(R3)                                                         
         USING SELISTD,R3                                                       
         CPYA  AR6,AR2                                                          
         LA    R6,SBSYS                                                         
         XR    RF,RF                                                            
*                                                                               
ADF12    TM    SEIND,SEISTRT       IS THE SYSTEM STARTED?                       
         B     ADF14               *NOP* NOT YET IN USE                         
         BZ    ADF14                                                            
         LA    RF,1(RF)                                                         
         STCM  RF,1,SBSYSCNT                                                    
         MVC   0(L'SESYS,R6),SESYS                                              
         LA    R6,L'SESYS(,R6)                                                  
*                                                                               
ADF14    BXLE  R3,R4,ADF12                                                      
*                                                                               
         LA    R6,SBTSKBLK                                                      
         USING EXCHNGD,R6                                                       
         L     R3,VTCB                                                          
         LH    R4,0(R3)                                                         
         L     R5,2(R3)                                                         
         LA    R3,6(R3)                                                         
         USING TCBD,R3                                                          
         XR    RF,RF                                                            
*                                                                               
ADF16    TM    TCBFLAG2,TCBNOPED+TCBAORED                                       
         BNZ   ADF20               YES                                          
         CLI   TCBID,C'*'          IGNORE IF NO *TASK HEADER                    
         BNE   ADF18                                                            
*                                                                               
         XC    EXCHNGD(EXCHNGLQ),EXCHNGD                                        
         MVC   EXCIDENT,TCBID      MOVE IN TCB IDENTIFIER                       
         LA    RF,1(RF)                                                         
         ST    RF,SBTSKMAX         BUMP MAX TASK COUNT                          
         LA    R6,EXCHNGLQ(R6)     NEXT TASK BLOCK                              
*                                                                               
ADF18    BXLE  R3,R4,ADF16                                                      
*                                                                               
ADF20    SAC   0                                                                
         ST    R3,SPRTSK           SET A(LAST TASK) AS SPARE ($OPS)             
         NI    SSBSTAT1,255-SSBUII SET SSBGO FOR AOR                            
         DROP  R6                                                               
         EJECT                                                                  
***********************************************************************         
* COMMON ADFIRST ACTIONS FOR BOTH AOR AND TOR                         *         
***********************************************************************         
ADF22    GOTO1 VAORNUM,DUB,TO24=Y  GET AOR NUMBER                               
         SAM31                                                                  
         LLC   R1,DUB+3                                                         
         SLL   R1,4                                                             
         STC   R1,SSBSYSIX                                                      
         OC    SSBSYSIX,SSBSYSID   SET EXTERNAL SYSID                           
*                                                                               
         XC    DUB,DUB                                                          
         MVI   DUB+1,3                                                          
         GOTO1 VLOCKSPC,DUB,TO24=Y CALL LOCKSPC TO ENTER A FACPAK               
         SAM31                                                                  
*                                                                               
         SAC   0                                                                
         TM    SSBSTAT5,SSB5WLM    WLM INSTALLED?                               
         BZ    ADF23               NO                                           
*NOP*    CLI   TORFLAG,C'Y'        IS THIS A TOR ?                              
*NOP*    BNE   ADF23               NO,BYPASS WLM CONNECT                        
         GOTO1 VWLMCT,WLMPARMS,C'WLMC'                                          
*                                                                               
ADF23    L     R2,VABENDPC         SET MVS ESPIE EXIT                           
         ESPIE SET,(2),((1,7),9,11,12,15)                                       
*                                                                               
         L     R2,VABENDES         SET MVS ESTAE EXIT                           
         ESTAE (2),CT,ASYNCH=YES,TERM=NO                                        
*                                                                               
         L     R7,VABENDIT         SET INTERVAL TIMER EXIT                      
         L     R5,VABNDITR                                                      
         GOTO1 VTICTOC,PARM,C'XSET',(R7),(R5),TO24=Y                            
         SAM31                                                                  
*                                                                               
         MVI   MQIOFLAG,0          SET MQIO FLAG TO NOT ACTIVE                  
         ICM   R5,15,SSBAATC                                                    
         BZ    ADF26                                                            
*                                                                               
         LH    R2,0(R5)                                                         
         L     R3,2(R5)                                                         
         LA    R5,6(R5)                                                         
         USING ATCD,R5             R5=A(ATC)                                    
         ST    RA,ATCSYSFC                                                      
         CLI   SSBJESIO,C' '       ATTACH JES SUBMIT FACILITY                   
         BNH   ADF24                                                            
*                                                                               
         LA    R4,SSBJESIO                                                      
         LA    R6,ATCOSECB                                                      
         ATTACH EPLOC=(R4),PARAM=((R5)),ECB=(R6),SZERO=YES                      
         ST    R1,ATCOSTCB         SAVE A(OPERATING SYSTEM TCB)                 
         LA    R5,0(R2,R5)         BUMP A(ATCD)                                 
         ST    RA,ATCSYSFC                                                      
*                                                                               
ADF24    CLI   SSBSMTP,C' '        ATTACH JES MAIL FACILITY                     
         BNH   ADF25                                                            
         MVI   ATCTYPE,ATCTSMTP    SET SMTP TYPE                                
*                                                                               
         LA    R4,SSBSMTP                                                       
         LA    R6,ATCOSECB                                                      
         ATTACH EPLOC=(R4),PARAM=((R5)),ECB=(R6),SZERO=YES                      
         ST    R1,ATCOSTCB         SAVE A(OPERATING SYSTEM TCB)                 
         LA    R5,0(R2,R5)         BUMP A(ATCD)                                 
         ST    RA,ATCSYSFC                                                      
*                                                                               
ADF25    OC    SSBMQION,SSBMQION   TEST FOR MQSERIES FACILITY                   
         BZ    ADF26                                                            
*NOP     CLI   TORFLAG,C'Y'                                                     
*NOP     BNE   ADF26                                                            
         MVI   MQIOFLAG,X'FF'      SET MQIO FLAG TO ACTIVE                      
         MVI   ATCTYPE,ATCTMQIO                                                 
         MVC   ATCARECB,VMQIOECB                                                
         MVC   ATCAIOPL,VMQIOLST                                                
*                                                                               
         LA    R4,SSBMQIO                                                       
         LA    R6,ATCOSECB         ATTACH MQIO SCHEDULER                        
         ATTACH EPLOC=(R4),PARAM=((R5)),ECB=(R6),SZERO=YES                      
         ST    R1,ATCOSTCB         SAVE A(OPERATING SYSTEM TCB)                 
*                                                                               
ADF26    L     R7,VSELIST          POINT TO SE 1 ENTRY                          
         LA    R7,6(R7)                                                         
         ST    R7,LASTSE                                                        
*                                                                               
         L     R7,VTCB             POINT TO TASK 1 ENTRY                        
         LA    R7,6(R7)                                                         
         ST    R7,LASTTCB                                                       
         CLI   TORFLAG,C'Y'        VTAM WAIT FOR TOR ONLY                       
         BNE   ADF28                                                            
         GOTO1 VLCM,PARM,VTWAIT                                                 
*                                                                               
         OC    SSBT2,SSBT2         TEST TIMER 2 ACTIVE                          
         BZ    ADF28                                                            
         OI    SSBSTAT1,SSBSVTAM   SET AWAITING VTAM                            
         LA    R7,1                                                             
         GOTO1 VTICTOC,PARM,C'2SET',(R7),TO24=Y                                 
         SAM31                                                                  
         LA    R1,TIMERECB                                                      
         WAIT  1,ECB=(1)                                                        
*                                                                               
ADF28    L     RF,AADNEXT                                                       
         BR    RF                                                               
         DROP  R3,R5,R8,R9,RA                                                   
         EJECT                                                                  
***********************************************************************         
* EOJ PENDING - CLOSE DOWN VTAM AND DETACH SUB-TASKS                  *         
***********************************************************************         
ADLAST   NTR1  BASE=*,LABEL=*                                                   
         SAC   0                   YOU NEVER KNOW AROUND HERE                   
         USING TASKCNST,R9                                                      
         L     R9,ADLCNST                                                       
*                                                                               
         L     RA,VSYSFX                                                        
         USING SYSFACD,RA                                                       
         L     R8,VSSB                                                          
         USING SSBD,R8                                                          
*                                                                               
         BRAS  RE,ARSOFF                                                        
         LAM   AR2,AR2,SSBALET                                                  
         ICM   R2,15,SSBATOR                                                    
         SAC   512                                                              
         AHI   R2,TORFACLQ                                                      
         USING SBEXCHD,R2                                                       
         LH    R4,0(,R2)                                                        
         L     R5,2(,R2)                                                        
         LA    R2,6(,R2)                                                        
*                                                                               
         CLC   ASTOKEN,SBSTOKEN    IS THIS OUR ROW?                             
         BE    *+10                YES                                          
         BXLE  R2,R4,*-10                                                       
         DC    H'0'                BAD - WHERE IS OUR DSPACE ENTRY?             
*                                                                               
         MVI   SBSTRTD,SBNO        SET FACPAK NOT STARTED                       
         MVI   SBAVLBL,SBNO        SET FACPAK NOT AVAILABLE                     
         XC    SBSTOKEN,SBSTOKEN   CLEAR STOKEN                                 
         BRAS  RE,ARSOFF                                                        
         DROP  R2                                                               
*                                                                               
         CLI   TORFLAG,C'Y'        VTAM CLOSE FOR TOR ONLY                      
         BNE   ADLST02                                                          
         GOTO1 VLCM,PARM,VTCLOSE   CLOSE DOWN VTAM                              
*                                                                               
ADLST02  L     RF,VSSB             DETACH ANY ATTACHED SUB-TASKS                
         L     R5,SSBAATC-SSBD(RF)                                              
         LH    R6,0(R5)                                                         
         L     R7,2(R5)                                                         
         LA    R5,6(R5)                                                         
         USING ATCD,R5             R5=A(ATC)                                    
*                                                                               
ADLST04  TM    ATCSTAT1,ATCSATCH   TEST TASK ATTACHED                           
         BZ    ADLST08             NO                                           
         TM    ATCSTAT1,ATCSAACT   TEST TASK IS BUSY                            
         BNZ   ADLST06             YES - WAIT FOR TASK TO EXIT                  
         LA    R1,ATCECB                                                        
         POST  (1)                 POST COMPLETION TO FORCE EXIT                
*                                                                               
ADLST06  LA    R1,ATCOSECB         WAIT FOR O/S TO POST COMPLETION              
         WAIT  1,ECB=(1)                                                        
         LA    R1,ATCOSTCB         DETACH TASK                                  
         DETACH (1),STAE=NO                                                     
*                                                                               
ADLST08  BXLE  R5,R6,ADLST04                                                    
                                                                                
***********************************************************************         
* AFTER ALL TCBS HAVE BEEN DETACHED                                   *         
* MAKE SURE ZIP IS GONE                                               *         
* CANCEL ESTAE                                                        *         
* SWITCH TO PRIMARY ASC MODE                                          *         
* DISCONNECT FROM WORKLOAD MANAGER AND EXIT                           *         
***********************************************************************         
         MVC   ZIPDSPCE,SSBDSPAC                                                
         MVC   ZIPFAC(4),SSBSYSN4                                               
         XR    R1,R1                                                            
         LA    RE,ZIPMIN           MINOR NAME                                   
ADLST10  CLI   0(RE),C' '                                                       
         BNH   ADLST11                                                          
         AHI   R1,1                                                             
         LA    RE,1(RE)                                                         
         B     ADLST10                                                          
*                                                                               
ADLST11  TM    SSBSTAT4,SSBSAOR    IF AOR APEND AOR CHR TO MINOR NAME           
         BZ    ADLST12                                                          
         AHI   R1,1                                                             
         LLC   RF,SSBSYSIX                                                      
         SRL   RF,4                                                             
         LA    RF,X'C0'(RF)                                                     
         STC   RF,0(RE)                                                         
                                                                                
***********************************************************************         
* WAIT FOR ENQUEUE. WHEN IT COMES BACK MEANS ZIP IS DOWN.             *         
***********************************************************************         
ADLST12  STC   R1,ZIPMINL          SET MINOR NAME LENGTH                        
*                                                                               
         LA    R1,18(R1)                                                        
         STCM  R1,3,LOGREC                                                      
         MVC   LOGREC+2(26),=C'ZIPSHUT +SHUTDOWN ZIP.....'                      
         MVC   LOGREC+20(8),ZIPMIN+2                                            
         WTO   TEXT=LOGREC,MCSFLAG=HRDCPY                                       
*                                                                               
         ISGENQ REQUEST=OBTAIN,QNAME=ZIPMAJ,RNAME=ZIPMIN,              *        
               RNAMELEN=ZIPMINL,SCOPE=SYSTEM,CONTROL=EXCLUSIVE,        *        
               ENQTOKEN=TOKNTOKN,RESLIST=NO,RNL=NO,COND=YES,           *        
               CONTENTIONACT=WAIT,WAITTYPE=SUSPEND,RETCODE=15,RSNCODE=0         
*                                                                               
         LTR   RF,RF                                                            
         BZ    ADLST20                                                          
         CIJNE RF,4,*+2            ISGENQRC_WARN                                
         NILH  GR0,X'0000'                                                      
         CHI   R0,ISGENQRSN_UNPROTECTEDQNAME                                    
         JNE   *+2                                                              
*                                                                               
ADLST20  ESTAE 0                   CANCEL ESTAE                                 
         SAC   0                   PRIMARY ASC MODE                             
         TM    SSBSTAT5,SSB5WLM    WLM INSTALLED?                               
         BZ    ADLST30             NO                                           
*                                                                               
         GOTO1 VWLMDS,WLMPARMS,C'WLMD'                                          
         B     ADLST30                                                          
*                                                                               
ADLST30  TIME  TU                                                               
         ST    R0,TKFULL                                                        
*                                                                               
         TIMEUSED STORADR=SSBCPUSE,LINKAGE=SYSTEM,CPU=MIC                       
*                                                                               
         LG    GR1,SSBCPUSE        GR1 = CPU TOTAL                              
         LG    GRF,SSBCPUTK        GRF = CPU TASKS                              
         SGR   GR1,GRF             GR1 = CPU SYSTEM                             
         LG    GRF,SSBCPUDF        GRF = PREVIOUS CPU SYS                       
         STG   GR1,SSBCPUDF        SAVE GR1 AS NEW PREVIOUS                     
         SGR   GR1,GRF             PRV-CPU TO GET CURRENT SYS USED              
         JNM   *+6                                                              
         XR    R1,R1                                                            
*                                                                               
         LA    RF,LOGREC                                                        
         USING ADRRECD,RF                                                       
         XC    ADRREC,ADRREC                                                    
         MVI   ADRSYSNO,1                                                       
         MVI   ADROVSYS,1                                                       
         MVI   ADRPRGNO,4                                                       
         MVI   ADRTASK,C'1'                                                     
         MVC   ADRDAYNO,SSBDAYNO                                                
         MVC   ADRSYSIX,SSBSYSIX                                                
         MVC   ADRINTM,TKFULL                                                   
         MVC   ADRSTTM,TKFULL                                                   
         MVC   ADRNDTM,TKFULL                                                   
         MVC   ADRSYM,=C'OVERHEAD'                                              
         MVC   ADRCPUSE,SSBCPUSE                                                
         ST    R1,ADRCPUTK                                                      
         GOTO1 VLOGGR,LOGREC                                                    
         J     EXIT                                                             
         DROP  RF                                                               
         EJECT                                                                  
*                                                                               
ADLCNST  DC    A(TASKCNST)                                                      
ZIPMAJ   DC    CL8'DDSENQ'                                                      
ZIPMINL  DC    AL1(L'ZIPMINL)                                                   
ZIPMIN   DC    C'?.ZIPADV  '                                                    
         ORG   ZIPMIN                                                           
ZIPDSPCE DS    C                   DSPACE                                       
         DS    CL4                 .ZIP                                         
ZIPFAC   DS    CL5                 FACPAK NAME PLUS AOR ID                      
TOKNTOKN DC    CL32' '                                                          
         DROP  R5,R9,RA,RB                                                      
         EJECT                                                                  
***********************************************************************         
* TIMER POP ERROR PROCESSING                                          *         
***********************************************************************         
IOEXIT   NTR1  BASE=*,LABEL=*                                                   
         ST    RD,IOEXRD                                                        
         LA    RD,IOEXWRK                                                       
         L     R9,IOXCNST                                                       
         USING TASKCNST,R9                                                      
         L     RA,VSYSFX                                                        
         USING SYSFACD,RA                                                       
         DC    H'0'                                                             
*                                                                               
         GOTO1 VHEXOUT,PARM,ASIDFLD,IOXASID,2,0,0,TO24=Y                        
         SAM31                                                                  
         WTO   TEXT=IOXM1H                                                      
*                                                                               
         LA    R2,ECBLST           GET A(ECBLST)                                
IOX02    OC    0(4,R2),0(R2)                                                    
         BZ    IOEXITX                                                          
         GOTO1 VHEXOUT,PARM,0(R2),IOXMECB,4,TO24=Y                              
         SAM31                                                                  
         WTO   TEXT=IOXM2H                                                      
*                                                                               
         TM    0(R2),X'80'         END OF LIST                                  
         BO    IOEXITX                                                          
         AHI   R2,4                                                             
         B     IOX02                                                            
*                                                                               
IOEXITX  L     RD,IOEXRD                                                        
         B     EXITOK                                                           
*                                                                               
IOXCNST  DC    A(TASKCNST)                                                      
IOEXRD   DS    F                                                                
IOEXWRK  DS    40D                                                              
*                                                                               
IOXM1H   DC    AL2(75)                                                          
         DC    C'FACPAK POST PROBLEM - ASID='                                   
IOXASID  DC    C'XXXX'                                                          
*                                                                               
IOXM2H   DC    AL2(75)                                                          
         DC    C'ECB DETECTED - ADDRESS='                                       
IOXMECB  DC    C'XXXXXXXX'                                                      
         DROP  R9,RA,RB                                                         
         EJECT                                                                  
***********************************************************************         
* POST ERROR HANDLING ROUTINE - ASSUME AOR HAS ABENDED                *         
***********************************************************************         
FACDWN   NTR1  BASE=*,LABEL=*      TURN OFF AOR DOWN FLAG                       
         L     R9,FDCNST                                                        
         USING TASKCNST,R9                                                      
         L     RA,VSYSFX                                                        
         USING SYSFACD,RA                                                       
         NI    ABNDECB,255-POSTED  TURN OFF AOR DOWN FLAG                       
         GOTO1 VTORRCV,TO24=Y      RECOVER ABENDING AOR TASKS                   
         SAM31                                                                  
         B     EXITOK                                                           
*                                                                               
FDCNST   DC    A(TASKCNST)                                                      
         DROP  R9,RA,RB                                                         
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO OUTPUT 'RAN OUT OF UTL ENTRIES' MESSAGE                  *         
***********************************************************************         
NOUTL    NTR1  BASE=*,LABEL=*                                                   
         L     R9,NUCNST                                                        
         USING TASKCNST,R9                                                      
         L     RA,VSYSFX                                                        
         USING SYSFACD,RA                                                       
         CLI   NUFLAG,C'N'                                                      
         BNE   EXIT                                                             
*                                                                               
         GOTO1 VTICTOC,DUB,C'SSET'                                              
*                                                                               
         MVI   NUFLAG,C'Y'                                                      
         L     RF,VSSB                                                          
         MVC   NUMSG+4(4),SSBSYSN4-SSBD(RF)                                     
*                                                                               
NOU02    XC    NUECB,NUECB                                                      
         XC    NUREPLY,NUREPLY                                                  
         WTOR  TEXT=(NUMSGH,NUREPLY,L'NUREPLY,NUECB)                            
         WAIT  ECB=NUECB                                                        
*                                                                               
         CLC   NUREPLY(3),=C'EOJ'  TEST REQUEST FOR EOJ                         
         BNE   NOU04                                                            
         L     RF,VSSB                                                          
         OI    SSBSTAT1-SSBD(RF),SSBSEOJ                                        
         B     EXIT                                                             
*                                                                               
NOU04    CLC   NUREPLY(6),=C'IGNORE' IGNORE LACK OF UTLS FOR S/RS               
         BE    EXIT                                                             
         B     NOU02                                                            
*                                                                               
NUECB    DC    F'0'                                                             
NUFLAG   DC    C'N'                                                             
NUREPLY  DC    CL8' '                                                           
NUMSGH   DC    AL2(50)                                                          
NUMSG    DC    C'*FACXXX* WARNING NO UTLS FREE - IGNORE OR EOJ'                 
*                                                                               
NUCNST   DC    A(TASKCNST)                                                      
         DROP  R9,RA,RB                                                         
         EJECT                                                                  
***********************************************************************         
* TASKER - USEFUL LITTLE ROUTINES - R9 ADDRESSES ALL PAST HERE        *         
***********************************************************************         
         DS    0D                                                               
TASKCNST DC    CL8'TASKCNST'                                                    
         USING TASKCNST,R9                                                      
*                                                                               
EXITOK   CR    RB,RB                                                            
         B     EXIT                                                             
*                                                                               
EXITL    CLI   *,255                                                            
         B     EXIT                                                             
*                                                                               
EXIT     XIT1                                                                   
*                                                                               
EXITR7   XIT1  REGS=(R7)                                                        
*                                                                               
ARSOFF   SAC   0                                                                
         LAM   AR0,ARF,ARZERO                                                   
         BR    RE                                                               
*                                                                               
RBLDCORE NTR1                                                                   
         L     RA,VSYSFX                                                        
         USING SYSFACD,RA                                                       
         GOTO1 VCALLOV,PARM,0,(C'!',0),0,TO24=Y                                 
         GOTO1 (RF),(R1),0,(C'D',0),0,TO24=Y                                    
         SAM31                                                                  
         L     RE,VSSB                                                          
         NI    SSBSTAT4-SSBD(RE),255-SSBNOGO                                    
         B     EXITOK                                                           
         DROP  RA                                                               
*                                                                               
         USING TCBD,R3                                                          
GETALET  SAC   0                   GET CORRECT ALET FOR TCBSVECB                
         LAM   AR0,ARF,ARZERO                                                   
         XR    R2,R2                                                            
         LAM   AR2,AR2,SSBALET     SET UP ACCESS REGISTER                       
         CLI   TCBSVECB,X'FD'      TEST TABS DATASPACE                          
         BNE   *+8                                                              
         LAM   AR2,AR2,SSBTBLET    SET UP ACCESS REGISTER                       
         CLI   TCBSVECB,X'FC'      TEST PGMS DATASPACE                          
         BNE   *+8                                                              
         LAM   AR2,AR2,SSBPGMTA    SET UP ACCESS REGISTER                       
         SAC   512                                                              
         BR    RE                                                               
         DROP  R3                                                               
*                                                                               
ADDTOLST XC    0(4,RF),0(RF)       ADD ECB AT 0(RF) TO LIST AT 0(R1)            
         ST    RF,0(R1)                                                         
         LA    R1,4(R1)                                                         
         BR    RE                                                               
         EJECT                                                                  
***********************************************************************         
* CONSTANTS AND WORKING STORAGE                                       *         
***********************************************************************         
FF       EQU   X'FF'                                                            
POSTED   EQU   X'40'                                                            
YES      EQU   C'Y'                                                             
NO       EQU   C'N'                                                             
*                                                                               
         DS    0D                                                               
ARZERO   DC    16F'0'              TO CLEAR ACCESS REGISTERS                    
*                                                                               
DUB      DS    D                                                                
CPUTIME  DS    D                   CPU USED TIME IN MICROSECS                   
PARM     DS    6F                                                               
*                                                                               
TFLIST   DS    4F                                                               
TFXAUTL  DS    F                                                                
SAVE1    DS    F                                                                
SAVE2    DS    F                                                                
SAVERE   DS    F                                                                
*                                                                               
PCMPLST  DS    4F                  PARAMETER LIST FOR LCM WRITE                 
PCMPRQ   DS    X                                                                
BYTE     DS    X                                                                
TASKMASK DS    X                   TOR/AOR APPLICATION RUN FLAG                 
FLIPFLOP DS    X                   LOAD BALANCE FOR GLOBAL FILE TEST            
*                                                                               
         DS    0D                                                               
         DC    CL8'*SMFSMF*'                                                    
SMFREC   DS    0X                    RECORD                                     
SMFIHDR  DS    0XL24                 IBM RECORD HEADER                          
SMFILEN  DC    AL2(SMFDATAX-SMFREC)  LENGTH                                     
SMFISEG  DC    XL2'0000'             SEGMENT DESCRIPTOR (SET TO ZERO)           
SMFIFLG  DC    XL1'D2'               FLAGS 80=SUBSYS,40=SUBTYP,12=VS2           
SMFIRTY  DC    AL1(METSMF)           RECORD TYPE = 249                          
SMFITME  DC    XL4'00'               TIME IN 1/100 SEC                          
SMFIDTE  DC    PL4'0'                DATE P'0CYYMMMF'                           
SMFISID  DC    XL4'00'               SYSTEM ID                                  
SMFISSI  DC    CL4'    '             SUBSYSTEM ID                               
SMFISTY  DC    AL2(METSTSK)          SUBTYPE = 6                                
SMFIHDRX EQU   *                                                                
*                                                                               
SMFDMAP  DS    0XL8                  MAP OF DDS RECORD DATA                     
SMFDHDRO DC    AL2(SMFDHDR-SMFREC)   OFFSET TO DDS HEADER                       
SMFDHDRL DC    AL2(SMFDHDRX-SMFDHDR) LENGTH OF DDS HEADER                       
SMFDATAO DC    AL2(SMFDATA-SMFREC)   OFFSET TO DDS DATA 1 AREA                  
SMFDATAL DC    AL2(SMFDATAX-SMFDATA) LENGTH OF DDS DATA 1 AREA                  
SMFDMAPX EQU   *                                                                
*                                                                               
SMFDHDR  DS    0CL32                 DDS HEADER                                 
SMFDJOB  DC    CL8' '                JOB NAME                                   
SMFDSTEP DC    CL16' '               STEP/PROC NAME                             
SMFDJES  DC    CL8' '                JES NAME                                   
SMFDHDRX EQU   *                                                                
*                                                                               
SMFDATA  DC    XL(METLNSQ)'00'       SMF DATA                                   
SMFDATAX EQU   *                                                                
*                                                                               
SMFERR   DC    F'0'                  SMF RETURN CODE                            
*                                                                               
         DS    0A                                                               
WLMPARMS DS    CL(WLMLNQ)          WLM CONNECT/DISCONNECT                       
         DS    0A                                                               
ELCVPARM DS    CL(ECLVWNQ)         JOIN/LEAVE ENCLAVE STORAGE                   
         DS    0D                                                               
BYTES8   DS    PL8                                                              
FWORD    DS    XL4                                                              
*                                                                               
VWRTOUT  DC    V(WRTOUT)                                                        
VTORRCV  DC    V(TORRCV)                                                        
VAORSLT  DC    V(AORSLT)                                                        
VSYSFX   DC    V(SYSFAC)                                                        
VMONITR  DC    V(MONITOR)                                                       
VSCRNCH  DC    V(SCRUNCH)                                                       
VLOGGR   DC    V(LOGGER)                                                        
VBILLIT  DC    V(BILLIT)                                                        
VMSGQIN  DC    V(MSGQIN)                                                        
VMQIO    DC    V(MQIO)                                                          
VCHAIN   DC    V(CHAIN)                                                         
VCRAPPER DC    V(CRAPPER)                                                       
VHEXOUT  DC    V(HEXOUT)                                                        
VRPLLST  DC    V(RPLLST)                                                        
VLCMECB  DC    V(LCMECB)                                                        
VMQIOECB DC    V(MQIOECB)                                                       
VMQIOLST DC    V(MQIOLST)                                                       
VMNBRPRG DC    V(MONBRPRG)                                                      
VSELIST0 DC    V(SELIST0)                                                       
VSELISTC DC    V(SELISTCT)                                                      
VAORNUM  DC    V(AORNUM)                                                        
VABENDPC DC    V(ABENDPC)                                                       
VABENDES DC    V(ABENDES)                                                       
VABENDIT DC    V(ABENDIT)                                                       
VABNDWHY DC    V(ABENDWHY)                                                      
VABNDITR DC    V(ABENDITR)                                                      
VSHIPFIX DC    V(SHIPFIX)                                                       
VWLMCT   DC    V(FAWLMCT)                                                       
VWLMDS   DC    V(FAWLMDS)                                                       
VWLMCE   DC    V(FAWLMCE)                                                       
VWLMDE   DC    V(FAWLMDE)                                                       
VWLMJN   DC    V(FAWLMJN)                                                       
VWLMLV   DC    V(FAWLMLV)                                                       
VWLMQE   DC    V(FAWLMQE)                                                       
*                                                                               
ATSKWK   DC    A(TASKWORK)                                                      
AADNEXT  DC    A(ADNEXT)                                                        
AADLAST  DC    A(ADLAST)                                                        
TKFULL   DS    F                                                                
*                                                                               
TORFLAG  DC    X'00'                                                            
MODE     DS    X                                                                
DIRECT   DS    X                                                                
MQIOFLAG DS    X                                                                
*                                                                               
MINUSONE DC    F'-1'                                                            
OLDWAIT  DC    A(0)                                                             
T0SEC    DC    A(0)                0 SECONDS IN TUS                             
T1TENTH  DC    A(3840)             1/10 SECONDS IN TUS                          
T1SEC    DC    A(38400)            1 SECOND IN TUS                              
IOECB    DC    AL1(NO)                                                          
*                                                                               
MAXMAXIO DC    A(FAMAXMIO)         EXTENDED MAX NUM OF I/O'S PER TASK           
ACCMAXIO DC    A(FAMAXAIO)         ACCOUNT  MAX NUM OF I/O'S PER TASK           
TALMAXIO DC    A(FAMAXAIO)         TALENT   MAX NUM OF I/O'S PER TASK           
MAXTKWTS DC    A(1000)             MAX TKWAIT POPS ALLOWED                      
MIDNIGHT DC    XL4'C5C10000'       TU ADJUSTMENT FACTOR FOR MIDNIGHT            
*                                                                               
TBKLIST  DS    4F                  FOR TORBACK ROUTINE                          
TBKBUFF  DS    A                                                                
TBKUXAD  DS    A                                                                
TBKASYS  DS    AL3                                                              
TBKARSYS DS    AL3                                                              
*                                                                               
$SYSNOP  DC    X'0103'                                                          
$T2      DC    X'0104'                                                          
$SUB     DC    X'0107'                                                          
$EXT     DC    X'010A'                                                          
$VTL     DC    X'010C'                                                          
$WKR     DC    X'010D'                                                          
$OPS     DC    X'010F'                                                          
$DISP    DC    X'0117'                                                          
$LOAD    DC    X'0118'                                                          
$SCR     DC    X'011F'                                                          
$CAN     DC    X'01FC'                                                          
$DONE    EQU   X'12'                                                            
$EOJ     DC    X'0113'                                                          
$HELP    DC    X'0109'                                                          
$TEST    DC    X'0136'                                                          
$BC      DC    X'0144'                                                          
*&&US                                                                           
$SPTDARE DC    X'0161'                                                          
$REPDARE DC    X'0162'                                                          
$MKGDARE DC    X'0163'                                                          
*&&                                                                             
$TOR     DC    X'016D'                                                          
$$BYE    DC    X'01EE'                                                          
$UNWIND  DC    X'01FB'                                                          
$ABEND   DC    X'01FF'                                                          
*                                                                               
TORWAKE  DC    XL4'FFFFFFFF'       TOR POST ADDRESS IDENTIFIER                  
*                                                                               
         DS    0D                                                               
         DC    CL8'ADFIRREG'                                                    
ADFIRREG DS    2D                  RETURN REGISTERS RB,RC,RD,RE                 
*                                                                               
         DS    0D                                                               
         DC    CL16'**TRACE**TRACE**'                                           
TRACERTN DS    CL8                                                              
TRACEDMP DS    CL8                                                              
TRACEPOP DS    F                                                                
TRACETIM DS    F                                                                
*                                                                               
LOGREC   DS    CL80                                                             
*                                                                               
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
* THIS SMALL TABLE IS FOR AUTOMATICS - FORMAT IS AS FOLLOWS:          *         
* XL1-S/R NUMBER (EG $TIM IS T10400 SO NUMBER IS 04)                  *         
* CL1-Y/N Y=CURRENTLY ACTIVE E  N=NOT                                 *         
* XL4-COUNT OF CALLS TODAY TO THIS S/R                                *         
* XL4-TIME OF LAST CALL                                               *         
* XL6-N/D                                                             *         
***********************************************************************         
         ORG   ADNEXT+(((*-ADNEXT+15)/16)*16)                                   
         DC    CL16'AUTOSR**AUTOSR**'                                           
AUTOSRTB DS    0XL16                                                            
TIMACTV  DC    X'04',C'N',XL14'00'       $TIM                                   
OPSACTV  DC    X'0F',C'N',XL14'00'       $OPS                                   
EXTACTV  DC    X'0A',C'N',XL14'00'       $EXT                                   
SCRACTV  DC    X'1F',C'N',XL14'00'       $SCR                                   
WKRACTV  DC    X'0D',C'N',XL14'00'       $WKR                                   
VTLACTV  DC    X'0C',C'N',XL14'00'       $VTL                                   
*                                                                               
*&&UK*&& DC    X'00'                                                            
*&&US                                                                           
RDRACTV  DC    X'62',C'N',XL14'00'       $REPDARE                               
SPTACTV  DC    X'61',C'N',XL14'00'       $SPTDARE                               
MKGACTV  DC    X'63',C'N',XL14'00'       $MKGDARE                               
         DC    X'00'                                                            
*&&                                                                             
         EJECT                                                                  
***********************************************************************         
* ECBS WITH IDENTIFIERS FOR DUMP ANALYSIS                             *         
***********************************************************************         
SPRTSK   DC    A(0),C'=SPRTSK*****' A(SPARE TASK) FOR SROPS IN AOR              
LASTSE   DC    A(0),C'=LASTSE*****' A(LAST SE ENTRY TO COMPLETE)                
LASTTCB  DC    A(0),C'=LASTTCB****' A(LAST TCB POSTED COMPLETE)                 
*                                                                               
TIMERECB DC    A(0),C'=TIMERECB***'                                             
DSPACECB DC    A(0),C'=DSPACECB***'                                             
DSOPSECB DC    A(0),C'=DSOPSECB***'                                             
POSTECB  DC    A(0),C'=POSTECB****'                                             
ABNDECB  DC    A(0),C'=ABNDECB****'                                             
POSTRECB DC    A(0),C'=POSTRECB***'                                             
*                                                                               
AALET    DC    A(0),C'=AALET******'                                             
TSTOKEN  DC    D'0',C'=TSTOKEN'                                                 
ASTOKEN  DC    D'0',C'=ASTOKEN'                                                 
ASIDFLD  DC    D'0',C'=ASIDFLD'                                                 
ASCBAD   DC    A(0),C'=ASCB ADDR  '                                             
*                                                                               
         DC    C'*ECBLST**ECBLST*'                                              
ECBLST   DC    XL256'00'                                                        
         DC    XL256'00'                                                        
ECBLSTLQ EQU   *-ECBLST                                                         
         DC    C'*ECBLSTX*ECBLSTX'                                              
*                                                                               
         DS    0D                                                               
         DC    CL16'*UTLZERO*UTLZERO'                                           
UTLZERO  DC    (TUTLXALN)X'00'                                                  
         ORG   UTLZERO+TSYM-UTLD                                                
         DC    CL8'NONENONE'                                                    
         ORG   UTLZERO+TTYPE2-UTLD                                              
         DC    AL1(TTYPEDUM)       DEFINE AS DUMMY TERMINAL                     
         ORG                                                                    
*                                                                               
         DS    0D                                                               
         DC    CL8'*SVCDUM*'                                                    
SVCDUM   DC    CL7'SVCDUM '        PGMNAME                                      
         DC    AL1(PGMIRFU)        PGMIND                                       
         DC    AL1(0,0)            PGMINUM,PGMCOSYS                             
         DC    AL1(0,PGMISWT)      PGMPRTY,PGMIND2                              
         DC    AL1(0,0,0,0)        PGMTSKMX,PGMTSK,PGMALNUM,PGMCTRY             
         DC    AL2(0)              PGMTEXT                                      
         DC    AL1(PGMIRTOR)       PGMIND3                                      
         DC    AL1(0),XL4'00'      PGMIND4,PGMCNT1                              
         DC    AL1(0),AL3(0)       N/D,PGMAGYLA                                 
         DC    XL14'00'            PGMVINFO                                     
         DC    XL32'00'            SPARE FOR PGMLST EXPANSION                   
*                                                                               
NOTORMSG DC    AL2(0070)                                                        
NOTOR    DC    CL60'*FACPAK* TOR FACPAK UNAVAILABLE - FACPAK ABENDING'          
RCMSG    DC    CL10'(RF)=XXXX'                                                  
*                                                                               
RESET    DC    CL8'RESET   '                                                    
INPUT    DC    CL8'INPUT   '                                                    
END      DC    CL8'END     '                                                    
*                                                                               
         DS    0D                                                               
         DC    CL8'ADFCARD*'                                                    
ADFCARD  DC    XL48'00'                                                         
*                                                                               
         ORG   ADNEXT+(((*-ADNEXT+15)/16)*16)                                   
         DC    CL16'TASKWORKTASKWORK'                                           
TASKWORK DS    512D                                                             
         DS    512D                                                             
         DC    CL16'TASKWRKXTASKWRKX'                                           
         EJECT                                                                  
***********************************************************************         
* INCLUDED BOOKS                                                      *         
***********************************************************************         
*FAATC                                                                          
       ++INCLUDE FAATC                                                          
*FAPGMLST                                                                       
       ++INCLUDE FAPGMLST                                                       
*FARPLD                                                                         
       ++INCLUDE FARPLD                                                         
*FASELIST                                                                       
       ++INCLUDE FASELIST                                                       
*FASSB                                                                          
       ++INCLUDE FASSB                                                          
*FATBHD                                                                         
       ++INCLUDE FATBHD                                                         
*FATCB                                                                          
       ++INCLUDE FATCB                                                          
*FAUTL                                                                          
       ++INCLUDE FAUTL                                                          
*FASYSFAC                                                                       
       ++INCLUDE FASYSFAC                                                       
*DMDSHDR                                                                        
       ++INCLUDE DMDSHDR                                                        
*FATABSD                                                                        
       ++INCLUDE FATABSD                                                        
*FATABSCOM                                                                      
       ++INCLUDE FATABSCOM                                                      
*DMDTFPH                                                                        
       ++INCLUDE DMDTFPH                                                        
*DMDTFIS                                                                        
       ++INCLUDE DMDTFIS                                                        
*FAPIGFACD                                                                      
       ++INCLUDE FAPIGFACD                                                      
*DDZIPBLK                                                                       
       ++INCLUDE DDZIPBLK                                                       
*FAWLMD                                                                         
       ++INCLUDE FAWLMD                                                         
*FAWLMED                                                                        
       ++INCLUDE FAWLMED                                                        
*DDSMFMETD                                                                      
       ++INCLUDE DDSMFMETD                                                      
*FAADRREC                                                                       
       ++INCLUDE FAADRREC                                                       
*                                                                               
         CVT     DSECT=YES                                                      
         IAZJSAB DSECT=YES                                                      
         IEESMCA                                                                
         IEFTIOT1                                                               
         IHAASCB                                                                
         IHAASSB                                                                
         IHAPSA                                                                 
         IHASTCB                                                                
         IKJTCB                                                                 
         ISGYCON                                                                
         ISGYENQ                                                                
*                                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'013FATASKER  03/23/19'                                      
         END                                                                    
