*          DATA SET ACPRORATA  AT LEVEL 022 AS OF 10/31/08                      
*CATALP PRORATA                                                                 
PRORATA TITLE 'CALCULATE BILLING RATES FROM A TRANSACTION'                      
*                                                                               
***********************************************************************         
* PLIST FOR PRORATA                                                   *         
* P1 BYTE0    =X'80' FOR OLD FILE STRUCTURE                           *         
* P1 BYTES1-3 =A(TRANSACTION RECORD)                                  *         
* P2 BYTE0    CLIENT CODE LENGTH                                      *         
* P2 BYTES1-3 =A(GOBLOCK) SET WITH W/C LEVEL VALUES                   *         
* P3=A(COMFACS)                                                       *         
* P4=A(EXCHANGE RULE)                                                 *         
* P5=A(OUTPUT BLOCK)                                                  *         
* P6=A(OUTPUT PTAEL LIST)                                             *         
***********************************************************************         
PRORATA  CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 PROWRKX-PROWRKD,**PROR**,RA,CLEAR=YES,RR=RE                      
         USING PROWRKD,RC          RC=A(LOCAL W/S)                              
         ST    RE,PRORELO          SAVE RELO FACTOR                             
         MVI   PROSPACE,C' '       SPACES                                       
         MVC   PROSPACE+1(L'PROSPACE-1),PROSPACE                                
         LR    R8,R1               R8=A(PARAMETER BLOCK)                        
*                                                                               
         USING PROIBLKD,R8                                                      
         TM    PROISTAT,PROIWRIQ   WRITEING PTA ELS BACK TO TRAN                
         BO    PRO05               YES LEAVE BUFFER ALONE                       
         ICM   RF,15,PROIAPTA                                                   
         BZ    *+8                                                              
         MVI   0(RF),0             SET E-O-LIST                                 
         ST    RF,PROAPTAN         SAVE A(FIRST SLOT)                           
*                                                                               
PRO05    SR    RF,RF                                                            
         ICM   RF,15,PROIACMF      RF=A(COMFACS)                                
         BNZ   *+6                                                              
         DC    H'0'                A(COMFACS) MISSING IN PARM BLOCK             
         USING COMFACSD,RF                                                      
         MVC   PROGETFT,CGETFACT                                                
         MVC   PRODATCN,CDATCON                                                 
         MVC   PROHELLO,CHELLO                                                  
         DROP  RF                                                               
*                                                                               
         USING TRNRECD,R2                                                       
         ICM   R2,15,PROIATRN      R2=A(TRANSACTION RECORD)                     
         MVC   PROFILE,=CL8'ACCMST '                                            
         LA    R3,TRNRFST          R3=A(FIRST ELEMENT ON NEW FILE)              
*                                                                               
         TM    PROISTAT,PROIOLDQ   OLD FILE STRUCTURE?                          
         BNO   PRO25               NO                                           
         MVC   PROFILE,=CL8'ACCFILE'                                            
         LA    R3,ACCORFST                                                      
         AR    R3,R2               R3=A(FIRST ELEMENT ON OLD FILE)              
*                                                                               
PRO25    CLI   0(R3),TRNELQ        TRANSACTION ELEMENT?                         
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
*&&US                                                                           
         TM    PROISTAT,PROIWRIQ   WANT TO WRITE PTA ELS BACK TO TRAN           
         BNO   *+12                NO, CONTINUE                                 
         BAS   RE,WRIPTA                                                        
         B     PROX                                                             
*&&                                                                             
*                                                                               
         ICM   R4,15,PROIAOUT      R4=A(OUTPUT BLOCK)                           
         USING PROBLKD,R4                                                       
         XC    PG$GEN(PA$VALS-PROBLKD),PG$GEN                                   
         LH    R0,=Y((PR$LN1Q+PR$LN2Q+PR$LN3Q+PR$LN4Q)/L'PA$VALS)               
         LA    RF,PA$VALS                                                       
         ZAP   0(L'PA$VALS,RF),=P'0' INITIALISE OUTPUT ACCUMULATORS             
         LA    RF,L'PA$VALS(RF)                                                 
         BCT   R0,*-10                                                          
*                                                                               
         ZAP   PA$NET,TRNAMNT-TRNELD(L'TRNAMNT,R3) TRANS NET AMOUNT             
         MVC   PROTSTAT,TRNSTAT-TRNELD(R3)         SAVE STATUS BYTE             
*&&US                                                                           
         TM    PROTSTAT,TRNSNOCM   NON COMMISSIONABLE ITEM                      
         BO    PRO30                                                            
*&&                                                                             
         ZAP   PRODUB,=P'0'                                                     
         ICM   RF,15,PROIAGOB      RF=A(GOBLOCK AT WC LEVEL)                    
         BZ    PRO30                                                            
         ZAP   PRODUB,GOAGYCOM-GOBLOCKD(L'GOAGYCOM,RF) COMM (4 DEC PLC)         
         ZAP   PROPL16,PA$NET                                                   
*&&US*&& AP    PROPL16,PA$DSC      ADD BACK CASH DISCOUNT                       
         SRP   PROPL16,2,0         INCREASE NET BILL TO 4 DEC PLACES            
         MP    PROPL16,PRODUB      APPLY COMISSION RATE                         
         SRP   PROPL16,64-8,5      DIV BY 100 AND ROUND TO 2 DEC PLACES         
         ZAP   PA$NLCOM,PROPL16    NOTIONAL COMMISSION                          
*                                                                               
PRO30    CLI   0(R3),EOR           END OF RECORD?                               
         BE    PRO200                                                           
         CLI   0(R3),BNDELQ        BILL NUMBER/DATE ELEMENT?                    
         BE    PRO50                                                            
         CLI   0(R3),SCIELQ        SUBSIDIARY CASH ELEMENT?                     
         BE    PRO70                                                            
         CLI   0(R3),OAMELQ        ORDER AMOUNT ELEMENT?                        
         BE    PRO80                                                            
         CLI   0(R3),PTAELQ        PROD TRANSACTION ACTIVITY ELEMENT?           
         BE    PRO90                                                            
         CLI   0(R3),TRXELQ        PROD EXTRA STATUS ELEMENT?                   
         BE    PRO150                                                           
*&&US                                                                           
         CLI   0(R3),CUBELQ        CLIENT UNBILLING ELEMENT                     
         BNE   *+8                                                              
         BAS   RE,CNVCUB                                                        
         CLI   0(R3),PRTELQ        PERSONNEL RATE ELEMENT                       
         BE    PRO85                                                            
*&&                                                                             
*                                                                               
PRO40    SR    R0,R0               BUMP TO NEXT ELEMENT                         
         IC    R0,1(,R3)                                                        
         AR    R3,R0                                                            
         B     PRO30                                                            
*                                                                               
         USING BNDELD,R3           BILL NUMBER/DATE ELEMENT                     
PRO50    DS    0H                                                               
*&&US                                                                           
         OC    BNDAMNT,BNDAMNT     ALLOCATED AMOUNT PRESENT ?                   
         BNZ   PRO54               YEP                                          
         SR    RF,RF               CLEAR                                        
         LR    RE,R3                                                            
PRO52    CLI   0(RE),0             END OF RECORD                                
         BE    PRO54                                                            
         CLI   0(RE),TRSELQ        X'60', FIND USED DATE                        
         BE    PRO53                                                            
         IC    RF,1(,RE)           NEXT ELEMENT                                 
         AR    RE,RF                                                            
         B     PRO52                                                            
*                                                                               
         USING TRSELD,RE                                                        
PRO53    OC    TRSUDAT,TRSUDAT     CHECK USED DATE                              
         BZ    PRO54               PROBABLY NOT BILLED                          
         CLC   TRSUDAT,=X'B8B2'    MAY18/92 OR OLDER ONLY                       
         BH    PRO54               AMOUNT NEEDS TO BE IN BNDAMNT                
         DROP  RE                                                               
*                                                                               
         USING TRNELD,RE                                                        
         LA    RE,TRNRFST                                                       
         TM    PROISTAT,PROIOLDQ   TEST OLD FILE STRUCTURE                      
         BNO   *+8                                                              
         LA    RE,TRNRECD+ACCORFST                                              
         ZAP   PRODUB,TRNAMNT      MUST PUT ON BOUNDRY FOR CVB                  
         CVB   RF,PRODUB                                                        
         STCM  RF,15,BNDAMNT       SAVE OFF AMOUNT                              
         DROP  RE                                                               
*&&                                                                             
PRO54    BAS   RE,CNVBND           CONVERT TO PTA FORMAT                        
         TM    PG$STAT,PG$FULLB    FULLY BILLED?                                
         BO    PRO40                                                            
         CLC   BNDBNO,PROSPACE     ALLOCATED BUT NOT BILLED?                    
*&&UK*&& BNH   PRO40                                                            
*&&US*&& BL    PRO40                                                            
*&&US*&& BE    PRO57                                                            
         CLI   BNDBNO,C'#'         GERMAN MULTI-BILL INDICATOR?                 
         BE    PRO40                                                            
         OI    PG$STAT,PG$OLDA     OLD ALLOCATION TYPE TRANSACTION              
*                                                                               
         CLC   PG$LBLDT,BNDDTE     LATEST BILL DATE?                            
         BH    *+10                                                             
         MVC   PG$LBLNO(L'PG$LBLNO+L'PG$LBLDT),BNDBNO BILL NO + DATE            
*                                                                               
         ICM   RF,15,BNDAMNT       BILLED AMOUNT                                
*&&UK                                                                           
         BZ    PRO60               IF ZERO, FULLY BILLED (UK ONLY)              
         ICM   RE,15,PROALAMT                                                   
         AR    RE,RF               OTHERWISE ADD ON TO ALLOC TOTAL              
         STCM  RE,15,PROALAMT                                                   
*&&                                                                             
*&&US                                                                           
         CVD   RF,PRODUB           SAVE BILLED AMOUNT                           
         AP    PA$NETBL,PRODUB     ACCUMULATE TRANSACTION TOTAL                 
         TM    BNDCMST,BNDCPACK    TEST COMMISSION RATE IS PACK DEC             
         BO    PRO55                                                            
         OC    BNDCMP+2(2),BNDCMP                                               
         BNZ   PRO55               MUST BE PACKED                               
         SR    RF,RF                                                            
         ICM   RF,3,BNDCMP                                                      
         CVD   RF,PRODUB2                                                       
         B     *+10                                                             
PRO55    ZAP   PRODUB2,BNDCMP      COMMISSION RATE                              
*&&US                                                                           
         XC    PROPL16,PROPL16                                                  
         ICM   RE,15,BNDCSD                                                     
         CVD   RE,PROPL16+8        CASH DISCOUNT BILLED                         
         AP    PROPL16,PRODUB      PLUS NET BILLED                              
*&&                                                                             
*&&UK*&& ZAP   PROPL16,PRODUB      NET BILLED                                   
         MP    PROPL16,PRODUB2     TIMES COMM RATE                              
         SRP   PROPL16,64-4,5                                                   
         AP    PA$COMBL,PROPL16    COMMISSION BILLED                            
*                                                                               
         ICM   RE,15,BNDCSD                                                     
         CVD   RE,PRODUB                                                        
         AP    PA$DSCB,PRODUB      CASH DISC BILLED                             
         ICM   RE,15,BNDHRS                                                     
         CVD   RE,PRODUB                                                        
         AP    PA$HRSB,PRODUB      HOURS BILLED                                 
*                                                                               
*&&                                                                             
         B     PRO40                                                            
*&&US                                                                           
PRO57    ICM   RF,15,BNDAMNT                                                    
         CVD   RF,PRODUB                                                        
         AP    PP$AALLO,PRODUB     ADD TO ALLOCATIONS                           
*                                                                               
*&&US                                                                           
         XC    PROPL16,PROPL16                                                  
         ICM   RF,15,BNDCSD        DISCOUNT                                     
         CVD   RF,PROPL16+8                                                     
         AP    PROPL16,PRODUB      PLUS NET ALLOCATION                          
*&&                                                                             
*&&UK*&& ZAP   PROPL16,PRODUB      NET ALLOCATION                               
         MP    PROPL16,BNDCMP      TIMES COMMISSION RATE                        
         SRP   PROPL16,64-4,5                                                   
         AP    PP$ACOMM,PROPL16    COMMISSION ALLOCATED                         
*                                                                               
         ICM   RF,15,BNDCSD        DISCOUNT                                     
         CVD   RF,PRODUB                                                        
         AP    PP$ADSCB,PRODUB                                                  
*                                                                               
         ICM   RF,15,BNDHRS        HOURS                                        
         CVD   RF,PRODUB                                                        
         AP    PP$HRSB,PRODUB                                                   
         B     PRO40                                                            
*&&                                                                             
*                                                                               
PRO60    OI    PG$STAT,PG$FULLB    ZERO MEANS FULLY AUTO BILLED                 
         ZAP   PA$NETBL,PA$NET     NET BILLING = TRANSACTION NET                
         B     PRO40                                                            
         DROP  R3                                                               
*                                                                               
         USING SCIELD,R3           SUBSIDIARY CASH ELEMENT                      
PRO70    CLI   SCITYPE,SCITSJHR    HOURS TYPE?                                  
         BNE   *+14                                                             
         ZAP   PA$HOURS,SCIAMNT    HOURS                                        
         B     PRO40                                                            
         CLI   SCITYPE,SCITCDSC    CASH DISCOUNT                                
         BNE   PRO40                                                            
         AP    PA$DSC,SCIAMNT      CASH DISCOUNT                                
         B     PRO40                                                            
         DROP  R3                                                               
*                                                                               
         USING OAMELD,R3           ORDER AMOUNT ELEMENT                         
PRO80    AP    PA$NET,OAMAMNT      ORDER AMOUNT                                 
         SP    PA$NET,OAMIVAL      INVOICES TODATE                              
         ZAP   PROORDAM,OAMAMNT    ORDER AMOUNT                                 
         ZAP   PROINVTD,OAMIVAL    INVOICES TODATE                              
         OI    PROSTAT,PROORDER    ORDER TRANSACTION                            
         B     PRO40                                                            
         DROP  R3                                                               
*                                                                               
*&&US                                                                           
         USING PRTELD,R3                                                        
PRO85    MVC   PROBYTE,PRTSTAT                                                  
         NI    PROBYTE,PRTSBILQ+PRTSNOTQ+PRTSRTEQ                               
         OC    PG$STAT2,PROBYTE                                                 
         TM    PG$STAT2,PG$NTIME   N TIME                                       
         BNZ   *+8                                                              
         TM    PG$STAT2,PG$RTIME   R TIME                                       
         BZ    *+8                                                              
         OI    PROTSTAT,TRNSNOCM   MAKE NON-COMMISH                             
         ZAP   PA$HOURS,PRTHOUR                                                 
         B     PRO40                                                            
         DROP  R3                                                               
*&&                                                                             
*                                                                               
         USING PTAELD,R3           PROD TRANSACTION ACTIVITY ELEMENT            
PRO90    BAS   RE,ADDPTA           ADD THIS PTAEL TO LIST                       
         MVC   PG$BLCUR,PTACUR     SET CURRENCY CODE                            
         TM    PTASTAT1,PTASCASH                                                
         BNO   *+8                                                              
         OI    PG$STAT,PG$CASHA    CASH ALLOCATION HAS OCCURED                  
         TM    PTASTAT1,PTASPEND   ACTIVITY PENDING (DRAFT)?                    
         BO    PRO110                                                           
         CLI   PTATYPE,PTATRAL     ALLOCATION TO BILL                           
         BNE   PRO100                                                           
         AP    PA$NETBL,PTANET     NET BILLED                                   
         AP    PA$COMBL,PTARCOM    COMMISSION BILLED                            
         AP    PA$DSCB,PTACDSC     CASH DISC BILLED                             
         LH    RE,PTAHOURS                                                      
         CVD   RE,PRODUB                                                        
         AP    PA$HRSB,PRODUB      HOURS BILLED                                 
         CLC   PG$LBLDT,PTARBLDT   LATEST BILL DATE?                            
         BH    *+10                                                             
         MVC   PG$LBLNO(L'PG$LBLNO+L'PG$LBLDT),PTARBLNO BILL NO + DATE          
         CLI   PTALN,PTARLN1Q      FOREIGN CURRENCY BILLING?                    
         BNH   PRO40                                                            
*                                                                               
         AP    PB$NETBL,PTANETF    FOREIGN CURRENCY NET BILLING                 
         AP    PB$COMBL,PTARFCOM   FOREIGN CURRENCY COMMISSION AMOUNT           
         B     PRO40                                                            
*                                                                               
PRO100   CLI   PTATYPE,PTATWOF     WRITE-OFF?                                   
         BE    *+12                                                             
         CLI   PTATYPE,PTATWOFR    WRITE-OFF RECOVERY?                          
         BNE   PRO105                                                           
         AP    PA$WOFAM,PTANET     WRITE-OFF AMOUNT                             
         AP    PA$DSCW,PTACDSC     CASH DISC WRITTEN OFF                        
         LH    RE,PTAHOURS                                                      
         CVD   RE,PRODUB                                                        
         AP    PA$HRSW,PRODUB      HOURS WRITTEN OFF                            
         B     PRO40                                                            
*                                                                               
PRO105   CLI   PTATYPE,PTATTRFT    TRANSFER TO?                                 
         BNE   PRO40                                                            
         AP    PA$XFRAM,PTANET     TRANSFER AMOUNT                              
         AP    PA$DSCX,PTACDSC     CASH DISC TRANSFERRED                        
         LH    RE,PTAHOURS                                                      
         CVD   RE,PRODUB                                                        
         AP    PA$HRSX,PRODUB      HOURS TRANSFERRED                            
         B     PRO40                                                            
*                                  ** PENDING VALUES **                         
PRO110   TM    PTASTAT1,PTASHOUR                                                
         BNO   *+8                                                              
         OI    PG$STAT,PG$HOURA    PENDING ALLOCATION IN HOURS                  
         CLI   PTATYPE,PTATRAL     ALLOCATION TO BILL                           
         BNE   PRO120                                                           
         TM    PTASTAT1,PTASREVS                                                
         BNO   *+8                                                              
         OI    PG$STAT,PG$REVS     PENDING ALLOCATION IS REVERSAL               
         AP    PP$AALLO,PTANET     ALLOCATION PENDING                           
         AP    PP$FALLO,PTANETF    FOREIGN ALLOCATION PENDING                   
         AP    PP$ACOMM,PTARCOM    COMMISSION AMOUNT PENDING                    
         AP    PP$ADSCB,PTACDSC    CASH DISC PENDING BILL                       
         LH    RE,PTAHOURS                                                      
         CVD   RE,PRODUB                                                        
         AP    PP$HRSB,PRODUB      HOURS PENDING BILL                           
         CLI   PTALN,PTARLN1Q      FOREIGN CURRENCY BILLING?                    
         BNH   PRO40                                                            
         AP    PP$FCOMM,PTARFCOM   FOREIGN COMMISSION AMOUNT PENDING            
         B     PRO40                                                            
*                                                                               
PRO120   CLI   PTATYPE,PTATWOF     WRITE-OFF?                                   
         BNE   PRO130                                                           
         AP    PP$AWOFF,PTANET     WRITE-OFF AMOUNT PENDING                     
         AP    PP$FWOFF,PTANETF    WRITE-OFF AMOUNT PENDING (BILLCURR)          
         AP    PP$ADSCW,PTACDSC    CASH DISC PENDING WRITE-OFF                  
         LH    RE,PTAHOURS                                                      
         CVD   RE,PRODUB                                                        
         AP    PP$HRSW,PRODUB      HOURS PENDING WRITE-OFF                      
         B     PRO40                                                            
PRO130   CLI   PTATYPE,PTATWOFR    WRITE-OFF RECOVERY?                          
         BNE   PRO140                                                           
         AP    PP$AWOFR,PTANET     WRITE-OFF RECOVERY AMOUNT PENDING            
         AP    PP$FWOFR,PTANETF    WRITE-OFF REC AMT ENDING (BILLCURR)          
         AP    PP$ADSCR,PTACDSC    CASH DISC PENDING RECOVERY                   
         LH    RE,PTAHOURS                                                      
         CVD   RE,PRODUB                                                        
         AP    PP$HRSR,PRODUB      HOURS PENDING RECOVERY                       
         B     PRO40                                                            
*                                                                               
PRO140   CLI   PTATYPE,PTATTRFT    TRANSFER TO?                                 
         BNE   PRO40                                                            
         AP    PP$AXFER,PTANET     TRANSFER AMOUNT PENDING                      
         AP    PP$FXFER,PTANETF    TRANSFER AMOUNT PENDING (BILLCURR)           
         AP    PP$ADSCX,PTACDSC    CASH DISC PENDING RECOVERY                   
         LH    RE,PTAHOURS                                                      
         CVD   RE,PRODUB                                                        
         AP    PP$HRSX,PRODUB      HOURS PENDING RECOVERY                       
         B     PRO40                                                            
         DROP  R3                                                               
*                                                                               
         USING TRXELD,R3                                                        
PRO150   TM    TRXSTA1,TRXSXALC    TEST EXCLUDE THIS ITEM FROM BILL             
         BNO   PRO40                                                            
         OI    PG$STAT,PG$FULLB    IF SO SET FULLY BILLED                       
         B     PRO40                                                            
         DROP  R3                                                               
*                                                                               
PRO200   TM    PG$STAT,PG$OLDA     OLD ALLOCATION TYPE?                         
         BNO   PRO250                                                           
*                                                                               
*                                  US LEAVES BILLED AS IS                       
*&&UK                                                                           
         TM    PG$STAT,PG$FULLB    FULLY BILLED (AUTOMATIC BILLING)             
         BNO   PRO210                                                           
         TM    PROSTAT,PROORDER    ORDER?                                       
         BO    PRO230                                                           
         B     PRO240                                                           
PRO210   ICM   RF,15,PROALAMT      ANYTHING ALLOCATED?                          
         BZ    PRO250                                                           
         CVD   RF,PRODUB                                                        
         ZAP   PA$NETBL,PRODUB                                                  
PRO220   TM    PROSTAT,PROORDER    ORDER AMOUNT INCLUDED                        
         BNO   PRO240                                                           
         SP    PA$NETBL,PROINVTD   INVOICES TODATE                              
PRO230   CP    PA$NETBL,=P'0'                                                   
         BH    PRO240                                                           
         ZAP   PA$NETBL,=P'0'                                                   
         OI    PG$STAT,PG$FULLB    FULLY BILLED (AUTOMATIC BILLING)             
         B     PRO260                                                           
*                                                                               
PRO240   ICM   RF,15,PROIAGOB      RF=A(GOBLOCK AT WC LEVEL)                    
         BZ    PRO250                                                           
         ZAP   PRODUB,GOAGYCOM-GOBLOCKD(L'GOAGYCOM,RF) COMM (4 DEC PLC)         
         ZAP   PROPL16,PA$NETBL    NET BILLED                                   
         SRP   PROPL16,2,0         INCREASE NET BILL TO 4 DEC PLACES            
         MP    PROPL16,PRODUB      APPLY COMISSION RATE                         
         SRP   PROPL16,64-8,5      DIV BY 100 AND ROUND TO 2 DEC PLACES         
         ZAP   PA$COMBL,PROPL16    COMMISSION BILLED                            
*&&                                                                             
PRO250   ZAP   PRODUB,PA$NETBL     FULLY BILLED?                                
         AP    PRODUB,PA$WOFAM                                                  
         CP    PRODUB,PA$NET                                                    
         BNE   *+12                                                             
         OI    PG$STAT,PG$FULLB    SET FULLY BILLED                             
         B     PRO260                                                           
*                                                                               
         CP    PA$NETBL,=P'0'      IS IT BILLED                                 
         BNE   PRO255                                                           
         CP    PA$WOFAM,=P'0'      OR WRITTEN OFF                               
         BE    PRO260                                                           
PRO255   OI    PG$STAT,PG$PARTB    SET PART BILLED                              
*                                                                               
PRO260   ZAP   PA$NETUB,PA$NET                                                  
         SP    PA$NETUB,PA$NETBL                                                
         SP    PA$NETUB,PA$WOFAM                                                
         SP    PA$NETUB,PA$XFRAM   NET UNBILLED AMOUNT                          
         TM    PG$STAT,PG$FULLB    IF MARKED FULLY BILLED (TRXSXALC)            
         BNO   *+10                                                             
         ZAP   PA$NETUB,=P'0'                                                   
*                                                                               
         CP    PA$NETUB,=P'0'      TEST UNBILLED ZERO                           
         BNE   *+8                                                              
         OI    PG$STAT3,PG$FULUT   ZERO UNBILLED - SET FULLY UTILISED           
         CP    PA$WOFAM,=P'0'                                                   
         BE    *+8                                                              
         OI    PG$STAT3,PG$WOFUP   UPDATED WRITE-OFFS                           
         CP    PA$XFRAM,=P'0'                                                   
         BE    *+8                                                              
         OI    PG$STAT3,PG$XFRUP   UPDATED TRANSFERS                            
         CP    PA$NETBL,=P'0'                                                   
         BE    *+8                                                              
         OI    PG$STAT3,PG$BILUP   UPDATED BILLING                              
         CP    PP$AALLO,=P'0'                                                   
         BE    PRO261                                                           
         OI    PG$STAT3,PG$BILAL   ALLOCATED BILLING                            
         ZAP   PRODUB,PA$NETUB     UNBILLED PORTION                             
         SP    PRODUB,PP$AALLO     ALLOCATED AMOUNT                             
         BNZ   *+8                                                              
         OI    PG$STAT3,PG$FULAL   FULLY ALLOCATED                              
PRO261   EQU   *                                                                
*&&US                                                                           
         ZAP   PRODUB,=P'0'        INIT COMM TO ZERO INCASE N-C TRAN            
         TM    PROTSTAT,TRNSNOCM   NON COMMISSIONABLE ITEM                      
         BO    PRO262                                                           
*&&                                                                             
         ICM   RF,15,PROIAGOB      RF=A(GOBLOCK AT WC LEVEL)                    
         BZ    PRO262                                                           
         ZAP   PRODUB,GOAGYCOM-GOBLOCKD(L'GOAGYCOM,RF) COMM (4 DEC PLC)         
         ZAP   PROPL16,PA$NETUB    NET UNBILLED                                 
*&&US                                                                           
         AP    PROPL16,PA$DSC      ADD BACK CASH DISCOUNT                       
         SP    PROPL16,PA$DSCB     TAKE OFF DISCOUNT BILLED                     
         SP    PROPL16,PA$DSCW     TAKE OFF DISCOUNT WRITTEN-OFF                
         SP    PROPL16,PA$DSCX     TAKE OFF DISCOUNT TRANSFERRED                
*&&                                                                             
         SRP   PROPL16,2,0         INCREASE NET BILL TO 4 DEC PLACES            
         MP    PROPL16,PRODUB      APPLY COMISSION RATE                         
         SRP   PROPL16,64-8,5      DIV BY 100 AND ROUND TO 2 DEC PLACES         
         ZAP   PA$COMUB,PROPL16    COMMISSION AMOUNT UNBILLED                   
*                                                                               
*                                  AGENCY NET AVAILABLE                         
PRO262   ZAP   PM$ANVBL,=P'0'                                                   
         CP    PA$NET,=P'0'                                                     
         BE    PRO270                                                           
         ZAP   PM$ANVBL,PA$NETUB   AGENCY NET UNBILLED                          
         SP    PM$ANVBL,PP$AALLO   MINUS ALLOCATION PENDING                     
         SP    PM$ANVBL,PP$AWOFF   MINUS WRITEOFFS PENDING                      
*        SP    PM$ANVBL,PP$AWOFR   MINUS RECOVERY PENDING                       
         SP    PM$ANVBL,PP$AXFER   MINUS TRANSFERS PENDING                      
*                                                                               
*                                  CASH DISCOUNT AVAILABLE                      
         ZAP   PM$DSVBA,PA$DSC     TOTAL CASH DISCOUNT AMOUNT                   
         SP    PM$DSVBA,PA$DSCB    CASH DISCOUNT BILLED                         
         SP    PM$DSVBA,PP$ADSCB   CASH DISCOUNT PENDING BILL                   
         SP    PM$DSVBA,PA$DSCW    CASH DISCOUNT WRITTEN-OFF                    
         SP    PM$DSVBA,PP$ADSCW   CASH DISCOUNT PENDING WOFF                   
*        SP    PM$DSVBA,PP$ADSCR   CASH DISCOUNT PENDING RECOVERY               
         SP    PM$DSVBA,PA$DSCX    CASH DISCOUNT TRANSFERED                     
         SP    PM$DSVBA,PP$ADSCX   CASH DISCOUNT PENDING TRANSFER               
*                                  COMMISSION AVAILABLE                         
PRO270   ZAP   PROPL16,PM$ANVBL    NET AVAILABLE                                
*&&US*&& AP    PROPL16,PM$DSVBA    PLUS CASH DISCOUNT AVAILABLE                 
         SRP   PROPL16,2,0         INCREASE TO 4 DEC PLACES                     
         MP    PROPL16,PRODUB      APPLY COMISSION RATE                         
         SRP   PROPL16,64-8,5      DIV BY 100 AND ROUND TO 2 DEC PLACES         
         ZAP   PM$ACVBL,PROPL16                                                 
*                                                                               
         ZAP   PA$GRSBL,PA$NETBL                                                
         AP    PA$GRSBL,PA$COMBL   GROSS BILLED                                 
*                                                                               
         ZAP   PA$GRSUB,PA$NETUB                                                
         AP    PA$GRSUB,PA$COMUB   GROSS UNBILLED                               
*                                                                               
         ZAP   PB$GRSBL,PB$NETBL                                                
         AP    PB$GRSBL,PB$COMBL   FOREIGN GROSS BILLED                         
*                                                                               
         ZAP   PM$PTVBL,=P'10000'  100% AVAILABLE                               
         CP    PM$ANVBL,PA$NET                                                  
         BE    LAB1                ALL STILL AVAILABLE                          
         ZAP   PM$PTVBL,=P'0'      0% AVAILABLE                                 
         ZAP   PROPL16,PM$ANVBL    SET AMOUNT AVAILABLE                         
         BZ    LAB1                NONE AVAILABLE                               
         CP    PA$NET,=P'0'                                                     
         BE    LAB1                                                             
         SRP   PROPL16,4,5                                                      
         DP    PROPL16,PA$NET      AVAILABLE NET/ORIGINAL NET                   
         ZAP   PM$PTVBL,PROPL16(8) %AGE OF ORIGINAL NET NOW AVAILABLE           
*                                                                               
LAB1     ZAP   PM$HRVBL,PA$HOURS   TOTAL HOURS                                  
         SP    PM$HRVBL,PA$HRSB    HOURS BILLED                                 
         SP    PM$HRVBL,PP$HRSB    HOURS PENDING BILL                           
         SP    PM$HRVBL,PA$HRSW    HOURS WRITTEN-OFF                            
         SP    PM$HRVBL,PP$HRSW    HOURS PENDING WOFF                           
*        SP    PM$HRVBL,PP$HRSR    HOURS PENDING RECOVERY                       
         SP    PM$HRVBL,PA$HRSX    HOURS TRANSFERED                             
         SP    PM$HRVBL,PP$HRSX    HOURS PENDING TRANSFER                       
*&&US                                                                           
         TM    PG$STAT2,PG$NTIME   N TIME                                       
         BNZ   *+8                                                              
         TM    PG$STAT2,PG$RTIME   R TIME                                       
         BZ    *+10                                                             
         ZAP   PM$HRVBL,=P'0'      NO BILLABLE HOURS FOR 'N' OR 'R'             
*&&                                                                             
         SR    RF,RF                                                            
         ICM   RF,15,PROIAEXR      SET ADDRESS OF CURRENCY RULE                 
         BZ    PROX                IF THERE IS ONE                              
*                                                                               
         ZAP   PRODUB,PA$NET       TRANSACTION NET AMOUNT                       
         BZ    LAB1A                                                            
         EXCHP PRODUB,(RF),DUB=PRODUB,WRK=PROWORK                               
LAB1A    ZAP   PB$NET,PRODUB       FOREIGN TRANSACTION NET AMOUNT               
*                                                                               
         ZAP   PRODUB,PA$NETUB     NET UNBILLED                                 
         BZ    LAB1B                                                            
         EXCHP PRODUB,(RF),DUB=PRODUB,WRK=PROWORK                               
LAB1B    ZAP   PB$NETUB,PRODUB     FOREIGN NET UNBILLED                         
*                                                                               
         ZAP   PRODUB,PA$COMUB     COMMISSION UNBILLED                          
         BZ    LAB1C                                                            
         EXCHP PRODUB,(RF),DUB=PRODUB,WRK=PROWORK                               
LAB1C    ZAP   PB$COMUB,PRODUB     FOREIGN COMMISSION UNBILLED                  
*                                                                               
         ZAP   PB$GRSUB,PB$NETUB                                                
         AP    PB$GRSUB,PB$COMUB   FOREIGN GROSS UNBILLED                       
*                                                                               
         ZAP   PRODUB,PM$ANVBL     AGENCY NET AVAILABLE                         
         BZ    LAB1F                                                            
         EXCHP PRODUB,(RF),DUB=PRODUB,WRK=PROWORK                               
LAB1F    ZAP   PM$FNVBL,PRODUB     FOREIGN NET AVAILABLE                        
*                                                                               
         ZAP   PRODUB,PM$ACVBL     AGENCY COMMISSION AVAILABLE                  
         BZ    LAB1G                                                            
         EXCHP PRODUB,(RF),DUB=PRODUB,WRK=PROWORK                               
LAB1G    ZAP   PM$FCVBL,PRODUB     FOREIGN COMMISSION AVAILABLE                 
*                                                                               
         ZAP   PRODUB,PM$DSVBA     AGENCY DISCOUNT AVAILABLE                    
         BZ    LAB1H                                                            
         EXCHP PRODUB,(RF),DUB=PRODUB,WRK=PROWORK                               
LAB1H    ZAP   PM$DSVBF,PRODUB     FOREIGN DISCOUNT AVAILABLE                   
*                                                                               
PROX     XIT1  ,                                                                
         EJECT                                                                  
***********************************************************************         
* CONVERT BNDEL INTO PTAEL - R3=A(BNDEL)                              *         
***********************************************************************         
         SPACE 1                                                                
         USING BNDEL,R4                                                         
         USING PTAEL,R3                                                         
CNVBND   TM    PROISTAT,PROIPTAQ                                                
         BNOR  RE                  LIST NOT CALLED FOR                          
         NTR1                                                                   
         OC    PROAPTAN,PROAPTAN                                                
         BZ    CNVBX               NOT DOING                                    
         XC    PROELEM,PROELEM                                                  
         LR    R4,R3                                                            
*&&US                                                                           
         OC    BNDBNO,BNDBNO       IS THIS A SPARE BNDEL                        
         BZ    CNVBX               YES, IGNORE                                  
*&&                                                                             
         LA    R3,PROELEM                                                       
         MVI   PTAEL,PTAELQ                                                     
         MVI   PTALN,PTARLN1Q      SET ALLOCATION                               
         MVI   PTATYPE,PTATRAL     SET ALLOCATION                               
         CLC   =C'WO',BNDBNO                                                    
         BNE   *+8                                                              
         MVI   PTATYPE,PTATWOF     SET WRITE-OFF                                
         CLC   =C'WR',BNDBNO                                                    
         BNE   *+8                                                              
         MVI   PTATYPE,PTATWOFR    SET WRITE-OFF RECOVERY                       
         CLI   PTATYPE,PTATRAL                                                  
         BE    *+8                                                              
         MVI   PTALN,PTAWLN1Q                                                   
         CLC   BNDBNO,PROSPACE                                                  
         BH    *+8                                                              
         OI    PTASTAT1,PTASPEND                                                
         ICM   RF,15,BNDAMNT       ALLOCATED AMOUNT                             
         CVD   RF,PRODUB                                                        
         ZAP   PTANET,PRODUB                                                    
         ZAP   PTANETF,=P'0'                                                    
         MVC   PTACUR,PROSPACE                                                  
         ZAP   PTACDSC,=P'0'                                                    
*&&US                                                                           
         USING SCIELD,R5           SUBSIDIARY CASH ELEMENT                      
         ICM   RF,15,BNDCSD        IN US SET CASH DISCOUNT AND HOURS            
         CVD   RF,PRODUB                                                        
         BNZ   CNVB04                                                           
         LR    R5,R4               FIND X'50' ELEMENT                           
CNVB02   CLI   0(R5),0             END OF RECORD?                               
         BE    CNVB04              NO CASH DISCOUNT                             
         CLI   0(R5),SCIELQ        SUBSIDIARY CASH ELEMENT?                     
         BNE   *+8                                                              
         CLI   SCITYPE,SCITCDSC    CASH DISCOUNT                                
         BNE   *+14                                                             
         ZAP   PRODUB,SCIAMNT      USE CASH DISCOUNT FROM THE X'50'             
         B     CNVB04                                                           
         SR    RF,RF                                                            
         IC    RF,1(R5)            ELEMENT LENGTH                               
         AR    R5,RF               NEXT ELEMENT                                 
         B     CNVB02                                                           
         DROP  R5                                                               
*                                                                               
         USING TRNELD,R5                                                        
CNVB04   ZAP   PTACDSC,PRODUB                                                   
         ICM   RF,15,BNDHRS                                                     
         STCM  RF,3,PTAHOURS                                                    
         BNZ   CNVB04D                                                          
         LA    R5,TRNRFST                                                       
         TM    PROISTAT,PROIOLDQ   TEST OLD FILE STRUCTURE                      
         BNO   *+8                                                              
         LA    R5,TRNRECD+ACCORFST                                              
         CP    PTANET,TRNAMNT                                                   
         BNE   CNVB04D             NEVER MIND                                   
         DROP  R5                                                               
*                                                                               
         USING PRTELD,R5                                                        
CNVB04A  CLI   0(R5),0             EOR                                          
         BE    CNVB04D             NO HOURS                                     
         CLI   0(R5),PRTELQ        X'40' PERSONAL RATE ELEMENT                  
         BE    *+14                                                             
         IC    RF,1(R5)                                                         
         AR    R5,RF                                                            
         B     CNVB04A                                                          
*                                                                               
         CLI   PTATYPE,PTATRAL     SET ALLOCATION                               
         BNE   CNVB04D                                                          
         TM    PRTSTAT,PRTSBILQ    BILLABLE TIME ONLY                           
         BZ    CNVB04D                                                          
         ZAP   PRODUB,PRTHOUR                                                   
         CVB   RF,PRODUB                                                        
         STCM  RF,3,PTAHOURS                                                    
         DROP  R5                                                               
*&&                                                                             
CNVB04D  CLI   PTATYPE,PTATRAL     TEST IF ALLOCATED TO BILL                    
         BNE   CNVB10              NO - MUST BE WO/WR                           
         MVC   PTARDATE,BNDDTE                                                  
         MVC   PTARBLNO,BNDBNO                                                  
         MVC   PTARBLDT,BNDDTE                                                  
*&&UK                                                                           
         SR    RF,RF                                                            
         ICM   RF,3,BNDCMR                                                      
         CVD   RF,PRODUB                                                        
         ZAP   PTARCORT,PRODUB     COMMISSION RATE                              
         CLI   PTATYPE,PTATRAL     TEST ALLOCATED TO BILL                       
         BNE   CNVB05                                                           
         TM    PTASTAT1,PTASPEND   TEST UPDATED                                 
         BO    CNVB05                                                           
         CP    PTANET,=P'0'        TEST ALLOC AMOUNT IS ZERO                    
         BNE   CNVB05              AND IF SO SET TO TRANSACTION AMT             
         LA    RF,TRNRFST+(TRNAMNT-TRNELD)                                      
         TM    PROISTAT,PROIOLDQ   TEST OLD FILE STRUCTURE                      
         BNO   *+8                                                              
         LA    RF,TRNRECD+ACCORFST+(TRNAMNT-TRNELD)                             
         ZAP   PTANET,0(L'TRNAMNT,RF)                                           
CNVB05   EQU   *                                                                
*&&                                                                             
*&&US                                                                           
         TM    BNDCMST,BNDCPACK    TEST COMMISSION RATE IS PACK DEC             
         BO    CNVB06                                                           
         OC    BNDCMP+2(2),BNDCMP                                               
         BNZ   CNVB06              MUST BE PACKED                               
         SR    RF,RF                                                            
         ICM   RF,3,BNDCMP                                                      
         CVD   RF,PRODUB                                                        
         ZAP   PTARCORT,PRODUB                                                  
         B     *+10                                                             
CNVB06   ZAP   PTARCORT,BNDCMP     COMMISSION RATE                              
         MVC   PTARFORM,BNDFORM    FORMAT/GROUP LEVEL                           
         MVC   PTARTYPE,BNDTYPE    WORKCODE TYPE                                
         MVC   PTARGSTC,BNDVATC    GST CODE                                     
         CLI   BNDLN,BNDLN2Q       DO WE HAVE PST DATA?                         
         BL    CNVB08              NO                                           
         MVC   PTARPSTC,BNDPSTC    YES, SAVE PST CODE                           
         MVC   PTARPRVC,BNDPRVC    PROVINCE CODE                                
         MVC   PTAMOA,BNDMOA       AND MOA ALSO                                 
         OC    BNDRUN,BNDRUN       DO WE HAVE A RUN DATE?                       
         BZ    *+10                NO,                                          
         MVC   PTARDATE,BNDRUN     YES, USE IT INSTEAD OF BILL DATE             
CNVB08   EQU   *                                                                
*&&                                                                             
         ZAP   PROPL16,PTANET      NET ALLOCATION                               
         AP    PROPL16,PTACDSC     ADD BACK CASH DISCOUNT                       
         MP    PROPL16,PTARCORT                                                 
         SRP   PROPL16,64-6,5                                                   
         ZAP   PTARCOM,PROPL16     COMMISSION ALLOCATED                         
         B     CNVB20                                                           
*                                                                               
CNVB10   MVC   PTAWREF,BNDBNO                                                   
*                                                                               
         PACK  PRODUB,BNDBNO+2(4)                                               
         CVB   RF,PRODUB                                                        
         LCR   RF,RF                                                            
         STCM  RF,3,PTASEQN                                                     
*                                                                               
         MVC   PTAWEXPA,PROSPACE   WE DON'T KNOW WHAT THIS IS                   
*&&US*&& MVC   PTAWWOT,BNDWOT      US HAS A WRITE-OFF TYPE                      
         GOTO1 PRODATCN,PROPARM,(2,BNDDTE),(1,PTAWDAT)                          
*                                                                               
CNVB20   BAS   RE,ADDPTA           ADD ELEMENT TO OUTPUT BLOCK                  
CNVBX    B     PROX                                                             
         EJECT                                                                  
***********************************************************************         
* CONVERT CUBELS INTO 2 OFFSETTING PTAELS, ALA =NBIL                  *         
***********************************************************************         
*&&US                                                                           
         USING CUBELD,R4                                                        
         USING PTAELD,R3                                                        
CNVCUB   TM    PROISTAT,PROIPTAQ                                                
         BNOR  RE                  LIST NOT CALLED FOR                          
         NTR1                                                                   
         OC    PROAPTAN,PROAPTAN                                                
         BZ    CNVCX               NOT DOING                                    
*                                                                               
         XC    PROELEM,PROELEM     BUILD REVERSED BILL PTA ELEMENT              
         LR    R4,R3                                                            
         LA    R3,PROELEM                                                       
         MVI   PTAEL,PTAELQ                                                     
         MVI   PTALN,PTARLN1Q      SET ALLOCATION                               
         MVI   PTATYPE,PTATRAL                                                  
         OI    PTASTAT1,PTASREVU                                                
         MVC   PTARBLNO,CUBNO                                                   
         MVC   PTARDATE,CUBDTE                                                  
         OC    CUBRUN,CUBRUN       DO WE HAVE A RUN DATE?                       
         BZ    *+10                NO                                           
         MVC   PTARDATE,CUBRUN     YES, USE IT INSTEAD OF BILL DATE             
         MVC   PTARBLDT,CUBDTE                                                  
         ICM   RF,15,CUBAMNT       AMOUNT                                       
         CVD   RF,PRODUB                                                        
         ZAP   PTANET,PRODUB                                                    
         ZAP   PTANETF,=P'0'                                                    
         MVC   PTACUR,PROSPACE                                                  
*                                                                               
         ICM   RF,15,CUBCSD        SET CASH DISCOUNT AND HOURS                  
         CVD   RF,PRODUB                                                        
         ZAP   PTACDSC,PRODUB                                                   
         ICM   RF,15,CUBHRS                                                     
         STCM  RF,3,PTAHOURS                                                    
*                                                                               
         TM    CUBCMST,BNDCPACK    TEST COMMISSION RATE IS PACK DEC             
         BO    CNVC06                                                           
         OC    CUBCMP+2(2),CUBCMP                                               
         BNZ   CNVC06              MUST BE PACKED                               
*                                                                               
         SR    RF,RF                                                            
         ICM   RF,3,CUBCMP                                                      
         CVD   RF,PRODUB                                                        
         ZAP   PTARCORT,PRODUB                                                  
         B     *+10                                                             
CNVC06   ZAP   PTARCORT,CUBCMP     COMMISSION RATE                              
*                                                                               
         ZAP   PROPL16,PTANET      CALCULATE COMMISSION AMOUNT                  
         AP    PROPL16,PTACDSC                                                  
         MP    PROPL16,PTARCORT                                                 
         SRP   PROPL16,64-6,5                                                   
         ZAP   PTARCOM,PROPL16                                                  
*                                                                               
         MVC   PTARTYPE,CUBTYPE                                                 
         MVC   PTARFORM,CUBFORM                                                 
         MVC   PTARGSTC,CUBGST                                                  
         MVC   PTARPSTC,CUBPST                                                  
         MVC   PTARPRVC,CUBPRV                                                  
*                                                                               
         MVC   PTARUNBD,CUBUNDTE                                                
         MVC   PTARUNBB,CUBUNNO                                                 
         BAS   RE,ADDPTA                                                        
*                                                                               
         MVI   PTASTAT1,PTASREVS   BUILD REVERSAL PTA ELEMENT                   
         MVC   PTARBLNO,CUBUNNO                                                 
         MVC   PTARDATE,CUBUNDTE                                                
         MVC   PTARBLDT,CUBUNDTE                                                
         MVC   PTARORGB,CUBNO      ORIGINAL BILL NO                             
         MVC   PTARORGD,CUBDTE     ORIGINAL BILL DATE                           
*                                                                               
         MP    PTANET,=P'-1'                                                    
         MP    PTACDSC,=P'-1'                                                   
         MP    PTARCOM,=P'-1'                                                   
         LH    RF,PTAHOURS                                                      
         LCR   RF,RF                                                            
         STH   RF,PTAHOURS                                                      
*                                                                               
         BAS   RE,ADDPTA                                                        
CNVCX    B     PROX                                                             
         EJECT                                                                  
***********************************************************************         
* ADD PTAEL TO LIST FOR RETURN TO CALLER - R3=A(PTAEL)                *         
***********************************************************************         
         SPACE 1                                                                
ADDPTA   TM    PROISTAT,PROIPTAQ   TEST LIST REQUIRED                           
         BNOR  RE                  NO - RETURN                                  
         LR    R0,RE               SAVE RETURN ADDRESS                          
         ICM   RF,15,PROAPTAN      RF=A(NEXT SLOT)                              
         BZR   RE                  NOT DOING                                    
         SR    RE,RE                                                            
         IC    RE,PTALN-PTAEL(R3)                                               
         EX    RE,*+8                                                           
         B     *+10                                                             
         MVC   0(0,RF),0(R3)                                                    
         AR    RF,RE                                                            
         MVI   0(RF),0             SET NEW E-O-L MARKER                         
         ST    RF,PROAPTAN         SAVE A(NEXT SLOT)                            
         LR    RE,R0               RESTORE RETURN ADDRESS                       
         BR    RE                                                               
         DROP  R3,R4                                                            
         EJECT                                                                  
*&&US                                                                           
***********************************************************************         
* WRITE PTA ELEMENTS OUT TO THE TRANSACTION AFTER DELETING 4B'S AND 77*         
* ON ENTRY R3 IS A(FIRST ELEMENT OF THE TRANSACTION FILE              *         
***********************************************************************         
WRIPTA   NTR1  ,                                                                
         ICM   R5,15,PROIAPTA      IS THERE A LIST OF PTA ELS                   
         BNZ   *+6                                                              
         DC    H'0'                WHAT AM I DOING HERE                         
*                                                                               
         LR    R2,R3               SET A(FIRST ELEMENT) IN R2                   
         XR    R1,R1                                                            
WRIP10   CLI   0(R2),EOR           EOR!                                         
         BE    WRIP50              YES, DELETE MARKED ELEMENTS                  
*                                                                               
         CLI   0(R2),BNDELQ        IS THIS A BND ELEMENT                        
         BNE   *+8                 NO                                           
         MVI   0(R2),X'FF'         YES, FLAG FOR DELETE                         
*                                                                               
         CLI   0(R2),CUBELQ        IS THIS A CUB ELEMENT                        
         BNE   *+8                 NO                                           
         MVI   0(R2),X'FF'         YES, FLAG FOR DELETE                         
*                                                                               
         CLI   0(R2),PTAELQ        IS THIS A PTA ELEMENT                        
         BNE   *+8                 NO                                           
         MVI   0(R2),X'FF'         YES, DELETE HERE, GET FROM BUFFER            
*                                                                               
         IC    R1,1(R2)                                                         
         LA    R2,0(R1,R2)                                                      
         B     WRIP10                                                           
*                                                                               
WRIP50   GOTO1 PROHELLO,PROPARM,(C'D',PROFILE),(X'FF',PROIATRN),0,0             
*                                                                               
         XR    R4,R4                                                            
*                                                                               
WRIP60   CLI   0(R5),0             END OF LIST                                  
         BE    WRIPX               YES, ALL DONE                                
         CLI   0(R5),PTAELQ        NO, IS THIS A PTA ELEMENT                    
         BNE   WRIP70              NO, GET NEXT                                 
*                                                                               
         XC    PROELEM,PROELEM     EXTRACT ELEMENT                              
         IC    R4,1(R5)                                                         
         BCTR  R4,0                                                             
         EX    R4,*+8                                                           
         B     *+10                                                             
         MVC   PROELEM(0),0(R5)                                                 
*                                                                               
WRIP62   L     R2,PROIATRN         R2=A(TRANSACTION RECORD)                     
         SR    RF,RF               TEST ROOM IN RECORD                          
         ICM   RF,3,TRNRLEN-TRNRECD(R2)  RF=LENGTH OF RECORD                    
*                                                                               
         TM    PROISTAT,PROIOLDQ   TEST OLD FILE                                
         BNO   *+8                                                              
         ICM   RF,3,ACCORLEN(R2)                                                
*                                                                               
         SR    R1,R1                                                            
         IC    R1,PROELEM+1        R1=LENGTH 0F ELEMENT                         
         AR    RF,R1                                                            
         CH    RF,=Y(MAXRLEN)                                                   
         BNH   WRIP68                                                           
                                                                                
         TM    PROISTAT,PROIMAXL   WANT TO EXIT ON MAX ERROR?                   
         BZ    WRIP64              NO                                           
         OI    PROISTAT,PROIERRQ   YES, INDICATE ERROR AND EXIT                 
         B     WRIPX                                                            
                                                                                
*                               ** TOO MANY PTAEL'S  DELETE FIRST **            
WRIP64   LA    RE,TRNRFST-TRNRECD(R2) SET A(FIRST ELEMENT) IN RE                
         TM    PROISTAT,PROIOLDQ   TEST OLD FILE                                
         BNO   *+8                                                              
         LA    RE,ACCORFST(R2)                                                  
*                                                                               
         SR    R1,R1                                                            
WRIP66   IC    R1,1(RE)            FIRST IS 44 ELEMENT                          
         LA    RE,0(R1,RE)                                                      
         CLI   0(RE),EOR           EOR!                                         
         BNE   *+6                                                              
         DC    H'0'                                                             
         CLI   0(RE),PTAELQ        IS THIS A PTA ELEMENT                        
         BNE   WRIP66              NO                                           
         MVI   0(RE),X'FF'         YES, DELETE HERE, GET FROM BUFFER            
         GOTO1 PROHELLO,PROPARM,(C'D',PROFILE),(X'FF',PROIATRN),0,0             
         B     WRIP62              TEST IT AGAIN                                
*                                                                               
WRIP68   GOTO1 PROHELLO,PROPARM,(C'P',PROFILE),PROIATRN,PROELEM,0               
         CLI   PROPARM+12,0        ALL OK                                       
         BE    WRIP70                                                           
         DC    H'0'                                                             
*                                                                               
WRIP70   SR    R4,R4                                                            
         IC    R4,1(R5)                                                         
         AR    R5,R4               GET NEXT ELEMENT IN TABLE                    
         B     WRIP60                                                           
*                                                                               
WRIPX    B     PROX                                                             
*                                                                               
*&&                                                                             
         EJECT                                                                  
         LTORG                                                                  
         SPACE 1                                                                
EOT      EQU   X'FF'                                                            
EOR      EQU   0                                                                
MAXRLEN  EQU   1990                                                             
         EJECT                                                                  
***********************************************************************         
* DSECT TO COVER ACPRORATA LOCAL WORKING STORAGE                      *         
***********************************************************************         
         SPACE 1                                                                
PROWRKD  DSECT                                                                  
PRODUB   DS    D                                                                
PRODUB2  DS    D                                                                
PROWORK  DS    XL64                                                             
PROPL16  DS    PL16                                                             
PROHALF  DS    H                                                                
PROPARM  DS    8F                                                               
PROBYTE  DS    X                                                                
PROSTAT  DS    X                   STATUS BYTE                                  
PROORDER EQU   X'80'               ORDER TRANSACTION                            
PROXRATE DS    XL4                 EXCHANGE RATE                                
PROALAMT DS    XL(L'BNDAMNT)       OLD STYLE ALLOCATION AMOUNT                  
PROINVTD DS    XL(L'OAMIVAL)       INVOICE AMOUNT TODATE                        
PROORDAM DS    XL(L'OAMAMNT)       ORIGINAL ORDER AMOUNT                        
PROSPACE DS    CL50                SPACES                                       
PROELEM  DS    XL255               AREA FOR PTAEL BUILDING                      
PROGETFT DS    A                   A(GETFACT)                                   
PRODATCN DS    A                   A(DATCON)                                    
PROHELLO DS    A                   A(HELLO)                                     
PRORELO  DS    A                   RELOCATE OFFSET                              
PROAPTAN DS    A                   A(NEXT SLOT FOR PTAEL LIST O/P)              
PROFILE  DS    CL8                 FOR HELLO CALL                               
PROTSTAT DS    CL1                 STATUS BYTE FOR CURRENT TRANSACTION          
PROWRKX  EQU   *                                                                
         SPACE 1                                                                
***********************************************************************         
* DSECT TO COVER INPUT PARAMETERS                                     *         
***********************************************************************         
         SPACE 1                                                                
PROIBLKD DSECT                                                                  
PROISTAT DS    0X     STATUS BYTE                                               
PROIOLDQ EQU   X'80'  OLD FILE STRUCTURE                                        
PROIPTAQ EQU   X'40'  RETURN PTAEL LIST                                         
*&&US                                                                           
PROIWRIQ EQU   X'20'  WRITE PTA ELEMEMTS BACK TO TRANSACTION                    
PROIERRQ EQU   X'10'  ERROR RETURN BIT                                          
PROIMAXL EQU   X'01'  RETURN WITH ERROR IF RECORD TOO BIG                       
*&&                                                                             
PROIATRN DS    A      A(TRANSACTION RECORD)                                     
PROIAGOB DS    A      A(GOBLOCK) SET WITH W/C LEVEL VALUES                      
PROIACMF DS    A      A(COMFACS)                                                
PROIAEXR DS    A      A(EXCHANGE RULE)                                          
PROIAOUT DS    A      A(OUTPUT BLOCK)                                           
PROIAPTA DS    A      A(OUTPUT LIST OF PTAELS)                                  
PROILNQ  EQU   *-PROIBLKD                                                       
         EJECT                                                                  
PROBLKD  DSECT                     OUTPUT BLOCK                                 
       ++INCLUDE ACPRORATAD                                                     
         EJECT                                                                  
GOBLOCKD DSECT                                                                  
         PRINT OFF                                                              
       ++INCLUDE ACGOBLOCK                                                      
         PRINT ON                                                               
         EJECT                                                                  
* DDCOMFACS                                                                     
         PRINT OFF                                                              
       ++INCLUDE DDCOMFACS                                                      
         PRINT ON                                                               
         SPACE 1                                                                
* FAFACTS                                                                       
         PRINT OFF                                                              
       ++INCLUDE FAFACTS                                                        
         PRINT ON                                                               
         SPACE 1                                                                
* DDCTRYEQUS                                                                    
         PRINT OFF                                                              
       ++INCLUDE DDCTRYEQUS                                                     
         PRINT ON                                                               
         SPACE 1                                                                
* DDLANGEQUS                                                                    
         PRINT OFF                                                              
       ++INCLUDE DDLANGEQUS                                                     
         PRINT ON                                                               
         SPACE 1                                                                
* DDMASTD                                                                       
         PRINT OFF                                                              
       ++INCLUDE DDMASTD                                                        
         PRINT ON                                                               
         SPACE 1                                                                
* ACGENFILE                                                                     
         PRINT OFF                                                              
       ++INCLUDE ACGENFILE                                                      
         PRINT ON                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'022ACPRORATA 10/31/08'                                      
         END                                                                    
