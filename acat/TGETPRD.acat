*          DATA SET TGETPRD    AT LEVEL 028 AS OF 05/01/02                      
*CATALP TGETPRD                                                                 
         TITLE 'GET PRODUCT CODE / SIMULATIONS /NAME'                           
*                                  PARAMETERS-IF ZERO NO DATA RETURNED          
*        OPERATES OFF OF RPWORK    A(INPUT PROD CODE)                           
*                                  A(OUTPUT PRODUCT CODE)                       
*                                  A(SIM CODE LIST)                             
*                                  A(PRODUCT NAME)                              
TGETPRD  ENMOD                                                                  
         REQUS                                                                  
         L     RA,=V(RPWORKC)                                                   
         USING RPWORK,RA                                                        
         L     RC,=V(TGPRDW)                                                    
         USING TGPRDW,RC                                                        
         USING ELEMD,R2                                                         
         L     R4,0(R1)                                                         
         L     R5,4(R1)                                                         
         MVC   GTPRDH(3),=C'   '                                                
         MVC   GTPRDH(2),0(R4)                                                  
         CLC   4(4,R1),=F'0'                                                    
         BE    *+10                                                             
         MVC   0(2,R5),0(R4)                                                    
         ST    R1,STORE1                                                        
         XC    SIMSTORE,SIMSTORE                                                
         XC    KEYP,KEYP                                                        
         XC    MYREC(20),MYREC                                                  
         MVI   TEFCODE,X'0A'                                                    
         MVI   TERCODE,X'04'                                                    
         MVC   TEAGY,BAGY                                                       
         MVC   TEMED,QMED                                                       
CLT2     GOTO1 =V(CLPACK),MYDMCB,QCLT,TECLT                                     
         CLC   PRAMC,TEAGY                                                      
         BE    *+8                                                              
         MVI   TPFRST,1                                                         
         CLI   TPFRST,1                                                         
         BNE   HAVSIM                                                           
RDPRD    BAS   R9,READD                                                         
         CLC   WORKP(13),KEYP                                                   
         BNE   FNDPNAM                                                          
         BAS   R9,READDIR                                                       
         TM    8(R1),X'50'                                                      
         BZ    *+6                                                              
         DC    H'0'                                                             
         LA    R2,MYREC+24                                                      
GETSIM   CLI   0(R2),X'00'                                                      
         BE    FNDPNAM                                                          
         CLI   0(R2),X'30'                                                      
         BE    *+16                                                             
         XR    R0,R0                                                            
         IC    R0,1(R2)                                                         
         AR    R2,R0                                                            
         B     GETSIM                                                           
HAVSIM   CLI   TPFRST,1                                                         
         BNE   HAVSIM2                                                          
         IC    R3,1(R2)                                                         
         BCTR  R3,R0                                                            
         EX    R3,MOVSIM                                                        
         B     *+10                                                             
MOVSIM   MVC   SIMSTORE(1),0(R2)                                                
HAVSIM2  LA    R2,SIMSTORE                                                      
         CLI   SIMSTORE,0                                                       
         BE    FNDPNAM                                                          
         L     R1,STORE1                                                        
         L     RF,4(R1)                                                         
         LA    R6,7                                                             
         LR    R7,R2                                                            
         SR    R0,R0                                                            
         IC    R0,1(R2)                                                         
         AR    R7,R0                                                            
         L     R9,8(R1)                                                         
         L     RE,0(R1)                                                         
         CLC   TEQISCP,0(RE)                                                    
         BE    *+12                                                             
SMLOOP   BXLE  R2,R6,*-10                                                       
         B     FNDPNAM                                                          
         LTR   R9,R9                                                            
         BZ    *+14                                                             
         MVC   0(3,R9),TEQACTP                                                  
         LA    R9,3(R9)                                                         
         MVC   GTPRDH,TEQACTP                                                   
         LTR   RF,RF                                                            
         BZ    *+10                                                             
         MVC   0(3,RF),TEQACTP                                                  
         B     SMLOOP                                                           
READD    MVC   KEYP(13),MYREC                                                   
         MVC   WORKP(13),KEYP                                                   
         LA    R3,DMRDHI                                                        
         LA    R4,SDIR                                                          
         LA    R5,WORKP                                                         
         LA    R6,KEYP                                                          
         STM   R3,R6,MYDMCB                                                     
         LA    R1,MYDMCB                                                        
         L     RF,=V(DATAMGR)                                                   
         BASR  RE,RF                                                            
         BR    R9                                                               
READDIR  LA    R3,GETDSK                                                        
         LA    R4,SPOT                                                          
         LA    R5,KEYP+14                                                       
         LA    R6,MYREC                                                         
         L     R7,=V(GETWORK)                                                   
         STM   R3,R7,MYDMCB                                                     
         LA    R1,MYDMCB                                                        
         LA    RE,8                                                             
         LA    RF,MYREC                                                         
         XC    0(200,RF),0(RF)                                                  
         LA    RF,200(RF)                                                       
         BCT   RE,*-10                                                          
         L     RF,=V(DATAMGR)                                                   
         BASR  RE,RF                                                            
         BR    R9                                                               
GETPRD   XC    KEYP,KEYP                                                        
         MVC   KEYP+1(1),BAGYMD                                                 
         GOTO1 =V(CLPACK),MYDMCB,QCLT,KEYP+2                                    
         L     R1,STORE1                                                        
         L     R2,0(R1)                                                         
         MVC   KEYP+4(3),GTPRDH                                                 
         MVC   MYREC(20),KEYP                                                   
         BAS   R9,READD                                                         
         TM    8(R1),X'50'                                                      
         BZ    *+6                                                              
         DC    H'0'                                                             
         LR    R8,R7                                                            
         BAS   R9,READDIR                                                       
         TM    8(R1),X'50'                                                      
         BZ    *+6                                                              
         DC    H'0'                                                             
         LR    R7,R8                                                            
         MVC   PHSTORE,PRDHDR                                                   
         MVC   PRDHDR(180),MYREC                                                
         L     R1,STORE1                                                        
         L     R2,12(R1)                                                        
         LTR   R2,R2                                                            
         BZ    *+10                                                             
         MVC   0(20,R2),PNAME                                                   
         MVC   0(3,R7),KEYP+4                                                   
         MVC   3(20,R7),PNAME                                                   
         MVC   PRDHDR(180),PHSTORE                                              
EXIT     XMOD  1                                                                
         LTORG                                                                  
FNDPNAM  CLI   TPFRST,1                                                         
         BE    CLR                                                              
         CLC   PRAMC,TEAGY                                                      
         BNE   CLR                                                              
         LA    R7,PNSTR                                                         
SRCHPN   CLI   0(R7),0                                                          
         BE    GETPRD                                                           
         CLI   0(R7),X'FF'                                                      
         BE    CLR                                                              
         CLC   0(3,R7),GTPRDH                                                   
         BE    *+12                                                             
         LA    R7,23(R7)                                                        
         B     SRCHPN                                                           
         L     R1,STORE1                                                        
         L     R2,12(R1)                                                        
         LTR   R2,R2                                                            
         BZ    EXIT                                                             
         MVC   0(20,R2),3(R7)                                                   
         B     EXIT                                                             
CLR      LA    R7,PNSTR                                                         
         MVI   TPFRST,0                                                         
         LA    RE,5                                                             
         XC    0(200,R7),0(R7)                                                  
         LA    R7,200(R7)                                                       
         BCT   RE,*-10                                                          
         XC    0(49,R7),0(R7)                                                   
         LA    R7,PNSTR                                                         
         MVC   PRAMC,TEAGY                                                      
         B     GETPRD                                                           
TGPRDW   CSECT                     WORK AREA FOR GET PRODUCT                    
TPFRST   DC    X'01'                                                            
GTPRDH   DS   CL3                  ACTUAL PRODUCT CODE                          
PRAMC    DS    CL4                                                              
PNSTR    DS    CL1050                                                           
         DC    X'FFFFFF'                                                        
STORE1   DS    F                                                                
MYDMCB   DS    6F                                                               
MYDUB    DS    D                                                                
MYREC    DS    200D                                                             
PHSTORE  DS    CL180                                                            
MYHALF   DS    H                                                                
WORKP    DS    CL20                                                             
KEYP     DS    CL20                                                             
SIMSTORE DS    CL255                                                            
         ORG   MYREC                                                            
       ++INCLUDE TEKEY                                                          
ELEMD    DSECT                                                                  
       ++INCLUDE TEQELEM                                                        
         PRINT OFF                                                              
       ++INCLUDE RPWORK                                                         
