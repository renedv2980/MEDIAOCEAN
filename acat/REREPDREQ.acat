*          DATA SET REREPDREQ  AT LEVEL 078 AS OF 03/17/15                      
*CATALP REDREQ                                                                  
         TITLE 'REREPDREQ - REDREQ - REP VALIDATE/PRINT REQUEST'                
*******************************************************************             
*                                                                 *             
*  REREPDREQ --- PRINT OUT DETAILS OF REQUEST FOR MULTI JOBS      *             
*                                                                 *             
* --------------------------------------------------------------- *             
*  HISTORY OF CHANGES:                                            *             
*  FEB07/94 (BU ) --- INITIAL INPUT                               *             
*                                                                 *             
*  MAR23/94 (BU ) --- FOR RRG, CHANGE LABEL TO RANK PER/OPTION3   *             
*                                                                 *             
*  APR24/95 (BU ) --- ADD CUR$ TO FOOTNOTE PRINTOUT               *             
*                                                                 *             
*  OCT09/95 (BU ) --- FOR RRG, CHANGE LABEL TO RANK MAX/OPTION1   *             
*                     SUPPRESS NUMERIC TEST                       *             
*                                                                 *             
*                                                                 *             
*                     ***  END TOMBSTONE  ***                     *             
*******************************************************************             
*  PARAM 2   =  A(REQUEST CARDS IN RRG:  RRG ALWAYS PASSING TWO)  *             
*                                                                 *             
*                                                                 *             
*                                                                 *             
*******************************************************************             
*                                                                               
         PRINT NOGEN                                                            
REDREQ   CSECT                                                                  
         NMOD1 256,**RDRQ**,R9                                                  
         USING REQREPD,RC                                                       
         L     RA,0(R1)            RESET A(WORKD)                               
         USING WORKD,RA                                                         
*                                                                               
         L     RF,4(R1)            SET A(REQUEST CARDS)                         
         MVC   REQCARDS(240),0(RF) SAVE REQUEST CARDS                           
         LA    RF,REQCARDS         SET A(REQCARDS)                              
         ST    RF,ACARDS                                                        
         LR    R2,R1                                                            
         XC    0(4,R2),0(R2)                                                    
         MVC   PRSWITCH,RCREQREP                                                
*                                                                               
         L     RE,ABOX                                                          
         MVI   BOXOFF-BOXD(RE),C'Y'                                             
*                                                                               
         BAS   RE,HEADS                PRINT HEADLINE                           
         BAS   RE,PICKTAB              PICK THE RIGHT PROCESS TABLE             
         BAS   RE,PROCESS              PROCESS THE REQUEST CARD                 
         CLI   PRSWITCH,C'N'       OTHER OUTPUT SKIPPED?                        
         BE    MAIN50              YES - NO CARD OUTPUT EITHER                  
         BAS   RE,DOMULTI                                                       
MAIN50   EQU   *                                                                
*                                                                               
         SR    R0,R0                                                            
         LTR   R0,R0                                                            
         XMOD1                                                                  
         EJECT                                                                  
*                   LOOP THROUGH TABLE AND HANDLE REQUESTS                      
         SPACE 2                                                                
PROCESS  NTR1                                                                   
*                                                                               
         L     R3,ATABLE                                                        
*                                                                               
PRSS0020 CLC   COLDISP(3,R3),=C'END'                                            
         BE    PRSS0220                                                         
         CLI   PRESENT(R3),C' '    CHECK FOR CARD PRESENCE?                     
         BE    PRSS0060            NO                                           
         CLI   PRESENT(R3),C'3'    CHECK FOR THIRD CARD?                        
         BE    PRSS0040            YES                                          
         DC    H'0'                NO  - ONLY THIRD CARD IS OPTIONAL            
*                                     AT THIS TIME                              
PRSS0040 EQU   *                                                                
         L     RF,ACARDS           SET A(REQUEST CARDS)                         
         LA    RF,160(RF)          DISPLACE TO THIRD CARD                       
         OC    0(80,RF),0(RF)      IS CARD PRESENT?                             
         BZ    PRSS0200            NO  - SKIP FIELD                             
PRSS0060 EQU   *                                                                
*                                                                               
         PACK  RQDUB,COLDISP(3,R3) GET ADDR/LENGTH OF FIELD                     
         CVB   R4,RQDUB                                                         
         A     R4,ACARDS                                                        
         BCTR  R4,0                                                             
         PACK  RQDUB,DATAWIDE(2,R3)                                             
         CVB   R5,RQDUB                                                         
         BCTR  R5,R0                                                            
         CLI   DATATYPE(R3),C'N'   NUMERIC TESTS                                
         BNE   PRSS0120                                                         
         EX    R5,SPACOMP          SPACES OK                                    
         BE    PRSS0160                                                         
         CLC   0(3,R4),=C'ALL'     AND SO IS ALL                                
         BE    PRSS0160                                                         
         CLI   DATAQUAL(R3),C'Z'   ZONES STRIPPED?                              
         BNE   PRSS0080            NO  - PROCEED                                
         BAS   RE,RESETZNS         YES - RESET ZONES                            
PRSS0080 EQU   *                                                                
         MVC   RQWORK(8),=8X'F0'                                                
         EX    R5,MUVZONES                                                      
         CLC   RQWORK(8),=8X'F0'                                                
         BE    PRSS0160                                                         
         CLI   PRSWITCH,C'Y'                                                    
         BNE   PRSS0100                                                         
         MVC   P+69(17),=C'FIELD NOT NUMERIC'                                   
         MVI   3(R2),1                                                          
         B     PRSS0160                                                         
         SPACE 2                                                                
PRSS0100 MVI   PRSWITCH,C'Y'                                                    
         B     PRSS0220                                                         
         SPACE 2                                                                
SPACOMP  CLC   0(0,R4),SPACES                                                   
MUVZONES MVZ   RQWORK(0),0(R4)                                                  
MUVFIELD MVC   P+45(0),0(R4)                                                    
         SPACE 2                                                                
PRSS0120 EQU   *                                                                
         CLI   DATATYPE(R3),C'D'   DATE TEST?                                   
         BNE   PRSS0160                                                         
         CLC   0(4,R4),SPACES                                                   
         BE    PRSS0160                                                         
         CLI   DATAQUAL(R3),C'D'   DAYS REQUIRED?                               
         BE    PRSS0140            YES                                          
         CLC   4(2,R4),=C'01'      DAY = 01?                                    
         BNE   PRSS0140            NO                                           
         MVC   4(2,R4),=C'  '      YES - ASSUME DAY NOT ORIG ENTERED            
PRSS0140 EQU   *                                                                
         MVC   DATE,0(R4)                                                       
         CLC   DATE+4(2),=C'  '                                                 
         BNE   *+10                                                             
         MVC   DATE+4(2),=C'01'                                                 
         GOTO1 GETDAY,RQWORK,DATE,RQDUB                                         
         CLC   RQDUB(3),SPACES                                                  
         BNE   PRSS0160                                                         
         CLI   PRSWITCH,C'Y'                                                    
         BNE   PRSS0100                                                         
         MVC   P+69(16),=C'NOT A VALID DATE'                                    
         MVI   3(R2),1                                                          
         SPACE 2                                                                
PRSS0160 EQU   *                                                                
         CLI   PRSWITCH,C'Y'       ONLY PRINT IF OPTION ON                      
         BNE   PRSS0200                                                         
         EX    R5,SPACOMP          AND FIELD IS NON-SPACES                      
         BE    PRSS0200                                                         
         MVC   P+24(20),FLDTITLE(R3)                                            
         EX    R5,MUVFIELD                                                      
*                                                                               
         CLI   DATATYPE(R3),C'L'   LOWER CASE FOR EXCLUDE FILTER                
         BNE   PRSS0180                                                         
         CLI   0(R4),X'C0'         LOWER CASE                                   
         BH    PRSS0180                                                         
         EX    R5,*+8                                                           
         B     *+10                                                             
         MVC   P+46(0),P+45                                                     
         OI    P+46,X'40'         TURN ON UPPER CASE                            
         MVI   P+45,C'*'           INDICATE EXCLUDE                             
*                                                                               
PRSS0180 BAS   RE,PRINT1                                                        
         SPACE 2                                                                
PRSS0200 EQU   *                                                                
         LA    R3,L'RRGQ2TAB(R3)                                                
         B     PRSS0020                                                         
*                                                                               
PRSS0220 EQU   *                                                                
         SR    R0,R0                                                            
         LTR   R0,R0                                                            
         XIT1                                                                   
         EJECT                                                                  
*                                                                               
*  RESETZNS - RESET THE ZONE BITS FOR THE FIELD IN QUESTION                     
*       R5  =  L(FIELD)                                                         
*       R4  =  A(FIELD)                                                         
RESETZNS NTR1                                                                   
         LA    R5,1(R5)            INCREMENT LEN BY 1                           
RZNS0010 EQU   *                                                                
         OI    0(R4),X'F0'         TURN ON ZONE BITS                            
         LA    R4,1(R4)            BUMP TO NEXT POSITION                        
         BCT   R5,RZNS0010         GO BACK AND SET NEXT                         
         XIT1                      FINISHED                                     
         EJECT                                                                  
*                   PRINT SOME MINIMAL HEADLINES                                
         SPACE 2                                                                
HEADS    NTR1                                                                   
*                                                                               
         CLI   PRSWITCH,C'Y'                                                    
         BNE   HEADEXIT                                                         
         MVC   P,SPACES                                                         
         MVC   P+24(6),=C'RUN ON'                                               
         GOTO1 DATCON,RQWORK,(4,RCDATE),(5,P+31)                                
         MVC   P+45(30),=C'DETAILS OF REQUEST      PAGE 1'                      
         GOTO1 PRINT,RQWORK,SPACES-1,=C'BC01'                                   
         BAS   RE,PRINT1                                                        
         MVC   P+45(30),=C'------------------      ------'                      
         MVC   P+28(2),=C'AT'                                                   
         TIME                                                                   
         SRL   R0,4                                                             
         LR    R1,R0                                                            
         XC    RQDUB,RQDUB                                                      
         ST    R1,RQWORK                                                        
         MVC   RQDUB+5(3),RQWORK                                                
         OI    RQDUB+7,X'0F'                                                    
         CVB   R3,RQDUB                                                         
         EDIT  (R3),(5,P+31),2,DUB=RQDUB,WRK=RQWORK                             
         BAS   RE,PRINT3                                                        
         SPACE 2                                                                
         MVC   P+24(25),=C'REQUEST FIELD        DATA'                           
         MVC   P+69(6),=C'ERRORS'                                               
         BAS   RE,PRINT1                                                        
         MVC   P+24(25),=C'-------------        ----'                           
         MVC   P+69(6),=C'------'                                               
         BAS   RE,PRINT3                                                        
*                                                                               
HEADEXIT EQU   *                                                                
         SR    R0,R0                                                            
         LTR   R0,R0                                                            
         XIT1                                                                   
         EJECT                                                                  
*                                                                               
* -----  PICKTAB - FIND THE REQUEST CARD(S) AND PICK THE RIGHT TABLE            
*                                                                               
PICKTAB  NTR1                                                                   
*                                                                               
         LA    RF,RRGQ2TAB         SET TO 2 CARD RRG                            
*                                                                               
PICK100  EQU   *                                                                
         ST    RF,ATABLE           STORE TABLE ADDR                             
*                                                                               
         SR    R0,R0                                                            
         LTR   R0,R0                                                            
         XIT1                                                                   
         EJECT                                                                  
*                                                                               
*- DOMULTI - FOR MULTIPLE CARD REQUESTS, PRINT OUT CARD IMAGES                  
*                                                                               
DOMULTI  NTR1                                                                   
*                                                                               
         BAS   RE,PRINT1           BLANK LINE                                   
         MVC   P(14),=C'COLUMN NUMBERS'                                         
         MVC   P+25(80),COLS                                                    
         BAS   RE,PRINT1                                                        
*                                                                               
         LA    R3,3                # REQUEST CARDS TO PRINT                     
*                                  FOR TIME BEING, RRG PRINTS 2                 
         L     R2,ACARDS                                                        
         LA    R4,1                COUNTER FOR EDIT                             
*                                                                               
DMUL10   EQU   *                                                                
         OC    0(80,R2),0(R2)                                                   
         BZ    DMUL20                                                           
         MVC   P(21),=C'REQUEST CARD NN IMAGE'                                  
         EDIT  (R4),(2,P+13),FILL=0                                             
         MVC   P+25(80),0(R2)                                                   
         BAS   RE,PRINT1                                                        
DMUL20   EQU   *                                                                
         LA    R2,80(R2)                                                        
         LA    R4,1(R4)                                                         
         BCT   R3,DMUL10                                                        
*                                                                               
DMULEXIT EQU   *                                                                
         SR    R0,R0                                                            
         LTR   R0,R0                                                            
         XIT1                                                                   
         SPACE                                                                  
COLS     DC    CL40'1...5...10...15...20...25...30...35...40'                   
         DC    CL40'...45...50...55...60...65...70...75...80'                   
         SPACE 2                                                                
         EJECT                                                                  
*                   PRINTING AIDS                                               
         SPACE 2                                                                
PRINT1   MVC   RQCON,=C'BL01'                                                   
         B     GOPRINT                                                          
         SPACE 2                                                                
PRINT2   MVC   RQCON,=C'BL02'                                                   
         B     GOPRINT                                                          
         SPACE 2                                                                
PRINT3   MVC   RQCON,=C'BL03'                                                   
         B     GOPRINT                                                          
         SPACE 2                                                                
GOPRINT  NTR1                                                                   
         GOTO1 PRINT,RQWORK,P-1,RQCON                                           
         MVC   P,SPACES                                                         
         XIT1                                                                   
         EJECT                                                                  
*                                                                               
* DISP   REQUEST FIELD TABLES                                                   
* ----   --------------------                                                   
*   0    CL3  -  COLUMN NUMBER (RELATIVE TO 1ST POSITION, 1ST CARD)             
*  +3    CL2  -  DATA WIDTH                                                     
*  +5    CL1  -  DATA TYPE                                                      
*                A = ALPHA                                                      
*                D = DATE                                                       
*                N = NUMERIC                                                    
*  +6    CL1  -  DATA TYPE QUALIFIERS                                           
*                Z = ZONES HAVE BEEN STRIPPED FROM NUMERIC                      
*                D = DAYS REQUIRED ON DATES FIELDS                              
*  +7    CL1  -  FOOTER CONTROL                                                 
*                F = PLACE IN GENERATED FOOT LINE(S)                            
*                - = DO NOT USE IN FOOT LINE(S)                                 
*  +8    CL1  -  CARD PRESENCE INDICATOR                                        
*                # = CHECK FOR CARD PRESENCE (NOT BINARY ZEROS)                 
*                    WHERE # IS CARD # (CARDS 1 AND 2 WILL ALWAYS               
*                    BE PRESENT)                                                
*  +9    CL20 -  REQUEST FIELD TITLE                                            
*                                                                               
* ------------------------------------------------------------------            
*  DISPLACEMENT EQUATES                                                         
COLDISP  EQU   0                                                                
DATAWIDE EQU   3                                                                
DATATYPE EQU   5                                                                
DATAQUAL EQU   6                                                                
FOOTCTRL EQU   7                                                                
PRESENT  EQU   8                                                                
FLDTITLE EQU   9                                                                
*                                                                               
*                                                                               
*                                                                               
*        TABLE OF REQUEST FIELDS (2-CARD RRG)                                   
*                                                                               
         SPACE 2                                                                
RRGQ2TAB DS    0CL29                                                            
*                             1         2         3         4                   
*                   0.2.4.6.8.0.2.4.6.8.0.2.4.6.8.0.2.4.6.8.0                   
         DC    CL29'07802A - REQUEST NUMBER'                                    
         DC    CL29'00302A - REPRESENTATIVE'                                    
         DC    CL29'00502A - REQUESTING OFFICE'                                 
         DC    CL29'00712A - REQUESTOR'                                         
         DC    CL29'01902A F OFFICE REGION'                                     
         DC    CL29'02102A F OFFICE'                                            
         DC    CL29'02305A F STATION'                                           
         DC    CL29'02801A F SALES DIVISION'                                    
         DC    CL29'02901A F SALES TEAM'                                        
         DC    CL29'03003A F SALESPERSON'                                       
         DC    CL29'03304A F AGENCY'                                            
         DC    CL29'03702A F AGENCY OFFICE'                                     
         DC    CL29'03904A F ADVERTISER'                                        
         DC    CL29'04303A F PRODUCT'                                           
         DC    CL29'04602A F PRODUCT CLASS'                                     
         DC    CL29'04802A F PRODUCT CATEGORY'                                  
         DC    CL29'05006D - START DATE'                                        
         DC    CL29'05606D - END DATE'                                          
         DC    CL29'06206DDF AS AT DATE'                                        
         DC    CL29'06801A - MONTH TYPE (BASIS)'                                
         DC    CL29'06901A F STATION TYPE'                                      
         DC    CL29'07001A - SEQUENCE'                                          
         DC    CL29'07102A F RANK MAX/OPTION1'                                  
         DC    CL29'07301A F RECAP OPTION'                                      
         DC    CL29'07401A F STATION GROUP'                                     
         DC    CL29'07501A F STATION SUBGROUP'                                  
         DC    CL29'07601L F CONTRACT TYPE'                                     
         DC    CL29'07701A F RANK PERIOD/OPTION3'                               
         DC    CL29'08101A - MONTH FORMAT'                                      
         DC    CL29'08202A F TVB REGION'                                        
         DC    CL29'08403A F OWNER'                                             
         DC    CL29'08704A F MARKET'                                            
         DC    CL29'09101N F RANK'                                              
         DC    CL29'09203A F POINT PERSON'                                      
         DC    CL29'09508A F NETWORK CON #'                                     
         DC    CL29'10308N F CONTRACT NUMBER'                                   
         DC    CL29'11103A F AFFILIATION'                                       
         DC    CL29'11401A - EXPANDED REQ'                                      
         DC    CL29'11501A F RATING SERVICE'                                    
         DC    CL29'11606A F RATING BOOK'                                       
         DC    CL29'12205A F DEMO CATEGORY'                                     
         DC    CL29'12706DDF AS-AT TO DATE'                                     
         DC    CL29'13301N F CREDIT RATING'                                     
         DC    CL29'13402N F LIABILITY POSITION'                                
         DC    CL29'13901NZ- SPACING CONTROL'                                   
         DC    CL29'16601A F3CUR$'                                              
         DC    CL29'23801A F3BEST $ OPTION'                                     
         DC    C'END'                                                           
         EJECT                                                                  
*                   DSECT FOR MODULE                                            
         SPACE 2                                                                
REQREPD  DSECT                                                                  
RQDUB    DS    D                                                                
ATABLE   DS    A                   A(PRINT CONTROL TABLE)                       
ACARDS   DS    A                   A(REQUEST CARD(S))                           
RQWORK   DS    CL24                                                             
PRSWITCH DS    CL1                                                              
RQCON    DS    CL4                                                              
DATE     DS    CL6                                                              
MULTIC   DS    CL1                 MULTI-CARD REQUEST FLAG                      
         DS    0F                                                               
REQCARDS DS    CL320               CARD STORAGE AREA                            
         PRINT OFF                                                              
         EJECT                                                                  
       ++INCLUDE REREPWORKD                                                     
         EJECT                                                                  
       ++INCLUDE DDBIGBOX                                                       
         EJECT                                                                  
       ++INCLUDE REXADDRD                                                       
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'078REREPDREQ 03/17/15'                                      
         END                                                                    
