*          DATA SET DDLABELS2  AT LEVEL 001 AS OF 07/31/87                      
*CATALP LABELS2                                                                 
         TITLE 'LABELS2 - PRINTS LABELS, 5 LINES, 2 ACROSS.'                    
*        IT IS INTENDED FOR LABELS 3 1/2 INCH BY 15/16 INCH LABELS.             
*        THE PARMS ARE THE SAME AS DDLABELS3. THERE IS A 5X34 LINE              
*        ARRAY.                                                                 
         PRINT NOGEN                                                            
LABELS2  CSECT                                                                  
         NMOD1 8,**LAB2**                                                       
         L     R9,0(R1)                                                         
         CLC   =C'$OPEN',0(R9)                                                  
         BE    FRSTTEST                                                         
         CLC   =C'$CLOS',0(R9)                                                  
         BNE   R9LD                                                             
         MVC   CLOSE,0(R9)                                                      
         MVI   FIRST,C'L'                                                       
         CP    ACROSS,=P'0'        IS ARRAY EMPTY                               
         BE    NDT                 YES, DON'T FLUSH - JUST END                  
         B     PRINTIT                                                          
R9LD     L     R9,0(R1)                                                         
         USING LINESD,R9                                                        
         L     R2,NOMBRE                                                        
NOTFRST  L     R5,SVPAD            LOAD PRINT ADDR                              
         MVC   0(34,R5),LINE1      1ST LINE OF LABEL                            
         LA    R5,70(R5)           GET TO LINE 2 AREA                           
         MVC   0(34,R5),LINE2      2ND LINE OF LABEL                            
         LA    R5,70(R5)           GET TO LINE 3 AREA                           
         MVC   0(34,R5),LINE3                                                   
         LA    R5,70(R5)           GET TO LINE 4 AREA                           
         MVC   0(34,R5),LINE4                                                   
         LA    R5,70(R5)           GET TO LINE 5 AREA                           
         MVC   0(34,R5),LINE5                                                   
         AP    ACROSS,=P'1'                                                     
         CP    ACROSS,=P'2'        ARE WE 2 ACROSS YET                          
         BNL   PRINTIT             YES                                          
         L     R5,SVPAD                                                         
         LA    R5,36(R5)                                                        
         ST    R5,SVPAD                                                         
         B     NDT                 NO                                           
         EJECT                                                                  
PRINTIT  ZAP   ACROSS,=P'0'        ZERO OUT ACROSS COUNTER                      
         LA    R5,ARRAY                                                         
LOOPRX   MVC   PCTL,=C'BL01'       SETUP SINGLE SPACE                           
LOOPR    MVC   P+1(70),0(R5)                                                    
         GOTO1 PRINT,DMCB,P,PCTL     PRINT A LINE                               
         LA    R5,70(R5)                                                        
         AP    ACROSS,=P'1'                                                     
         CP    ACROSS,=P'4'        UP TO LINE 4                                 
         BL    LOOPR               NO                                           
         MVI   PCTL+3,C'2'         YES, LINE 5 TAKES 2 SP AFTER                 
         CP    ACROSS,=P'5'        ALL DONE                                     
         BL    LOOPR               NO                                           
         MVI   PCTL+3,C'2'                                                      
         MVI   P,C' '              YES, GO TO NEXT LABEL SET                    
         MVC   P+1(L'P-1),P                                                     
         GOTO1 PRINT,DMCB,P,PCTL                                                
         CLI   FIRST,C'F'                                                       
         BE    NOCLR                                                            
         MVC   ARRAY(70),SPACES    CLEAR ARRAY                                  
         MVC   ARRAY2(140),ARRAY                                                
         MVC   ARRAY4(140),ARRAY                                                
NOCLR    LA    R5,ARRAY                                                         
         ST    R5,SVPAD            BACK TO START FOR NEXT                       
         ZAP   ACROSS,=P'0'        DITTO ACROSS COUNTER.                        
         CLI   FIRST,C'F'          FIRST TIME                                   
         BNE   NDT                                                              
         SP    CTLN,=P'1'                                                       
         BZ    *+8                                                              
         B     LOOPRX                                                           
NOLINUP  MVC   ARRAY(70),SPACES                                                 
         MVC   ARRAY2(140),ARRAY                                                
         MVC   ARRAY4(140),ARRAY                                                
         MVI   FIRST,C'N'          YES,NOT ANY MORE                             
         B     XIT                                                              
FRSTTEST CLI   FIRST,C'F'          WERE WE CLOSED                               
         BE    FRSTM               YES, START NEW ARRAY, ETC                    
         B     SPCL                GO TO SPECIAL START                          
FRSTM    MVI   ARRAY,C'*'          DO LINEUP                                    
         MVC   ARRAY+1(32),ARRAY                                                
         MVC   ARRAY+36(33),ARRAY                                               
         MVC   ARRAY2(70),ARRAY                                                 
         MVC   ARRAY3(70),ARRAY                                                 
         MVC   ARRAY4(70),ARRAY                                                 
         MVC   ARRAY5(70),ARRAY                                                 
         MVC   PRINT,4(R1)                                                      
SPCL     L     R2,8(R1)            HOW MANY OF EACH                             
         LTR   R2,R2                                                            
         BNZ   *+8                                                              
         LH    R2,=H'1'                                                         
         ST    R2,NOMBRE                                                        
         CLI   FIRST,C'F'          IF NOT CLOSED DON'T REFRESH PRINT            
         BNE   XIT                 POSITION ADDRESS OR DO LINEUPS               
         LA    R5,ARRAY                                                         
         ST    R5,SVPAD                                                         
         L     R2,12(R1)           NUMBER OF LINEUP SETS                        
         LTR   R2,R2                                                            
         BZ    NOLINUP                                                          
         CVD   R2,DOUBLE                                                        
         ZAP   CTLN,DOUBLE                                                      
         B     PRINTIT                                                          
         SPACE 2                                                                
NDT      CLI   FIRST,C'L'          WAS IT A CLOSE                               
         BNE   CHKLIM                                                           
         CLI   CLOSE+5,C'P'        FORCE CLOSE OF PRINT (CLOSP PASSED)          
         BNE   MVIF                                                             
         GOTO1 PRINT,DMCB,=C'CLOSE'                                             
MVIF     MVI   FIRST,C'F'          RESET FOR NEXT REQUEST                       
XIT      XIT1                                                                   
         SPACE 1                                                                
CHKLIM   BCT   R2,NOTFRST                                                       
         B     XIT                                                              
         EJECT                                                                  
FIRST    DC    C'F'                                                             
SVPAD    DS    F                                                                
CLOSE    DS    CL6                                                              
P        DC    CL132' '                                                         
SPACES   DC    CL132' '                                                         
CTLN     DS    PL1                                                              
PRINT    DS    F                                                                
PCTL     DC    CL4'BC01'                                                        
ACROSS   DC    PL1'0'                                                           
DOUBLE   DS    D                                                                
DMCB     DS    6F                                                               
NOMBRE   DS    F                                                                
ARRAY    DS    CL36           LINE 1 ARRAY                                      
         DS    CL34                                                             
ARRAY2   DS    CL36           LINE 2 ARRAY                                      
         DS    CL34                                                             
ARRAY3   DS    CL36           LINE 3 ARRAY                                      
         DS    CL34                                                             
ARRAY4   DS    CL36           LINE 4 ARRAY                                      
         DS    CL34                                                             
ARRAY5   DS    CL36           LINE 5 ARRAY                                      
         DS    CL34                                                             
         SPACE 3                                                                
         LTORG                                                                  
         SPACE 2                                                                
LINESD   DSECT                                                                  
LINE1    DS    CL34           NAME LINE                                         
LINE2    DS    CL34           STREET                                            
LINE3    DS    CL34           CITY/STATE/(ZIP FOR US)                           
LINE4    DS    CL34           ZIP FOR CANADA                                    
LINE5    DS    CL34           ATTN LINE                                         
         SPACE 3                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'001DDLABELS2 07/31/87'                                      
         END                                                                    
