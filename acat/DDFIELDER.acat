*          DATA SET DDFIELDER  AT LEVEL 018 AS OF 05/01/02                      
*CATALP FIELDER                                                                 
         TITLE 'FIELDER - GENERIC FIELD/ELEMENT MAPPING PROGRAM'                
*                                                                               
***********************************************************************         
*        PARAM1 - BYTE 0     NOT USED                                           
*                 BYTES 1-3  A(FLDRBLK)                                         
***********************************************************************         
         SPACE 1                                                                
FIELDER  CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 FDRWRKL,**FLDR**                                                 
         USING FDRWRKD,RC                                                       
         L     R9,0(R1)            A(CONTROL BLOCK)                             
         USING FLDRBLKD,R9                                                      
         L     RF,FLDRACOM         GET COMFACS ADDRESSES                        
         USING COMFACSD,RF                                                      
         MVC   AMINIO,CMINIO                                                    
         L     RA,FLDRMINB         A(MINBLKD)                                   
         USING MINBLKD,RA                                                       
         MVI   SPACES,C' '                                                      
         MVC   SPACES+1(L'SPACES-1),SPACES                                      
*                                                                               
         CLI   FLDRMODE,C'V'       VALIDATE                                     
         BE    VAL                                                              
         CLI   FLDRMODE,C'D'       DISPLAY                                      
         BE    DISP                                                             
         CLI   FLDRMODE,C'F'       FIELD                                        
         BE    FIELD                                                            
         DC    H'0'                                                             
         EJECT                                                                  
***********************************************************************         
*        'VALIDATION' - SCREEN FIELDS TO ELEMS                                  
***********************************************************************         
         SPACE 1                                                                
VAL      DS    0H                                                               
         XC    MINEKEY,MINEKEY     CLEAR ALL EXISTING FIELD ELEMS               
         LA    R3,MINEKEY                                                       
         USING FLDELEMD,R3                                                      
         MVI   FLDELEM,FLDELCQ                                                  
         MVI   MINFILTL,1                                                       
         LA    R0,MINHI                                                         
         B     VAL04B                                                           
*                                                                               
VAL04    DS    0H                                                               
         LA    R0,MINSEQ                                                        
VAL04B   DS    0H                                                               
         OC    MINMKEY,MINMKEY     IF NOT MINIO SET PRESENT                     
         BZ    VAL06               SKIP ELEM WORK                               
         GOTO1 AMINIO,DMCB,((R0),MINBLKD)                                       
         CLI   MINERR,0            TREAT ALL ERRORS AS EOF                      
         BNE   VAL06                                                            
         GOTO1 (RF),(R1),('MINDEL',MINBLKD)                                     
         CLI   MINERR,0                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
         B     VAL04                                                            
*                                                                               
VAL06    DS    0H                                                               
         XC    SEQTAB,SEQTAB       CLEAR SEQ TAB                                
         L     R2,FLDRFLD          A(FIRST FIELD)                               
         BAS   RE,BUMP2                                                         
*                                                                               
VAL10    DS    0H                                                               
         ZIC   RF,0(R1)            FIELD NUMBER (NOTE- R1 SET BY BUMP)          
         IC    RE,SEQTAB(RF)       BUMP SEQ NUMBER                              
         LA    RE,1(RE)                                                         
         STC   RE,SEQTAB(RF)                                                    
*                                                                               
         CLI   5(R2),0             TEST ANY INPUT                               
         BE    VAL14                                                            
*                                                                               
         L     R3,MINELEM                                                       
         XC    0(100,R3),0(R3)                                                  
         MVI   FLDELEM,FLDELCQ                                                  
         STC   RF,FLDELFLD         FIELD NUMBER                                 
         STC   RE,FLDELSEQ         SEQ NUMBER                                   
         ZIC   RF,5(R2)            DATA LENGTH                                  
         BCTR  RF,0                                                             
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   FLDELDTA(0),8(R2)                                                
         LA    RF,FLDELFXL+1(RF)   ACTUAL ELEM LENGTH                           
         STC   RF,FLDELLEN                                                      
         GOTO1 AMINIO,DMCB,('MINADD',MINBLKD)                                   
         CLI   MINERR,0                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
VAL14    DS    0H                                                               
         BAS   RE,BUMP             NEXT SCREEN FIELD                            
         BNE   VAL10                                                            
*                                                                               
         B     EXIT                                                             
         EJECT                                                                  
***********************************************************************         
*        DISPLAY - ELEMS TO SCREEN FIELDS                                       
***********************************************************************         
         SPACE 1                                                                
DISP     DS    0H                                                               
         XC    SEQTAB,SEQTAB                                                    
         L     R2,FLDRFLD          A(FIRST FIELD)                               
         BAS   RE,BUMP2                                                         
*                                                                               
DSP04    DS    0H                                                               
         ZIC   RF,0(R1)            FIELD NUMBER (NOTE- R1 SET BY BUMP)          
         STC   RF,HALF                                                          
         IC    RE,SEQTAB(RF)       BUMP SEQUENCE NUMBER                         
         LA    RE,1(RE)                                                         
         STC   RE,SEQTAB(RF)                                                    
         STC   RE,HALF+1                                                        
         LA    R3,MINEKEY                                                       
         XC    MINEKEY,MINEKEY                                                  
         MVI   FLDELEM,FLDELCQ                                                  
         MVC   FLDELFLD-1(2),HALF     FIELD NUMBER AND SEQ NUMBER               
         MVI   MINFILTL,3                                                       
         MVC   FDATA,SPACES                                                     
         OC    MINMKEY,MINMKEY     IF NO MINIO SET PRESENT                      
         BZ    DSP08               DONT LOOK FOR ELEMS                          
         GOTO1 AMINIO,DMCB,('MINHI',MINBLKD)                                    
         CLI   MINERR,0                                                         
         BNE   DSP08                                                            
*                                                                               
         L     R3,MINELEM                                                       
         ZIC   RF,1(R3)                                                         
         SH    RF,=Y(FLDELFXL+1)                                                
         CH    RF,=Y(L'FDATA-1)                                                 
         BNH   *+8                                                              
         LA    RF,L'FDATA-1                                                     
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   FDATA(0),FLDELDTA                                                
*                                                                               
DSP08    DS    0H                                                               
         BAS   RE,DSPFLD                                                        
         BAS   RE,BUMP                                                          
         BNE   DSP04                                                            
*                                                                               
         B     EXIT                                                             
         EJECT                                                                  
***********************************************************************         
*        FIELD - GET INDIVIDUAL FIELD                                           
*              - FLDRFLDN IS FIELD NUMBER TO GET                                
*              - TEXT IS PUT IN FLDRTXT                                         
*              - NOTE- ONLY FIRST FIELD WITH GIVEN NUMBER IS FETCHED            
***********************************************************************         
         SPACE 1                                                                
FIELD    DS    0H                                                               
         LA    R3,MINEKEY                                                       
         XC    MINEKEY,MINEKEY                                                  
         MVI   FLDELEM,FLDELCQ                                                  
         MVC   FLDELFLD-1(1),FLDRFLDN   FIELD NUMBER                            
         MVI   FLDELSEQ-1,1             LOOK FOR FIRST                          
         MVI   MINFILTL,3                                                       
         MVC   FLDRTXT,SPACES                                                   
         OC    MINMKEY,MINMKEY     IF NO MINIO SET PRESENT                      
         BZ    FLD08               DONT LOOK FOR ELEMS                          
         GOTO1 AMINIO,DMCB,('MINHI',MINBLKD)                                    
         CLI   MINERR,0                                                         
         BNE   FLD08                                                            
*                                                                               
         L     R3,MINELEM                                                       
         ZIC   RF,1(R3)                                                         
         SH    RF,=Y(FLDELFXL+1)                                                
         CH    RF,=Y(L'FLDRTXT-1)                                               
         BNH   *+8                                                              
         LA    RF,L'FLDRTXT-1                                                   
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   FLDRTXT(0),FLDELDTA                                              
*                                                                               
FLD08    DS    0H                                                               
         B     EXIT                                                             
         EJECT                                                                  
***********************************************************************         
*        MISCELLANEOUS LITTLE ROUTINES                                          
***********************************************************************         
         SPACE 1                                                                
*        BUMP TO NEXT POSSIBLE FIELD                                            
*        AND POINT R1 TO EXTENDED HDR                                           
         SPACE 1                                                                
BUMP     DS    0H                                                               
         ZIC   R0,0(R2)                                                         
         AR    R2,R0                                                            
         CLI   0(R2),0             END OF SCREEN, RETURN =                      
         BER   RE                                                               
         C     R2,FLDRLST          AT LAST FIELD?                               
         BNH   BUMP2                                                            
         CR    RE,RE                                                            
         BR    RE                                                               
*                                                                               
BUMP2    TM    1(R2),X'20'         IGNORE PROTECTED FIELDS                      
         BNZ   BUMP                                                             
         TM    1(R2),X'02'         AND ONES WITHOUT EXTENDED HDRS               
         BZ    BUMP                                                             
         ZIC   R1,0(R2)                                                         
         AR    R1,R2                                                            
         SH    R1,=H'8'                                                         
         CLI   0(R1),0             MUST HAVE FIELD NUMBER                       
         BE    BUMP                                                             
         BR    RE                  HAVE GOOD FIELD, RETURN NOT =                
         SPACE 1                                                                
*        FIELD DISPLAY ROUTINE - DISPLAY FDATA IN FIELD AT R2                   
         SPACE 1                                                                
DSPFLD   NTR1                                                                   
         ZIC   R3,0(R2)            FIND FLD LENGTH                              
         SH    R3,=H'17'                                                        
         ZIC   RE,5(R2)            PAD FLD WITH TRAILING SPACES                 
         LR    RF,R3               (NOT SIMPLE OC BECAUSE OF L.CASE)            
         SR    RF,RE                                                            
         BM    DSPFLD2             NOTHING TO PAD                               
         LA    RE,8(R2,RE)         RE = A(FIRST TRAILING CHARACTER)             
         EX    RF,DSPFOC           PAD SPACES                                   
DSPFLD2  EX    R3,DSPFCLC          CHECK FOR CHANGE                             
         BE    DSPFLDX                                                          
         EX    R3,DSPFMVC          MOVE IN NEW VALUE                            
         OI    6(R2),X'80'         TRANSMIT                                     
         LA    R3,1(R3)            RESET INPUT LENGTH                           
         LA    RE,7(R2,R3)                                                      
         CLI   0(RE),C' '                                                       
         BH    *+8                                                              
         BCT   R3,*-12                                                          
         STC   R3,5(R2)                                                         
DSPFLDX  XIT1                                                                   
DSPFOC   OC    0(0,RE),SPACES                                                   
DSPFCLC  CLC   8(0,R2),FDATA                                                    
DSPFMVC  MVC   8(0,R2),FDATA                                                    
*                                                                               
EXIT     DS    0H                                                               
         XIT1                                                                   
         EJECT                                                                  
         LTORG                                                                  
         SPACE 3                                                                
FDRWRKD  DSECT                                                                  
DMCB     DS    6F                                                               
DUB      DS    D                                                                
FULL     DS    F                                                                
HALF     DS    H                                                                
BYTE     DS    X                                                                
AMINIO   DS    A                                                                
WORK     DS    XL64                                                             
SEQTAB   DS    XL256                                                            
SPACES   DS    CL80                                                             
FDATA    DS    CL80                                                             
FDRWRKL  EQU   *-FDRWRKD                                                        
         SPACE 2                                                                
*                                                                               
         EJECT                                                                  
*                                                                               
       ++INCLUDE DDFLDRBLK                                                      
*                                                                               
         PRINT OFF                                                              
       ++INCLUDE DDCOMFACS                                                      
       ++INCLUDE DDMINBLK                                                       
         PRINT ON                                                               
*                                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'018DDFIELDER 05/01/02'                                      
         END                                                                    
