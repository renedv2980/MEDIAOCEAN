*          DATA SET DDBRDMON   AT LEVEL 007 AS OF 05/12/03                      
*CATALP BRDMON                                                                  
         TITLE 'BROADCAST MONTH CONVERSION'                                     
*                             PAR 1 = A(6-BYTE YYMMDD DATE)                     
*                             PAR 2 = A(6-BYTE YYMMDD RESULT)                   
*                              IF 1ST PARAMETER HAS X'FF' IN HIGH               
*                              ORDER BYTE THEN BOTH PARAMETERS ARE              
*                              IN 2-BYTE COMPRESSED DATE FORMAT                 
*                                                                               
BRDMON   CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 WORKX-WORKD,*BRDMON*,RR=R5                                       
         USING WORKD,RC                                                         
*                                                                               
         L     R2,0(R1)            INPUT ADDRESS                                
         L     R3,4(R1)            OUTPUT ADDRESS                               
         MVC   PAR1,0(R2)                                                       
*                                                                               
         MVI   CENTURY,19                                                       
         CLI   PAR1,C'9'           TEST Y2K DATE COMING IN                      
         BNH   B8                                                               
         MVI   CENTURY,20                                                       
         SR    R0,R0                                                            
         IC    R0,PAR1                                                          
         SH    R0,=H'10'                                                        
         STC   R0,PAR1                                                          
*                                                                               
B8       MVI   INPCOMP,C'N'        ASSUME INPUT IS NOT COMPRESSED               
         CLI   0(R1),X'FF'         TEST INPUT IS COMPRESSED                     
         BNE   B100                NO                                           
*                                                                               
         MVI   INPCOMP,C'Y'        YES - UNPACK DATE                            
         SR    R4,R4                                                            
         IC    R4,0(R2)                                                         
         SRL   R4,1                                                             
         CVD   R4,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  PAR1(2),DUB         YEAR                                         
*                                                                               
B10      IC    R4,0(R2)                                                         
         SLL   R4,31                                                            
         SRL   R4,23                                                            
         IC    R4,1(R2)                                                         
         SRL   R4,5                                                             
         CVD   R4,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  PAR1+2(2),DUB       MONTH                                        
*                                                                               
         IC    R4,1(R2)                                                         
         SLL   R4,27                                                            
         SRL   R4,27                                                            
         CVD   R4,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  PAR1+4(2),DUB        DAY                                         
         EJECT                                                                  
* SEE IF DATE IS WITHIN THE BROADCAST MONTH                                     
*                                                                               
B100     LA    RE,HIDAYS           A(TABLE)                                     
         PACK  DUB,PAR1+2(2)       INPUT MONTH                                  
         CVB   RF,DUB                                                           
         SR    R1,R1                                                            
*                                                                               
         IC    R1,0(RE)            FIND NO. OF DAYS IN THE MONTH                
         LA    RE,1(RE)                                                         
         BCT   RF,*-8                                                           
         LR    R4,R1               SAVE                                         
         CHI   R4,28               LAND ON FEB?                                 
         BNE   B125                NO                                           
*                                                                               
         PACK  DUB,PAR1(2)         SEE IF ITS A LEAP YEAR                       
         CVB   R7,DUB                                                           
         SR    R6,R6                                                            
         LA    R8,4                                                             
         DR    R6,R8                                                            
         CHI   R6,0                LEAP YEAR?                                   
         BNE   B125                NOPE                                         
         LA    R1,29               BOTH R1 & R4 NEED TO BE SET TO 29            
         LR    R4,R1                                                            
*                                                                               
B125     PACK  DUB,PAR1+4(2)       INPUT DAY                                    
         CVB   R6,DUB              IN R6                                        
         SR    R1,R6                                                            
         CH    R1,=H'7'                                                         
         BNL   B200                                                             
*                                                                               
         L     RF,=V(GETDAY)                                                    
         AR    RF,R5               ADD RELOCATION CONSTANT                      
         GOTO1 (RF),DMCB,PAR1,DUB                                               
         CLI   DMCB,0              TEST VALID DAY OF WEEK RETURNED              
         BNE   *+6                 YES                                          
         DC    H'0'                                                             
*                                                                               
         LA    R5,7                NO. OF DAYS/WEEK                             
         ZIC   RF,DMCB             DAY NUMBER                                   
         SR    R5,RF               DISPLACMENT TO SUNDAY                        
         AR    R5,R6               PLUS INPUT DAY                               
*                                                                               
         CR    R5,R4               TEST GIVEN DATE WITHIN BROAD MONTH           
         BNH   B200                YES                                          
*                                                                               
         MVC   PAR1+4(2),=C'01'    RETURN FIRST OF NEXT MONTH                   
         PACK  DUB,PAR1+2(2)       MONTH                                        
         CVB   R5,DUB                                                           
         CH    R5,=H'12'           TEST DECEMBER                                
         BL    B150                NO                                           
*                                                                               
         SR    R5,R5               RESET MONTH                                  
         PACK  DUB,PAR1(2)         INCREMENT YEAR                               
         CP    DUB,=P'99'                                                       
         BNH   *+8                                                              
         MVI   CENTURY,20                                                       
         AP    DUB,=P'1'                                                        
         OI    DUB+7,X'0F'                                                      
         UNPK  PAR1(2),DUB                                                      
*                                                                               
B150     LA    R5,1(R5)            MONTH                                        
         CVD   R5,DUB                                                           
         UNPK  PAR1+2(2),DUB+6(2)                                               
         OI    PAR1+3,X'F0'                                                     
*                                                                               
B200     CLI   INPCOMP,C'Y'        2-BYTE COMPRESSED DATE FORMAT?               
         BE    B300                YES                                          
         MVC   0(6,R3),PAR1                                                     
         CLI   CENTURY,20                                                       
         BNE   RETURN                                                           
         IC    R0,0(R3)                                                         
         AH    R0,=H'10'                                                        
         STC   R0,0(R3)            RETURN Y2K DATE FORMAT                       
         B     RETURN                                                           
*                                                                               
* PUT RESULT IN 2-BYTE COMPRESSED DATE FIELD                                    
*                                                                               
B300     PACK  DUB,PAR1(2)         YEAR                                         
         CVB   R4,DUB                                                           
         CH    R4,=H'27'           YEARS BELOW 27 ARE 2000+                     
         BH    *+8                                                              
         LA    R4,100(R4)                                                       
         SLL   R4,9                                                             
*                                                                               
         PACK  DUB,PAR1+2(2)       MONTH                                        
         CVB   R5,DUB                                                           
         SLL   R5,5                                                             
         OR    R4,R5                                                            
*                                                                               
         PACK  DUB,PAR1+4(2)       DAY                                          
         CVB   R5,DUB                                                           
         OR    R4,R5                                                            
*                                                                               
         STC   R4,1(R3)            STORE DATE IN PAR 2                          
         SRL   R4,8                                                             
         STC   R4,0(R3)                                                         
*                                                                               
RETURN   XMOD1                                                                  
         SPACE 2                                                                
HIDAYS   DC    AL1(31,28,31,30,31,30,31,31,30,31,30,31)                         
         SPACE 2                                                                
         LTORG                                                                  
         SPACE 2                                                                
WORKD    DSECT                                                                  
DUB      DS    D                                                                
DMCB     DS    6F                  PARAMETER LIST                               
PAR1     DS    CL6                 INPUT YYMMDD                                 
INPCOMP  DS    C                   'Y' IF INPUT IS COMPRESSED                   
CENTURY  DS    X                                                                
WORKX    EQU   *                                                                
         SPACE 3                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'007DDBRDMON  05/12/03'                                      
         END                                                                    
