*          DATA SET REINVCODE  AT LEVEL 005 AS OF 01/04/80                      
*CATALP INVCODE                                                                 
         TITLE 'VALIDATE CODE EXPRESSIONS - BUILD CODE ELEMENTS'                
*              PARA 1    BYTE 1-3  A(FIELD HEADER)                              
*                                  BYTE 0 SET TO X'FF' IF ERROR                 
*              PARA 2    BYTE 0    NUMBER OF ELEMENTS TO BE BUILT               
*                        BYTE 1-3  A(OUTPUT AREA)                               
*              PARA 3    BYTE 1-3  A(SCANNER)                                   
*                                                                               
INVCODE  CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 30,INVCODE                                                       
         USING WORKD,RC                                                         
         LR    R2,R1                                                            
         XR    R3,R3                                                            
         IC    R3,4(R2)            NUMBER TO BUILD                              
         L     R1,4(R2)            OUTPUT AREA                                  
         SPACE 1                                                                
INVCD2   MVC   0(2,R1),=X'FF0A'    CODE AND LENGTH                              
         MVC   2(2,R1),=C'  '      PROGRAM CODE                                 
         XC    4(2,R1),4(R1)       TEXT                                         
         MVI   6(R1),C' '          BOOK                                         
         XC    7(3,R1),7(R1)       SPARE                                        
         LA    R1,10(R1)                                                        
         BCT   R3,INVCD2           NEXT AREA                                    
         EJECT                                                                  
         L     R4,0(R2)            A(HEADER)                                    
         XR    R5,R5                                                            
         IC    R5,5(R4)            LENGTH OF INPUT                              
         LTR   R5,R5                                                            
         BZ    EXXMOD                                                           
         IC    R5,4(R2)            NUMBER                                       
         MVC   SCANNER,8(R2)                                                    
         SPACE 1                                                                
         GOTO1 SCANNER,DMCB,(R4),((R5),BLOCK)                                   
         CLI   DMCB+4,0                                                         
         BE    INVERR                                                           
         XR    R5,R5                                                            
         IC    R5,DMCB+4                                                        
         L     R3,4(R2)            OUTPUT AREA                                  
         LA    R6,BLOCK                                                         
         SPACE 1                                                                
INVCD3   OC    0(2,R6),0(R6)                                                    
         BZ    INVNEXT             NO INPUT                                     
         CLI   1(R6),0                                                          
         BE    INVONE              ONE SIDED                                    
         SPACE 1                                                                
         MVI   0(R3),X'CD'         ELEMENT CODE                                 
         CLI   0(R6),2                                                          
         BNE   INVERR                                                           
         TM    2(R6),X'40'                                                      
         BNO   INVERR                                                           
         MVC   2(2,R3),12(R6)      PROGRAM CODE                                 
         SPACE 1                                                                
         TM    3(R6),X'80'                                                      
         BNO   INVERR                                                           
         CLC   8(4,R6),=F'9999'                                                 
         BH    INVERR                                                           
         MVC   4(2,R3),10(R6)      TEXT CODE                                    
         B     INVNEXT                                                          
         SPACE 1                                                                
INVONE   MVI   0(R3),X'CD'         VALID CODE                                   
         TM    2(R6),X'40'                                                      
         BO    INVPRG              MUST BE A PROGRAM CODE                       
         TM    2(R6),X'80'                                                      
         BNO   INVERR                                                           
         CLC   4(4,R6),=F'9999'                                                 
         BH    INVERR                                                           
         MVC   4(2,R3),6(R6)       TEXT NUMBER                                  
         B     INVNEXT                                                          
         SPACE 1                                                                
INVPRG   CLI   0(R6),2                                                          
         BNE   INVERR                                                           
         MVC   2(2,R3),12(R6)                                                   
         SPACE 1                                                                
INVNEXT  LA    R3,10(R3)                                                        
         LA    R6,32(R6)                                                        
         BCT   R5,INVCD3                                                        
         B     EXXMOD                                                           
         SPACE 1                                                                
INVERR   MVI   0(R2),X'FF'                                                      
EXXMOD   XMOD  1                                                                
         SPACE 2                                                                
WORKD    DSECT                                                                  
SCANNER  DS    A                                                                
DMCB     DS    6F                                                               
BLOCK    DS    5CL32                                                            
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'005REINVCODE 01/04/80'                                      
         END                                                                    
