*          DATA SET NEGOALIO   AT LEVEL 024 AS OF 05/01/02                      
*CATALP NEGOALIO                                                                
         TITLE 'GOAL READING MODULE FOR NETWORK PERFORMANCE'                    
NEGOALIO CSECT                                                                  
******************************************************************              
*  CALLED BY NEMED67.                                                           
*  THIS IS A CONVERSION OF THE OLD PERFORMANCE REPORT (SPSPL47,67).             
*  IN ORDER TO FACILITATE CONVERSION, THIS REPORT CONFIGURES MEMORY             
*  SLIGHTLY DIFFERENT THAN OTHER REPORTS. IT ASSUMES W/S COMES RIGHT            
*  AFTER THE NETBLOCK.                                                          
*                                                                               
*  AT ENTRY:                                                                    
*                                                                               
*   R9 ->  NETSYSD                                                              
*                                                                               
*          NETBLOCK                                                             
*                                                                               
*          NET DEMO BLOCK                                                       
*                                                                               
*          DBLOCK                                                               
*                                                                               
*          ARGS FROM EDIT TO PRINT MODULE                                       
*                                                                               
*          ARGS  TO/FROM GOALIO                                                 
*                                                                               
*          400 BYTES OF LOCAL W/S                                               
*                                                                               
*   R7 ->  GLOBAL W/S: USED FOR SEVERAL PURPOSES BY PRINT MODULE                
*                                                                               
*   R7+3200 - INTERNAL PRDLIST                                                  
*                                                                               
****** REMEMBER **** THIS STRUCTURE IS HARD CODED. CHANGES MUST ALSO            
*                    BE MADE IN NEMED67 AND NEGOALIO.                           
*                                                                               
*                                                                               
******************************************************************              
*                                                                               
         PRINT NOGEN                                                            
         NMOD1 2,**GLIO**                                                       
         USING NETSYSD,R9                                                       
         L     RC,0(R1)                                                         
         USING GEND,RC                                                          
         L     RA,ATWA                                                          
         USING T808FFD,RA                                                       
         L     R8,ASPOOLD                                                       
         USING SPOOLD,R8                                                        
         LA    R7,GLOBALWS                                                      
         LA    R7,3200(R7)         USE R7 TO POINT TO PRDLIST                   
*                                                                               
         NETGO NVSETSPT,DMCB       ALL READING FROM SPOT FILE                   
*                                                                               
         CLI   GOALMODE,GOALFRST                                                
         BNE   GOAL18                                                           
         L     R2,NPRDS                                                         
         GOTO1 XSORT,DMCB,(R7),(R2),8,1,0                                       
         EJECT                                                                  
*              BUILD CPP GUIDE POOL                                             
         SPACE 3                                                                
         LA    R4,KEY                                                           
         USING GOALREC,R4                                                       
         LA    R2,H1+4             USE HEADLINE AREA                            
         SR    R3,R3                                                            
         USING CPPD,R2                                                          
         XC    KEY,KEY                                                          
         MVI   GKEYTYPE,X'02'                                                   
         MVC   GKEYAM(3),NBACTAM                                                
         MVI   GKEYPRD,X'FF'                                                    
         LA    R5,160                                                           
         MVC   FILENAME,=C'SPTDIR '                                             
         GOTO1 HIGH                                                             
         SPACE 2                                                                
CPP2     CLC   KEY(5),KEYSAVE                                                   
         BNE   CPP99                                                            
         CLC   GKEYEST,NBSELEST                                                 
         BE    CPP4                                                             
         BH    CPP3                                                             
         MVC   GKEYEST,NBSELEST                                                 
         MVC   FILENAME,=C'SPTDIR '                                             
         GOTO1 HIGH                GET TO RECORD FOR THIS EST                   
         CLC   KEY(5),KEYSAVE                                                   
         BNE   CPP99                                                            
         CLC   GKEYEST,NBSELEST                                                 
         BE    CPP4                                                             
         SPACE 2                                                                
CPP3     MVC   DUB(2),KEYSAVE+5    SKIP TO NEXT MARKET                          
         LH    R1,DUB                                                           
         LA    R1,1(R1)                                                         
         STH   R1,DUB                                                           
         MVC   GKEYMKT,DUB                                                      
         MVC   GKEYEST,NBSELEST                                                 
         MVI   GKEYDPT,0                                                        
         MVC   FILENAME,=C'SPTDIR '                                             
         GOTO1 HIGH                                                             
         B     CPP2                                                             
         SPACE 2                                                                
CPP4     CLI   NBSELDP,0           FILTER DAYPART                               
         BE    CPP6                                                             
         CLC   NBSELDP,GKEYDPT                                                  
         BNE   CPP14                                                            
         SPACE 2                                                                
CPP6     MVC   FILENAME,=C'SPTFIL '                                             
         GOTO1 GETREC                                                           
         LA    R6,IO                                                            
         MVI   ELCODE,X'21'                                                     
         BAS   RE,GETEL                                                         
         USING GLEMENT,R6                                                       
         SPACE 2                                                                
CPP8     MVC   CPPMKT,GKEYMKT                                                   
         MVC   CPPSTART,GLWEEK                                                  
         MVC   CPPEND,GLWEEK                                                    
         MVC   CPPDP,GKEYDPT                                                    
         L     R1,GLBUDGET                                                      
         M     R0,=F'20'                                                        
         D     R0,=F'100'                                                       
         SR    R0,R0                                                            
         D     R0,GLGRP                                                         
         AH    R1,=H'1'                                                         
         SRA   R1,1                                                             
         ST    R1,CPPRATE                                                       
         LR    R1,R2                                                            
         SH    R1,=H'11'           SEE IF PREVIOUS ENTRY IS FOR THE             
         CLC   0(7,R1),0(R2)       SAME RATE/DP/MARKET                          
         BNE   CPP10                                                            
         MVC   9(2,R1),CPPEND      IF SO CHANGE ITS END DATE                    
         B     CPP12                                                            
         SPACE 2                                                                
CPP10    LA    R3,1(R3)            IF NOT THIS IS A NEW ENTRY                   
         LA    R2,11(R2)                                                        
         BCT   R5,CPP12                                                         
         B     CPP99                                                            
         SPACE 2                                                                
CPP12    BAS   RE,NEXTEL                                                        
         BE    CPP8                                                             
         SPACE 2                                                                
CPP14    MVC   FILENAME,=C'SPTDIR '                                             
         GOTO1 SEQ                                                              
         B     CPP2                                                             
         SPACE 2                                                                
CPP99    DS    0H                                                               
         ST    R3,H1                                                            
         EJECT                                                                  
*              CONTROL READING OF GOALS                                         
         SPACE 2                                                                
GOAL2    LR    R3,R7                                                            
         B     GOAL6                                                            
         SPACE 2                                                                
GOAL4    L     R3,PAKPOINT         POSITION TO PRODUCT LIST                     
         LA    R3,8(R3)                                                         
         SPACE 2                                                                
         USING LISTD,R3                                                         
GOAL6    ST    R3,PAKPOINT                                                      
         OC    0(8,R3),0(R3)                                                    
         BZ    GOAL38                                                           
         CLI   LISTNO,0                                                         
         BE    GOAL4                                                            
         LA    R4,KEY              BUILD KEY FOR THIS PRODUCT                   
         XC    KEY,KEY                                                          
         USING GOALREC,R4                                                       
         MVI   GKEYTYPE,X'02'                                                   
         MVC   GKEYAM(3),NBACTAM                                                
         MVC   GKEYPRD,LISTNO                                                   
         MVC   GKEYMKT,SELMKT                                                   
         MVC   GKEYEST,NBSELEST                                                 
         MVC   FILENAME,=C'SPTDIR '                                             
         GOTO1 HIGH                                                             
         B     GOAL10                                                           
         SPACE 2                                                                
GOAL8    LA    R4,KEY                                                           
         MVC   FILENAME,=C'SPTDIR '                                             
         GOTO1 SEQ                                                              
         SPACE 2                                                                
GOAL10   CLC   KEY(5),KEYSAVE      CHECK MATCH ON BRAND                         
         BNE   GOAL4                                                            
         OC    SELMKT,SELMKT                                                    
         BZ    GOAL12                                                           
         CLC   KEY(7),KEYSAVE                  OR MARKET IF SPECIFIED           
         BNE   GOAL4                                                            
         SPACE 2                                                                
GOAL12   CLC   GKEYEST,NBSELEST    HAVE WE GOT THE RIGHT ESTIMATE               
         BE    GOAL14                                                           
         BH    GOAL13                                                           
         MVC   GKEYEST,NBSELEST    NO - SO SKIP TO THAT POINT                   
         MVC   FILENAME,=C'SPTDIR '                                             
         GOTO1 HIGH                                                             
         CLC   KEY(5),KEYSAVE                                                   
         BNE   GOAL4                                                            
         CLC   KEY(8),KEYSAVE      DO WE HAVE RIGHT ESTIMATE NOW                
         BE    GOAL14                                                           
         SPACE 2                                                                
GOAL13   OC    SELMKT,SELMKT         NO - IF ONE MARKET WAS SELECTED            
         BNZ   GOAL4                    GO AND GET NEXT PRODUCT                 
         MVC   DUB(2),KEYSAVE+5         OTHERWISE SKIP TO NEXT MKT              
         LH    R1,DUB                                                           
         LA    R1,1(R1)                                                         
         STH   R1,DUB                                                           
         MVC   GKEYMKT,DUB                                                      
         MVC   GKEYEST,NBSELEST                                                 
         MVI   GKEYDPT,0                                                        
         MVC   FILENAME,=C'SPTDIR '                                             
         GOTO1 HIGH                                                             
         MVC   KEYSAVE+5(2),KEY+5                                               
         B     GOAL10                                                           
         SPACE 2                                                                
GOAL14   CLI   NBSELDP,0           CHECK DP FILTER                              
         BE    GOAL16                                                           
         CLC   NBSELDP,GKEYDPT                                                  
         BNE   GOAL8                                                            
         EJECT                                                                  
*              CONTROL PROCESSING OF GOALS                                      
         SPACE 3                                                                
GOAL16   MVC   FILENAME,=C'SPTFIL '                                             
         GOTO1 GETREC                                                           
         LA    R6,IO                                                            
         MVI   ELCODE,X'21'                                                     
         BAS   RE,GETEL                                                         
         B     GOAL20                                                           
         SPACE 2                                                                
GOAL18   L     R6,BUYPOINT                                                      
         BAS   RE,NEXTEL                                                        
         SPACE 2                                                                
GOAL20   BNE   GOAL8                                                            
         USING GLEMENT,R6                                                       
         LA    R4,IO                                                            
         ST    R6,BUYPOINT                                                      
         MVC   GOALDP,GKEYDPT      PASS USER BACK GOAL VALUES                   
         MVC   GOALBRND,GKEYPRD                                                 
         MVC   GOALMKT,GKEYMKT                                                  
         L     R1,GLBUDGET                                                      
         SR    R0,R0                                                            
         D     R0,=F'100'                                                       
         ST    R1,GOALDOL                                                       
         MVC   GOALGRP,GLGRP                                                    
         LA    RF,1                                                             
         LA    R1,WKLIST           LOOK UP WEEK                                 
         L     R0,NUMWKS                                                        
         SPACE 2                                                                
GOAL22   CLC   GLWEEK,0(R1)        MATCH ON START                               
         BE    GOAL26                                                           
         CLC   GLWEEK,2(R1)        BEFORE END - MAY BE OK                       
         BNH   GOAL24                                                           
         LA    R1,4(R1)                                                         
         LA    RF,1(RF)                                                         
         BCT   R0,GOAL22                                                        
         B     GOAL18                                                           
*                                                                               
GOAL24   EQU   *                   ??? DONT UNDERSTAND THIS REMAINING??         
***********************          LOGIC SO COMMENTED OUT                         
******** CLC   ESTDATES(6),USERQSTR             IF EST DATES IN USE             
******** BNE   GOAL18                                                           
*                                                                               
GOAL26   STC   RF,GOALWEEK                                                      
         MVI   GOALMODE,PROCGOAL                                                
         OC    GOALDOL,GOALDOL     IF EITHER DOLLARS                            
         BZ    GOAL28                                                           
         OC    GOALGRP,GOALGRP               OR POINTS ARE ZERO,                
         BZ    GOAL28                                                           
         B     XIT                                                              
         SPACE 2                                                                
GOAL28   LA    R2,H1+4             CHECK CPP GUIDE                              
         L     R0,H1                                                            
         LTR   R0,R0                                                            
         BZ    XIT                                                              
         USING CPPD,R2                                                          
         SPACE 2                                                                
GOAL30   CLC   CPPMKT,GOALMKT      MUST MATCH ON MARKET                         
         BE    GOAL32                                                           
         CLC   CPPMKT,=H'777'                   (777 IS GLOBAL)                 
         BE    GOAL32                                                           
         CLC   CPPMKT,=H'7777'                  (7777 IS GLOBAL)                
         BNE   GOAL36                                                           
         SPACE 2                                                                
GOAL32   CLC   CPPDP,GOALDP                   AND DAYPART                       
         BNE   GOAL36                                                           
         CLC   GLWEEK,CPPSTART     AND FIT BETWEEN START AND END                
         BL    GOAL36                                                           
         CLC   GLWEEK,CPPEND                                                    
         BH    GOAL36                                                           
         OC    GOALDOL,GOALDOL                                                  
         BZ    GOAL34                                                           
         L     R1,GOALDOL          GRP = $ / CPP                                
         M     R0,=F'10'                                                        
         D     R0,CPPRATE                                                       
         ST    R1,GOALGRP                                                       
         B     XIT                                                              
         SPACE 2                                                                
GOAL34   L     R1,GOALGRP          $ = GRP X CPP                                
         M     R0,CPPRATE                                                       
         D     R0,=F'10'                                                        
         ST    R1,GOALDOL                                                       
         B     XIT                                                              
         SPACE 2                                                                
GOAL36   LA    R2,11(R2)                                                        
         BCT   R0,GOAL30                                                        
         B     XIT                                                              
         SPACE 2                                                                
GOAL38   MVI   H1,C' '                                                          
         MOVE  (H1+1,1847),H1                                                   
         MVI   GOALMODE,GOALLAST                                                
*                                                                               
XIT      NETGO NVSETUNT,DMCB       RESET TO UNITFILE                            
         XIT1                                                                   
         SPACE 2                                                                
         GETEL (R6),DATADISP,ELCODE                                             
         EJECT                                                                  
*              DSECTS ETC                                                       
         SPACE 3                                                                
*                                                                               
LISTD    DSECT                                                                  
LISTNO   DS    CL1                                                              
LISTDIV  DS    CL2                                                              
LISTPRD  DS    CL3                                                              
LISTTARG DS    CL1                                                              
LISTACNO DS    CL1                                                              
         SPACE 2                                                                
CPPD     DSECT                                                                  
CPPRATE  DS    CL4                                                              
CPPMKT   DS    CL2                                                              
CPPDP    DS    CL1                                                              
CPPSTART DS    CL2                                                              
CPPEND   DS    CL2                                                              
         SPACE 2                                                                
         EJECT                                                                  
T808FFD  DSECT                                                                  
         DS    CL64                                                             
CONHEADH DS    0C                                                               
*                                                                               
         EJECT                                                                  
       ++INCLUDE SPGENGOAL                                                      
*                                                                               
         PRINT OFF                                                              
       ++INCLUDE NETINCLS                                                       
         PRINT ON                                                               
*                                                                               
       ++INCLUDE NEPERFINCL                                                     
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'024NEGOALIO  05/01/02'                                      
         END                                                                    
