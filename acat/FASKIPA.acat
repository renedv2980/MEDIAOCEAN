*          DATA SET FASKIPA    AT LEVEL 007 AS OF 05/01/02                      
*CATALP FASKIPA                                                                 
*                                                                               
*&&      SET   AP=N                                                             
*                                                                               
* 3/16/84 INSERT *&&AP FOR OS/DOS 2260 RESETPL LOGIC                            
*                                                                               
         TITLE 'SKIPA -  SKIP/ACTV TERMINAL - OPEN/CLOSE LINE'                  
*PARAMS FOR TERMINAL ORIENTED CALL                                              
*                                                                               
*PARAM1  F     C'A'=TERM ACTIVATE   AL3(UTL ENTRY)                              
*              C'S'=TERM SKIP       AL3(UTL ENTRY)                              
*              C'T'=TERM TEST STAT  AL3(UTL ENTRY)                              
*                                                                               
*PARAM2  CL1   C'S'=SPECIFIC POLL   N/D                                         
*              C'G'=GENERAL POLL    N/D                                         
*              C'X'=ANY POLL        N/D                                         
         SPACE 1                                                                
*PARAMS FOR LINE ORIENTED CALL                                                  
*                                                                               
*PARAM1  F     C'L'=LINE ACTION     AL3(DECB)                                   
*                                                                               
*PARAM0  F     C'A'=LINE CU/DV ACTV AL3(CU0000/CUDV00/0000IX)                   
*              C'S'=LINE CU/DV SKIP AL3(CU0000/CUDV00/0000IX)                   
*              C'T'=LINE CU/DV TEST AL3(CU0000/CUDV00/0000IX)                   
*              C'O'=LINE OPEN       N/D                                         
*              C'C'=LINE CLOSE      N/D                                         
*              C'R'=LINE RESETPOLL  N/D                                         
         SPACE 1                                                                
*PARAM3  F     C'XTIM' IF CALLER HAS SUSPENDED TIMER HIMSELF                    
         SPACE 1                                                                
*RETURN VALUE                                                                   
*                                                                               
*PARAM2  CL1   C'+'    ACTV FOR ACTN T                                          
*              C'-'    SKIP FOR ACTN T                                          
*              ZERO    COMPLETED OK FOR ALL OTHER ACTIONS                       
*              NONZERO FAILED FOR ALL OTHER ACTIONS                             
         SPACE 1                                                                
         PRINT NOGEN                                                            
SKIPA    CSECT                                                                  
         NMOD1 SKIPWKX-SKIPWK,**SKIP**                                          
         USING SKIPWK,RC           RC=A(TEMP W/S)                               
         LR    RA,R1               RA=A(PARMS)                                  
         MVI   RSLT,X'00'                                                       
         MVI   RSLL,X'00'                                                       
         MVI   TIMER,C'Y'          SET TIMER ACTIVE                             
         CLC   8(4,RA),=C'XTIM'                                                 
         BNE   *+8                                                              
         MVI   TIMER,C'X'          SET NO TIMER REQUIRED                        
*                                                                               
         CLI   0(RA),C'L'          LINE ACTION                                  
         BE    LIN                                                              
         B     TRM                 TERM ACTION                                  
*                                                                               
EXIT     BAS   RE,RSET             RESET TIMER IF INACTIVE                      
         XMOD1 1                                                                
         EJECT                                                                  
*TERMINAL ORIENTED ACTIONS                                                      
*                                                                               
TRM      CLI   0(RA),C'A'          TEST TERM ACTION                             
         BE    TRM2                                                             
         CLI   0(RA),C'S'                                                       
         BE    TRM2                                                             
         CLI   0(RA),C'T'                                                       
         BE    TRM2+4                                                           
         DC    H'0'                DIE IF INVALID TERM ACTION                   
*                                                                               
TRM2     BAS   RE,SSET             SUSPEND TIMER                                
         L     R2,0(RA)            R2=A(UTL)                                    
         USING UTLD,R2                                                          
         LA    R2,0(R2)                                                         
         L     R1,TDECB                                                         
         USING DECBD,R1                                                         
         ST    R1,ADECB            SAVE A(DECB FOR LINE)                        
         MVC   APOLL,DECBPOLL      SAVE A(POLLING LIST)                         
         MVC   ACTN,0(RA)          SAVE ACTN (A/S/T)                            
         MVC   TYPE,4(RA)          SAVE TYPE (G/S/X)                            
         MVI   4(RA),0             SET N/F RETURN                               
*                                                                               
         GETLA (R2),SKIPLINE       GET LINE/ADDR AND SET CC                     
         MVI   SKIPIX,0                                                         
         LA    RF,SABS                                                          
         BNZ   *+8                 TEST BSC OR S/S TERM                         
         LA    RF,SASS                                                          
         BASR  RE,RF               SKIP/ACTV/TEST TERM IN POLL LIST             
*                                                                               
TRMX     MVC   4(1,RA),RSLT        RETURN TERMINAL ACTION RESULT                
         B     EXIT                                                             
         EJECT                                                                  
*LINE ORIENTED ACTIONS                                                          
*                                                                               
LIN      L     R1,0(RA)            SAVE A(LINE DECB)                            
         USING DECBD,R1                                                         
         LA    R1,0(R1)                                                         
         ST    R1,ADECB            SAVE A(DECB FOR LINE)                        
         MVC   APOLL,DECBPOLL      SAVE A(POLLING LIST FOR LINE)                
         MVC   ADTF,DECBDTF        SAVE A(BTAM DTF/DCB)                         
*                                                                               
LINO     CLI   4(RA),C'O'          OPEN LINE                                    
         BNE   LINC                                                             
         BAS   RE,SSET             SUSPEND TIMER                                
         BAS   R9,OPN                                                           
         B     LINX                                                             
*                                                                               
LINC     CLI   4(RA),C'C'          CLOSE LINE                                   
         BNE   LINR                                                             
         BAS   RE,SSET             SUSPEND TIMER                                
         BAS   R9,CLS                                                           
         B     LINX                                                             
*                                                                               
LINR     CLI   4(RA),C'R'          RESET POLL ON LINE                           
         BNE   LINAST                                                           
         BAS   RE,SSET             SUSPEND TIMER                                
         BAS   R9,RSP                                                           
         B     LINX                                                             
*                                                                               
LINAST   CLI   4(RA),C'A'          ACTIVATE CU/DV FOR LINE                      
         BE    LINAST2                                                          
         CLI   4(RA),C'S'          SKIP CU/DV FOR LINE                          
         BE    LINAST2                                                          
         CLI   4(RA),C'T'          TEST CU/DV FOR LINE                          
         BE    LINAST2+4                                                        
         DC    H'0'                DIE IF INVALID LINE ACTION                   
*                                                                               
LINAST2  BAS   RE,SSET             SUSPEND TIMER                                
         L     RE,4(RA)            IF CUDV=0000 THEN USE INDEX ONLY             
         MVC   SKIPCU(3),0(RE)     IF DV=00 THEN USE CU ONLY                    
         MVC   ACTN,4(RA)                                                       
         MVI   TYPE,0                                                           
         LA    RF,SABS                                                          
         TM    DECBTYPE,DECBTBSC   TEST S/S OR BSC LINE                         
         BO    LINAST4                                                          
         TM    DECBTYPE,DECBT378                                                
         BO    LINAST4                                                          
         LA    RF,SASS                                                          
LINAST4  BASR  RE,RF               SKIP/ACTV/TEST POLL FOR CU/DV                
         MVC   RSLL,RSLT                                                        
*                                                                               
LINX     MVC   4(1,RA),RSLL        RETURN LINE ACTION RESULT                    
         B     EXIT                                                             
         EJECT                                                                  
*SKIP/ACTIVATE S/S TERMINAL                                                     
*                                                                               
SASS     NTR1                                                                   
         MVI   RSLT,X'01'          SET NOT COMPLETED RETURN                     
         MVI   SAVERSLT,X'10'      SET FIRST PASS                               
         L     R4,APOLL            GET A(POLLING LIST)                          
         CLI   TYPE,C'G'                                                        
         BNE   *+8                                                              
         MVI   SKIPDV,X'FF'        SET SEARCH FOR GENERAL POLL                  
*                                                                               
SASS2    CLI   SKIPCU,0            TEST IF INDEX MATCH ONLY                     
         BNE   SASS2A                                                           
         MVC   INDEX,2(R4)         EXTRACT INDEX FROM S/S POLL                  
         NI    INDEX,X'0F'                                                      
         CLC   INDEX,SKIPIX        MATCH ON INDEX                               
         BE    SASS6                                                            
         B     SASS4                                                            
SASS2A   CLC   0(1,R4),SKIPCU      MATCH ON CU                                  
         BNE   SASS4                                                            
         CLI   SKIPDV,0            TEST IF CU MATCH ONLY                        
         BE    SASS6                                                            
         CLC   1(1,R4),SKIPDV      MATCH ON DV                                  
         BE    SASS6                                                            
*                                                                               
SASS4    EQU   *                                                                
*&&DO*&& TM    2(R4),X'80'         DOS END OF LIST BIT                          
*&&DO*&& BO    SASS14                                                           
*&&OS*&& CLI   3(R4),X'FF'         MVS END OF LIST HAS -VE OFFSET               
*&&OS*&& BE    SASS14                                                           
         LA    R4,3(R4)            BUMP TO NEXT                                 
         B     SASS2                                                            
*                                                                               
SASS6    CLI   ACTN,C'S'           SKIP/ACTV ACTIONS                            
         BNE   SASS8                                                            
         TM    2(R4),X'40'         TEST TERMINAL ON SKIP                        
         BO    SASS4                                                            
         OI    2(R4),X'40'         SET TO SKIP                                  
         B     SASS12                                                           
SASS8    CLI   ACTN,C'A'                                                        
         BNE   SASS10                                                           
         TM    2(R4),X'40'         TEST TERMINAL ACTIVE                         
         BZ    SASS4                                                            
         NI    2(R4),X'BF'         SET TO ACTIVE                                
         B     SASS12                                                           
*                                                                               
SASS10   MVI   RSLT,C'+'           TEST ACTION                                  
         TM    2(R4),X'40'                                                      
         BZ    XIT                                                              
         MVI   RSLT,C'-'                                                        
         B     XIT                                                              
*                                                                               
SASS12   MVI   RSLT,X'00'          SET ACTION COMPLETED RESULT                  
         B     SASS4                                                            
*                                                                               
SASS14   CLI   TYPE,C'G'           G OR S REQUIRE ONLY ONE PASS OF LIST         
         BE    XIT                                                              
         CLI   TYPE,C'S'                                                        
         BE    XIT                                                              
         CLI   SAVERSLT,X'10'      TEST SECOND PASS COMPLETED                   
         BE    SASS16              NO                                           
         CLI   RSLT,X'00'          OK IF SUCCEEDED ON SCND PASS                 
         BE    XIT                                                              
         CLI   SAVERSLT,X'00'                                                   
         BNE   XIT                                                              
         MVI   RSLT,X'00'          OK IF SUCCEEDED ON FRST PASS                 
         B     XIT                                                              
*                                                                               
SASS16   MVC   SAVERSLT,RSLT       SAVE CONDITION FROM FIRST PASS               
         MVI   RSLT,X'01'                                                       
         MVI   SKIPDV,X'FF'        SET SEARCH TO GENERAL                        
         L     R4,APOLL            RE-SET LIST POINTER                          
         B     SASS2               DO SECOND PASS OF LIST                       
         EJECT                                                                  
*SKIP/ACTIVATE BSC TERM                                                         
*DOS FORMAT    X'0101XX',X'40407F7F2D00',X'37',X'FE'                            
*MVS FORMAT    X'0202XX',X'40407F7F2D01',X'373737373702',X'FE'                  
*                                                                               
SABS     NTR1                                                                   
         MVI   RSLT,X'01'          SET NOT COMPLETED RETURN                     
         MVI   SAVERSLT,X'10'      SET FIRST PASS                               
         CLI   TYPE,C'G'                                                        
         BNE   *+8                                                              
         MVI   SKIPDV,X'7F'        SET SEARCH FOR GENERAL POLL                  
*                                                                               
SABS1    L     R4,APOLL            R4=A(POLLING LIST)                           
         LR    R7,R4                                                            
         SH    R4,=H'3'                                                         
*&&DO*&& MVI   SAVEEOAL,1          DOS END OF ACTIVE LIST LEN                   
*&&OS*&& MVI   SAVEEOAL,6          MVS END OF ACTIVE LIST LEN                   
         SR    R8,R8                                                            
         IC    R8,1(R4)            GET NUM OF ACTIVE ENTRYS                     
*&&OS*&& BCTR  R8,0                MVS HAS EOT SEQ IN ACTIVE LIST               
         MH    R8,=H'6'                                                         
         AR    R8,R7               R8=A(END OF ACTIVE LIST)                     
         SR    R2,R2               R2=NUM OF ACTIVE TERMS                       
         SR    R3,R3               R3=NUM OF SKIP TERMS                         
         XC    ACTVLST,ACTVLST                                                  
         XC    SKIPLST,SKIPLST                                                  
         LA    R5,ACTVLST          R5=A(NEXT ACTIVE ENTRY)                      
         LA    R6,SKIPLST          R6=A(NEXT SKIP ENTRY)                        
*                                                                               
SABS2    CR    R7,R8               TEST FOR END OF ACTIVE LIST                  
         BE    SABS7                                                            
         CLI   ACTN,C'S'           TEST IF SKIP                                 
         BNE   SABS2B                                                           
         CLI   SKIPCU,0            TEST IF MATCH ON INDEX                       
         BNE   SABS2A                                                           
         CLC   5(1,R7),SKIPIX                                                   
         BE    SABS4                                                            
         B     SABS2B                                                           
SABS2A   CLC   1(1,R7),SKIPCU      TEST CU                                      
         BNE   SABS2B                                                           
         CLI   SKIPDV,0            TEST IF ONLY CU REQUIRED                     
         BE    SABS4                                                            
         CLC   2(1,R7),SKIPDV      TEST DV                                      
         BE    SABS4                                                            
SABS2B   MVC   0(6,R5),0(R7)       MOVE TO ACTIVE LIST                          
         LA    R5,6(R5)            BUMP POINTER                                 
         LA    R2,1(R2)            BUMP COUNT                                   
         B     SABS6                                                            
*                                                                               
SABS4    MVC   0(6,R6),0(R7)       MOVE TO SKIP LIST                            
         LA    R6,6(R6)            BUMP POINTER                                 
         LA    R3,1(R3)            BUMP COUNT                                   
         MVI   RSLT,X'00'          SET ENTRY SKIPPED                            
*                                                                               
SABS6    LA    R7,6(R7)            BUMP LIST POINTER                            
         B     SABS2                                                            
*                                                                               
SABS7    CLI   0(R7),X'37'         TEST FOR END OF ACTIVE LIST CHR              
         BNE   DEATH                                                            
         SR    R1,R1               GET LENGTH OF END OF ACTIVE LIST             
         IC    R1,SAVEEOAL                                                      
         BCTR  R1,0                                                             
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   SAVEEOA(0),0(R7)                                                 
         LA    R7,1(R1,R7)         BUMP PAST END OF ACTIVE LIST                 
*                                                                               
SABS8    CLI   0(R7),X'FE'         LOOK FOR STOP CHAR                           
         BE    SABS14                                                           
         CLI   ACTN,C'A'           TEST IF ACTIVATE                             
         BNE   SABS8B                                                           
         CLI   SKIPCU,0            TEST IF MATCH ON INDEX                       
         BNE   SABS8A                                                           
         CLC   5(1,R7),SKIPIX                                                   
         BE    SABS10                                                           
         B     SABS8B                                                           
SABS8A   CLC   1(1,R7),SKIPCU      TEST CU                                      
         BNE   SABS8B                                                           
         CLI   SKIPDV,0            TEST IF ONLY CU REQUIRED                     
         BE    SABS10                                                           
         CLC   2(1,R7),SKIPDV      TEST DV                                      
         BE    SABS10                                                           
SABS8B   MVC   0(6,R6),0(R7)       MOVE TO SKIP LIST                            
         LA    R6,6(R6)            BUMP POINTER                                 
         LA    R3,1(R3)            BUMP COUNT                                   
         B     SABS12                                                           
*                                                                               
SABS10   MVC   0(6,R5),0(R7)       MOVE TO ACTIVE LIST                          
         LA    R5,6(R5)            BUMP POINTER                                 
         LA    R2,1(R2)            BUMP COUNT                                   
         MVI   RSLT,X'00'          SET ENTRY ACTIVATED                          
*                                                                               
SABS12   LA    R7,6(R7)            BUMP LIST POINTER                            
         B     SABS8                                                            
*                                                                               
SABS14   CLI   ACTN,C'T'                                                        
         BE    SABS18                                                           
         CLI   RSLT,X'00'          NO-MATCH FOR ACTIVATE/SKIP                   
         BNE   SABS22                                                           
         SR    R1,R1               GET LENGTH OF END OF ACTIVE LIST             
         IC    R1,SAVEEOAL                                                      
         BCTR  R1,0                                                             
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R5),SAVEEOA     SET END OF ACTIVE LIST CHRS                  
         MVI   0(R6),X'FE'         SET END OF TOTAL LISTS CHR                   
*                                                                               
         TM    2(R4),X'07'         MAKE SURE LIST IS INACTIVE                   
         BZ    SABS16                                                           
         BAS   R9,RSP              RESET POLL                                   
*                                                                               
SABS16   STC   R2,1(R4)            SET ACTIVE COUNT                             
*&&OS*&& LA    R1,1(R2)            MVS HAS EOT SEQ IN ACTIVE LIST               
*&&OS*&& STC   R1,1(R4)                                                         
         MH    R2,=H'6'                                                         
         SR    R1,R1               GET LENGTH OF END OF ACTIVE LIST             
         IC    R1,SAVEEOAL                                                      
         BCTR  R1,0                                                             
         AR    R2,R1                                                            
         EX    R2,*+8                                                           
         B     *+10                                                             
         MVC   3(0,R4),ACTVLST     MOVE ACTIVE LIST+EOAL CHR                    
         LA    R4,4(R4,R2)         BUMP TO FIRST SKIP ENTRY                     
         MH    R3,=H'6'                                                         
         EX    R3,*+8                                                           
         B     SABS22                                                           
         MVC   0(0,R4),SKIPLST     MOVE SKIP LIST+STOP CHAR                     
*                                                                               
SABS18   MVI   RSLT,C'+'           TEST TERMINAL STATUS                         
         LTR   R2,R2                                                            
         BZ    SABS20              NO ACTIVE ENTRIES                            
         LA    R4,ACTVLST                                                       
SABS18A  CLI   SKIPCU,0            TEST IF MATCH ON INDEX                       
         BNE   SABS18B                                                          
         CLC   5(1,R4),SKIPIX                                                   
         BE    XIT                                                              
         B     SABS18C                                                          
SABS18B  CLC   1(1,R4),SKIPCU      TEST CU                                      
         BNE   SABS18C                                                          
         CLI   SKIPDV,0            TEST IF ONLY CU REQUIRED                     
         BE    XIT                                                              
         CLC   2(1,R4),SKIPDV      TEST DV                                      
         BE    XIT                                                              
SABS18C  LA    R4,6(R4)                                                         
         BCT   R2,SABS18A                                                       
*                                                                               
SABS20   MVI   RSLT,X'01'          SET NOT FOUND RESULT                         
         LTR   R3,R3                                                            
         BZ    XIT                 NO SKIP ENTRIES                              
         LA    R4,SKIPLST                                                       
SABS20A  CLI   SKIPCU,0            TEST IF MATCH ON INDEX                       
         BNE   SABS20B                                                          
         CLC   5(1,R4),SKIPIX                                                   
         BE    SABS20C                                                          
         B     SABS20D                                                          
SABS20B  CLC   1(1,R4),SKIPCU      TEST CU                                      
         BNE   SABS20D                                                          
         CLI   SKIPDV,0            TEST IF ONLY CU REQUIRED                     
         BE    SABS20C                                                          
         CLC   2(1,R4),SKIPDV      TEST DV                                      
         BNE   SABS20D                                                          
SABS20C  MVI   RSLT,C'-'                                                        
         B     XIT                                                              
SABS20D  LA    R4,6(R4)                                                         
         BCT   R3,SABS20A                                                       
         B     XIT                                                              
*                                                                               
SABS22   CLI   TYPE,C'G'           G OR S REQUIRE ONLY ONE PASS OF LIST         
         BE    XIT                                                              
         CLI   TYPE,C'S'                                                        
         BE    XIT                                                              
         CLI   SAVERSLT,X'10'      TEST SECOND PASS COMPLETED                   
         BE    SABS24              NO                                           
         CLI   RSLT,X'00'          OK IF SUCCEEDED ON SCND PASS                 
         BE    XIT                                                              
         CLI   SAVERSLT,X'00'                                                   
         BNE   XIT                                                              
         MVI   RSLT,X'00'          OK IF SUCCEEDED ON FRST PASS                 
         B     XIT                                                              
*                                                                               
SABS24   MVC   SAVERSLT,RSLT       SAVE CONDITION FROM FIRST PASS               
         MVI   RSLT,X'01'                                                       
         MVI   SKIPDV,X'7F'        SET SEARCH TO GENERAL                        
         B     SABS1               DO SECOND PASS OF LIST                       
         EJECT                                                                  
*TIMER SET/RESET SUBROUTINES                                                    
*                                                                               
SSET     NTR1                      SUPEND TIMER                                 
         CLI   TIMER,C'X'                                                       
         BE    XIT                 EXIT IF NO TIMER                             
         CLI   TIMER,C'N'                                                       
         BE    XIT                 EXIT IF ALREADY SUSPENDED                    
         GOTO1 =V(TICTOC),PLIST,C'SSET'                                         
         MVI   TIMER,C'N'                                                       
         B     XIT                                                              
         SPACE 2                                                                
RSET     NTR1                      ACTIVATE TIMER                               
         CLI   TIMER,C'X'                                                       
         BE    XIT                 EXIT IF NO TIMER                             
         CLI   TIMER,C'Y'                                                       
         BE    XIT                 EXIT IF ALREADY ACTIVE                       
         GOTO1 =V(TICTOC),PLIST,C'RSET'                                         
         MVI   TIMER,C'Y'                                                       
         B     XIT                                                              
         SPACE 2                                                                
XIT      XIT1                                                                   
         SPACE 2                                                                
DEATH    BAS   RE,RSET             ACTIVE TIMER IF SUSPENDED                    
         DC    H'0',C'POLL LIST IS CLOBBERED'                                   
         DS    0H                                                               
         EJECT                                                                  
*DOS OPEN/CLOSE/RESETPOLL SUBROUTINES                                           
*                                                                               
*&&DO                                                                           
OPN      L     R1,ADTF                                                          
         OPEN  (1)                                                              
         BR    R9                                                               
*                                                                               
CLS      L     R1,ADTF                                                          
         CLOSE (1)                                                              
         BR    R9                                                               
*                                                                               
RSP      L     R1,ADECB                                                         
         TM    DECBSTAT,DECBSREP+DECBSRPP                                       
         BZ    RSPX                EXIT IF NO READ/RESETPL IN PROCESS           
         NI    DECBSTAT,X'F0'                                                   
         OI    DECBSTAT,DECBSRPP   SET RESET POLL IN PROCESS                    
*&&AP                                                                           
         TM    DECBTYPE,DECBTSS                                                 
         BZ    RSP2                                                             
         L     RE,APOLL            SET STOP FLAGS FOR 2260 AUTOPOLL             
RSP1     OI    2(RE),X'20'                                                      
         TM    2(RE),X'80'         DOS END OF LIST BIT                          
         BO    RSPX                                                             
         LA    RE,3(RE)                                                         
         B     RSP1                                                             
*&&                                                                             
RSP2     RESETPL (1),INLINE=YES                                                 
         L     R1,ADECB                                                         
         WAIT  ECB=(1)                                                          
RSPX     BR    R9                                                               
*&&                                                                             
         EJECT                                                                  
*MVS OPEN/CLOSE/RESETPOLL SUBROUTINES                                           
*                                                                               
*&&OS                                                                           
OPN      L     R2,ADTF                                                          
         OPEN  ((2),INOUT)                                                      
         TM    48(R2),X'10'        TEST SUCCESSFUL OPEN                         
         BOR   R9                  YES                                          
         MVI   RSLL,X'FF'          NO RETURN FAILURE RESULT                     
         BR    R9                                                               
*                                                                               
CLS      L     R2,ADTF                                                          
         CLOSE ((2))                                                            
         BR    R9                                                               
*                                                                               
RSP      L     R1,ADECB                                                         
         TM    DECBSTAT,DECBSREP+DECBSRPP                                       
         BZ    RSPX                EXIT IF NO READ/RESETPL IN PROCESS           
         NI    DECBSTAT,X'F0'                                                   
         OI    DECBSTAT,DECBSRPP   SET RESET POLL IN PROCESS                    
*&&AP                                                                           
         TM    DECBTYPE,DECBTSS                                                 
         BZ    RSP2                                                             
         L     RE,APOLL            SET STOP FLAGS FOR 2260 AUTOPOLL             
RSP1     OI    2(RE),X'20'                                                      
         CLI   3(RE),X'FF'         MVS END OF LIST HAS -VE OFFSET               
         BE    RSPX                                                             
         LA    RE,3(RE)                                                         
         B     RSP1                                                             
*&&                                                                             
         PRINT GEN                                                              
RSP2     RESETPL (1)                                                            
RSPX     BR    R9                                                               
         PRINT NOGEN                                                            
*&&                                                                             
         EJECT                                                                  
         LTORG                                                                  
         SPACE 2                                                                
SKIPWK   DSECT                                                                  
PLIST    DS    4F                                                               
ADECB    DS    A                                                                
ADTF     DS    A                                                                
APOLL    DS    A                                                                
*                                                                               
TIMER    DS    C                   Y=ACTIVE,N=SUSPENDED,X=NOTUSED               
ACTN     DS    C                                                                
TYPE     DS    C                                                                
RSLT     DS    C                                                                
RSLL     DS    C                                                                
INDEX    DS    C                                                                
SAVERSLT DS    C                                                                
SAVEEOAL DS    C                                                                
SAVEEOA  DS    CL8                                                              
*                                                                               
SKIPLINE DS    CL4                                                              
SKIPCU   DS    XL1                                                              
SKIPDV   DS    XL1                                                              
SKIPIX   DS    XL1                                                              
         DS    XL1                                                              
*                                                                               
ACTVLST  DS    CL200                                                            
SKIPLST  DS    CL200                                                            
SKIPWKX  DS    0C                                                               
         EJECT                                                                  
*FAUTL                                                                          
       ++INCLUDE FAUTL                                                          
         EJECT                                                                  
*FADECB                                                                         
       ++INCLUDE FADECB                                                         
         SPACE 2                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'007FASKIPA   05/01/02'                                      
         END                                                                    
