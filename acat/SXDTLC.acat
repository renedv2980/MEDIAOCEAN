*          DATA SET SXDTLC     AT LEVEL 054 AS OF 05/01/02                      
*CATALP SXDTLC                                                                  
         TITLE 'SXDTLC - SPOT EXTRACT - BUY RECORD DETAILS'                     
         SPACE 1                                                                
*                                                                               
*NEED TO ADD CODE FOR CANADA (GETRATE)                                          
*                                                                               
         PRINT NOGEN                                                            
SXDTLC   CSECT                                                                  
         NMOD1 0,*SXDTLC*                                                       
         L     RC,0(R1)                                                         
         LA    RA,2048(RC)                                                      
         LA    RA,2048(RA)                                                      
         USING SPWORKD,RC,RA                                                    
         L     R2,4(R1)                                                         
         USING SXDTLD,R2           R2=A(EXTRACT RECORD)                         
         L     R3,ADBUY                                                         
         USING BUYRECD,R3          R3=A(SPOT RECORD)                            
         L     R1,8(R1)                                                         
         ST    R1,AELEM            KEEP A(CURRENT ELEMENT TO PROCESS)           
*                                                                               
SXBLEN   DS    0H                                                               
         XC    SXDTLLEN,SXDTLLEN   RECORD LENGTH/TYPE/ENDOFREC                  
         MVC   SXDTLLEN(2),=AL2(SXDTLDL)                                        
         MVC   SXDTLTYP,=AL2(SXDTLDQ)                                           
         MVI   SXDTLDX,SXTRTQ                                                   
         MVI   SXDTLDX+1,SXTRTQ                                                 
*                                                                               
         BAS   RE,INIT             INITIALIZATION                               
*                                                                               
         BAS   RE,SETKEY           SET DATA FROM KEY                            
*                                                                               
         BAS   RE,FNDPG            FIND PIGGY BACK PRODUCT                      
*                                                                               
         BAS   RE,SETBDEL          GET INFO FROM BDELEM                         
*                                                                               
         BAS   RE,SETREG           GET INFO FROM REGELEM                        
*                                                                               
         BAS   RE,SETAFF           GET INFO FROM AFFELEM                        
*                                                                               
         B     EXIT                                                             
         EJECT                                                                  
*                                                                               
* INITIALIZATION                                                                
*                                                                               
INIT     NTR1                                                                   
*                                                                               
         XC    PRDCD1,PRDCD1       CLEAR PRODUCT CODES                          
         XC    PRDCD2,PRDCD2                                                    
         XC    SPTLN1,SPTLN1       CLEAR SPOT LENGTHS                           
         XC    SPTLN2,SPTLN2                                                    
         B     XIT                                                              
         EJECT                                                                  
*                                                                               
* CONVERT DATA FROM KEY                                                         
*                                                                               
SETKEY   NTR1                                                                   
         BAS   RE,FNDMED           FIND CORRECT MEDIA CODE                      
         MVI   SXDTLMED-1,SXTRTQ   BUY MEDIA                                    
         MVC   SXDTLMED,FULL                                                    
         MVI   SXDTLCLT-1,SXTRTQ       CLIENT                                   
         GOTO1 CLUNPK,DMCB,BUYKCLT,SXDTLCLT                                     
         BAS   RE,GCLT             GET CLT RECORD                               
         MVI   SXDTLPRD-1,SXTRTQ       PRODUCT                                  
         MVC   PRDCD1,BUYKPRD                                                   
         LA    R1,BUYKPRD                                                       
         BAS   RE,GPRD             GET PRD CODE                                 
         MVC   SXDTLPRD,FULL                                                    
         MVI   SXDTLMKT-1,SXTRTQ       MARKET                                   
         MVC   WORK(8),SPACES                                                   
         GOTO1 MSUNPK,DMCB,(X'80',BUYMSTA),SXDTLMKT,WORK                        
         MVI   SXDTLSTN-1,SXTRTQ   STATION                                      
         MVC   SXDTLSTN,WORK                                                    
******   MVI   SXDTLSTS-1,SXTRTQ   NETWORK                                      
         MVC   SXDTLSTS,WORK+5                                                  
         CLI   SXDTLSTS,C' '       IF NO NETWORK                                
         BH    SK10                                                             
         MVC   SXDTLSTS(1),WORK+4  SET BAND                                     
         CLI   SXDTLSTS,C' '       IF NO BAND                                   
         BH    SK10                                                             
         MVI   SXDTLSTS,C'T'       SET TO TV                                    
*                                                                               
SK10     MVI   SXDTLEST-1,SXTRTQ   ESTIMATE                                     
         EDIT  (1,BUYKEST),(3,SXDTLEST),FILL=0                                  
         MVI   SXDTLLIN-1,SXTRTQ   LINE NUMBER                                  
         EDIT  (1,BUYKBUY),(3,SXDTLLIN),FILL=0                                  
         B     XIT                                                              
         EJECT                                                                  
*                                                                               
*        SET INFO IN EXTRACT RECORD FROM BDELEM                                 
*                                                                               
SETBDEL  NTR1                                                                   
         LA    R4,BDELEM           R4=A(FIRST ELEMENT)                          
         USING BDELEM,R4                                                        
         MVC   SPTLN1,BDSEC        SAVE SPOT LENGTH                             
         CLI   BDTIME,0                                                         
         BE    *+10                                                             
         MVC   SPTLN1,BDTIME                                                    
         B     XIT                                                              
         DROP  R4                                                               
         EJECT                                                                  
*        SET INFO IN EXTRACT RECORD FROM REGELEM                                
*                                                                               
SETREG   NTR1                                                                   
         L     R4,AELEM            R4=A(ELEMENT TO PROCESS)                     
         USING REGELEM,R4                                                       
         LTR   R4,R4                                                            
         BZ    REX                                                              
*                                                                               
         MVI   SXDTLBDT-1,SXTRTQ   BUY DATE                                     
         GOTO1 DATCON,DMCB,(2,RDATE),(10,SXDTLBDT)                              
****     MVC   SXDTLBDT(2),=C'19'                                               
****     CLI   SXDTLBDT+2,C'8'     IF THE YEAR IS LESS THAN 8X                  
****     BNL   *+10                                                             
****     MVC   SXDTLBDT(2),=C'20'  SET CENTURY TO 20                            
*                                                                               
         MVI   SXDTLSNO-1,SXTRTQ   BUY SPOT NUMBER                              
****     MVC   SXDTLSNO,......                                                  
*                                                                               
         MVI   SXDTLCDT-1,SXTRTQ   CLR (PAY) DATE                               
         OC    RPAY,RPAY                                                        
         BZ    RE10                                                             
         GOTO1 DATCON,DMCB,(2,RPAY),(10,SXDTLCDT)                               
****     MVC   SXDTLCDT(2),=C'19'                                               
****     CLI   SXDTLCDT+2,C'8'     IF THE YEAR IS LESS THAN 8X                  
****     BNL   *+10                                                             
****     MVC   SXDTLCDT(2),=C'20'  SET CENTURY TO 20                            
*                                                                               
RE10     MVI   SXDTLSMI-1,SXTRTQ   BUY STATUS - MINUS                           
         MVI   SXDTLSMI,0                                                       
         TM    RSTATUS,X'80'                                                    
         BNO   *+8                                                              
         MVI   SXDTLSMI,1                                                       
*                                                                               
         MVI   SXDTLSMD-1,SXTRTQ   BUY STATUS - MINUSSED                        
         MVI   SXDTLSMD,0                                                       
         TM    RSTATUS,X'40'                                                    
         BNO   *+8                                                              
         MVI   SXDTLSMD,1                                                       
*                                                                               
         MVI   SXDTLS1P-1,SXTRTQ   BUY STATUS - PRD1 - PAYSALL                  
         MVI   SXDTLS1P,0                                                       
         TM    RSTATUS,X'10'                                                    
         BNO   *+8                                                              
         MVI   SXDTLS1P,1                                                       
*                                                                               
         MVI   SXDTLS2P-1,SXTRTQ   BUY STATUS - PRD2 - PAYSALL                  
         MVI   SXDTLS2P,0                                                       
         TM    RSTATUS,X'08'                                                    
         BNO   *+8                                                              
         MVI   SXDTLS2P,1                                                       
*                                                                               
         MVI   SXDTLSHI-1,SXTRTQ   BUY STATUS - HIATUS                          
         MVI   SXDTLSHI,0                                                       
         TM    RSTATUS,X'04'                                                    
         BNO   *+8                                                              
         MVI   SXDTLSHI,1                                                       
*                                                                               
         MVI   SXDTLSMG-1,SXTRTQ   BUY STATUS - MAKEGOOD                        
         MVI   SXDTLSMG,0                                                       
         TM    RSTATUS,X'02'                                                    
         BNO   *+8                                                              
         MVI   SXDTLSMG,1                                                       
*                                                                               
         MVI   SXDTLPR1-1,SXTRTQ   BUY PRODUCT 1                                
         MVI   SXDTLSL1-1,SXTRTQ   BUY PRODUCT 1 SPOT LENGTH                    
         MVI   SXDTLPR2-1,SXTRTQ   BUY PRODUCT 2                                
         MVI   SXDTLSL2-1,SXTRTQ   BUY PRODUCT 2 SPOT LENGTH                    
*                                                                               
         MVI   SXDTLGRS-1,SXTRTQ   GROSS                                        
         MVI   SXDTLNET-1,SXTRTQ   NET                                          
*                                                                               
         BAS   RE,GETAMT                                                        
*                                                                               
         CLI   0(R4),X'0B'         POL ELEMENT                                  
         BL    RE100                                                            
         CLI   1(R4),10                                                         
         BNH   RE200               ANY ALLOCATED PRODUCTS                       
         MVI   SXDTLNUM-1,SXTRTQ   BUY NUMBER OF SPOTS                          
         MVC   SXDTLNUM,=C'001'    POL - 1 SPOT PER ELEMENT                     
         CLI   SXDTLSMI,1          IS THIS A MINUS SPOT                         
         BNE   *+8                                                              
         MVI   SXDTLNUM,C'-'       YES - SET TO NEGATIVE                        
         CLI   SXDTLSHI,1          IS THIS HIATUS                               
         BNE   *+10                                                             
         MVC   SXDTLNUM,=C'000'    YES - MAKE ZERO                              
*                                                                               
         LA    R1,10(R4)                                                        
         BAS   RE,GPRD             GET PRD CODE                                 
         MVC   SXDTLPR1,FULL                                                    
         EDIT  (1,11(R4)),(3,SXDTLSL1),FILL=0                                   
*                                                                               
         CLI   1(R4),14                                                         
         BNH   RE200               ANY ALLOCATED PRODUCTS                       
         LA    R1,14(R4)                                                        
         BAS   RE,GPRD             GET PRD CODE                                 
         MVC   SXDTLPR2,FULL                                                    
         EDIT  (1,15(R4)),(3,SXDTLSL2),FILL=0                                   
         B     RE200                                                            
*                                                                               
*        NON - POL BUYS                                                         
*                                                                               
RE100    DS    0H                                                               
         MVI   SXDTLNUM-1,SXTRTQ   BUY NUMBER OF SPOTS                          
         EDIT  RNUM,(3,SXDTLNUM),FILL=0                                         
         CLI   SXDTLSMI,1          IS THIS A MINUS SPOT                         
         BNE   *+8                                                              
         MVI   SXDTLNUM,C'-'                                                    
         CLI   SXDTLSHI,1          IS THIS HIATUS                               
         BNE   *+10                                                             
         MVC   SXDTLNUM,=C'000'    YES - MAKE ZERO                              
*                                                                               
         MVC   SXDTLPR1,SXDTLPRD   PRODUCT 1                                    
         EDIT  SPTLN1,(3,SXDTLSL1),FILL=0  SLN 1                                
         CLI   PRDCD2,0                                                         
         BE    RE200                                                            
         MVC   SXDTLPR2,SXDTLPPR                                                
         EDIT  SPTLN2,(3,SXDTLSL2),FILL=0                                       
*                                                                               
RE200    DS    0H                                                               
*                                                                               
REX      B     XIT                                                              
         DROP  R4                                                               
         EJECT                                                                  
*                                                                               
*        CALL GETRATE TO GET GROSS AND NET AMOUNTS                              
*                                                                               
GETAMT   NTR1                                                                   
         L     R4,AELEM                                                         
         LTR   R4,R4                                                            
         BZ    GAX                                                              
*                                                                               
         CLI   SXDTLSHI,1          IS THIS HIATUS                               
         BNE   GA10                                                             
         XC    GROSS,GROSS         AMOUNTS S/B ZERO                             
         XC    NET,NET                                                          
         XC    TAX,TAX                                                          
         B     GA20                                                             
*                                                                               
GA10     XC    DUB(2),DUB                                                       
         MVC   DUB(2),PRDCD1       FIRST PRODUCT                                
         GOTO1 GETRATE,DMCB,(DUB,SPOTS),(DUB+1,(R3)),(0,(R4))                   
*                                                                               
GA20     MVC   GRGROSS,GROSS                                                    
         MVC   GRNET,NET                                                        
         MVC   GRTAX,TAX                                                        
*                                                                               
         CLI   PRDCD2,0            TEST FOR PIGGYBACK                           
         BE    GA100                                                            
         CLI   SXDTLSHI,1          IS THIS HIATUS                               
         BE    GA30                                                             
*                                                                               
         MVC   DUB(2),PRDCD2       NOW DO SECOND PRODUCT                        
         GOTO1 GETRATE,DMCB,(DUB,SPOTS),(DUB+1,(R3)),(0,(R4))                   
*                                                                               
GA30     L     R1,GRGROSS                                                       
         A     R1,GROSS                                                         
         ST    R1,GRGROSS                                                       
         L     R1,GRNET                                                         
         A     R1,NET                                                           
         ST    R1,GRNET                                                         
         L     R1,GRTAX                                                         
         A     R1,TAX                                                           
         ST    R1,GRTAX                                                         
*                                                                               
GA100    EDIT  GRGROSS,(12,SXDTLGRS),FILL=0                                     
         EDIT  GRNET,(12,SXDTLNET),FILL=0                                       
*                                                                               
GAX      B     XIT                                                              
         EJECT                                                                  
*                                                                               
*        SET INFO IN EXTRACT RECORD FROM AFFELEM                                
*                                                                               
SETAFF   NTR1                                                                   
         L     R4,AELEM            R4=A(ELEMENT TO PROCESS)                     
         USING AFFELEM,R4                                                       
         LTR   R4,R4                                                            
         BZ    SAX                                                              
         ZIC   R1,1(R4)            BUMP TO NEXT ELEM                            
         AR    R4,R1                                                            
         CLI   0(R4),X'10'         IS THIS AN AFFELEM                           
         BE    SA10                                                             
         CLI   0(R4),X'12'         IS THIS A FILEM ELEMENT                      
         BE    SA30                                                             
         B     SAX                                                              
*                                                                               
SA10     MVI   SXDTLADT-1,SXTRTQ   BUY AFFID DATE                               
         OC    ADATE,ADATE                                                      
         BZ    SA20                                                             
         GOTO1 DATCON,DMCB,(2,ADATE),(10,SXDTLADT)                              
****     MVC   SXDTLADT(2),=C'19'                                               
****     CLI   SXDTLADT+2,C'8'     IF THE YEAR IS LESS THAN 8X                  
****     BNL   *+10                                                             
****     MVC   SXDTLADT(2),=C'20'  SET CENTURY TO 20                            
*                                                                               
SA20     MVI   SXDTLATM-1,SXTRTQ   BUY AFFID TIME                               
         EDIT  ATIME,(4,SXDTLATM),FILL=0                                        
*                                                                               
         ZIC   R1,1(R4)            BUMP TO NEXT ELEM                            
         AR    R4,R1                                                            
         CLI   0(R4),X'12'         IS THIS A FILEM ELEMENT                      
         BNE   SAX                                                              
         USING FLMELEM,R4                                                       
*                                                                               
SA30     MVI   SXDTLFL1-1,SXTRTQ   BUY FILM 1                                   
         MVC   DUB(2),3(R4)        SET SEQUENCE NUMBER                          
         BAS   RE,GETCML                                                        
         MVC   SXDTLFL1,WORK                                                    
         MVI   SXDTLFL1-2,SXTRTQ   BUY FILM 2                                   
         CLI   1(R7),7             TEST PIGGYBACK                               
         BNE   SAX                                                              
         MVC   DUB(2),5(R7)                                                     
         BAS   RE,GETCML                                                        
         MVC   SXDTLFL2,WORK                                                    
*                                                                               
SAX      B     XIT                                                              
         DROP  R4                                                               
         EJECT                                                                  
*                                                                               
*        FIND PIGGYBACK PRODUCT - ONLY NON - POL P/B                            
*                                                                               
FNDPG    NTR1                                                                   
         MVI   SXDTLPPR-1,SXTRTQ   PRD 2                                        
         MVC   SXDTLPPR,=C'000'    KEY CANNOT BE NULL                           
         LA    R4,BDELEM           R4=A(FIRST ELEMENT)                          
         USING PBELEM,R4                                                        
         MVI   ELCODE,X'04'                                                     
         BAS   RE,NEXTEL                                                        
         BNE   PGX                                                              
         MVC   SXDTLPPR,PBPRD                                                   
         MVC   PRDCD2,PBPROD                                                    
         MVC   SPTLN2,PBTIME                                                    
*                                                                               
PGX      B     XIT                                                              
         DROP  R4                                                               
         EJECT                                                                  
*                                                                               
*        FIND CORRECT MEDIA CODE                                                
*                                                                               
FNDMED   NTR1                                                                   
         MVI   FULL,C' '                                                        
         MVC   BYTE,BUYKAM         AGY/MED                                      
         NI    BYTE,X'0F'                                                       
         ZIC   R1,BYTE                                                          
         BCTR  R1,0                                                             
         LA    R1,MEDTAB(R1)                                                    
         MVC   FULL(1),0(R1)                                                    
*                                                                               
FMX      B     XIT                                                              
         EJECT                                                                  
*                                                                               
* SUBROUTINE TO FIND NEW TRAFFIC COMMERCIAL SEQUENCE                            
*                                                                               
GETCML   NTR1                                                                   
         MVC   WORK(8),=C'********'    PRESET FOR NOT FOUND                     
         L     R4,ADCOMREC         A(CMLTAB)                                    
*                                                                               
GETCML2  CLC   0(10,R4),=X'FFFFFFFFFFFFFFFFFFFF'                                
         BE    GCMLX                                                            
         CLC   8(2,R4),DUB         MATCH CMML SEQ                               
         BE    GETCML4                                                          
         LA    R4,10(R4)                                                        
         B     GETCML2                                                          
*                                                                               
GETCML4  MVC   WORK(8),0(R4)       RETURN ENTRY                                 
*                                                                               
GCMLX    B     EXIT                                                             
         EJECT                                                                  
*                                                                               
*        GET CLIENT RECORD INTO ADCLT                                           
*                                                                               
GCLT     NTR1                                                                   
         MVC   AREC,ADCLT                                                       
         XC    KEY,KEY                                                          
         LA    R4,KEY                                                           
         USING CLTHDRD,R4                                                       
         MVC   CKEYAM,BUYKAM       SET AGY/MED                                  
         MVC   CKEYCLT,BUYKCLT     SET CLIENT                                   
         L     R4,ADCLT            CLEAR IO AREA                                
         CLC   0(L'CKEY,R4),KEY    SAME CLIENT                                  
         BE    GC20                                                             
         LA    R1,8                                                             
*                                                                               
GC10     XC    0(250,R4),0(R4)                                                  
         LA    R4,250(R4)                                                       
         BCT   R1,GC10                                                          
*                                                                               
         GOTO1 HIGH                                                             
         CLC   KEY(L'CKEY),KEYSAVE                                              
         BE    *+6                                                              
         DC    H'0'                                                             
         GOTO1 GET                                                              
         CLI   8(R1),0                                                          
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
GC20     MVC   SVMCLCOD,CMCLTCOD                                                
         B     XIT                                                              
         EJECT                                                                  
*                                                                               
*        GET PRODUCT CODE                                                       
*        R1 - A(PRD CODE)                                                       
*                                                                               
GPRD     NTR1                                                                   
         L     R4,ADCLT                                                         
         USING CLTHDRD,R4                                                       
         LA    R4,CLIST-CLTHDRD(R4)                                             
         MVC   FULL,SPACES                                                      
*                                                                               
GP10     CLI   0(R4),C'A'                                                       
         BL    GPX                                                              
         CLC   3(1,R4),0(R1)       MATCH PRD CODE                               
         BE    GP20                                                             
         LA    R4,4(R4)                                                         
         B     GP10                                                             
*                                                                               
GP20     MVC   FULL(3),0(R4)       SET PROD CODE                                
*                                                                               
GPX      B     XIT                                                              
         EJECT                                                                  
*                                                                               
*        GET ESTIMATE RECORD INTO ADEST                                         
*                                                                               
GEST     NTR1                                                                   
         XC    KEY,KEY                                                          
         LA    R4,KEY                                                           
         USING ESTHDRD,R4                                                       
         MVC   EKEYAM,BUYKAM       SET AGY/MED                                  
         MVC   EKEYCLT,BUYKCLT     SET CLIENT                                   
         MVC   EKEYPRD,SXDTLPRD    SET PRODUCT                                  
         MVC   EKEYEST,BUYKEST     SET ESTIMATE                                 
         L     R4,ADEST                                                         
         CLC   0(L'EKEY,R4),KEY                                                 
         BE    GEX                                                              
         MVC   AREC,ADEST                                                       
         LA    R1,4                                                             
*                                                                               
GE10     XC    0(250,R4),0(R4)                                                  
         LA    R4,250(R4)                                                       
         BCT   R1,GE10                                                          
*                                                                               
         GOTO1 HIGH                                                             
         CLC   KEY(L'EKEY),KEYSAVE                                              
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         GOTO1 GET                                                              
         CLI   8(R1),0                                                          
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
GEX      B     XIT                                                              
         EJECT                                                                  
NEXTEL   CLI   0(R4),0                                                          
         BE    NEXTELX                                                          
         ZIC   R0,1(R4)                                                         
         LTR   R0,R0                                                            
         BNZ   *+6                                                              
         DC    H'0'                                                             
         AR    R4,R0                                                            
*                                                                               
NEXTEL2  CLI   0(R4),0                                                          
         BE    NEXTELX                                                          
         CLC   ELCODE,0(R4)                                                     
         BNE   NEXTEL                                                           
         CR    RB,RB                                                            
         B     *+6                                                              
*                                                                               
NEXTELX  LTR   RB,RB                                                            
         BR    RE                                                               
         EJECT                                                                  
*                                                                               
XIT      XIT1                                                                   
EXIT     XMOD1 1                                                                
*                                                                               
         EJECT                                                                  
SXTRTQ   EQU   X'5E'               FIELD SEPARATOR CHAR                         
*                                                                               
MEDTAB   DC    C'TRNX'                                                          
*                                                                               
GRGROSS  DS    F                                                                
GRNET    DS    F                                                                
GRTAX    DS    F                                                                
*                                                                               
ELCODE   DS    XL1                                                              
PRDCD1   DS    XL1                 PRODUCT CODE 1                               
SPTLN1   DS    XL1                 SPOT LENGTH FROM BDSEC/BDTIME                
PRDCD2   DS    XL1                 PRODUCT CODE 2                               
SPTLN2   DS    XL1                 SPOT LENGTH FROM PBTIME                      
*                                                                               
SVMCLCOD DS    XL2                 TRAFFIC CLIENT CODE                          
*                                                                               
         DS    XL3                                                              
AELEM    DS    A                                                                
DEMTBL   DS    CL220               USED BY DEMOCON                              
       ++INCLUDE DEDBLOCK                                                       
         EJECT                                                                  
         LTORG                                                                  
         EJECT                                                                  
       ++INCLUDE SXDTLD                                                         
         EJECT                                                                  
BUYRECD  DSECT                                                                  
       ++INCLUDE SPGENBUY                                                       
         EJECT                                                                  
CLTHDRD  DSECT                                                                  
       ++INCLUDE SPGENCLT                                                       
         EJECT                                                                  
ESTHDRD  DSECT                                                                  
       ++INCLUDE SPGENEST                                                       
         EJECT                                                                  
*SPMEDBLOCK                                                                     
*SPREWORKD                                                                      
         PRINT OFF                                                              
       ++INCLUDE SPMEDBLOCK                                                     
       ++INCLUDE SPREPWORKD                                                     
         EJECT                                                                  
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'054SXDTLC    05/01/02'                                      
         END                                                                    
