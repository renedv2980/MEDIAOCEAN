*          DATA SET REGENDMV   AT LEVEL 060 AS OF 04/02/96                      
*CATALP REGENDMV                                                                
         TITLE 'REGENDMV - REPPAK DISPLAY MOD/VERSION NUMBERS'                  
*              PARAMETER 1 =       A(CONREC)                                    
*                        2 =       A(OUTPUT AREA FOR TEXT)                      
*                                       BYTE 0 = NUM OF CHAR ON RETURN          
*                        3 =       A(GENOLD)                                    
*                                                                               
***********************************************************************         
* HISTORY:                                                                      
*                                                                               
* 21MAR/96 (RHV) --- DATE OF BIRTH                                              
*                                                                               
* ***  END TOMBSTONE  ***                                                       
***********************************************************************         
REGENDMV CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 DMVWRKQ,REGENDMV,CLEAR=YES                                       
         USING DMVWRKD,RC                                                       
         ST    R1,SAVER1                                                        
         L     R5,8(R1)                                                         
         USING GENOLD,R5                                                        
         L     RA,0(R1)                                                         
         USING RCONREC,RA          CONTRACT RECORD                              
         L     R9,4(R1)            OUTPUT AREA                                  
*                                                                               
*   GET REQUIRED ELEMENTS                                                       
         LA    R6,RCONREC                                                       
         MVI   ELCODE,X'20'                                                     
         BAS   RE,GETEL                                                         
         BNE   MAIN600                                                          
         LR    R8,R6                                                            
         USING RCONSEND,R8                                                      
         LA    R6,RCONREC                                                       
         MVI   ELCODE,X'1F'                                                     
         BAS   RE,GETEL                                                         
         BNE   MAIN600         MISSING ELEMENT? EXIT ROUTINE                    
         LR    R4,R6                                                            
         USING RCONXEL,R4                                                       
*                                                                               
*   DISPLAY CONF/UNCF/ORIGINAL CONFRIMED                                        
*                                                                               
MAIN100  TM    RCONCONF,X'80'                                                   
         BZ    MAIN110                                                          
         MVC   0(4,R9),=C'UNCF'                                                 
         LA    R9,4(R9)                                                         
         B     MAIN200                                                          
MAIN110  TM    RCONCONF,X'40'                                                   
         BZ    MAIN600         ERROR                                            
         CLI   RCONMOD,0                                                        
         BNE   MAIN120                                                          
         MVC   0(18,R9),=C'ORIGINAL CONFIRMED'                                  
         LA    R9,18(R9)                                                        
         B     MAIN400                                                          
MAIN120  MVC   0(4,R9),=C'CONF'                                                 
         LA    R9,4(R9)                                                         
*                                                                               
* DISPLAY NEXT MOD# (OR MOD# FOR CONFIRMED CONTRACTS)                           
*                                                                               
MAIN200  DS    0H                                                               
         CLI   RCONMOD,X'FF'                                                    
         BE    MAIN300                                                          
         ZIC   R2,RCONMOD                                                       
         TM    RCONCONF,X'80'                                                   
         BZ    *+8                                                              
         LA    R2,1(R2)                                                         
         LA    R9,1(R9)                                                         
         MVC   0(4,R9),=C'MOD '                                                 
         LA    R9,4(R9)                                                         
         EDIT  (R2),(3,0(R9)),ALIGN=LEFT,ZERO=NOBLANK                           
         AR    R9,R0                                                            
*                                                                               
* DISPLAY CURRENT VERSION                                                       
*                                                                               
MAIN300  DS    0H                                                               
         TM    RCONCONF,X'40'                                                   
         BO    MAIN400                                                          
         CLI   RCONMOD,X'FF'                                                    
         BE    MAIN310                                                          
         MVI   0(R9),C','                                                       
         LA    R9,1(R9)                                                         
MAIN310  LA    R9,1(R9)                                                         
         MVC   0(2,R9),=C'V.'                                                   
         LA    R9,2(R9)                                                         
*                                                                               
         CLC   RCONSRV,RCONSSV                                                  
         BH    MAIN320                                                          
         ZIC   R2,RCONSSV                                                       
         B     MAIN330                                                          
MAIN320  ZIC   R2,RCONSRV                                                       
MAIN330  EDIT  (R2),(3,0(R9)),ALIGN=LEFT,ZERO=NOBLANK                           
         AR    R9,R0                                                            
         CH    R2,=H'1'                                                         
         BE    MAIN600                                                          
*                                                                               
* DISPLAY LAST VERSION/MOD                                                      
*                                                                               
MAIN400  DS    0H                                                               
         LA    R6,RCONREC                                                       
         MVI   ELCODE,X'22'                                                     
         BAS   RE,GETEL                                                         
         BE    MAIN405                                                          
         TM    RCONCONF,X'20'+X'40'                                             
         BNZ   MAIN600                                                          
         SR    R6,R6                                                            
*                        FIND PREVIOUS VERSION NUMBER                           
MAIN405  SR    R0,R0                                                            
         LA    RF,1                                                             
         LA    R2,1                                                             
         LA    RE,RCONS2V1              FIRST STATION VERSION #                 
MAIN410  LA    R1,3                     LOOP THRU LAST 3 VERSION #'S            
MAIN420  CLM   RF,1,0(RE)               NEXT VS. HIGHEST SO FAR                 
         BNL   MAIN430                                                          
         LR    R2,RF                    OLD HIGHEST IS 2ND HIGHEST              
         IC    RF,0(RE)                 REPLACE W/HIGHEST                       
         B     MAIN440                                                          
MAIN430  CLM   R2,1,0(RE)                                                       
         BNL   MAIN440                                                          
         IC    R2,0(RE)                                                         
MAIN440  LA    RE,1(RE)                                                         
         BCT   R1,MAIN420               LOOP THRU 3 VERSION NUMBERS             
         LTR   R0,R0                    FIRST TIME THRU?                        
         BNZ   MAIN450                                                          
         LA    RE,RCONSRV1              FIRST REP VERSION #                     
         LA    R0,1                     FLIP FLAG                               
         B     MAIN410                                                          
MAIN450  DS    0H                                                               
         LA    R9,1(R9)                                                         
         LTR   R6,R6                                                            
         BZ    MAIN460                                                          
         TM    RCONCONF,X'40'                                                   
         BO    MAIN460                                                          
         USING RMODELEM,R6                                                      
         ZIC   R3,RMODEL1V                                                      
         CR    R2,R3                                                            
         BH    MAIN460                                                          
         CLI   RMODEL1M,0                                                       
         BNE   MAIN455                                                          
         MVC   0(14,R9),=C'(LAST=ORIG CF)'                                      
         LA    R9,14(R9)                                                        
         B     MAIN600                                                          
MAIN455  MVC   0(13,R9),=C'(LAST=CF MOD '                                       
         LA    R9,13(R9)                                                        
         EDIT  RMODEL1M,(4,0(R9)),ALIGN=LEFT,ZERO=NOBLANK,TRAIL=C')'            
         AR    R9,R0                                                            
         B     MAIN600                                                          
MAIN460  MVC   0(7,R9),=C'(LAST=V'                                              
         LA    R9,7(R9)                                                         
         TM    RCONCONF,X'40'                                                   
         BZ    *+6                                                              
         LR    R2,RF                                                            
         EDIT  (R2),(4,0(R9)),ALIGN=LEFT,ZERO=NOBLANK,TRAIL=C')'                
         AR    R9,R0                                                            
*                                                                               
* WRAP UP & LEAVE                                                               
*                                                                               
MAIN600  DS    0H                                                               
         L     R1,SAVER1                                                        
         L     R2,4(R1)                                                         
         SR    R9,R2                                                            
         STC   R9,4(R1)                                                         
         XMOD1                                                                  
         EJECT                                                                  
         LTORG                                                                  
         EJECT                                                                  
*                                                                               
       ++INCLUDE RECNTWR2K                                                      
         EJECT                                                                  
DMVWRKD  DSECT                                                                  
SAVER1   DS    F                                                                
DMVWRKQ  EQU   *-DMVWRKD                                                        
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'060REGENDMV  04/02/96'                                      
         END                                                                    
