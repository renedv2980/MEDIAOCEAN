*          DATA SET RESPLRTNS  AT LEVEL 043 AS OF 05/01/02                      
*CATALP REDOSPL                                                                 
         TITLE 'RESPLRTN - CALCULATES SPL SHARE VALUES'                         
********************************************************************            
*                                                                  *            
*        RESPLRTN --- REP ROUTINE TO GET SPL AMT                   *            
* ---------------------------------------------------------------- *            
* UPDATE HISTORY:                                                  *            
*                                                                  *            
* AUG15/89 (SNS) - INITIAL                                         *            
*                                                                  *            
*                                                                  *            
********************************************************************            
*                                                                               
*        P1     =    A(CONTRACT)                                                
*        P2     =    A(MONTABLE)                                                
*        P3     =    A(STATION LIST)                                            
*        P4     =    # OF STATIONS  IN STATION LIST                             
*        P5     =    A(SPLTABLE)                                                
*        P6     =    0=THIS YEAR, 1=PRIOR YEAR                                  
         SPACE                                                                  
REDOSPL  CSECT                                                                  
         PRINT NOGEN                                                            
         ENTRY DOSPL                                                            
         ENTRY SETSPTBL                                                         
DOSPL    NMOD1 60,*DOSPL*                                                       
         L     R6,0(R1)            R2 PTS TO CONTRACT                           
         USING RCONREC,R6                                                       
         LA    R2,RCONELEM         POINT TO FIRST ELEMENT                       
         DROP  R6                                                               
         L     R6,4(R1)            R6 POINTS TO MONTABLE                        
         L     R5,8(R1)            R5 POINTS TO STATION LIST                    
         ST    R5,ASTALIST         STORE ADDRESS OF STATION LIST                
         L     R7,12(R1)           SAVE THE # OF STATIONS IN LIST               
         STC   R7,NUMSTA                                                        
         L     R3,16(R1)           R3 POINTS TO SPLTABLE                        
         MVI   NOSPL,0             SPL ELEMENT GOOD                             
         L     RE,20(R1)          THIS YEAR OR PRIOR YEAR                       
         STC   RE,WHICHYR                                                       
*                                                                               
         SR    R1,R1                                                            
DOSPL25  CLI   0(R2),0             END OF REC?                                  
         BE    DOSPLXT                                                          
         CLI   0(R2),6             SPL ELEMENT?                                 
         BE    DOSPL50             YES - GO PROCESS IT                          
         IC    R1,1(R2)                                                         
         AR    R2,R1                                                            
         B     DOSPL25                                                          
*                                                                               
DOSPL50  CLI   WHICHYR,0          THIS YEAR?                                    
         BNE   DOSPL60            NO -NEED LAST YEAR'S DATA                     
*                                                                               
         L     R1,12(R6)                                                        
         TM    6(R6),X'01'        CHOICES ARE X'00' OR X'01'                    
         BO    DOSPL55                                                          
         L     R1,8(R6)                                                         
DOSPL55  LTR   R1,R1                                                            
         BZ    DOSPL80             TRY NEXT MONTH                               
         STCM  R1,15,MONAMT                                                     
         B     DOSPL75                                                          
*                                                                               
DOSPL60  L     R1,36(R6)          LAST YEAR'S DATA                              
         TM    6(R6),X'02'        CHOICES ARE X'02' OR X'06'                    
         BO    DOSPL65                                                          
         L     R1,24(R6)                                                        
DOSPL65  LTR   R1,R1                                                            
*                                                                               
**** TEMPORARY                                                                  
******** BZ    DOSPL80             TRY NEXT MONTH                               
******** STCM  R1,15,MONAMT                                                     
**** TEMPORARY                                                                  
*                                                                               
         BNZ   DOSPL70                                                          
         L     R1,32(R6)          LAST YEARS ACTIVITY?                          
         LTR   R1,R1                                                            
         BZ    DOSPL80                                                          
*                                                                               
DOSPL70  STCM  R1,15,MONAMT                                                     
*                                  CHECK FOR SPL ELEMENT                        
DOSPL75  BAS   RE,POSTSPL                                                       
         CLI   NOSPL,1                                                          
         BE    DOSPLXT            CONTRACT NO GOOD                              
         SPACE                                                                  
DOSPL80  DS    0H                                                               
         LA    R6,40(R6)                                                        
         OC    0(4,R6),0(R6)                                                    
         BZ    DOSPLXT            THAT'S IT FOR THIS CONTRACT                   
         LA    R3,46(R3)                                                        
         B     DOSPL50                                                          
         SPACE                                                                  
DOSPLXT  XIT1                                                                   
         EJECT                                                                  
* POSTSPL - CALCULATES FOR EACH SPL ELEMENT-THE                                 
*           MINI ELEMENT STATION SHARE AND ADDS IT TO                           
*           ITS CORRECT POSITION IN THE SPL TABLE                               
         SPACE                                                                  
POSTSPL  NTR1                                                                   
         XR    R7,R7                                                            
PSPL10   IC    R7,8(R2)           # OF MINI-ELEMENTS                            
         LA    R4,9(R2)           FIRST MINI-EL                                 
         ICM   R1,15,5(R4)        ANY AMT FOR MY STATION?                       
         BNP   PSPLXT             MUST BE                                       
*                                                                               
* CALCULATES PERCENTAGE OF MY STATION ESTIMATE                                  
* TO ACTUAL IN MONTABLE - THIS PERCENTAGE WILL                                  
* THEN BE APPLIED TO OTHER STATIONS IN MARKET                                   
*  TO GET THERE ACTUALS                                                         
         SPACE                                                                  
         SR    RE,RE                                                            
         ICM   RF,15,MONAMT                                                     
         LA    R1,100                                                           
         MR    RE,R1                                                            
*                                                                               
         ICM   R1,15,5(R4)                                                      
         SRA   R1,1                                                             
         AR    RF,R1                                                            
         ICM   R1,15,5(R4)                                                      
         DR    RE,R1                                                            
         STCM  RF,15,PERCENT                                                    
*                                                                               
         LR    R8,R3              POINT R8 TO CURRENT SPL TABLE ENTRY           
         ICM   R0,15,6(R8)                                                      
         ICM   RF,15,MONAMT                                                     
         AR    R0,RF                                                            
         STCM  R0,15,6(R8)        ADD MY STATION TO MY STATION TOTAL            
         B     PSPL60             GET NEXT MINI ELEMENT                         
*                                                                               
PSPL25   L     R5,ASTALIST                                                      
         ZIC   RE,NUMSTA                                                        
PSPL30   DS    0H                                                               
         CLC   0(5,R5),0(R4)      STATION MATCH                                 
         BE    PSPL35                                                           
         CLC   0(3,R5),=C'IND'                                                  
         BE    PSPL35                                                           
         LA    R5,5(R5)           NEXT STATION                                  
         LA    R8,4(R8)           NEXT SPL COLUMN                               
         BCT   RE,PSPL30          IF NOT IN LIST & NO IND                       
         B     PSPL60             JUST GET NEXT SPL ELEMENT                     
         SPACE                                                                  
PSPL35   OC    5(4,R4),5(R4)      ANY AMT FOR STATION?                          
         BZ    PSPL60             NO - SKIP CALCULATIONS                        
         BAS   RE,APPCNT          CALC AMT FOR STAT IN MKT                      
*                                                                               
PSPL60   LA    R4,9(R4)           NEXT MINI ELEMENT                             
         LR    R8,R3              POINT R8 TO CURRENT SPL TABLE ENTRY           
         LA    R8,6(R8)           THEN POINT PAST DATE                          
         BCT   R7,PSPL25                                                        
         SPACE                                                                  
         B     DOSPLXT                                                          
         SPACE                                                                  
PSPLXT   MVI   NOSPL,1            DON'T WANT THIS CONTRACT                      
         B     DOSPLXT            EXIT                                          
         EJECT                                                                  
* APPCNT - APPLIES PERCENTAGE CALCULATED TO EACH ESTIMATED STATION              
*          AMT GIVEN AND ADDS IT TO SPLTABLE                                    
         SPACE                                                                  
APPCNT   NTR1                                                                   
         ICM   RF,15,5(R4)        SPL ESTIMATE                                  
         XR    RE,RE                                                            
         ICM   R1,15,PERCENT                                                    
         MR    RE,R1                                                            
         A     RF,=F'50'                                                        
         BNO   APPCNT20                                                         
         A     RE,=F'1'           ADD FOR OVERFLOW                              
APPCNT20 D     RE,=F'100'                                                       
         ICM   R0,15,0(R8)                                                      
         AR    R0,RF                                                            
         STCM  R0,15,0(R8)                                                      
         B     DOSPLXT                                                          
         EJECT                                                                  
* SETSPTBL - SETS UP DATES FOR SPLTABLE                                         
* P1= A(MONTABLE) , P2= A(SPLTABLE)                                             
         SPACE                                                                  
SETSPTBL NMOD1 0,*SETSPL*                                                       
         L     R6,0(R1)           POINT TO MONTABLE                             
         L     R3,4(R1)           POINT TO SPL TABLE                            
SETSPL20 OC    0(4,R6),0(R6)                                                    
         BZ    SETSPL50                                                         
*                                                                               
         XC    0(46,R3),0(R3)     CLEAR OUT                                     
         MVC   0(6,R3),0(R6)      MOVE MONTH DATE TO SPL TABLE                  
         LA    R3,46(R3)          NEXT ENTRY IN SPLTABLE                        
         LA    R6,40(R6)          NEXT ENTRY IN MONTABLE                        
         B     SETSPL20                                                         
*                                                                               
SETSPL50 MVC   0(6,R3),=X'00'                                                   
*                                                                               
SETSPLX  XIT1                                                                   
         EJECT                                                                  
*        RESPLRTN LOCAL VARIABLES                                               
NOSPL    DS    XL1                                                              
NUMSTA   DS    XL1                                                              
WHICHYR  DS    XL1                                                              
PERCENT  DS    F                                                                
MONAMT   DS    F                                                                
ASTALIST DS    F                                                                
         EJECT                                                                  
         LTORG                                                                  
         EJECT                                                                  
       ++INCLUDE REGENCON                                                       
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'043RESPLRTNS 05/01/02'                                      
         END                                                                    
