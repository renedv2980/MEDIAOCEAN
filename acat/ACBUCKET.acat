*          DATA SET ACBUCKET   AT LEVEL 007 AS OF 09/12/02                      
*CATALP ACBUCKET                                                                
ACBUCKET TITLE 'MODULE TO MAINTAIN BUCKET RECORDS FOR AN ACCOUNT'               
ACBUCKET CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 WORKX-WORKD,**BUCK**,RA,CLEAR=YES,RR=RE                          
         USING WORKD,RC                                                         
         ST    R1,ABUCKBLK         SAVE A(BUCKET BLOCK)                         
         ST    RE,RELO                                                          
         MVC   SAVERD,4(RD)        SAVE A(START OF W/S)                         
         LR    R9,R1               R9=A(BUCKET BLOCK)                           
         USING BUCKBLKD,R9                                                      
         CLI   BUCKCTRY,0          TEST USER PASSED COUNTRY                     
         BNE   *+8                 YES                                          
*&&US*&& MVI   BUCKCTRY,CTRYUSA                                                 
*&&UK*&& MVI   BUCKCTRY,CTRYGBR                                                 
         MVI   SPACES,C' '                                                      
         MVC   SPACES+1(L'SPACES-1),SPACES                                      
*                                                                               
BUCK1    L     R2,BUCKACOM                                                      
         USING COMFACSD,R2                                                      
         MVC   VDATAMGR,CDATAMGR                                                
         MVC   VHELLO,CHELLO                                                    
         DROP  R2                                                               
*                                                                               
         ICM   RF,15,BUCKCNVM      GET USERS A(CONVMOS)                         
         BNZ   BUCK2                                                            
         ICM   RF,15,ACONVMOS      ELSE USE LINKED ADDRESS                      
         BNZ   *+6                                                              
         DC    H'0'                                                             
*                                                                               
BUCK2    ST    RF,VCONVMOS                                                      
         L     R8,BUCKATRN                                                      
         USING TRNRECD,R8                                                       
**       MVC   FILE,ACCOUNT                                                     
         MVC   FILE,ACCFIL                                                      
         MVC   DATADISP,=Y(ACCORFST)                                            
         MVI   BUCKERR,0                                                        
         CLI   BUCKACT,BUCKALST    TEST LAST TIME CALL                          
         BE    BUCK6               YES                                          
         CLI   BUCKACT,BUCKAPBK    POST PRIOR BUCKET                            
         BE    BUCK20              YES                                          
*                                                                               
         LA    R7,TRNRECD                                                       
         AH    R7,DATADISP                                                      
         USING TRNELD,R7                                                        
         GOTO1 VCONVMOS,DMCB,(X'FE',TRNELD),MOS                                 
*                                                                               
BUCK4    CLC   PRODUL,TRNKUNT      TEST PRODUCTION                              
         BE    BUCKX               YES-EXIT RIGHT AWAY                          
         CLC   PCTLUL,TRNKUNT                                                   
         BE    BUCKX               EXIT ALSO FOR PROJECT CONTROL                
*                                                                               
         OC    BUCKLTRN,BUCKLTRN   TEST FIRST CALL                              
         BZ    BUCK8               YES                                          
         LA    R1,TRNKDATE-TRNRECD-1                                            
         CLI   BUCKLEV,BUCKLCON    TEST CONTRA-OFFICE BUCKETS                   
         BE    *+8                                                              
         LA    R1,TRNKCULC-TRNRECD-1                                            
         EX    R1,*+8                                                           
         B     *+10                                                             
         CLC   TRNKEY(0),BUCKLTRN  TEST FOR CHANGE IN KEY                       
         BE    BUCK8               NO                                           
*                                                                               
BUCK6    BAS   RE,PUTBUK           RETURN BUCKET RECORD(S) TO CALLER            
*                                                                               
BUCK8    CLI   BUCKACT,BUCKALST    TEST LAST TIME CALL                          
         BNE   BUCK10              NO                                           
         XC    BUCKLTRN,BUCKLTRN                                                
         B     BUCKX                                                            
*                                                                               
BUCK10   MVC   BUCKLTRN,TRNKEY     UPDATE LAST TRANSACTION KEY                  
         BAS   RE,UPDCAC           UPDATE CONTRA-ACCOUNT BUFFER                 
         B     BUCKX                                                            
         EJECT                                                                  
* POST PRIOR MONTHS BUCKETS TO BUFFER                                           
*                                                                               
*                                                                               
BUCK20   L     R8,BUCKACHD                                                      
         USING CHDRECD,R8                                                       
         CLC   PRODUL,CHDKUNT      TEST PRODUCTION                              
         BE    BUCKX               YES-EXIT RIGHT AWAY                          
         CLC   PCTLUL,CHDKUNT                                                   
         BE    BUCKX               EXIT ALSO FOR PROJECT CONTROL                
         OC    BUCKLTRN,BUCKLTRN   TEST FIRST CALL                              
         BZ    BUCK22              YES                                          
         LA    R1,TRNKDATE-TRNRECD-1                                            
         CLI   BUCKLEV,BUCKLCON    TEST CONTRA-OFFICE BUCKETS                   
         BE    *+8                                                              
         LA    R1,TRNKCULC-TRNRECD-1                                            
         EX    R1,*+8                                                           
         B     *+10                                                             
         CLC   CHDKEY(0),BUCKLTRN  TEST FOR CHANGE IN KEY                       
         BE    BUCK22              NO                                           
         BAS   RE,PUTBUK           RETURN BUCKET RECORD(S) TO CALLER            
         XC    BUCKLTRN,BUCKLTRN                                                
*                                                                               
BUCK22   MVC   BUCKLTRN,CHDKEY     UPDATE LAST TRANSACTION KEY                  
         MVC   BUKTYPE,CHDKBTYP    BUCKET TYPE                                  
         MVI   BUKSTAT,BUKSLIVE                                                 
         LA    R7,CHDRECD                                                       
         AH    R7,DATADISP                                                      
         USING BUKELD,R7                                                        
BUCK23   CLI   0(R7),0                                                          
         BE    BUCKX                                                            
         CLI   0(R7),BUKELQ                                                     
         BNE   BUCK24                                                           
         MVC   BUKELEM,BUKEL                                                    
         B     BUCK28                                                           
BUCK24   CLI   0(R7),PBKELQ        MAKE BUCKET ELEM. FROM PRIOR ELEM.           
         BNE   BUCK30                                                           
         USING PBKELD,R7                                                        
BUCK25   MVI   BUKELEM+(BUKEL-BUKELD),BUKELQ                                    
         MVI   BUKELEM+(BUKLN-BUKELD),BUKLNQ                                    
         MVC   BUKELEM+(BUKMOS-BUKELD)(L'BUKMOS),PBKHI                          
         ZAP   DR,=P'0'                                                         
         ZAP   CR,=P'0'                                                         
         CP    PBKDR,BHI           CHECK FOR OVERFLOW                           
         BNH   *+16                                                             
         ZAP   DR,BHI                                                           
         SP    PBKDR,BHI                                                        
*                                                                               
         CP    PBKDR,BLO                                                        
         BNL   *+16                                                             
         ZAP   DR,BLO                                                           
         SP    PBKDR,BLO                                                        
*                                                                               
         CP    PBKCR,BHI                                                        
         BNH   *+16                                                             
         ZAP   CR,BHI                                                           
         SP    PBKCR,BHI                                                        
*                                                                               
         CP    PBKCR,BLO                                                        
         BNL   *+16                                                             
         ZAP   CR,BLO                                                           
         SP    PBKCR,BLO                                                        
*                                                                               
         CP    DR,=P'0'            TEST ANY OVERFLOW                            
         BNE   *+14                                                             
         CP    CR,=P'0'                                                         
         BE    BUCK27                                                           
         ZAP   BUKELEM+(BUKDR-BUKELD)(L'BUKDR),DR                               
         ZAP   BUKELEM+(BUKCR-BUKELD)(L'BUKCR),CR                               
         GOTO1 UPDBUK              ADD ELEMENT WITH MAX AMOUNT                  
         B     BUCK25              AND ADD AONTHER                              
*                                                                               
BUCK27   ZAP   BUKELEM+(BUKDR-BUKELD)(L'BUKDR),PBKDR                            
         ZAP   BUKELEM+(BUKCR-BUKELD)(L'BUKCR),PBKCR                            
*                                                                               
BUCK28   GOTO1 UPDBUK              ADD TO BUFFER                                
BUCK30   SR    R0,R0                                                            
         IC    R0,1(R7)                                                         
         AR    R7,R0                                                            
         B     BUCK23                                                           
         DROP  R7,R8                                                            
BUCKX    XMOD1 1                                                                
*                                                                               
EXIT     XIT1  ,                                                                
         EJECT                                                                  
* SUB-ROUTINE TO UPDATE THE CONTRA-ACCOUNT BUCKET BUFFER BASED                  
* ON THE TRANSACTION PASSED BY THE CALLER                                       
*                                                                               
         USING TRNRECD,R8                                                       
         USING TRNELD,R7                                                        
UPDCAC   NTR1  ,                                                                
*                                                                               
         MVI   BUKSTAT,BUKSLIVE    SET BUCKET STATUS TO LIVE                    
*                                                                               
         LA    R3,BUKELEM                                                       
         USING BUKELD,R3                                                        
         MVI   BUKEL,BUKELQ        BUILD VIRGIN HISTORY ELEMENT                 
         MVI   BUKLN,BUKLNQ                                                     
         MVC   BUKMOS,MOS                                                       
         ZAP   BUKDR,PZERO                                                      
         ZAP   BUKCR,PZERO                                                      
*                                                                               
         CLI   SYSCTRY,SYSCNAM     TEST USA                                     
         BNE   UPDCAC10                                                         
         LA    R1,TRNELD                                                        
         USING SCIELD,R1                                                        
         SR    R0,R0                                                            
UPDCAC04 CLI   SCIEL,0             TEST EOR                                     
         BE    UPDCAC10                                                         
         CLI   SCIEL,SCIELQ        TEST COKE SUBSIDIARY CASH ELEMENTS           
         BNE   UPDCAC08                                                         
         CLI   SCITYPE,SCITGRNT                                                 
         BE    UPDCAC12                                                         
         CLI   SCITYPE,SCITCOKE                                                 
         BE    UPDCAC12                                                         
UPDCAC08 IC    R0,SCILN            BUMP TO NEXT ELEMENT                         
         AR    R1,R0                                                            
         B     UPDCAC04                                                         
         DROP  R1                                                               
*                                                                               
UPDCAC10 MVI   BUKTYPE,C' '        BUILD HISTORY ELEMENT                        
         LA    R1,BUKDR                                                         
         TM    TRNSTAT,TRNSDR                                                   
         BNZ   *+8                                                              
         LA    R1,BUKCR                                                         
         AP    0(6,R1),TRNAMNT                                                  
         GOTO1 UPDBUK,BUKELD       UPDATE HISTORY ELEMENT                       
*                                                                               
UPDCAC12 LA    R2,TRNELD                                                        
         USING SCIELD,R2                                                        
UPDCAC14 ZIC   R0,SCILN            BUMP TO NEXT ELEMENT                         
         AR    R2,R0                                                            
         CLI   SCIEL,0             TEST E-O-R                                   
         BE    UPDCAC30                                                         
         CLI   SCIEL,SCIELQ                                                     
         BNE   UPDCAC26                                                         
*                                                                               
         ZAP   BUKDR,PZERO         LOCATE SUBSIDIARY CASH ELEMENTS              
         ZAP   BUKCR,PZERO                                                      
         LA    R1,BUKTAB                                                        
         USING BUKTABD,R1          R1=A(BUCKET TYPE TABLE)                      
UPDCAC16 CLI   BUKTCTRY,BUKTEOTQ   TEST E-O-T                                   
         BE    UPDCAC14                                                         
         CLI   BUKTCTRY,SYSCANY    TEST ANY COUNTRY VALID                       
         BE    UPDCAC20                                                         
         TM    BUKTCTRY,SYSCNOT    TEST 'NOT' COUNTRY                           
         BZ    UPDCAC18                                                         
         MVC   BYTE,BUKTCTRY       EXTRACT COUNTRY FROM TABLE                   
         NI    BYTE,FF-SYSCNOT     TURN OFF 'NOT' BIT                           
         CLC   BYTE,SYSCTRY        MATCH ON COUNTRY CODE                        
         BNE   UPDCAC20            CARRY ON IF NOT EQUAL                        
         B     UPDCAC22                                                         
UPDCAC18 CLC   BUKTCTRY,SYSCTRY    MATCH ON COUNTRY CODE                        
         BNE   UPDCAC22                                                         
UPDCAC20 CLC   BUKTITYP,SCITYPE    MATCH ON BUCKET (ELEMENT) TYPE               
         BNE   UPDCAC22                                                         
         CLC   BUKTLEDG,SPACES     TEST SPECIAL LEDGER IN TABLE                 
         BE    UPDCAC24                                                         
         CLC   BUKTLEDG,TRNKUNT    YES - MATCH ON LEDGER                        
         BE    UPDCAC24                                                         
UPDCAC22 LA    R1,BUKTABL(R1)      BUMP TO NEXT TABLE ENTRY                     
         B     UPDCAC16                                                         
*                                                                               
UPDCAC24 CLI   BUKTOTYP,0          TEST DON'T CREATE BUCKET RECORD              
         BE    UPDCAC14                                                         
         MVC   SCITYPE,BUKTOTYP    SET BUCKET TYPE IN ELEMENT                   
         MVC   BUKTYPE,SCITYPE     SET BUCKET TYPE FOR RECORD                   
         SR    RF,RF                                                            
         ICM   RF,3,BUKTROUT                                                    
         BZ    UPDCAC14                                                         
         LA    RF,ACBUCKET(RF)                                                  
         BASR  RE,RF               CALL SUBSIDIARY CASH ROUTINE                 
         B     UPDCAC14            RETURNS AT 0(RE) TO SKIP BUCKET              
         GOTO1 UPDBUK,BUKELD       RETURNS AT 4(RE) TO UPDATE BUCKET            
         B     UPDCAC14                                                         
         DROP  R1                                                               
*                                                                               
         PUSH  USING                                                            
         DROP  R3                                                               
         USING BUKELD,R2                                                        
UPDCAC26 CLI   BUKEL,BUKELQ        TEST BUCKET ELEMENT                          
         BNE   UPDCAC28                                                         
         MVI   BUKTYPE,C' '        SET DEFAULT BUCKET                           
         GOTO1 UPDBUK,BUKELD       ADD TO BUCKET RECORD                         
         B     UPDCAC14                                                         
         POP   USING                                                            
*                                                                               
UPDCAC28 DS    0H                                                               
         B     UPDCAC14                                                         
*                                                                               
UPDCAC30 DS    0H                                                               
*                                                                               
UPDCACX  B     EXIT                                                             
         EJECT                                                                  
BTGRNT   DS    0H                  GROSS/NET FOR US COKE EXPENDITURE            
         ZAP   BUKDR,SCIGRS        FOR COKE GROSS=DEBIT, NET=CREDIT             
         CLI   SCILN,SCILN2Q                                                    
         BL    *+10                                                             
         ZAP   BUKCR,SCINET                                                     
         B     4(RE)                                                            
*                                                                               
BTVEHI   DS    0H                  VEHICLES                                     
BTHOUR   DS    0H                  HOURS                                        
BTGRSS   DS    0H                  GROSS                                        
BTIVAT2P DS    0H                  VAT                                          
BTGLEV   DS    0H                  NET (GROSS LESS VAT)                         
         LA    R1,BUKDR            POST DEBIT/CREDIT BASED ON TRNSTAT           
         TM    TRNSTAT,TRNSDR                                                   
         BNZ   *+8                                                              
         LA    R1,BUKCR                                                         
         ZAP   0(L'BUKCR,R1),SCIAMNT                                            
         B     4(RE)                                                            
*                                                                               
BTHOUR1R DS    0H                  HOURS (1R LEDGER ONLY)                       
         LA    R1,BUKCR                                                         
         CP    TRNAMNT,PZERO                                                    
         BE    *+8                                                              
         LA    R1,BUKDR                                                         
         ZAP   0(L'BUKDR,R1),SCIAMNT                                            
         B     4(RE)                                                            
*                                                                               
BTGRSS1J DS    0H                  GROSS (US 1J LEDGER ONLY)                    
BTHOUR1J DS    0H                  HOURS (US 1J LEDGER ONLY)                    
BTVEHI1J DS    0H                  VEHICLES (US 1J LEDGER ONLY)                 
BTFEEA   DS    0H                  FEE ADJUSTMENTS                              
BTFEES   DS    0H                  FEES                                         
         ZAP   BUKDR,SCIAMNT       AMOUNT TO DEBIT                              
         CLI   SCILN,SCILN2Q                                                    
         BNE   *+10                                                             
         ZAP   BUKCR,SCIADMN       ADMIN TO CREDIT (IF PRESENT)                 
         B     4(RE)                                                            
*                                                                               
BTDEPR41 DS    0H                  ASSET DEPRECIATION (LEDGER 41 ONLY)          
BTBENE   DS    0H                  BENEFITS (US 1R LEDGER ONLY?)                
         ZAP   BUKDR,SCIAMNT       BENEFIT                                      
         ZAP   BUKCR,SCIADMN       ADMINISTRATIVE                               
         B     4(RE)                                                            
*                                                                               
BTFORDST DS    0H                  INCOME DEBIT (UK LEDGER ST ONLY)             
BTJHRSST DS    0H                  NET DEBIT (UK LEDGER ST ONLY)                
         ZAP   BUKDR,SCIAMNT                                                    
         B     4(RE)                                                            
*                                                                               
BTINCAST DS    0H                  INCOME CREDIT (UK ST LEDGER ONLY)            
BTGLEVST DS    0H                  NET CREDIT (UK LEDGER ST ONLY)               
         ZAP   BUKCR,SCIAMNT                                                    
         B     4(RE)                                                            
         DROP  R3                                                               
         EJECT                                                                  
* SUB-ROUTINE TO MAINTAIN THE BUCKET BUFFER BASED ON THE BUCKET                 
* ELEMENT AT BUKELEM--CALLED BY UPDCAC                                          
*                                                                               
UPDBUK   L     R1,BUCKABUF                                                      
         USING BUKBUFD,R1          R1=A(BUCKET BUFFER)                          
         ICM   RF,15,BUKBHNUM      RF=NUMBER OF ENTRIES IN TABLE                
         LA    R1,BUKBHDRL(R1)     BUMP OVER TABLE HEADER                       
         LTR   R0,RF               TEST TABLE EMPTY                             
         BZ    UPDBUK6                                                          
*                                                                               
UPDBUK2  CLI   BUKBTYPE,BUKBEOTQ   TEST END OF TABLE                            
         BE    UPDBUK6                                                          
         CLC   BUKEY,BUKBKEY       MATCH ON BUCKET TYPE, STATUS & MONTH         
         BNE   UPDBUK4                                                          
         CLC   BUKBMOS,BUKELEM+(BUKMOS-BUKELD)                                  
         BE    UPDBUK8                                                          
*                                                                               
UPDBUK4  LA    R1,BUKBUFL(R1)      BUMP TO NEXT BUFFER ENTRY                    
         BCT   R0,UPDBUK2                                                       
*                                                                               
UPDBUK6  LA    RF,1(RF)            INCREMENT NUMBER OF TABLE ENTRIES            
         CH    RF,=Y(BUKBMAXN)     TEST SPACE FOR NEW ENTRY                     
         BNH   *+6                                                              
         DC    H'0'                                                             
         MVC   BUKBKEY,BUKEY                                                    
         MVC   BUKBMOS,BUKELEM+(BUKMOS-BUKELD)                                  
         ZAP   BUKBDR,BUKELEM+(BUKDR-BUKELD)(L'BUKDR)                           
         ZAP   BUKBCR,BUKELEM+(BUKCR-BUKELD)(L'BUKCR)                           
         MVI   BUKBUFD+BUKBUFL,BUKBEOTQ                                         
         L     R1,BUCKABUF                                                      
         STCM  RF,15,BUKBHNUM                                                   
         B     UPDBUKX                                                          
*                                                                               
UPDBUK8  ZAP   DR,BUKELEM+(BUKDR-BUKELD)(L'BUKDR)                               
         ZAP   CR,BUKELEM+(BUKCR-BUKELD)(L'BUKCR)                               
         AP    DR,BUKBDR                                                        
         AP    CR,BUKBCR                                                        
*                                                                               
         CP    DR,BHI                                                           
         BH    UPDBUK4                                                          
         CP    DR,BLO                                                           
         BL    UPDBUK4                                                          
         CP    CR,BHI                                                           
         BH    UPDBUK4                                                          
         CP    CR,BLO                                                           
         BL    UPDBUK4                                                          
         AP    BUKBDR,BUKELEM+(BUKDR-BUKELD)(L'BUKDR)                           
         AP    BUKBCR,BUKELEM+(BUKCR-BUKELD)(L'BUKCR)                           
*                                                                               
UPDBUKX  BR    RE                                                               
         DROP  R1                                                               
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO UPDATE BUCKET RECORDS                                    *         
***********************************************************************         
         SPACE 1                                                                
PUTBUK   NTR1  ,                                                                
         L     R1,BUCKABUF         R1=A(FIRST BUCKET ELEMENT)                   
         USING BUKBUFD,R1                                                       
         ICM   R0,15,BUKBHNUM      R0=NUMBER OF BUCKET ELEMENTS                 
         BZ    PUTBUKX                                                          
         LA    R1,BUKBHDRL(R1)                                                  
         SH    R0,=H'1'                                                         
         BZ    PUTBUK6             EXIT IF ONLY ONE BUCKET ELEMENT              
*                                                                               
PUTBUK2  LA    RF,BUKBUFL(R1)      SORT ELEMENTS INTO ASCENDING SEQ             
         LR    RE,R0                                                            
PUTBUK4  CLC   BUKBUFD(BUKBKEYL),0(RF)                                          
         BNH   *+22                                                             
         XC    0(BUKBUFL,R1),0(RF)                                              
         XC    0(BUKBUFL,RF),0(R1)                                              
         XC    0(BUKBUFL,R1),0(RF)                                              
         LA    RF,BUKBUFL(RF)                                                   
         BCT   RE,PUTBUK4                                                       
         LA    R1,BUKBUFL(R1)                                                   
         BCT   R0,PUTBUK2                                                       
         DROP  R1                                                               
*                                                                               
PUTBUK6  XC    BUKEY,BUKEY         SET FIRST TIME FLAG                          
         L     R2,BUCKABUF                                                      
         USING BUKBUFD,R2          R2=A(BUFFER ENTRY)                           
         ICM   R0,15,BUKBHNUM      R0=NUMBER OF BUFFER ENTRIES                  
         LA    R2,BUKBHDRL(R2)                                                  
         L     R5,BUCKABUK                                                      
         USING CACRECD,R5          R5=A(CONTRA RECORD)                          
*                                                                               
PUTBUK8  CLC   BUKBKEY,BUKEY       TEST CHANGE OF BUCKET TYPE                   
         BE    PUTBUK14                                                         
         CLI   BUKTYPE,0           TEST FIRST TIME                              
         BE    PUTBUK12                                                         
         CLI   BUKSTAT,BUKSLIVE                                                 
         BE    PUTBUK10                                                         
         B     PUTBUK12                                                         
*                                                                               
PUTBUK10 CLI   BUCKFILT,0          TEST ANY TYPE FILTER                         
         BE    *+14                                                             
         CLC   BUCKFILT,BUKTYPE    APPLY FILTER AGAINST LAST TYPE               
         BNE   PUTBUK12            SKIP THIS BUCKET                             
         BAS   RE,GO               HOOK ROUTINE HERE                            
*                                                                               
PUTBUK12 MVC   BUKEY,BUKBKEY       SET CURRENT BUCKET TYPE                      
         BAS   RE,GETBUK                                                        
*                                                                               
PUTBUK14 CLI   BUKBSTAT,BUKSLIVE   TEST LIVE DATA                               
         BNE   PUTBUK16                                                         
         LA    R1,BUKELEM                                                       
         USING BUKELD,R1                                                        
         MVI   BUKEL,BUKELQ                                                     
         MVI   BUKLN,BUKLNQ                                                     
         MVC   BUKMOS,BUKBMOS                                                   
         ZAP   BUKDR,BUKBDR                                                     
         ZAP   BUKCR,BUKBCR                                                     
         GOTO1 ADDBUK,CACRECD      ADD BUCKET ELEMENT TO RECORD                 
*                                                                               
PUTBUK16 LA    R2,BUKBUFL(R2)      BUMP TO NEXT BUFFER ENTRY                    
         BCT   R0,PUTBUK8                                                       
*                                                                               
PUTBUK18 CLI   BUCKFILT,0          TEST ANY TYPE FILTER                         
         BE    *+14                                                             
         CLC   BUCKFILT,BUKTYPE    APPLY FILTER                                 
         BNE   PUTBUK20            SKIP THIS BUCKET                             
         BAS   RE,GO               GIVE CALLER FINAL HOOK                       
*                                                                               
PUTBUK20 L     R1,BUCKABUF                                                      
         XC    0(4,R1),0(R1)       CLEAR BUCKET ENTRY COUNT                     
*                                                                               
PUTBUKX  B     EXIT                                                             
         DROP  R1,R2                                                            
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO ADD A BUCKET ELEMENT TO A BUCKET RECORD                  *         
*                                                                     *         
* NTRY - R1=A(RECORD TO ADD BUCKET INTO)                              *         
*        BUKELEM CONTAINS BUCKET ELEMENT                              *         
***********************************************************************         
         SPACE 1                                                                
ADDBUK   NTR1  ,                                                                
         LR    R4,R1                                                            
         USING ACCRECD,R4          R4=A(RECORD CONTAINING BUCKETS)              
         LA    R2,BUKELEM                                                       
         USING BUKELD,R2           R2=A(BUCKET ELEMENT TO BE ADDED)             
         GOTO1 VHELLO,ELIST,(C'G',FILE),('BUKELQ',ACCRECD),            *        
               (L'BUKMOS,BUKMOS)                                                
         CLI   ELERR,0                                                          
         BNE   ADDBUK02            NO MATCHING BUCKET ELEMENT                   
         L     R1,ELADDR                                                        
         ZAP   DR,BUKDR-BUKELD(L'BUKDR,R1)                                      
         ZAP   CR,BUKCR-BUKELD(L'BUKCR,R1)                                      
         AP    DR,BUKDR                                                         
         AP    CR,BUKCR                                                         
*                                                                               
         CP    DR,BHI                                                           
         BH    ADDBUK02                                                         
         CP    DR,BLO                                                           
         BL    ADDBUK02                                                         
         CP    CR,BHI                                                           
         BH    ADDBUK02                                                         
         CP    CR,BLO                                                           
         BL    ADDBUK02                                                         
         ZAP   BUKDR-BUKELD(L'BUKDR,R1),DR                                      
         ZAP   BUKCR-BUKELD(L'BUKCR,R1),CR                                      
         B     ADDBUKX                                                          
*                                                                               
ADDBUK02 LA    R2,ACCRECD                                                       
         AH    R2,DATADISP         POINT TO FIRST CONTRA-HEADER ELEMENT         
         SR    RE,RE               RE=A(OLDEST BUCKET ELEMENT)                  
         SR    R1,R1               R1=NUMBER OF BUCKET ELEMENTS                 
         SR    R0,R0                                                            
ADDBUK04 CLI   BUKEL,0             TEST E-O-R                                   
         BE    ADDBUK08                                                         
         CLI   BUKEL,BUKELQ        TEST BUCKET ELEMENT                          
         BNE   ADDBUK06                                                         
         LA    R1,1(R1)            BUMP NUMBER OF BUCKETS                       
         LTR   RE,RE               TEST FIRST BUCKET ELEMENT                    
         BNZ   ADDBUK06                                                         
         LA    RE,BUKELD           YES - SAVE IT'S ADDRESS                      
ADDBUK06 IC    R0,BUKLN            BUMP TO NEXT ELEMENT                         
         AR    R2,R0                                                            
         B     ADDBUK04                                                         
*                                                                               
ADDBUK08 CH    R1,BUKMAXN          ADD NEW BUCKET IF ENOUGH ROOM                
         BNL   ADDBUK10                                                         
         GOTO1 VHELLO,ELIST,(C'P',FILE),ACCRECD,BUKELEM,0                       
         CLI   ELERR,0                                                          
         BE    *+6                                                              
         DC    H'0'                CAN'T ADD THE ELEMENT                        
         B     ADDBUKX                                                          
*                                                                               
ADDBUK10 LTR   R2,RE               POINT TO OLDEST (FIRST) BUCKET               
         BNZ   *+6                                                              
         DC    H'0'                                                             
         MVC   BUKSAVE,BUKELD                                                   
         MVI   BUKEL,DELELQ                                                     
         LA    R2,BUKSAVE          POINT TO SAVED BUCKET ELEMENT                
         GOTO1 VHELLO,ELIST,(C'D',FILE),('DELELQ',ACCRECD),0                    
         GOTO1 VHELLO,ELIST,(C'P',FILE),ACCRECD,BUKELEM,0                       
         CLI   ELERR,0                                                          
         BE    *+6                                                              
         DC    H'0'                CAN'T ADD THE ELEMENT                        
*                                                                               
         GOTO1 VHELLO,ELIST,(C'G',FILE),('PBKELQ',ACCRECD),0                    
         CLI   ELERR,0                                                          
         BNE   ADDBUK12                                                         
         L     R3,ELADDR           UPDATE PRIOR BUCKET ELEMENT                  
         USING PBKELD,R3                                                        
         CLC   PBKLOW,BUKYEAR                                                   
         BL    *+10                                                             
         MVC   PBKLOW,BUKYEAR                                                   
         CLC   PBKHI,BUKYEAR                                                    
         BH    *+10                                                             
         MVC   PBKHI,BUKYEAR                                                    
         AP    PBKDR,BUKDR                                                      
         AP    PBKCR,BUKCR                                                      
         B     ADDBUKX                                                          
*                                                                               
ADDBUK12 XC    ELEMENT,ELEMENT     CREATE PRIOR BUCKET ELEMENT                  
         LA    R3,ELEMENT                                                       
         MVI   PBKEL,PBKELQ                                                     
         MVI   PBKLN,PBKLNQ                                                     
         MVC   PBKLOW,BUKYEAR                                                   
         MVC   PBKHI,BUKYEAR                                                    
         ZAP   PBKDR,BUKDR                                                      
         ZAP   PBKCR,BUKCR                                                      
         GOTO1 VHELLO,ELIST,(C'P',FILE),ACCRECD,PBKELD,0                        
         CLI   ELERR,0                                                          
         BE    *+6                                                              
         DC    H'0'                CAN'T ADD THE ELEMENT                        
*                                                                               
ADDBUKX  B     EXIT                                                             
         DROP  R2,R3,R4                                                         
         EJECT                                                                  
***********************************************************************         
* BUILD CONTRA-HEADER - AT ENTRY, R5=A(BUCKET RECORD AREA)            *         
***********************************************************************         
         SPACE 1                                                                
         USING BUKBUFD,R2                                                       
GETBUK   NTR1  ,                                                                
         USING CACRECD,R5                                                       
         L     R0,BUCKABUK                                                      
         LA    R1,BUCKLBUK                                                      
         SR    RF,RF                                                            
         MVCL  R0,RE                                                            
         MVC   CACKEY,SPACES                                                    
         MVC   CACKEY(CACKSPAC-CACKEY),BUCKLTRN                                 
         MVC   CACKBTYP,BUKTYPE                                                 
         CLI   BUCKLEV,BUCKLOFF    TEST OFFICE LEVEL                            
         BNE   *+10                                                             
         MVC   CACKCULC,SPACES     YES-CLEAR CONTRA FIELD                       
*                                                                               
         LH    RE,DATADISP                                                      
         LA    RE,1(RE)                                                         
         STCM  RE,3,CACRLEN                                                     
*                                                                               
GETBUKX  B     EXIT                                                             
         DROP  R2,R5                                                            
         EJECT                                                                  
* HOOK ROUTINE TO CALLER                                                        
*                                                                               
GO       NTR1  ,                                                                
         L     RF,BUCKHOOK         RF=A(CALLER ROUTINE)                         
         L     RE,SAVERD                                                        
         LM    R0,RC,20(RE)                                                     
         BASR  RE,RF                                                            
         XIT1  ,                                                                
         EJECT                                                                  
* LITERAL POOL                                                                  
*                                                                               
         SPACE 2                                                                
         LTORG                                                                  
*                                                                               
ACONVMOS DC    V(CONVMOS)                                                       
*                                                                               
BUKMAXN  DC    H'108'              MAXIMUM NUMBER OF BUCKET ELEMENTS            
PZERO    DC    P'0'                                                             
ACCOUNT  DC    CL8'ACCOUNT'                                                     
ACCFIL   DC    CL8'ACCFIL '                                                     
PRODUL   DC    C'SJ'               PRODUCTION UNIT/LEDGER                       
PCTLUL   DC    C'1J'               PROJECT CONTROL UNIT/LEDGER                  
RETUNT   DC    C'3'                RETAIL UNIT CODE                             
CPERULUS DC    C'1R'               US COSTING/PERSON UNIT/LEDGER CODE           
FILMULUK DC    C'2F'               UK FILM LEDGER                               
BHI      DC    PL6'99999999999'                                                 
BLO      DC    PL6'-99999999999'                                                
DR       DC    PL8'0'                                                           
CR       DC    PL8'0'                                                           
         EJECT                                                                  
BUKTAB   DS    0X                  ** CONTRA BUCKET TABLE **                    
         DC    AL1(SYSCNAM),AL1(SCITGRNT,SCITDFLT),C'SE'                        
         DC    AL2(BTGRNT-ACBUCKET)                                             
         DC    AL1(SYSCNAM),AL1(SCITCOKE,SCITGRNT),C'SE'                        
         DC    AL2(BTGRNT-ACBUCKET)                                             
         DC    AL1(SYSCNAM),AL1(SCITGRNT,SCITGRNT),C'  '                        
         DC    AL2(BTGRNT-ACBUCKET)                                             
         DC    AL1(SYSCEUR),AL1(SCITGLEV,SCITGLEV),C'ST'                        
         DC    AL2(BTGLEVST-ACBUCKET)                                           
         DC    AL1(SYSCEUR),AL1(SCITINCA,SCITINCA),C'ST'                        
         DC    AL2(BTINCAST-ACBUCKET)                                           
         DC    AL1(SYSCEUR),AL1(SCITFORD,SCITGLEV),C'ST'                        
         DC    AL2(BTFORDST-ACBUCKET)                                           
         DC    AL1(SYSCEUR),AL1(SCITJHRS,SCITINCA),C'ST'                        
         DC    AL2(BTJHRSST-ACBUCKET)                                           
         DC    AL1(SYSCNOT+SYSCNAM),AL1(SCITIVAT,SCITIVAT),C'2P'                
         DC    AL2(BTIVAT2P-ACBUCKET)                                           
         DC    AL1(SYSCEUR),AL1(SCITDEPR,SCITDEPR),C'41'                        
         DC    AL2(BTDEPR41-ACBUCKET)                                           
         DC    AL1(SYSCANY),AL1(SCITVEHI,SCITVEHI),C'1J'                        
         DC    AL2(BTVEHI1J-ACBUCKET)                                           
         DC    AL1(SYSCANY),AL1(SCITHOUR,0),C'1C'                               
         DC    AL2(0)                                                           
         DC    AL1(SYSCANY),AL1(SCITHOUR,SCITHOUR),C'1J'                        
         DC    AL2(BTHOUR1J-ACBUCKET)                                           
         DC    AL1(SYSCANY),AL1(SCITGRSS,SCITGRSS),C'1J'                        
         DC    AL2(BTGRSS1J-ACBUCKET)                                           
         DC    AL1(SYSCANY),AL1(SCITGRSS,SCITGRSS),C'  '                        
         DC    AL2(BTGRSS-ACBUCKET)                                             
         DC    AL1(SYSCANY),AL1(SCITBENE,SCITBENE),C'  '                        
         DC    AL2(BTBENE-ACBUCKET)                                             
         DC    AL1(SYSCANY),AL1(SCITJHRS,SCITHOUR),C'  '                        
         DC    AL2(BTHOUR-ACBUCKET)                                             
         DC    AL1(SYSCANY),AL1(SCITHOUR,SCITHOUR),C'1R'                        
         DC    AL2(BTHOUR1R-ACBUCKET)                                           
         DC    AL1(SYSCANY),AL1(SCITHOUR,SCITHOUR),C'  '                        
         DC    AL2(BTHOUR-ACBUCKET)                                             
         DC    AL1(SYSCANY),AL1(SCITGLEV,SCITGLEV),C'  '                        
         DC    AL2(BTGLEV-ACBUCKET)                                             
         DC    AL1(SYSCANY),AL1(SCITVEHI,SCITVEHI),C'  '                        
         DC    AL2(BTVEHI-ACBUCKET)                                             
         DC    AL1(SYSCANY),AL1(SCITFEEA,SCITFEEA),C'  '                        
         DC    AL2(BTFEEA-ACBUCKET)                                             
         DC    AL1(SYSCANY),AL1(SCITFEES,SCITFEES),C'  '                        
         DC    AL2(BTFEES-ACBUCKET)                                             
BUKTABX  DC    AL1(BUKTEOTQ)                                                    
         EJECT                                                                  
BUKTABD  DSECT                     ** CONTRA BUCKET TABLE DSECT **              
BUKTCTRY DS    XL1                 COUNTRY CODE (SEE EQUATES)                   
BUKTEOTQ EQU   255                 END OF TABLE INDICATOR                       
BUKTITYP DS    CL1                 INPUT BUCKET TYPE (IN ELEMENT)               
BUKTOTYP DS    CL1                 OUTPUT BUCKET TYPE (IN RECORD)               
BUKTLEDG DS    CL2                 UNIT/LEDGER CODE (SPACES=ANY)                
BUKTROUT DS    AL2                 DISPLACEMENT TO ROUTINE                      
BUKTABL  EQU   *-BUKTABD                                                        
         SPACE 2                                                                
BUKBUFD  DSECT                     ** LIST OF BUCKETS DSECT **                  
BUKBHDR  DS    0X                  TABLE HEADER                                 
BUKBHNUM DS    XL4                 NUMBER OF ENTRIES IN TABLE                   
BUKBHDRL EQU   *-BUKBHDR                                                        
         ORG   BUKBHDR                                                          
BUKBKEY  DS    0CL2                BUCKET BUFFER KEY TYPE                       
BUKBTYPE DS    CL1                 BUCKET TYPE VALUE                            
BUKBSTAT DS    CL1                 BUCKET STATUS (LIVE/DRAFT DATA)              
BUKBEOTQ EQU   255                 END OF TABLE INDICATOR                       
BUKBMOS  DS    PL2                 BUCKET MONTH OF SERVICE (YYMM)               
BUKBKEYL EQU   *-BUKBUFD           LENGTH OF KEY FOR SORTING                    
BUKBDR   DS    PL8                 BUCKET DEBIT AMOUNT                          
BUKBCR   DS    PL8                 BUCKET CREDIT AMOUNT                         
BUKBUFL  EQU   *-BUKBUFD                                                        
BUKBMAXN EQU   420                 MAXIMUM NUMBER OF BUCKET ENTRIES             
         EJECT                                                                  
* DSECT TO COVER LOCAL WORKING STORAGE                                          
*                                                                               
WORKD    DSECT                                                                  
RELO     DS    A                                                                
SAVERD   DS    A                                                                
ABUCKBLK DS    A                                                                
VCONVMOS DS    V                                                                
VDATAMGR DS    V                                                                
VHELLO   DS    V                                                                
*                                                                               
DUB      DS    D                                                                
FULL     DS    F                                                                
HALF     DS    H                                                                
BYTE     DS    XL1                                                              
BYTE2    DS    XL1                                                              
WORK     DS    CL64                                                             
SPACES   DS    CL64                                                             
ELEMENT  DS    XL256                                                            
DMCB     DS    6A                                                               
ELIST    DS    3A                                                               
ELERR    DS    0XL1                                                             
ELADDR   DS    A                                                                
ELADDR2  DS    A                                                                
*                                                                               
BUKELEM  DS    XL(BUKLNQ)          BUCKET ELEMENT AREA                          
BUKEY    DS    0CL2                BUCKET KEY                                   
BUKTYPE  DS    CL1                 BUCKET TYPE                                  
BUKSTAT  DS    CL1                 BUCKET STATUS                                
BUKSLIVE EQU   C'L'                LIVE                                         
BUKSDRFT EQU   C'D'                DRAFT                                        
BUKSAVE  DS    XL(BUKLNQ)          BUCKET ELEMENT SAVE AREA                     
*                                                                               
SYSCTRY  DS    XL1                 SYSTEM COUNTRY CODE                          
SYSCEUR  EQU   1                   EUROPE                                       
SYSCNAM  EQU   2                   NORTH AMERICA                                
SYSCANY  EQU   0                   ALL CONTINENTS VALID                         
SYSCNOT  EQU   X'80'               NOT BIT (FOR TABLES)                         
*                                                                               
DATADISP DS    H                                                                
MOS      DS    PL2                                                              
FILE     DS    CL8                                                              
*                                                                               
WORKX    EQU   *                                                                
         SPACE 2                                                                
* EQUATES                                                                       
*                                                                               
DELELQ   EQU   X'FF'                                                            
FF       EQU   X'FF'                                                            
         EJECT                                                                  
       ++INCLUDE ACBUCKETD                                                      
         EJECT                                                                  
* ACGENFILE                                                                     
         PRINT OFF                                                              
       ++INCLUDE ACGENFILE                                                      
         PRINT ON                                                               
         SPACE 2                                                                
* ACGENMODES                                                                    
         PRINT OFF                                                              
       ++INCLUDE ACGENMODES                                                     
         PRINT ON                                                               
         SPACE 2                                                                
* DDCOMFACS                                                                     
         PRINT OFF                                                              
       ++INCLUDE DDCOMFACS                                                      
         PRINT ON                                                               
         SPACE 2                                                                
* DDCTRYEQUS                                                                    
         PRINT OFF                                                              
       ++INCLUDE DDCTRYEQUS                                                     
         PRINT ON                                                               
         SPACE 2                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'007ACBUCKET  09/12/02'                                      
         END                                                                    
