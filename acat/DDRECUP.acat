*          DATA SET DDRECUP    AT LEVEL 023 AS OF 03/01/18                      
*CATALP RECUP                                                                   
*                                                                               
*   CHANGE LOG                                                                  
*                                                                               
*   SMUR 09/28/10   ALLOW 4K TRAFFIC BUY ACTIVITY RECORDS                       
*   MHER 10/05/09   ALLOW 4K SPOT (TRF) INST RECAP RECORDS                      
*   AHYD 12/12/08   ALLOW 4K PRINT ESTIMATE BUCKET RECORDS                      
*   AHYD 07/24/08   MOVE RELO FOR MEMORY PROTECT. ADD RECUPWKL WORKD            
*   MHER 11/15/06   ALLOW 6K SPOT BUY RECORDS                                   
*   MHER 05/04/06   ALLOW 4K SPOT CLRST RECORDS                                 
*   MHER 08/25/03   ALLOW 4K SPOT GOAL RECORDS                                  
*   BPLA 08/06/02   ALLOW 3K PRINT CONTRACTS                                    
*   BPLA 03/26/01   ALLOW FOR 4K PRINT ADDN'L CHG RECORDS                       
*   MHER 11/21/00   FOR DARE, ALLOW 4K RECORDS                                  
*   MHER 02/23/00   C'T' FOR XSPFILE                                            
*   MHER 11/9/99    MAX SPTFILE BUY RECORD SIZE TO 3972                         
*                                                                               
         TITLE '*** ELEMENT DELETION/INSERTION RTN ***'                         
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *         
*                                                                     *         
*  'RECUP' PARAMETER LIST                                             *         
*                                                                     *         
*              WORD 1 BYTE  0   - SYSTEM CODE - SEE SYSTAB            *         
*                                    (X'FE'   - SEE WORD 4)                     
*                     BYTES 1-3 - A(RECORD)                           *         
*              WORD 2 BYTES 1-3 - A(ELEMENT)                          *         
*              WORD 3 BYTE  0   - C'R' = RETURN ERROR ON REC OVERFLOW *         
*                                        INSTEAD OF BLOWING UP        *         
*                                        (RECUP WILL LEAVE AS 'R' IF  *         
*                                        OK, ELSE WILL SET TO X'00')  *         
*                     BYTES 1-3 - A(INSERTION)                        *         
*              WORD 4 BYTES 1-3 - IF WORD 1 BYTE 0 = X'FE'            *         
*                                 A(SPECIAL SYSTEM DEFINITION)        *         
*                                 (BYTES 2-7 OF A SYSTAB ENTRY)       *         
*                                                                     *         
*        FUNCTION - IF ELEMENT ADDRESS IS OUTSIDE RECORD LIMITS,      *         
*                   THE ELEMENT IS INSERTED. IF THE ELEMENT ADDRESS   *         
*                   IS WITHIN LIMITS OF THE RECORD, THE ELEMENT       *         
*                   IS DELETED, RECORD LENGTH IS UPDATED TO           *         
*                   REFLECT THE APPROPRIATE CHANGE                    *         
*                                                                     *         
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *         
*                                                                               
         PRINT NOGEN                                                            
RECUP    CSECT                                                                  
         NMOD1 RECUPWKL,RECUP,RR=RE                                             
         LM    R2,R4,0(R1)         LOAD PARAMS                                  
         LA    R2,0(R2)            CLEAR HOB                                    
         LA    R3,0(R3)            CLEAR HOB                                    
         LA    R4,0(R4)            CLEAR HOB                                    
*                                                                               
         USING RECUPWK,RC                                                       
         ST    RE,RELO                                                          
         BRAS  RE,SETSYS                                                        
                                                                                
CHECKEL  MVC   H,0(RA)                                                          
         LH    R7,H                                                             
         LA    R7,0(R7,R2)         POINT TO NEXT BYTE AT EOR                    
         LR    RE,R9                                                            
         SR    R0,R0                                                            
         BCTR  R7,0                                                             
CHECKEL1 CR    RE,R7               CALCULATE RECORD LENGTH                      
         BNL   *+14                                                             
         IC    R0,1(RE)                                                         
         AR    RE,R0                                                            
         B     CHECKEL1                                                         
         LR    R7,RE                                                            
         SR    RE,R2                                                            
         STH   RE,H                                                             
         SR    R5,R5                                                            
*                                                                               
         LR    R8,R9                                                            
         CR    R7,R3                                                            
         BNH   CHECKEL3                                                         
         CR    R2,R3                                                            
         BH    CHECKEL3                                                         
*                                  ELEMENT WITHIN RECORD                        
CHECKEL2 CR    R8,R3                                                            
         BE    RECDEL              DELETE                                       
         BL    *+6                                                              
         DC    H'0'                NOT AN ELEMENT ADDR                          
         IC    R5,1(R8)                                                         
         AR    R8,R5                                                            
         B     CHECKEL2                                                         
*                                  ELEMENT OUTSIDE OF RECORD                    
CHECKEL3 CR    R7,R4               TEST LAST BYTE VS ELEM ADDR                  
         BNL   CHKEL3B                                                          
*                                  NB- HELLO SETS RECORD LENGTH                 
*                                      WITH EXTRA BYTE                          
         BCTR  R4,R0               THIS ADJUSTMENT ALLOWS FOR TRYING            
         CR    R7,R4               TO ADD AN ELEM THERE                         
         BNL   *+6                                                              
         DC    H'0'                INSERTION ADDRESS OUTSIDE RECORD             
CHKEL3B  CR    R2,R4                                                            
         BL    *+6                                                              
         DC    H'0'                INSERTION ADDRESS OUTSIDE RECORD             
CHECKEL4 CR    R8,R4                                                            
         BE    RECIN                                                            
         BL    *+6                                                              
         DC    H'0'                INSERTION ADDR NOT AT ELEMENT                
         IC    R5,1(R8)                                                         
         AR    R8,R5                                                            
         B     CHECKEL4                                                         
* ELEMENT TO BE DELETED                                                         
RECDEL   SR    R4,R4                                                            
         IC    R4,1(R3)            GET LENGTH OF ELEMENT TO BE DELETED          
         MVI   0(R3),0             CLEAR EOR                                    
         BCTR  R7,0                                                             
         LR    R5,R7               SAVE FOR BXLE LIMIT                          
         LH    R7,H                                                             
         SR    R7,R4                                                            
         STH   R7,H                                                             
         MVC   0(2,RA),H           UPDATE RECORD LENGTH                         
         LA    R7,0(R4,R3)         POINT TO NXT ELEMENT IN RECORD               
         CLI   0(R7),0             TEST E-O-R                                   
         BE    RECUPX                                                           
RECDEL01 IC    R4,1(R7)            LENGTH OF NXT ELEM                           
         LTR   R4,R4                                                            
         BC    13,RECUPX                                                        
         LR    R6,R4                                                            
         BCTR  R6,R0               SET EX LENGTH                                
         EX    R6,MVCLFT           MVC  0(0,R3),0(R7)                           
         AR    R3,R4               BUMP PNTR TO END OF ELEMENT MOVED            
         BXLE  R7,R4,RECDEL01                                                   
         MVI   0(R3),0             CLEAR EOR                                    
         B     RECUPX              EXIT TO CALLER                               
*                                                                               
MVCLFT   MVC   0(0,R3),0(R7)                                                    
*                                                                               
* INSERT NEW ELEMENT                                                            
*                                                                               
RECIN    SR    R6,R6                                                            
         ICM   R6,1,1(R3)          LENGTH OF NEW ELEM                           
         BNZ   *+6                                                              
         DC    H'0'                                                             
         AH    R6,H                                                             
         TM    MAXLEN,X'80'        NO MAXIMUM CHECK                             
         BNZ   RECINB                                                           
         CH    R6,MAXLEN                                                        
         BNH   RECINB                                                           
*                                  RECORD WILL BE TOO BIG                       
         CLI   8(R1),C'R'          DOES CALLER WANT TO KNOW                     
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         MVI   8(R1),0             YES, TELL HIM                                
         B     RECUPX                                                           
*                                                                               
RECINB   STH   R6,H                                                             
         AR    R6,R2                                                            
         MVI   0(R6),0             PUT ZERO AT END OF NEW REC                   
*                                                                               
         MVC   0(2,RA),H           UPDATE RECORD LENGTH                         
         LR    R5,R7               POINT TO NEXT BYTE AT EOR                    
         LR    R1,R5                                                            
         SR    R1,R4               NUMBER OF BYTES TO BE SHFTD                  
         SR    R0,R0                                                            
         SR    R6,R6                                                            
         IC    R6,1(R3)            NEW ELEM LGTH                                
         LR    R8,R6               ELEM LGTH                                    
         BCTR  R8,0                EX LGTH                                      
         LR    R9,R8               SAVE                                         
         DR    R0,R6               BCT CNT IN R1/REMNDR IN R0                   
         LTR   R1,R1               TEST IF ANY CNT                              
         BZ    RECIN02             NO-MOVE ONLY REMNG BYTES                     
*                                                                               
         LR    R2,R5                                                            
RECIN01  EQU   *                                                                
         SR    R2,R6               BUMP LFT THRU RECD                           
         EX    R8,MVCRHT           MOVING DATA RGHT FOR A DISTANCE              
         SR    R5,R6               EQUAL TO THE LGTH OF THE NEW ELEM            
         BCT   R1,RECIN01                                                       
*                                                                               
RECIN02  EQU   *                                                                
         LTR   R0,R0               TEST IF ANY REMNG BYTES                      
         BZ    RECIN04             NO-INSERT NEW ELEM                           
         LR    R8,R0                                                            
         BCTR  R8,0                SET EXECUTE FOR REMNG BYTES                  
         AR    R5,R6                                                            
         SR    R5,R0               SET DESTINATION ADDRESS                      
         LR    R2,R4               INSERTION ADDRESS                            
         EX    R8,MVCRHT                                                        
         LR    R8,R9               RESTORE NEW ELEM EX LGTH                     
*                                                                               
RECIN04  LR    R5,R4               INSERT ADDRESS                               
         LR    R2,R3               ELEMENT ADDRESS                              
         EX    R8,MVCRHT           INSERT NEW ELEM                              
*                                                                               
RECUPX   XIT1                      RETURN                                       
*                                                                               
MVCRHT   MVC   0(0,R5),0(R2)       SHIFT CURRENT DATA RIGHT                     
         EJECT                                                                  
SETSYS   DS    0H                                                               
         CLI   0(R1),X'FE'                                                      
         BNE   SS1                                                              
         L     RF,12(R1)           A(SPECIAL SYS DEFINITION)                    
         XC    SPCLENT,SPCLENT                                                  
         MVC   SPCLENT+2(6),0(RF)                                               
         LA    RF,SPCLENT          ONLY AN IDIOT COULD FORGET THIS              
         B     SS8                                                              
*                                                                               
SS1      DS    0H                                                               
         LA    RF,SYSTAB                                                        
         LA    R0,SYSTABN                                                       
*                                                                               
SS2      DS    0H                                                               
         CLC   0(1,R1),0(RF)       TEST VS GIVEN SYSTEM                         
         BE    SS8                                                              
         LA    RF,SYSTABL(RF)                                                   
         BCT   R0,SS2                                                           
         DC    H'0'                                                             
*                                                                               
SS8      DS    0H                                                               
         SR    R9,R9                                                            
         ICM   R9,3,2(RF)          ELEM START                                   
         AR    R9,R2                                                            
         SR    RA,RA                                                            
         ICM   RA,3,4(RF)          LENGTH POSITION                              
         AR    RA,R2                                                            
         MVC   MAXLEN,6(RF)        MAXIMUM LENGTH                               
*                                                                               
         ICM   RF,15,8(RF)         TEST SPECIAL ROUTINE                         
         BZR   RE                  NO - RETURN                                  
         A     RF,RELO                                                          
         BASR  R0,RF               ** SET R0 IN CASE OF DUMP **                 
         SPACE 2                                                                
* ALL PRINTPAK RECORDS                                                          
* START WITH AGY(2)/MED(1) EXCEPT PUBFILE RECORDS                               
*                                                                               
SETPRNT  DS    0H                  SPECIALS FOR PRINT                           
         CLI   1(R2),X'99'         PUB RECS                                     
         BH    *+14                                                             
         MVC   MAXLEN,=H'3976'     ARE BIG                                      
         B     SETPRNTX                                                         
*                                                                               
         CLI   3(R2),X'28'         SEE IF SPACE CU RECORD                       
         BNE   *+14                                                             
         MVC   MAXLEN,=H'2976'     SET MAXIMUM TO 3000                          
         B     SETPRNTX                                                         
*                                                                               
         CLI   3(R2),X'60'         ADDITIONAL CHARGE RECORDS                    
         BNE   *+14                                                             
         MVC   MAXLEN,=H'3976'     SET MAXIMUM TO 4000                          
         B     SETPRNTX                                                         
*                                                                               
         CLI   3(R2),X'20'         INSERTION RECS NOW 3000 5/18/87              
         BNE   *+14                                                             
         MVC   MAXLEN,=H'2976'                                                  
         B     SETPRNTX                                                         
*                                                                               
         CLI   3(R2),X'10'         CONTRACTS ARE 3000                           
         BNE   *+14                                                             
         MVC   MAXLEN,=H'2976'                                                  
         B     SETPRNTX                                                         
*                                                                               
         CLI   3(R2),X'09'        ESTIMATE BUCKETS - 4000                       
         BNE   SETPRNTX                                                         
*                                                                               
SETPRNT2 MVC   MAXLEN,=H'3976'                                                  
*                                                                               
SETPRNTX BR    RE                                                               
         SPACE 2                                                                
* SPOT BUY RECORDS HAVE MAX LEN 3976                                            
* AND TRAFFIC BUYACT REC 0A2E                                                   
* AND TRAFFIC INST RECAP 0A24                                                   
* AND DARE RECORDS (0D34-0D38)                                                  
* AND 0D76 (SPOT CLRST) RECORDS                                                 
* AND GOAL RECORDS                                                              
* ALL OTHER RECORDS HAVE 1976                                                   
*                                                                               
SETSPOT  DS    0H                                                               
         CLI   0(R2),X'02'         TEST GOAL RECORD                             
         BE    SET3976                                                          
         CLC   =X'0A24',0(R2)      TEST INST RECAP RECORD                       
         BE    SET3976                                                          
         CLC   =X'0A2E',0(R2)      TEST TRAFFIC BUYACT REC                      
         BE    SET3976                                                          
         CLI   0(R2),X'10'         TEST BUY RECORD                              
         BHR   RE                                                               
         CLC   0(2,R2),=X'0D76'                                                 
         BER   RE                                                               
         CLC   0(2,R2),=X'0D34'                                                 
         BL    SETSPLO                                                          
         CLC   0(2,R2),=X'0D38'                                                 
         BH    SETSPLO                                                          
SET3976  MVC   MAXLEN,=H'3976'                                                  
         BR    RE                                                               
SETSPLO  MVC   MAXLEN,=H'1976'                                                  
         BR    RE                                                               
*                                                                               
         SPACE 3                                                                
*        SYSTEM TABLE                                                           
*              SYSTEM (1)                                                       
*              ELEM START (2)                                                   
*              LENGTH POSITION (2)                                              
*              MAXIMUM REC LENGTH (2)                                           
*                                                                               
         DS    0D                                                               
SYSTAB   DS    0XL12                                                            
         DC    X'0000',AL2(24,13,5972),AL4(SETSPOT) SPOT                        
         DC    X'0100',AL2(33,25,999),AL4(SETPRNT)  PRINT                       
         DC    X'0200',AL2(34,27,3972),AL4(0)       REP                         
         DC    X'FF00',AL2(2,0),X'8000',AL4(0)      ELEM BUFFER                 
*                                                                               
         DC    C'S ',AL2(24,13,5972),AL4(SETSPOT)   SPOT SPTFIL                 
         DC    C'T ',AL2(42,32,3972),AL4(0)         SPOT XSPFIL                 
         DC    C'P ',AL2(33,25,999),AL4(SETPRNT)    PRINT                       
         DC    C'R ',AL2(34,27,3972),AL4(0)         REP                         
         DC    C'L ',AL2(42,32,1976),AL4(0)         PLANNING                    
         DC    C'X ',AL2(16,09,3976),AL4(0)         PLANNING - XVERT            
         DC    C'U ',AL2(27,20,5972),AL4(0)         NETWORK UNITS               
*                                                                               
SYSTABL  EQU   12                                                               
SYSTABN  EQU   (*-SYSTAB)/SYSTABL                                               
         SPACE 2                                                                
RECUPWK  DSECT                                                                  
RELO     DS    A                                                                
H        DS    H                                                                
MAXLEN   DS    H                                                                
SPCLENT  DS    XL12                                                             
RECUPWKL EQU   *-RECUPWK                                                        
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'023DDRECUP   03/01/18'                                      
         END                                                                    
