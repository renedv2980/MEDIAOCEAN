*          DATA SET MPRDR93S   AT LEVEL 060 AS OF 05/01/02                      
*CATALP MPRDR93                                                                 
         TITLE 'T51093 - READREX TESTER'                                        
T51093   CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 0,T51093,RR=R2                                                   
         LA    R7,2048(RB)                                                      
         LA    R7,2048(R7)                                                      
         USING T51093+4096,R7                                                   
*                                                                               
         L     RC,0(R1)                                                         
         USING GEND,RC                                                          
         L     R8,ASPOOLD                                                       
         USING SPOOLD,R8                                                        
         L     RA,ATWA                                                          
         USING CONHEADH-64,RA      BASE SCREEN FOR SYSTEM + THIS PROG           
         L     R9,ASYSD                                                         
         USING SYSD,R9                                                          
         ST    R2,RELO                                                          
*                                                                               
         GOTO1 SETADDR                                                          
*                                                                               
         CLI   MODE,VALKEY         VALIDATE RECORD KEY                          
         BE    VKEY                                                             
         CLI   MODE,PRINTREP       PRINT REPORT                                 
         BE    PREP                                                             
         B     XIT                                                              
         EJECT                                                                  
*        VALIDATE KEY                                                           
         SPACE 2                                                                
VKEY     DS    0H                                                               
         LA    R4,KEY                                                           
         XC    KEY,KEY                                                          
         LA    R2,RXTSURH          SURVEY                                       
         GOTO1 VALSURV                                                          
*                                                                               
         LA    R2,RXTTRGH          TARGET                                       
         NI    4(R2),X'DF'         FORCE REVALIDATION                           
         GOTO1 VALTARG                                                          
         MVC   KEY,SVTRGKEY                                                     
         GOTO1 GETREC                                                           
         L     RF,=A(TARGREC)       MOVE RECORD                                 
         A     RF,RELO                                                          
         LA    R1,2000                                                          
         L     RE,AIO                                                           
         MOVE  ((RF),(R1)),(RE)                                                 
*                                                                               
         LA    R2,RXTBASH          BASE                                         
         NI    4(R2),X'DF'         FORCE REVALIDATION                           
         CLI   5(R2),0             IF NONE                                      
         BNE   VK10                                                             
         L     RF,=A(BASEREC)                                                   
         A     RF,RELO                                                          
         XC    0(64,RF),0(RF)      CLEAR BASE REC KEY                           
         B     VK20                                                             
*                                                                               
VK10     DS    0H                                                               
         GOTO1 VALTARG                                                          
         MVC   KEY,SVTRGKEY                                                     
         GOTO1 GETREC                                                           
         L     RF,=A(BASEREC)       MOVE RECORD                                 
         A     RF,RELO                                                          
         LA    R1,2000                                                          
         L     RE,AIO                                                           
         MOVE  ((RF),(R1)),(RE)                                                 
*                                                                               
VK20     DS    0H                                                               
         LA    R2,RXTRFNH          RESPONSE FUNCTION RECORD                     
         NI    4(R2),X'DF'         FORCE REVALIDATION                           
         CLI   5(R2),0             IF NONE                                      
         BNE   VK22                                                             
         L     RF,=A(RESPREC)                                                   
         A     RF,RELO                                                          
         XC    0(64,RF),0(RF)      CLEAR BASE REC KEY                           
         B     VK24                                                             
*                                                                               
VK22     DS    0H                                                               
         GOTO1 ANY                                                              
         XC    KEY,KEY                                                          
         LA    R4,KEY                                                           
         USING QRFKEY,R4                                                        
         MVI   QRFKSYS,C'Q'                                                     
         MVC   QRFKAGY,TWAAGY                                                   
         MVI   QRFKRCD,X'27'                                                    
         MVC   QRFKCOD,WORK                                                     
         GOTO1 HIGH                                                             
         CLC   KEY(QKEYL),KEYSAVE                                               
         BE    *+12                                                             
         MVI   ERROR,NOTFOUND                                                   
         B     TRAPERR                                                          
*                                                                               
         GOTO1 GETREC                                                           
         L     RF,=A(RESPREC)       MOVE RECORD                                 
         A     RF,RELO                                                          
         LA    R1,2000                                                          
         L     RE,AIO                                                           
         MOVE  ((RF),(R1)),(RE)                                                 
*                                                                               
VK24     DS    0H                                                               
         LA    R2,RXTOPTH          VALIDATE OPTIONS                             
         BAS   RE,VALOPT                                                        
*                                                                               
VK80     DS    0H                                                               
         B     XIT                                                              
*                                                                               
         SPACE 3                                                                
*        VALIDATE OPTIONS                                                       
*                                                                               
VALOPT   DS    0H                                                               
         MVC   QOPTS,8(R2)                                                      
         BR    RE                                                               
         EJECT                                                                  
         SPACE 2                                                                
PREP     DS    0H                                                               
         CLI   OFFLINE,C'Y'        SKIP UNLESS OFFLINE                          
         BNE   XIT                                                              
*                                                                               
         MVI   DASHES,C'-'                                                      
         MVC   DASHES+1(L'DASHES-1),DASHES                                      
*                                                                               
         L     R5,VMPQBLK                                                       
         USING MPQBLKD,R5                                                       
         MVI   MPQBNOWT,C'N'       DONT SUPRESS WGTS                            
         MVC   MPQBPPRE,QOPT7      POP PRECISION                                
         NI    MPQBPPRE,X'0F'      STRIP ZONE                                   
         DROP  R5                                                               
*                            **NB- AREA COVERED BY RXCLTD WILL BE               
*                                  KEPT AT BOOKVAL (AN UNUSED                   
*                                  CORE-RESIDENT PHASE) BECAUSE                 
*                                  NOTHING ELSE GETS SAVED                      
*                                  FROM ONE REQ TO ANOTHER                      
*                                                                               
         L     RE,BOOKVAL          GET RXCTLD AREA                              
         CLC   =X'90EC',0(RE)      TEST FIRST TIME (NMOD PRESENT)               
         BNE   PREP2                                                            
         LA    RF,RXCTLDL          CLEAR                                        
         XCEF                                                                   
*                                                                               
PREP2    DS    0H                                                               
         L     R6,BOOKVAL                                                       
         USING RXCTLD,R6                                                        
*                                                                               
         LA    RF,PCMTAB                                                        
         ST    RF,RXPCMTAB                                                      
         LA    RF,DVTAB                                                         
         ST    RF,RXDVTAB                                                       
         LA    RF,WAVLST                                                        
         ST    RF,RXWAVLST                                                      
         LA    RF,DVL              SET DATA VECTOR LENGTH                       
         ST    RF,RXDVL                                                         
         L     RF,=A(TARGREC)      TARGET RECORD                                
         A     RF,RELO                                                          
         ST    RF,RXTRGR                                                        
         L     RF,=A(BASEREC)      BASE REC                                     
         A     RF,RELO                                                          
         ST    RF,RXBASR                                                        
         CLI   0(RF),0             TEST ANY BASE                                
         BNE   *+10                                                             
         XC    RXBASR,RXBASR       NO CLEAR ADDRESS                             
         L     RF,=A(RESPREC)      RESP FUNCTION REC                            
         A     RF,RELO                                                          
         ST    RF,RXRESPR                                                       
         MVC   RXSURV,RXTSUR                                                    
         OC    RXSURV,SPACES                                                    
         MVC   RXOPTS(6),QOPTS                                                  
         XC    RXINS,RXINS                                                      
*                                                                               
PREP3    DS    0H                                                               
         SR    R5,R5                                                            
         LA    R4,DVTAB                                                         
         LA    R3,VLINC                                                         
         XC    RXNDVS,RXNDVS                                                    
         MVI   INSRTSW,C'N'                                                     
*                                                                               
PREP3D   DS    0H                                                               
         XC    0(DVL,R4),0(R4)     CLEAR VECTOR                                 
         LA    R2,RXTNAMH(R5)       VEH CODE                                    
         CLI   5(R2),0                                                          
         BE    PREP3M                                                           
*                                                                               
         L     RF,RXNDVS           BUMP VECTOR COUNT                            
         LA    RF,1(RF)                                                         
         ST    RF,RXNDVS                                                        
*                                                                               
         MVC   0(12,R4),8(R2)                                                   
         LA    R2,RXTQCD(R5)       VEH Q CODE                                   
         MVC   12(8,R4),0(R2)                                                   
         LA    R2,RXTINSH(R5)      INS COUNT                                    
         CLI   5(R2),0                                                          
         BE    PREP3F                                                           
         ZIC   R1,5(R2)                                                         
         BCTR  R1,R0                                                            
         EX    R1,*+8                                                           
         B     *+10                                                             
         PACK  DUB,8(0,R2)                                                      
         CVB   R1,DUB                                                           
         LTR   R1,R1                                                            
         BNP   *+8                                                              
         MVI   INSRTSW,C'Y'                                                     
         ST    R1,44(R4)                                                        
PREP3F   DS    0H                                                               
         LA    R2,RXTADJH(R5)      ADJUSTMENT                                   
         CLI   5(R2),0                                                          
         BE    PREP3H                                                           
         ZIC   R0,5(R2)                                                         
         GOTO1 CASHVAL,DMCB,(3,8(R2)),(R0)                                      
         CLI   DMCB,X'FF'                                                       
         BNE   *+12                                                             
         MVI   ERROR,INVALID                                                    
         B     TRAPERR                                                          
         MVC   20(4,R4),DMCB+4                                                  
*                                                                               
PREP3H   DS    0H                                                               
         LA    R2,RXTIMPH(R5)      IMPACT                                       
         CLI   5(R2),0                                                          
         BE    PREP3I                                                           
         ZIC   R0,5(R2)                                                         
         GOTO1 CASHVAL,DMCB,(3,8(R2)),(R0)                                      
         CLI   DMCB,X'FF'                                                       
         BNE   *+12                                                             
         MVI   ERROR,INVALID                                                    
         B     TRAPERR                                                          
         MVC   24(4,R4),DMCB+4                                                  
*                                                                               
PREP3I   DS    0H                                                               
         LA    R2,RXTPDUH(R5)      PROTO-DUPF                                   
         CLI   5(R2),0                                                          
         BE    PREP3J                                                           
         ZIC   R0,5(R2)                                                         
         GOTO1 CASHVAL,DMCB,(3,8(R2)),(R0)                                      
         CLI   DMCB,X'FF'                                                       
         BNE   *+12                                                             
         MVI   ERROR,INVALID                                                    
         B     TRAPERR                                                          
         MVC   28(4,R4),DMCB+4                                                  
*                                                                               
PREP3J   DS    0H                                                               
         LA    R4,DVL(R4)                                                       
*                                                                               
PREP3M   DS    0H                                                               
         LA    R5,VLINL(R5)                                                     
         BCT   R3,PREP3D                                                        
*                                                                               
         LA    R2,RXTWVLH                                                       
         XC    SCANWRK,SCANWRK                                                  
         GOTO1 SCANNER,DMCB,(R2),SCANWRK                                        
         LA    R5,SCANWRK                                                       
         LA    R4,WAVLST                                                        
*                                                                               
PREP4    DS    0H                                                               
         CLI   0(R5),0                                                          
         BE    PREP6                                                            
         GOTO1 DATVAL,DMCB,(2,12(R5)),DUB                                       
         OC    DMCB(4),DMCB                                                     
         BNZ   *+12                                                             
         MVI   ERROR,INVDATE                                                    
         B     TRAPERR                                                          
         MVC   DUB+4(2),=C'01'                                                  
         GOTO1 DATCON,DMCB,DUB,(2,0(R4))                                        
         LA    R4,2(R4)                                                         
         LA    R5,32(R5)           NEXT DATE                                    
         B     PREP4                                                            
*                                                                               
PREP6    DS    0H                                                               
         MVI   0(R4),0             SET END OF WAV LIST                          
*                                                                               
         LA    R2,RXTDTAH          DATA ITEMS                                   
         XC    SCANWRK,SCANWRK                                                  
         GOTO1 SCANNER,DMCB,(R2),SCANWRK                                        
         LA    R5,SCANWRK                                                       
         LA    R4,PCMTB            WHERE TO ADD DATA ITEMS                      
         LA    R3,48               BYTE USED SO FAR IN DATA VECTOR              
         CLI   INSRTSW,C'Y'        TEST HAVE INSERTS                            
         BE    *+12                                                             
         LA    R4,PCMTA          NO- REMOVE INSERTS FROM DATA VECTOR            
         LA    R3,44                                                            
         USING PCMDD,R4                                                         
*                                                                               
PREP6D   DS    0H                                                               
         CLI   0(R5),0                                                          
         BE    PREP8                                                            
         LA    RF,BASWDS                                                        
         MVI   ERROR,INVALID                                                    
*                                                                               
PREP6F   DS    0H                                                               
         CLI   0(RF),X'FF'                                                      
         BE    TRAPERR                                                          
         CLC   0(3,RF),12(R5)      TEST 3 CHARACTERS                            
         BE    *+12                                                             
         LA    RF,4(RF)                                                         
         B     PREP6F                                                           
*                                                                               
         MVC   PCBASE,3(RF)        SET BASE CODE                                
         MVI   PCQUAL,Q#           QUAL = COUNT                                 
         XC    PCSFX,PCSFX         SUFFIX = ZERO                                
         CLI   12+3(R5),C'0'                                                    
         BNH   PREP6H                                                           
         MVC   PCSFX+1(1),12+3(R5)                                              
         NI    PCSFX+1,X'0F'                                                    
*                                                                               
PREP6H   DS    0H                                                               
         MVI   PCTYP,C'D'                                                       
         STCM  R3,15,PCWHERE       SET DATA VECTOR DISP                         
*                                                                               
         LA    R3,4(R3)            BUMP DATA VECTOR DISP                        
         LA    R4,PCMDL(R4)        NEXT COMMAND                                 
         LA    R5,32(R5)           NEXT FIELD                                   
         B     PREP6D                                                           
*                                                                               
PREP8    DS    0H                                                               
         L     RF,=V(COVAIL)       SET COVAIL ADDRESS                           
         A     RF,RELO                                                          
         ST    RF,RXCOVAIL                                                      
         L     RF,=V(BINSRCH)      SET BINSRCH ADDRESS                          
         A     RF,RELO                                                          
         ST    RF,RXBINSCH                                                      
*                                                                               
         GOTO1 =V(READREX),DMCB,GEND,RXCTLD,RR=RELO                             
*                                                                               
         B     XIT                                                              
         EJECT                                                                  
*        VARIOUS SHORT ROUTINES                                                 
*        ----------------------                                                 
*                                                                               
GENDISP  ZIC   R1,0(R2)            GENERAL DISPLAY                              
         SH    R1,=H'9'                                                         
         EX    R1,GDCLC            TEST ALREADY ON SCREEN                       
         BER   RE                  YES- RETURN (CC=)                            
         EX    R1,GDMVC            NO- PUT IT THERE                             
         OI    6(R2),X'80'         TRANSMIT                                     
         BR    RE                  RETURN (CC NOT=)                             
*                                                                               
GDCLC    CLC   8(0,R2),WORK                                                     
GDMVC    MVC   8(0,R2),WORK                                                     
         SPACE 2                                                                
BUMP     ZIC   RF,0(R2)            BUMP TO NEXT SCREEN FIELD                    
         AR    R2,RF                                                            
         CLI   0(R2),0             EOS- RETURN =                                
         BR    RE                                                               
         SPACE 2                                                                
BUMPU    ZIC   RF,0(R2)            BUMP TO NEXT UNPROTECTED FIELD               
         AR    R2,RF                                                            
         CLI   0(R2),0             EOS- RETRUN =                                
         BER   RE                                                               
         TM    1(R2),X'20'                                                      
         BNZ   BUMPU                                                            
         LTR   RE,RE               NOT EOS- RETURN NOT =                        
         BR    RE                                                               
*                                                                               
NUMDISP  DS    0H                  DISPLAY A SIMPLE NUMERIC FIELD               
         EDIT  (B4,FULL),(10,WORK),ALIGN=LEFT                                   
         B     GENDISP                                                          
*                                                                               
BADX     LTR   RB,RB                                                            
         B     XIT                 CC OF NEQ = ERROR EXIT                       
*                                                                               
GOODX    CR    RB,RB                                                            
         B     XIT                 CC OF EQ = GOOD EXIT                         
*                                                                               
XIT      XIT1                                                                   
         SPACE 2                                                                
         PRINT GEN                                                              
         GETEL R6,DATADISP,ELCODE                                               
         PRINT NOGEN                                                            
         SPACE 1                                                                
TRAPERR  DS    0H                                                               
         CLI   OFFLINE,C'Y'                                                     
         BNE   *+6                                                              
         DC    H'0'                                                             
         GOTO1 ERREX                                                            
         B     XIT                                                              
         SPACE 1                                                                
         LTORG                                                                  
         SPACE 2                                                                
         DS    0D                                                               
BASWDS   DS    0X                                                               
         DC    CL3'AUD',AL1(BAUD)                                               
         DC    CL3'RCH',AL1(BRCH)                                               
         DC    CL3'VRD',AL1(BBRD)                                               
         DC    CL3'DUP',AL1(BDUP)                                               
         DC    CL3'NET',AL1(BNET)                                               
         DC    CL3'SUM',AL1(BSUM)                                               
         DC    CL3'UNI',AL1(BUNIQ)                                              
         DC    X'FF'                                                            
*                                                                               
         DS    0D                                                               
         DC    CL8'*PCMTAB*'                                                    
PCMTAB   DS    0X                                                               
         DC    AL1(BVCOD),AL1(Q#),AL2(0),C'D',AL4(0)                            
         DC    AL1(BVQCD),AL1(Q#),AL2(0),C'D',AL4(12)                           
         DC    AL1(BVADJ),AL1(Q#),AL2(0),C'D',AL4(20)                           
         DC    AL1(BVIMPF),AL1(Q#),AL2(0),C'D',AL4(24)                          
         DC    AL1(BVDUPF),AL1(Q#),AL2(0),C'D',AL4(28)                          
         DC    AL1(BVCTL),AL1(Q#),AL2(0),C'D',AL4(32)                           
PCMTA    DC    AL1(BVINS),AL1(Q#),AL2(0),C'D',AL4(44)                           
PCMTB    DS    XL100               ROOM FOR MORE COMMANDS                       
*                                  NOTE - 48 BYTES OF DATA VECTOR USED          
         DS    0D                                                               
         DC    CL8'*DVTAB**'                                                    
DVTAB    DS    XL2000                                                           
DVL      EQU   72                  CONSTANT FOR DATA VECTOR LENGTH              
*                                                                               
         DS    0D                                                               
         DC    CL8'*TARGRC*'                                                    
TARGREC  DS    XL2000                                                           
         DC    CL8'*BASERC*'                                                    
BASEREC  DS    XL2000                                                           
         DC    CL8'*RESPRC*'                                                    
RESPREC  DS    XL2000                                                           
*                                                                               
         DC    CL8'*RXCTLC*'                                                    
RXCTLA   DC    800X'00'            CONTROL AREA                                 
         EJECT                                                                  
         PRINT OFF                                                              
       ++INCLUDE MPGENQS                                                        
       ++INCLUDE DDSPOOLD                                                       
       ++INCLUDE DDSPLWORKD                                                     
       ++INCLUDE MPRDRFFD                                                       
       ++INCLUDE DDGENTWA                                                       
         PRINT ON                                                               
         ORG   CONTAGH                                                          
       ++INCLUDE MPRDR92D                                                       
*                                                                               
VLINL    EQU   (RXTNAM2H-RXTNAMH)            LENGTH OF VEH LINE                 
VLINC    EQU   (RXTNAMLH-RXTNAMH)/VLINL+1    NUMBER OF VEH LINES                
         EJECT                                                                  
       ++INCLUDE MPQBLKD                                                        
         SPACE 2                                                                
       ++INCLUDE MPRXCTLD                                                       
         SPACE 2                                                                
       ++INCLUDE MPRDRWORKD                                                     
         SPACE 2                                                                
         ORG   SYSUSAV             SAVED AREA                                   
         DS    0D                                                               
*                                                                               
         SPACE 2                                                                
         ORG   SYSUNON             NON-SAVED AREA                               
RELO     DS    F                                                                
WAVLST   DS    XL21                                                             
DASHES   DS    XL40                                                             
INSRTSW  DS    C                                                                
*                                                                               
*                                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'060MPRDR93S  05/01/02'                                      
         END                                                                    
