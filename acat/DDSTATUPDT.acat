*          DATA SET DDSTATUPDT AT LEVEL 004 AS OF 06/03/14                      
*CATALP STATUPDT                                                                
*                                                                               
*UPSI 1XXXXXXX TAPE INPUT ELSE DISK INPUT  (EQU INTAPE)                         
*UPSI X1XXXXXX INHIBIT DISK UPDATE         (EQU NODISK)                         
*UPSI XX1XXXXX INHIBIT TAPE ADROUT I/OS    (EQU NOTAPE)                         
*UPSI XXX1XXXX INHIBIT TAPE ADROUT2 I/OS   (EQU NOTAPE2)                        
*UPSI XXXX1XXX INHIBIT SORT PHASE          (EQU NOSORT)                         
*UPSI XXXXX1XX GROUP TERMS ON 1ST 4 CHRS   (EQU GRPTRM)                         
*UPSI XXXXXXX1 SET DSPACE=T & DDSIO=DDSION (EQU TSTSYS)                         
*                                                                               
         TITLE 'STATUPDT - NEW ADRFILE STATISTICS FILE UPDATE'                  
         PRINT NOGEN                                                            
STATUPDT CSECT                                                                  
         ENTRY SSB                                                              
         ENTRY UTL                                                              
         NBASE 0,STATUPDT,WORK=A(STATWORK)                                      
GETSVP   ST    R1,ACOMRG           SAVE MVS PARM ADDR                           
         L     R1,0(R1)                                                         
         LH    R2,0(R1)            R2=L'PARM DATA                               
         LTR   R2,R2                                                            
         BZ    GETSVPX                                                          
         CHI   R2,8                                                             
         BNH   *+8                                                              
         LA    R2,8                                                             
         LA    R1,2(R1)            R1=A(PARM DATA)                              
         LA    RF,GETSVPT                                                       
GETSVP1  CLI   0(R1),C','                                                       
         BE    GETSVPX                                                          
         CLI   0(R1),C'0'                                                       
         BE    GETSVP2                                                          
         CLI   0(R1),C'1'                                                       
         BNE   GETSVP2                                                          
         OC    UPSI,0(RF)                                                       
GETSVP2  LA    R1,1(R1)                                                         
         LA    RF,1(RF)                                                         
         BCT   R2,GETSVP1                                                       
         B     GETSVPX                                                          
GETSVPT  DC    X'8040201008040201'                                              
GETSVPX  EQU   *                                                                
INTAPE   EQU   X'80'                                                            
NODISK   EQU   X'40'                                                            
NOTAPE   EQU   X'20'                                                            
NOTAPE2  EQU   X'10'                                                            
NOSORT   EQU   X'08'                                                            
GRPTRM   EQU   X'04'                                                            
TSTSYS   EQU   X'01'                                                            
         EJECT                                                                  
INITIAL  L     RF,=A(SYSIN)        TEST IF ANY SYSIN CARDS                      
         BASR  RE,RF                                                            
         L     RA,=V(CPRINT)                                                    
         USING DPRINT,RA                                                        
         MVC   TITLE(30),=C'     STATUPDT DOWN TIME REPORT'                     
         L     RF,=A(SUBA)                                                      
         MVC   SUB1(L'SUBA),0(RF)                                               
         L     RF,=A(SUBB)                                                      
         MVC   SUB2(L'SUBB),0(RF)                                               
         MVC   UTIME,TIMEUP        SYSTEM START UP TIME                         
         XC    DTIME,DTIME                                                      
         DATE  DATE,FUNNY=NO                                                    
         TIME                                                                   
         ST    R1,ALTDATE          SAVE 4 BTYE PACKED JULIAN DATE               
         ST    R1,POPDATE                                                       
*                                                                               
INIT0    TM    UPSI,TSTSYS         SET TEST SYSTEM DDSIO AND DSPACE             
         BZ    INIT0X                                                           
         LT    RF,=V(DDSIO)                                                     
         BZ    *+10                                                             
*&&UK*&& MVC   0(8,RF),=CL8'DDSIO'                                              
*&&US*&& MVC   0(8,RF),=CL8'DDSION'                                             
         L     RF,=V(SSB)                                                       
         MVI   SSODSPAC-SSOOFF(RF),C'T'                                         
INIT0X   EQU   *                                                                
*                                                                               
         GOTO1 =V(DATAMGR),DMCB,=C'DTFAD',=C'ADRFILE'                           
         MVC   VADRFILE,DMCB+12                                                 
         GOTO1 (RF),(R1),,=C'STATS'                                             
         MVC   VSTATS,DMCB+12                                                   
*                                                                               
         L     RE,VADRFILE         RE=V(ADRFILE)                                
         OI    36(RE),X'80'        SET READ-ONLY                                
         MVC   52(2,RE),=H'6400'   SET BLOCK LENGTH IN DTF DBLKSZ               
         OI    52(RE),X'80'        SET F/L REC FLAG IN DTF DBLKSZ               
         ST    RE,PLDADDS+12                                                    
         GOTO1 =V(DADDS),PLDADDS   OPEN ADRFILE RECORDER FILE                   
*                                                                               
         L     RE,VSTATS           RE=V(STATS)                                  
         TM    UPSI,NODISK+NOSORT  TEST NOT UPDATING STATS FILE                 
         BZ    *+8                                                              
         OI    36(RE),X'80'        SET READ-ONLY                                
         ST    RE,PLDADDS+12                                                    
         BASR  RE,RF               OPEN STATS FILE                              
*                                                                               
         LA    RF,OPENOUT          OPEN TAPE FILE                               
         TM    UPSI,INTAPE                                                      
         BZ    *+8                                                              
         LA    RF,OPENIN                                                        
         BASR  R9,RF                                                            
         EJECT                                                                  
         L     R6,=A(NDXREC)                                                    
         LR    R0,R6                                                            
         BAS   R9,SSTATS           READ PROGRAM INDEX RECORD                    
         LR    R1,R6                                                            
         AHI   R1,2416                                                          
         MVC   0(12,R1),=12X'FF'                                                
         LH    R7,2(R6)                                                         
         BCTR  R7,0                R7=NUM OF TERMINAL INDEX RECORDS             
         L     R6,=A(TRMREC)                                                    
INIT1    AHI   R6,-16                                                           
         MVC   TEMP(16),0(R6)                                                   
         LR    R0,R6                                                            
         BAS   R9,SSTATS           READ TERMINAL INDEX RECORD                   
         MVC   0(16,R6),TEMP                                                    
         AHI   R6,2416                                                          
         BCT   R7,INIT1                                                         
         MVC   0(12,R6),=12X'FF'                                                
         B     INPUT                                                            
         EJECT                                                                  
INPUT    LA    R2,ADRRCD           R2=A(ADPAK RECORDER FILE RECORD)             
         USING ADRRECD,R2                                                       
         CLI   FLAG,1                                                           
         BE    INPUT1                                                           
         BH    INPUT9                                                           
         MVI   FLAG,1              FIRST TIME FIX TO FORCE I/O                  
*                                                                               
INPUT0   LH    R6,IOPTRMAX                                                      
         LA    R6,1(R6)                                                         
         STH   R6,IOPTR                                                         
*                                                                               
INPUT1   L     R3,=A(IOBUFF)                                                    
         CLC   IOPTR,IOPTRMAX                                                   
         BNH   INPUT5                                                           
         MVC   IOPTR,=H'1'                                                      
         TM    UPSI,INTAPE         TEST TAPE INPUT                              
         BZ    INPUT2                                                           
         BAS   R9,GETIN                                                         
         B     INPUT5                                                           
*                                                                               
INPUT2   GOTO1 DATAMGR,DMCB,DMRSEQ,ADRFILE,ADRADR,(R3)                          
         TM    8(R1),X'80'                                                      
         BO    OUTPUT              END-OF-FILE                                  
         CLI   8(R1),0                                                          
         BE    *+6                                                              
         DC    H'0'                                                             
         BAS   R9,PUTOUT           COPY DISK BLOCK TO TAPE                      
*                                                                               
INPUT5   CLC   0(4,R3),=C'$PQS'    TEST IF NEW STYLE RECORD                     
         BNE   INPUT5A                                                          
         TM    39(R3),X'80'        THIS IS SET ON IN 80-CHR CODE                
         BZ    INPUT5A                                                          
         MVC   RECLEN,=H'80'                                                    
         MVC   IOPTRMAX,=H'80'                                                  
         L     RF,=A(RECCARD)                                                   
         MVC   21(2,RF),=C'80'                                                  
INPUT5A  LH    R7,IOPTR            R7=INDEX INTO BLOCK                          
         BCTR  R7,0                                                             
         MH    R7,RECLEN                                                        
         AR    R3,R7                                                            
         LH    RF,RECLEN                                                        
         BCTR  RF,0                                                             
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   ADRRCD(0),0(R3)     MOVE RECORD OUT OF BLOCK                     
*                                                                               
INPUT6   LTR   R7,R7               TEST FIRST CHR OF NEW BLOCK                  
         BNZ   *+12                                                             
         CLI   ADRREC,C'*'         IF EQ C'*' THEN IGNORE WHOLE BLOCK           
         BE    INPUT0                                                           
         CLC   ADRREC(4),=C'$PQT'  EXTRACT TIMER POP LOG INFO                   
         BNE   INPUT6X                                                          
         MVC   POPSYSN4(16),ADRREC+40                                           
         CLI   POPSYSN4+3,C' '                                                  
         BNE   *+14                                                             
         MVC   TITLE+1(3),POPSYSN4                                              
         B     INPUT6X                                                          
         MVC   TITLE(4),POPSYSN4                                                
INPUT6X  EQU   *                                                                
*                                                                               
INPUT7   BAS   R9,PUTOUT2          PUT RECORD TO ADROUT2 FOR SAS                
         CLI   ADRREC,C'A'                                                      
         BNL   INPUT8                                                           
         LH    R7,IOPTR            IGNORE NON FACPAK TRANSACTIONS               
         LA    R7,1(R7)                                                         
         STH   R7,IOPTR                                                         
         B     INPUT1                                                           
*                                                                               
INPUT8   MVC   TEMP(80),ADRRCD     TRM = 1/LUID/LUID                            
         MVC   TEMP(1),FLAG                                                     
         MVI   TEMP+1,0                                                         
         MVC   TEMP+2(14),SPACES                                                
         MVC   TEMP+2(4),ADRSYM                                                 
         TM    UPSI,GRPTRM         TEST IF GROUPING TERMS                       
         BO    *+10                                                             
         MVC   TEMP+9(4),ADRSYM+4                                               
         MVI   FLAG,2                                                           
         B     INPUTX                                                           
*                                                                               
INPUT9   MVC   TEMP(80),ADRRCD     PRG = 2/SYS/PRG                              
         MVC   TEMP(1),FLAG                                                     
         MVI   TEMP+1,0                                                         
         MVI   FLAG,1                                                           
         LH    R7,IOPTR                                                         
         LA    R7,1(R7)                                                         
         STH   R7,IOPTR                                                         
         XC    DUB,DUB             SEARCH PRG INDEX                             
         L     R7,=A(NDXREC)                                                    
         LA    R7,16(R7)                                                        
INPUT10  CLI   0(R7),X'FF'                                                      
         BE    INPUT14                                                          
         CLC   0(1,R7),ADROVSYS    SAME SYSNUM                                  
         BNE   INPUT12             NO SKIP ENTRY                                
         CLC   1(1,R7),ADRPRGNO                                                 
         BE    INPUT16                                                          
         CLI   1(R7),0                                                          
         BNE   INPUT12                                                          
         MVI   DUB,1               SET MISC PROG ENTRY FOUND                    
         ST    R7,DUB+4                                                         
INPUT12  LA    R7,21(R7)                                                        
         B     INPUT10                                                          
INPUT14  CLI   DUB,1               WAS MISC PROG ENTRY FOUND                    
         BNE   INPUT1              NO DO NOT RELEASE                            
         L     R7,DUB+4                                                         
INPUT16  MVC   TEMP+2(14),2(R7)    EXTRACT SYS & PRG NAMES                      
         B     INPUTX              RELEASE MISC PROG RECORD TO SORT             
         EJECT                                                                  
INPUTX   OC    COUNT,COUNT         FIRST TIME PRINT START-UP TIME               
         BNZ   INPUTX2                                                          
         MVC   STIME,UTIME         SAVE EST START TIME                          
         CLC   UTIME,ADRSTTM                                                    
         BH    *+10                                                             
         MVC   UTIME,ADRSTTM                                                    
         BAS   RE,FORMAT                                                        
INPUTX2  L     R1,COUNT            BUMP TRANSACTION TIME                        
         AHI   R1,1                                                             
         ST    R1,COUNT                                                         
         MVC   DTIME,UTIME                                                      
         MVC   UTIME,ADRNDTM                                                    
         CLC   DTIME,STIME         IGNORE TRANS BEFORE EST START TIME           
         BL    INPUTX4                                                          
         L     R4,UTIME                                                         
         CL    R4,DTIME                                                         
         BNH   INPUTX4                                                          
         SL    R4,DTIME                                                         
         CL    R4,DOWNDUR          COMPARE TO MIN REPORTABLE DUR                
         BNH   *+8                                                              
         BAS   RE,FORMAT                                                        
INPUTX4  CLI   PUTDONE,C'Y'                                                     
         BE    INPUTX5                                                          
         TM    UPSI,NOSORT         TEST IF SORT PHASE INHIBITED                 
         BZ    *+12                                                             
         MVI   PUTDONE,C'X'        SET PUT TO SORTER INHIBITED                  
         B     INPUT                                                            
         MVC   DMCB+0(4),=A(SORTCARD)                                           
         MVC   DMCB+4(4),=A(RECCARD)                                            
         XC    DMCB+8(4),DMCB+8                                                 
         GOTO1 =V(SORTER),DMCB                                                  
INPUTX5  GOTO1 =V(SORTER),DMCB,=C'PUT',TEMP                                     
         MVI   PUTDONE,C'Y'                                                     
         B     INPUT                                                            
         EJECT                                                                  
OUTPUT   CLI   PUTDONE,C'X'        EXIT IF NO OUTPUT                            
         BL    UPDEOJ                                                           
         MVC   DTIME,UTIME                                                      
         MVC   UTIME,TIMEDOWN      SYSTEM DOWN TIME                             
         BAS   RE,FORMAT                                                        
         CLI   PUTDONE,C'X'        EXIT IF SORT INHIBITED                       
         BE    UPDEOJ                                                           
*                                                                               
OUTPUT1  GOTO1 =V(SORTER),DMCB,=C'GET'                                          
         ICM   R3,15,DMCB+4                                                     
         BNZ   *+8                                                              
OUTPUT1A LA    R3,=8X'FF'          POINT TO DUMMY EOF RECORD                    
         LH    RF,RECLEN                                                        
         BCTR  RF,0                                                             
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   ADRRCD(0),0(R3)     SAVE SORT RECORD                             
*                                                                               
OUT1     LA    R2,ADRRCD                                                        
         USING ADRRECD,R2          R2=A(OUTPUT FROM SORT)                       
         L     R3,=A(ROWHDR)                                                    
         USING STATRECD,R3         R3=A(ROWS OF ACCUMULATERS)                   
         CLC   LADRRCD(16),ADRRCD                                               
         BNE   OUT6                CONTROL BREAK                                
*                                                                               
OUT2     SR    RE,RE               SET RF TO ROW NUM 01 THRU 14                 
         L     RF,ADRSTTM                                                       
         CL    RF,TUMINTM                                                       
         BNL   *+12                                                             
         LA    RF,1                ROW#01 FOR TRANS BEFORE TUMINTM              
         B     OUT3                                                             
         SL    RF,TUMINTM                                                       
         D     RE,TUHOUR                                                        
         CHI   RF,11                                                            
         BNH   *+12                                                             
         LA    RF,14               ROW#14 FOR TRANS AFTER TUMINTM+12HRS         
         B     OUT3                                                             
         LA    RF,2(RF)            ROW#02 THRU ROW#13 FOR HOURS                 
OUT3     MHI   RF,40                                                            
         LA    R4,STPRD0(RF)                                                    
         USING STATPRDD,R4         R4=A(ROW OF HOURLY ACCUMULATERS)             
*                                                                               
OUT3A    TM    ADRCPUTM,X'80'      BYPASS FUNNY RECORDS                         
         BO    OUT3B                                                            
         TM    ADRMSGI,X'80'                                                    
         BO    OUT3B                                                            
         TM    ADRIOCNT,X'80'                                                   
         BO    OUT3B                                                            
         TM    ADROVCNT,X'80'                                                   
         BO    OUT3B                                                            
         CLC   ADRINTM,ADRSTTM     TEST INPUT/START/END TIMES OK                
         BH    OUT3B                                                            
         CLC   ADRSTTM,ADRNDTM                                                  
         BNH   OUT4                                                             
*                                                                               
OUT3B    L     RF,SPTRCTR          JUST BUMP TRANSACTION COUNTER                
         LA    RF,1(RF)                                                         
         ST    RF,SPTRCTR                                                       
         B     OUT5                                                             
*                                                                               
OUT4     L     RF,SPTRCTR          BUMP TRANSACTION COUNT                       
         LA    RF,1(RF)                                                         
         ST    RF,SPTRCTR                                                       
*                                                                               
         L     RF,ADRNDTM          BUMP TRANS TIME                              
         CL    RF,ADRSTTM                                                       
         BNL   *+8                                                              
         AL    RF,TUDAY                                                         
         SL    RF,ADRSTTM                                                       
         AL    RF,SPTRTM                                                        
         ST    RF,SPTRTM                                                        
*                                                                               
         L     RF,SPCPUTM          BUMP CPU TIME                                
         AL    RF,ADRCPUTM                                                      
         ST    RF,SPCPUTM                                                       
*                                                                               
         L     RF,SPIQTM           BUMP IN Q TIME                               
         L     R0,ADRSTTM                                                       
         CL    R0,ADRINTM                                                       
         BNL   *+8                                                              
         AL    R0,TUDAY                                                         
         SL    R0,ADRINTM          R0=INPUT QUEUE TIME                          
         AR    RF,R0                                                            
         ST    RF,SPIQTM                                                        
         CL    R0,TUQTM            BUMP INPUT QUEUE FREQUENCY                   
         BL    *+16                                                             
         L     RF,SPIQFREQ                                                      
         LA    RF,1(RF)                                                         
         ST    RF,SPIQFREQ                                                      
*                                                                               
         L     RF,SPIQLEN          BUMP INPUT MSG LENGTH                        
         AH    RF,ADRMSGI                                                       
         ST    RF,SPIQLEN                                                       
*                                                                               
         L     RF,SPOQLEN          BUMP OUTPUT MSG LENGTH                       
         AH    RF,ADRMSGO                                                       
         ST    RF,SPOQLEN                                                       
*                                                                               
         SR    RF,RF               BUMP I/O COUNTER * 10                        
         ICM   RF,7,ADRIOCNT                                                    
         MHI   RF,10                                                            
         A     RF,SPIOCTR                                                       
         ST    RF,SPIOCTR                                                       
*                                                                               
         SR    RF,RF               BUMP OVERLAY COUNTER * 10                    
         ICM   RF,1,ADROVCNT                                                    
         MHI   RF,10                                                            
         A     RF,SPOVCTR                                                       
         ST    RF,SPOVCTR                                                       
*                                                                               
OUT5     MVC   LADRRCD,ADRRCD      SAVE LAST ADRREC                             
         B     OUTPUT1                                                          
*                                                                               
OUT6     CLI   LADRRCD,0           FIRST TIME INITIALIZE GRAND TOTALS           
         BNE   OUT8                                                             
         L     R3,=A(TROWHDR)                                                   
         XC    SHDRFLDS,SHDRFLDS                                                
         MVI   SHTYPE,1                                                         
         MVC   STCOUNT,=H'1'                                                    
         MVC   STDATE,DATE                                                      
         LA    R4,STPRD0                                                        
         LA    R7,15                                                            
         XC    0(40,R4),0(R4)                                                   
         LA    R4,40(R4)                                                        
         BCT   R7,*-10                                                          
         L     R3,=A(ROWHDR)                                                    
         B     OUT30                                                            
*                                                                               
OUT8     LA    RE,STPRD0           ADD ROW#1 THRU ROW#14 TO ROW#0               
         LA    RF,STPRD1                                                        
         LA    R7,14                                                            
         BAS   R9,ADDEF                                                         
         LA    RF,40(RF)                                                        
         BCT   R7,*-8                                                           
*                                                                               
         CLI   LADRRCD,1           BUMP GRAND TOTALS FOR TERMINALS              
         BNE   OUT10                                                            
         L     RE,=A(TROWS)                                                     
         L     RF,=A(ROWS)                                                      
         LA    R7,15                                                            
         BAS   R9,ADDEF                                                         
         LA    RE,40(RE)                                                        
         LA    RF,40(RF)                                                        
         BCT   R7,*-12                                                          
*                                                                               
OUT10    LA    R4,STPRD0           AVERAGE ROW#0 THRU ROW#14                    
         LA    R7,15                                                            
         BAS   R9,AVGROW                                                        
         LA    R4,40(R4)                                                        
         BCT   R7,*-8                                                           
*                                                                               
         LA    R2,LADRRCD          BUILD HEADER DATA FROM LAST RECORD           
         USING ADRRECD,R2                                                       
         MVC   SHDRFLDS,LADRRCD                                                 
         MVC   STCOUNT,=H'1'                                                    
         MVC   STDATE,DATE                                                      
*                                                                               
         CLI   SHTYPE,1            TERMINAL DATA                                
         BNE   OUT14                                                            
         L     R7,=A(TRMREC)       DISK ADDR FROM TERMINAL INDEX                
OUT12    CLC   0(4,R7),SHNAME1                                                  
         BNE   OUT13                                                            
         CLC   4(4,R7),SHNAME2                                                  
         BNE   OUT13                                                            
         MVC   DSKADR,8(R7)                                                     
         B     OUT20                                                            
OUT13    CLI   0(R7),X'FF'                                                      
         BE    *+12                                                             
         LA    R7,12(R7)                                                        
         B     OUT12                                                            
         CLI   8(R7),X'FF'         CHECK FOR SPACE FOR NEW TERMINAL             
         BNE   *+12                                                             
         OI    NEWTRM,X'02'        SET NO ROOM LEFT FOR TERMINALS               
         B     OUT50                                                            
         OI    NEWTRM,X'01'        SET NEW TERMINAL ADDED                       
         MVC   0(4,R7),SHNAME1     BUILD NEW ENTRY                              
         MVC   4(4,R7),SHNAME2                                                  
         MVC   DSKADR,8(R7)                                                     
         B     OUT20                                                            
*                                                                               
OUT14    L     R7,=A(NDXREC)       PROGRAM DATA                                 
         LA    R7,16(R7)                                                        
OUT16    CLI   0(R7),X'FF'         SEARCH PROGRAM INDEX RECORD                  
         BNE   *+6                                                              
         DC    H'0'                DIE IF CANT FIND IT                          
         CLC   2(14,R7),SHNAME1                                                 
         BE    *+12                                                             
         LA    R7,21(R7)                                                        
         B     OUT16                                                            
         MVC   DSKADR,17(R7)                                                    
*                                                                               
OUT20    L     R7,=A(ROWHDR)                                                    
         BAS   RE,UPDREC                                                        
*                                                                               
OUT30    L     R3,=A(ROWHDR)       CLEAR ROW#0 THRU ROW#14                      
         LA    R4,STPRD0                                                        
         LA    R0,15                                                            
         XC    0(40,R4),0(R4)                                                   
         LA    R4,40(R4)                                                        
         BCT   R0,*-10                                                          
*                                                                               
         CLI   LADRRCD,0           FIRST TIME                                   
         BE    OUT50               YES                                          
         CLI   ADRRCD,X'FF'        LAST TIME                                    
         BE    OUT60                                                            
         CLC   LADRRCD(1),ADRRCD                                                
         BE    OUT50               END OF MINOR CONTROL BREAK                   
*                                                                               
OUT40    L     R4,=A(TROWS)        AVERAGE GRAND TOTALS                         
         LA    R7,15                                                            
         BAS   R9,AVGROW                                                        
         LA    R4,40(R4)                                                        
         BCT   R7,*-8                                                           
*                                                                               
         L     R7,=A(TRMREC)       UPDATE GRAND TOTAL REC                       
         MVC   DSKADR,8(R7)                                                     
         L     R7,=A(TROWHDR)                                                   
         BAS   RE,UPDREC                                                        
*                                                                               
OUT50    MVC   LADRRCD,ADRRCD                                                   
         B     OUT1                                                             
         EJECT                                                                  
OUT60    CLI   NEWTRM,0            EXIT IF NO NEW TERMINALS                     
         BE    UPDEOJ                                                           
         TM    NEWTRM,X'02'        EXIT IF NO ROOM FOR NEW TERMINALS            
         BO    UPDEOJ                                                           
         L     R7,=A(NDXREC)       SORT PROGRAM INDEX                           
         LH    R5,2(R7)                                                         
         BCTR  R5,0                                                             
         MHI   R5,200              R5=NUM OF ENTRYS IN TRM INDEX                
         L     R6,=A(TRMREC)                                                    
         GOTO1 =V(XSORT),DMCB,(R6),(R5),12,8,0                                  
         LH    R7,2(R7)            R7=NUM OF TRM INDEX RECS                     
         BCTR  R7,0                                                             
         MVC   DSKADR,=X'00010100'                                              
         L     R5,=A(IOBUFF)                                                    
         LR    R0,R5                                                            
OUT62    BAS   R9,SSTATS           UPDATE EACH TRM INDEX REC                    
         LHI   R1,2400                                                          
         LA    RF,16(R5)                                                        
         MOVE  ((RF),(R1)),(R6)                                                 
         BAS   R9,WSTATS                                                        
         AHI   R6,2400                                                          
         BCT   R7,OUT62                                                         
*                                                                               
UPDEOJ   LA    RF,CLSEOUT          CLOSE TAPE FILE                              
         TM    UPSI,INTAPE                                                      
         BZ    *+8                                                              
         LA    RF,CLSEIN                                                        
         BASR  R9,RF                                                            
*                                                                               
         SR    R1,R1               SET OK RETURN CODE                           
         CLI   PUTDONE,C'N'                                                     
         BNE   *+8                                                              
         LA    R1,4                SET NO PUTS DONE                             
         TM    NEWTRM,X'02'                                                     
         BZ    *+8                                                              
         LA    R1,8                SET RAN OUT OF SPACE FOR TERMINALS           
*                                                                               
         XBASE RC=(R1)                                                          
         EJECT                                                                  
RSTATS   LA    R8,DMRDIR                                                        
         B     IOSTATS                                                          
WSTATS   LA    R8,DMWRT                                                         
         TM    UPSI,NODISK         TEST OPTION (NO WRITE)                       
         BO    IOSTATSX                                                         
         B     IOSTATS                                                          
SSTATS   LA    R8,DMRSEQ                                                        
*                                                                               
IOSTATS  GOTO1 DATAMGR,DMCB,(R8),STATS,DSKADR,(R0)                              
         CLI   8(R1),0                                                          
         BE    IOSTATSX                                                         
         DC    H'0'                                                             
IOSTATSX BR    R9                                                               
         EJECT                                                                  
OPENIN   L     R8,=A(ADRIN)        OPEN INPUT TAPE FILE                         
         TM    UPSI,NOTAPE                                                      
         BO    OPENINX                                                          
         OPEN  ((R8),INPUT)                                                     
OPENINX  BR    R9                                                               
*                                                                               
OPENOUT  L     R8,=A(ADROUT)       OPEN OUTPUT TAPE FILE                        
         TM    UPSI,NOTAPE                                                      
         BO    OPENOUTX                                                         
         OPEN  ((8),OUTPUT)                                                     
         TM    UPSI,NOTAPE2                                                     
         BO    OPENOUTX                                                         
         L     R8,=A(ADROUT2)                                                   
         OPEN  ((8),OUTPUT)                                                     
OPENOUTX BR    R9                                                               
*                                                                               
GETIN    L     R8,=A(ADRIN)        GET INPUT TAPE RECORD                        
         L     R0,=A(IOBUFF)                                                    
         TM    UPSI,NOTAPE                                                      
         BO    GETINX                                                           
         GET   (8),(0)                                                          
GETINX   BR    R9                                                               
*                                                                               
PUTOUT   L     R8,=A(ADROUT)       PUT OUTPUT TAPE RECORD                       
         L     R0,=A(IOBUFF)                                                    
         TM    UPSI,NOTAPE                                                      
         BO    PUTOUTX                                                          
         PUT   (8),(0)                                                          
         BR    R9                                                               
*                                                                               
PUTOUT2  TM    UPSI,NOTAPE2        BUILD ALTERNATE RECORD FOR SAS               
         BOR   R9                                                               
         L     R8,=A(ALTREC)                                                    
         MVC   00(64,R8),ADRREC    FIRST 64 BYTES ARE THE SAME                  
         XC    64(24,R8),64(R8)                                                 
         CLI   0(R8),C'A'          TEST IF TRANSACTION RECORD                   
         BL    PUTOUT4             NO                                           
         LA    R8,64(R8)                                                        
         MVC   00(16,R8),ADROSIN   ADD THE NEXT 16 BYTES OF ADRREC              
*                                                                               
PUTOUT4  L     R8,=A(ADROUT2)      PUT ALT OUTPUT TAPE RECORD                   
         L     R0,=A(ALTREC)                                                    
         PUT   (8),(0)                                                          
PUTOUTX  BR    R9                                                               
*                                                                               
CLSEIN   L     R8,=A(ADRIN)        CLOSE INPUT TAPE FILE                        
         TM    UPSI,NOTAPE                                                      
         BO    CLSEINX                                                          
         CLOSE ((8))                                                            
CLSEINX  BR    R9                                                               
*                                                                               
CLSEOUT  L     R8,=A(ADROUT)       CLOSE OUTPUT TAPE FILE                       
         TM    UPSI,NOTAPE                                                      
         BO    CLSEOUTX                                                         
         CLOSE ((8))                                                            
         TM    UPSI,NOTAPE2                                                     
         BO    CLSEOUTX                                                         
         L     R8,=A(ADROUT2)                                                   
         CLOSE ((8))                                                            
CLSEOUTX BR    R9                                                               
         EJECT                                                                  
ADDEF    SR    R1,R1               ADD ROW#F TO ROW#E                           
         LA    R8,10                                                            
ADDEF1   L     R0,0(R1,RE)                                                      
         AL    R0,0(R1,RF)                                                      
         ST    R0,0(R1,RE)                                                      
         LA    R1,4(R1)                                                         
         BCT   R8,ADDEF1                                                        
ADDEFX   BR    R9                                                               
         EJECT                                                                  
AVGROW   LA    R1,4                REFORM AND AVERAGE ROW AT R4                 
         OC    0(4,R4),0(R4)                                                    
         BZ    AVGROWX             BYPASS EMPTY ROWS                            
*                                                                               
         LA    R5,3                CONVERT TIMES TO AVERAGE MILLISECS           
AVGROW1  SR    RE,RE                                                            
         L     RF,0(R1,R4)         RE/RF TOTAL TIME IN TUS                      
         D     RE,=F'38400'                                                     
         LR    R0,RF                                                            
         MHI   R0,2000             R0=WHOLE SECONDS IN MILLISECS*2              
         SRDL  RE,31               RE/RF=REMAINDER IN TUS*2                     
         MHI   RF,10                                                            
         D     RE,=F'384'          MILLISECS = (TU*10)/384                      
         AR    RF,R0               RF=TIME IN MILLISECS*2                       
         SR    RE,RE                                                            
         D     RE,0(R4)            RF=MILLISECS*2/NUMOFTRANS                    
         AHI   RF,1                                                             
         SRL   RF,1                                                             
         ST    RF,0(R1,R4)                                                      
         LA    R1,4(R1)                                                         
         BCT   R5,AVGROW1                                                       
*                                                                               
         LA    R5,2                CONVERT FREQS TO AVERAGE PERCENT             
AVGROW2  L     RE,0(R1,R4)                                                      
         MHI   RE,100                                                           
         SRDA  RE,31                                                            
         D     RE,0(R4)                                                         
         AHI   RF,1                                                             
         SRA   RF,1                                                             
         ST    RF,0(R1,R4)                                                      
         LA    R1,4(R1)                                                         
         BCT   R5,AVGROW2                                                       
*                                                                               
         LA    R5,4                AVERAGE COUNTERS & LENGTHS                   
AVGROW3  L     RE,0(R1,R4)                                                      
         SRDA  RE,31                                                            
         D     RE,0(R4)                                                         
         AHI   RF,1                                                             
         SRA   RF,1                                                             
         ST    RF,0(R1,R4)                                                      
         LA    R1,4(R1)                                                         
         BCT   R5,AVGROW3                                                       
*                                                                               
AVGROWX  BR    R9                                                               
         EJECT                                                                  
UPDREC   NTR1                      READ RECORD INTO IOBUFF AND UPDATE           
         L     R3,=A(IOBUFF)                                                    
         LR    R0,R3                                                            
         BAS   R9,RSTATS                                                        
*                                                                               
         CLC   STDATE,STDATE-SHDRFLDS(R7)                                       
         BE    UPDRECX             DO NOT UPDATE IF SAME DATES (?RERUN)         
         MVC   STDATE,STDATE-SHDRFLDS(R7)                                       
         LH    R1,STCOUNT          SAVE UPDATE COUNT (N)                        
*                                                                               
         MVC   SHDRFLDS,0(R7)                                                   
         LA    R6,STOTFLDS-SHDRFLDS(R7)                                         
         MVC   SDCOUNT(8),0(R6)                                                 
         LA    R6,8(R6)                                                         
         LA    R5,SDPRD0                                                        
         LA    R0,15                                                            
         MVC   0(40,R5),0(R6)                                                   
         LA    R5,40(R5)                                                        
         LA    R6,40(R6)                                                        
         BCT   R0,*-14                                                          
*                                                                               
         LR    R0,R1               R0=N                                         
         LA    R1,1(R1)                                                         
         STH   R1,STCOUNT          R1=N+1                                       
         LA    R5,STPRD0           R5=A(TOTAL VALUE)                            
         LA    R6,SDPRD0           R6=A(DAYLY VALUE)                            
         LA    R8,150                                                           
*                                                                               
UPD2     L     RF,0(R5)            NEW TOTAL = ((TOTAL*N)+DAYLY)/(N+1)          
         MR    RE,R0                                                            
         A     RF,0(R6)                                                         
         LTR   R0,R0                                                            
         BZ    *+18                                                             
         SLDA  RE,1                                                             
         DR    RE,R1                                                            
         AHI   RF,1                                                             
         SRA   RF,1                                                             
         ST    RF,0(R5)                                                         
         LA    R5,4(R5)                                                         
         LA    R6,4(R6)                                                         
         BCT   R8,UPD2                                                          
         LR    R0,R3                                                            
         BAS   R9,WSTATS                                                        
*                                                                               
UPDRECX  XIT1                                                                   
         EJECT                                                                  
FORMAT   NTR1                                                                   
         L     R1,DTIME            FORMAT AND PRINT DOWN-TIME REPORT            
         LA    R2,P                                                             
         BAS   RE,EDTIME                                                        
         L     R1,UTIME                                                         
         LA    R2,P+11                                                          
         BAS   RE,EDTIME                                                        
         CL    R1,DTIME                                                         
         BL    FORMAT2                                                          
         SL    R1,DTIME                                                         
         CL    R1,UTIME                                                         
         BE    FORMAT2                                                          
         LA    R2,P+22                                                          
         BAS   RE,EDTIME                                                        
FORMAT2  GOTO1 =V(PRINTER)                                                      
         BASR  RE,RF                                                            
FORMATX  XIT1                                                                   
*                                                                               
EDTIME   NTR1                      EDIT TIME FIELD                              
         LTR   R5,R1                                                            
         BZ    EDTIMEX                                                          
         SR    R4,R4                                                            
         D     R4,SECFCTR          R5=SECONDS                                   
         SR    R4,R4                                                            
         D     R4,=F'60'           R4=SECS,R5=MINS                              
         CVD   R4,DUB                                                           
         UNPK  6(2,R2),DUB                                                      
         OI    7(R2),X'F0'                                                      
         MVI   5(R2),C'.'                                                       
         SR    R4,R4                                                            
         D     R4,=F'60'           R4=MINS,R5=HOURS                             
         EDIT  (R4),(2,3(R2)),ZERO=NOBLANK                                      
         LTR   R5,R5                                                            
         BZ    EDTIMEX                                                          
         OC    3(2,R2),=C'00'                                                   
         MVI   2(R2),C'.'                                                       
         EDIT  (R5),(2,0(R2))                                                   
EDTIMEX  XIT1                                                                   
         EJECT                                                                  
         LTORG                                                                  
         EJECT                                                                  
         DS    0F                  BASIC TIME UNITS AND PARAMATERS              
TUDAY    DC    X'C5C10000'         ONE DAY IN TUS                               
TUHOUR   DC    A(0060*60*38400)    LENGTH OF ONE HOUR IN TUS                    
SECFCTR  DC    F'38400'            FACTOR FOR TUS TO SECS                       
*&&UK                                                                           
TUMINTM  DC    A(0480*60*38400)    EARLIEST TRANSACTION HOUR....08.00AM         
DOWNDUR  DC    A(0005*60*38400)    DOWN TIME AT LEAST 5 MINUTES                 
TIMEUP   DC    A(0420*60*38400)    SYSTEM UP TIME IN TU'S.......07.00AM         
TIMEDOWN DC    X'AD08E000'         SYSTEM DOWN TIME 1260 MINS...09.00PM         
*&&                                                                             
*&&US                                                                           
TUMINTM  DC    A(0120*60*38400)    EARLIEST TRANSACTION HOUR....08.00AM         
DOWNDUR  DC    A(0002*60*38400)    DOWN TIME AT LEAST 2 MINUTES                 
TIMEUP   DC    A(0060*60*38400)    SYSTEM UP TIME IN TU'S.......07.00AM         
TIMEDOWN DC    A(0900*60*38400)    SYSTEM DOWN TIME.............09.00PM         
*&&                                                                             
         EJECT                                                                  
DUB      DS    D                                                                
FULL     DS    F                                                                
TEMP     DS    CL80                                                             
SAVER1   DS    F                                                                
ACOMRG   DS    F                                                                
ADRADR   DC    F'0'                                                             
DSKADR   DC    F'0'                                                             
COUNT    DC    F'0'                                                             
DTIME    DC    F'0'                                                             
UTIME    DC    F'0'                                                             
STIME    DC    F'0'                                                             
TUQTM    DC    F'3840'             1/10TH SECOND IN TUS                         
IOPTR    DC    H'0'                                                             
IOPTRMAX DC    H'100'              ADRFILE BLOCKING FACTOR                      
RECLEN   DC    H'64'               ADRFILE LOGICAL RECORD LENGTH                
FLAG     DC    X'00'                                                            
NEWTRM   DC    X'00'                                                            
PUTDONE  DC    C'N'                                                             
*                                                                               
DMCB     DC    6F'0'                                                            
PLDADDS  DC    A(14),A(IOBUFF),A(0),A(0),A(*+4),A(0)   14=V(DAOPEN)             
VADRFILE DC    A(0)                                                             
VSTATS   DC    A(0)                                                             
DATAMGR  DC    V(DATAMGR)                                                       
DMRSEQ   DC    CL8'DMRSEQ'                                                      
DMRDIR   DC    CL8'DMRDIR'                                                      
DMWRT    DC    CL8'DMWRT'                                                       
ADRFILE  DC    CL8'ADRFILE'                                                     
STATS    DC    CL8'STATS'                                                       
*                                                                               
DATE     DS    CL6,CL2             DDS C'YYMMDD' DATE                           
ALTDATE  DS    F                   MVS JULIAN DATE P'0CYYDDDF'                  
UPSI     DC    X'00'                                                            
         DS    CL3                                                              
WORK     DS    CL20                                                             
*                                                                               
POPSYSN4 DC    CL4' '              POP FACPAK FOUR CHR NAME                     
POPDATE  DC    PL4'0'              POP JULIAN DATE P'0CYYDDDF'                  
POPDATE4 DC    CL8' '              POP C'DD/MM/YY' DATE                         
*                                                                               
ADRRCD   DC    XL80'00'                                                         
LADRRCD  DC    XL80'00'                                                         
ALTREC   DC    XL88'00'            FIRST 64 BYTES PLUS EXTRA 24 BYTES           
*                                                                               
SUBA     DC    CL40'  TAKEN     BROUGHT       DOWN   REASON'                    
SUBB     DC    CL40' DOWN AT     UP AT        TIME   ------'                    
*                                                                               
SORTCARD DC    CL80'SORT FIELDS=(1,16,A),FORMAT=BI,WORK=1 '                     
RECCARD  DC    CL80'RECORD TYPE=F,LENGTH=64 '                                   
*                                                                               
ROWHDR   DS    6F                                                               
ROWS     DS    150F                                                             
TROWHDR  DS    6F                                                               
TROWS    DS    150F                                                             
IOBUFF   DS    2000F                                                            
*                                                                               
NDXREC   DS    4F                                                               
         DS    110XL21             MAX 200 PROGRAMS                             
         DS    XL90                                                             
         DS    XL12                                                             
*                                                                               
         DS    4F                                                               
TRMREC   DS    12000XL12           MAX 12000 TERMINALS                          
         DS    XL12                                                             
         EJECT                                                                  
***********************************************************************         
* TEST IF ANY SYSIN CARDS                                             *         
***********************************************************************         
SYSIN    NTR1  BASE=*,LABEL=*                                                   
*                                                                               
SYSIN1   GOTO1 =V(CARDS),PARM,C,=C'RE10'                                        
         CLC   C(2),=C'/*'                                                      
         BE    SYSINX                                                           
         CLI   C,C'*'                                                           
         BE    SYSIN1                                                           
         CLC   C(6),=C'DDSIO='                                                  
         BE    SYSIN2                                                           
         CLC   C(7),=C'DSPACE='                                                 
         BE    SYSIN3                                                           
         B     SYSIN1                                                           
*                                                                               
SYSIN2   LT    RE,=V(DDSIO)        SET DDSIO AND NOP UPSI SETTING               
         BZ    SYSIN1                                                           
         MVC   0(8,RE),C+6                                                      
         L     RE,=A(UPSI)                                                      
         NI    0(RE),255-TSTSYS                                                 
         B     SYSIN1                                                           
*                                                                               
SYSIN3   L     RE,=V(SSB)          SET DSPACE AND NOP UPSI SETTING              
         MVC   SSODSPAC-SSOOFF(1,RE),C+7                                        
         L     RE,=A(UPSI)                                                      
         NI    0(RE),255-TSTSYS                                                 
         B     SYSIN1                                                           
*                                                                               
SYSINX   XIT1                                                                   
*                                                                               
PARM     DS    6F                                                               
C        DC    CL80' '                                                          
         LTORG                                                                  
         EJECT                                                                  
ADROUT   DCB   DDNAME=ADROUT,DSORG=PS,MACRF=(PM),                      X        
               RECFM=F,BLKSIZE=6400                                             
*                                                                               
ADROUT2  DCB   DDNAME=ADROUT2,DSORG=PS,MACRF=(PM),                     X        
               RECFM=FB,BLKSIZE=30800,LRECL=88                                  
*                                                                               
ADRIN    DCB   DDNAME=ADRIN,DSORG=PS,MACRF=(GM),EODAD=OUTPUT,          X        
               RECFM=F,BLKSIZE=6400                                             
         EJECT                                                                  
         DS    0D                                                               
         DC    C'STATWORK'                                                      
STATWORK DS    4096D                                                            
*                                                                               
         DS    0D                                                               
SSB      DC    X'0000FF',X'02',4X'00',CL8' ',32X'00',A(0),972X'00'              
*                                                                               
         DS    0D                                                               
UTL      DC    F'0',X'01',XL3'00',248X'00'                                      
*                                                                               
* FAADRREC                                                                      
       ++INCLUDE FAADRREC                                                       
*                                                                               
         ORG   ADRRECD             INTERNAL VERSION OF 1ST 16 BYTES             
ADRTYPE  DS    C                                                                
ADRTYPE1 DS    C                                                                
ADRNAME1 DS    CL7                                                              
ADRNAME2 DS    CL7                                                              
         EJECT                                                                  
* DDSTATREC                                                                     
       ++INCLUDE DDSTATREC                                                      
                                                                                
* DDDPRINT                                                                      
       ++INCLUDE DDDPRINT                                                       
                                                                                
* FASSBOFF                                                                      
SSOOFFD  DSECT                                                                  
       ++INCLUDE FASSBOFF                                                       
                                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'004DDSTATUPDT06/03/14'                                      
         END                                                                    
