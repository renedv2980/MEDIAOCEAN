*          DATA SET REREPVAL4S AT LEVEL 123 AS OF 05/01/02                      
*CATALP REPVAL4                                                                 
         TITLE 'MODULE TO CALCULATE CONTRACT VALUES'                            
***********************************************************************         
*  HISTORY OF CHANGES:  SUPERCEDES   'REREPVALM'/REPVALM3             *         
***********************************************************************         
* JAN18/96 (BU ) --- SET 'CONFIRMED/UNCONFIRMED' FLAG                 *         
*                                                                     *         
* JAN24/96 (BU ) --- CONSIDER BUYR = ACC- ORDERS AS 'CONFIRMED'       *         
*                                                                     *         
* OCT31/96 (BU ) --- CONSIDER 'OTHER' (NOT ACE/GRAPHNET) ORDERS AS    *         
*                    CONFIRMED!                                       *         
*                    REREPVALM3 IS TO BECOME REREPVALM0               *         
*                                                                     *         
* NOV06/96 (BU ) --- FIX 'FIXPOST' CALCULATION WHEN ONLY INVOICE      *         
*                    BUCKETS ARE FOUND:  WAS ROUNDING FROM RRG        *         
*                                                                     *         
* NOV07/96 (BU ) --- REMOVE X'04' ELEMENT CHECK FROM 'PENDING' DEFI-  *         
*                    NITION                                           *         
*                                                                     *         
* NOV08/96 (BU ) --- BACK OUT ABOVE CHANGE UNTIL ACC- ORDERS ARE      *         
*                    RESOLVED IN THIS .                               *         
*                                                                     *         
* NOV13/96 (BU ) --- REINSTATE ABOVE CHANGE, PLUS TEST FOR ACC-       *         
*                    ORDERS TO BE CONSIDERED NOT PENDING.             *         
*                                                                     *         
* AUG19/97 (BU ) --- EXTRA FLAG FOR PRESENCE OF INVOICE INFO          *         
*                    FOR MONTH                                        *         
*                                                                     *         
* OCT27/97 (BU ) --- ALTERNATE CALENDAR ANALYSIS VERSION.             *         
*                                                                     *         
* APR10/98 (BU ) --- INCREASE 'NEXT YEAR' PROCESSING TO PERMIT        *         
*                    NRRGON SPECIAL DATA GENERATION.                  *         
*          NOTE:     SUPERCEDES REREPVALM0 (VALU2NEW LINK)!           *         
*                                                                     *         
* NOV02/98 (BU ) --- ADD THIS WEEK 2YEAR PRIOR                        *         
*                    SUPERCEDES REREPVALMA (VALU3NEW LINK)!           *         
*                                                                     *         
* MAR17/99 (BU ) --- TURN ON FORECAST/ZERO DOLLARS FLAG               *         
*                                                                     *         
* FEB25/00 (BU ) --- NEW BUCKETS FOR TRADE DOLLAR                     *         
*                                                                     *         
*                                                                     *         
*                                                                     *         
*                                                                     *         
*                     **  END TOMBSTONE  **                           *         
***********************************************************************         
VALU3NEW CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 20,**VALU**,R9                                                   
         USING VALUED,RC                                                        
         MVC   SAVEPARA,0(R1)                                                   
*                                                                               
***********************************************************************         
** SAVEPARA                     BYTE                                            
** DISPLACE                                                                     
**                                                                              
** 0-3        R2    PARAMETER 1 0       X'80' - ALTERNATE CALENDAR              
**                                              FIGURES DELIVERED               
**                              1-3     A(CONTRACT)                             
** 4-7        R3    PARAMETER 2 0       NOT USED                                
**                              1-3     A(OUTPUT AREA)                          
** 8-11       R4    PARAMETER 3 0       C=CALENDAR  4=4-WEEK                    
**                                      B=BROADCAST 5=5WEEK                     
**                              1-3     A(INFORMATION BLOCK)                    
** 12-15      R5    PARAMETER 4         A(22-BYTE DATE INFO BLOCK)              
**           (R6)-> SAVR6 -> RE (AS NEEDED)                                     
** 16-19      RE    PARAMETER 5 0       A OR T=ACCT OPTION/$ OR CENTS           
**                                      P=NRML WITH CENTS, ELSE=NRML $          
**                              1-3     A(08-BYTE) TO FROM ACTIV DATES          
** 20-23      R7    PARAMETER 6 0       R=CALL FROM RRG                         
**                  PARAMETER 6 1-3     KEYMONTH (LAST REQD MONTH)              
**                                                                              
***********************************************************************         
*                                                                               
         LM    R2,R7,0(R1)                                                      
*                                                                               
*   R6 CONTAINS ACTIVITY DATES, AND IS TRASHED EARLY IN THE MODULE.             
*      R6 IS THEREFORE SAVED IN SAVER6, WHICH IS USED FOR ACTIVITY DATE         
*         CHECKING.                                                             
*                                                                               
         ST    R6,SAVR6            SAVE CURRENT VALUE OF R6                     
         EJECT                                                                  
*              SET UP VALUES                                                    
         SPACE 3                                                                
*                                                                               
*  ESTABLISH PRIMARY OR ALTERNATE BUCKET COMPARISONS                            
*                                                                               
         MVI   ESTTYPE,X'03'       SET PRIMARY ESTIMATE BUCKET                  
         MVI   INVTYPE,X'04'       SET PRIMARY INVOICE  BUCKET                  
         MVI   TESTTYPE,X'63'      SET TRADE PRIMARY ESTIMATE BUCKET            
         MVI   TINVTYPE,X'64'      SET TRADE PRIMARY INVOICE  BUCKET            
         TM    SAVEPARA,X'80'      ALTERNATE CALENDAR WANTED?                   
*                                                                               
*   TEST DUMP                                                                   
****>>>> DC    H'0'                                                             
*   TEST DUMP END                                                               
*                                                                               
         BNO   VANE0020            NO                                           
         MVI   ESTTYPE,X'53'       SET ALTERNATE ESTIMATE BUCKET                
         MVI   INVTYPE,X'54'       SET ALTERNATE INVOICE  BUCKET                
         MVI   TESTTYPE,X'83'      SET TRADE ALT ESTIMATE BUCKET                
         MVI   TINVTYPE,X'84'      SET TRADE ALT INVOICE  BUCKET                
VANE0020 EQU   *                                                                
         ST    R3,SAVR3            SAVE A(OUTPUT AREA)                          
         MVI   ASATRANG,C'N'       SET AS-AT RANGE TEST OFF                     
         OC    TOCURASF(8,R5),TOCURASF(R5)   ANY AS-AT TO-DATES?                
         BZ    VANE0040            NO  -                                        
         MVI   ASATRANG,C'Y'       YES - SET AS-AT RANGE TEST ON                
VANE0040 EQU   *                                                                
         ST    R4,AMONINFO       SAVE ADDRESS OF MONINFO                        
         MVC   KEYMONTH(8),0(R7)                                                
         SPACE 2                                                                
         LA    RF,72               SIX YEARS' WORTH OF BUCKETS                  
*                                                                               
*              LOOP THROUGH MONTH TABLE TO CONTROL VALUES                       
*                                                                               
VANE0060 EQU   *                                                                
         ST    RF,SAVRF            SAVE LOOP COUNT VALUE                        
         CLI   0(R3),0                                                          
         BE    VANE0080                                                         
         MVI   FLAG6(R3),0         INITIALIZE FLAG  BYTE                        
         MVI   FLAG7(R3),0         INITIALIZE FLAG2 BYTE                        
         XC    BUCKDISP(BUCKSIZE,R3),BUCKDISP(R3)                               
         LR    R8,R3              SAVE A(START OF MONTH ENTRY)                  
         LA    R3,BUCKDISP(R3)    A(FIRST BUCKET IN DATA)                       
         CLI   SAVEPARA+16,C'A'                                                 
         BE    VANE0180                                                         
         CLI   SAVEPARA+16,C'T'    FOR TAPE                                     
         BE    VANE0180            USE ACCOUNTING DATA                          
         MVI   INVANY,0                                                         
         MVI   INVANY2,0                                                        
         BAS   RE,GETVALS                                                       
         OC    FLAG6(1,R8),INVANY      SETON/OFF INVOICE INDICATOR              
         OC    FLAG7(1,R8),INVANY2     SETON/OFF INVOICE INDICATOR              
         LA    R3,BUCKSIZE(R3)     SKIP DATA TO NEXT MONTH ENTRY                
         L     RF,SAVRF            RESTORE LOOP COUNTER                         
         BCT   RF,VANE0060                                                      
         SPACE 2                                                                
VANE0080 EQU   *                                                                
         CLI   SAVEPARA+20,C'R'    CALL FROM RRG?                               
         BNE   VANE0100            NO                                           
         CLI   SAVEPARA+16,C'A'    ACCOUNTING REPORT?                           
         BNE   VANE0100            NO                                           
         BAS   RE,FIXPOST          YES - SET PRIOR PERIOD ADJUSTMENTS           
VANE0100 EQU   *                                                                
         L     R3,SAVEPARA+4       REPOINT TO OUTPUT AREA                       
         CLI   SAVEPARA+20,C'R'    CALL FROM RRG?                               
         BE    VANE0300            YES - LEAVE PENNIES                          
         CLI   SAVEPARA+16,C'P'    TEST IF PENNIES WANTED                       
         BE    VANE0300                                                         
         CLI   SAVEPARA+16,C'T'    FOR TAPE                                     
         BE    VANE0300            DO NOT ROUND                                 
VANE0120 CLI   0(R3),0                                                          
*                                  ROUND OUT PENNIES                            
         BE    VANE0300                                                         
         LA    R3,BUCKDISP(R3)     SET TO BUCKET AREA                           
         LA    RF,NUMBUCKS         SET TO NUMBER OF BUCKETS                     
*                                                                               
VANE0140 OC    0(4,R3),0(R3)                                                    
         BZ    VANE0160                                                         
*                                                                               
         L     R6,0(R3)                                                         
         SRDA  R6,31               DOUBLE                                       
         D     R6,=F'100'          DIVIDE OUT PENNIES                           
         LTR   R7,R7               TEST QUOTIENT NEGATIVE                       
         BL    *+8                                                              
         AH    R7,=H'1'            ADD 1 TO ROUND ONLY ON POSITIVE              
         SRA   R7,1                HALF                                         
         ST    R7,0(R3)                                                         
*                                                                               
VANE0160 LA    R3,4(R3)                                                         
         BCT   RF,VANE0140                                                      
         B     VANE0120                                                         
*                                                                               
         SPACE 3                                                                
VANE0180 BAS   RE,ACCTVALS                                                      
         OC    FLAG6(1,R8),INVANY  SET ANY INVOICE FLAG FROM MONTH              
*                                                                               
*  NOTE:  FORMER TEST FOR WHETHER MONTH BEING PROCESSED WAS KEYMONTH            
*    DEPENDED ON THE MONTH TABLE CORRESPONDING TO THE DATES OF THE              
*    REQUEST.  IN THAT CASE, THE MONTH AFTER THE REQUEST END DATE WAS           
*    FILLED WITH ZERO.  THE NEW FORMAT OF THE TABLE, WHICH INCLUDES             
*    DATES ONE YEAR BEYOND THE END DATE, MAKES SUCH TESTS IMPOSSIBLE.           
*    THE FOLLOWING ROUTINE SUBSTITUTES FOR THAT TEST.  SEE DOCUMENT-            
*    ATION WITHIN THE ROUTINE FOR MORE.  BILL UHR.  12/04/91.                   
*                                                                               
         BAS   RE,CHKKEYMO         TEST IF KEY MONTH=THIS MONTH                 
         BNZ   VANE0200                                                         
         CLI   INVANY,0            YES- TEST IF ANY INVOICES                    
         BNE   VANE0240                 YES- ACCT DATA=INVOICE                  
         L     R0,TOTORD(R3)            NO - ACCT DATA=ALL ESTIMATES            
         A     R0,CUASATIN(R3)                                                  
         ST    R0,PRASATOR(R3)                                                  
         B     VANE0260                                                         
*                                                                               
VANE0200 CLI   INVANY,0            TEST IF ANY INVOICES                         
         BNE   VANE0220                                                         
         MVC   PRASATOR(4,R3),TOTORD(R3)  NO-ESTS THIS MON=ADJUSTMT             
         B     VANE0260                                                         
*                                                                               
VANE0220 CLI   INVPREV,0           TEST ANY PREVIOUS INVOICES                   
         BNE   VANE0240                                                         
         L     R0,GROSSORD(R3)            NO-ADJUST INVS - PREV EST             
         S     R0,CUASATIN(R3)                                                  
         ST    R0,PRASATOR(R3)                                                  
         B     VANE0260                                                         
*                                                                               
VANE0240 MVC   PRASATOR(4,R3),GROSSORD(R3)  YES-ACCT DATA=INV THIS MON          
*                                                                               
VANE0260 EQU   *                                                                
*                                                                               
*  NOTE:  MONINF TABLE IS 8 BYTES IF CALL IS MADE FROM RRG, AND 16              
*         BYTES IF CALL IS MADE FROM REPORTER.                                  
*                                                                               
         LA    R4,08(R4)           INCREMENT A(MONTH INFO TABLE)                
         CLI   SAVEPARA+20,C'R'    TEST IF CALL FROM RRG                        
         BE    VANE0280            YES - ONLY INCREMENT 8 BYTES                 
         LA    R4,08(R4)           NO  - INCREMENT 8 ADDITIONAL BYTES           
VANE0280 EQU   *                                                                
         LA    R3,BUCKSIZE(R3)     SKIP DATA TO NEXT MONTH ENTRY                
         L     RF,SAVRF            RESTORE LOOP COUNTER                         
         BCT   RF,VANE0060                                                      
         B     VANE0080                                                         
         SPACE 2                                                                
VANE0300 EQU   *                                                                
         L     R3,SAVR3            RESET A(OUTPUT AREA)                         
         LA    RE,CONSPINF         CHECK FOR 'CONTRACT SPECIFIC'                
*                                        INFORMATION FLAG                       
         SR    R3,RE               ADDRESS IN 'MISCELL. STORAGE' AREA           
         OC    0(4,R3),0(R3)       ANY ADDRESS THERE?                           
         BZ    VANE0320            NO  - FINISHED                               
         GOTO1 CONSPECS,PARALIST,(R3)                                           
VANE0320 EQU   *                                                                
         XMOD1 1                                                                
         EJECT                                                                  
*                                                                               
*    CHKKEYMO --- ROUTINE USES POSITION WITHIN THE OUTPUT TABLE,                
*       WHOSE ADDRESS APPEARS IN 'ANEWMON', TO DETERMINE THE MONTH              
*       BEING PROCESSED.  THE TWO-BYTE YYMM OF THE 'CURRENT DATE'               
*       FOR THAT MONTH IS DISPLACED 'CURDATE' BYTES IN.  THIS DATE              
*       IS THEN FOUND IN THE 'MONINFO' TABLE, WHICH CORRESPONDS TO              
*       THE DATES OF THE REQUEST.  ONCE FOUND, THE NEXT DATE IS                 
*       CHECKED FOR PRESENCE/ABSENCE, AND A CONDITION CODE IS SET               
*       ACCORDINGLY.                                                            
*                                                                               
CHKKEYMO NTR1                                                                   
         L     R3,AMONINFO         A(MONTH INFORMATION TABLE)                   
CKEY0010 EQU   *                                                                
         CLC   0(2,R3),CURDATE(R8) MONINFO VS O/P TABLE CURR DATE               
         BE    CKEY0030            FOUND                                        
         LA    R3,08(R3)           BUMP TO NEXT MONINFO BUCKET                  
         CLI   SAVEPARA+20,C'R'    TEST IF CALL FROM RRG                        
         BE    CKEY0020            YES - ONLY INCREMENT 8 BYTES                 
         LA    R3,08(R3)           NO  - INCREMENT ADDITIONAL 8 BYTES           
CKEY0020 EQU   *                                                                
         CLI   0(R3),0             ANY DATE IN BUCKET?                          
         BNE   CKEY0010            YES - CHECK NEXT                             
         B     CKEY0050            NO  - SET CC NOT ZERO                        
CKEY0030 EQU   *                                                                
         CLI   SAVEPARA+20,C'R'    TEST IF CALL FROM RRG                        
         BNE   CKEY0040            YES - TEST 8 BYTES PAST                      
         CLI   8(R3),0             NEXT MONINF ENTRY EMPTY?                     
         BE    CKEY0060            YES - SET CC EQUAL ZERO                      
         B     CKEY0050            NO  -                                        
CKEY0040 EQU   *                                                                
         CLI   16(R3),0            NO  - TEST 16 BYTES PAST                     
         BE    CKEY0060            YES - SET CC EQUAL ZERO                      
CKEY0050 EQU   *                                                                
         LTR   R3,R3               SET CC NOT = ZERO                            
         B     CKEY0070            EXIT                                         
CKEY0060 EQU   *                                                                
         SR    R3,R3                                                            
CKEY0070 EQU   *                                                                
         B     XIT                                                              
         EJECT                                                                  
*              SCAN THE CONTRACT FOR VALUES                                     
         SPACE 3                                                                
GETVALS  NTR1                                                                   
         LR    R4,R8               A(MONTH ENTRY IN OUTPUT AREA)                
         LA    R2,34(R2)           R2 A(CONTRACT)                               
*                                  R3 A(OUTPUT)                                 
*                                  R4 A(OUTPUT AREA: EBCDIC DATE)               
*                                  R5 A(2-BYTE MONDAY)                          
         SPACE 2                                                                
GVAL0020 EQU   *                                                                
         CLI   0(R2),0                  TEST END OF CONTRACT                    
         BE    XIT                                                              
         CLC   0(1,R2),ESTTYPE          ORDERED BUCKET?                         
         BNE   GVAL0460                 NO  - CHECK FOR INVOICE BUCK            
*                                                                               
*  FOLLOWING SECTION OF CODE DEALS WITH BUCKETING DOLLARS FOR                   
*     ****   ORDERED   ****                                                     
*                                                                               
         USING RCONBKEL,R2                                                      
         CLC   RCONBKYR(2),CURDATE(R4)  MONTH OF SERVICE  THIS YEAR?            
         BNE   GVAL0140                 NO                                      
         MVC   DUB,RCONBKAM                                                     
         CLI   ASATRANG,C'N'            AS-AT DATE RANGE TEST?                  
         BE    GVAL0040                 NO                                      
         CLC   RCONBKWK,CURASATF(R5)                                            
         BL    GVAL0080                 BEFORE AS-AT FROM DATE                  
         CLC   RCONBKWK,TOCURASF(R5)                                            
         BH    GVAL0080                 AFTER AS-AT TO DATE                     
         B     GVAL0060                 BETWEEN DATES (INCLUSIVE)               
GVAL0040 EQU   *                                                                
         CLC   RCONBKWK,CURASATF(R5)    ORDER PLACED BY AS AT DATE?             
         BH    GVAL0120                 NO                                      
GVAL0060 EQU   *                                                                
         L     R6,DUB                                                           
         A     R6,TOTORD(R3)            ADD: TOT ORD THIS YEAR                  
         ST    R6,TOTORD(R3)                                                    
*                                                                               
* CHECK TO SEE IF DOLLARS WERE ORDERED THIS WEEK/LAST WEEK                      
*       OR THIS WEEK/PRIOR YEAR                                                 
*                                                                               
GVAL0080 EQU   *                                                                
         CLC   RCONBKWK,CURASATF(R5)    ORDER PLACED THIS WEEK?                 
         BNE   GVAL0100                 NO - CHECK LAST WEEK                    
         SPACE 1                                                                
         L     R6,DUB                                                           
         A     R6,THISWKOR(R3)          ADD: THIS WEEK ESTIMATE                 
         ST    R6,THISWKOR(R3)                                                  
*                                                                               
GVAL0100 EQU   *                                                                
         CLC   RCONBKWK,LASTWEKE(R5)    ORDER PLACED LAST WEEK?                 
         BNE   GVAL0120                 NO -                                    
         SPACE 1                                                                
         L     R6,DUB                                                           
         A     R6,LASTWKOR(R3)          ADD: LAST WEEK ESTIMATE                 
         ST    R6,LASTWKOR(R3)                                                  
*                                                                               
GVAL0120 EQU   *                                                                
         OC    SAVEPARA+17(3),SAVEPARA+17    TEST ACTIVITY PERIOD               
         BZ    GVAL2840                      NO ACTIVITY PERIOD ENTERED         
         L     RE,SAVR6                      LOAD A(ACTIVITY DATES)             
         CLC   RCONBKWK,CURAPERS(RE)         ACTIVITY START DATE                
         BL    GVAL2840                      BUCKET < ACTIVITY START            
         CLC   RCONBKWK,CURAPERE(RE)         ACTIVITY END DATE                  
         BH    GVAL2840                      BUCKET > ACTIVITY END              
         L     R6,DUB                        ADD: ACTIVITY PERIOD               
         A     R6,GROSSORD(R3)               CURRENT GROSS                      
         ST    R6,GROSSORD(R3)                                                  
         B     GVAL2840                                                         
GVAL0140 EQU   *                                                                
         CLC   RCONBKYR(2),PRIDATE(R4)       MON OF SVC LAST YEAR?              
         BNE   GVAL0260                      NO  - CHECK 2 YEARS PRIOR          
         MVC   DUB,RCONBKAM                  ADD: PRIOR TOTAL ORDERED           
*        CLI   ASATRANG,C'N'             AS-AT DATE RANGE TEST?                 
*        BE    GVAL0160                  NO  - ACCUMULATE                       
*        CLC   RCONBKWK,PRIASATF(R5)                                            
*        BL    GVAL0240                  BEFORE AS-AT FROM                      
*        CLC   RCONBKWK,TOPRIASF(R5)                                            
*        BH    GVAL0240                  AFTER AS-AT TO                         
GVAL0160 EQU   *                         BETWEEN DATES (INCLUSIVE)              
         L     R6,DUB                                                           
         A     R6,PRTOTORD(R3)               PRIOR TOTAL ORDERED                
         ST    R6,PRTOTORD(R3)                                                  
*                                                                               
         CLC   RCONBKWK,PRIASATF(R5)    ORD PLACED THIS WEEK/LAST YR?           
         BNE   GVAL0180                 NO - CHECK LAST WEEK                    
         SPACE 1                                                                
         L     R6,DUB                                                           
         A     R6,THPRWKOR(R3)          ADD: THIS WEEK/PRIOR YEAR               
         ST    R6,THPRWKOR(R3)                                                  
*                                                                               
GVAL0180 EQU   *                                                                
         CLI   ASATRANG,C'N'                 AS-AT DATE RANGE TEST?             
         BE    GVAL0200                      NO                                 
         CLC   RCONBKWK,PRIASATF(R5)                                            
         BL    GVAL0240                      BEFORE AS-AT FROM                  
         CLC   RCONBKWK,TOPRIASF(R5)                                            
         BH    GVAL0240                      AFTER AS-AT TO                     
         B     GVAL0220                      BETWEEN DATES (INCLUSIVE)          
GVAL0200 EQU   *                                                                
         CLC   RCONBKWK,PRIASATF(R5)         LAST YEAR AS AT DATE?              
         BH    GVAL0240                      NO                                 
GVAL0220 EQU   *                                                                
         L     R6,DUB                        ADD: PRIOR AS AT ORDERED           
         A     R6,PRASATOR(R3)                                                  
         ST    R6,PRASATOR(R3)               PRIOR AS AT ORDERED                
         SPACE 1                                                                
GVAL0240 EQU   *                                                                
         OC    SAVEPARA+17(3),SAVEPARA+17    FOR ACTIVITY PERIOD                
         BZ    GVAL0360                      NO ACTIVITY PERIOD ENTERED         
*                                                                               
*   IN EVENT NRGON OPTION IS REQUESTED, 'NEXT YEAR' MAY BE USED TO              
*        ACCUMULATE 'LAST YEAR' AS OF AN ALTERNATE AS-AT DATE'.                 
*        IN THIS CASE, THE 'PRIOR YEAR' BUCKETS MUST BE REVIEWED                
*        AGAIN IN THE 'NEXT YEAR' SECTION.                                      
*                                                                               
         L     RE,SAVR6                      LOAD A(ACTIVITY DATES)             
         CLC   RCONBKWK,PRIAPERS(RE)         ACTIVITY START DATE                
         BL    GVAL0360                      BUCKET < ACTIVITY START            
         CLC   RCONBKWK,PRIAPERE(RE)         ACTIVITY END DATE                  
         BH    GVAL0360                      BUCKET > ACTIVITY END              
         L     R6,DUB                        ADD: ACTIV PER PRIOR ORD           
         A     R6,PRACTORD(R3)                                                  
         ST    R6,PRACTORD(R3)               ACTIV PER PRIOR ORDERED            
         B     GVAL0360                                                         
GVAL0260 EQU   *                                                                
         CLC   RCONBKYR(2),PRI2DATE(R4)      MON OF SVC 2 YRS PRIOR?            
         BNE   GVAL0360                      NO  - CHECK FOR NEXT YEAR          
         MVC   DUB,RCONBKAM                  ADD: 2 YRS PRI TOT ORD             
*        CLI   ASATRANG,C'N'             AS-AT DATE RANGE TEST?                 
*        BE    GVAL0280                  NO  - ACCUMULATE                       
*        CLC   RCONBKWK,PRIASATF(R5)                                            
*        BL    GVAL0340                  BEFORE AS-AT FROM                      
*        CLC   RCONBKWK,TOPRIASF(R5)                                            
*        BH    GVAL0340                  AFTER AS-AT TO                         
GVAL0280 EQU   *                         BETWEEN DATES (INCLUSIVE)              
         L     R6,DUB                                                           
         A     R6,P2TOTORD(R3)               2 YRS PRIOR TOTAL ORDERED          
         ST    R6,P2TOTORD(R3)                                                  
*                                                                               
         CLC   RCONBKWK,PRI2ASAT(R5)    ORD PLACED THIS WEEK/2YR PRIOR?         
         BNE   GVAL0290                 NO - CHECK LAST WEEK                    
         SPACE 1                                                                
         L     R6,DUB                                                           
         A     R6,THP2WKOR(R3)          ADD: THIS WEEK/2YRS PRIOR               
         ST    R6,THP2WKOR(R3)                                                  
*                                                                               
GVAL0290 EQU   *                                                                
*                                                                               
         CLI   ASATRANG,C'N'                AS-AT DATE RANGE TEST?              
         BE    GVAL0300                     NO                                  
         CLC   RCONBKWK,PRI2ASAT(R5)                                            
         BL    GVAL0340                     BEFORE AS-AT FROM                   
         CLC   RCONBKWK,TOPR2ASF(R5)                                            
         BH    GVAL0340                     AFTER AS-AT TO                      
         B     GVAL0320                     BETWEEN DATES (INCLUSIVE)           
GVAL0300 EQU   *                                                                
         CLC   RCONBKWK,PRI2ASAT(R5)         2 YEAR PRIOR AS AT DATE?           
         BH    GVAL0340                      NO                                 
GVAL0320 EQU   *                                                                
         L     R6,DUB                        ADD: 2 YR PRIOR AS AT ORD          
         A     R6,P2ASATOR(R3)                                                  
         ST    R6,P2ASATOR(R3)               2 YR PRIOR AS AT ORDERED           
         SPACE 1                                                                
GVAL0340 EQU   *                                                                
         OC    SAVEPARA+17(3),SAVEPARA+17    FOR ACTIVITY PERIOD                
*        BZ    GVAL2840                      NO ACTIVITY PERIOD ENTERED         
*                                                                               
*   NO ACTIVITY DATES ARE CALCULATED FOR 2 YEARS PRIOR                          
*                                                                               
         B     GVAL2840                      NO ACTIVITY PERIOD ENTERED         
         L     RE,SAVR6                      LOAD A(ACTIVITY DATES)             
         CLC   RCONBKWK,PR2APERS(RE)         ACTIVITY START DATE                
         BL    GVAL2840                      BUCKET < ACTIVITY START            
         CLC   RCONBKWK,PR2APERE(RE)         ACTIVITY END DATE                  
         BH    GVAL2840                      BUCKET > ACTIVITY END              
         L     R6,DUB                        ADD: ACT PER 2 YR PRI ORD          
         A     R6,P2PRACOR(R3)                                                  
         ST    R6,P2PRACOR(R3)               ACTIVITY PER 2 YR PRI ORD          
         B     GVAL2840                                                         
GVAL0360 EQU   *                                                                
         CLC   RCONBKYR(2),NEXTDATE(R4)      MON OF SVC NEXT YEAR?              
         BNE   GVAL2840                      NO  - GO TO NEXT BUCKET            
         MVC   DUB,RCONBKAM                  ADD: NEXT YR TOTAL ORDERED         
*                                                                               
*        CLI   ASATRANG,C'N'                 AS-AT DATE RANGE TEST?             
*        BE    GVAL0380                      NO                                 
*        CLC   RCONBKWK,NEXASATF(R5)                                            
*        BL    GVAL0440                      BEFORE AS-AT FROM                  
*        CLC   RCONBKWK,TONXTASF(R5)                                            
*        BH    GVAL0440                      AFTER AS-AT TO                     
*        B     GVAL0420                      BETWEEN DATES (INCLUSIVE)          
GVAL0380 EQU   *                                                                
         L     R6,DUB                                                           
         A     R6,NEXTOTOR(R3)               NEXT YEAR TOTAL ORDERED            
         ST    R6,NEXTOTOR(R3)                                                  
*                                                                               
         CLI   ASATRANG,C'N'                 AS-AT DATE RANGE TEST?             
         BE    GVAL0400                      NO                                 
         CLC   RCONBKWK,NEXASATF(R5)                                            
         BL    GVAL0440                      BEFORE AS-AT FROM                  
         CLC   RCONBKWK,TONXTASF(R5)                                            
         BH    GVAL0440                      AFTER AS-AT TO                     
         B     GVAL0420                      BETWEEN DATES (INCLUSIVE)          
GVAL0400 EQU   *                                                                
         CLC   RCONBKWK,NEXASATF(R5)         NEXT YR AS AT DATE?                
         BH    GVAL0440                      NO                                 
GVAL0420 EQU   *                                                                
         L     R6,DUB                        ADD: NEXT YEAR AS AT ORD           
         A     R6,NXASATOR(R3)                                                  
         ST    R6,NXASATOR(R3)               NEXT YEAR AS AT ORDERED            
         SPACE 1                                                                
GVAL0440 EQU   *                                                                
         OC    SAVEPARA+17(3),SAVEPARA+17    FOR ACTIVITY PERIOD                
         BZ    GVAL2840                      NO ACTIVITY PERIOD ENTERED         
*                                                                               
*   IN EVENT 'NEXT YEAR' BUCKETS ARE USED AS AN ALTERNATIVE DATE                
*        FOR 'LAST YEAR' FIGURES                                                
*                                                                               
         L     RE,SAVR6                      LOAD A(ACTIVITY DATES)             
         CLC   RCONBKWK,NEXAPERS(RE)         ACTIVITY START DATE                
         BL    GVAL2840                      BUCKET < ACTIVITY START            
         CLC   RCONBKWK,NEXAPERE(RE)         ACTIVITY END DATE                  
         BH    GVAL2840                      BUCKET > ACTIVITY END              
         L     R6,DUB                        ADD: ACT PER NEXT YR ORD           
         A     R6,NXACTORD(R3)                                                  
         ST    R6,NXACTORD(R3)               ACTIVITY PER NEXT YR ORD           
         B     GVAL2840                                                         
*                                                                               
*  FOLLOWING SECTION OF CODE DEALS WITH BUCKETING DOLLARS FOR                   
*     ****   INVOICE   ****                                                     
*                                                                               
GVAL0460 EQU   *                                                                
         CLC   0(1,R2),INVTYPE           TEST: INVOICE BUCKET?                  
         BNE   GVAL0760                  NO  - SKIP IT                          
         USING RCONSTEL,R2                                                      
*                                                                               
         CLC   RCONSTYR(2),CURDATE(R4)   MON OF SVC THIS YEAR?                  
         BNE   GVAL0520                  NO  - TEST PRIOR YEAR                  
         CLI   ASATRANG,C'N'                 AS-AT DATE RANGE TEST?             
         BE    GVAL0480                      NO                                 
         CLC   RCONSTWK,CURASATF(R5)                                            
         BL    GVAL2840                      BEFORE AS-AT FROM                  
         CLC   RCONSTWK,TOCURASF(R5)                                            
         BH    GVAL2840                      AFTER AS-AT TO                     
         B     GVAL0500                      BETWEEN DATES (INCLUSIVE)          
GVAL0480 EQU   *                                                                
         OI    INVANY,X'20'              SET 'INV FOR MON EXISTS' FLAG          
         CLC   RCONSTWK,CURASATF(R5)     THIS YEAR AS AT DATE?                  
         BH    GVAL2840                  NO  -                                  
GVAL0500 EQU   *                                                                
         MVC   DUB,RCONSTAM                                                     
         L     R6,DUB                    ADD: CURRENT AS AT INVOICE             
         A     R6,CUASATIN(R3)                                                  
         ST    R6,CUASATIN(R3)                                                  
         OI    INVANY,X'01'              SET CURRENT INVOICE SWITCH             
         B     GVAL2840                                                         
GVAL0520 EQU   *                                                                
         CLC   RCONSTYR(2),PRIDATE(R4)   MON OF SVC LAST YEAR?                  
         BNE   GVAL0600                  NO  - TEST 2 YEARS PRIOR               
         MVC   DUB,RCONSTAM                                                     
*        CLI   ASATRANG,C'N'             AS-AT DATE RANGE TEST?                 
*        BE    GVAL0540                  NO  - ACCUMULATE                       
*        CLC   RCONSTWK,PRIASATF(R5)                                            
*        BL    GVAL0680                  BEFORE AS-AT FROM                      
*        CLC   RCONSTWK,TOPRIASF(R5)                                            
*        BH    GVAL0680                  AFTER AS-AT TO                         
GVAL0540 EQU   *                         BETWEEN DATES (INCLUSIVE)              
         L     R6,DUB                    ADD: PRIOR TOTAL INVOICE               
         A     R6,PRTOTINV(R3)                                                  
         ST    R6,PRTOTINV(R3)                                                  
         OI    INVANY,X'02'              SET ANY PRIOR INVOICE SWITCH           
*                                                                               
         CLI   ASATRANG,C'N'             AS-AT DATE RANGE TEST?                 
         BE    GVAL0560                  NO                                     
         CLC   RCONSTWK,PRIASATF(R5)                                            
         BL    GVAL0680                  BEFORE AS-AT FROM                      
         CLC   RCONSTWK,TOPRIASF(R5)                                            
         BH    GVAL0680                      AFTER AS-AT TO                     
         B     GVAL0580                      BETWEEN DATES (INCLUSIVE)          
GVAL0560 EQU   *                                                                
         OI    INVANY,X'40'        SET 'INV FOR MONTH EXISTS' FLAG              
         CLC   RCONSTWK,PRIASATF(R5)     LAST YEAR AS AT DATE                   
         BH    GVAL0680                                                         
GVAL0580 EQU   *                                                                
         L     R6,DUB                    ADD: PRIOR AS AT INVOICE               
         A     R6,PRASATIN(R3)                                                  
         ST    R6,PRASATIN(R3)                                                  
         OI    INVANY,X'04'              SET PRIOR AS AT INV SWTCH              
         B     GVAL0680                                                         
*                                                                               
* SITUATION DESCRIBED ABOVE FOR 'NEXT YEAR/LAST YEAR' REUSAGE                   
*        APPLIES TO INVOICE BUCKETS ALSO.  IF NRGON OPTION                      
*        REQUESTED, PRIOR PERIOD BUCKETS MUST BE REEXAMINED.                    
*                                                                               
GVAL0600 EQU   *                                                                
         CLC   RCONSTYR(2),PRI2DATE(R4)  MON OF SVC 2 YEAR PRIOR?               
         BNE   GVAL0680                  NO                                     
         MVC   DUB,RCONSTAM                                                     
*        CLI   ASATRANG,C'N'             AS-AT DATE RANGE TEST?                 
*        BE    GVAL0620                  NO  - ACCUMULATE                       
*        CLC   RCONSTWK,PRI2ASAT(R5)                                            
*        BL    GVAL2840                  BEFORE AS-AT FROM                      
*        CLC   RCONSTWK,TOPR2ASF(R5)                                            
*        BH    GVAL2840                  AFTER AS-AT TO                         
GVAL0620 EQU   *                         BETWEEN DATES (INCLUSIVE)              
         L     R6,DUB                    ADD: 2 YRS PRI TOTAL INVOICE           
         A     R6,P2TOTINV(R3)                                                  
         ST    R6,P2TOTINV(R3)                                                  
         OI    INVANY,X'08'              SET ANY 2 YRS PRIOR INV SWTCH          
*                                                                               
         CLI   ASATRANG,C'N'                 AS-AT DATE RANGE TEST?             
         BE    GVAL0640                      NO                                 
         CLC   RCONSTWK,PRI2ASAT(R5)                                            
         BL    GVAL2840                      BEFORE AS-AT FROM                  
         CLC   RCONSTWK,TOPR2ASF(R5)                                            
         BH    GVAL2840                      AFTER AS-AT TO                     
         B     GVAL0660                      BETWEEN DATES (INCLUSIVE)          
GVAL0640 EQU   *                                                                
         OI    INVANY,X'80'        SET 'INV FOR MONTH EXISTS' FLAG              
         CLC   RCONSTWK,PRI2ASAT(R5)     2 YEAR PRIOR AS AT DATE                
         BH    GVAL2840                                                         
GVAL0660 EQU   *                                                                
         L     R6,DUB                    ADD: 2 YRS PRI AS AT INVOICE           
         A     R6,P2ASATIN(R3)                                                  
         ST    R6,P2ASATIN(R3)                                                  
         OI    INVANY,X'10'              SET 2 YRS PRI AS AT INV SWTCH          
         B     GVAL2840                                                         
*                                                                               
***>>>                                                                          
GVAL0680 EQU   *                                                                
         CLC   RCONSTYR(2),NEXTDATE(R4)  MON OF SVC NEXT YEAR?                  
         BNE   GVAL2840                  NO                                     
         MVC   DUB,RCONSTAM                                                     
*        CLI   ASATRANG,C'N'             AS-AT DATE RANGE TEST?                 
*        BE    GVAL0700                  NO  - ACCUMULATE                       
*        CLC   RCONSTWK,NEXASATF(R5)                                            
*        BL    GVAL2840                  BEFORE AS-AT FROM                      
*        CLC   RCONSTWK,TONXTASF(R5)                                            
*        BH    GVAL2840                  AFTER AS-AT TO                         
GVAL0700 EQU   *                         BETWEEN DATES (INCLUSIVE)              
         L     R6,DUB                    ADD: NEXT YEAR TOTAL INVOICE           
         A     R6,NXTOTINV(R3)                                                  
         ST    R6,NXTOTINV(R3)                                                  
         OI    INVANY2,X'80'             SET ANY NEXT YEAR INV SWTCH            
         CLI   ASATRANG,C'N'                 AS-AT DATE RANGE TEST?             
         BE    GVAL0720                      NO                                 
         CLC   RCONSTWK,NEXASATF(R5)                                            
         BL    GVAL2840                      BEFORE AS-AT FROM                  
         CLC   RCONSTWK,TONXTASF(R5)                                            
         BH    GVAL2840                      AFTER AS-AT TO                     
         B     GVAL0740                      BETWEEN DATES (INCLUSIVE)          
GVAL0720 EQU   *                                                                
         OI    INVANY2,X'40'       SET 'INV FOR MONTH EXISTS' FLAG              
         CLC   RCONSTWK,NEXASATF(R5)     NEXT YEAR AS AT DATE                   
         BH    GVAL2840                                                         
GVAL0740 EQU   *                                                                
         L     R6,DUB                    ADD: NEXT YEAR AS AT INVOICE           
         A     R6,NXASATIN(R3)                                                  
         ST    R6,NXASATIN(R3)                                                  
         OI    INVANY2,X'20'             SET NEXT YEAR AS AT INV SWTCH          
         B     GVAL2840                                                         
*                                                                               
***>>>                                                                          
GVAL0760 EQU   *                                                                
         CLI   0(R2),X'23'              FORECAST BUCKET?                        
         BNE   GVAL0840                 NO  -  NO MORE BUCKETS                  
*                                                                               
*  FOLLOWING SECTION OF CODE DEALS WITH BUCKETING DOLLARS FOR                   
*     ****   FORECAST  ****                                                     
*                                                                               
         USING RCONFCEL,R2                                                      
         CLC   RCONFCYR(2),CURDATE(R4)  MONTH OF SERVICE  THIS YEAR?            
         BNE   GVAL0780                 NO                                      
         MVC   DUB,RCONFCAM                                                     
         CLC   RCONFCWK,CURASATF(R5)    ORDER PLACED BY AS AT DATE?             
         BH    GVAL2840                 NO                                      
         SPACE 1                                                                
         L     R6,DUB                                                           
         A     R6,CURRFORC(R3)          ADD: FORECAST THIS YEAR                 
         ST    R6,CURRFORC(R3)                                                  
         B     GVAL2840                                                         
GVAL0780 EQU   *                                                                
         CLC   RCONFCYR(2),PRIDATE(R4)       MON OF SVC LAST YEAR?              
         BNE   GVAL0800                      NO  - CHECK 2 YEARS PRIOR          
         MVC   DUB,RCONFCAM                  ADD: PRIOR FORECAST                
*                                                                               
         CLC   RCONFCWK,PRIASATF(R5)         LAST YEAR AS AT DATE?              
         BH    GVAL2840                      NO                                 
         L     R6,DUB                        ADD: PRIOR FORECAST                
         A     R6,PRIOFORC(R3)                                                  
         ST    R6,PRIOFORC(R3)               PRIOR FORECAST                     
         B     GVAL2840                                                         
GVAL0800 EQU   *                                                                
         CLC   RCONFCYR(2),PRI2DATE(R4)      MON OF SVC 2 YRS PRIOR?            
         BNE   GVAL0820                      NO  - CHECK FOR NEXT YEAR          
         MVC   DUB,RCONFCAM                  ADD: 2 YRS PRI FORECAST            
*                                                                               
         CLC   RCONFCWK,PRI2ASAT(R5)         2 YEAR PRIOR AS AT DATE?           
         BH    GVAL2840                      NO                                 
         L     R6,DUB                        ADD: 2 YR PRIOR FORECAST           
         A     R6,PRI2FORC(R3)                                                  
         ST    R6,PRI2FORC(R3)               2 YR PRIOR AS AT FORECAST          
         B     GVAL2840                                                         
GVAL0820 EQU   *                                                                
         CLC   RCONFCYR(2),NEXTDATE(R4)      MON OF SVC NEXT YEAR?              
         BNE   GVAL2840                      NO  - GO TO NEXT BUCKET            
         MVC   DUB,RCONFCAM                  ADD: NEXT YR FORECAST              
*                                                                               
         CLC   RCONFCWK,NEXASATF(R5)         NEXT YR AS AT DATE?                
         BH    GVAL2840                      NO                                 
         L     R6,DUB                        ADD: NEXT YEAR FORECAST            
         A     R6,NEXTFORC(R3)                                                  
         ST    R6,NEXTFORC(R3)               NEXT YEAR FORECAST                 
         B     GVAL2840                      NO ACTIVITY PERIOD ENTERED         
*                                                                               
GVAL0840 EQU   *                                                                
**>>>    TRADE PROCESSING SECTION START                                         
*                                                                               
*  FOLLOWING SECTION OF CODE DEALS WITH BUCKETING DOLLARS FOR                   
*     ****   TRADE ORDERED   ****                                               
*                                                                               
         CLC   0(1,R2),TESTTYPE         TRADE ORDERED BUCKET?                   
         BNE   GVAL1460                 NO  - CHECK FOR INVOICE BUCK            
         USING RCONBKEL,R2                                                      
         CLC   RCONBKYR(2),CURDATE(R4)  MONTH OF SERVICE  THIS YEAR?            
         BNE   GVAL1140                 NO                                      
         MVC   DUB,RCONBKAM                                                     
         CLI   ASATRANG,C'N'            AS-AT DATE RANGE TEST?                  
         BE    GVAL1040                 NO                                      
         CLC   RCONBKWK,CURASATF(R5)                                            
         BL    GVAL1080                 BEFORE AS-AT FROM DATE                  
         CLC   RCONBKWK,TOCURASF(R5)                                            
         BH    GVAL1080                 AFTER AS-AT TO DATE                     
         B     GVAL1060                 BETWEEN DATES (INCLUSIVE)               
GVAL1040 EQU   *                                                                
         CLC   RCONBKWK,CURASATF(R5)    ORDER PLACED BY AS AT DATE?             
         BH    GVAL1120                 NO                                      
GVAL1060 EQU   *                                                                
         L     R6,DUB                                                           
         A     R6,TRTOTORD(R3)          ADD: TRADE TOT ORD THIS YEAR            
         ST    R6,TRTOTORD(R3)                                                  
*                                                                               
* CHECK TO SEE IF DOLLARS WERE ORDERED THIS WEEK/LAST WEEK                      
*       OR THIS WEEK/PRIOR YEAR                                                 
*                                                                               
GVAL1080 EQU   *                                                                
         CLC   RCONBKWK,CURASATF(R5)    ORDER PLACED THIS WEEK?                 
         BNE   GVAL1100                 NO - CHECK LAST WEEK                    
         SPACE 1                                                                
         L     R6,DUB                                                           
         A     R6,TRTHWKOR(R3)          ADD: TRADE THIS WEEK ESTIMATE           
         ST    R6,TRTHWKOR(R3)                                                  
*                                                                               
GVAL1100 EQU   *                                                                
         CLC   RCONBKWK,LASTWEKE(R5)    ORDER PLACED LAST WEEK?                 
         BNE   GVAL2840                 NO -                                    
         SPACE 1                                                                
         L     R6,DUB                                                           
         A     R6,TRLSWKOR(R3)          ADD: TRADE LAST WEEK ESTIMATE           
         ST    R6,TRLSWKOR(R3)                                                  
*                                                                               
GVAL1120 EQU   *                                                                
         OC    SAVEPARA+17(3),SAVEPARA+17    TEST ACTIVITY PERIOD               
         BZ    GVAL2840                      NO ACTIVITY PERIOD ENTERED         
         L     RE,SAVR6                      LOAD A(ACTIVITY DATES)             
         CLC   RCONBKWK,CURAPERS(RE)         ACTIVITY START DATE                
         BL    GVAL2840                      BUCKET < ACTIVITY START            
         CLC   RCONBKWK,CURAPERE(RE)         ACTIVITY END DATE                  
         BH    GVAL2840                      BUCKET > ACTIVITY END              
         L     R6,DUB                        ADD: ACTIVITY PERIOD               
         A     R6,TRGRSORD(R3)               TRADE CURRENT GROSS                
         ST    R6,TRGRSORD(R3)                                                  
         B     GVAL2840                                                         
GVAL1140 EQU   *                                                                
         CLC   RCONBKYR(2),PRIDATE(R4)       MON OF SVC LAST YEAR?              
         BNE   GVAL1260                      NO  - CHECK 2 YEARS PRIOR          
         MVC   DUB,RCONBKAM                  ADD: PRIOR TOTAL ORDERED           
GVAL1160 EQU   *                         BETWEEN DATES (INCLUSIVE)              
         L     R6,DUB                                                           
         A     R6,TRPRTOTO(R3)               TRADE PRIOR TOTAL ORDERED          
         ST    R6,TRPRTOTO(R3)                                                  
*                                                                               
*   NO 'TRADE - THIS WEEK PRIOR YEAR' BUCKETS ARE AVAILABLE                     
*                                                                               
**       CLC   RCONBKWK,PRIASATF(R5)    ORD PLACED THIS WEEK/LAST YR?           
**       BNE   GVAL1180                 NO - CHECK LAST WEEK                    
**       SPACE 1                                                                
**       L     R6,DUB                                                           
**       A     R6,THPRWKOR(R3)          ADD: THIS WEEK/PRIOR YEAR               
**       ST    R6,THPRWKOR(R3)                                                  
*                                                                               
GVAL1180 EQU   *                                                                
         CLI   ASATRANG,C'N'                 AS-AT DATE RANGE TEST?             
         BE    GVAL1200                      NO                                 
         CLC   RCONBKWK,PRIASATF(R5)                                            
         BL    GVAL1240                      BEFORE AS-AT FROM                  
         CLC   RCONBKWK,TOPRIASF(R5)                                            
         BH    GVAL1240                      AFTER AS-AT TO                     
         B     GVAL1220                      BETWEEN DATES (INCLUSIVE)          
GVAL1200 EQU   *                                                                
         CLC   RCONBKWK,PRIASATF(R5)         LAST YEAR AS AT DATE?              
         BH    GVAL1240                      NO                                 
GVAL1220 EQU   *                                                                
         L     R6,DUB                        ADD: PRIOR AS AT ORDERED           
         A     R6,TRPRAAOR(R3)                                                  
         ST    R6,TRPRAAOR(R3)               TRADE PRIOR AS AT ORDERED          
         SPACE 1                                                                
GVAL1240 EQU   *                                                                
         OC    SAVEPARA+17(3),SAVEPARA+17    FOR ACTIVITY PERIOD                
         BZ    GVAL2840                      NO ACTIVITY PERIOD ENTERED         
*                                                                               
*   IN EVENT NRGON OPTION IS REQUESTED, 'NEXT YEAR' MAY BE USED TO              
*        ACCUMULATE 'LAST YEAR' AS OF AN ALTERNATE AS-AT DATE'.                 
*        IN THIS CASE, THE 'PRIOR YEAR' BUCKETS MUST BE REVIEWED                
*        AGAIN IN THE 'NEXT YEAR' SECTION.                                      
*                                                                               
         L     RE,SAVR6                      LOAD A(ACTIVITY DATES)             
         CLC   RCONBKWK,PRIAPERS(RE)         ACTIVITY START DATE                
         BL    GVAL2840                      BUCKET < ACTIVITY START            
         CLC   RCONBKWK,PRIAPERE(RE)         ACTIVITY END DATE                  
         BH    GVAL2840                      BUCKET > ACTIVITY END              
         L     R6,DUB                        ADD: ACTIV PER PRIOR ORD           
         A     R6,TRPRACOR(R3)               TRADE PRIOR PER ACT ORDER          
         ST    R6,TRPRACOR(R3)                                                  
         B     GVAL2840                                                         
GVAL1260 EQU   *                                                                
         B     GVAL2840                                                         
*                                                                               
*  FOLLOWING SECTION OF CODE DEALS WITH BUCKETING DOLLARS FOR                   
*     ****   INVOICE   ****                                                     
*                                                                               
GVAL1460 EQU   *                                                                
         CLC   0(1,R2),TINVTYPE         TRADE INVOICE BUCKET?                   
         BNE   GVAL2840                  NO  - SKIP IT                          
         USING RCONSTEL,R2                                                      
*                                                                               
         CLC   RCONSTYR(2),CURDATE(R4)   MON OF SVC THIS YEAR?                  
         BNE   GVAL1520                  NO  - TEST PRIOR YEAR                  
         CLI   ASATRANG,C'N'                 AS-AT DATE RANGE TEST?             
         BE    GVAL1480                      NO                                 
         CLC   RCONSTWK,CURASATF(R5)                                            
         BL    GVAL2840                      BEFORE AS-AT FROM                  
         CLC   RCONSTWK,TOCURASF(R5)                                            
         BH    GVAL2840                      AFTER AS-AT TO                     
         B     GVAL1500                      BETWEEN DATES (INCLUSIVE)          
GVAL1480 EQU   *                                                                
***>>>   OI    INVANY2,X'10'             SET 'INV FOR MON EXISTS' FLAG          
         CLC   RCONSTWK,CURASATF(R5)     THIS YEAR AS AT DATE?                  
         BH    GVAL2840                  NO  -                                  
GVAL1500 EQU   *                                                                
         MVC   DUB,RCONSTAM                                                     
         L     R6,DUB                    ADD: CURRENT AS AT INVOICE             
         A     R6,TRCUAAIN(R3)                                                  
         ST    R6,TRCUAAIN(R3)                                                  
         OI    INVANY2,X'10'             SET CURRENT INVOICE SWITCH             
         B     GVAL2840                                                         
GVAL1520 EQU   *                                                                
         CLC   RCONSTYR(2),PRIDATE(R4)   MON OF SVC LAST YEAR?                  
         BNE   GVAL1600                  NO  - TEST 2 YEARS PRIOR               
         MVC   DUB,RCONSTAM                                                     
*        CLI   ASATRANG,C'N'             AS-AT DATE RANGE TEST?                 
*        BE    GVAL1540                  NO  - ACCUMULATE                       
*        CLC   RCONSTWK,PRIASATF(R5)                                            
*        BL    GVAL1680                  BEFORE AS-AT FROM                      
*        CLC   RCONSTWK,TOPRIASF(R5)                                            
*        BH    GVAL1680                  AFTER AS-AT TO                         
GVAL1540 EQU   *                         BETWEEN DATES (INCLUSIVE)              
         L     R6,DUB                    ADD: PRIOR TOTAL INVOICE               
         A     R6,TRPRTOTI(R3)                                                  
         ST    R6,TRPRTOTI(R3)                                                  
         OI    INVANY2,X'08'             SET ANY PRIOR INVOICE SWITCH           
*                                                                               
         CLI   ASATRANG,C'N'             AS-AT DATE RANGE TEST?                 
         BE    GVAL1560                  NO                                     
         CLC   RCONSTWK,PRIASATF(R5)                                            
         BL    GVAL1680                  BEFORE AS-AT FROM                      
         CLC   RCONSTWK,TOPRIASF(R5)                                            
         BH    GVAL1680                      AFTER AS-AT TO                     
         B     GVAL1580                      BETWEEN DATES (INCLUSIVE)          
GVAL1560 EQU   *                                                                
***>>>   OI    INVANY,X'40'        SET 'INV FOR MONTH EXISTS' FLAG              
         CLC   RCONSTWK,PRIASATF(R5)     LAST YEAR AS AT DATE                   
         BH    GVAL1680                                                         
GVAL1580 EQU   *                                                                
         L     R6,DUB                    ADD: PRIOR AS AT INVOICE               
         A     R6,TRPRAAIN(R3)                                                  
         ST    R6,TRPRAAIN(R3)                                                  
         OI    INVANY2,X'04'             SET PRIOR AS AT INV SWTCH              
         B     GVAL1680                                                         
*                                                                               
GVAL1600 EQU   *                                                                
         B     GVAL2840                                                         
*                                                                               
*   NOTE:  NO TWO-YEAR PRIOR OR NEXT YEAR FOR TRADE DOLLARS                     
*                                                                               
         CLC   RCONSTYR(2),PRI2DATE(R4)  MON OF SVC 2 YEAR PRIOR?               
         BNE   GVAL1680                  NO                                     
         MVC   DUB,RCONSTAM                                                     
*        CLI   ASATRANG,C'N'             AS-AT DATE RANGE TEST?                 
*        BE    GVAL1620                  NO  - ACCUMULATE                       
*        CLC   RCONSTWK,PRI2ASAT(R5)                                            
*        BL    GVAL2840                  BEFORE AS-AT FROM                      
*        CLC   RCONSTWK,TOPR2ASF(R5)                                            
*        BH    GVAL2840                  AFTER AS-AT TO                         
GVAL1620 EQU   *                         BETWEEN DATES (INCLUSIVE)              
         L     R6,DUB                    ADD: 2 YRS PRI TOTAL INVOICE           
         A     R6,P2TOTINV(R3)                                                  
         ST    R6,P2TOTINV(R3)                                                  
         OI    INVANY,X'08'              SET ANY 2 YRS PRIOR INV SWTCH          
*                                                                               
         CLI   ASATRANG,C'N'                 AS-AT DATE RANGE TEST?             
         BE    GVAL1640                      NO                                 
         CLC   RCONSTWK,PRI2ASAT(R5)                                            
         BL    GVAL2840                      BEFORE AS-AT FROM                  
         CLC   RCONSTWK,TOPR2ASF(R5)                                            
         BH    GVAL2840                      AFTER AS-AT TO                     
         B     GVAL1660                      BETWEEN DATES (INCLUSIVE)          
GVAL1640 EQU   *                                                                
         OI    INVANY,X'80'        SET 'INV FOR MONTH EXISTS' FLAG              
         CLC   RCONSTWK,PRI2ASAT(R5)     2 YEAR PRIOR AS AT DATE                
         BH    GVAL2840                                                         
GVAL1660 EQU   *                                                                
         L     R6,DUB                    ADD: 2 YRS PRI AS AT INVOICE           
         A     R6,P2ASATIN(R3)                                                  
         ST    R6,P2ASATIN(R3)                                                  
         OI    INVANY,X'10'              SET 2 YRS PRI AS AT INV SWTCH          
         B     GVAL2840                                                         
*                                                                               
***>>>                                                                          
GVAL1680 EQU   *                                                                
         CLC   RCONSTYR(2),NEXTDATE(R4)  MON OF SVC NEXT YEAR?                  
         BNE   GVAL2840                  NO                                     
         MVC   DUB,RCONSTAM                                                     
*        CLI   ASATRANG,C'N'             AS-AT DATE RANGE TEST?                 
*        BE    GVAL1700                  NO  - ACCUMULATE                       
*        CLC   RCONSTWK,NEXASATF(R5)                                            
*        BL    GVAL2840                  BEFORE AS-AT FROM                      
*        CLC   RCONSTWK,TONXTASF(R5)                                            
*        BH    GVAL2840                  AFTER AS-AT TO                         
GVAL1700 EQU   *                         BETWEEN DATES (INCLUSIVE)              
         L     R6,DUB                    ADD: NEXT YEAR TOTAL INVOICE           
         A     R6,NXTOTINV(R3)                                                  
         ST    R6,NXTOTINV(R3)                                                  
         OI    INVANY2,X'80'             SET ANY NEXT YEAR INV SWTCH            
         CLI   ASATRANG,C'N'                 AS-AT DATE RANGE TEST?             
         BE    GVAL1720                      NO                                 
         CLC   RCONSTWK,NEXASATF(R5)                                            
         BL    GVAL2840                      BEFORE AS-AT FROM                  
         CLC   RCONSTWK,TONXTASF(R5)                                            
         BH    GVAL2840                      AFTER AS-AT TO                     
         B     GVAL1740                      BETWEEN DATES (INCLUSIVE)          
GVAL1720 EQU   *                                                                
         OI    INVANY2,X'40'       SET 'INV FOR MONTH EXISTS' FLAG              
         CLC   RCONSTWK,NEXASATF(R5)     NEXT YEAR AS AT DATE                   
         BH    GVAL2840                                                         
GVAL1740 EQU   *                                                                
         L     R6,DUB                    ADD: NEXT YEAR AS AT INVOICE           
         A     R6,NXASATIN(R3)                                                  
         ST    R6,NXASATIN(R3)                                                  
         OI    INVANY2,X'20'             SET NEXT YEAR AS AT INV SWTCH          
         B     GVAL2840                                                         
**>>>    TRADE PROCESSING SECTION END                                           
GVAL2840 EQU   *                                                                
         SR    R6,R6                                                            
         IC    R6,1(R2)                                                         
         AR    R2,R6                                                            
         B     GVAL0020                                                         
         SPACE 2                                                                
XIT      XIT1                                                                   
         EJECT                                                                  
ACCTVALS NTR1                                                                   
         LA    R2,34(R2)           R2 A(CONTRACT)                               
*                                  R3 A(OUTPUT TABLE: $ BUCKETS)                
*                                  R8 A(OUTPUT TABLE: MONTH INFO)               
         XC    INVPREV(2),INVPREV                                               
         SPACE 2                                                                
ACCV0002 EQU   *                                                                
         CLI   0(R2),0                                                          
         BE    XIT                                                              
         CLC   0(1,R2),ESTTYPE           ESTIMATE BUCKET?                       
         BNE   ACCV0016                                                         
         USING RCONBKEL,R2                                                      
         CLC   RCONBKYR(2),CURDATE(R8)   MONTH OF SERVICE?                      
         BE    ACCV0008                  YES - PROCESS IT                       
         C     R4,AMONINFO               STILL AT 1ST REQUESTED MON?            
         BNE   ACCV0080                  NO  - DO NOT NEED                      
         CLC   RCONBKYR(2),CURDATE(R8)   MON OF SVC < 1ST REQ MON?              
         BH    ACCV0080                  NO  - DO NOT NEED                      
ACCV0008 EQU   *                                                                
         MVC   FULL,RCONBKAM                                                    
         L     R0,FULL                                                          
         CLC   RCONBKWK,KEYMONE          ACTIVITY IN KEYMONTH?                  
         BH    ACCV0080                  NO  - AFTER END MONTH                  
         CLC   RCONBKWK,KEYMONS                                                 
         BNL   ACCV0012                  YES - TREAT AS THIS MONTH              
         A     R0,ACPREVMO(R3)           NO  - PREVIOUS MONTH                   
         ST    R0,ACPREVMO(R3)                                                  
         B     ACCV0080                                                         
ACCV0012 EQU   *                                                                
         A     R0,ACCURRMO(R3)           THIS MONTH                             
         ST    R0,ACCURRMO(R3)                                                  
         B     ACCV0080                                                         
*                                                                               
         DROP  R2                                                               
*                                                                               
ACCV0016 EQU   *                                                                
         CLC   0(1,R2),INVTYPE           INVOICE BUCKET?                        
         BNE   ACCV0080                                                         
         USING RCONSTEL,R2                                                      
         CLC   RCONSTYR(2),CURDATE(R8)   MONTH OF SERVICE?                      
         BE    ACCV0020                  YES                                    
         C     R4,AMONINFO               STILL AT 1ST REQUESTED MON?            
         BNE   ACCV0080                  NO                                     
         CLC   RCONSTYR(2),CURDATE(R8)   MON OF SVC < 1ST REQ MON?              
         BH    ACCV0080                  NO  - DO NOT NEED                      
ACCV0020 EQU   *                                                                
         MVC   FULL,RCONSTAM                                                    
         L     R0,FULL                                                          
         CLC   RCONSTWK,KEYMONE          ACTIVITY IN KEYMONTH?                  
         BH    ACCV0080                  NO  - AFTER END MONTH                  
         MVI   INVANY,1                  YES - SET INVOICE FLAG                 
         CLC   RCONSTWK,KEYMONS          EARLIER THAN KEYMONTH?                 
         BNL   ACCV0024                  NO  - KEY MONTH INVOICE                
         MVI   INVPREV,1                 YES - SET PREV INVOICE FLAG            
         B     ACCV0080                                                         
ACCV0024 EQU   *                                                                
         A     R0,ACKEYMO(R3)            KEY MONTH INVOICE                      
         ST    R0,ACKEYMO(R3)                                                   
*                                                                               
ACCV0080 EQU   *                                                                
         SR    R6,R6                                                            
         IC    R6,1(R2)                                                         
         AR    R2,R6                                                            
         B     ACCV0002                                                         
         EJECT                                                                  
*                                                                               
*    FIXPOST:        FOR FIRST MONTH, ONLY REPORT THE ACTIVITY FOR              
*                    THE EOM PERIOD AND NOT THE TOTAL DOLLARS                   
*                                                                               
FIXPOST  NTR1                                                                   
*                                                                               
*    FIND FIRST MONTH OF REQUEST IN OUTPUT                                      
*                                                                               
         L     R2,AMONINFO         A(REQUEST DATE GRID)                         
         MVC   REQSTART,0(R2)      SET FIRST MONTH OF REQUEST                   
         L     R3,SAVR3            A(OUTPUT AREA)                               
         LA    R0,72               LOOP CONTROL                                 
FIXP0010 EQU   *                                                                
*                                                                               
*   FIRST POSITION OF MONINFO TABLE IS BINARY YYMM.  BINARY EQUIVA-             
*     LENT IN OUTPUT AREA IS 'CURDATE' CHARACTERS INTO THE FIELD.               
*                                                                               
         CLC   0(2,R2),CURDATE(R3) FIRST DATE OF REQUEST?                       
         BE    FIXP0020            YES                                          
         LA    R3,NEXTBUCK(R3)     NO  - CHECK NEXT MONTH                       
         BCT   R0,FIXP0010                                                      
         DC    H'0'                NOT FOUND IN TABLE?                          
FIXP0020 EQU   *                                                                
         ST    R3,FIRSTMON         SAVE A(1ST BUCKET OF REQUEST)                
         XC    FULL,FULL           FINAL RESULT                                 
*                                                                               
         L     R3,=F'-2'           USE R3 TO HOLD EST $                         
         L     R4,=F'-2'           USE R4 TO HOLD INV $                         
         SR    R5,R5               USE R5 AS A FLAG                             
         L     R6,SAVEPARA         SET A(CONTRACT RECORD)                       
         MVC   ELCODE,ESTTYPE      RETRIEVE 1ST 03/53 ELEMENT                   
         BAS   RE,GETEL                                                         
         BNE   FIXP0230            NONE FOUND - DO '04'/'24'S ONLY              
*                                                                               
FIXP0030 EQU   *                                                                
         CLC   2(2,R6),REQSTART    BUCKET SERVICE MONTH VS REQSTART             
         BH    FIXP0200            AFTER IT -                                   
         ST    R6,WORD             SAVE POINTER TO THIS EL                      
FIXP0040 EQU   *                                                                
         SR    RF,RF                                                            
         ZICM  RE,6(R6),4          EXTRACT AMOUNT                               
         BZ    FIXP0050            NO DOLLARS                                   
         CLI   SAVEPARA+20,C'R'    RRG REQUEST?                                 
         BNE   FIXP0045            NO  - DROP PENNIES                           
         LR    RF,RE               YES - DON'T DROP PENNIES                     
         B     FIXP0050                                                         
FIXP0045 EQU   *                                                                
         SRDA  RE,31               SHIFT IT INTO RF AND DOUBLE                  
         D     RE,=F'100'          DROP PENNIES                                 
         LTR   RF,RF               ANY VALUE?                                   
         BM    *+8                 NEGATIVE                                     
         AH    RF,=H'1'            YES - ADD 1 FOR ROUNDING                     
         SRA   RF,1                DIVIDE BY TWO                                
FIXP0050 EQU   *                                                                
         C     R3,=F'-2'           HAS R3 BEEN USED?                            
         BNE   FIXP0060            YES                                          
         SR    R3,R3               NO  - INITIALIZE IT                          
FIXP0060 EQU   *                                                                
         AR    R3,RF               ACCUMULATE '03' VALUE                        
         CLC   4(2,R6),KEYMONS     PRIOR TO KEYMONTH START?                     
         BL    FIXP0070            YES -                                        
         CLC   4(2,R6),KEYMONE     AFTER KEYMONTH END?                          
         BH    FIXP0080            YES - PAST USEFUL ELEMENTS                   
         LA    RE,1                                                             
         OR    R5,RE                                                            
FIXP0070 EQU   *                                                                
         BAS   RE,NEXTEL           GET NEXT '03' ELEMENT                        
         BNE   FIXP0080            NO MORE -                                    
         L     RF,WORD             SET A(PREVIOUS ELEMENT)                      
         CLC   0(4,RF),0(R6)       SAME MONTH?                                  
         BE    FIXP0040            YES - GO BACK AND PROCESS IT                 
FIXP0080 EQU   *                                                                
         L     R6,SAVEPARA         NOW LOOK FOR INV EL'S                        
         MVC   ELCODE,INVTYPE      GET FIRST INV ELEMENT                        
         BAS   RE,GETEL                                                         
         BNE   FIXP0150            NONE FOUND                                   
FIXP0090 EQU   *                                                                
         L     RF,WORD             SET A(PREVIOUS ELEMENT)                      
         CLC   2(2,RF),2(R6)       SAME MONTH?                                  
         BNE   FIXP0140            NO                                           
         CLC   4(2,R6),KEYMONE     AFTER KEYMONTH?                              
         BH    FIXP0150            YES                                          
         SR    RF,RF               NO                                           
         ZICM  RE,6(R6),4          INVOICE $$?                                  
         BZ    FIXP0100            NO                                           
         CLI   SAVEPARA+20,C'R'    RRG REQUEST?                                 
         BNE   FIXP0095            NO  - DROP PENNIES                           
         LR    RF,RE               YES - DON'T DROP PENNIES                     
         B     FIXP0100                                                         
FIXP0095 EQU   *                                                                
         SRDA  RE,31               DROP PENNIES AS BEFORE                       
         D     RE,=F'100'                                                       
         LTR   RF,RF                                                            
         BM    *+8                                                              
         AH    RF,=H'1'                                                         
         SRA   RF,1                                                             
FIXP0100 EQU   *                                                                
         C     R4,=F'-2'           INVOICE $$ USED?                             
         BNE   FIXP0110            YES                                          
         SR    R4,R4               NO  - INITIALIZE                             
FIXP0110 EQU   *                                                                
         AR    R4,RF               ACCUMULATE INVOICE $$                        
         CLC   4(2,R6),KEYMONS     PRIOR TO KEYMONTH?                           
         BL    FIXP0130            YES                                          
         LA    RE,2                NO                                           
         OR    R5,RE                                                            
         L     RF,WORD             SET A(PREVIOUS ELEMENT)                      
         CLC   4(2,RF),4(R6)       ACTIVITY IN SAME WEEK?                       
         BE    FIXP0120            YES -                                        
         CLC   4(2,RF),KEYMONS     NO  - ORDERED IN KEYMONTH?                   
         BL    FIXP0140            NO                                           
FIXP0120 EQU   *                                                                
         L     R3,=F'-2'           INVOICE REPLACES ORDERED                     
         B     FIXP0140            SET EST TO 'NOT USED'                        
FIXP0130 EQU   *                                                                
         C     R3,=F'-2'           EST 'NOT USED'?                              
         BE    FIXP0140            YES                                          
         LR    R3,R4               IF INV IS IN PAST, USE BEST DOLLAR           
         LA    RE,2                                                             
         NR    R5,RE               AND UPDATES TO ORDERED ARE USELESS           
FIXP0140 EQU   *                                                                
         BAS   RE,NEXTEL           GET NEXT ELEMENT                             
         BE    FIXP0090            GO BACK AND PROCESS IT                       
FIXP0150 EQU   *                                                                
         LTR   R5,R5                NO MORE '04'S -  IS FLAG SET?               
         BZ    FIXP0180             NO  - SKIP THIS MONTH                       
         C     R3,=F'-2'           IS EST USED?                                 
         BE    FIXP0170            NO                                           
         C     R4,=F'-2'           IS INV USED?                                 
         BE    FIXP0160            NO                                           
         SR    R4,R3               GET NET AMT FOR BROADCAST MONTH              
         B     FIXP0170                                                         
FIXP0160 EQU   *                                                                
         LR    R4,R3                                                            
FIXP0170 EQU   *                                                                
         A     R4,FULL              ADD IN RUNNING TOTAL                        
         ST    R4,FULL              STORE IT                                    
FIXP0180 EQU   *                                                                
         L     R3,=F'-2'           SET EST TO 'NOT USED'                        
         L     R4,=F'-2'           SET INV TO 'NOT USED'                        
         SR    R5,R5                                                            
         MVC   ELCODE,ESTTYPE                                                   
         L     R6,WORD             RE-SET TO LAST X'03'/X'53' ELT               
FIXP0190 EQU   *                                                                
         BAS   RE,NEXTEL           GET NEXT '03'/'53' ELEMENT                   
         BNE   FIXP0200            NO MORE                                      
         L     RF,WORD                                                          
         CLC   0(4,R6),0(RF)       NEXT MONTH?                                  
         BE    FIXP0190            YES                                          
         B     FIXP0030            NO  - PROCESS IT                             
*                                                                               
FIXP0200 EQU   *               PROCESS X'04' ELS NOT COVERED BY X'03'           
*                                  OR  X'54' ELS NOT COVERED BY X'53'           
         L     R6,SAVEPARA         SET A(CONTRACT RECORD)                       
         MVC   ELCODE,INVTYPE      GET FIRST INV ELEMENT                        
         BAS   RE,GETEL                                                         
         BNE   FIXP0300            NONE FOUND - EXIT                            
         B     FIXP0220                                                         
FIXP0210 EQU   *                                                                
         BAS   RE,NEXTEL           GET NEXT INV ELEMENT                         
         BNE   FIXP0300            NONE LEFT - EXIT                             
FIXP0220 EQU   *                                                                
         CLC   2(2,R6),REQSTART    MONTH OF SERVICE = REQSTART?                 
         BH    FIXP0300            NO  -                                        
         CLC   4(2,R6),KEYMONS     ACTIVITY DURING KEYMONTH?                    
         BL    FIXP0210            NO  - PRIOR                                  
         CLC   4(2,R6),KEYMONE                                                  
         BH    FIXP0210            AFTER                                        
         BAS   RE,FINDAN03         RETRIEVE AN '03' ELEMENT                     
*                                     FOR THIS DATE                             
         OC    EST03ADD,EST03ADD   ANY 03 FOUND?                                
         BNZ   FIXP0210            ORDERED EXISTS FOR THIS 04                   
         ZICM  RE,6(R6),4          NO 03 FOR THIS 04                            
         BZ    FIXP0210            NO DOLLARS FOR THIS INV                      
         CLI   SAVEPARA+20,C'R'    RRG REQUEST?                                 
         BNE   FIXP0225            NO  - DROP PENNIES                           
         LR    RF,RE               YES - DON'T DROP PENNIES                     
         B     FIXP0228                                                         
FIXP0225 EQU   *                                                                
         SR    RF,RF               DOLLARS - DROP PENNIES AS BEFORE             
         SRDA  RE,31                                                            
         D     RE,=F'100'                                                       
         LTR   RF,RF                                                            
         BM    *+8                                                              
         AH    RF,=H'1'                                                         
         SRA   RF,1                                                             
FIXP0228 EQU   *                                                                
         L     RE,FULL                                                          
         AR    RE,RF                                                            
         ST    RE,FULL                                                          
         B     FIXP0210                                                         
*                                                                               
*        SEPARATE PROCESSING FOR CONTRACTS WITH ONLY INV ELEMENTS               
*                                                                               
FIXP0230 EQU   *                                                                
         SR    R4,R4                    USE R4 TO HOLD INV $                    
         SR    R5,R5                    USE R5 AS A FLAG                        
         L     R6,SAVEPARA         SET A(CONTRACT RECORD)                       
         MVC   ELCODE,INVTYPE                                                   
         BAS   RE,GETEL                                                         
         BNE   FIXP0300                                                         
*                                                                               
FIXP0240 EQU   *                                                                
         SR    R3,R3                                                            
         CLC   2(2,R6),REQSTART    BUCKET DATE LATER THAN REQSTART?             
         BH    FIXP0300            YES - FINISHED                               
         ST    R6,WORD             NO  - SAVE POINTER TO THIS EL                
FIXP0250 EQU   *                                                                
         SR    RF,RF                                                            
         ZICM  RE,6(R6),4          GET ELEMENT $$                               
         BZ    FIXP0260            NONE -                                       
         CLI   SAVEPARA+20,C'R'    RRG REQUEST?                                 
         BNE   FIXP0255            NO  - DROP PENNIES                           
         LR    RF,RE               YES - DON'T DROP PENNIES                     
         B     FIXP0260                                                         
FIXP0255 EQU   *                                                                
         SRDA  RE,31               STRIP PENNIES                                
         D     RE,=F'100'                                                       
         LTR   RF,RF                                                            
         BM    *+8                                                              
         AH    RF,=H'1'                                                         
         SRA   RF,1                                                             
FIXP0260 EQU   *                                                                
         CLC   4(2,R6),KEYMONS     ACTIVITY PRIOR TO KEYMONTH?                  
         BL    FIXP0270            YES                                          
         CLC   4(2,R6),KEYMONE     ACTIVITY AFTER KEYMONTH?                     
         BH    FIXP0280            YES -  PAST USEFUL ELEMENTS                  
         LA    R5,1                                                             
         AR    R4,RF                                                            
FIXP0270 EQU   *                                                                
         BAS   RE,NEXTEL           GET NEXT ELEMENT                             
         BNE   FIXP0280            NONE LEFT                                    
         L     RF,WORD                                                          
         CLC   0(4,RF),0(R6)       SAME MONTH?                                  
         BE    FIXP0250                                                         
         LR    R3,R6                                                            
FIXP0280 EQU   *                                                                
         LTR   R5,R5               FLAG SET?                                    
         BZ    FIXP0290            NO  - SKIP THIS MONTH                        
         A     R4,FULL             ADD IN RUNNING TOTAL                         
         ST    R4,FULL             STORE IT                                     
FIXP0290 EQU   *                                                                
         SR    R4,R4                                                            
         SR    R5,R5                                                            
         CR    R3,R6                                                            
         BE    FIXP0240                                                         
*                                                                               
*        FIXPOST EXIT                                                           
*                                                                               
FIXP0300 EQU   *                                                                
         L     R3,FIRSTMON         RESET A(FIRST MONTH OF REQUEST)              
         LA    R3,BUCKDISP(R3)     POINT TO $$ BUCKETS                          
         MVC   PRASATOR(4,R3),FULL                                              
*                                  LOAD CALCED AMOUNT FOR FIRST MONTH           
*                                    PRIOR MONTHS ADJUSTMENTS                   
         XIT1                                                                   
         EJECT                                                                  
*                                                                               
*   FINDAN03:                                                                   
*        R6          = A(04 ELEMENT)                                            
*        SAVEPARA    = A(CONTRACT RECORD)                                       
*        EST03ADD    = RETURN ADDRESS OF 03 ELEMENT                             
*                      ZERO IF NOT FOUND                                        
*                                                                               
FINDAN03 NTR1                                                                   
         XC    EST03ADD,EST03ADD   ZERO OUT RETURN ADDRESS                      
         L     R3,SAVEPARA         SET A(CONTRACT RECORD)                       
         LA    R3,34(R3)           SET A(01 ELEMENT IN CONTRACT RECORD)         
FIAN0010 EQU   *                                                                
         CLI   0(R3),0             END OF RECORD?                               
         BE    FIAN0040            YES - FINISHED                               
         CLC   0(1,R3),ESTTYPE     X'03'/X'53' ELEMENT?                         
         BE    FIAN0030            YES                                          
FIAN0020 EQU   *                                                                
         ZIC   RF,1(R3)            NO  - BUMP TO NEXT ELEMENT                   
         AR    R3,RF                                                            
         B     FIAN0010            GO BACK FOR NEXT                             
FIAN0030 EQU   *                                                                
         CLC   2(2,R3),2(R6)       03/53 DATE = 04/24 DATE?                     
         BNE   FIAN0020            NO  - GO BACK FOR NEXT                       
         ST    R3,EST03ADD         YES - SAVE ADDRESS                           
FIAN0040 EQU   *                                                                
         XIT1                      EXIT ROUTINE                                 
         EJECT                                                                  
*                                                                               
*   ROUTINE IS ONLY CALLED IF CONTRACT SPECIFIC INFORMATION IS ASKED            
*     FOR.  AT THIS TIME (MARCH 1992) ONLY 'RRG' (REREPRG02) CAN                
*     ASK FOR IT.                                                               
*     THE 'PENDING FLAG' (SEE REREPRGEQC) WILL BE RETURNED AS:                  
*        SPACE  =  NOT PENDING                                                  
*            Y  =  PENDING/NEW SAR ELEMENT NOT FOUND                            
*            P  =  PENDING/NEW SAR ELEMENT     FOUND                            
*                                                                               
CONSPECS NTR1                                                                   
         L     R2,SAVEPARA         RELOAD A(CONTRACT RECORD)                    
         LA    R2,34(R2)           SET TO 1ST ELEMENT                           
*                                                                               
         L     R4,0(R1)            A(CONTRACT SPECIFIC INFO FLAG)               
*                                     = A(CONSPEC TABLE AREA)                   
         L     R4,0(R4)            A(TABLE AREA)                                
         XC    0(100,R4),0(R4)     RESET AREA                                   
         L     R3,SAVR3            RESET A(OUTPUT AREA/NEWMON TABLE)            
         LA    RE,CONSPINF         GET A(CONSPEC AREA)                          
         SR    R3,RE                                                            
         L     R3,0(R3)            FROM MISCELLANEOUS STORAGE AREA              
         LA    R3,CSPENDNG(R3)     SET 'PENDING/NOT PENDING'                    
         MVI   0(R3),C'Y'          SET TO 'PENDING-OLD SAR ELEMENT'             
         MVI   2(R3),C'N'          SET 'FORECAST?' FLAG TO 'NO'                 
         CLC   =C'ACC-',RCONBUYR-RCONELEM(R2)                                   
*                                  ACCOUNTING CONTRACT?                         
         BNE   CSPE0008            NO                                           
         MVI   0(R3),C'N'          YES - SET TO 'NOT PENDING'                   
CSPE0008 EQU   *                                                                
         TM    40(R2),X'10'        ANY BUYS ENTERED?                            
*                                     CHECKING CONT MODIF REASON BYTE           
*                                       SET ON BUY ENTRY                        
         BNO   CSPE0010            NO                                           
         MVI   0(R3),C'N'          YES - SET TO 'NOT PENDING'                   
         OI    3(R3),X'80'         SET 'INCOMPLETE' FLAG ON                     
CSPE0010 EQU   *                                                                
         CLI   0(R2),0             TEST END OF CONTRACT                         
         BE    CSPE0120            FOUND                                        
         CLC   0(1,R2),ESTTYPE     BOOKED DOLLAR ELEMENT?                       
         BE    CSPE0020            YES                                          
*                                                                               
*   REMOVE X'04'/X'54' ELEMENTS FROM 'PENDING' DEFINITION                       
*                                                                               
*        CLC   0(1,R2),INVTYPE     BILLED DOLLAR ELEMENT?                       
*        BE    CSPE0020            YES                                          
*                                                                               
         CLI   0(R2),X'06'         SPL/EPL ELEMENT?                             
         BNE   CSPE0030            NO                                           
         BAS   RE,SETUPSPL         YES - SET UP SPL INFO                        
         NI    3(R3),X'FF'-X'80'   SET 'INCOMPLETE' FLAG OFF                    
*                                     NOW 'COMPLETED' BY SPL                    
         OI    3(R3),X'40'         SET 'SPL W/IN ACTIVITY PERIOD'               
         OC    DXTRAFRM(4,R5),DXTRAFRM(R5)                                      
*                                  EXTRA SET OF DATES ENTERED?                  
         BZ    CSPE0020            NO  - LEAVE AS 'W/IN'                        
         CLC   2(2,R2),DXTRAFRM(R5)                                             
*                                  DATE SPL ENTERED VS EXTRA START              
         BL    CSPE0018            EARLIER THAN START DATE                      
         CLC   2(2,R2),DXTRATO(R5)                                              
*                                  DATE SPL ENTERED VS EXTRA END                
         BNH   CSPE0020            BETWEEN START AND END: ACCEPTED              
CSPE0018 EQU   *                                                                
         NI    3(R3),X'FF'-X'40'   SET 'SPL NOT W/IN ACTIVITY PER'              
CSPE0020 EQU   *                                                                
         MVI   0(R3),C'N'          SET TO 'NOT PENDING'                         
         B     CSPE0060            GET NEXT ELEMENT                             
CSPE0030 EQU   *                                                                
         CLI   0(R2),X'12'         'SAR' ELEMENT?                               
         BNE   CSPE0060            NO  - GET NEXT ELEMENT                       
         CLI   1(R2),120           OLD SAR ELEMENT (LEN=120)                    
         BE    CSPE0060            YES - DON'T USE IT                           
*                                                                               
*   LOAD THE SAR FIELDS INTO THE CONTRACT SPECIFIC TABLE AREA, SET              
*     THE 'PENDING' FLAG TO 'PENDING/SAR ELEMENT FOUND' (IF STILL               
*     PENDING).  *** ADDITIONAL WORK MAY BE REQUIRED HERE!!! ***                
*                                                                               
         USING RSARXEL,R2                                                       
         ZIC   RF,RSARXSHG         ADD PENNIES TO SHARE GOAL                    
         SR    RE,RE                  FOR FINAL RRG USE                         
         M     RE,=F'100'                                                       
         ST    RF,CSSHGOAL(R4)     LOAD SHARE GOAL VALUE                        
         MVC   CSBUDGET(4,R4),RSARXBGT                                          
*                                  LOAD BUDGET $$                               
         MVC   FULL,RSARXBGT                                                    
         L     RF,FULL                                                          
         SR    RE,RE                                                            
         M     RE,=F'100'          INSERT PENNIES FOR RRG USE                   
         ST    RF,FULL                                                          
         MVC   CSMKTDOL(4,R4),FULL                                              
*                                  LOAD MARKET DOLLARS                          
         ZIC   R7,RSARXSHG         GET SHARE GOAL                               
         SR    RE,RE                                                            
         MVC   FULL,RSARXBGT       GET BUDGET $$                                
         L     RF,FULL                                                          
         MR    RE,R7               MARKET $$ X SHR GOAL                         
         SLDA  RE,1                DOUBLE FOR ROUNDING                          
         AH    RF,=H'100'                                                       
         D     RE,=F'100'                                                       
         SRA   RF,1                                                             
         SR    RE,RE                                                            
         M     RE,=F'100'          INSERT PENNIES FOR RRG USE                   
         ST    RF,FULL                                                          
         MVC   CSSHRDOL(4,R4),FULL                                              
*                                  INSERT STATION SHARE $$                      
CSPE0048 EQU   *                                                                
         CLI   0(R3),C'N'          NOT 'PENDING'?                               
         BE    CSPE0060            CORRECT                                      
         MVI   0(R3),C'P'          SET TO 'PENDING+SAR FOUND'                   
         TM    RSARXFLG,X'10'      'FORECAST?' FLAG SET?                        
         BNO   CSPE0060            NO  -                                        
         MVI   2(R3),C'Y'          YES - SET RETURN TO 'YES'                    
         TM    RSARXFLG,X'40'      $ZERO BUDGET ENTERED?                        
         BNO   CSPE0060            NO  -                                        
         OI    3(R3),X'10'         YES - SET FORECAST = $0                      
*                                                                               
         DROP  R2                                                               
*                                                                               
CSPE0060 EQU   *                                                                
         CLI   0(R2),X'1F'         EXTENDED DESCRIP ELT?                        
         BNE   CSPE0070            NO  - GET NEXT ELEMENT                       
         TM    RCONCONF-RCONXEL(R2),X'40'                                       
         BO    CSPE0065            CONFIRMED NOW                                
         TM    RCONCONF-RCONXEL(R2),X'20'                                       
*                                  CONFIRMED PREVIOUSLY?                        
         BNO   CSPE0070            NO  - UNCONFIRMED                            
CSPE0065 EQU   *                                                                
         OI    3(R3),X'20'         CONFIRMED/PREV CONFIRMED                     
CSPE0070 EQU   *                                                                
         SR    RF,RF               BUMP TO NEXT ELEMENT                         
         IC    RF,1(R2)                                                         
         AR    R2,RF                                                            
         B     CSPE0010                                                         
CSPE0120 EQU   *                                                                
         L     R2,SAVEPARA         RELOAD A(CONTRACT RECORD)                    
         LA    R2,34(R2)           SET TO 1ST ELEMENT                           
         CLC   =C'ACC-',RCONBUYR-RCONELEM(R2)                                   
*                                  ACCOUNTING CONTRACT?                         
         BNE   CSPE0160            NO                                           
         OI    3(R3),X'20'         YES - CONSIDER 'CONFIRMED'                   
CSPE0160 EQU   *                                                                
         TM    RCONMODR+1-RCONELEM(R2),X'80'    ACE CONTRACT?                   
         BO    CSPE0200            YES - LEAVE AS IS                            
         TM    RCONMODR+1-RCONELEM(R2),X'40'    GRAPHNET CONTRACT?              
         BO    CSPE0200            YES - LEAVE AS IS                            
         OI    3(R3),X'20'         NO  - OTHER: CONSIDER 'CONFIRMED'            
CSPE0200 EQU   *                                                                
         B     XIT                                                              
*                                                                               
         EJECT                                                                  
*                                                                               
*   SETUPSPL:  ANALYSIS OF X'06' ELEMENT TO DETERMINE REP'D STATION             
*      SPL PERCENT                                                              
*        R2  =  A(X'06' ELEMENT)                                                
*                                                                               
SETUPSPL NTR1                                                                   
         L     R3,SAVR3            RESET A(OUTPUT AREA/NEWMON TABLE)            
         LA    RE,CONSPINF         GET A(CONSPEC AREA)                          
         SR    R3,RE                                                            
         L     R3,0(R3)            FROM MISCELLANEOUS STORAGE AREA              
         USING RCONSPEL,R2                                                      
         MVI   CSSPLOSS(R3),C'N'   SET SPL LOSS TO 'NO'                         
         TM    RCONSPES,X'04'      SPL AS PERCENTS?                             
         BNO   SSPL0060            NO  - STORED AS $$                           
         TM    RCONSPES,X'40'      SPL A LOSS?                                  
         BNO   SSPL0015            NO                                           
*                                  MAYBE - IF AT LEAST ONE STATION              
*                                     HAS A NON-ZERO VALUE                      
         ZIC   R0,RCONSPNU         SET # OF MINI ELTS                           
         LA    RF,RCONSPAM         1ST STATION AMOUNT                           
SSPL0005 EQU   *                                                                
         OC    0(4,RF),0(RF)       ANY VALUE IN FIELD?                          
         BNZ   SSPL0020            YES - IT'S A REAL LOSS!                      
         LA    RF,9(RF)            NO  - BUMP TO NEXT STA'S $$                  
         BCT   R0,SSPL0005                                                      
*                                  NO $$ - NOT A LOSS                           
SSPL0015 EQU   *                                                                
         MVC   CSSPLPCT(4,R3),RCONSPAM                                          
*                                  NO  - SET REP'D STATION SPL PCT              
         MVC   CSSPLPER(4,R3),RCONSPAM                                          
*                                  ALSO SET EXCLUSIVE PERCENT                   
         B     SSPL0900            DONE                                         
SSPL0020 EQU   *                                                                
         MVI   CSSPLOSS(R3),C'Y'   SET SPL LOSS TO 'YES'                        
SSPL0030 EQU   *                                                                
         CLI   0(R2),0             END OF RECORD?                               
         BNE   *+6                                                              
         DC    H'0'                YES - NO X'08' - DUMP                        
         CLI   0(R2),X'08'         AUTO-CLOSE ELEMENT?                          
         BE    SSPL0040            YES - PROCESS IT                             
         ZIC   RF,1(R2)            NO  - BUMP TO NEXT ELEMENT                   
         AR    R2,RF                                                            
         B     SSPL0030            GO BACK FOR NEXT                             
SSPL0040 EQU   *                                                                
         MVC   CSSPLPCT(4,R3),RCONAC$$-RCONACEL(R2)                             
*                                  LOAD TOTAL BUDGET $$ INTO PERCENT            
*                                     FIELD AS 'LOSS'                           
         MVC   CSSPLLSS(4,R3),RCONAC$$-RCONACEL(R2)                             
*                                  ALSO SET EXCLUSIVE LOSS                      
         B     SSPL0900            DONE                                         
SSPL0060 EQU   *                                                                
         SR    R1,R1               ZERO ACCUMULATOR                             
         LA    R4,RCONSPST         A(1ST STATION)                               
         ZIC   R0,RCONSPNU         # OF MINI-ELTS                               
         MVC   BUCKET,5(R4)        UNLOAD REP'D STATION $$                      
         L     R7,BUCKET           SAVE VALUE IN R7                             
SSPL0070 EQU   *                                                                
         A     R1,BUCKET           ACCUMULATE STATION $$                        
         LA    R4,9(R4)            BUMP TO NEXT MINI-ELT                        
         MVC   BUCKET,5(R4)        UNLOAD STATION $$                            
         BCT   R0,SSPL0070         GO BACK FOR NEXT                             
         LTR   R7,R7               ANY REP'D STATION $$?                        
         BNZ   SSPL0090            YES                                          
         MVI   CSSPLOSS(R3),C'Y'   SET SPL LOSS TO 'YES'                        
         ST    R1,BUCKET                                                        
         MVC   CSSPLPCT(4,R3),BUCKET                                            
*                                  INSERT TOTAL $$ INTO PERCENT                 
*                                     FIELD AS 'LOSS'                           
         MVC   CSSPLLSS(4,R3),BUCKET                                            
*                                  ALSO SET EXCLUSIVE LOSS                      
         B     SSPL0900            DONE                                         
SSPL0090 EQU   *                                                                
         SR    R6,R6               CLEAR                                        
         M     R6,=F'10000'        MULT FOR DEC'L ALIGNMENT                     
*                                                                               
*    10,000 PRODUCES ALIGNMENT FOR X.XX%                                        
*                                                                               
         SLDA  R6,1                DOUBLE FOR DIVIDE                            
         AR    R7,R1               ADD TOTAL FOR ROUNDING                       
         DR    R6,R1               DIVIDE BY TOTAL                              
         SRA   R7,1                DIVIDE BY 2                                  
         ST    R7,BUCKET                                                        
         MVC   CSSPLPCT(4,R3),BUCKET                                            
*                                  INSERT PERCENT                               
         MVC   CSSPLPER(4,R3),BUCKET                                            
*                                  ALSO SET EXCLUSIVE PERCENT                   
         B     SSPL0900            DONE                                         
SSPL0900 EQU   *                                                                
         XIT1                                                                   
         DROP  R2                                                               
         EJECT                                                                  
         GETEL R6,34,ELCODE                                                     
         EJECT                                                                  
*                                                                               
*        LOCAL WORKSPACE  FOR THIS MODULE    *                                  
*                                                                               
SAVRF    DS    F                   SAVE AREA FOR RF (LOOP CONTROL)              
SAVR3    DS    F                   SAVE AREA FOR R3 (OUTPUT AREA)               
SAVR6    DS    F                   SAVE AREA FOR R6 (ACTIVITY DATES)            
PARALIST DS    6F                  PARAMETER LIST                               
ASATRANG DS    CL1                 AS-AT RANGE TESTING FLAG                     
ELCODE   DS    CL1                 ELEMENT CODE FOR GETEL                       
REQSTART DS    CL2                 FIRST MONTH OF REQUEST                       
EST03ADD DS    A                   RETURNED ADDRESS OF 03 ELEMENT               
WORD     DS    F                                                                
BUCKET   DS    F                                                                
FIRSTMON DS    A                   A(FIRST MONTH OF REQUEST)                    
ESTTYPE  DS    XL1                 ESTIMATE BUCKET TYPE                         
*                                  X'03'  =  REGULAR ESTIMATE BUCKET            
*                                  X'53'  =  ALTERNATE CALENDAR EST             
INVTYPE  DS    XL1                 INVOICE  BUCKET TYPE                         
*                                  X'04'  =  REGULAR ESTIMATE BUCKET            
*                                  X'54'  =  ALTERNATE CALENDAR EST             
TESTTYPE DS    XL1                 TRADE ESTIMATE BUCKET TYPE                   
*                                  X'63'  =  REGULAR ESTIMATE BUCKET            
*                                  X'83'  =  ALTERNATE CALENDAR EST             
TINVTYPE DS    XL1                 INVOICE  BUCKET TYPE                         
*                                  X'64'  =  REGULAR ESTIMATE BUCKET            
*                                  X'84'  =  ALTERNATE CALENDAR EST             
*                                                                               
*  INCLUDE REREPRGEQC          (EQUATES FOR USE BY VALU3NEW - TRADE)            
       ++INCLUDE REREPRGEQC                                                     
*                                                                               
*   FOLLOWING EQU'S COVER DISPLACEMENT OF BUCKETS IN ACCOUNTING                 
*     ROUTINE 'ACCTVALS'                                                        
*                                                                               
ACCURRMO EQU   0                   CURRENT  MONTH                               
ACPREVMO EQU   4                   PREVIOUS MONTH                               
ACKEYMO  EQU   8                   KEY MONTH INVOICE                            
*                                                                               
*   FOLLOWING EQU'S COVER DISPLACEMENTS OF FROM/TO ACTIVITY PERIODS             
*                                                                               
CURAPERS EQU   0                   CURRENT ACTIVITY PERIOD START                
CURAPERE EQU   2                   CURRENT ACTIVITY PERIOD END                  
PRIAPERS EQU   4                   PRIOR ACTIVITY PERIOD START                  
PRIAPERE EQU   6                   PRIOR ACTIVITY PERIOD END                    
PR2APERS EQU   8                   2 YRS PRIOR ACTIVITY PERIOD START            
PR2APERE EQU   10                  2 YRS PRIOR ACTIVITY PERIOD END              
NEXAPERS EQU   12                  NEXT YEAR ACTIVITY PERIOD END                
NEXAPERE EQU   14                  NEXT YEAR ACTIVITY PERIOD END                
*                                                                               
*                                                                               
*   FOLLOWING EQU'S COVER DISPLACEMENTS IN MONTH INFORMATION TABLE              
*                                                                               
MICURDAT EQU   0                   CURRENT YEAR                                 
MIPRIDAT EQU   4                   PRIOR YEAR                                   
MI2PRDAT EQU   8                   2 YEARS' PRIOR                               
MINEXDAT EQU   12                  NEXT YEAR                                    
*              DSECT FOR THE MODULE                                             
         SPACE 3                                                                
VALUED   DSECT                                                                  
WORK     DS    CL32                                                             
PARA     DS    F                                                                
DUB      DS    D                                                                
WEEK     DS    CL2                                                              
ACTSTART DS    CL2                                                              
ACTEND   DS    CL2                                                              
LASTWEEK DS    CL2                                                              
LASTACTS DS    CL2                                                              
LASTACTE DS    CL2                                                              
THISSERV DS    CL8                                                              
LASTSERV DS    CL8                                                              
BASIS    DS    CL1                                                              
         DS    0F                                                               
SAVEPARA DS    CL24                                                             
ACCADJ   DS    D                                                                
INVPREV  DS    CL1                                                              
INVANY   DS    CL1                                                              
INVANY2  DS    CL1                                                              
KEYMONTH DS    0F                                                               
KEYMONS  DS    H                                                                
KEYMONE  DS    H                                                                
LASTMONS DS    H                                                                
LASTMONE DS    H                                                                
FULL     DS    F                                                                
AMONINFO DS    F            ADDRESS OF MONINFO                                  
         PRINT OFF                                                              
         EJECT                                                                  
       ++INCLUDE REGENCON                                                       
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'123REREPVAL4S05/01/02'                                      
         END                                                                    
