*          DATA SET FAPWDGEN   AT LEVEL 001 AS OF 05/16/16                      
*CATALP PWDGEN                                                                  
***********************************************************************         
*GENERATES A 10 CHARACTER PASSWORD FROM A TIME BASED RANDOM NUMBER    *         
*R1=A(10 CHR AREA TO RECEIVE GENERATED PASSWORD)                      *         
***********************************************************************         
PWDGEN   CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 WORKX-WORKD,*PWDGEN*                                             
         USING WORKD,RC                                                         
         SR    R2,R2                                                            
         ICM   R2,7,1(R1)          R2=A(10 CHARACTER OUTPUT AREA)               
         MVI   PASS,1                                                           
*                                                                               
GEN      STCK  DUB                 TIME IN CLOCK UNITS                          
         LM    R0,R1,DUB                                                        
         SRDL  R0,8                SHIFT OUT BITS THAT DONT CHANGE MUCH         
         LR    R0,R1                                                            
         MR    R0,R0               SQUARE                                       
         ALR   R1,R0               FOLD                                         
         STM   R0,R1,DUB           SAVE                                         
*                                                                               
GEN2     LHI   RF,5                CREATE FIVE CHRS OF PASSWORD                 
GEN3     SRDL  R0,5                                                             
         SRL   R1,27               SHIFT OUT A NUMBER 00-31                     
         LA    R1,CHRTAB(R1)                                                    
         MVC   0(1,R2),0(R1)       CONVERT TO A LETTER OR NUMBER                
         LA    R2,1(R2)                                                         
         BCT   RF,GEN3                                                          
*                                                                               
         CLI   PASS,1              SWITCH TO SECOND HALF OF PASSWORD            
         BNE   EXIT                                                             
         MVI   PASS,2                                                           
         L     R0,DUB+4            USE SECOND HALF OF RANDOM NUMBER             
         B     GEN2                                                             
*                                                                               
EXIT     XMOD1 1                                                                
*                                                                               
CHRTAB   DC    C'ABCDEFGHJKLMNPQRSTVWXYZ123456789' NO I,U,O AND 0.              
*                                                                               
         LTORG                                                                  
*                                                                               
WORKD    DSECT                                                                  
DUB      DS    D                                                                
PASS     DS    X                                                                
WORKX    EQU   *                                                                
*                                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'001FAPWDGEN  05/16/16'                                      
         END                                                                    
