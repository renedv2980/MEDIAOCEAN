*          DATA SET DDXSORT    AT LEVEL 005 AS OF 01/08/10                      
*CATALP XSORT                                                                   
         TITLE 'XSORT - EXCHANGE SORT MODULE'                                   
         SPACE 1                                                                
*        PARAM1  BYTE  0      X'00' = ASCENDING,X'FF' = DESCENDING              
*                BYTES 1-3    A(LIST)                                           
*                BYTES 1-3    X'FFFFFF' - 31-BIT A(LIST) IN PARAM6              
*                                                                               
*        PARAM2  BYTES 0-3    NUM OF ENTRIES                                    
*                                                                               
*        PARAM3  BYTE    3    LENGTH OF ENTRY                                   
*                                                                               
*        PARAM4  BYTE    3    LENGTH OF KEY                                     
*                                                                               
*        PARAM5  BYTE    3    DISPLACEMENT OF KEY INTO ENTRY                    
*                                                                               
*        PARAM6  BYTE  0-3    31-BIT A(LIST)                                    
         SPACE 1                                                                
         PRINT NOGEN                                                            
XSORT    RSECT                                                                  
         NMOD1 WORKX-WORKD,XSORT                                                
         USING WORKD,RC                                                         
         LA    RE,*+6              SET 24-BIT MODE                              
         BSM   0,RE                                                             
         MVC   EXECWRK,EXECBLK     COPY BLOCK TO WS                             
         EJECT                                                                  
*                  PROCESS PARAMETER LIST                                       
         SPACE 1                                                                
         L     R3,4(R1)            NUM OF ENTRIES                               
         BCTR  R3,0                                                             
         LTR   R3,R3                                                            
         BNP   XSORTEXT            FEWER THAN 2 ENTRIES                         
         L     R4,8(R1)            R4 = LENGTH OF ENTRY                         
         MR    R2,R4                                                            
         LR    R5,R3               LENGTH * (NUM OF ENTRIES-1)                  
         CLC   =X'FFFFFF',1(R1)    31-BIT ADDR?                                 
         BE    XS10                                                             
         L     R3,0(R1)            R3 = A(FIRST ENTRY)                          
         LA    R3,0(R3)            CLEAR HOB                                    
         B     XS20                                                             
*                                                                               
XS10     L     R3,20(R1)           R3 = 31-BIT A(FIRST ENTRY)                   
         LA    RE,*+10                                                          
         O     RE,=X'80000000'                                                  
         BSM   0,RE                SWITCH TO 31-BIT MODE                        
*                                                                               
XS20     AR    R5,R3                                                            
         BCTR  R5,0                R5 = A((LAST ENTRY) - 1)                     
         LCR   R2,R4               R2 = COMPLEMENT OF LENGTH                    
         MVI   XSORTBC+1,X'D0'     SET BRANCH TO BNH IF ACSENDING               
         CLI   0(R1),0                                                          
         BE    *+8                                                              
         MVI   XSORTBC+1,X'B0'     SET BRANCH TO BNL IF DESCENDING              
         LR    R6,R4                                                            
         AH    R6,=X'3000'         NOTE - R3 USED IN XC'S                       
*                                  DISPLACEMENTS IN XC'S                        
         STH   R6,XSORTXC1+4                                                    
         STH   R6,XSORTXC2+2                                                    
         STH   R6,XSORTXC3+4                                                    
         BCTR  R6,0                                                             
*                                  LENGTHS IN XC'S                              
         STC   R6,XSORTXC1+1                                                    
         STC   R6,XSORTXC2+1                                                    
         STC   R6,XSORTXC3+1                                                    
         L     R7,12(R1)                                                        
         BCTR  R7,0                                                             
*                                  LENGTH IN CLC                                
         STC   R7,XSORTCLC+1                                                    
         L     R7,16(R1)                                                        
*                                  DISPLACEMENTS IN CLC                         
         STC   R7,XSORTCLC+3                                                    
         LA    R7,1(R7,R6)         ADD ENTRY LENGTH                             
         STH   R7,XSORTCLC+4                                                    
*                                                                               
         SR    R6,R6                                                            
         LR    R7,R3               SAVE START OF LIST                           
         B     XSORTCLC                                                         
         EJECT                                                                  
*                   SORT LOGIC                                                  
         SPACE 1                                                                
***********************************************************************         
* THIS BLOCK OF INSTRUCTIONS IS COPIED INTO WORKING STORAGE BELOW               
EXECBLK  DS    0H                                                               
         CLC   0(0,R3),0(R3)       COMPARE KEYS                                 
         BC    0,XSORTNOX          NO EXCHANGE                                  
         XC    0(0,R3),0(R3)       EXCHANGE                                     
         XC    0(0,R3),0(R3)       THE                                          
         XC    0(0,R3),0(R3)       ENTRIES                                      
         B     XSORTBLX                                                         
EXECBLKQ EQU   *-EXECBLK                                                        
***********************************************************************         
*                                                                               
XSORTBLX LA    R6,1                RESET SWITCH                                 
XSORTNOX BXLE  R3,R4,XSORTCLC      NEXT COMPARE                                 
         BCT   R6,XSORTEXT         EXIT IF NO EXCHANGES + RESET SW              
         LR    R3,R7               RESTORE TO START OF LIST                     
         BXH   R5,R2,XSORTCLC      SHORTEN LIST AND DO ANOTHER PASS             
*                                                                               
XSORTEXT XMOD1 1                                                                
*                                                                               
         LTORG                                                                  
         SPACE 1                                                                
WORKD    DSECT                                                                  
* THE INSTRUCTIONS AT EXECBLK ARE COPIED INTO WORKING STORAGE BELOW             
* AND THEN MODIFIED. I.E., THE INSTRUCTIONS EXECUTE OUT OF WORKING              
* STORAGE, NOT OUT OF THE PROGRAM CSECT. THIS MAKES THE CODE REENTRANT.         
*                                                                               
         DS    0H                                                               
EXECWRK  DS    0XL(EXECBLKQ)                                                    
XSORTCLC CLC   0(0,R3),0(R3)       COMPARE KEYS                                 
XSORTBC  BC    0,XSORTNOX          NO EXCHANGE                                  
XSORTXC1 XC    0(0,R3),0(R3)       EXCHANGE                                     
XSORTXC2 XC    0(0,R3),0(R3)       THE                                          
XSORTXC3 XC    0(0,R3),0(R3)       ENTRIES                                      
         B     XSORTBLX                                                         
*                                                                               
WORKX    EQU   *                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'005DDXSORT   01/08/10'                                      
         END                                                                    
