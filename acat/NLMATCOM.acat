*          DATA SET NLMATCOM   AT LEVEL 036 AS OF 04/14/03                      
*CATALP NLMATCOM                                                                
*                                                                               
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *         
*                                                                               
         TITLE 'INSERTION ORDERS - CHECK FOR MATERIALS REPEAT'                  
*                                                                               
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *         
*                                                                               
* BPLA 06/00    NOW LIVE SOURCE (WAS NLMATCOB)                                  
*               CATALP AND ++INCLUDE T413WKA RESTORED                           
*                                                                               
* BPLA 02/09/00 COPY OF NLMATCOM LEVEL 27 MADE 02/09/00                         
*               T413WKAB FOR T413WKA                                            
*                                                                               
* SMYE 04/16/99 "JULJUI" CHANGED TO "JULJUL" IN DATETAB                         
*                                                                               
* BPLA 2/96     CLEAR SPACEND BEFORE READING REPS                               
*                                                                               
* BPLA 11/95    FRENCH CHANGES + PUBLISHER MAT REPEAT CHECK                     
*                                                                               
* BPLA 5/18/93  BEFORE SAVING REPEAT DATE BE SURE INSERTION IS                  
*               STILL ON FILE                                                   
*                                                                               
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *         
*                                                                               
MATCOM   CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 0,MATCOM,RR=R7                                                   
*                                                                               
         ST    R7,RELO                                                          
         B     *+8                                                              
RELO     DC    F'0'                                                             
*                                                                               
         USING POLWRKD,R9                                                       
         USING T413FFD,RA                                                       
         USING IOWORKD,R8                                                       
         L     RC,PPFILEC                                                       
         LA    R7,1(RC)                                                         
         LA    R7,4095(R7)                                                      
         USING POLFILED,RC,R7                                                   
*                                                                               
* TRY SETTING DICTIONARY HERE USING PUBLANG                                     
*                                                                               
         L     R5,=A(DICSECTM)                                                  
         A     R5,RELO                                                          
         USING DICSECTM,R5                                                      
         XC    DMCB,DMCB                                                        
         LA    R1,DMCB                                                          
         USING DICTATED,R1                                                      
         MVI   DDACTN,DDACTNL      TRANSLATE LIST                               
         MVI   DDRETN,DDCASEU                                                   
         MVI   DDSYS,4                                                          
         MVI   DDLANG,0            DEFAULT TO ENGLISH                           
         CLI   PUBLANG,C'F'                                                     
         BNE   *+8                                                              
         MVI   DDLANG,4            4 IS FRENCH                                  
*                                                                               
         MVC   MYLANG,PUBLANG                                                   
*                                                                               
         LA    RF,DCLIST                                                        
         STCM  RF,7,DDIADR                                                      
         LA    RF,DSLIST                                                        
         STCM  RF,7,DDOADR                                                      
         L     RF,VCOMFACS                                                      
         USING COMFACSD,RF                                                      
         GOTO1 CDICTATE                                                         
         DROP  R1,R5,RF                                                         
*                                                                               
         L     R6,=A(DICSECTM)                                                  
         A     R6,RELO                                                          
         USING DICSECTM,R6                                                      
*                                                                               
         ZAP   MATCNT,=P'0'                                                     
*                                                                               
         XC    BRPTDAT,BRPTDAT     CLEAR REPEAT DATE                            
         XC    RPTPUB,RPTPUB       CLEAR REPEAT PUB                             
         CLI   TSCHGSW-STATDIF,C'C'                                             
         BNE   MATC2                                                            
*                                                                               
         B     MATC2                                                            
*                                                                               
         L     R2,LASTIOEL         IF CHANGE - USE LAST IOELEM                  
         USING PIOELEM,R2                                                       
         CLC   PIOJOB,PBDJOB                                                    
         BNE   MATC2                                                            
         OC    BRPTDAT,PIORPTDT                                                 
         BNZ   MATC8               LAST WAS A REPEAT                            
*                                                                               
         LA    R1,P+05                                                          
*--->    MVC   0(19,R1),=C'YOU HAVE MATERIALS.'                                 
         MVC   0(L'PP@IOM1,R1),PP@IOM1                                          
         LR    R3,R1               SCAN BACKWARDS FOR NON-SPACE                 
         LA    R3,L'PP@IOM1(R3)                                                 
MATC     CLI   0(R3),C' '                                                       
         BH    MATC1                                                            
         BCT   R3,MATC                                                          
*                                                                               
MATC1    MVI   1(R3),C','                                                       
         LA    R1,132(R1)                                                       
*--->    MVC   0(19,R1),=C'SEE ORIGINAL ORDER.'                                 
         MVC   0(L'PP@IOM2,R1),PP@IOM2                                          
         MVI   MATSW,C'R'                                                       
         B     MATC9B              SKIP TO PRINTING                             
*                                                                               
MATC2    DS    0H                                                               
         CLI   SVRPTDT,C'0'                                                     
         BL    MATC4                                                            
         MVI   MATSW,C'N'                                                       
         BE    MATCOMX                                                          
*                                                                               
*                                  RPT DATE IN QCARD                            
         MVI   MATSW,C'R'                                                       
         GOTO1 DATCON,DMCB,(0,SVRPTDT),(3,BRPTDAT)                              
         B     MATC8                                                            
*                                  SEARCH FOR REPEAT                            
MATC4    DS    0H                                                               
         MVC   W(32),PLISREC       SAVE KEY OF POSSIBLE COMMENT RECORD          
*                                                                               
         MVC   SVTSP,TSPUB-STATDIF SAVE TSPUB                                   
         MVC   SVMATOPT,MATOPT     AND MATERIALS REPEAT OPTION                  
         XC    SVPLKEY,SVPLKEY                                                  
         MVI   PUBLCK,C'N'                                                      
         OC    PUBPLSH,PUBPLSH     SEE IF PUB HAS PUBLISHER                     
         BZ    MATC4X                                                           
*                                                                               
* MUST READ TO SEE IF I/O REPEAT CHECK IS ON                                    
*                                                                               
         XC    KEY,KEY                                                          
         MVC   KEY(3),TSAGY-STATDIF                                             
         MVI   KEY+3,X'11'                                                      
         MVC   KEY+4(4),PUBPLSH                                                 
         BAS   RE,MCHIGH                                                        
         BAS   RE,MCTSTERR                                                      
         CLC   KEY(9),KEYSAVE                                                   
         BE    *+6                                                              
         DC    H'0'                MISSING PUBLISHER (REP)                      
*                                                                               
         XC    SPACEND+150(200),SPACEND+150                                     
*                                                                               
         BAS   RE,MCGET                                                         
         BAS   RE,MCTSTERR                                                      
         LA    R2,SPACEND                                                       
         TM    PREPSTAT-PREPREC(R2),X'02'                                       
         BNO   MATC4X                                                           
         MVI   PUBLCK,C'Y'         PERFORM PUBLISHER I/O REPEAT CHECK           
         MVI   MATOPT,C'N'         OVERIDE MAT REPEAT OPTION                    
*                                                                               
* REAL MATOPT WAS SAVED AND WILL BE RESTORED SEARCH FOR REPEAT                  
*                                                                               
MATC4X   DS    0H                                                               
         XC    BRPTDAT,BRPTDAT                                                  
*                                                                               
MATC4XA  DS    0H                                                               
         XC    KEY,KEY                                                          
         MVC   KEY(3),TSAGY-STATDIF                                             
         MVI   KEY+3,X'20'                                                      
         MVC   KEY+4(6),TSCLT-STATDIF                                           
         MVC   KEY+10(4),TSPUB-STATDIF                                          
         CLI   MATOPT,C'Y'         RPT CHECK ACROSS EDITS + ZONES               
         BE    MATC4B                                                           
         CLI   MATOPT,C'E'         RPT CHECK ACROSS EDITS + ZONES               
         BE    MATC4B                                                           
         MVC   KEY+14(1),TSPUB+4-STATDIF                                        
         CLI   MATOPT,C'Z'         ONLY ONE ZONE                                
         BE    MATC4B                                                           
         MVC   KEY+15(1),TSPUB+5-STATDIF                                        
*                                                                               
MATC4B   DS    0H                                                               
         BAS   RE,MCHIGH                                                        
         B     *+8                                                              
*                                                                               
MATC5    DS    0H                                                               
         BAS   RE,MCSEQ                                                         
         BAS   RE,MCTSTERR                                                      
         TM    KEY+25,X'80'        BYPASS DELETES                               
         BNZ   MATC5                                                            
         CLC   KEY(14),KEYSAVE                                                  
         BNE   MATC8                                                            
         CLI   MATOPT,C'Y'         CROSS ZONE AND EDIT                          
         BE    MATC5D                                                           
         CLI   MATOPT,C'E'         CROSS ZONE AND EDIT                          
         BE    MATC5D                                                           
         CLI   MATOPT,C'Z'                                                      
         BNE   MATC5C                                                           
         CLC   KEY+14(1),TSPUB+4-STATDIF                                        
         BNE   MATC8                                                            
         B     MATC5D                                                           
*                                                                               
MATC5C   DS    0H                                                               
         CLC   KEY+14(2),TSPUB+4-STATDIF                                        
         BNE   MATC8                                                            
*                                                                               
MATC5D   DS    0H                                                               
         CLC   KEY+16(3),SVFRSTDT                                               
         BNL   MATC5                                                            
         BAS   RE,MCGET                                                         
         BAS   RE,MCTSTERR                                                      
*                                                                               
         LA    R2,SPACEND          TEST CURRENT JOB SAME                        
         CLI   MULTIAD,C'Y'                                                     
         BNE   MATC5F                                                           
         CLC   PBDJOB-PBUYREC(6,R2),MATJOB                                      
         BNE   MATC5               NO - BYPASS                                  
         B     MATC5X                                                           
*                                                                               
MATC5F   CLC   PBDJOB-PBUYREC(6,R2),TSJOB-STATDIF                               
         BNE   MATC5               NO - BYPASS                                  
*                                                                               
MATC5X   DS    0H                                                               
         LA    R2,33(R2)                                                        
*                                                                               
MATC6    DS    0H                                                               
         MVI   ELCODE,X'70'                                                     
         BAS   RE,MCNXTEL                                                       
         BNE   MATC7               EOR                                          
         USING PIOELEM,R2                                                       
*                                                                               
         CLI   MULTIAD,C'Y'                                                     
         BNE   MATC6F                                                           
         CLC   PIOJOB,MATJOB                                                    
         BNE   MATC6                                                            
         B     MATC6X                                                           
*                                                                               
MATC6F   DS    0H                                                               
         CLC   PIOJOB,TSJOB-STATDIF                                             
         BNE   MATC6                                                            
*                                                                               
MATC6X   DS    0H                                                               
         CLC   PIODATE,BTODAY      SKIP IGNORE DATE TEST                        
         BE    *+14                IF PREVIOUS IO DONE TODAY                    
         CLC   PIODATE,SVIGNDAT                                                 
         BNL   MATC6                                                            
         CLC   PIOIDATE,BRPTDAT                                                 
         BNH   MATC6                                                            
         BAS   RE,CHKDATE                                                       
         CLI   BYTE,0              MEANS INSERTION NO LONGER ON FILE            
         BE    MATC6               DON'T SAVE THIS DATE                         
         MVC   BRPTDAT,PIOIDATE                                                 
         MVC   RPTPUB,KEY+10       SAVE REPEAT PUB ALSO                         
*                                  USE MOST RECENT INSERT DATE                  
         B     MATC6                                                            
*                                                                               
MATC7    DS    0H                                                               
         B     MATC5                                                            
*                                                                               
MATC8    DS    0H                                                               
         CLI   PUBLCK,C'Y'         SEE IF PERFORMING PUBLISHER CHECK            
         BNE   MATC8M                                                           
         OC    SVPLKEY,SVPLKEY     SEE IF FIRST TIME                            
         BNZ   MATC8B                                                           
         XC    KEY,KEY                                                          
         MVC   KEY(2),=C'0P'       PUBLISHER POINTERS                           
         MVC   KEY+2(3),TSAGY-STATDIF                                           
         MVC   KEY+5(4),PUBPLSH                                                 
         BAS   RE,MCPHIGH                                                       
         BAS   RE,MCTSTERR                                                      
         B     MATC8E                                                           
*                                                                               
MATC8B   MVC   KEY,SVPLKEY                                                      
         BAS   RE,MCPHIGH                                                       
MATC8D   BAS   RE,MCPSEQ                                                        
*                                                                               
MATC8E   CLC   KEY(10),KEYSAVE                                                  
         BNE   MATC8H                                                           
         CLC   KEY+10(6),SVTSP     SEE IF ORIGINAL TSPUB                        
         BE    MATC8D              SKIP SINCE I DID IT FIRST                    
         MVC   TSPUB-STATDIF(6),KEY+10                                          
         MVC   SVPLKEY,KEY         SAVE THIS KEY                                
         B     MATC4XA                                                          
*                                                                               
MATC8H   MVC   MATOPT,SVMATOPT                                                  
         MVC   TSPUB-STATDIF(6),SVTSP                                           
*                                                                               
MATC8M   DS    0H                                                               
*                                                                               
* RESTORE COMMENT RECORD ONLY NECESSARY ON-LINE                                 
* BECAUSE OF SHARED WORK AREA                                                   
*                                                                               
         CLC   W(32),PLISREC                                                    
         BE    MATC8X                                                           
         CLC   W(3),RCSVAGY                                                     
         BNE   MATC8X                                                           
         CLI   W+3,X'40'                                                        
         BNE   MATC8X                                                           
         MVC   KEY(32),W                                                        
         BAS   RE,MCHIGH                                                        
         BAS   RE,MCGET                                                         
         BAS   RE,MCTSTERR                                                      
*                                                                               
MATC8X   DS    0H                                                               
         MVI   MATSW,C'N'                                                       
         OC    BRPTDAT,BRPTDAT                                                  
         BZ    MATCOMX                                                          
         MVC   WORK(12),SPACES     JUST IN CASE SOMETHING LEFT IN WORK          
*                                                                               
         GOTO1 DATCON,DMCB,(3,BRPTDAT),(5,WORK)                                 
*                                                                               
* SET REPEAT DATE ID ADLIST WHEN MULTIAD=Y                                      
*                                                                               
         CLI   MULTIAD,C'Y'                                                     
         BNE   MATC8W                                                           
         L     RE,AADLIST          BE SURE I HAVE THE ADDRESS                   
         LTR   RE,RE                                                            
         BZ    MATC8W              IF NOT JUST SKIP                             
*                                                                               
MATC8P   DS    0H                                                               
         CLC   1(6,RE),MATJOB                                                   
         BNE   MATC8S                                                           
         MVC   7(3,RE),BRPTDAT     SAVE REPEAT DATE                             
         MVC   10(6,RE),RPTPUB     AND REPEAT PUB                               
         B     MATC8W                                                           
*                                                                               
MATC8S   LA    RE,16(RE)           NEXT AD                                      
         OC    0(7,RE),0(RE)                                                    
         BNZ   MATC8P                                                           
*                                                                               
* ADCODE NOT IN TABLE - SHOULD NEVER HAPPEN                                     
* HOWEVER DON'T DIE - JUST LEAVE REPEAT DATE BLANK                              
*                                                                               
MATC8W   DS    0H                                                               
         CLI   MYLANG,C'F'                                                      
         BNE   *+8                                                              
         BAS   RE,FRDATE                                                        
*                                                                               
         MVI   MATSW,C'R'                                                       
*                                                                               
MATC9    DS    0H                                                               
         LA    R1,P+05                                                          
*--->    MVC   0(19,R1),=C'YOU HAVE MATERIALS.'                                 
         MVC   0(L'PP@IOM1,R1),PP@IOM1                                          
         LA    R1,132(R1)                                                       
*                                                                               
         CLI   MYLANG,C'F'         SEE IF FRENCH                                
         BE    MATF9               GO TO SPECIAL CODE FOR REPEAT                
*                                                                               
         MVC   0(17,R1),=C'AD IS A REPEAT OF'                                   
         MVC   18(8,R1),WORK                                                    
         LA    R3,26(R1)                                                        
         CLI   TSPBFREQ-STATDIF,C'M'                                            
         BNE   MATC9A                                                           
         LA    R3,24(R1)                                                        
         MVC   21(6,R1),23(R1)                                                  
*                                                                               
MATC9A   DS    0H                                                               
         MVC   0(11,R3),=C' INSERTION'                                          
         CLI   QMEDIA,C'O'                                                      
         BNE   *+10                                                             
         MVC   0(11,R3),=C' POSTING  '                                          
*                                                                               
* NOTE - FRENCH CODE RETURNS TO MATC9AX                                         
*                                                                               
MATC9AX  DS    0H                                                               
         OC    RPTPUB,RPTPUB       CHECK FOR REPEAT PUB                         
         BZ    MATC9B                                                           
         CLC   RPTPUB,SVTSP        DON'T DISPLAY IF SAME AS ORIGINAL            
         BE    MATC9B                                                           
         GOTO1 AIOPRNT                                                          
*--->    MVC   P+05(3),=C'FOR'                                                  
         MVC   P+05(L'PP@FOR),PP@FOR                                            
*                                                                               
         LA    R3,P+10                                                          
         CLI   MYLANG,C'F'         SEE IF FRENCH                                
         BE    *+8                                                              
         LA    R3,P+9                                                           
*                                                                               
         GOTO1 APUBEDIT,DMCB,RPTPUB,0(R3)                                       
*                                                                               
         MVC   SVPUBKEY,PUBREC     SAVE KEY OF PUB IN PUBREC                    
*                                  SINCE I NEED TO TO REREAD IT                 
         XC    PUBREC(50),PUBREC                                                
         XC    KEY,KEY                                                          
         MVC   KEY(1),TSMED-STATDIF                                             
         MVC   KEY+1(6),RPTPUB                                                  
         MVC   KEY+7(2),TSAGY-STATDIF                                           
         BAS   RE,MCPHIGH                                                       
         B     *+8                                                              
FP2      BAS   RE,MCPSEQ                                                        
         CLC   KEY(7),KEYSAVE                                                   
         BNE   FP8                                                              
         CLC   KEY+7(2),KEYSAVE+7                                               
         BE    FP4                                                              
         CLC   KEY+7(2),=C'ZZ'                                                  
         BNE   FP2                                                              
         CLI   PAGYPROF+16,C'0'    TEST DEFAULT                                 
         BE    FP8                 NO                                           
*                                                                               
FP4      DS    0H                                                               
         CLI   KEY+9,X'81'                                                      
         BNE   FP6                                                              
         CLI   PUBREC,0                                                         
         BNE   FP10                                                             
         LA    R0,PUBREC                                                        
         ST    R0,AREC                                                          
         BAS   RE,MCPGET                                                        
         B     FP2                                                              
*                                                                               
FP6      DS    0H                                                               
         B     FP2                                                              
*                                                                               
FP8      DS    0H                                                               
         CLI   PUBREC,0                                                         
         BE    FPNO                                                             
*                                                                               
FP10     DS    0H                                                               
         LA    R1,P+27                                                          
FP12     CLI   0(R1),C' '                                                       
         BH    FP15                                                             
         BCT   R1,FP12                                                          
*                                                                               
FP15     MVC   2(20,R1),PUBNAME                                                 
         MVC   134(20,R1),PUBZNAME IN PSECOND                                   
         GOTO1 AIOPRNT                                                          
*                                                                               
* MUST REREAD PUB THAT WAS IN PUBREC                                            
*                                                                               
         XC    KEY,KEY                                                          
         MVC   KEY(20),SVPUBKEY                                                 
         BAS   RE,MCPHIGH                                                       
         CLC   KEY(10),KEYSAVE                                                  
         BE    *+6                                                              
         DC    H'0'                SOMETHING VERY WRONG                         
         LA    R0,PUBREC                                                        
         ST    R0,AREC                                                          
         BAS   RE,MCPGET                                                        
         B     MATCOMX                                                          
*                                                                               
FPNO     DC    H'0'                SOMETHING VERY WRONG                         
*                                                                               
MATC9B   DS    0H                                                               
         GOTO1 AIOPRNT                                                          
         B     MATCOMX                                                          
         EJECT                                                                  
*                                                                               
* SPECIAL FENCH CODE                                                            
*                                                                               
MATF9    DS    0H                                                               
         MVC   0(L'PP@IOM3,R1),PP@IOM3                                          
*                                                                               
         LA    R3,L'PP@IOM3(R1)    SCAN BACKWARDS FOR NON-SPACE                 
         DS    0H                                                               
         MVC   1(2,R3),=C'L'''                                                  
         MVC   3(L'PP@INSER,R3),PP@INSER                                        
         CLI   QMEDIA,C'O'                                                      
         BNE   MATF9B                                                           
         MVC   3(L'PP@INSER,R3),SPACES                                          
         MVC   3(09,R3),=C'AFFICHAGE'                                           
*                                                                               
MATF9B   DS    0H                                                               
         LA    R3,L'PP@INSER+3(R3) SCAN BACKWARDS FOR NON-SPACE                 
*                                                                               
MATF9D   CLI   0(R3),C' '                                                       
         BH    MATF9E                                                           
         BCT   R3,MATF9D                                                        
*                                                                               
MATF9E   MVC   2(8,R3),WORK                                                     
         LA    R3,10(R3)                                                        
         CLI   TSPBFREQ-STATDIF,C'M'                                            
         BNE   MATF9F                                                           
         AHI   R3,-10              BACK-UP R3                                   
         MVC   5(6,R3),7(R3)       MOVE /YEAR AND SPACES                        
*                                                                               
MATF9F   DS    0H                                                               
         GOTO1 AIOPRNT                                                          
         B     MATCOMX                                                          
*                                                                               
MATCOMX  DS    0H                                                               
*                                                                               
JUMPXIT1 XIT1                                                                   
         EJECT                                                                  
*                                                                               
* SEE IF INSERTION DATE STILL ON FILE BEFORE USING IT AS A REPEAT DATE          
*                                                                               
CHKDATE  NTR1                                                                   
         MVI   BYTE,0                                                           
         MVC   MATSVKEY,KEY                                                     
         XC    KEY,KEY                                                          
         MVC   KEY(16),MATSVKEY                                                 
         MVC   KEY+16(3),PIOIDATE                                               
         BAS   RE,MCHIGH                                                        
         CLC   KEY(19),KEYSAVE                                                  
         BNE   CHKDATX                                                          
         MVI   BYTE,1                                                           
*                                                                               
CHKDATX  MVC   KEY,MATSVKEY                                                     
         BAS   RE,MCHIGH           MUST RESTORE SEQ READ                        
         J     JUMPXIT1                                                         
         EJECT                                                                  
*                                                                               
MCHIGH   LA    R0,DMRDHI                                                        
         MVC   KEYSAVE,KEY                                                      
         B     MCDIR                                                            
*                                                                               
MCSEQ    LA    R0,DMRSEQ                                                        
*                                                                               
MCDIR    NTR1                                                                   
*                                                                               
MCDIR1   DS    0H                                                               
         GOTO1 DATAMGR,DMCB,(DMINBTS,(R0)),PRTDIR,KEY,KEY                       
*                                                                               
         CLI   KEY+25,X'FF'        BYPASS FF DELETES                            
         BNE   MCDIR2                                                           
         LA    R0,DMRSEQ                                                        
         B     MCDIR1                                                           
MCDIR2   DS    0H                                                               
         B     MCDMX                                                            
*                                                                               
MCGET    NTR1                                                                   
         LA    R0,SPACEND                                                       
         GOTO1 DATAMGR,DMCB,(DMINBTS,GETREC),PRTFILE,KEY+27,(R0),DMWORK         
*                                                                               
         AP    MATCNT,=P'1'                                                     
         CP    MATCNT,=P'100'                                                   
         BL    MCDMX                                                            
*                                                                               
* ON EVERY 100TH BUY UNLOCK LOCKTAB                                             
*                                                                               
         GOTO1 DATAMGR,DMCB,=C'DMUNLK',PRTFILE                                  
         ZAP   MATCNT,=P'0'                                                     
*                                                                               
MCDMX    J     JUMPXIT1                                                         
         EJECT                                                                  
*                                                                               
* PUBFILE DATAMGR ROUTINES                                                      
*                                                                               
MCPHIGH  LA    R0,DMRDHI                                                        
         MVC   KEYSAVE,KEY                                                      
         B     MCPDIR                                                           
*                                                                               
MCPSEQ   LA    R0,DMRSEQ                                                        
*                                                                               
MCPDIR   NTR1                                                                   
*                                                                               
MCPDIR1  DS    0H                                                               
         GOTO1 DATAMGR,DMCB,(DMINBTS,(R0)),PUBDIR,KEY,KEY                       
*                                                                               
         CLI   KEY+25,X'FF'        BYPASS FF DELETES                            
         BNE   MCPDIR2                                                          
         LA    R0,DMRSEQ                                                        
         B     MCPDIR1                                                          
MCPDIR2  DS    0H                                                               
         B     MCPDMX                                                           
*                                                                               
MCPGET   NTR1                                                                   
         LA    R0,PUBREC                                                        
         GOTO1 DATAMGR,DMCB,(DMINBTS,GETREC),PUBFILE,KEY+27,(R0),DMWORK         
         B     MCPDMX                                                           
MCPDMX   J     JUMPXIT1                                                         
         EJECT                                                                  
*                                                                               
MCNXTEL  DS    0H                                                               
         SR    R0,R0                                                            
         IC    R0,1(R2)                                                         
         AR    R2,R0                                                            
         CLI   0(R2),0                                                          
         BE    MCNXTELX                                                         
         CLC   ELCODE,0(R2)                                                     
         BER   RE                                                               
         B     MCNXTEL+2                                                        
MCNXTELX LTR   R2,R2                                                            
         BR    RE                                                               
*                                                                               
MCTSTERR DS    0H                                                               
         CLI   ERR,0                                                            
         BNE   MCTSTE2                                                          
         MVC   BYTE,DMOUTBTS                                                    
         NC    BYTE,DMCB+8                                                      
         BZR   RE                                                               
         MVC   ERR+1,DMCB+8                                                     
MCTSTE2  DS    0H                                                               
         B     MATCOMX                                                          
         EJECT                                                                  
*                                                                               
FRDATE   NTR1                      CONVERT DATE IN WORK (MMMDD/YY)              
         LA    R1,DATETAB                                                       
FRDAT2   CLI   0(R1),X'FF'         END OF TABLE                                 
         BNE   *+6                                                              
         DC    H'0'                SOMEHTING VERY WRONG                         
*                                                                               
         CLC   WORK(3),0(R1)                                                    
         BE    FRDAT10                                                          
         LA    R1,6(R1)                                                         
         B     FRDAT2                                                           
*                                  TO FRENCH                                    
FRDAT10  MVC   WORK(3),3(R1)                                                    
         J     JUMPXIT1                                                         
*                                                                               
DATETAB  DC    C'JANJAN'                                                        
         DC    C'FEBFEV'                                                        
         DC    C'MARMAR'                                                        
         DC    C'APRAVR'                                                        
         DC    C'MAYMAI'                                                        
         DC    C'JUNJUN'                                                        
         DC    C'JULJUL'                                                        
         DC    C'AUGAOU'                                                        
         DC    C'SEPSEP'                                                        
         DC    C'OCTOCT'                                                        
         DC    C'NOVNOV'                                                        
         DC    C'DECDEC'                                                        
         DC    X'FF'                                                            
         LTORG                                                                  
*                                                                               
MATSVKEY DS    CL32                                                             
MATCNT   DS    PL5                                                              
MYLANG   DS    CL1                                                              
SVPUBKEY DS    CL20                ONLY NEED 20 BYTES                           
SVTSP    DS    CL6                                                              
SVMATOPT DS    CL1                 SAVED "REAL" MATOPT                          
PUBLCK   DS    CL1                 PUBLISHER CHECK                              
SVPLKEY  DS    CL32                                                             
*                                                                               
         PRINT OFF                                                              
*                                                                               
* DICTIONARY FOR PP72MAT AND NLMATCOM                                           
*                                                                               
       ++INCLUDE PPIOMATD                                                       
         EJECT                                                                  
*                                                                               
       ++INCLUDE T413WKA                                                        
         EJECT                                                                  
*                                                                               
       ++INCLUDE POLFILE                                                        
         EJECT                                                                  
*                                                                               
       ++INCLUDE T413WKB                                                        
         EJECT                                                                  
*                                                                               
         PRINT ON                                                               
*                                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'036NLMATCOM  04/14/03'                                      
         END                                                                    
