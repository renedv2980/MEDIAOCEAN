*          DATA SET PP72JOB    AT LEVEL 006 AS OF 09/03/02                      
*CATALP PP72JOB                                                                 
         TITLE 'CHANGE LOG'                                                     
*                                                                               
* BPLA   06/01   ADD PRODUCTION JOB                                             
*                ADD PRODUCTION HOUSE (A REP)                                   
*                                                                               
* BPLA   06/00   NOW LIVE SOURCE (WAS PP72JOBB)                                 
*                CATALP AND ++INCLUDE PP72WORK RESTORED                         
*                                                                               
* SMYE  02/20/98  CODE FOR PUB LIST                                             
*           ***** NOTE: PUB LIST RECORD BEING READ INTO PUBREC ABOVE            
*                                                                               
         TITLE 'PRINTPAK - JOB RECORD LIST PROGRAM - PP075'                     
JOBLST   CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 0,**JOBLST**                                                     
*                                                                               
         USING PPWORKD,RA                                                       
         USING IOWORKD,R8                                                       
         L     RC,PPFILEC                                                       
         USING PPFILED,RC,R9                                                    
*                                                                               
         MVI   MAXLINES,45                                                      
*                                                                               
         LA    RE,1(R8)                                                         
         LA    RE,4095(RE)                                                      
         USING IOWORKD+4096,RE                                                  
*                                                                               
         CLI   PINSREC+16,X'FF'                                                 
         BE    JB4                                                              
         OC    PINSREC+16(6),PINSREC+16                                         
         BZ    JB4                                                              
*                                                                               
* BUILD LIST OF PUB INSTRUCTION OVERRIDE RECORDS                                
*                                                                               
         L     R2,PBCOUNT                                                       
         LA    R2,1(R2)                                                         
         ST    R2,PBCOUNT                                                       
         C     R2,PBMAX                                                         
         BH    EXIT                                                             
         MHI   R2,6                                                             
         LA    R2,PBLIST-6(R2)                                                  
         MVC   0(6,R2),PINSREC+16                                               
         B     EXIT                                                             
*                                                                               
JB4      DS    0H                  BUILD PRINT LINE                             
         LA    R4,2                COUNT PRINT LINES NEEDED                     
         CLC   PJOBKPRD,=C'ZZZ'                                                 
         BNE   *+8                                                              
         LA    R4,1(R4)                                                         
         CLI   PJOBBLCC,C' '       SEE IF I HAVE A BILLING CONTACT              
         BNH   *+8                                                              
         LA    R4,1(R4)            YES - NEED ANOTHER LINE                      
         CLI   PJOBFILT,C' '       SEE IF I HAVE A FILTER                       
         BNH   *+8                                                              
         LA    R4,1(R4)            YES - NEED ANOTHER LINE                      
         CLI   PJOBFSI,C'Y'        SEE FREE STANDING INSERT                     
         BNE   *+8                                                              
         LA    R4,1(R4)            YES - NEED ANOTHER LINE                      
         CLI   PJOBPLIS,C' '       SEE IF I HAVE A PUB LIST                     
         BNH   *+8                                                              
         LA    R4,1(R4)            YES - NEED ANOTHER LINE                      
         CLI   PJOBPJOB,C' '       SEE IF I HAVE PRODUCTION JOB                 
         BNH   *+8                                                              
         LA    R4,1(R4)            YES - NEED ANOTHER LINE                      
         CLI   PJOBPROD,C' '       SEE IF I HAVE PRODUCTION HOUSE               
         BNH   *+8                                                              
         LA    R4,1(R4)            YES - NEED ANOTHER LINE                      
         L     R1,PBCOUNT                                                       
         LA    R1,2(R1)                                                         
         SR    R0,R0                                                            
         D     R0,=F'3'                                                         
         AR    R4,R1                                                            
         CLC   PBCOUNT,PBMAX                                                    
         BNH   *+8                                                              
         LA    R4,1(R4)                                                         
         CLI   PINSREC+16,X'FF'                                                 
         BE    *+8                                                              
         LA    R4,1(R4)                                                         
*                                                                               
         DROP  RE                  FOR ADDRESSING PINSREC                       
*                                                                               
         STC   R4,LINENEED                                                      
         LA    R2,P                                                             
         USING JLLIND,R2                                                        
         MVC   JLJOB,PJOBKJOB                                                   
         MVC   JLCAP1,PJOBCAP1                                                  
         MVC   JLCAP2,PJOBCAP2                                                  
         MVC   JLCPY,PJOBCPY                                                    
         MVC   JLSPC,PJOBSPC                                                    
         CLI   PJOBSPC,C' '                                                     
         BH    JB6                                                              
         CLI   PJOBKMED,C'N'                                                    
         BNE   JB6                                                              
         MVC   W,SPACES                                                         
         LA    R4,W                                                             
         MVI   0(R4),C'0'                                                       
         LA    R0,1                                                             
         ZAP   DUB,PJOBTUNS                                                     
         BZ    *+8                                                              
         BAS   RE,JBEDL                                                         
         AR    R4,R0                                                            
         MVI   0(R4),C'L'                                                       
         CLI   PJOBUIND,C'I'                                                    
         BNE   *+14                                                             
         MVC   0(2,R4),=C'IN'                                                   
         LA    R4,1(R4)                                                         
*                                                                               
         CLI   PJOBPRM,C' '                                                     
         BNH   *+14                                                             
         MVC   2(1,R4),PJOBPRM                                                  
         MVI   3(R4),C'C'                                                       
*                                                                               
         MVC   JLSPC,W                                                          
*                                                                               
         LA    R4,W                                                             
         MVC   W,SPACES                                                         
         CP    PJOBCOLS,=P'0'                                                   
         BE    JB6                                                              
         ZAP   DUB,PJOBUNTS                                                     
         BZ    JB6                                                              
         MVI   0(R4),C'('                                                       
         LA    R4,1(R4)                                                         
         BAS   RE,JBEDL                                                         
         AR    R4,R0                                                            
         MVI   1(R4),C'X'                                                       
         LA    R4,3(R4)                                                         
         ZAP   DUB,PJOBCOLS                                                     
         BAS   RE,JBEDL                                                         
         AR    R4,R0                                                            
         MVI   0(R4),C')'                                                       
         MVC   JLSPC2,W                                                         
*                                                                               
JB6      DS    0H                                                               
         OC    PJOBSTA,PJOBSTA                                                  
         BZ    JB8                                                              
*****    GOTO1 DTCNV,DMCB,(1,PJOBSTA),(3,JLSTART)                               
         GOTO1 DATCON,DMCB,(3,PJOBSTA),(5,JLSTART)                              
*                                                                               
JB8      DS    0H                                                               
         OC    PJOBEND,PJOBEND                                                  
         BZ    JB10                                                             
*******  GOTO1 DTCNV,DMCB,(1,PJOBEND),(3,JLEND)                                 
         GOTO1 DATCON,DMCB,(3,PJOBEND),(5,JLEND)                                
*                                                                               
JB10     DS    0H                                                               
         MVC   JLSIG,PJOBSIG                                                    
*                                                                               
         CLC   PJOBKPRD,=C'ZZZ'                                                 
         BNE   JB12                                                             
         CLC   JLCAP2(25),SPACES                                                
         BNE   JB10C                                                            
         CLC   PJOBALO+32(3),SPACES      SEE IF IT WILL FIT                     
         BH    JB10C                                                            
         MVC   JLCAP2(11),=C'ALLOCATION='                                       
         MVC   JLCAP2+12(32),PJOBALO                                            
         BAS   RE,JBPRT                                                         
         B     JB12K                                                            
*                                                                               
JB10C    BAS   RE,JBPRT                                                         
         MVC   P+10(11),=C'ALLOCATION='                                         
         MVC   P+22(47),PJOBALO                                                 
         BAS   RE,JBPRT                                                         
         B     JB12K                                                            
*                                                                               
*        HERE IF I DIDN'T PRINT ALLOCATION                                      
JB12     DS    0H                                                               
         CLI   PJOBBLCC,C' '     SEE IF BILLING CONTACT GIVEN                   
         BNH   JB12D             FINALLY GO PRINT THE LINE                      
         CLC   JLCAP2,SPACES     SEE IF I CAN USE PSECOND                       
         BNE   JB12C                                                            
         CLC   PJOBBLCC+26(4),SPACES     SEE IF I CAN FIT IT                    
         BH    JB12C                     NO - MUST USE NEXT LINE                
         MVC   JLCAP2(16),=C'BILLING CONTACT='                                  
         MVC   JLCAP2+17(26),PJOBBLCC                                           
         BAS   RE,JBPRT                                                         
         B     JB13                                                             
*                                                                               
JB12C    BAS   RE,JBPRT                                                         
         MVC   P+10(16),=C'BILLING CONTACT='                                    
         MVC   P+27(30),PJOBBLCC                                                
         CLI   PJOBFILT,C' '                                                    
         BNH   JB12CX                                                           
         MVC   PSECOND+10(07),=C'FILTER='                                       
         MVC   PSECOND+17(6),PJOBFILT                                           
         MVI   PJOBFILT,C' '            SO I WON'T REDO AT JB13                 
         B     JB12D                                                            
*                                                                               
JB12CX   CLI   PJOBFSI,C'Y'                                                     
         BNE   JB12D                                                            
         MVC   PSECOND+10(23),=C'FREE STANDING INSERT AD'                       
         MVI   PJOBFSI,C' '                                                     
JB12D    BAS   RE,JBPRT                                                         
         B     JB13                                                             
*                                                                               
JB12K    DS    0H                HERE IF I JUST PRINTED ALLOCATION              
         CLI   PJOBBLCC,C' '     SEE IF BILLING CONTACT GIVEN                   
         BNH   JB13                                                             
         MVC   P+10(16),=C'BILLING CONTACT='                                    
         MVC   P+27(30),PJOBBLCC                                                
         CLI   PJOBFILT,C' '                                                    
         BNH   JB12KX                                                           
         MVC   PSECOND+10(07),=C'FILTER='                                       
         MVC   PSECOND+17(6),PJOBFILT                                           
         MVI   PJOBFILT,C' '            SO I WON'T REDO AT JB13                 
         BAS   RE,JBPRT                                                         
         B     JB12M                                                            
*                                                                               
JB12KX   CLI   PJOBFSI,C'Y'                                                     
         BNE   JB12M                                                            
         MVC   PSECOND+10(23),=C'FREE STANDING INSERT AD'                       
         MVI   PJOBFSI,C' '                                                     
         BAS   RE,JBPRT                                                         
*                                                                               
JB12M    DS    0H                                                               
*                                                                               
JB13     DS    0H                                                               
         CLI   PJOBFILT,C' '            SEE IF I NEED TO DO FILTER              
         BNH   JB13C                                                            
         MVC   P+10(07),=C'FILTER='                                             
         MVC   P+17(6),PJOBFILT                                                 
         BAS   RE,JBPRT                                                         
*                                                                               
JB13C    DS    0H                                                               
         CLI   PJOBFSI,C'Y'            SEE IF FSI AD                            
         BNE   JB13C3                                                           
         MVC   P+10(23),=C'FREE STANDING INSERT AD'                             
         BAS   RE,JBPRT                                                         
*                                                                               
JB13C3   DS    0H                                                               
         CLI   PJOBPJOB,C' '      SEE I HAVE A PRODUCTION JOB                   
         BNH   JB13D                                                            
         MVC   P+10(15),=C'PRODUCTION JOB='                                     
         MVC   P+26(6),PJOBPJOB                                                 
         BAS   RE,JBPRT                                                         
*                                                                               
*    ******* NOTE: PROD HSE RECORD BEING READ INTO PUBREC *********             
*                                                                               
JB13D    DS    0H                                                               
         CLI   PJOBPROD,C' '           SEE IF PRODUCTION HOUSE                  
         BNH   JB13F                                                            
         MVC   P+10(17),=C'PRODUCTION HOUSE='                                   
         MVC   P+28(4),PJOBPROD                                                 
         MVC   P+35(22),=C'**RECORD NOT ON FILE**'                              
         MVC   PPGKEY,KEY          SAVE PPG'S KEY                               
         XC    KEY+3(22),KEY+3                                                  
         MVI   KEY+3,X'11'         REP                                          
         MVC   KEY+4(4),PJOBPROD                                                
JB13D2   GOTO1 HIGH                                                             
         CLC   KEY(9),KEYSAVE                                                   
         BE    JB13D4              RECORD FOUND                                 
         B     JB13DX                                                           
*                                                                               
JB13D4   LA    RE,PUBREC           READ PUB LIST INTO PUBREC                    
         ST    RE,AREC                                                          
         GOTO1 GETPRT                                                           
         XC    P+35(22),P+25                                                    
         MVC   P+35(30),PUBREC+35   REALLY PREPNAME                             
JB13DX   BAS   RE,JBPRT                                                         
         MVC   KEY(64),PPGKEY      RESTORE PPG'S KEYS                           
         GOTO1 HIGH                AND SEQUENCE                                 
*                                                                               
*                                                                               
*    ******* NOTE: PUB LIST RECORD BEING READ INTO PUBREC *********             
*                                                                               
JB13F    DS    0H                                                               
         CLI   PJOBPLIS,C' '           SEE IF PUB LIST                          
         BNH   JB13X                                                            
         MVC   P+10(9),=C'PUB LIST='                                            
         MVC   P+20(3),PJOBPLIS                                                 
         MVC   P+25(22),=C'**RECORD NOT ON FILE**'                              
         MVC   PPGKEY,KEY          SAVE PPG'S KEY                               
         XC    KEY+7(18),KEY+7                                                  
         MVI   KEY+3,X'17'         PUB LIST RECORD                              
         MVC   KEY+7(3),PJOBPLIS                                                
JB13G    GOTO1 HIGH                                                             
         CLC   KEY(10),KEYSAVE                                                  
         BE    JB13J               RECORD FOUND                                 
         CLC   KEYSAVE+4(3),=C'ZZZ'   WAS I LOOKING FOR ALL CLIENTS ?           
         BE    JB13K                  YES - RECORD NOT FOUND                    
         MVC   KEY,KEYSAVE                                                      
         MVC   KEY+4(3),=C'ZZZ'                                                 
         B     JB13G               TRY FOR "ZZZ" CLIENT CODE LIST REC           
JB13J    LA    RE,PUBREC           READ PUB LIST INTO PUBREC                    
         ST    RE,AREC                                                          
         GOTO1 GETPRT                                                           
         XC    P+25(22),P+25                                                    
         MVC   P+25(20),PUBREC+40    PUB LIST DESCRIPTION                       
JB13K    BAS   RE,JBPRT                                                         
         MVC   KEY(64),PPGKEY      RESTORE PPG'S KEYS                           
         GOTO1 HIGH                AND SEQUENCE                                 
*                                                                               
JB13X    DS    0H                                                               
         OC    PBCOUNT,PBCOUNT                                                  
         BZ    JB22                                                             
*                                  PRINT OVERRIDE PUBS                          
         MVC   P+10(30),=C'SPECIAL INSTRUCTIONS EXIST FOR'                      
         L     R4,PBCOUNT                                                       
         C     R4,PBMAX                                                         
         BNH   *+8                                                              
         L     R4,PBMAX                                                         
         LA    R5,PBLIST                                                        
JB14     DS    0H                                                               
         LA    R7,3                                                             
         LA    R6,P+43                                                          
JB16     DS    0H                                                               
         GOTO1 APUBEDIT,DMCB,(R5),(R6)                                          
*                                                                               
         LA    R5,6(R5)                                                         
         LA    R6,18(R6)                                                        
         BCT   R4,*+8                                                           
         B     JB18                                                             
         BCT   R7,JB16                                                          
         BAS   RE,JBPRT                                                         
         B     JB14                                                             
JB18     DS    0H                                                               
         BAS   RE,JBPRT                                                         
JB20     DS    0H                                                               
         CLC   PBMAX,PBCOUNT                                                    
         BNL   JB22                                                             
         MVC   P+43(10),=C'AND OTHERS'                                          
         BAS   RE,JBPRT                                                         
*                                                                               
JB22     DS    0H                                                               
         XC    PBCOUNT(256),PBCOUNT                                             
*                                                                               
         LA    RE,1(R8)                                                         
         LA    RE,4095(RE)                                                      
         USING IOWORKD+4096,RE                                                  
         CLI   PINSREC+16,X'FF'                                                 
         BE    JB50                                                             
         MVC   P+10(L'NOFFWRN),NOFFWRN                                          
         DROP  RE                                                               
         BAS   RE,JBPRT                                                         
JB50     DS    0H                                                               
         BAS   RE,JBPRT                                                         
         B     EXIT                                                             
         EJECT                                                                  
*                                  PRINT                                        
JBPRT    NTR1                                                                   
*                                                                               
         MVI   RCSUBPRG,75         JOB LIST                                     
         SR    RE,RE                                                            
         SR    RF,RF                                                            
         IC    RE,LINE                                                          
         IC    RF,LINENEED                                                      
         AR    RE,RF                                                            
         STC   RE,BYTE                                                          
         CLC   BYTE,MAXLINES                                                    
         BL    *+8                                                              
         MVI   FORCEHED,C'Y'                                                    
         MVI   LINENEED,0                                                       
         GOTO1 REPORT                                                           
*                                                                               
         J     EXIT                                                             
*                                                                               
JBEDL    EDIT  (P8,DUB),(5,(R4)),ALIGN=LEFT                                     
         BR    RE                                                               
*                                                                               
EXIT     XIT1                                                                   
*                                                                               
NOFFWRN  DC    C'*** WARNING - NO ''ALL PUBS'' INSTRUCTION RECORD PRESEX        
               NT ***'                                                          
*                                                                               
PBMAX    DC    F'42'                                                            
PBCOUNT  DC    F'0'                                                             
PBLIST   DC    XL252'00'                                                        
*                                                                               
         LTORG                                                                  
         EJECT                                                                  
*                                  PRINT LINE DSECT                             
JLLIND   DSECT                                                                  
         DS    CL2                                                              
JLJOB    DS    CL6                                                              
         DS    CL2                                                              
JLCAP1   DS    CL25                                                             
         DS    CL1                                                              
JLCPY    DS    CL17                                                             
         DS    CL2                                                              
JLSPC    DS    CL17                                                             
         DS    CL2                                                              
JLSTART  DS    CL8                                                              
         DS    CL1                                                              
JLEND    DS    CL8                                                              
         DS    CL2                                                              
JLSIG    DS    CL12                                                             
         ORG   JLLIND+132                                                       
*                                  SECOND LINE                                  
         DS    CL10                                                             
JLCAP2   DS    CL25                                                             
         DS    CL20                                                             
JLSPC2   DS    CL17                                                             
*                                                                               
       ++INCLUDE PP72WORK                                                       
*                                                                               
         PRINT OFF                                                              
*                                                                               
       ++INCLUDE PPMODEQU                                                       
*                                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'006PP72JOB   09/03/02'                                      
         END                                                                    
