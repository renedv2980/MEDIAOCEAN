*          DATA SET PRWRIJOB   AT LEVEL 038 AS OF 10/17/18                      
*CATALP PRWRIJOB                                                                
         TITLE 'PRWRIJOB - CHANGE LOG'                                          
         TITLE 'PRWRIJOB - JOB RECORD FIELDS - ENTRY POINTS'                    
         ENTRY IJOB                                                             
         ENTRY OJOB                                                             
PRWRIJOB CSECT                                                                  
         TITLE 'PRWRIJOB - JOB RECORD FIELDS - INPUT - IJOB'                    
***********************************************************************         
*                                                                     *         
*        ROUTINE TO EXTRACT INFO FOR AD FOUND IN BUY OR BILL RECORD   *         
*                                                                     *         
*NTRY                                                                 *         
*        R2==>  DRIVER INPUT AREA                                     *         
*        R8==>  SPOOL WORKAREA                                        *         
*        R9==>  PRINTWRITER WORKAREA                                  *         
*        RA==>  DRIVER GLOBAL AREA                                    *         
*        RC==>  GENCON WORKAREA                                       *         
*                                                                     *         
*        GLARGS+1  C'C' - AD CAPTION                                  *         
*                  C'F' - AD FILTER                                   *         
*                  C'H' - PRODUCTION HOUSE REP CODE                   *         
*                  C'S' - SPACE DESCRIPTION                           *         
*                  C'A' - AD ID                                       *         
*                  C'%' - AD CODE SHARE                               *         
*                  C'#' - AD COPY NUMBER                              *         
*                  C'K' - BILL CONTACT                                *         
*                                                                     *         
*        GLARGS+2  C'C' - AD CODE AND CAPTION                         *         
*                                                                     *         
*EXIT    FOR REP CODE - FORMAT USED BY OADRREP                        *         
*                                                                     *         
***********************************************************************         
         SPACE 2                                                                
         PRINT NOGEN                                                            
IJOB     DS    0D                                                               
         NMOD1 300,**IJOB                                                       
*                                                                               
         USING SPOOLD,R8           ESTABLISH SPOOL WORKAREA                     
         USING SYSD,R9             ESTABLISH PRINTWRITER WORKAREA               
         USING GLOBALD,RA          EWSTABLISH DRIVER WORKAREA                   
*                                                                               
         LR    R7,RC               SAVE WORKING STORAGE POINTER                 
*                                                                               
         L     RC,GLAWORKD         RESET WORKING STORAGE POINTER                
         USING GEND,RC                                                          
*                                                                               
         OC    IJOBARGS,IJOBARGS   SAVE ARGUMENTS OF FIRST JOB KYWD             
         BNZ   *+10                                                             
         MVC   IJOBARGS,GLARGS                                                  
*                                                                               
         L     R6,AIO1             ESTABLISH BUY RECORD                         
         USING PBUYRECD,R6                                                      
*                                                                               
         CLI   PBUYKRCD,PBUYKIDQ   SKIP IF BUY RECORD NOT IN CORE               
         BNE   IJOBBUYN                                                         
*                                                                               
         MVC   IJOBWMED,PBUYKMED   SAVE MEDIA   CODE                            
         MVC   IJOBWCLT,PBUYKCLT   SAVE CLIENT  CODE                            
         MVC   IJOBWPRD,PBUYKPRD   SAVE PRODUCT CODE                            
         MVC   IJOBWJOB,PBDJOB     SAVE JOB     CODE                            
         MVC   IJOBJBSV,PBDJOB     SAVE ORIGINAL JOB CODE                       
*                                                                               
         B     IJOBINI                                                          
*                                                                               
IJOBBUYN DS    0H                                                               
*                                                                               
         USING PBILLRCD,R6         ESTABLISH BILL RECORD                        
*                                                                               
         CLI   PBILKRCD,PBILKIDQ   SKIP IF NOT A BILL RECORD IN CORE            
         BNE   IJOBBLLN                                                         
*                                                                               
         MVC   IJOBWMED,PBILKMED   SAVE MEDIA   CODE                            
         MVC   IJOBWCLT,PBILKCLT   SAVE CLIENT  CODE                            
         MVC   IJOBWPRD,PBILKPRD   SAVE PRODUCT CODE                            
         XC    IJOBWJOB,IJOBWJOB   INIT JOB     CODE                            
         MVC   IJOBJBSV,IJOBWJOB   SAVE ORIGINAL JOB CODE                       
*                                                                               
         SR    R0,R0                                                            
         LA    RF,PBILLEL          POINT TO FIRST ELEMENT                       
         USING PBILOTH,RF          ESTABLISH OTHERS ELEMENT                     
*                                                                               
IJOBBLLL DS    0H                                                               
*                                                                               
         CLI   PBILOTH,0                                                        
         BE    IJOBBLLD            DONE IF END OF RECORD FOUND                  
*                                                                               
         CLI   PBILOTH,X'09'       SEARCH FOR OTHERS ELEMENT                    
         BE    IJOBBLLF                                                         
*                                                                               
IJOBBLLC DS    0H                                                               
*                                                                               
         IC    R0,PBILOTH+1        ELEMENT LENGTH                               
         AR    RF,R0               POINT TO NEXT ELEMENT                        
         B     IJOBBLLL                                                         
*                                                                               
IJOBBLLF DS    0H                                                               
*                                                                               
         MVC   IJOBWJOB,PBILLJOB   SAVE JOB     CODE                            
         MVC   IJOBJBSV,PBILLJOB   SAVE ORIGINAL JOB CODE                       
*                                                                               
IJOBBLLD DS    0H                                                               
*                                                                               
         B     IJOBINI                                                          
*                                                                               
IJOBBLLN DS    0H                                                               
*                                                                               
         B     IJOBX               CAN NOT DO ANYTHING                          
*                                                                               
*        SET UP BXLE REGISTERS IF FIRST TIME FOR PRODUCT                        
*                                                                               
IJOBINI  DS    0H                                                               
*                                                                               
         ICM   R5,15,PBAADBF       START OF ADD BUFFER                          
         BZ    IJOBX               NO BUFFER PROVIDED - SKIP FIELD              
*                                                                               
         USING ADBFD,R5            ESTABLISH AD BUFFER                          
*                                                                               
         CLC   ADBFPRD,IJOBWPRD    RE-INITIALIZE AD BUFFER ON PRD CHG           
         BNE   *+10                                                             
         CLC   ADBFCLT,IJOBWCLT    OR CLIENT CHANGE                             
         BNE   *+10                                                             
         CLC   ADBFMED,IJOBWMED    OR MEDIA CHANGE                              
         BE    IJOBINIX                                                         
*                                                                               
         LA    R3,ADBFENT          POINT TO FIRST ENTRY IN BUFFER               
         LA    RE,ADBFLN           LENGTH OF BUFFER ENTRY                       
         LR    RF,R3                                                            
         BCTR  RF,0                END OF BUFFER                                
         ST    R3,ADBFBXLE         BXLE REGS FOR AD BUFFER                      
         STM   RE,RF,ADBFBXLE+4    BXLE REGS FOR AD BUFFER                      
*                                                                               
         USING ADBFENT,R3          ESTABLISH ENTRY                              
*                                                                               
         XC    ADBFENT(ADBFHDLN),ADBFENT  INIT FIRST BUFFER ENTRY               
         XC    ADBFJBKY,ADBFJBKY                                                
         XC    ADBFJBHD,ADBFJBHD                                                
         XC    ADBFJBSB(PJSUBMAX),ADBFJBSB   FIRST SUBCODE                      
*                                                                               
         MVC   ADBFMED,IJOBWMED    RESET BUFFER CONTROL FIELDS                  
         MVC   ADBFCLT,IJOBWCLT                                                 
         MVC   ADBFPRD,IJOBWPRD                                                 
*                                                                               
IJOBINIX DS    0H                                                               
*                                                                               
         TM    PBBYOPT2,PBBYCPYQ   SKIP IF NOT COPY SPLITS                      
         BNO   *+12                                                             
         CLI   IJOBJBSV,C'!'       IF NOT MASTER ADCODE                         
         BE    *+14                                                             
         ZAP   PBSUBSHR,=P'100000'    DEFAULT TO 100% SHARE                     
         B     IJOBMSTX                                                         
*                                                                               
         CLC   IJOBARGS,GLARGS     IF NOT FIRST JOB KEYWORD                     
         BE    *+14                                                             
         MVC   IJOBWJOB,IJOBSUB       USE CURRENT SUBCODE                       
         B     IJOBMSTX                                                         
*                                                                               
         SP    CTR,=P'1'                                                        
         B     *+8                                                              
         DC    H'0'                                                             
CTR      DC    PL2'9'                                                           
*                                                                               
         OC    IJOBSUB,IJOBSUB     IF FIRST TIME FOR THIS MASTER                
         BNZ   IJOBSUB1                                                         
*                                                                               
         MVC   IJOBSUB,IJOBWJOB       SET SUB TO MASTER                         
         ZAP   PBSUBSHR,=P'0'         NO SHARE FOR MASTER                       
         B     IJOBMSTX               AND USE MASTER                            
*                                                                               
IJOBSUB1 DS    0H                                                               
*                                                                               
         LA    R1,IJOBSUBS         POINT TO LIST OF SUBCODES                    
         USING PJSUBEL,R1          ESTABLISH PUB SUBCODE ELM                    
*                                                                               
         CLC   IJOBSUB,IJOBJBSV    IF MASTER USED LAST TIME                     
         BE    IJOBSUBF               USE FIRST SUBCODE                         
*                                                                               
IJOBSUBL DS    0H                                                               
*                                                                               
         OC    0(PJSUBMAX,R1),0(R1) IF END OF SUBCODES                          
         BNZ   *+6                                                              
         DC    H'0'                    SHOULDN'T HAPPEN                         
*                                                                               
         CLC   IJOBSUB,PJSUBCOD    SKIP SUB DOESN'T MATCH CURRENT SUB           
         BNE   IJOBSUBC                                                         
*                                                                               
         LA    R1,PJSUBMAX(R1)     BUMP TO NEXT SUBCODE                         
         B     IJOBSUBF                                                         
*                                                                               
IJOBSUBC DS    0H                                                               
*                                                                               
         LA    R1,PJSUBMAX(R1)     BUMP TO NEXT IN LIST                         
         B     IJOBSUBL                                                         
*                                                                               
IJOBSUBF DS    0H                                                               
*                                                                               
         OC    0(PJSUBMAX,R1),0(R1) IF PAST END OF SUBCODES                     
         BNZ   IJOBSUBG                                                         
*                                                                               
         MVC   IJOBSUB,IJOBJBSV       START OVER AT MASTER                      
         ZAP   PBSUBSHR,=P'0'         WITH 0% SHARE                             
*                                                                               
         B     IJOBMSTX                                                         
*                                                                               
IJOBSUBG DS    0H                                                               
*                                                                               
         MVC   IJOBWJOB,PJSUBCOD   PROCESS THIS SUBCODE                         
         MVC   IJOBSUB,PJSUBCOD    SAVE ENTRY IN LIST                           
         ZAP   PBSUBSHR,PJSUBPCT   SET SUB PCT SHARE                            
*                                                                               
         LA    R1,PJSUBMAX(R1)     POINT TO NEXT SUBCODE ELEMENT                
*                                                                               
         OC    0(PJSUBMAX,R1),0(R1) IF THERE IS ANOTHER ONE                     
         BZ    *+8                                                              
         OI    PBRTCODE,PBRTMORQ      SET TO RETURN FOR MORE DATA               
*                                                                               
IJOBMSTX DS    0H                                                               
*                                                                               
         DROP  R1                                                               
*                                                                               
*        SEARCH AD BUFFER TO SEE IF AD CAME UP BEFORE                           
*                                                                               
IJOBSRCH DS    0H                                                               
*                                                                               
         OC    IJOBWJOB,IJOBWJOB   SKIP IF NO JOB CODE FOUND                    
         BZ    IJOBSRCX                                                         
*                                                                               
         L     R3,ADBFBXLE          LOAD BUFFER BXLE REGS                       
         LM    RE,RF,ADBFBXLE+4                                                 
*                                                                               
         CR    RF,R3               BUFFER IS EMPTY IF END BEFORE START          
         BNH   IJOBSRCX              GO ADD AD TO BUFFER                        
*                                                                               
         USING ADBFENT,R3          ESTABLISH BUFFER ENTRY                       
*                                                                               
IJOBSRCL DS    0H                                                               
*                                                                               
         CLC   IJOBWJOB,PJOBKJOB-PJOBKEY+ADBFJBKY FIND AD CODE IN TABLE         
         BE    IJOBSRCF            FOUND                                        
*                                                                               
IJOBSRCC DS    0H                                                               
*                                                                               
         BXLE  R3,RE,IJOBSRCL                                                   
*                                                                               
         B     IJOBSRCD            NOT FOUND                                    
*                                                                               
IJOBSRCF DS    0H                  AD IN BUFFER                                 
*                                                                               
*        SAVE SUBCODES IF THIS IS MASTER ADCODE                                 
*                                                                               
         TM    PBBYOPT2,PBBYCPYQ   SKIP IF NOT COPY SPLIT                       
         BNO   IJOBADDX                                                         
*                                                                               
         CLI   IJOBJBSV,C'!'       IF NOT MASTER CODE                           
         BE    *+20                                                             
         XC    IJOBSUB,IJOBSUB        INIT CURRENT SUBCODE SAVEAREA             
         ZAP   PBSUBSHR,=P'100000'    100% SHARE                                
         B     IJOBADDX                                                         
*                                                                               
         CLI   IJOBWJOB,C'!'       IF ACTUAL MASTER                             
         BNE   IJOBADDX                                                         
*                                                                               
*        SAVE SUB CODES FOR MASTER CODE                                         
*                                                                               
         LA    R0,11               MAX SUB CODES                                
         LA    RF,ADBFJBSB         BUFFER SUB CODES                             
         LA    RE,IJOBSUBS         SUBS SAVEAREA                                
*                                                                               
         MVC   0(PJSUBMAX,RE),0(RF) SAVE SUB CODES                              
         LA    RE,PJSUBMAX(RE)                                                  
         LA    RF,PJSUBMAX(RF)                                                  
         BCT   R0,*-14                                                          
*                                                                               
         OI    PBRTCODE,PBRTMORQ      SET TO RETURN FOR MORE DATA               
         MVC   IJOBSUB,IJOBWJOB       SET SUB TO MASTER                         
         ZAP   PBSUBSHR,=P'0'         0% SHARE                                  
*                                                                               
         B     IJOBADDX                                                         
*                                                                               
IJOBSRCD DS    0H                                                               
*                                                                               
*        AD NOT IN BUFFER                                                       
*                                                                               
         L     R0,PBAADBF          BUFFER START                                 
         A     R0,PBLADBF          PLUS BUFFER LENGTH                           
*                                                                               
         LA    R1,0(RE,R3)         END OF NEW BUFFER ELEMENT                    
*                                                                               
         CR    R1,R0               ELEMENT MUST FIT IN BUFFER                   
         BL    *+6                                                              
         DC    H'0'                INCREASE SIZE OF AD BUFFER                   
*                                                                               
         USING ADBFENT,R3          ESTABLISH BUFFER ENTRY                       
*                                                                               
         XC    ADBFENT(ADBFHDLN),ADBFENT  INIT NEW BUFFER ENTRY                 
         XC    ADBFJBKY,ADBFJBKY                                                
         XC    ADBFJBHD,ADBFJBHD                                                
         XC    ADBFJBSB(PJSUBMAX),ADBFJBSB   FIRST SUBCODE                      
*                                                                               
IJOBSRCX DS    0H                                                               
*                                                                               
*        READ JOB RECORD FOR NEW AD                                             
*                                                                               
IJOBADD  DS    0H                                                               
*                                                                               
         OC    IJOBWJOB,IJOBWJOB   SKIP IF NO JOB CODE FOUND                    
         BZ    IJOBADDX                                                         
*                                                                               
         XC    KEY,KEY                                                          
         LA    R4,KEY              ESTABLISH KEYAREA AS                         
         USING PJOBRECD,R4           JOB RECORD KEY                             
*                                                                               
         MVC   PJOBKAGY,PBAGY      SET AGENCY                                   
         MVC   PJOBKMED,ADBFMED    SET MEDIA                                    
         MVI   PJOBKRCD,PJOBKIDQ   SET TO READ JOB RECORD                       
         MVC   PJOBKCLT,ADBFCLT    SET CLIENT                                   
         MVC   PJOBKPRD,ADBFPRD    SET PRODUCT                                  
         MVC   PJOBKJOB,IJOBWJOB   SET JOB CODE                                 
*                                                                               
         GOTO1 HIGH                READ POINTER                                 
*                                                                               
         CLC   PJOBKEY,KEYSAVE     IF RECORD NOT FOUND                          
         BNE   IJOBRSTR               SKIP                                      
*                                                                               
         MVC   4(4,R7),AIO         SAVE CURRENT AIO ADDRESS                     
*                                                                               
         LA    RF,80(R7)           SET I/O AREA ADDRESS                         
         ST    RF,AIO                                                           
*                                                                               
         GOTO1 GETREC              READ RECORD                                  
         BE    *+6                                                              
         DC    H'0'                NO ERRORS TOLERATED                          
*                                                                               
         L     R4,AIO              POINT TO FOUND RECORD                        
         MVC   AIO,4(R7)           RESTORE CURRENT AIO ADDRESS                  
*                                                                               
*        ADD AD TO BUFFER                                                       
*                                                                               
         MVC   ADBFDISK,KEY+27     SAVE JOB RECORD DISK ADDRESS                 
*                                                                               
         ICM   RF,15,PBUTLA                                                     
         BZ    *+10                                                             
         MVC   ADBFSE,4(RF)        SAVE JOB RECORD SE                           
*                                                                               
         MVC   ADBFJBKY,PJOBKEY    SAVE JOB RECORD KEY                          
         MVC   ADBFJBHD,PJOBELEM   SAVE JOB HEADER ELEMENT                      
*                                                                               
         L     RF,ADBFENA          NEW END OF BUFFER                            
         A     RF,ADBFLEN                                                       
         ST    RF,ADBFENA                                                       
*                                                                               
*        SAVE SUBCODES IF THIS IS MASTER ADCODE                                 
*                                                                               
         TM    PBBYOPT2,PBBYCPYQ   SKIP IF NOT COPY SPLIT                       
         BNO   *+12                                                             
         CLI   PJOBKJOB,C'!'       IF NOT MASTER CODE                           
         BE    *+20                                                             
         XC    IJOBSUB,IJOBSUB        INIT CURRENT SUBCODE SAVEAREA             
         ZAP   PBSUBSHR,=P'100000'    100% SHARE                                
         B     IJOBSBX                                                          
*                                                                               
         LA    R1,ADBFJBSB         START OF SUBCODES                            
         LA    RE,IJOBSUBS         START OF CURRENT SUBCODES                    
*                                                                               
         XC    0(PJSUBMAX,R1),0(R1) INIT FIRST AREA                             
         XC    0(PJSUBMAX,RE),0(RE) INIT FIRST AREA                             
*                                                                               
         LA    R6,PJOBELEM         POINT TO FIRST ELM IN RECORD                 
         SR    RF,RF                                                            
*                                                                               
IJOBSBLP DS    0H                                                               
*                                                                               
         USING PJSUBEL,R6          ESTABLISH SUB ELEMENT CODE                   
*                                                                               
         CLI   PJSUBCDE,0          DONE AT END OF RECORD                        
         BE    IJOBSBDN                                                         
*                                                                               
         CLI   PJSUBCDE,PJSUBIDQ   FIND SUBCODE ELEMENT                         
         BNE   IJOBSBCN                                                         
*                                                                               
         IC    RF,PJSUBLEN         GET ELEMENT LENGTH                           
         BCTR  RF,0                DECREMENT FOR EXECUTE                        
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R1),PJSUBEL     SAVE SUBCODE ELEMENT                         
*                                                                               
         OI    PBRTCODE,PBRTMORQ      SET TO RETURN FOR MORE DATA               
*                                                                               
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   0(0,RE),PJSUBEL     SAVE SUBCODE ELEMENT                         
*                                                                               
         LA    R1,PJSUBMAX(R1)     NEXT SUBCODE ELM AREA                        
         XC    0(PJSUBMAX,R1),0(R1) INIT AREA                                   
*                                                                               
         LA    RE,PJSUBMAX(RE)     NEXT SUBCODE ELM AREA                        
         XC    0(PJSUBMAX,RE),0(RE) INIT AREA                                   
*                                                                               
IJOBSBCN DS    0H                                                               
*                                                                               
         IC    RF,PJSUBLEN         ELEMENT LENGTH                               
         LA    R6,0(RF,R6)         NEXT ELEMENT                                 
         B     IJOBSBLP                                                         
*                                                                               
IJOBSBDN DS    0H                                                               
*                                                                               
         BRAS  RE,SUBGET           MAKE SURE SUBCODES IN BUFFER                 
*                                                                               
IJOBSBX  DS    0H                                                               
*                                                                               
IJOBRSTR DS    0H                  RESTORE FILE POSITIONING                     
*                                                                               
         MVC   KEY,IOKEYSVE        RESTORE LAST USED POINTER                    
         OI    DMINBTS,X'08'       PASS BACK DELETED RECORDS                    
*                                                                               
         GOTO1 HIGH                RE-POINT FILE                                
*                                                                               
IJOBADDX DS    0H                                                               
*                                                                               
*        RETURN DESIRED DATA TO DRIVER                                          
*                                                                               
IJOBDATA DS    0H                                                               
*                                                                               
*        PASS FILTER TO DRIVER                                                  
*                                                                               
IJOBFLT  DS    0H                                                               
*                                                                               
         CLI   GLARGS+1,C'F'       SKIP IF AD FILTER NOT DESIRED                
         BNE   IJOBFLTN                                                         
*                                                                               
         OC    IJOBWJOB,IJOBWJOB   DONE IF NO JOB CODE FOUND                    
         BZ    IJOBFLTX                                                         
*                                                                               
         MVI   0(R2),L'PJOBFILT    SET DATA LENGTH                              
         MVC   1(L'PJOBFILT,R2),PJOBFILT-PJOBELEM+ADBFJBHD  AD FILTER           
*                                                                               
IJOBFLTX DS    0H                                                               
*                                                                               
         B     IJOBX                                                            
*                                                                               
IJOBFLTN DS    0H                                                               
*                                                                               
*        PASS AD ID TO DRIVER                                                   
*                                                                               
IJOBAID  DS    0H                                                               
*                                                                               
         CLI   GLARGS+1,C'A'       SKIP IF AD ID NOT DESIRED                    
         BNE   IJOBAIDN                                                         
*                                                                               
         OC    IJOBWJOB,IJOBWJOB   DONE IF NO JOB CODE FOUND                    
         BZ    IJOBAIDX                                                         
*                                                                               
         MVC   0(L'PJOBADID,R2),PJOBADID-PJOBELEM+ADBFJBHD  AD FILTER           
*                                                                               
IJOBAIDX DS    0H                                                               
*                                                                               
         B     IJOBX                                                            
*                                                                               
IJOBAIDN DS    0H                                                               
*                                                                               
*        PASS REPS TO DRIVER                                                    
*                                                                               
IJOBREP  DS    0H                                                               
*                                                                               
         CLI   GLARGS+1,C'H'       SKIP IF NOT A REP FIELD                      
         BE    *+8                                                              
         CLI   GLARGS+1,C'B'       SKIP IF NOT A REP FIELD                      
         BNE   IJOBREPN                                                         
*                                                                               
         OC    IJOBWJOB,IJOBWJOB   IF NO JOB CODE FOUND                         
         BNZ   *+12                                                             
         MVI   0(R2),X'FF'            INDICATE NO REP FOUND                     
         B     IJOBREPX                                                         
*                                                                               
         CLI   GLARGS+1,C'H'       IF PRODUCTION HOUSE                          
         BNE   *+14                                                             
         MVC   IJOBWREP,PJOBPROD-PJOBELEM+ADBFJBHD SAVE REP CODE                
         B     IJOBREP0                                                         
*                                                                               
         CLI   GLARGS+1,C'B'       IF BILLING REP                               
         BNE   *+14                                                             
         MVC   IJOBWREP,PJOBBREP-PJOBELEM+ADBFJBHD SAVE REP CODE                
         B     IJOBREP0                                                         
*                                                                               
         B     IJOBREPN                                                         
*                                                                               
IJOBREP0 DS    0H                                                               
*                                                                               
*        SEARCH BUFFER FOR DISK ADDRESS                                         
*                                                                               
         ICM   RE,15,PBAREPBF      ESTABLISH REP BUFFER                         
         USING REPBUFD,RE                                                       
         BNZ   *+12                                                             
         MVI   0(R2),X'FF'            INDICATE NO REP AVAILABLE                 
         B     IJOBREPX               IF NO BUFFER AVAILABLE                    
*                                                                               
         OC    REPBUFD(REPBUFLN),REPBUFD   IF NO TABLE AVAILABLE                
         BNZ   IJOBREPA                                                         
*                                                                               
         GOTO1 =V(BLDREP)             GO BUILD IT                               
*                                                                               
         ICM   RE,15,PBAREPBF      ESTABLISH REP BUFFER                         
*                                                                               
IJOBREPA DS    0H                                                               
*                                                                               
*        FIND REP IN TABLE                                                      
*                                                                               
IJOBREPL DS    0H                                                               
*                                                                               
         OC    REPBUFD(REPBUFLN),REPBUFD IF END OF TABLE                        
         BNE   *+12                                                             
         MVI   0(R2),X'FF'            INDICATE NO REP AVAILABLE                 
         B     IJOBREPX                                                         
*                                                                               
         CLC   REPBUFN,IJOBWREP       MATCH REP NUMBER                          
         BE    IJOBREPF                                                         
*                                                                               
IJOBREPC DS    0H                                                               
*                                                                               
         LA    RE,REPBUFLN(RE)     BUMP TABLE ENTRY POINTER                     
         B     IJOBREPL                                                         
*                                                                               
IJOBREPF DS    0H                                                               
*                                                                               
         CLI   GLARGS+2,C'C'       IF NUMBER ONLY REQUIRED                      
         BNE   IJOBREP1                                                         
*                                                                               
         MVC   0(4,R2),REPBUFN        RETURN IT                                 
         B     IJOBREPX                                                         
*                                                                               
IJOBREP1 DS    0H                  ELSE                                         
*                                                                               
         MVC   0(10,R2),REPBUFNA         RETURN SHORT NAME                      
         MVC   10(4,R2),REPBUDA          AND REP REC DISK ADDRESS               
         MVI   16(R2),C'R'               REP RECORD TYPE                        
         MVC   17(1,R2),REPBUFSE         PASS SE NUMBER                         
*                                                                               
IJOBREPX DS    0H                                                               
*                                                                               
         B     IJOBX                                                            
*                                                                               
IJOBREPN DS    0H                                                               
*                                                                               
*        CAPTION WITH OR WITHOUT AD CODE                                        
*                                                                               
IJOBCAP  DS    0H                                                               
*                                                                               
         CLI   GLARGS+2,C'C'       SKIP IF NO ADCODE  WANTED                    
         BE    *+8                                                              
         CLI   GLARGS+1,C'C'       OR   IF NO CAPTION WANTED                    
         BNE   IJOBCAPN                                                         
*                                                                               
         OC    IJOBWJOB,IJOBWJOB   IF NO JOB CODE FOUND                         
         BNZ   *+12                                                             
         MVI   0(R2),X'FF'            INDICATE NOT FOUND                        
         B     IJOBCAPX                                                         
*                                                                               
         L     R1,GLADTENT         ESTABLISH INPUT DRIVER ENTRY                 
         USING DRIND,R1                                                         
*                                                                               
         SR    RF,RF                                                            
         IC    RF,DRINLEN          GET INPUT LENGTH                             
*                                                                               
         SH    RF,=H'5'            ALLOW ROOM FOR DISK ADDR & SE                
         BNP   IJOBCAPX            CAN'T CONTINUE                               
*                                                                               
         STC   RF,0(R2)            PASS DISPLACEMENT FOR DISK ADDR              
*                                                                               
         LA    R2,1(R2)            BUMP OUTPUT POINTER                          
         BCTR  RF,0                DECREMENT TOTAL LENGTH AVAILABLE             
*                                                                               
         CLI   GLARGS+2,C'C'       IF ADCODE INCLUDED                           
         BNE   *+10                                                             
         MVC   0(L'PJOBKJOB,R2),PJOBKJOB-PJOBKEY+ADBFJBKY PASS ADCODE           
*                                                                               
         LA    R2,L'PJOBKJOB(R2)   BUMP OUTPUT POINTER                          
*                                                                               
         SH    RF,=Y(L'PJOBKJOB)   ALLOW FOR ROOM ADCODE TAKES                  
         BNP   IJOBCAP2            DONE IF NO ROOM LEFT                         
*                                                                               
         BCTR  RF,0                DECREMENT FOR EXECUTE                        
*                                                                               
         EX    RF,*+8              JUST ENOUGH CAPTION TO SORT ON               
         B     *+10                                                             
         MVC   0(0,R2),PJOBCAP1-PJOBELEM+ADBFJBHD PASS CAPTION                  
*                                                                               
         LA    R2,1(RF,R2)          BUMP OUTPUT POINTER                         
*                                                                               
IJOBCAP2 DS    0H                                                               
*                                                                               
         MVC   0(4,R2),ADBFDISK    PASS DISK ADDRESS                            
         MVC   4(1,R2),ADBFSE      PASS SE                                      
*                                                                               
IJOBCAPX DS    0H                                                               
*                                                                               
         B     IJOBX                                                            
*                                                                               
IJOBCAPN DS    0H                                                               
*                                                                               
*        SPACE DESCRIPTION                                                      
*                                                                               
IJOBSPC  DS    0H                                                               
*                                                                               
         CLI   GLARGS+1,C'S'       SKIP IF NO SPACE WANTED                      
         BNE   IJOBSPCN                                                         
*                                                                               
         OC    IJOBWJOB,IJOBWJOB   IF NO JOB CODE FOUND                         
         BZ    IJOBSPCX               SKIP SPACE DESCRIPTION                    
*                                                                               
         CLC   PJOBSPC-PJOBELEM+ADBFJBHD,SPACES  IF SPACE DESC ENTERED          
         BNH   *+14                                                             
         MVC   0(L'PJOBSPC,R2),PJOBSPC-PJOBELEM+ADBFJBHD     USE IT             
         B     IJOBX                                                            
*                                                                               
         CLI   PJOBKMED-PJOBELEM+ADBFJBHD,C'N' NEWS HAVE ALT SPACE              
         BNE   IJOBX                                                            
*                                  NEWSPAPERS LINES/COLS                        
*                                                                               
         LR    R5,R2               SPACE DESC BUILD POINTER                     
*                                                                               
         CP    PJOBCOLS-PJOBELEM+ADBFJBHD,=P'0' SKIP IF NO COLUMNS              
         BE    IJOBSPC8                                                         
*                                                                               
         ZAP   DUB,PJOBCOLS-PJOBELEM+ADBFJBHD DISPLAY NUMBER OF COLUMNS         
         EDIT  (P8,DUB),(5,0(R5)),ALIGN=LEFT                                    
*                                                                               
         AR    R5,R0               BUMP PTR BY LENGTH USED                      
*                                                                               
         MVI   0(R5),C'X'          ADD SEPARATOR                                
         LA    R5,1(R5)                                                         
*                                                                               
IJOBSPC8 DS    0H                                                               
*                                                                               
         ZAP   DUB,PJOBUNTS-PJOBELEM+ADBFJBHD  COPY NUMBER OF UNITS             
*                                                                               
         CLI   PJOBUIND-PJOBELEM+ADBFJBHD,X'89' LOWERCASE 'I' = INCHES          
         BNE   IJOBSPC2                                                         
*                                                                               
         EDIT  (P8,DUB),(6,0(R5)),2,ALIGN=LEFT       2 DECIMAL PLACES           
         B     IJOBSPC4                                                         
*                                                                               
IJOBSPC2 DS    0H                                                               
*                                                                               
         EDIT  (P8,DUB),(5,0(R5)),ALIGN=LEFT         NO DECIMALS PLACES         
*                                                                               
IJOBSPC4 DS    0H                                                               
*                                                                               
         AR    R5,R0               BUMP PTR BY LENGTH USED                      
*                                                                               
         MVC   0(1,R5),PJOBUIND-PJOBELEM+ADBFJBHD PRINT UNITS                   
*                                                                               
         CLI   PJOBUIND-PJOBELEM+ADBFJBHD,X'89' LOWERCASE 'I' = INCHES          
         BNE   *+8                 INDICATE INCHES IF USED                      
         MVI   0(R5),C'I'                                                       
*                                                                               
IJOBSPCX DS    0H                                                               
*                                                                               
         B     IJOBX               DONE                                         
*                                                                               
IJOBSPCN DS    0H                                                               
*                                                                               
*        RETURN UNIT ADCODE SHARE                                               
*                                                                               
IJOBSHR  DS    0H                                                               
*                                                                               
         CLI   GLARGS+1,C'%'       SKIP IF NOT LOOKING FOR SHARE                
         BNE   IJOBSHRN                                                         
*                                                                               
         TM    PBBYOPT2,PBBYCPYQ   SKIP IF NOT COPY SPLITS                      
         BNO   IJOBSHRX                                                         
*                                                                               
         TP    PBSUBSHR            SKIP IF NO SHARE PRESENT                     
         BNZ   IJOBSHRX                                                         
*                                                                               
         ZAP   0(8,R2),PBSUBSHR                                                 
*                                                                               
IJOBSHRX DS    0H                                                               
*                                                                               
         B     IJOBX               DONE                                         
*                                                                               
IJOBSHRN DS    0H                                                               
*                                                                               
*        RETURN COPY NUMBER                                                     
*                                                                               
IJOBCP#  DS    0H                                                               
*                                                                               
         CLI   GLARGS+1,C'#'       SKIP IF NOT LOOKING FOR COPY NUMBER          
         BNE   IJOBCP#N                                                         
*                                                                               
         MVC   0(17,R2),PJOBCPY-PJOBELEM+ADBFJBHD PASS COPY NUMBER              
*                                                                               
IJOBCP#X DS    0H                                                               
*                                                                               
         B     IJOBX               DONE                                         
*                                                                               
IJOBCP#N DS    0H                                                               
*                                                                               
         CLI   GLARGS+1,C'K'       SKIP IF NOT BILL CONTACT                     
         BNE   IJOBBCNN                                                         
*                                                                               
         MVC   0(30,R2),PJOBBLCC-PJOBELEM+ADBFJBHD PASS BILL CONTACT            
*                                                                               
IJOBBCNX DS    0H                                                               
*                                                                               
         B     IJOBX               DONE                                         
*                                                                               
IJOBBCNN DS    0H                                                               
*                                                                               
*        USE IELM TO EXTRACT DATA                                               
*                                                                               
IJOBELM  DS    0H                                                               
*                                                                               
         OC    IJOBWJOB,IJOBWJOB   DONE IF NO JOB CODE FOUND                    
         BZ    IJOBELMX                                                         
*                                                                               
         GOTO1 =V(IELM),IJOBPRMS,ADBFJBKY    IELM WITH A(JOBREC)                
*                                                                               
IJOBELMX DS    0H                                                               
*                                                                               
         B     IJOBX                                                            
*                                                                               
         DROP  RE                                                               
*                                                                               
IJOBX    DS    0H                                                               
*                                                                               
         XIT1                                                                   
*                                                                               
         LTORG                                                                  
*                                                                               
         DS    CL256               FOR NEW Z/OS PROCESSOR                       
IJOBPRMS DS    6F                  PARAMETER LIST                               
IJOBWMED DS    CL1                 MEDIA   CODE                                 
IJOBWCLT DS    CL3                 CLIENT  CODE                                 
IJOBWPRD DS    CL3                 PRODUCT CODE                                 
IJOBWJOB DS    CL(L'PJOBKJOB)      JOB     CODE                                 
IJOBWREP DS    CL(L'PJOBPROD)      REP     CODE                                 
IJOBJBSV DS    CL(L'PJOBKJOB)      JOB     CODE SAVEAREA                        
IJOBARGS DS    CL16                FIRST JOB KYWD ARGUMENTS SAVEAREA            
IJOBSUB  DS    XL6                 CURRENT SUB CODE                             
IJOBSUBS DS    11XL(PJSUBMAX)      SUB CODES STORAGE AREA                       
*                                                                               
         DROP                                                                   
*                                                                               
         TITLE 'PRWRIJOB - JOB RECORD FIELDS - INPUT - SUBGET'                  
***********************************************************************         
*                                                                     *         
*        ROUTINE TO ADD SUB ADCODES TO AD BUFFER                      *         
*                                                                     *         
*NTRY                                                                 *         
*        R3==>  AD BUFFER MASTER CODE ENTRY                           *         
*        R5==>  AD BUFFER                                             *         
*        R7==>  WORKING STORAGE OF CALLING ROUTINE                    *         
*        R8==>  SPOOL WORKAREA                                        *         
*        R9==>  PRINTWRITER WORKAREA                                  *         
*        RA==>  DRIVER GLOBAL AREA                                    *         
*        RC==>  GENCON WORKAREA                                       *         
*                                                                     *         
*EXIT    SUB ADCODES ADDED TO BUFFER                                  *         
*                                                                     *         
***********************************************************************         
         SPACE 2                                                                
         PRINT NOGEN                                                            
         DS    0D                                                               
SUBGET   NTR1  BASE=*,LABEL=*                                                   
*                                                                               
         USING SPOOLD,R8           ESTABLISH SPOOL WORKAREA                     
         USING SYSD,R9             ESTABLISH PRINTWRITER WORKAREA               
         USING GLOBALD,RA          EWSTABLISH DRIVER WORKAREA                   
         USING GEND,RC                                                          
*                                                                               
         USING ADBFD,R5            ESTABLISH AD BUFFER                          
*                                                                               
         USING ADBFENT,R3          ESTABLISH BUFFER ENTRY                       
*                                                                               
*        SEARCH AD BUFFER TO SEE IF AD CAME UP BEFORE                           
*                                                                               
         LA    R4,ADBFJBSB         POINT TO FIRST SUBCODE OF MASTER             
         USING PJSUBEL,R4          ESTABLISH SUB CODE ELEMENT                   
*                                                                               
         DROP  R3                                                               
*                                                                               
SBGTSUBL DS    0H                                                               
*                                                                               
         OC    PJSUBCDE,PJSUBCDE   DONE AT END OF SUBCODES                      
         BZ    SBGTSUBD                                                         
*                                                                               
SBGTSRCH DS    0H                                                               
*                                                                               
         L     R6,ADBFBXLE          LOAD BUFFER BXLE REGS                       
         LM    RE,RF,ADBFBXLE+4                                                 
*                                                                               
         CR    RF,R6               BUFFER IS EMPTY IF END BEFORE START          
         BNH   SBGTSRCX              GO ADD AD TO BUFFER                        
*                                                                               
         USING ADBFENT,R6          ESTABLISH BUFFER ENTRY                       
*                                                                               
SBGTSRCL DS    0H                                                               
*                                                                               
         CLC   PJSUBCDE,PJOBKJOB-PJOBKEY+ADBFJBKY FIND AD CODE IN TABLE         
         BE    SBGTSUBC            FOUND - SKIP ADDING TO BUFFER                
*                                                                               
SBGTSRCC DS    0H                                                               
*                                                                               
         BXLE  R6,RE,SBGTSRCL                                                   
*                                                                               
SBGTSRCD DS    0H                                                               
*                                                                               
*        AD NOT IN BUFFER                                                       
*                                                                               
         L     R0,PBAADBF          BUFFER START                                 
         A     R0,PBLADBF          PLUS BUFFER LENGTH                           
*                                                                               
         LA    R1,0(RE,R6)         END OF NEW BUFFER ELEMENT                    
*                                                                               
         CR    R1,R0               ELEMENT MUST FIT IN BUFFER                   
         BL    *+6                                                              
         DC    H'0'                INCREASE SIZE OF AD BUFFER                   
*                                                                               
         USING ADBFENT,R6          ESTABLISH BUFFER ENTRY                       
*                                                                               
         XC    ADBFENT(ADBFHDLN),ADBFENT  INIT NEW BUFFER ENTRY                 
         XC    ADBFJBKY,ADBFJBKY                                                
         XC    ADBFJBHD,ADBFJBHD                                                
         XC    ADBFJBSB(PJSUBMAX),ADBFJBSB   FIRST SUBCODE                      
*                                                                               
SBGTSRCX DS    0H                                                               
*                                                                               
*        READ JOB RECORD FOR NEW AD                                             
*                                                                               
SBGTADD  DS    0H                                                               
*                                                                               
         XC    KEY,KEY                                                          
         LA    R2,KEY              ESTABLISH KEYAREA AS                         
         USING PJOBRECD,R2           JOB RECORD KEY                             
*                                                                               
         MVC   PJOBKAGY,PBAGY      SET AGENCY                                   
         MVC   PJOBKMED,ADBFMED    SET MEDIA                                    
         MVI   PJOBKRCD,PJOBKIDQ   SET TO READ JOB RECORD                       
         MVC   PJOBKCLT,ADBFCLT    SET CLIENT                                   
         MVC   PJOBKPRD,ADBFPRD    SET PRODUCT                                  
         MVC   PJOBKJOB,PJSUBCOD   SET JOB CODE                                 
*                                                                               
         GOTO1 HIGH                READ POINTER                                 
*                                                                               
         CLC   PJOBKEY,KEYSAVE     IF RECORD NOT FOUND                          
         BNE   SBGTSUBC               SKIP                                      
*                                                                               
         MVC   4(4,R7),AIO         SAVE CURRENT AIO ADDRESS                     
*                                                                               
         LA    RF,80(R7)           SET I/O AREA ADDRESS                         
         ST    RF,AIO                                                           
*                                                                               
         GOTO1 GETREC              READ RECORD                                  
         BE    *+6                                                              
         DC    H'0'                NO ERRORS TOLERATED                          
*                                                                               
         L     R2,AIO              POINT TO FOUND RECORD                        
         MVC   AIO,4(R7)           RESTORE CURRENT AIO ADDRESS                  
*                                                                               
*        ADD AD TO BUFFER                                                       
*                                                                               
         MVC   ADBFDISK,KEY+27     SAVE JOB RECORD DISK ADDRESS                 
*                                                                               
         ICM   RF,15,PBUTLA                                                     
         BZ    *+10                                                             
         MVC   ADBFSE,4(RF)        SAVE JOB RECORD SE                           
*                                                                               
         MVC   ADBFJBKY,PJOBKEY    SAVE JOB RECORD KEY                          
         MVC   ADBFJBHD,PJOBELEM   SAVE JOB HEADER ELEMENT                      
*                                                                               
SBGTSBX  DS    0H                                                               
*                                                                               
         L     RF,ADBFENA          NEW END OF BUFFER                            
         A     RF,ADBFLEN                                                       
         ST    RF,ADBFENA                                                       
*                                                                               
SBGTSUBC DS    0H                                                               
*                                                                               
         LA    R4,PJSUBMAX(R4)     NEXT SUB CODE ELEMENT                        
         B     SBGTSUBL                                                         
*                                                                               
SBGTSUBD DS    0H                                                               
*                                                                               
SUBGETX  DS    0H                                                               
*                                                                               
         XIT1                                                                   
*                                                                               
         LTORG                                                                  
*                                                                               
         DROP                                                                   
*                                                                               
         TITLE 'PRWRIJOB - JOB RECORD FIELDS - OUTPUT - OJOB'                   
***********************************************************************         
*                                                                     *         
*        ROUTINE TO PRINT DATA FROM JOBREC                            *         
*                                                                     *         
*NTRY                                                                 *         
*        R2==>  DRIVER INPUT AREA                                     *         
*               X'FF' - NO AD FOUND                                   *         
*               XL1   - DISPLACEMENT TO DISK ADDRESS                  *         
*               XL6   - JOB CODE IF ASKED FOR                         *         
*               XL?   - VARIABLE AMOUNT OF CAPTION                    *         
*               XL4   - DISK ADDRESS                                  *         
*               XL1   - SE CODE                                       *         
*        R3==>  DRIVER OUTPUT AREA                                    *         
*        R8==>  SPOOL WORKAREA                                        *         
*        R9==>  PRINTWRITER WORKAREA                                  *         
*        RA==>  DRIVER GLOBAL AREA                                    *         
*        RC==>  GENCON WORKAREA                                       *         
*                                                                     *         
*        GLARGS    X'00'                                              *         
*                  C'C' - CAPTION                                     *         
*        GLARGS+1  C'C' - JOB CODE                                    *         
*                                                                     *         
*EXIT                                                                 *         
*                                                                     *         
***********************************************************************         
         SPACE 2                                                                
         PRINT NOGEN                                                            
OJOB     DS    0D                                                               
         NMOD1 300,**+OJOB                                                      
*                                                                               
         USING SPOOLD,R8           ESTABLISH SPOOL WORKAREA                     
         USING SYSD,R9             ESTABLISH PRINTWRITER WORKAREA               
         USING GLOBALD,RA          EWSTABLISH DRIVER WORKAREA                   
*                                                                               
         LR    R7,RC               SAVE WORKING STORAGE POINTER                 
*                                                                               
         L     RC,GLAWORKD         RESET WORKING STORAGE POINTER                
         USING GEND,RC                                                          
*                                                                               
         MVC   LABLAREA,SPACES     INIT OUTPUT AREA                             
         MVC   CODEAREA,SPACES                                                  
         MVC   NAMEAREA,SPACES                                                  
*                                                                               
         GOTO1 BLDLBLA             FORMAT TITLE                                 
*                                                                               
         CLI   GLARGS+1,C'C'       IF JOB CODE PASSED                           
         BNE   OJOB1                                                            
*                                                                               
         MVC   CODEAREA(6),1(R2)      PRINT IT                                  
*                                                                               
         CLI   0(R2),X'FF'            IF NO CODE AVAILABLE                      
         BNE   *+14                                                             
         MVC   CODEAREA(6),=C'*NONE*'    PRINT 'NONE'                           
         B     OJOBGENX                                                         
*                                                                               
OJOB1    DS    0H                                                               
*                                                                               
         CLI   0(R2),X'FF'         DONE IF NO CODE AVAILABLE                    
         BE    OJOBGENX                                                         
*                                                                               
         CLI   GLARGS,C'C'         DONE IF NO CAPTION WANTED                    
         BNE   OJOBGENX                                                         
*                                                                               
         SR    R1,R1                                                            
         IC    R1,0(R2)            GET DISPLACEMENT OF DISK ADDRESS             
         LA    R1,0(R2,R1)         POINT TO DISK ADDRESS                        
*                                                                               
         OC    0(4,R1),0(R1)       SKIP IF NO DISK ADDRESS                      
         BZ    OJOBGENX                                                         
*                                                                               
         XC    KEY,KEY                                                          
         MVC   KEY+27(4),0(R1)     SET DISK ADDRESS                             
*                                                                               
         L     RF,PBUTLA           POINT TO UTLA                                
         ZIC   R0,4(RF)            SAVE CURRENT SE NUMBER                       
*                                                                               
         CLI   4(R1),0             SKIP IF NO SE NUMBER PASSED                  
         BE    *+10                                                             
         MVC   4(1,RF),4(R1)       CHANGE SE NUMBER FOR FILES                   
*                                                                               
         ST    R7,AIO              READ INTO WORKAREA                           
         OI    DMINBTS,X'08'       PASS DELETED RECORDS                         
*                                                                               
         GOTO1 GETREC                                                           
*                                                                               
         L     RF,PBUTLA           POINT TO UTLA                                
         STC   R0,4(RF)            RESTORE CURRENT SE NUMBER                    
*                                                                               
         L     R4,AIO              ESTABLISH FOUND RECORD                       
         USING PJOBREC,R4                                                       
*                                                                               
         MVC   NAMEAREA(L'PJOBCAP1),PJOBCAP1   PRINT CAPTION                    
*                                                                               
         CLI   MYLTYP,C'H'         EXIT IF HEAD OR MIDLINE                      
         BE    *+8                                                              
         CLI   MYLTYP,C'M'         EXIT IF HEAD OR MIDLINE                      
         BE    OJOBGENX                                                         
*                                                                               
*        ** WARNING : THIS PART ASSUMES JOB                                     
*        -------   IS NOT A HEADLINE OR                                         
*                     MIDLINE                                                   
*                                                                               
         LR    R1,R3               POINT TO START OF PRINT AREA                 
*                                                                               
         CLI   GLARGS+1,C'C'       IF ADCODE WANTED                             
         BNE   *+14                                                             
         MVC   0(L'PJOBKJOB,R1),PJOBKJOB   PRINT IT                             
         LA    R1,L'PJOBKJOB+1(R1)         AND BUMP OUTPUT POINTER              
*                                                                               
         CLI   GLARGS,C'C'         DONE IF NO CAPTION WANTED                    
         BNE   OJOBGENX                                                         
*                                                                               
         MVC   0(L'PJOBCAP1,R1),PJOBCAP1                                        
         MVC   198(L'PJOBCAP2,R1),PJOBCAP2                                      
*                                                                               
         B     OJOBX               PRINTAREA OKAY AS IS                         
*                                                                               
OJOBGENX DS    0H                                                               
*                                                                               
         GOTO1 GENOUTA             GENERAL EXIT ROUTINE                         
*                                                                               
OJOBX    DS    0H                                                               
*                                                                               
         XIT1                                                                   
*                                                                               
         LTORG                                                                  
*                                                                               
         DROP                                                                   
         TITLE   'PRWRIJOB DSECTS / STORAGE'                                    
* PRWRIWORKD                                                                    
         PRINT OFF                                                              
       ++INCLUDE PRWRIWORKD                                                     
         PRINT ON                                                               
* INCLUDE DRGLOBAL                                                              
         PRINT OFF                                                              
       ++INCLUDE DRGLOBAL                                                       
         PRINT ON                                                               
* DRIVETABLE                                                                    
         PRINT OFF                                                              
       ++INCLUDE DRIVETABLE                                                     
         PRINT ON                                                               
* DRINTRECD2                                                                    
         PRINT OFF                                                              
       ++INCLUDE DRINTRECD2                                                     
         PRINT ON                                                               
* DDSPOOLD                                                                      
         PRINT OFF                                                              
       ++INCLUDE DDSPOOLD                                                       
         PRINT ON                                                               
* DDSPLWORKD                                                                    
         PRINT OFF                                                              
       ++INCLUDE DDSPLWORKD                                                     
         PRINT ON                                                               
* PRGENFILE                                                                     
         PRINT OFF                                                              
       ++INCLUDE PRGENFILE                                                      
         PRINT ON                                                               
* PRGLOBEQUS                                                                    
         PRINT OFF                                                              
       ++INCLUDE PRGLOBEQUS                                                     
         PRINT ON                                                               
PRWRIJOB CSECT                                                                  
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'038PRWRIJOB  10/17/18'                                      
         END                                                                    
