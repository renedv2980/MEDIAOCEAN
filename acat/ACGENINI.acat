*          DATA SET ACGENINI   AT LEVEL 056 AS OF 08/13/01                      
*CATALP ACGENINI                                                                
ACGENINI TITLE '- GENERAL INITIALIZATION'                                       
***********************************************************************         
* NTRY: R9=A(GLOBAL W/S)                                              *         
*       P1=A(CONTROLLER ADDRESSES 2)                                  *         
*       P2=DISPLACEMENT TO MIXED CASE DICTIONARY OUTPUT FROM BSDICT   *         
*       P3=3 CHARACTER PROGRAM NAME TO GET PROFILES                   *         
*       P4=0                                                          *         
* EXIT: CC=LOW IF ERROR                                               *         
*       CC=HIGH IF FIRST TIME CALL                                    *         
*       CC=EQUAL IF NOT FIRST TIME AND NO ERROR                       *         
*       P4=2 CHARS LEDGER, NONSPACE IF MISSING                        *         
***********************************************************************         
ACGENINI CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 IWORKL,**ACGI**,RR=RE                                            
         USING IWORKD,RC           RC=A(LOCAL WORKING STORAGE)                  
         USING WORKD,R9            R9=A(GLOBAL WORKING STORAGE)                 
         MVC   IPARMS,0(R1)                                                     
         LA    R7,12(R1)                                                        
         ST    R7,IPLEDG                                                        
         MVC   0(2,R7),=CL2' '                                                  
         SPACE 1                                                                
***********************************************************************         
* INITIALISE ADDRESSES                                                *         
***********************************************************************         
         SPACE 1                                                                
INITAD   ST    RE,BCRELO                                                        
         L     RF,4(RD)            SAVE ROOT'S RD VALUE                         
         ST    RF,BCSVRD                                                        
         L     RE,64(RF)           SAVE ROOT'S BASE REGISTER VALUES             
         ST    RE,BCBASER1                                                      
         LH    R0,=Y(4096)                                                      
         AR    RE,R0                                                            
         ST    RE,BCBASER2                                                      
         AR    RE,R0                                                            
         ST    RE,BCBASER3                                                      
         AR    RE,R0                                                            
         ST    RE,BCBASER4                                                      
*                                                                               
         L     RF,4(RF)            GET ROOT'S ORIGINAL R1 VALUE                 
         L     R1,24(RF)                                                        
         MVC   CUABIN,0(R1)        COMPANY CODE                                 
         MVC   AINP,00(R1)         A(TIOB)                                      
         MVC   ATWA,04(R1)         A(TWA)                                       
         MVC   ASYS,08(R1)         A(SYSTEM FACILITY LIST)                      
         MVC   ATIA,12(R1)         A(TIA)                                       
         MVC   ACOM,16(R1)         A(COMMON FACILITY LIST)                      
         L     R1,20(R1)           R1=A(FACPAK EXTRA INFORMATION)               
         MVC   CUCTRY,1(R1)        SET AGENCY COUNTRY                           
         MVC   CULANG,3(R1)                                                     
*                                                                               
         L     R1,ACOM                                                          
         USING COMFACSD,R1                                                      
         MVC   VDMGR,CDATAMGR                                                   
         MVC   VCOLY,CCALLOV                                                    
         MVC   VGETMSG,CGETMSG                                                  
         MVC   VGETTXT,CGETTXT                                                  
         MVC   VHELLO,CHELLO                                                    
         MVC   VSCANNER,CSCANNER                                                
         MVC   VUNSCAN,CUNSCAN                                                  
         MVC   VHEXIN,CHEXIN                                                    
         MVC   VHEXOUT,CHEXOUT                                                  
         MVC   VCASHVAL,CCASHVAL                                                
         MVC   VDATVAL,CDATVAL                                                  
         MVC   VDATCON,CDATCON                                                  
         MVC   VSCUNKEY,CSCUNKEY                                                
         MVC   VADDAY,CADDAY                                                    
         MVC   VPERVERT,CPERVERT                                                
         MVC   VGETFACT,CGETFACT                                                
         MVC   VGETDAY,CGETDAY                                                  
         MVC   VPERVAL,CPERVAL                                                  
         MVC   VMINIO,CMINIO                                                    
         MVC   VPARSNIP,CPARSNIP                                                
         MVC   VSWITCH,CSWITCH                                                  
         MVC   CUREDIT,CCUREDIT                                                 
         MVC   VDICTAT,CDICTATE                                                 
         MVC   VGETPROF,CGETPROF                                                
         MVC   VEDITOR,CEDITOR                                                  
         MVC   VSECRET,CSECRET                                                  
         MVC   VGETHELP,CGETHELP                                                
*&&UK*&& MVC   VCONVERT,CCONVERT                                                
*&&UK*&& MVC   VPRORATA,CPRORATA                                                
         MVC   VGETCUR,CGETCUR                                                  
         MVC   VBLDCUR,CBLDCUR                                                  
*                                                                               
         L     R2,=A(PHASES)       R2=A(PHASE LIST)                             
         A     R2,BCRELO                                                        
         LA    R3,APHASES          R3=A(ADDRESS LIST)                           
         LA    R4,PHASESN          R4=MAXIMUM NUMBER OF PHASES                  
         ICM   R0,14,=X'D9000A'                                                 
         LA    R1,BCPARM                                                        
         L     RF,VCOLY                                                         
INITAD02 CLI   0(R2),FF                                                         
         BE    INITAD04                                                         
         ICM   R0,1,0(R2)                                                       
         GOTO1 (RF),(R1),0,(R0)                                                 
         MVC   0(4,R3),0(R1)                                                    
         LA    R2,1(R2)                                                         
         LA    R3,4(R3)                                                         
         BCT   R4,INITAD02                                                      
*                                                                               
INITAD04 LA    R0,ADDRS1N          SET CONTROLLER ADDRESSES 1                   
         SR    RE,RE               RE=INDEX TO ADDRESS VALUE                    
         LA    RF,ADDRS1                                                        
         L     R1,0(RF,RE)                                                      
         A     R1,BCRELO           RELOCATE AND STORE IN W/S                    
         ST    R1,AADDRS1(RE)                                                   
         LA    RE,4(RE)                                                         
         BCT   R0,*-16                                                          
*                                                                               
         LA    R0,ADDRS2N          SET CONTROLLER ADDRESSES 2                   
         SR    RE,RE               RE=INDEX TO ADDRESS VALUE                    
         L     RF,IPADDRS2                                                      
         L     R1,0(RF,RE)                                                      
         A     R1,BCRELO           RELOCATE AND STORE IN W/S                    
         ST    R1,AADDRS2(RE)                                                   
         LA    RE,4(RE)                                                         
         BCT   R0,*-16                                                          
*                                                                               
         LA    R0,AROUTS1N         SET ROUTINE ADDRESSES - 1                    
         L     RF,VROUT1                                                        
         SR    RE,RE                                                            
         LA    R1,AROUTS1                                                       
INITAD06 ST    RF,0(R1)            SET A(ROUTS)                                 
         STCM  RE,1,0(R1)          SET ROUTINE NUMBER                           
         LA    RE,1(RE)                                                         
         LA    R1,4(R1)                                                         
         BCT   R0,INITAD06                                                      
*                                                                               
         LA    R0,AROUTS2N         SET ROUTINE ADDRESSES - 2                    
         L     RF,VROUT2                                                        
         SR    RE,RE                                                            
         LA    R1,AROUTS2                                                       
INITAD08 ST    RF,0(R1)            SET A(ROUTS)                                 
         STCM  RE,1,0(R1)          SET ROUTINE NUMBER                           
         LA    RE,1(RE)                                                         
         LA    R1,4(R1)                                                         
         BCT   R0,INITAD08                                                      
*                                                                               
         LA    R0,AROUTS3N         SET ROUTINE ADDRESSES - 3                    
         L     RF,VROUT3                                                        
         SR    RE,RE                                                            
         LA    R1,AROUTS3                                                       
INITAD10 ST    RF,0(R1)            SET A(ROUTS)                                 
         STCM  RE,1,0(R1)          SET ROUTINE NUMBER                           
         LA    RE,1(RE)                                                         
         LA    R1,4(R1)                                                         
         BCT   R0,INITAD10                                                      
*                                                                               
INITAD18 L     R1,=A(ANAWS)        SET SUNDRY STORAGE ADDRESSES                 
         A     R1,BCRELO                                                        
         LA    R0,ANAWSN                                                        
INITAD20 ICM   RE,15,0(R1)         RE=AL2(AREA),AL2(ADDRESS)                    
         SRDL  RE,16                                                            
         SRL   RF,16                                                            
         LA    RE,WORKD(RE)                                                     
         LA    RF,WORKD(RF)                                                     
         STCM  RE,15,0(RF)         SET AREA ADDRESS                             
         LA    R1,L'ANAWS(R1)                                                   
         BCT   R0,INITAD20                                                      
*                                                                               
         LH    RE,=Y(OSVALS2-TWAD) SET A(OVERLAY SECONDARY STORAGE)             
         A     RE,ATWA                                                          
         ST    RE,BOSVALS2                                                      
*                                                                               
         LH    RE,=Y(SECBLK-TWAD)  SET A(SECURITY ACCESS BLOCK)                 
         A     RE,ATWA                                                          
         ST    RE,ASECBLK                                                       
*                                                                               
         LA    R0,COLTABL          SET TABLE LENGTHS                            
         ST    R0,LCOLTAB                                                       
         LA    R0,OPVTABL                                                       
         ST    R0,LOPVTAB                                                       
*                                                                               
INITADX  DS    0H                                                               
         EJECT                                                                  
***********************************************************************         
* INITIALISE GENERAL VALUES                                           *         
***********************************************************************         
         SPACE 1                                                                
INITGV   L     RA,ATWA                                                          
         USING TWAD,RA             RA=A(TWA)                                    
         LH    R5,=Y(BSVALS-TWAD)                                               
         LA    R5,TWAD(R5)                                                      
         USING BSVALS,R5           R5=A(TWA SAVE AREA)                          
         MVC   BCDTADSP,=Y(ACCORFST)                                            
*&&UK*&& MVC   BCSYSTEM,=C'UK'                                                  
*&&US*&& MVC   BCSYSTEM,=C'US'                                                  
         MVI   BCEFFS,FF                                                        
         MVC   BCEFFS+1(L'BCEFFS-1),BCEFFS                                      
         MVI   BCSPACES,C' '                                                    
         MVC   BCSPACES+1(L'BCSPACES-1),BCSPACES                                
         ZAP   BCPZERO,=P'0'                                                    
         MVC   CUACCS,TWAACCS      LIMIT ACCESS                                 
         MVC   CUUSER,TWAUSRID     USER-ID NUMBER                               
         MVC   CUAUTH,TWAAUTH      AUTHORIZATION CODE                           
         MVC   CUAALF,TWAAGY       AGENCY ALPHA-ID                              
         GOTO1 VGETFACT,BCPARM,0,0                                              
         L     R2,0(R1)                                                         
         USING FACTSD,R2           R1=A(SYSTEM DEFINITION BLOCK)                
         MVC   ACTRY,FAACTRY       SET A(COUNTRY & LANGUAGE TABLES)             
         MVC   ALANG,FAALANG                                                    
         MVI   ASONOFF,ASON        SET ONLINE OR OFFLINE SWITCH                 
*                                                                               
         MVC   CUTSYM,FASYM                                                     
         MVC   ASEDAT,FADATE       SYSTEM DATES (VARIOUS FORMATS)               
         MVC   ASBDAT,FADATEB                                                   
         CLI   TWAOFFC,C'*'        TEST DDS OFFICE CODE                         
         BNE   *+8                                                              
         MVI   CUSTAT,CUSDDS       SET DDS STATUS INDICATOR                     
         OC    CUPASS,FAPASSWD     TEST CONNECTED WITH PASSWORD                 
         BNZ   *+6                                                              
         DC    H'0'                                                             
         TM    FATFLAG,X'08'       YES - TEST PERSONAL PASSWORD                 
         BNZ   *+6                                                              
         DC    H'0'                                                             
         OI    CUSTAT,CUSPER                                                    
INITGV02 MVC   ASTIME,FATIME       SYSTEM TIME (STANDARD FORMAT)                
         MVC   ASSYSN,FASYS        SYSTEM NUMBER                                
         MVC   ASSYSO,FAOVSYS      SYSTEM NUMBER (FOR CALLOV)                   
         MVC   ASSIN,FASIN         SYSTEM INPUT NUMBER                          
         MVC   ASIOASTR,FAIOASTR   SYSTEM EXTRA AREA ADDRESS                    
         MVC   ASIOALEN,FAIOALEN   SYSTEM EXTRA AREA LENGTH                     
*                                                                               
         MVC   BCPARM(4),BCEFFS    SET A(UTL ENTRY)                             
         GOTO1 VSWITCH,BCPARM                                                   
         MVC   BCAUTL,0(R1)                                                     
         L     RF,BCAUTL                                                        
*        MVC   CUTERM,TCFN-UTLD(RF)  NO LONGER USED                             
         MVC   CUTERM,=X'0000'                                                  
*                                                                               
         MVC   BCPARM(4),BCEFFS    SET A(SYSFACS)                               
         MVI   BCPARM,X'FE'                                                     
         GOTO1 VSWITCH,BCPARM                                                   
         MVC   ASYSFAC,0(R1)                                                    
         DROP  R2                                                               
*                                                                               
         L     R1,BCAUTL           AGENCY ALPHA VALUE FOR SECURITY              
         MVC   CUASEC,TAGYSEC-UTLD(R1)                                          
*                                                                               
         GOTO1 VGETFACT,BCPARM,(X'80',0),F#TCBD                                 
         L     R1,0(R1)                                                         
         USING F@TCBD,R1                                                        
         SR    R0,R0                                                            
         ICM   R0,1,F@BSWNUM       R0=N'ENTRIES IN TCBSWTAB                     
         BZ    INITGV06                                                         
         CLM   R0,1,=AL1(SYSSWMAX)                                              
         BNH   *+8                                                              
         LA    R0,SYSSWMAX                                                      
         LA    R1,F@BSWTAB                                                      
         USING F@BSWTAB,R1         R1=A(TCB SWITCH TABLE)                       
         L     RE,ASWSTAB                                                       
         USING SYSSWTAB,RE         RE=A(LOCAL SWITCH TABLE)                     
INITGV04 MVC   SYSSWSYS,F@BSWSYS                                                
         MVC   SYSSWSOV,F@BSWSOV                                                
         MVC   SYSSWAGB,F@BSWAGB                                                
         MVC   SYSSWACS,F@BSWACS                                                
         MVC   SYSSWAC2,F@BSWAC2                                                
         LA    R1,F@BSWLEN(R1)     BUMP TO NEXT                                 
         LA    RE,SYSSWLEN(RE)                                                  
         BCT   R0,INITGV04         DO FOR NUMBER OF ENTRIES                     
         DROP  R1,RE                                                            
*                                                                               
INITGV06 L     R1,AINP             EXTRACT PFKEY NUMBER                         
         USING TIOBD,R1                                                         
         ICM   RE,1,TIOBAID                                                     
         BZ    *+12                                                             
         OI    BCINDS1,BCIANYPF    SET USER ENTERED PF KEY THIS TIME            
         STC   RE,BCPFKEY          AND SET PFKEY NUMBER                         
         SR    RE,RE                                                            
         ICM   RE,3,TIOBCURD                                                    
         LA    RE,TWAD(RE)                                                      
         ST    RE,BCACUR           SET A(CURSOR)                                
         DROP  R1                                                               
*                                                                               
         CLI   CULANG,0            SET DEFAULT LANGUAGE                         
         BNE   *+8                                                              
*&&UK*&& MVI   CULANG,LANGEUK                                                   
*&&US*&& MVI   CULANG,LANGEUS                                                   
         SR    R1,R1                                                            
         IC    R1,CULANG                                                        
         SLL   R1,3                                                             
         L     R0,=A(LANGCHAR-L'LANGCHAR)                                       
         A     R0,BCRELO                                                        
         AR    R1,R0                                                            
         MVC   BCCHARS,0(R1)       SET SPECIAL CHARACTERS                       
         CLI   CUCTRY,0            SET DEFAULT COUNTRY                          
         BNE   *+8                                                              
*&&UK*&& MVI   CUCTRY,CTRYGBR                                                   
*&&US*&& MVI   CUCTRY,CTRYUSA                                                   
*                                                                               
         MVC   BCSWSYSN,ASSYSO     SET SWITCHED SYSTEM INFO                     
         MVC   BCSWSYSC,ASSYSO                                                  
         MVC   BCSWSYSP,ASSYSO                                                  
*                                                                               
         GOTO1 VDATCON,BCPARM,(3,ASBDAT),(1,ASPDAT)                             
         GOTO1 (RF),(R1),(3,ASBDAT),(2,ASCDAT)                                  
*                                                                               
         L     R1,AFILTAB          FIND FILE TABLE ENTRY (FOR IOEX)             
         SR    RE,RE                                                            
INITGV10 CLI   0(R1),EOT           TEST EOT                                     
         BNE   *+6                                                              
         DC    H'0'                THIS SYSTEM NOT SUPPORTED                    
         CLC   0(1,R1),ASSYSO      MATCH ON OVSYS NUMBER                        
         BE    *+16                                                             
         ICM   RE,3,4(R1)                                                       
         LA    R1,5(RE,R1)                                                      
         B     INITGV10                                                         
         LA    R1,6(R1)                                                         
         ST    R1,AFILNTRY         SAVE A(FILE TABLE ENTRY)                     
*                                                                               
         TM    TWAINDS1,TWAIINIT   TEST VALUES SAVED LAST TIME                  
         BZ    INITGV12                                                         
         LA    R0,BCVALS           MOVE SAVE VALUES TO GLOBAL W/S               
         LA    RE,BSVALS                                                        
         LA    R1,BCVALSL                                                       
         LR    RF,R1                                                            
         MVCL  R0,RE                                                            
         NI    IINDS,FF-IIFRST                                                  
         B     INITGV90                                                         
*                                                                               
INITGV12 OI    IINDS,IIFRST                                                     
*        GOTO1 VDMGR,BCPARM,=C'DTFADD',=C'ACCFIL'                               
*        L     R1,12(R1)                                                        
*        TM    ISFTYPE-ISDTF(R1),ISFTEMU                                        
*        BNZ   *+14                                                             
*        MVC   FVMSGNO,=AL2(AE$FNCNA)                                           
*        B     INITGVL                                                          
*                                                                               
         OI    TWAINDS1,TWAIINIT   INITIALISE DICTIONARY                        
*                                                                               
         GOTO1 VDICTAT,BCPARM,C'LU  ',DICGEN,BC@YES                             
         GOTO1 (RF),(R1),,ADICUPR,BSDICT                                        
         L     R2,IPDICMIX                                                      
         GOTO1 (RF),(R1),C'LL  ',ADICMIX,BSDICT(R2)                             
*                                                                               
         GOTO1 VDATCON,(R1),(7,0),('FF',0)                                      
         SR    RE,RE                                                            
         ICM   RE,7,5(R1)                                                       
         MVC   BCMONTHS,0(RE)                                                   
*                                                                               
         LA    R2,IOKEY            READ COMPANY RECORD & EXTRACT VALUES         
         USING CPYRECD,R2                                                       
         MVC   CPYKEY,BCSPACES                                                  
         MVC   CPYKCPY,CUABIN                                                   
         GOTO1 AIO,IOREAD+IOACCMST+IO1                                          
         BE    *+6                                                              
         DC    H'0'                CAN'T READ COMPANY RECORD                    
*                                                                               
         L     R2,AIO1             LOCATE COMPANY ELEMENT                       
         LA    R1,CPYRFST                                                       
         USING CPYEL,R1                                                         
         SR    R0,R0                                                            
INITGV14 CLI   CPYEL,0             TEST EOR                                     
         BNE   *+6                                                              
         DC    H'0'                                                             
         CLI   CPYEL,CPYELQ        TEST COMPANY ELEMENT                         
         BE    *+14                                                             
         IC    R0,CPYLN                                                         
         AR    R1,R0                                                            
         B     INITGV14                                                         
         SR    RE,RE               EXTRACT COMPANY ELEMENT                      
         ICM   RE,1,CPYLN                                                       
         LA    R0,L'BCCPYEL                                                     
         CR    RE,R0                                                            
         BNH   *+6                                                              
         LR    RE,R0                                                            
         BCTR  RE,0                                                             
         EX    RE,*+4                                                           
         MVC   BCCPYEL(0),CPYELD                                                
         CLI   CPYLN,CPYLN2Q       SET CURRENCY CODE                            
         BL    *+10                                                             
         MVC   CSCPYCUR,CPYCURR                                                 
         TM    CPYSTAT1,CPYSOROE   SET LENGTH OF OFFICE CODE                    
         BZ    *+8                                                              
         MVI   BCOFFLEN,1                                                       
         TM    CPYSTAT4,CPYSOFF2                                                
         BZ    *+8                                                              
         MVI   BCOFFLEN,2                                                       
*                                                                               
         MVI   BCDPTLEN,2          SET LENGTH OF DEPARTMENT CODE                
         CLI   CPYDEPTL,0                                                       
         BE    *+10                                                             
         MVC   BCDPTLEN,CPYDEPTL                                                
*                                                                               
         LA    R2,IOKEY            READ LEDGER RECORDS                          
         USING LDGRECD,R2                                                       
         MVC   LDGKEY,BCSPACES                                                  
         MVC   LDGKCPY,CUABIN                                                   
         MVC   LDGKUNT(L'CPYPROD),BCCPYEL+(CPYPROD-CPYELD)                      
         GOTO1 AGETLDG             GET PRODUCTION LEDGER TABLE ENTRY            
         BE    INITGV15                                                         
         MVC   0(2,R7),LDGKUNT     GET BAD LEDGER                               
         NI    TWAINDS1,FF-TWAIINIT                                             
         B     INITGVL                                                          
*                                                                               
INITGV15 LA    R3,INITLDG                                                       
INITGV16 CLI   0(R3),EOT                                                        
         BE    INITGV17                                                         
         MVC   LDGKUNT(L'CPYPROD),0(R3)                                         
         GOTO1 AGETLDG                                                          
         BE    *+18                                                             
         MVC   0(2,R7),LDGKUNT     GET BAD LEDGER                               
         NI    TWAINDS1,FF-TWAIINIT                                             
         B     INITGVL                                                          
         LA    R3,2(R3)                                                         
         B     INITGV16                                                         
*                                                                               
INITGV17 LA    R2,IOKEY                                                         
         USING CTIREC,R2           BUILD KEY OF ID RECORD                       
         XC    CTIKEY,CTIKEY                                                    
         MVI   CTIKTYP,CTIKTYPQ                                                 
         MVC   CTIKNUM,CUUSER                                                   
         GOTO1 AIO,IORD+IOCTFILE+IO1                                            
         BE    *+6                                                              
         DC    H'0'                                                             
         L     R2,AIO1                                                          
         LA    R1,CTIDATA                                                       
         XR    RF,RF                                                            
INITGV18 CLI   0(R1),0             TEST E-O-R                                   
         BE    INITGV28                                                         
*                                                                               
         USING CTDSCD,R1                                                        
         CLI   CTDSCEL,CTDSCELQ    TEST FOR DESCRIPTION (CODE) ELEMENT          
         BNE   INITGV19                                                         
         MVC   BCUSERID,CTDSC                                                   
         B     INITGV26                                                         
*                                                                               
         USING CTDSTD,R1                                                        
INITGV19 CLI   CTDSTD,CTDSTELQ     TEST FOR DESTINATION ELEMENT                 
         BNE   INITGV20                                                         
         MVC   BCDSTNAM,CTDSTNAM                                                
         MVC   BCDSTAD1,CTDSTADD                                                
         CLI   CTDSTLEN,CTDSTAD2-CTDSTD                                         
         BNH   INITGV26                                                         
         MVC   BCDSTAD2,CTDSTAD2                                                
         CLI   CTDSTLEN,CTDSTAD3-CTDSTD                                         
         BNH   INITGV26                                                         
         MVC   BCDSTAD3,CTDSTAD3                                                
         B     INITGV26                                                         
*                                                                               
         USING CTORGD,R1                                                        
INITGV20 CLI   CTORGEL,CTORGELQ    TEST FOR ORIGIN ELEMENT                      
         BNE   INITGV26                                                         
         MVC   BCORGNAM,CTORGNAM                                                
         MVC   BCORGADD,CTORGADD                                                
         DROP  R1                                                               
*                                                                               
INITGV26 IC    RF,1(R1)                                                         
         BXH   R1,RF,INITGV18                                                   
*                                                                               
INITGV28 LA    R2,IOKEY                                                         
         USING CTWREC,R2           BUILD KEY OF GROUP RECORD                    
         XC    CTWKEY,CTWKEY                                                    
         MVI   CTWKTYP,CTWKTYPQ                                                 
         MVC   CTWKAGY,CUAALF                                                   
         MVI   CTWKREC,CTWKRIDG                                                 
         MVC   CTWKID,BCUSERID     USE CODE FROM USER-ID RECORD                 
         GOTO1 AIO,IORD+IOCTFILE+IO1                                            
         BNE   *+8                                                              
         OI    TWAINDS1,TWAIUGID   SET CONNECTED USERID IS A GROUPID            
         CLC   CUUSER,BCCPYEL+(CPYUID-CPYELD)                                   
         BNE   *+8                                                              
         OI    TWAINDS1,TWAIUPID   SET CONNECTED USERID IS PRINCIPAL ID         
*                                                                               
         GOTO1 VDATCON,BCPARM,(5,0),(0,BCWORK)                                  
         MVC   BCTMON+0(1),BCWORK+1                                             
         MVC   BCTMON+1(1),BCWORK+3                                             
         CLI   BCWORK+2,C'1'                                                    
         BNE   INITGV30                                                         
         MVI   BCTMON+1,C'A'                                                    
         CLI   BCWORK+3,C'0'                                                    
         BE    INITGV30                                                         
         MVI   BCTMON+1,C'B'                                                    
         CLI   BCWORK+3,C'1'                                                    
         BE    INITGV30                                                         
         MVI   BCTMON+1,C'C'                                                    
*                                                                               
INITGV30 GOTO1 (RF),(R1),,(1,BCTODAYP)                                          
         MVC   BCTMONP,BCTODAYP                                                 
         GOTO1 (RF),(R1),,(2,BCTODAYC)                                          
         GOTO1 (RF),(R1),,(3,BCTODAYB)                                          
*&&UK*&& LA    R0,365                                                           
*&&US*&& LA    R0,720                                                           
         GOTO1 VADDAY,BCPARM,BCWORK,BCWORK+6,(R0)                               
         LNR   R0,R0                                                            
         GOTO1 (RF),(R1),,BCWORK+12,(R0)                                        
         GOTO1 VDATCON,BCPARM,(0,BCWORK+6),(1,BCTDATH)                          
         GOTO1 (RF),(R1),(0,BCWORK+12),(1,BCTDATL)                              
         TM    BCCPYST4,CPYSOV12                                                
         BZ    *+10                                                             
         XC    BCTDATL,BCTDATL     ANY DATE BACKWARDS ALLOWABLE                 
*                                                                               
         XC    BCWORK,BCWORK       SAVE AGENCY LEVEL PROGRAM PROFILE            
         MVI   BCWORK+00,C'A'-X'40'                                             
         MVC   BCWORK+01(3),IPNAME                                              
         MVC   BCWORK+12(2),TWAAGY                                              
         GOTO1 VGETPROF,BCPARM,BCWORK,BCBPROF1,VDMGR                            
         MVI   BCWORK+03,C'2'                                                   
         GOTO1 (RF),(R1),,BCBPROF2                                              
*                                                                               
INITGV90 L     R1,AOFFBLK                                                       
         USING OFFALD,R1           R1=A(OFFAL BLOCK)                            
         MVC   OFFACOMF,ACOM                                                    
         MVC   OFFAALPH,TWAAGY                                                  
         MVC   OFFACPY,CUABIN                                                   
         MVC   OFFACST1,BCCPYST1                                                
         MVC   OFFACST2,BCCPYST2                                                
         MVC   OFFACST3,BCCPYST3                                                
         MVC   OFFACST4,BCCPYST4                                                
         MVC   OFFALIMA,TWAACCS                                                 
         MVC   OFFAAUTH,TWAAUTH                                                 
         MVI   OFFAINDS,OFFAIOFF   OFFICE SECURITY CHECK                        
         MVI   OFFAACT,OFFAINI     INITIALIZE                                   
         OC    OFFASAV(OFFASAVL),BCOFFSAV                                       
         BZ    *+8                                                              
         MVI   OFFAACT,OFFARES     RESTORE                                      
         GOTO1 VOFFAL                                                           
         BE    *+6                                                              
         DC    H'0'                                                             
         MVC   BCOFFSAV,OFFASAV    SAVE OFFAL VALUES                            
         DROP  R1                                                               
*                                                                               
         L     R1,AINP             EXTRACT CURSOR POSITION                      
         MVC   CSCURDSP,TIOBCURS-TIOBD(R1)                                      
*                                                                               
         LA    RF,SECBLKL                                                       
         L     R2,ASECBLK                                                       
         USING SECD,R2                                                          
         GOTO1 VSECRET,BCPARM,('SECPINIT',SECD),(RF)                            
         ORG   *-2                                                              
         TM    SECINDS,SECIINIT    TEST SECRET BLOCK INITIALISED                
         BNZ   *+12                                                             
         BASR  RE,RF                                                            
         BE    *+6                                                              
         DC    H'0'                                                             
         MVC   CSBPID,SECPID       EXTRACT USER PUBLIC-ID                       
         DROP  R2                                                               
         OC    CSCPYCUR,CSCPYCUR   TEST CURRENCY CODE RESOLVED                  
         BNZ   INITGV94                                                         
         LA    R1,CURTAB           LOOK UP DEFAULT CURRENCY IN TABLE            
INITGV92 CLI   0(R1),0                                                          
         BNE   *+6                                                              
         DC    H'0'                                                             
         CLC   CUCTRY,0(R1)        MATCH ON COUNTRY CODE                        
         BE    *+12                                                             
         LA    R1,L'CURTAB(R1)                                                  
         B     INITGV92                                                         
         MVC   CSCPYCUR,1(R1)                                                   
*&&UK                                                                           
INITGV94 GOTO1 VBLDCUR,BCPARM,CSCPYCUR,(X'80',CSCURCPY),ACOM                    
*&&                                                                             
*&&US                                                                           
INITGV94 MVI   CSCURCPY+(CURTDECP-CURTABD),2                                    
*&&                                                                             
         TM    IINDS,IIFRST        TEST WAS FIRST TIME                          
         BZ    INITGVE                                                          
INITGVH  CLI   *,0                 SET CC HIGH FOR FIRST TIME                   
         B     INITGVX                                                          
INITGVL  CLI   *,FF                SET CC LOW FOR ERROR                         
         B     INITGVX                                                          
INITGVE  CR    RB,RB               SET CC EQUAL                                 
INITGVX  XMOD1 ,                                                                
         EJECT                                                                  
FF       EQU   X'FF'                                                            
         SPACE 1                                                                
INITLDG  DC    C'SE'               LIST OF LEDGERS FOR INITIALISATION           
         DC    C'SI'                                                            
*&&UK*&& DC    C'SQ'                                                            
*&&US*&& DC    C'1R'                                                            
         DC    AL1(EOT)                                                         
         SPACE 1                                                                
         LTORG                                                                  
         EJECT                                                                  
ANAWS    DS    0XL4                ** A(NON-ADDRESSABLE W/S AREAS) **           
         DC    AL2(IOAREA1+L'IODA+L'IOWORK-WORKD,AIO1-WORKD)                    
         DC    AL2(IOAREA2+L'IODA+L'IOWORK-WORKD,AIO2-WORKD)                    
         DC    AL2(IOAREA3+L'IODA+L'IOWORK-WORKD,AIO3-WORKD)                    
         DC    AL2(IOAREA4+L'IODA+L'IOWORK-WORKD,AIO4-WORKD)                    
         DC    AL2(IOAREA5+L'IODA+L'IOWORK-WORKD,AIO5-WORKD)                    
         DC    AL2(IOAREA6+L'IODA+L'IOWORK-WORKD,AIO6-WORKD)                    
         DC    AL2(IOAREA7+L'IODA+L'IOWORK-WORKD,AIO7-WORKD)                    
         DC    AL2(IOAREA8+L'IODA+L'IOWORK-WORKD,AIO8-WORKD)                    
         DC    AL2(IOAREA9+L'IODA+L'IOWORK-WORKD,AIO9-WORKD)                    
         DC    AL2(IOAREAA+L'IODA+L'IOWORK-WORKD,AIOA-WORKD)                    
         DC    AL2(SWSTAB-WORKD,ASWSTAB-WORKD)                                  
         DC    AL2(GOPBLK-WORKD,AGOPBLK-WORKD)                                  
         DC    AL2(GOXBLK-WORKD,AGOXBLK-WORKD)                                  
         DC    AL2(OFFBLK-WORKD,AOFFBLK-WORKD)                                  
         DC    AL2(VATBLK-WORKD,AVATBLK-WORKD)                                  
         DC    AL2(ADTBLK-WORKD,AADTBLK-WORKD)                                  
         DC    AL2(JOBBLK-WORKD,AJOBBLK-WORKD)                                  
         DC    AL2(COLTAB-WORKD,ACOLTAB-WORKD)                                  
         DC    AL2(OPVTAB-WORKD,AOPVTAB-WORKD)                                  
         DC    AL2(TSABLK-WORKD,ATSABLK-WORKD)                                  
         DC    AL2(OVERWRK-WORKD,AOVERWRK-WORKD)                                
         DC    AL2(WORKX-WORKD,AWORKX-WORKD)                                    
ANAWSN   EQU   (*-ANAWS)/L'ANAWS                                                
*                                                                               
DICGEN   DS    0XL4                ** UPPER CASE DICTIONARY LIST **             
         DCDDL AC#YES,4                                                         
         DCDDL AC#NO,4                                                          
         DCDDL AC#DR,3                                                          
         DCDDL AC#CR,3                                                          
DICGENX  DC    AL1(EOT)                                                         
         EJECT                                                                  
LANGCHAR DS    0CL8                ** LANGUAGE DEPENDANT CHARACTERS **          
         DC    CL8',=?/#.'         ENGLISH                                      
         DC    CL8',=?/#.'         AMERICAN                                     
         DC    CL8'#=?/#,'         GERMAN                                       
         DC    CL8',=?/#,'         FRENCH                                       
         DC    CL8',=?/#,'         SPANISH                                      
         DC    CL8',=?/#,'         ITALIAN                                      
         DC    CL8',=?/#,'         DUTCH                                        
         DC    CL8',=?/#,'         ???                                          
         DC    CL8',=?/#,'         ???                                          
         DC    CL8',=?/#,'         ???                                          
         DC    CL8',=?/#,'         ???                                          
         DC    CL8',=?/#,'         ???                                          
         SPACE 1                                                                
PHASES   DS    0X                  ** LOADED PHASE LIST **                      
         DC    AL1(QADDTRN)                                                     
         DC    AL1(QCHOPPER)                                                    
         DC    AL1(QOFFAL)                                                      
         DC    AL1(QREPORT)                                                     
         DC    AL1(QREQTWA)                                                     
         DC    AL1(QSPOON)                                                      
         DC    AL1(QSQUASH)                                                     
         DC    AL1(QTSAR)                                                       
         DC    AL1(QJOBBER)                                                     
         DC    AL1(QGETOPT)                                                     
         DC    AL1(QBATFAC1)                                                    
         DC    AL1(QBATFAC2)                                                    
         DC    AL1(QBATFAC3)                                                    
         DC    AL1(QQSORT)                                                      
         DC    AL1(QTIMVAL)                                                     
         DC    AL1(QGETIDS)                                                     
         DC    AL1(QBATFAC4)                                                    
         DC    AL1(QFALINK)                                                     
PHASESN  EQU   *-PHASES                                                         
         SPACE 1                                                                
ADDRS1   DS    0F                  ** CONTROLLER ADDRESSES 1 **                 
         DC    V(AMTVAL)                                                        
         DC    V(CONVMOS)                                                       
         DC    V(BMONVAL)                                                       
         DC    V(VATICAN)                                                       
         DC    V(ACJOBCOL)                                                      
         DC    V(CATCALL)                                                       
         DC    V(EXCEL)                                                         
         DC    V(ACSRCHC)                                                       
         DC    A(SYSTAB)                                                        
         DC    A(CMDTAB)                                                        
         DC    A(FILTAB)                                                        
         ORG   ADDRS1+(L'ADDRS1*ADDRS1N)                                        
         EJECT                                                                  
***********************************************************************         
* SYSTEM FILE NAMES TABLE (FILE NUMBERS 1 THRU 9)                     *         
***********************************************************************         
         SPACE 1                                                                
FILTAB   DS    0X                                                               
*                                  ** ACCOUNT SYSTEM FILES **                   
FILACC   DC    AL1(6),C'ACC',AL2(FILACCX-*)                                     
*                                                                               
         DC    AL1(IOACCFIL/256),C'ACCFIL '                                     
         DC    AL1(FILIVL+FILIIS,0)                                             
         DC    AL1(0,42,49),AL2(1000)                                           
         DC    XL5'00'                                                          
*                                                                               
         DC    AL1(IOACCDIR/256),C'ACCDIR '                                     
         DC    AL1(FILIIS,FILIID)                                               
         DC    AL1(IOACCMST/256,42,08),AL2(54)                                  
         DC    XL5'00'                                                          
*                                                                               
         DC    AL1(IOACCMST/256),C'ACCMST '                                     
         DC    AL1(FILIDA,FILIDI)                                               
         DC    AL1(IOACCDIR/256,42,56),AL2(2000)                                
         DC    XL5'00'                                                          
*                                                                               
         DC    AL1(IOACCARC/256),C'ACCARC '                                     
         DC    AL1(FILIDA,FILIDI)                                               
         DC    AL1(IOACCDIR/256,42,56),AL2(2000)                                
         DC    XL5'00'                                                          
*                                                                               
FILACCX  DC    AL1(EOT)                                                         
*                                  ** CONTROL SYSTEM FILES **                   
FILCON   DC    AL1(10),C'CON',AL2(FILCONX-*)                                    
*                                                                               
         DC    AL1(IOGENDIR/256),C'GENDIR '                                     
         DC    AL1(FILIIS,FILIID)                                               
         DC    AL1(IOGENFIL/256,32,04),AL2(40)                                  
         DC    XL5'00'                                                          
*                                                                               
         DC    AL1(IOGENFIL/256),C'GENFIL '                                     
         DC    AL1(FILIDA,FILIDI)                                               
         DC    AL1(IOGENDIR/256,32,42),AL2(2000)                                
         DC    XL5'00'                                                          
*                                                                               
         DC    AL1(IOCONFIL/256),C'CTFILE '                                     
         DC    AL1(FILIVL+FILIIS,0)                                             
         DC    AL1(0,25,28),AL2(2000)                                           
         DC    XL5'00'                                                          
*                                                                               
FILCONX  DC    AL1(EOT)                                                         
*                                                                               
FILTABX  DC    AL1(EOT)                                                         
         EJECT                                                                  
***********************************************************************         
* TABLE OF GLOBAL SYSTEM FILES (FILE NUMBERS 10 THRU 15)              *         
***********************************************************************         
         SPACE 1                                                                
SYSTAB   DS    0X                                                               
*                                                                               
         DC    AL1(15),C'CTFILE '                                               
         DC    AL1(FILIVL+FILIIS,0)                                             
         DC    AL1(0,25,29),AL2(2000)                                           
         DC    XL5'00'                                                          
*                                                                               
SYSTABX  DS    AL1(EOT)                                                         
         SPACE 2                                                                
***********************************************************************         
* SYSTEM FILE COMMANDS TABLE                                          *         
***********************************************************************         
         SPACE 1                                                                
CMDTAB   DS    0X                                                               
*                                  ** INDEX SEQUENTIAL COMMANDS **              
CMDIS    DC    AL1(FILIVL+FILIIS,0),AL2(CMDISX-*)                               
         DC    C'DMRDHI ',AL1(IOHI,0,0)                                         
         DC    C'DMREAD ',AL1(IORD,0,0)                                         
         DC    C'DMRSEQ ',AL1(IOSQ,0,0)                                         
         DC    C'DMADD  ',AL1(IOADD,0,0)                                        
         DC    C'DMWRT  ',AL1(IOWRITE,0,0)                                      
CMDISX   DC    AL1(EOT)                                                         
*                                  ** DIRECT ACCESS COMMANDS **                 
CMDDA    DC    AL1(FILIDA,0),AL2(CMDDAX-*)                                      
         DC    C'GETREC ',AL1(IOHI,CMDIDARQ+CMDIDAXC,0)                         
         DC    C'GETREC ',AL1(IORD,CMDIDARQ+CMDIDAXC,0)                         
         DC    C'GETREC ',AL1(IOSQ,CMDIDARQ+CMDIDAXC,0)                         
         DC    C'GETREC ',AL1(IOGET,CMDIDARQ,0)                                 
         DC    C'ADDREC ',AL1(IOADDREC,CMDIDADD,0)                              
         DC    C'PUTREC ',AL1(IOPUTREC,CMDIDARQ,0)                              
CMDDAX   DC    AL1(EOT)                                                         
*                                                                               
CMDTABX  DC    AL1(EOT)                                                         
         EJECT                                                                  
***********************************************************************         
* TABLE OF DEFAULT CURRENCY CODES FOR ALL COUNTRIES                   *         
***********************************************************************         
         SPACE 1                                                                
CURTAB   DS    0XL4                                                             
       ++INCLUDE ACCURTAB                                                       
CURTABX  DC    AL1(0)                                                           
         EJECT                                                                  
***********************************************************************         
* INITIALIZATION LOCAL W/S                                            *         
***********************************************************************         
         SPACE 1                                                                
IWORKD   DSECT                                                                  
IPARMS   DS    0XL(IPARMSL)        * INPUT PARAMETERS *                         
IPADDRS2 DS    A                   A(CONTROLLER ADDRESSES 2)                    
IPDICMIX DS    A                   DISP. TO MIXED CASE DICTIONARY LIST          
IPNAME   DS    CL3                 3 CHARACTER PROGRAM NAME FOR PROFILE         
         DS    CL1                 N/D                                          
IPLEDG   DS    A                   2 CHAR LEDGER, NONSPACE IF MISSING           
IPARMSL  EQU   *-IPADDRS2                                                       
*                                                                               
IINDS    DS    XL1                 * INDICATORS *                               
IIFRST   EQU   X'80'               FIRST TIME                                   
IWORKL   EQU   *-IWORKD                                                         
         SPACE 1                                                                
* DDCOMFACS                                                                     
         PRINT OFF                                                              
       ++INCLUDE DDCOMFACS                                                      
         PRINT ON                                                               
         SPACE 1                                                                
* FAFACTS                                                                       
         PRINT OFF                                                              
       ++INCLUDE FAFACTS                                                        
         PRINT ON                                                               
         SPACE 1                                                                
* FASECRETD                                                                     
         PRINT OFF                                                              
       ++INCLUDE FASECRETD                                                      
         PRINT ON                                                               
         SPACE 1                                                                
* FAUTL                                                                         
         PRINT OFF                                                              
       ++INCLUDE FAUTL                                                          
         PRINT ON                                                               
         SPACE 1                                                                
* FATCB                                                                         
         PRINT OFF                                                              
       ++INCLUDE FATCB                                                          
         PRINT ON                                                               
         SPACE 1                                                                
* DDCOREQUS                                                                     
         PRINT OFF                                                              
       ++INCLUDE DDCOREQUS                                                      
         PRINT ON                                                               
         SPACE 1                                                                
* CTGENFILE                                                                     
         PRINT OFF                                                              
       ++INCLUDE CTGENFILE                                                      
         PRINT ON                                                               
         SPACE 1                                                                
* ACDDEQUS                                                                      
         PRINT OFF                                                              
       ++INCLUDE ACDDEQUS                                                       
         PRINT ON                                                               
         SPACE 1                                                                
* ACGENDAY                                                                      
         PRINT OFF                                                              
       ++INCLUDE ACGENDAY                                                       
         PRINT ON                                                               
         SPACE 1                                                                
* DDGETHELPD                                                                    
         PRINT OFF                                                              
       ++INCLUDE DDGETHELPD                                                     
         PRINT ON                                                               
         SPACE 1                                                                
* DMDTFIS                                                                       
*********PRINT OFF                                                              
*********INCLUDE DMDTFIS                                                        
*********PRINT ON                                                               
         EJECT                                                                  
* ACGENWORK                                                                     
       ++INCLUDE ACGENWORK                                                      
         SPACE 1                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'056ACGENINI  08/13/01'                                      
         END                                                                    
