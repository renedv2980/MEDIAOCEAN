*          DATA SET BUTEXTIO   AT LEVEL 011 AS OF 05/01/02                      
*CATALP TEXTIO                                                                  
         TITLE 'TEXTIO - I/O CONTROL FOR ABC TEXT RECORDS'                      
TEXTIO   CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 MODDX-MODD,**TXIO**,RA,CLEAR=YES                                 
         USING MODD,RC                                                          
         MVC   USERRD,4(RD)        SAVE USERS RD                                
         SPACE 1                                                                
         L     R9,0(R1)            USER PASSES A(TEXT BLOCK)                    
         USING TXTIOD,R9                                                        
         SPACE 1                                                                
         L     R1,TXTACOM                                                       
         USING COMFACSD,R1                                                      
         MVC   DATAMGR,CDATAMGR    DATAMGR FROM COMFACS                         
         EJECT                                                                  
*              HANDLE I/O FOR TEXT RECORDS                                      
         SPACE 3                                                                
READ     LA    R4,KEY              BUILD INITIAL KEY                            
         SR    R3,R3               COUNT RECORDS IN R3                          
         USING BUKEY,R4                                                         
         XC    KEY,KEY                                                          
         MVI   BUKSYS,C'B'                                                      
         MVC   BUKAGY,TXTAGY                                                    
         MVI   BUKRTYP,X'02'                                                    
         MVC   BUKNODE,TXTNODE                                                  
         MVC   BUKCODE,TXTCODE                                                  
         LA    R1,BUKCODE+7        CODE IS ZERO FILLED                          
         LA    R0,8                SO STRIP OUT TRAILING SPACES                 
         SPACE 1                                                                
READB    CLI   0(R1),C' '                                                       
         BH    READD                                                            
         MVI   0(R1),0                                                          
         BCTR  R1,0                                                             
         BCT   R0,READB                                                         
         SPACE 1                                                                
READD    MVI   BUTSUB,BUTSUBQ      AND SUB KEY                                  
         MVC   BUTDTYP,TXTDATA     (POSSIBLE DATA TYPE)                         
         MVC   BUTPER,TXTPER       (POSSIBLE PERIOD)                            
         BAS   RE,HIGH                                                          
         B     READ4                                                            
         SPACE 1                                                                
READ2    BAS   RE,SEQ                                                           
         SPACE 1                                                                
READ4    CLC   KEY(29),KEYSAVE     CHECK C/B                                    
         BNE   READEND                                                          
         SPACE 1                                                                
         BAS   RE,FILTKEY          FILTER ON KEY                                
         BNE   READ2                                                            
         BAS   RE,GETREC                                                        
         BAS   RE,DIGTEXT          DIG OUT TEXT                                 
         BAS   RE,GOHOOK           HOOK TO USER                                 
         LA    R3,1(R3)                                                         
         ZIC   R1,TXTOPT           CHECK OPTIONAL N'RECORDS REACHED             
         CR    R3,R1                                                            
         BNE   READ2                                                            
         SPACE 1                                                                
READEND  B     XIT                                                              
         EJECT                                                                  
*              FILTER ON KEY FIELDS                                             
         SPACE 3                                                                
FILTKEY  NTR1                                                                   
         CLI   TXTFILT,C'*'        CHECK FIRST FILTER BYTE                      
         BE    FILTKEY2                                                         
         CLI   TXTFILT,0                                                        
         BE    FILTKEY2                                                         
         CLC   TXTFILT(1),BUTCODE                                               
         BNE   NO                                                               
         SPACE 1                                                                
FILTKEY2 CLI   TXTFILT+1,C'*'      CHECK SECOND FILTER BYTE                     
         BE    FILTKEY4                                                         
         CLI   TXTFILT+1,0                                                      
         BE    FILTKEY4                                                         
         CLC   TXTFILT+1(1),BUTCODE+1                                           
         BNE   NO                                                               
         SPACE 1                                                                
FILTKEY4 B     YES                                                              
         SPACE 1                                                                
YES      SR    R1,R1                                                            
         B     *+8                                                              
         SPACE 1                                                                
NO       LA    R1,1                                                             
         LTR   R1,R1                                                            
         B     XIT                                                              
         EJECT                                                                  
*              DIG OUT TEXT LINES                                               
         SPACE 3                                                                
DIGTEXT  NTR1                                                                   
         LA    R4,IO                                                            
         USING BUKEY,R4                                                         
         MVC   TXTTCODE,BUTCODE    PASS BACK ACTUAL TEXT CODE                   
         LA    R6,IO                                                            
         MVI   ELCODE,X'25'                                                     
         MVI   TXTSYM,0            LOOK FOR SYMBOL                              
         BAS   RE,GETEL                                                         
         BNE   DIGTEXT2                                                         
         USING BUFTD,R6                                                         
         MVC   TXTSYM,BUFTSYM                                                   
         SPACE 1                                                                
DIGTEXT2 LA    R6,IO                                                            
         MVI   ELCODE,X'26'                                                     
         L     R2,TXTAAREA                                                      
         SR    R3,R3               COUNT LINES IN R3                            
         BAS   RE,GETEL                                                         
         B     DIGTEXT6                                                         
         SPACE 1                                                                
DIGTEXT4 BAS   RE,NEXTEL                                                        
         SPACE 1                                                                
DIGTEXT6 BNE   XIT                                                              
         USING BUTXTD,R6                                                        
DIGTEXT7 LA    R3,1(R3)                                                         
         STC   R3,TXTNUM                                                        
         ZIC   R1,TXTWIDE                                                       
         LTR   R1,R1                                                            
         BNZ   *+8                                                              
         LA    R1,60                                                            
         BCTR  R1,0                                                             
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R2),SPACES      PRECLEAR LINE                                
*                                                                               
         CLM   R3,1,BUTXTSEQ       TEST AT RIGHT SEQUENCE NUMBER                
         BE    DIGTEXT8            YES                                          
         CLI   TXTMAX,0            TEST FOR MAXIMUM LINES                       
         BE    *+14                                                             
         CLC   TXTNUM,TXTMAX       TEST MAXIMUM LINES REACHED                   
         BE    XIT                                                              
         LA    R2,1(R1,R2)         POINT TO NEXT LINE                           
         B     DIGTEXT7                                                         
         SPACE 1                                                                
DIGTEXT8 ZIC   R5,BUTXTLEN                                                      
         SH    R5,=Y(BUTEXT-BUTXTD+1)                                           
         CR    R5,R1               TEST TEXT LENGTH WIDER THAN LINE             
         BNH   *+6                 NO                                           
         LR    R5,R1               YES-REDUCE TO MAXIMUM                        
         EX    R5,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R2),BUTEXT      THEN MOVE OUT TEXT                           
         CLI   TXTMAX,0            CHECK WE HAVE ENOUGH LINES                   
         BE    DIGTEXT9                                                         
         CLC   TXTNUM,TXTMAX                                                    
         BE    XIT                                                              
         SPACE 1                                                                
DIGTEXT9 LA    R2,1(R1,R2)                                                      
         B     DIGTEXT4                                                         
         EJECT                                                                  
*              DATA MANAGER AIDS                                                
         SPACE 3                                                                
HIGH     NTR1                                                                   
         MVC   KEYSAVE,KEY         BUDDIR CALLS                                 
         MVC   COMMAND,=CL8'DMRDHI'                                             
         B     DIR                                                              
         SPACE 1                                                                
SEQ      NTR1                                                                   
         MVC   COMMAND,=CL8'DMRSEQ'                                             
         SPACE 1                                                                
DIR      GOTO1 DATAMGR,DMCB,(X'C0',COMMAND),=C'BUDDIR',KEY,KEY,0                
         B     DMCHECK                                                          
         SPACE 1                                                                
GETREC   NTR1                                                                   
         LA    R4,KEY                                                           
         USING BUKEY,R4                                                         
         GOTO1 DATAMGR,DMCB,(X'C0',=C'GETREC'),=C'BUDFILE',            X        
               BUKDA,IO,DMWORK                                                  
         SPACE 1                                                                
DMCHECK  CLI   DMCB+8,0                                                         
         BE    XIT                                                              
         DC    H'0'                                                             
         SPACE 1                                                                
GOHOOK   NTR1                                                                   
         L     RF,TXTHOOK                                                       
         LTR   RF,RF                                                            
         BZ    XIT                                                              
         L     RE,USERRD           USERS RD                                     
         LM    R0,RC,20(RE)        RESTORE USERS R0-RC                          
         BASR  RE,RF               RF CONTAINS A(HOOK ROUTINE)                  
         XIT1                                                                   
         SPACE 1                                                                
XIT      XIT1                                                                   
         SPACE 1                                                                
         GETEL (R6),42,ELCODE                                                   
         SPACE 1                                                                
SPACES   DC    CL132' '                                                         
         SPACE 2                                                                
* LITERAL POOL                                                                  
*                                                                               
         LTORG                                                                  
         EJECT                                                                  
*              DSECT TO COVER TEXTIO WORKING STORAGE                            
         SPACE 3                                                                
MODD     DSECT                                                                  
USERRD   DS    A                                                                
DATAMGR  DS    V                                                                
DUB      DS    D                                                                
WORK     DS    CL64                                                             
DMCB     DS    6F                                                               
*                                                                               
ELCODE   DS    CL1                                                              
*                                                                               
KEY      DS    CL64                                                             
KEYSAVE  DS    CL64                                                             
COMMAND  DS    CL8                                                              
DMWORK   DS    CL96                                                             
IO       DS    CL1000                                                           
MODDX    EQU   *                                                                
         EJECT                                                                  
       ++INCLUDE DDCOMFACS                                                      
         EJECT                                                                  
       ++INCLUDE BUTEXTIOD                                                      
         EJECT                                                                  
       ++INCLUDE BUGENFILE                                                      
         SPACE 1                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'011BUTEXTIO  05/01/02'                                      
         END                                                                    
