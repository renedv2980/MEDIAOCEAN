*          DATA SET PPREPZ502B AT LEVEL 044 AS OF 12/27/94                      
*CATALP PPZ502A                                                                 
*        TITLE 'PPREPZ502 - EPIC - INVOICE CONVERSION'                          
         TITLE 'PPREPZ502 - EPIC - INVOICE CONVERSION'                          
         PRINT NOGEN                                                            
***********************************************************************         
*                                                                               
*                                                                               
*        QOPT1      Y= TEST-NO FILE MARKING (DOESNT SET RCWRITE)                
*                   X= TEST-NO AND PRODUCE DATA ERROR REPORT 9                  
*        QOPT2      TRACE A=ALL,E=ERRORS,M=E+MODES,F=M+FIELDS                   
*                         R=M+ADDED RECORDS                                     
*        QOPT3      Y=LIST PREVIOUS CONVERSIONS                                 
*        QOPT4      Y=OK TO REPLACE INVOICES ON FILE                            
*        QOPT5      Y=CREATE IM REQUEST FILE,P=PRINT IT TOO                     
*        QOPT6      Y OR N POST AFFIDS - PASSED ON TO IM REQUEST,               
*                     DOES NOTHING IN THIS PROGRAM                              
*        QOPT7      I=REQUESTOR IS INVOICE ID TO FILTER ON                      
*                                                                               
*        Q2RERUN(6) =  RE-RUN DATE                                              
*                                                                               
***********************************************************************         
*                                                                     *         
*   REGISTER USAGE                                                    *         
*   R0 - OS & WORK                                                    *         
*   R1 - OS & WORK                                                    *         
*   R2 - WORK                                                         *         
*   R3 - WORK                                                         *         
*   R4 - WORK                                                         *         
*   R5 - EZBLOCK BASE                                                 *         
*   R6 - WORK                                                         *         
*   R7 - 2ND BASE                                                     *         
*   R8 - GLOBALD BASE                                                 *         
*   R9 - PPWORKD BASE                                                 *         
*   RA - PPWORK2D BASE                                                *         
*   RB - 1ST BASE                                                     *         
*   RC - PPZ5WRKD                                                     *         
*   RD - STD REG SAVE CHAIN                                           *         
*   RE - OS & WORK                                                    *         
*   RF - OS & WORK                                                    *         
*                                                                     *         
***********************************************************************         
         TITLE 'PPREPZ502 - EPIC - INVOICE CONVERSION - CHANGE LOG'             
***********************************************************************         
*        CHANGE LOG                                                   *         
*                                                                     *         
*BOBY 11/94      CREATION                                             *         
***********************************************************************         
         TITLE 'PPREPZ502 - EPIC - INVOICE CONVERSION - INIT'                   
PPZ502   CSECT                                                                  
         NMOD1 0,*PPZ502*,R7                                                    
*                                                                               
         L     R9,0(R1)                                                         
         USING PPWORKD,R9          ESTABLISH REPORT WORKAREA 1                  
*                                                                               
         MVI   RC2DSECT,C'Y'       INDICATE SECOND WORKAREA PRESENT             
*                                                                               
         L     RA,PPWORK2C         POINT TO SECOND WORKAREA                     
         USING PPWORK2D,RA         ESTABLISH SECOND WORKAREA                    
*                                                                               
         L     R5,=A(EZBLOCKC)     ESTABLISH EZBLOCK                            
         USING EZBLOCKD,R5                                                      
*                                                                               
         TITLE 'PPREPZ502 - EPIC - INVOICE CONVERSION - MODE'                   
***********************************************************************         
*                                                                     *         
*        DETERMINE MODE                                               *         
*                                                                     *         
***********************************************************************         
         SPACE 2                                                                
         CLI   MODE,RUNFRST                                                     
         BE    PRFRST              START OF RUN                                 
*                                                                               
         CLI   MODE,PROCREQ        START OF REQUEST                             
         BE    PROC                                                             
*                                                                               
         CLI   MODE,RUNLAST        END OF RUN                                   
         BE    PRLAST                                                           
*                                                                               
         B     EXIT                IGNORE ALL OTHER MODES                       
*                                                                               
         TITLE 'PPREPZ502 - EPIC - INVOICE CONVERSION - PRFRST'                 
***********************************************************************         
*                                                                     *         
*        FIRST TIME FOR RUN                                           *         
*                                                                     *         
***********************************************************************         
         SPACE 2                                                                
PRFRST   DS    0H                                                               
*                                                                               
         L     RF,VMASTC           DONE IF RUNNING SOON                         
         OC    MCREMPQK-MASTD(,RF),MCREMPQK-MASTD(RF)                           
         BNZ   EXIT                                                             
*                                                                               
*        WRITE OUT DUMMY Z9 REQUEST FILE                                        
*                                                                               
         L     R3,=A(PZZ9RQF)                                                   
         OPEN  ((R3),(OUTPUT))     OPEN Z9 REQUEST FILE                         
*                                                                               
         LTR   RF,RF                                                            
         BZ    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         MVC   P(2),=C'/*'         KEEP DDMASTER HAPPY                          
         MVC   P+2(78),SPACES                                                   
*                                                                               
         PUT   (R3),P                                                           
*                                                                               
         MVC   P(80),SPACES                                                     
*                                                                               
         CLOSE (R3)                CLOSE Z9 REQUEST FILE                        
*                                                                               
         XC    VDRIVER,VDRIVER     INITIALIZE DRIVER                            
         MVC   DPGFILE,=CL8'PPZ505  '                                           
         MVC   DRIVER,=CL8'T00A3A'                                              
*                                                                               
         MVI   XFF,X'FF'                                                        
         MVC   XFF+1(L'XFF-1),XFF                                               
         MVI   DASHES,C'-'                                                      
         MVC   DASHES+1(L'DASHES-1),DASHES                                      
*                                                                               
*        IF RUNNING SOON, NO SECOND STEP OR AUTO REQUESTS *                     
*                                                                               
         L     RF,VMASTC                                                        
         OC    MCREMPQK-MASTD(,RF),MCREMPQK-MASTD(RF)                           
         BNZ   PR1STX                                                           
*                                                                               
         OPEN  (PZIMRQF,(OUTPUT))  OPEN REQUEST FILE                            
*                                                                               
         LTR   RF,RF                                                            
         BZ    *+6                                                              
         DC    H'0'                                                             
*                                                                               
PR1STX   DS    0H                                                               
*                                                                               
         B     EXIT                                                             
*                                                                               
         TITLE 'PPREPZ502 - EPIC - INVOICE CONVERSION - PRLAST'                 
***********************************************************************         
*                                                                     *         
*        LAST  TIME FOR RUN                                           *         
*                                                                     *         
***********************************************************************         
         SPACE 2                                                                
PRLAST   DS    0H                                                               
*                                                                               
*        IF RUNNING SOON, NO SECOND STEP OR AUTO REQUESTS *                     
*                                                                               
         L     RF,VMASTC                                                        
         OC    MCREMPQK-MASTD(,RF),MCREMPQK-MASTD(RF)                           
         BNZ   PRLASTX                                                          
*                                                                               
         MVC   P(2),=C'/*'         KEEP DDMASTER HAPPY                          
         MVC   P+2(78),SPACES                                                   
*                                                                               
         PUT   PZIMRQF,P                                                        
*                                                                               
         MVC   P(80),SPACES                                                     
*                                                                               
         CLOSE PZIMRQF             CLOSE REQUEST FILE                           
*                                                                               
*        NOW SEE IF WE NEED TO RUN Z9 INVOICE FACSIMILE REPORT *                
*                                                                               
         CLI   EZZ9REQ,C'Y'        THEY WANT AUTO Z9 INV FAC REPORT             
         BNE   PRLASTX              NO                                          
*                                                                               
         TM    FIRSTSW,X'08'       WERE ANY INVOICES CONVERTED                  
         BZ    PRLASTX              NO                                          
*                                                                               
         L     R3,=A(PZZ9RQF)                                                   
         OPEN  ((R3),(OUTPUT))                                                  
*                                                                               
         LTR   RF,RF                                                            
         BZ    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         MVC   P,SPACES                                                         
         MVC   P(2),=C'Z9'                                                      
         MVC   P+2(2),EZAGY                                                     
         GOTO1 DATCON,DMCB,(5,0),P+5   USE DATE AS REQUEST NUMBER               
*                                                                               
*        WRITE REQUEST TO FILE                                                  
*                                                                               
         MVC   P+12(52),=C'0203CON 0303REP 0506OV,T/A 0903ALL 1306TODAYC        
               C 1401 *'                                                        
         MVC   P+62(1),CONSEMPL    SAVE SENUM FOR MPL FILE                      
*                                                                               
         PUT   (R3),P                                                           
*                                                                               
         MVC   P(2),=C'/*'         KEEP DDMASTER HAPPY                          
         MVC   P+2(78),SPACES                                                   
*                                                                               
         PUT   (R3),P                                                           
*                                                                               
         MVC   P(80),SPACES                                                     
*                                                                               
         CLOSE (R3)                                                             
*                                                                               
PRLASTX  DS    0H                                                               
         B     EXIT                                                             
*                                                                               
         TITLE 'PPREPZ502 - EPIC - INVOICE CONVERSION - EXIT'                   
***********************************************************************         
*                                                                     *         
*        RETURN TO CALLER                                             *         
*                                                                     *         
***********************************************************************         
         SPACE 2                                                                
EXIT     XIT1                                                                   
*                                                                               
EXITX    DS    0H                                                               
*                                                                               
         L     RF,UTL                                                           
         MVC   4(1,RF),SAVSYS      RESTORE SYSTEM NUM                           
         XIT1                                                                   
*                                                                               
         TITLE 'PPREPZ502 - EPIC - INVOICE CONVERSION - PROC'                   
***********************************************************************         
*                                                                     *         
*        REQUEST FIRST                                                *         
*                                                                     *         
***********************************************************************         
         SPACE 2                                                                
PROC     DS    0H                                                               
*                                                                               
         L     RF,UTL                                                           
         MVC   SAVSYS,4(RF)        SAVE SE NUMBER                               
*                                                                               
         L     R8,=A(GLAREA)                                                    
         USING GLOBALD,R8                                                       
*                                                                               
         XC    HEADHOOK,HEADHOOK                                                
*                                                                               
         CLI   QOPT5,C' '          IM OPTION, BLANK=N                           
         BNE   *+8                                                              
         MVI   QOPT5,C'N'                                                       
*                                                                               
*        INITIALIZE EZBLOCK                                                     
*                                                                               
         LA    RE,EZBLOCKD                                                      
         LA    RF,EZBLOCKL                                                      
         XCEF                                                                   
*                                                                               
         L     RE,=A(WRKRBUFF)                                                  
         ST    RE,EZWKRBUF                                                      
         LH    RF,=AL2(WRKRBEND-WRKRBUFF)                                       
         XCEF                                                                   
*                                                                               
         L     RE,=A(WRKRREC)                                                   
         ST    RE,EZWKRREC                                                      
         LH    RF,=AL2(WRKRRECE-WRKRREC)                                        
         XCEF                                                                   
*                                                                               
         XC    EZWKRIND,EZWKRIND                                                
         MVC   EZWKRIND(2),RCORIGID                                             
*                                                                               
         GOTO1 DATAMGR,DMCB,(0,=C'GFILE'),=C'WRKFIL',EZWKRIND,EZWKRREC,C        
               EZWKRBUF                                                         
*                                                                               
         LA    R1,EZWKRIND                                                      
         USING UKRECD,R1                                                        
*                                                                               
         MVC   EZWKRFIL,UKUSRINF                                                
*                                                                               
         DROP  R1                                                               
*                                                                               
         LA    RF,EZMPROC          HOOK ADDRESS                                 
         ST    RF,EZHOOK                                                        
*                                                                               
         L     RF,=A(IOAREA1)      RECORD AREA                                  
         ST    RF,EZAREC                                                        
*                                                                               
         L     RF,=V(EZRECTAB)     RECORD/FIELD TABLE                           
         ST    RF,EZARECTB                                                      
*                                                                               
         L     RE,=A(PRTABC)       PRODUCT TABLE                                
         ST    RE,EZPRTAB                                                       
         LH    RF,=AL2(PRTABE-PRTABC)                                           
         XCEF                                                                   
         MVC   EZPRTABL,=Y(PRTABL) PROD TABLE LENGTH                            
*                                                                               
         MVC   EZCOMFCS,VCOMFACS                                                
         MVC   EZPRINT,PRINT                                                    
         MVC   EZUTL,UTL                                                        
*                                                                               
         MVC   EZPRNTSE,PRNTSE                                                  
*                                                                               
         MVC   EZWRITE,RCWRITE                                                  
         MVC   EZAGY,QAGENCY                                                    
         MVC   EZPROF2,PROGPROF    Z5 PROFILE                                   
*                                                                               
*        INITIALIZE DRIVER                                                      
*                                                                               
         MVI   ACTVSW,C'N'                                                      
         MVI   FORCEHED,C'Y'                                                    
*                                                                               
         LA    RE,GLOBALD          INIT DRIVER STORAGE TO ZEROS                 
         L     RF,=F'44000'                                                     
         XCEF                                                                   
*                                                                               
         MVC   GLSIZE,=F'44000'    GLOBAL SIZE                                  
         MVC   GLOBALD(8),=C'*GLOBAL*'                                          
*                                                                               
         MVI   GLTRACE,C'N'                                                     
*                                                                               
         GOTO1 LOADER,DMCB,DPGFILE,0    LOAD DPG PROGRAM                        
         ICM   R6,15,DMCB+4                                                     
         BNZ   *+6                                                              
         DC    H'0'                                                             
         ST    R6,GLAPROG                                                       
*                                                                               
         OC    VDRIVER,VDRIVER                                                  
         BNZ   PR14                                                             
*                                                                               
         GOTO1 LOADER,DMCB,DRIVER       LOAD DRIVER                             
         ICM   R6,15,DMCB+4                                                     
         BNZ   *+6                                                              
         DC    H'0'                                                             
         ST    R6,VDRIVER                                                       
*                                                                               
PR14     DS    0H                                                               
*                                                                               
         LA    RE,PPWORKD                                                       
         ST    RE,GLAWORKD         PRINT WORK ADDR                              
*                                                                               
         LA    RE,DRHOOK                                                        
         ST    RE,GLAHOOK          OUR DRIVER HOOK                              
*                                                                               
         MVI   GLTWORKD,GLTPRINT   PRINT WORK                                   
*                                                                               
         MVI   GLDETHED,C'Y'       I GET HEADS AT DETAIL TIME                   
*                                                                               
         LA    RE,PROGPROF                                                      
         ST    RE,GLAPPROF                                                      
*                                                                               
         MVI   GLOPT1,C'Y'         ACTIVATE REPORTS FOR INIT                    
         MVC   GLOPT2(9),GLOPT1                                                 
*                                                                               
         MVI   GLMODE,GLINIT       INITIALIZE DRIVER                            
         MVI   GLBOXOPT,C'Y'       USE BOXES                                    
*                                                                               
         CLI   QOPT2,C'A'          IF TRACING EVERYTHING                        
         BNE   *+8                                                              
         MVI   GLTRACE,C'Y'           SET DRIVER TRACE OPTION                   
*                                                                               
         GOTO1 VDRIVER,DMCB,GLOBALD                                             
*                                                                               
         MVI   GLOPT1,C'N'         DE-ACTIVATE                                  
         MVC   GLOPT2(9),GLOPT1                                                 
*                                                                               
         EJECT                                                                  
*                                  SET OPTIONS                                  
*                                  -----------                                  
*                                                                               
         MVC   EZTEST,QOPT1        TEST OPTION                                  
*                                                                               
         CLI   QOPT1,C'X'          TEST OPTION WITH DATA ERROR REPORT 9         
         BNE   *+8                                                              
         MVI   EZTEST,C'Y'         ALSO MEANS TEST OPTION                       
*                                                                               
         MVI   EZTRACE,0           TRACE OPTION                                 
*                                                                               
         CLI   QOPT2,C'N'          SKIP IF NOT TRACING                          
         BE    PROPTTRX                                                         
         CLI   QOPT2,C' '                                                       
         BE    PROPTTRX                                                         
*                                                                               
         CLI   QOPT2,C'A'                                                       
         BNE   *+8                                                              
         MVI   EZTRACE,X'FF'       TRACE EVERYTHING                             
*                                                                               
         CLI   QOPT2,C'E'                                                       
         BNE   *+8                                                              
         MVI   EZTRACE,X'20'       TRACE ERRORS                                 
*                                                                               
         CLI   QOPT2,C'M'                                                       
         BNE   *+8                                                              
         MVI   EZTRACE,X'60'       TRACE MODES AND ERRORS                       
*                                                                               
         CLI   QOPT2,C'F'                                                       
         BNE   *+8                                                              
         MVI   EZTRACE,X'E0'       TRACE FIELDS AND ERRORS AND MODES            
*                                                                               
         CLI   QOPT2,C'R'                                                       
         BNE   *+8                                                              
         MVI   EZTRACE,X'70'       TRACE MODES, ERRORS, AND ADDED RECS          
*                                                                               
PROPTTRX DS    0H                                                               
*                                                                               
         MVC   PREVOPT,QOPT3       Y=LIST PREVIOUS CONVERTS                     
         MVC   EZRPLOPT,QOPT4      Y=OK TO REPLACE INVOICES                     
*                                                                               
         CLI   QOPT7,C'I'          IF REQUESTOR IS AN INVOICE FILTER            
         BNE   *+10                                                             
         MVC   EZSELINV,QUESTOR       PASS IT TO PZMOD                          
*                                                                               
         XC    RERUN,RERUN         RE-RUN AS OF DATE                            
*                                                                               
         CLI   Q2RERUN,C' '        IF RE-RUN DATE GIVEN                         
         BNH   PR24                                                             
*                                                                               
         OC    Q2RERUN,=6C'0'         USE IT AS RE-RUN DATE                     
*                                                                               
         GOTO1 DATCON,DMCB,Q2RERUN,(1,RERUN)                                    
*                                                                               
         EJECT                                                                  
*                                                                               
*                               READ WORKER FILE INDEXES                        
*                               -------------------------                       
*                               **NOTE- DRIVER INPUT DONE IN EZMPROC            
*                                                                               
PR24     DS    0H                                                               
*                                                                               
         XC    EZWKRIND,EZWKRIND   INITIALIZE INDEX                             
*                                                                               
PRWKRLP  DS    0H                                                               
*                                                                               
*        READ FIRST/NEXT WORKER RECORD                                          
*                                                                               
*                                                                               
         GOTO1 DATAMGR,DMCB,=C'INDEX',EZWKRFIL,EZWKRIND,EZWKRREC,      C        
               EZWKRBUF                                                         
*                                                                               
         TM    DMCB+8,X'80'        TEST EOF                                     
         BO    PRWKRDN                                                          
*                                                                               
         LA    R4,EZWKRIND         ESTABLISH WORKER RECORD                      
         USING UKRECD,R4                                                        
*                                                                               
         CLI   UKDAY,X'98'         SKIP IF NOT AN EPIC FILE                     
         BNE   PRWKRCN                                                          
*                                                                               
         TM    UKSTAT,X'08'        SKIP FILES ON KEEP                           
         BNO   PRWKR34                                                          
*                                                                               
         OC    RERUN,RERUN         UNLESS DOING A RERUN                         
         BNZ   PRWKR34                                                          
*                                                                               
         CLI   PREVOPT,C'Y'        UNLESS SHOWING PREVIOUS CONVERTS             
         BNE   PRWKRCN                                                          
*                                                                               
PRWKR34  DS    0H                                                               
*                                                                               
         USING EZWKRIXD,R4                                                      
*                                                                               
         CLC   EZWIUID,RCORIGID    TEST RIGHT ID                                
         BNE   PRWKRCN                                                          
*                                                                               
*        READ FIRST RECORD                                                      
*                                                                               
         MVC   SVCIADDR,EZWKRIND+UKCIADDR-UKRECD                                
*                                                                               
         GOTO1 DATAMGR,DMCB,=C'READ',EZWKRFIL,EZWKRIND,EZWKRREC,       X        
               EZWKRBUF                                                         
*                                                                               
         TM    DMCB+8,X'80'        IF EOF ON FIRST READ                         
         BO    PRWKRRDX               SKIP- GET NEXT INDEX                      
*                                                                               
         L     R3,EZWKRBUF         POINT TO FOUND RECORD                        
         MVC   SVWFREC0,0(R3)      SAVE 64 BYTE DESCRIPTOR REC                  
*                                                                               
         USING W_RECD,R3           ESTABLISH WORKER RECORD                      
*                                                                               
*        FILTER ON DATES IF NEEDED                                              
*                                                                               
         CLC   QSTART(12),SPACES   ANY REQUEST DATE                             
         BNH   PRWKRDTX             NO                                          
*                                                                               
         GOTO1 DATCON,DMCB,(2,W_AGELD),(0,WORK) CONVERT DATES                   
*                                                                               
         CLC   WORK(6),QSTART                                                   
         BL    PRWKRRDX             BYPASS                                      
         CLC   WORK(6),QEND                                                     
         BH    PRWKRRDX             BYPASS                                      
*                                                                               
PRWKRDTX DS   0H                                                                
*                                                                               
         MVI   UNCONVSW,C'N'       CLEAR UNCONVERTED SW                         
         MVI   CONVSW,C'N'         AND CONVERTED THIS TIME SW                   
*                                                                               
         L     RE,EZWKRBUF                                                      
         LA    RF,W_DESC-W_RECD(,RE)                                            
         MVC   EZSRCE,EZWCSRCE-EZWKRCMD(RF)                                     
*                                                                               
         GOTO1 =V(PZMOD),DMCB,EZBLOCKD                                          
*                                                                               
         CLI   CONVSW,C'Y'         TEST ANY CONVERTED THIS TIME                 
         BNE   PRWKRRDX            NO- NEXT INDEX                               
         CLI   EZTEST,C'Y'         TEST SUPPRESSING FILE MARKING                
         BE    PRWKRRDX             YES- SKIP                                   
         CLI   EZWRITE,C'Y'        WRITE TO FILE                                
         BNE   PRWKRRDX             NO- SKIP                                    
*                                                                               
         XC    WORK,WORK           BUILD COMMENT IN WORK                        
*                                                                               
         MVC   WORK(16),SVWFREC0+48  FROM SAVED COMMENT                         
*                                                                               
         MVI   WORK,X'80'          STOP EZMOD DROPPING BLANKS                   
*                                                                               
         GOTO1 DATCON,DMCB,(5,0),(1,WORK+1)  TODAY'S DATE PWOS                  
*                                                                               
         CLI   UNCONVSW,C'Y'       TEST ANY REMAIN UNCONVERTED                  
         BE    *+8                                                              
         OI    WORK,X'40'          SET FILE COMPLETELY CONVERTED                
*                                                                               
         L     RF,EZWKRREC         SET COMMENT                                  
         XC    0(96,RF),0(RF)                                                   
         LA    R3,28(RF)                                                        
*                                                                               
         USING W_RECD,R3                                                        
         MVC   W_DESC,WORK                                                      
*                                                                               
         GOTO1 DATAMGR,DMCB,=C'COMMENT',EZWKRFIL,EZWKRIND,EZWKRREC,    X        
               EZWKRBUF                                                         
*                                                                               
         CLI   UNCONVSW,C'Y'       TEST ANY REMAIN UNCONVERTED                  
         BE    PRWKRRDX                                                         
*                                                                               
         GOTO1 DATAMGR,DMCB,=C'KEEP',EZWKRFIL,EZWKRIND,EZWKRREC,       X        
               EZWKRBUF                                                         
*                                                                               
PRWKRRDX DS    0H                                                               
*                                                                               
*        RESET FILE POINTERS                                                    
*                                                                               
         OI    EZWKRIND+UKFLAG-UKRECD,UKFLDAT                                   
         MVC   EZWKRIND+UKCIADDR-UKRECD(2),SVCIADDR                             
*                                                                               
         GOTO1 DATAMGR,DMCB,=C'INDEX',EZWKRFIL,EZWKRIND,EZWKRREC,      C        
               EZWKRBUF                                                         
*                                                                               
         TM    DMCB+8,X'80'        TEST EOF                                     
         BZ    *+6                                                              
         DC    H'0'                                                             
*                                                                               
PRWKRCN  DS    0H                                                               
*                                                                               
         B     PRWKRLP             NEXT INDEX                                   
*                                                                               
PRWKRDN  DS    0H                                                               
*                                                                               
         DROP  R3,R4                                                            
*                                  DRIVER OUTPUT PHASE                          
         MVI   FORCEHED,C'Y'                                                    
         MVI   GLMODE,GLOUTPUT                                                  
*                                                                               
         GOTO1 VDRIVER,DMCB,GLOBALD                                             
*                                                                               
         B     EXITX                                                            
         TITLE 'PPREPZ502 - EPIC - INVOICE CONVERSION - EZMPROC'                
***********************************************************************         
*                                                                     *         
*        EZMOD PROCESSING ROUTINE                                     *         
*                                                                     *         
***********************************************************************         
         SPACE 2                                                                
         DS    0D                                                               
EZMPROC  NTR1                                                                   
*                                                                               
         CLI   EZMODE,EZERRP       PROCESS ERROR                                
         BE    EZMP30                                                           
*                                                                               
         CLI   EZMODE,EZSPTP       SPOT DETAIL                                  
         BE    EZMP16                                                           
*                                                                               
         CLI   EZMODE,EZINVL       INVOICE LAST                                 
         BNE   EZMP10                                                           
*                                                                               
         TM    EZIHCVST,EZIHCDEL   TEST DELETED INVOICE                         
         BO    EZMP80               PRINT AS REPORT 10                          
*                                                                               
         TM    EZIHCVST,EZIHCVQ    TEST ALREADY CONVERTED                       
         BZ    EZMP10               NO- TRY TO CONVERT                          
         TM    EZIHCVST,EZIHRCVQ   TEST RECONVERT                               
         BO    EZMP10               YES, TRY TO CONVERT                         
*                                                                               
         OC    RERUN,RERUN         YES- DO WE HAVE RERUN DATE                   
         BZ    EZMP26               NO- PUT ON PREVIOUS LIST                    
         CLC   EZIHCVDT,RERUN      YES- TEST = CONVERTED DATE                   
         BNE   EZMP26              NO- PUT ON PREVIOUS LIST                     
         B     EZMP21                                                           
*                                                                               
EZMP10   CLI   EZMODE,EZINVP       PROC INV                                     
         BNE   EZMP16                                                           
*                                                                               
         TM    EZIHCVST,EZIHCDEL   TEST DELETED INVOICE                         
         BO    EZMP90               BYPASS                                      
*                                                                               
         TM    EZIHCVST,EZIHCVQ    TEST ALREADY CONVERTED                       
         BZ    EZMP20               NO- TRY TO CONVERT                          
         TM    EZIHCVST,EZIHRCVQ   TEST RECONVERT                               
         BO    EZMP20               YES, TRY TO CONVERT                         
         OC    RERUN,RERUN         YES- DO WE HAVE RERUN DATE                   
         BZ    EZMP26              NO- PUT ON PREVIOUS LIST                     
         CLC   EZIHCVDT,RERUN      YES- TEST = CONVERTED DATE                   
         BNE   EZMP26               NO- PUT ON PREVIOUS LIST                    
         B     EZMP21              ELSE RE-CONVERT                              
*                                                                               
EZMP16   TM    EZIHCVST,EZIHCDEL   TEST DELETED INVOICE                         
         BO    EZMP90               BYPASS                                      
         TM    EZIHCVST,EZIHCVQ    TEST ALREADY CONVERTED                       
         BZ    EZMP20               NO- PROCESS SPOT                            
         TM    EZIHCVST,EZIHRCVQ   TEST RECONVERT                               
         BO    EZMP20               YES, PROCESS SPOT                           
*                                                                               
         OC    RERUN,RERUN         UNLESS DOING A RERUN                         
         BZ    EZMP90               NO- EXIT                                    
*                                                                               
         CLC   EZIHCVDT,RERUN      YES- TEST = CONVERTED DATE                   
         BNE   EZMP90               NO- EXIT                                    
         B     EZMP21                                                           
*                                                                               
*                                  ELSE PROCESS SPOT                            
*                                                                               
EZMP20   DS    0H                                                               
         OC    RERUN,RERUN         DO WE HAVE RERUN DATE                        
         BNZ   EZMP90               YES, UNCONVERTED/RECONV BYPASSED            
*                                                                               
EZMP21   DS    0H                                                               
*                                                                               
         GOTO1 =V(PZADDINV),DMCB,EZBLOCKD,PPWORKD                               
*                                                                               
         CLI   EZMODE,EZSPTP       IGNORE UNLESS DETAIL ELEMENT                 
         BNE   EZMP90                                                           
*                                                                               
         CLI   EZERRCD,0           IF PZADDINV RETURN ERROR                     
         BNE   EZMP24              INVOICE WAS NOT CONVERTED                    
*                                                                               
*                                  REPORT 1 - CONVERTED INVOICE                 
         MVI   GLOPT1,C'Y'                                                      
         MVI   RPTNO,1                                                          
         MVI   ACTVSW,C'Y'         SET HAVE ACTIVITY                            
         MVI   CONVSW,C'Y'         SET HAVE DONE A CONVERSION                   
*                                                                               
         OI    FIRSTSW,X'08'       SET ON CONVERTED INVOICES SW                 
*                                                                               
         CLI   QOPT1,C'X'          X= TEST-NO FILE MARKING                      
         BE    *+16                 NO IM REQUESTS                              
         CLI   QOPT1,C'Y'          Y= TEST-NO FILE MARKING                      
         BE    *+8                  NO IM REQUESTS                              
         BAS   RE,GENIMRQ          GENERATE IM REQUEST                          
*                                                                               
         MVI   GLMODE,GLINPUT                                                   
*                                                                               
         GOTO1 VDRIVER,DMCB,GLOBALD    PRINT REPORT                             
*                                                                               
         MVI   GLOPT1,C'N'         DE-ACTIVATE                                  
         B     EZMP90                                                           
*                                  REPORT 2 - UNCONVERTED                       
EZMP24   DS    0H                                                               
*                                                                               
         MVI   UNCONVSW,C'Y'       SET HAVE UN-CONVERTED                        
         MVI   GLOPT2,C'Y'                                                      
         MVI   RPTNO,2                                                          
         MVI   ACTVSW,C'Y'         SET HAVE ACTIVITY                            
         MVI   GLMODE,GLINPUT                                                   
*                                                                               
         GOTO1 VDRIVER,DMCB,GLOBALD                                             
*                                                                               
         MVI   GLOPT2,C'N'         DE-ACTIVATE                                  
         B     EZMP90                                                           
*                                  REPORT 3 - PREVIOUSLY CONVERTED              
EZMP26   DS    0H                                                               
*                                                                               
         CLI   EZMODE,EZINVL       DON'T PROC UNTIL INV LAST                    
         BNE   EZMP90                                                           
*                                                                               
         CLI   PREVOPT,C'Y'        TEST DOING PREVIOUS REPORT                   
         BNE   EZMP28              NO- SKIP                                     
*                                                                               
         MVI   GLOPT3,C'Y'                                                      
         MVI   RPTNO,3                                                          
         MVI   ACTVSW,C'Y'         SET HAVE ACTIVITY                            
         MVI   GLMODE,GLINPUT                                                   
*                                                                               
         GOTO1 VDRIVER,DMCB,GLOBALD                                             
*                                                                               
         MVI   GLOPT3,C'N'         DE-ACTIVATE                                  
*                                                                               
EZMP28   DS    0H                                                               
*                                                                               
         MVI   EZMODE,EZINVL       SET TO SKIP PROC OF INVOICE                  
         B     EZMP90                                                           
*                                  HANDLE ALL ERRORS                            
*                                  -----------------                            
EZMP30   DS    0H                                                               
         TM    EZIHCVST,EZIHCDEL   TEST DELETED INVOICE                         
         BO    EZMP90               BYPASS                                      
*                                                                               
         CLI   EZERRCD,EZERRRNF    TEST RECORD NOT FOUND                        
         BE    EZMP34                                                           
         CLI   EZERRCD,EZERRSCD    OR SPOTPAK CODE ERROR                        
         BNE   EZMP70              ELSE- MUST BE DATA ERROR                     
*                                                                               
EZMP34   DS    0H                                                               
         CLI   EZRNFRPT,C'Y'       TEST A REPEATED REC NOT FOUND                
         BE    EZMP90              YES- SKIP                                    
         ICM   R2,15,EZERRFAD      A(FIELD IN ERROR)                            
         LA    R0,EZIHAVNM         TEST ADVERTISER FIELD                        
         CR    R2,R0                                                            
         BE    EZMP36                                                           
         LA    R0,EZIHAVCD                                                      
         CR    R2,R0                                                            
         BNE   EZMP40                                                           
*                                                                               
EZMP36   DS    0H                  REPORT 4 - MISSING CLIENTS                   
         MVI   GLOPT4,C'Y'                                                      
         MVI   RPTNO,4                                                          
         MVI   ACTVSW,C'Y'         SET HAVE ACTIVITY                            
         MVI   GLMODE,GLINPUT                                                   
         GOTO1 VDRIVER,DMCB,GLOBALD                                             
         MVI   GLOPT4,C'N'         DE-ACTIVATE                                  
         B     EZMP90                                                           
*                                                                               
EZMP40   DS    0H                                                               
         LA    R0,EZIHPRNM         TEST PRODUCT FIELD                           
         CR    R2,R0                                                            
         BE    EZMP44                                                           
         LA    R0,EZIHPRCD                                                      
         CR    R2,R0                                                            
         BE    EZMP44                                                           
         BNE   EZMP74                                                           
*                                                                               
EZMP44   DS    0H                  REPORT 5 - MISSING PRODUCTS                  
         MVI   GLOPT5,C'Y'                                                      
         MVI   RPTNO,5                                                          
         MVI   ACTVSW,C'Y'         SET HAVE ACTIVITY                            
         MVI   GLMODE,GLINPUT                                                   
         GOTO1 VDRIVER,DMCB,GLOBALD                                             
         MVI   GLOPT5,C'N'         DE-ACTIVATE                                  
         B     EZMP90                                                           
*                                  REPORT 9 - DATA ERRORS                       
EZMP70   CLI   QOPT1,C'X'          PRODUCE DATA ERROR REPORT                    
         BNE   EZMP90               NO                                          
*                                                                               
EZMP74   DS    0H                                                               
         MVI   GLOPT9,C'Y'                                                      
         MVI   RPTNO,9                                                          
         MVI   ACTVSW,C'Y'         SET HAVE ACTIVITY                            
         MVI   GLMODE,GLINPUT                                                   
         GOTO1 VDRIVER,DMCB,GLOBALD                                             
         MVI   GLOPT9,C'N'         DE-ACTIVATE                                  
         B     EZMP90                                                           
*                                                                               
*                                  REPORT 10 - DELETED INVOICES                 
EZMP80   DS    0H                                                               
         MVI   GLOPT10,C'Y'                                                     
         MVI   RPTNO,10                                                         
         MVI   ACTVSW,C'Y'         SET HAVE ACTIVITY                            
         MVI   GLMODE,GLINPUT                                                   
         GOTO1 VDRIVER,DMCB,GLOBALD                                             
         MVI   GLOPT10,C'N'        DE-ACTIVATE                                  
*                                                                               
EZMP90   DS    0H                                                               
*                                                                               
         XIT1                                                                   
*                                                                               
         TITLE 'PPREPZ502 - EPIC - INVOICE CONVERSION - GENIMRQ'                
***********************************************************************         
*                                                                     *         
*        GENERATE IM REQUESTS                                         *         
*                                                                     *         
***********************************************************************         
         SPACE 2                                                                
         DS    0D                                                               
GENIMRQ  NTR1                                                                   
*                                                                               
         CLI   QOPT5,C'N'          TEST OPTION TO DO IM'S                       
         BE    GENIMRQX                                                         
*                                                                               
*        IF RUNNING SOON, NO SECOND STEP OR AUTO REQUESTS *                     
*                                                                               
         L     RF,VMASTC                                                        
         OC    MCREMPQK-MASTD(,RF),MCREMPQK-MASTD(RF)                           
         BNZ   GENIMRQX                                                         
*                                                                               
*        BUILD SPOOF TYPE REQUEST                                               
*                                                                               
         MVC   P,SPACES                                                         
         MVC   P(2),=C'IM'                                                      
         MVC   P+2(2),EZAGY                                                     
         GOTO1 DATCON,DMCB,(5,0),P+5  TODAY'S DATE AS REQUEST NUMBER            
*                                                                               
         LA    R1,P+12             POINT TO START OF REQUEST FIELDS             
*                                                                               
*        RECORD FIELD                                                           
*                                                                               
         MVC   0(2,R1),=C'02'      FIELD ID                                     
         MVC   2(2,R1),=C'07'      FIELD LENGTH                                 
         MVC   4(7,R1),=CL7'INVOICE'   FIELD DATA                               
         LA    R1,4+1+7(R1)        POINT TO NEXT FIELD - P+24                   
*                                                                               
*        ACTION FIELD                                                           
*                                                                               
         MVC   0(2,R1),=C'03'      FIELD ID                                     
         MVC   2(2,R1),=C'06'      FIELD LENGTH                                 
         MVC   4(6,R1),=CL7'REPORT'   FIELD DATA                                
         LA    R1,4+1+6(R1)        POINT TO NEXT FIELD - P+35                   
*                                                                               
*        PRINT FIELD                                                            
*                                                                               
         MVC   0(2,R1),=C'05'      FIELD ID                                     
         MVC   2(2,R1),=C'08'      FIELD LENGTH                                 
         MVC   4(8,R1),=CL8'SOON,T/A'   FIELD DATA                              
         LA    R1,4+1+8(R1)        POINT TO NEXT FIELD - P+48                   
*                                                                               
*        MEDIA FIELD                                                            
*                                                                               
         MVC   0(2,R1),=C'09'      FIELD ID                                     
         MVC   2(2,R1),=C'01'      FIELD LENGTH                                 
         MVC   4(1,R1),QMEDIA      FIELD DATA                                   
         LA    R1,4+1+1(R1)        POINT TO NEXT FIELD - P+54                   
*                                                                               
*        CLIENT FIELD                                                           
*                                                                               
         MVC   0(2,R1),=C'10'      FIELD ID                                     
         MVC   2(2,R1),=C'03'      FIELD LENGTH                                 
         MVC   4(3,R1),EZIHLADC    FIELD DATA                                   
         LA    R1,4+1+3(R1)        POINT TO NEXT FIELD - P+62                   
*                                                                               
*        PRODUCT FIELD                                                          
*                                                                               
         MVC   0(2,R1),=C'11'      FIELD ID                                     
         MVC   2(2,R1),=C'03'      FIELD LENGTH                                 
         MVC   4(3,R1),EZIHLPRC    FIELD DATA                                   
         CLC   EZIHLPRC,SPACES     IF NO PRODUCT GIVEN                          
         BNE   *+10                                                             
         MVC   4(3,R1),=CL3'ALL'      REPLACE WITH 'ALL'                        
         LA    R1,4+1+3(R1)        POINT TO NEXT FIELD - P+70                   
*                                                                               
         PUT   PZIMRQF,P           WRITE OUT FIRST RECORD OF REQUEST            
*                                                                               
         CLI   QOPT5,C'P'          TEST TO PRINT ALSO                           
         BNE   GENIMRQ1                                                         
*                                                                               
         GOTO1 REPORT              WRITE OUT FIRST RECORD OF REQUEST            
*                                                                               
GENIMRQ1 DS    0H                                                               
*                                                                               
         MVC   P,SPACES                                                         
         MVC   P(2),=C'IM'                                                      
         MVC   P+2(2),EZAGY                                                     
         GOTO1 DATCON,DMCB,(5,0),P+5  TODAY'S DATE AS REQUEST NUMBER            
*                                                                               
         LA    R1,P+12             POINT TO START OF REQUEST FIELDS             
*                                                                               
*        PUB FIELD                                                              
*                                                                               
         MVC   0(2,R1),=C'12'      FIELD ID                                     
         MVC   2(2,R1),=C'13'      FIELD LENGTH                                 
         MVC   4(13,R1),EZSNPUB    FIELD DATA                                   
         LA    R1,4+1+13(R1)       POINT TO NEXT FIELD - P+30                   
*                                                                               
*        PERIOD FIELD                                                           
*                                                                               
         MVC   0(2,R1),=C'13'      FIELD ID                                     
*                                                                               
         CLC   EZIHDSDT,SPACES     SKIP IF NO INVOICE START DATE                
         BNH   GENIMRQ2                                                         
*                                                                               
         LA    RF,4(R1)            PERIOD FIELD START                           
         MVC   0(8,RF),EZIHDSDT    INVOICE START DATE                           
*                                                                               
         LA    R0,8                MAX LENGTH OF DATE                           
         LA    RF,7(RF)            POINT TO LAST OF START DATE                  
*                                                                               
         CLI   0(RF),C' '          FIND END OF DATE                             
         BH    *+12                                                             
         BCTR  RF,0                BACK UP A BYTE                               
         BCT   R0,*-10                                                          
         DC    H'0'                SHOULD NOT BE HERE                           
*                                                                               
         MVI   1(RF),C'-'          SET DATE SEPARATOR                           
*                                                                               
         MVC   1(8,RF),EZIHDEDT    MOVE IN INVOICE END DATE                     
*                                                                               
         LA    R0,8                MAX LENGTH OF DATE                           
         LA    RF,7(RF)            POINT TO LAST OF START DATE                  
*                                                                               
         CLI   0(RF),C' '          FIND END OF DATE                             
         BH    *+12                                                             
         BCTR  RF,0                BACK UP A BYTE                               
         BCT   R0,*-10                                                          
         DC    H'0'                SHOULD NOT BE HERE                           
*                                                                               
         LA    RF,1(RF)            POINT TO END OF DATE                         
         SR    RF,R1               FIELD LENGTH                                 
         SH    RF,=H'4'            MINUS HEADER LENGTH                          
         CVD   RF,DUB              MAKE CHARACTER                               
         OI    DUB+7,X'0F'         FORCE SIGN                                   
         UNPK  2(2,R1),DUB         SET FIELD LENGTH IN CHARACTER                
*                                                                               
         LA    R1,4(RF,R1)         POINT TO END OF FIELD P+52                   
*                                                                               
         MVI   0(R1),C'*'          END OF REQUEST MARKER                        
*                                                                               
         B     GENIMRQ3                                                         
*                                                                               
GENIMRQ2 DS    0H                                                               
*                                                                               
*        USE MONTH OF SERVICE START AND END AS INVOICE DATES                    
*                                                                               
         XC    WORK,WORK                                                        
         LA    R4,WORK                                                          
         USING PERVALD,R4          ESTABLISH PERVAL WORKAREA                    
*                                                                               
         GOTO1 VPERVAL,DMCB,(6,EZIHDMOS),(R4) VALIDATE PERIOD                   
*                                                                               
         CLI   4(R1),0             CHECK FOR ERRORS                             
         BNE   GENIMRQ3                                                         
*                                                                               
         LA    RF,4(R1)                                                         
         MVC   0(17,RF),PVALCPER   START AND END OF MONTH OF SERVICE            
*                                                                               
         LA    R0,17               MAX LENGTH OF PERIOD                         
         LA    RF,16(RF)           POINT TO LAST OF PERIOD                      
*                                                                               
         CLI   0(RF),C' '          FIND END OF PERIOD                           
         BH    *+12                                                             
         BCTR  RF,0                BACK UP A BYTE                               
         BCT   R0,*-10                                                          
         DC    H'0'                SHOULD NOT BE HERE                           
*                                                                               
         LA    RF,1(RF)            POINT TO END OF PERIOD                       
         SR    RF,R1               FIELD LENGTH                                 
         SH    RF,=H'4'            MINUS HEADER LENGTH                          
         CVD   RF,DUB              MAKE CHARACTER                               
         OI    DUB+7,X'0F'         FORCE SIGN                                   
         UNPK  2(2,R1),DUB         SET FIELD LENGTH IN CHARACTER                
*                                                                               
         LA    R1,4(RF,R1)         POINT TO END OF FIELD P+52                   
*                                                                               
         MVI   0(R1),C'*'          END OF REQUEST MARKER                        
*                                                                               
GENIMRQ3 DS    0H                                                               
*                                                                               
         GOTO1 REPORT              WRITER SECOND CARD OF REQUEST                
*                                                                               
         PUT   PZIMRQF,P                                                        
*                                                                               
         CLI   QOPT5,C'P'          TEST TO PRINT ALSO                           
         BNE   GENIMRQ5                                                         
*                                                                               
         GOTO1 REPORT                                                           
*                                                                               
GENIMRQ5 DS    0H                                                               
*                                                                               
GENIMRQX DS    0H                                                               
         XIT1                                                                   
*                                                                               
         TITLE 'PPREPZ502 - EPIC - INVOICE CONVERSION - DRHOOK'                 
***********************************************************************         
*                                                                     *         
*        DRIVER HOOK ROUTINE                                          *         
*                                                                     *         
***********************************************************************         
         SPACE 2                                                                
         DS    0D                                                               
DRHOOK   NTR1                                                                   
*                                                                               
*        RESOLVE MODE                                                           
*                                                                               
         CLI   GLHOOK,GLROUT       TEST TO EXECUTE USER ROUTINE                 
         BE    DRHROUT                                                          
*                                                                               
         CLI   GLHOOK,GLRESOLV     TEST TO RESOLVE LABELS                       
         BE    DRHRSLV                                                          
*                                                                               
         CLI   GLHOOK,GLRESLIT     TEST TO RESOLVE LITERAL                      
         BE    DRHRSLT                                                          
*                                                                               
         CLI   GLHOOK,GLHEAD       HEADLINES                                    
         BE    DRHHEAD                                                          
*                                                                               
         CLI   GLHOOK,GLPUTSRT     ABOUT TO PUT RECORD TO SORT                  
         BE    DRHPSRT                                                          
*                                                                               
         XIT1                                                                   
*                                                                               
         TITLE 'PPREPZ502 - EPIC - INVOICE CONVERSION - DRHRSLV'                
***********************************************************************         
*                                                                     *         
*        RESOLVE ROUTINE LABELS                                       *         
*                                                                     *         
***********************************************************************         
         SPACE 2                                                                
DRHRSLV  LA    R1,ROUTLIST                                                      
         LA    R2,GLLABEL                                                       
*                                                                               
DH020    CLI   0(R1),X'FF'                                                      
         BE    DRHRSLVX                                                         
         CLC   0(8,R1),0(R2)                                                    
         BE    DH030                                                            
         LA    R1,12(R1)                                                        
         B     DH020                                                            
*                                                                               
DH030    MVC   GLAROUT,8(R1)                                                    
*                                                                               
DRHRSLVX DS    0H                                                               
         XIT1                                                                   
         SPACE 2                                                                
ROUTLIST DS    0F                                                               
*                                  (W=WRITE TO DRIVER, R=READ)                  
         DC    C'WBPUB   ',A(WBPUB)                                             
         DC    C'WBDATE  ',A(WBDATE)                                            
         DC    C'WCDATE  ',A(WCDATE)                                            
         DC    C'WICLTN  ',A(WICLTN)                                            
         DC    C'WIPRDN  ',A(WIPRDN)                                            
         DC    C'WINVNO  ',A(WINVNO)                                            
         DC    C'WMOS    ',A(WMOS)                                              
         DC    C'WIEST   ',A(WIEST)                                             
         DC    C'WIGRS   ',A(WIGRS)                                             
         DC    C'WSRC    ',A(WSRC)                                              
         DC    C'WCMNT   ',A(WCMNT)                                             
         DC    C'WNCREAS ',A(WNCREAS)                                           
         DC    C'WERRCD  ',A(WERRCD)                                            
         DC    C'WERRREC ',A(WERRREC)                                           
         DC    C'WERRFLD ',A(WERRFLD)                                           
         DC    C'WERRMSG ',A(WERRMSG)                                           
         DC    C'WERRDTA ',A(WERRDTA)                                           
         DC    C'RBPUB   ',A(RBPUB)                                             
         DC    C'RBDATE  ',A(RBDATE)                                            
         DC    X'FF'                                                            
*                                                                               
         TITLE 'PPREPZ502 - EPIC - INVOICE CONVERSION - DRHROUT'                
***********************************************************************         
*                                                                     *         
*        EXECUTE DRIVER ROUTINE                                       *         
*                                                                     *         
***********************************************************************         
         SPACE 2                                                                
DRHROUT  DS    0H                                                               
*                                                                               
*        EXECUTE DRIVER ROUTINE                                                 
*                                                                               
         CLI   GLMODE,GLINPUT      YES - INPUT PHASE                            
         BE    DRHIN                                                            
*                                                                               
         CLI   GLMODE,GLOUTPUT           OUTPUT PHASE                           
         BE    DRHOUT                                                           
*                                                                               
         DC    H'0'                SHOULD NOT BE HERE                           
*                                                                               
*        EXECUTE INPUT PHASE ROUTINES                                           
*                                                                               
DRHIN    L     RF,GLAROUT                                                       
         L     R3,GLAIFLD                                                       
         BASR  RE,RF                                                            
         XIT1                                                                   
*                                                                               
*        EXECUTE OUTPUT PHASE ROUTINES                                          
*                                                                               
DRHOUT   DS    0H                                                               
         L     RF,GLAROUT                                                       
         L     R2,GLAIFLD                                                       
         L     R3,GLAOFLD                                                       
         LTR   R3,R3               IF OUTPUT FIELD NOT GIVEN                    
         BNZ   *+8                                                              
         LA    R3,WORK             USE WORK                                     
         BASR  RE,RF                                                            
         XIT1                                                                   
         TITLE 'PPREPZ502 - EPIC - INVOICE CONVERSION - INPUT ROUTINES'         
***********************************************************************         
*                                                                     *         
*        INPUT ROUTINES                                               *         
*                                                                     *         
***********************************************************************         
         SPACE 2                                                                
WBPUB    DS    0H                  BATCH STATION                                
*                                                                               
         MVC   0(6,R3),EZSNPUB     INTERNAL PUB ID                              
         MVC   6(20,R3),EZSNANM1   PUB NAME                                     
         MVC   26(20,R3),EZSNANM2  PUB NAME - SECOND PART                       
*                                                                               
         XIT1                                                                   
*                                                                               
WBDATE   DS    0H                  BATCH DATE                                   
         GOTO1 DATCON,DMCB,(3,EZBTBRDT),(1,0(R3))   PWOS                        
*                                                                               
         XIT1                                                                   
*                                                                               
WICLTN   DS    0H                  INVOICE CLIENT NAME                          
         MVC   0(35,R3),SPACES                                                  
         MVC   0(20,R3),EZIHLADN                                                
         CLI   EZIHLADN,C' '                                                    
         BH    WICLTN1                                                          
         MVC   0(20,R3),EZIHAVNM   TAPE NAME                                    
         CLI   EZIHAVNM,C' '                                                    
         BH    WICLTNX                                                          
         MVC   0(9,R3),=C'*MISSING*'                                            
         B     WICLTNX                                                          
*                                                                               
WICLTN1  DS    0H                                                               
*                                                                               
         LA    RF,20(R3)           FLOAT CODE AS (XXX)                          
         CLI   0(RF),C' '                                                       
         BH    *+8                                                              
         BCT   RF,*-8                                                           
         MVI   2(RF),C'('                                                       
         MVC   3(3,RF),EZIHLADC                                                 
         CLC   3(3,RF),SPACES      IF NO CODE                                   
         BH    *+10                                                             
         MVC   3(3,RF),=C'***'     SET TO ***                                   
         LA    RE,6(RF)                                                         
         CLI   5(RF),C' '                                                       
         BH    *+6                                                              
         BCTR  RE,R0                                                            
         MVI   0(RE),C')'                                                       
*                                                                               
         OC    0(35,R3),SPACES                                                  
*                                                                               
WICLTNX  DS    0H                                                               
         XIT1                                                                   
         SPACE 2                                                                
WIPRDN   DS    0H                  PRODUCT NAME                                 
         MVC   0(35,R3),SPACES                                                  
         CLI   GLRECNO,5           IF MISSING PRD REPORT                        
         BNE   WIPRDN4                                                          
*                                                                               
         MVC   0(25,R3),EZERRDTA   USE ERROR FIELD                              
         B     WIPRDNX                                                          
*                                                                               
WIPRDN4  DS    0H                                                               
         MVC   0(20,R3),EZIHLPRN   ELSE USE INVOICE PRD                         
         CLI   0(R3),C' '                                                       
         BH    WIPRDN8                                                          
         MVC   0(20,R3),EZIHPRNM   ELSE USE INVOICE TAPE PRD                    
         CLI   0(R3),C' '                                                       
         BH    WIPRDNX                                                          
         MVC   0(20,R3),EZSPLPRN   DETAIL LOOKUP                                
         CLI   0(R3),C' '                                                       
         BH    WIPRDNX                                                          
         MVC   0(20,R3),EZSPPRNM   DETAIL SENT IN NAME                          
         CLI   0(R3),C' '                                                       
         BH    WIPRDNX                                                          
         MVC   0(9,R3),=C'*MISSING*'                                            
         B     WIPRDNX                                                          
*                                                                               
WIPRDN8  DS    0H                                                               
*                                                                               
         LA    RF,20(R3)           FLOAT CODE AS (XXX)                          
         CLI   0(RF),C' '                                                       
         BH    *+8                                                              
         BCT   RF,*-8                                                           
         MVI   2(RF),C'('                                                       
         MVC   3(3,RF),EZIHLPRC                                                 
         CLC   3(3,RF),SPACES      IF NO CODE                                   
         BH    *+10                                                             
         MVC   3(3,RF),=C'***'     SET TO ***                                   
         LA    RE,6(RF)                                                         
         CLI   5(RF),C' '                                                       
         BH    *+6                                                              
         BCTR  RE,R0                                                            
         MVI   0(RE),C')'                                                       
*                                                                               
         OC    0(35,R3),SPACES     FORCE UPPERCASE                              
*                                                                               
WIPRDNX  DS    0H                                                               
         XIT1                                                                   
         SPACE 2                                                                
WINVNO   DS    0H                  INVOICE NUMBER                               
         MVC   0(10,R3),EZIHINV                                                 
         CLC   EZIHINV,SPACES                                                   
         BNE   EXIT                                                             
         MVC   0(3,R3),=C'***'     NO INV = ***                                 
         XIT1                                                                   
         SPACE 2                                                                
WMOS     DS    0H                  INVOICE MONTH OF SERVICE                     
         MVC   0(2,R3),EZIHBMOS                                                 
         MVI   2(R3),0                                                          
         XIT1                                                                   
         SPACE 2                                                                
WIEST    DS    0H                  INVOICE ESTIMATE NUMBER                      
*                                                                               
         MVC   0(2,R3),EZIHBEST    USE HEADER ESTIMATE IF PRESENT               
*                                                                               
         OC    EZIHBEST,EZIHBEST   WAS CONVERTED EST ZERO                       
         BNE   EXIT                 NO                                          
*                                                                               
         MVC   0(2,R3),EZIHBEST    USE DETAIL ESTIMATE                          
*                                                                               
         OC    EZSPBEST,EZSPBEST   WAS CONVERTED EST ZERO                       
         BNE   EXIT                 NO                                          
*                                                                               
         CLI   EZMODE,EZERRP       PROCESS ERROR                                
         BNE   EXIT                                                             
*                                                                               
         TM    EZIHCVST,EZIHCOVR   CLIENT OVERRIDE PRESENT                      
         BO    EXIT                 YES, DON'T FIND EST                         
*                                                                               
* SEE IF ANY EST IN 10 CHAR EST FIELD                                           
*                                                                               
         LA    RE,EZIHEST                                                       
         LA    RF,3                                                             
         LA    R1,WORK                                                          
         MVC   WORK(3),=C'000'                                                  
WIEST10  CLI   0(RE),C'0'                                                       
         BL    WIEST14                                                          
         CLI   0(RE),C'9'                                                       
         BH    WIEST14                                                          
         MVC   0(1,R1),0(RE)                                                    
         LA    R1,1(,R1)                                                        
         LA    RE,1(,RE)                                                        
         BCT   RF,WIEST10                                                       
WIEST14  CLC   WORK(3),=C'000'                                                  
         BE    EXIT                                                             
         PACK  DUB,WORK(3)                                                      
         CVB   R0,DUB                                                           
         STCM  R0,3,0(R3)                                                       
         XIT1                                                                   
*                                                                               
WIGRS    DS    0H                  INSERT GROSS AMOUNT                          
         ICM   RF,15,EZSPBGRS      INSERT COST                                  
         STCM  RF,15,0(R3)                                                      
         XIT1                                                                   
         SPACE 2                                                                
WSRC     DS    0H                  SOURCE                                       
         LA    RE,SVWFREC0                                                      
         LA    RF,W_DESC-W_RECD(RE)                                             
         MVC   0(4,R3),EZWCSRCE-EZWKRCMD(RF)                                    
         XIT1                                                                   
         SPACE 2                                                                
WCMNT    DS    0H                  COMMENT ON CONVERTED INVOICE                 
         MVC   0(80,R3),SPACES                                                  
*                                                                               
         LR    RF,R3                                                            
         TM    EZIHCVST,EZIHCOVR   CLIENT/PROD OVERRIDE                         
         BZ    WCMNT20                                                          
*                                                                               
         OC    EZIHAVCD,EZIHAVCD   STATION SUPPLIED ADV CODE?                   
         BZ    WCMNT10              NO                                          
*                                                                               
         MVC   0(12,RF),=C'INVOICE-CLT('                                        
         MVC   12(3,RF),EZIHAVCD                                                
         LA    RF,15(,RF)                                                       
         CLI   EZIHAVCD+2,C' '                                                  
         BH    *+6                                                              
         BCTR  RF,0                                                             
         MVI   0(RF),C')'                                                       
         LA    RF,2(,RF)                                                        
         OC    EZIHPRCD,EZIHPRCD   STATION SUPPLIED PRODUCT CODE?               
         BZ    WCMNT10              NO                                          
         BCTR  RF,0                                                             
*        MVI   0(RF),C'-'                                                       
         MVC   1(4,RF),=C'PRD('                                                 
         MVC   5(L'EZIHPRCD,RF),EZIHPRCD                                        
         LA    RF,5+L'EZIHPRCD(,RF)                                             
         CLI   0(RF),C' '                                                       
         BH    *+8                                                              
         BCT   RF,*-8                                                           
         MVI   1(RF),C')'                                                       
         LA    RF,3(,RF)                                                        
WCMNT10  MVC   0(13,RF),=C'OVERRIDE CLT('                                       
         MVC   13(3,RF),EZIHLADC                                                
         LA    RF,16(,RF)                                                       
         CLI   EZIHLADC+2,C' '                                                  
         BH    *+6                                                              
         BCTR  RF,0                                                             
         MVI   0(RF),C')'                                                       
         CLI   EZIHCVPR,0                                                       
         BE    WCMNT20                                                          
         MVI   1(RF),C'-'                                                       
         MVC   2(4,RF),=C'PRD('                                                 
         MVC   6(3,RF),EZIHLPRC                                                 
         LA    RF,9(,RF)                                                        
         CLI   EZIHLPRC+2,C' '                                                  
         BH    *+6                                                              
         BCTR  RF,0                                                             
         MVI   0(RF),C')'                                                       
WCMNT20  TM    EZIHCVST,EZIHRCVQ   RECONVERT REQUEST                            
         BZ    WCMNT30                                                          
         MVC   2(9,RF),=C'RECONVERT'                                            
WCMNT30  LA    RF,11(,RF)                                                       
         OC    EZIHCINV,EZIHCINV   WAS THIS A COMBINED INVOICE                  
         BZ    EXIT                                                             
         MVC   1(9,RF),=C'COMBINED='                                            
         MVC   10(10,RF),EZIHCINV                                               
         XIT1                                                                   
         SPACE 2                                                                
WNCREAS  DS    0H                  REASON FOR NO CONVERSION                     
         MVC   0(40,R3),EZERRMSG                                                
         XIT1                                                                   
         SPACE 2                                                                
WCDATE   DS    0H                  DATE OF PREVIOUS CONVERSION                  
         MVC   0(3,R3),EZIHCVDT                                                 
         XIT1                                                                   
         SPACE 2                                                                
WERRCD   DS    0H                  ERROR CODE                                   
         MVC   0(1,R3),EZERRCD                                                  
         XIT1                                                                   
         SPACE 2                                                                
WERRREC  DS    0H                  ERROR RECORD                                 
         MVC   0(20,R3),EZERRREC                                                
         XIT1                                                                   
         SPACE 2                                                                
WERRFLD  DS    0H                  ERROR FIELD                                  
         MVC   0(20,R3),EZERRFLD                                                
         XIT1                                                                   
         SPACE 2                                                                
WERRMSG  DS    0H                  ERROR MESSAGE                                
         MVC   0(40,R3),EZERRMSG                                                
         XIT1                                                                   
         SPACE 2                                                                
WERRDTA  DS    0H                  ERROR DATA                                   
         MVC   0(30,R3),EZERRDTA                                                
         OC    0(30,R3),SPACES                                                  
         XIT1                                                                   
         SPACE 2                                                                
*        OUTPUT ROUTINES                                                        
         SPACE 2                                                                
RBPUB    DS    0H                                                               
*                                                                               
         MVC   0(60,R3),SPACES     INIT PRINT AREA                              
*                                                                               
         GOTO1 PUBEDIT,DMCB,(R2),(C'S',0(R3))  PRINT PUB ID                     
*                                                                               
         CLC   SVBPUB,0(R2)        SAME STATION                                 
         BE    *+10                                                             
         MVC   SVBPUB,0(R2)        SAVE STATION                                 
*                                                                               
         LA    R1,13(R3)           FIND END OF PUB NUMBER                       
         LA    R0,13                                                            
*                                                                               
         CLI   0(R1),C' '                                                       
         BH    *+12                                                             
         BCTR  R1,0                                                             
         BCT   R0,*-10                                                          
         BCTR  R1,0                NO PUB NUMBER                                
*                                                                               
         LA    R1,2(R1)            START OF PUB NAME                            
*                                                                               
         MVC   0(20,R1),6(R2)      PRINT PUB NAME                               
*                                                                               
         LA    R1,20(R1)           FIND END OF NAME                             
         LA    R0,20                                                            
*                                                                               
         CLI   0(R1),C' '                                                       
         BH    *+12                                                             
         BCTR  R1,0                                                             
         BCT   R0,*-10                                                          
         BCTR  R1,0                NO PUB NAME                                  
*                                                                               
         LA    R1,2(R1)            START OF PUB NAME 2                          
*                                                                               
         MVC   0(20,R1),26(R2)     PRINT SECOND PART OF PUB NAME                
*                                                                               
         OC    0(60,R3),SPACES     MAKE UPPERCASE                               
*                                                                               
RBPUBX   DS    0H                                                               
         XIT1                                                                   
*                                                                               
RBDATE   DS    0H                                                               
         GOTO1 DATCON,DMCB,(1,0(R2)),(8,SVBDATE)                                
         MVC   0(8,R3),SVBDATE                                                  
         XIT1                                                                   
         SPACE 2                                                                
         EJECT                                                                  
*                                                                               
*        RESOLVE LITERALS                                                       
*                                                                               
DRHRSLT  DS    0H                                                               
         DC    H'0'                                                             
         SPACE 2                                                                
*        HEADLINE ROUTINE                                                       
*                                                                               
DRHHEAD  DS    0H                                                               
         MVC   HEAD1+40(55),SPACES  CLEAR TITLE                                 
         MVC   HEAD2+40(55),SPACES                                              
*                                                                               
         CLI   EZTEST,C'Y'                                                      
         BE    *+12                                                             
         CLI   EZWRITE,C'N'                                                     
         BNE   *+10                                                             
         MVC   HEAD5+50(29),=CL29'** REPORT ONLY - NO UPDATE **'                
*                                                                               
         CLI   EZTEST,C'Y'                                                      
         BNE   *+10                                                             
         MVC   HEAD5+80(3),=C'(T)'                                              
         CLI   EZWRITE,C'N'                                                     
         BNE   *+10                                                             
         MVC   HEAD5+85(3),=C'(W)'                                              
*                                                                               
         CLI   GLRECNO,1           CONVERTED INVOICES                           
         BNE   DH404                                                            
*                                                                               
         BAS   RE,SETHDG                                                        
         CLI   QOPT1,C'X'                                                       
         BE    DRH402                                                           
         CLI   QOPT1,C'Y'                                                       
         BE    DRH402                                                           
         MVC   HEAD1+50(31),=C'EPIC - CONVERTED INVOICE REPORT'                 
         MVC   HEAD2+50(31),DASHES                                              
         B     DH440                                                            
DRH402   MVC   HEAD1+50(33),=C'EPIC - CONVERTIBLE INVOICE REPORT'               
         MVC   HEAD2+50(33),DASHES                                              
         B     DH440                                                            
*                                                                               
DH404    DS    0H                                                               
         CLI   GLRECNO,2           UNCONVERTED INVOICES                         
         BNE   DH408                                                            
*                                                                               
         BAS   RE,SETHDG                                                        
         MVC   HEAD1+50(33),=C'EPIC - UNCONVERTED INVOICE REPORT'               
         MVC   HEAD2+50(33),DASHES                                              
         B     DH440                                                            
*                                                                               
DH408    DS    0H                                                               
         CLI   GLRECNO,3           PREVIOUSLY CONVERTED                         
         BNE   DH412                                                            
*                                                                               
         BAS   RE,SETHDG                                                        
         MVC   HEAD1+50(36),=C'EPIC - PREVIOUSLY CONVERTED INVOICES'            
         MVC   HEAD2+50(36),DASHES                                              
         B     DH440                                                            
*                                                                               
DH412    DS    0H                                                               
         CLI   GLRECNO,4           MISSING CLIENT REPORT                        
         BNE   DH416                                                            
*                                                                               
         MVC   HEAD1+50(26),=C'EPIC - MISSING CLIENT LIST'                      
         MVC   HEAD2+50(26),DASHES                                              
         B     DH440                                                            
*                                                                               
DH416    DS    0H                                                               
         CLI   GLRECNO,5           MISSING PRODUCT REPORT                       
         BNE   DH420                                                            
*                                                                               
         MVC   HEAD1+50(27),=C'EPIC - MISSING PRODUCT LIST'                     
         MVC   HEAD2+50(27),DASHES                                              
         B     DH440                                                            
*                                                                               
DH420    DS    0H                                                               
DH424    DS    0H                                                               
         CLI   GLRECNO,7           FILM/PRODUCT ERROR REPORT                    
         BNE   DH428                                                            
*                                                                               
         MVC   HEAD1+45(25),=C'EPIC - PRODUCT ERROR LIST'                       
         MVC   HEAD2+45(25),DASHES                                              
         B     DH440                                                            
*                                                                               
DH428    DS    0H                                                               
*                                                                               
DH432    DS    0H                                                               
         CLI   GLRECNO,9           DATA ERROR REPORT                            
         BNE   DH434                                                            
*                                                                               
         BAS   RE,SETHDG                                                        
         MVC   HEAD1+50(24),=C'EPIC - DATA ERROR REPORT'                        
         MVC   HEAD2+50(24),DASHES                                              
DH434    DS    0H                                                               
         CLI   GLRECNO,10          DELETED INVOICES REPORT                      
         BNE   DH440                                                            
*                                                                               
         BAS   RE,SETHDG                                                        
         MVC   HEAD1+47(30),=C'EPIC - DELETED INVOICES REPORT'                  
         MVC   HEAD2+47(30),DASHES                                              
*                                                                               
DH440    DS    0H                                                               
         XIT1                                                                   
         SPACE 2                                                                
*        SET STATION/BATCH DATA                                                 
*                                                                               
         DS    0D                                                               
SETHDG   NTR1                                                                   
*                                                                               
         XIT1                                                                   
*                                                                               
*        INPUT TEST - PASS TO SORT OR NOT                                       
*                                                                               
DRHPSRT  DS    0H                                                               
         CLC   GLRECNO,RPTNO       IF NOT RIGHT RECORD                          
         BE    DRHPSRTX                                                         
         MVI   GLHOOK,GLDONT       DON'T PUT TO SORT                            
*                                  NOTE- SCHEME INVOLVING GLOPTS                
*                                  NEVER WORKED BECAUSE IN DPG                  
*                                  RECORD CONDITION DONT APPLY                  
*                                  TO FIELDS                                    
DRHPSRTX DS    0H                                                               
         XIT1                                                                   
*                                                                               
         SPACE 2                                                                
DIV      DIV   (R0),(RF)       RETURNS VIA RE                                   
*                                                                               
* OPEN MPL FILE AFTER FINDING OUT WHICH ONE TO OPEN *                           
*                                                                               
         DS    0D                                                               
MPLOPN   NTR1                                                                   
*                                                                               
         ICM   RF,15,UTL           MUST BE A UTL ADDRESS                        
         BNZ   *+6                                                              
         DC    H'0'                                                             
*                                                                               
         MVC   4(1,RF),CONSEMPL                                                 
*                                                                               
         L     R2,ADBUY            USE AS WORK AREA FOR OPEN                    
         GOTO1 DATAMGR,DMCB,=C'OPEN',=CL7'MPL',FILELIST,(R2)                    
         MVI   EZMPLOPN,C'Y'                                                    
         XIT1                                                                   
*                                                                               
FILELIST DS    0X                                                               
         DC    CL8'UMPLDIR'                                                     
         DC    CL8'UMPLFIL'                                                     
         DC    CL10'X'                                                          
CONSEMPL DC    X'05'                                                            
*                                                                               
MPLTAB   DC    C'1',CL7'MPL1',X'05' ADV 1 USE MPL1                              
         DC    C'2',CL7'MPL2',X'35'  "  2     MPL2                              
         DC    C'3',CL7'MPL3',X'55'  "  3     MPL3                              
         DC    C'4',CL7'MPL4',X'45'  "  4     MPL4                              
         DC    C'5',CL7'MPL5',X'85'  "  5     MPL5                              
         DC    C'6',CL7'MPL6',X'75'  "  6     MPL6                              
         DC    C'R',CL7'MPLQ',X'65' REP       MPLQ                              
         DC    C'T',CL7'MPL1',X'05' TST USE MPL1 ALSO                           
MPLENTLN EQU   9                                                                
MPLTABLN EQU   (*-MPLTAB)/9                                                     
         EJECT                                                                  
         LTORG                                                                  
*                                                                               
         TITLE 'PPREPZ502 - EPIC - INVOICE CONVERSION - PPZ5WRKD'               
***********************************************************************         
*                                                                     *         
*        PPZ2WRKC - LOCAL WORKING STORAGE                             *         
*                                                                     *         
***********************************************************************         
         SPACE 2                                                                
PPZ5WRKC DS    0D                                                               
*                                                                               
FIRSTSW  DS    X               0 = FIRST TIME, 01 = DRIVER INITIALIAZED         
*                              8 = AT LEAST 1 INVOICE CONVERTED                 
SAVSYS   DS    X                                                                
RERUN    DS    XL3                 RERUN DATE                                   
TRCOPT   DS    C                                                                
PREVOPT  DS    C                                                                
UNCONVSW DS    C                                                                
CONVSW   DS    C                                                                
ACTVSW   DS    CL1                                                              
SVCIADDR DS    XL2                                                              
*                                                                               
VDRIVER  DS    A                                                                
VPERVAL  DC    V(PERVAL)                                                        
*                                                                               
DPGFILE  DS    CL8                                                              
DRIVER   DS    CL8                                                              
PPDRIVER DS    CL8                                                              
XFF      DS    XL20                                                             
X        DS    XL256                                                            
*                                                                               
SVKEY    DS    CL32                                                             
         DS    0D                                                               
SVWFREC0 DS    XL64                                                             
*                                                                               
SVBPUB   DS    XL6                 PUB SAVE AREA                                
SVBDPUB  DS    XL6                 SAVED BAD (DOES NOT EXIST) PUB               
SVBDATE  DS    CL8                                                              
*                                                                               
RPTNO    DS    XL1                                                              
MPLKEY   DS    CL40                                                             
MPLKEYSV DS    CL40                                                             
*                                                                               
PPZ5WRKX EQU   *                   END OF LOCAL WORKING STORAGE                 
PPZ5WRKL EQU   *-PPZ5WRKC          LENGTH OF WORKING STORAGE                    
*                                                                               
         EJECT                                                                  
*                                                                               
PZIMRQF  DCB   DDNAME=PZIMRQF,                                         X        
               DSORG=PS,                                               X        
               RECFM=FB,                                               X        
               LRECL=80,                                               X        
               BLKSIZE=2000,                                           X        
               MACRF=PM                                                         
*                                                                               
PZZ9RQF  DCB   DDNAME=PZZ9RQF,                                         X        
               DSORG=PS,                                               X        
               RECFM=FB,                                               X        
               LRECL=80,                                               X        
               BLKSIZE=2000,                                           X        
               MACRF=PM                                                         
*                                                                               
         DS    0D                                                               
         DC    CL8'*WKRBUF*'                                                    
WRKRBUFF DS    14336X                                                           
WRKRBEND EQU   *                                                                
         DS    0D                                                               
         DC    CL8'*WKRREC*'                                                    
WRKRREC  DS    4096X                                                            
WRKRRECE EQU   *                                                                
         DS    0D                                                               
         DC    CL8'*PZBLKF*'                                                    
EZBLOCKC DS    0X                                                               
         ORG   *+EZBLOCKL                                                       
         DS    0D                                                               
         DC    CL8'*PRTAB**'                                                    
PRTABC   DS    0X                                                               
PRTABL   EQU   23100               ENTRY LEN = 77, 300 ENTRIES                  
         ORG   *+PRTABL                                                         
PRTABE   EQU   *                                                                
         DS    0D                                                               
         DC    CL8'*IOAREA1'                                                    
IOAREA1  DS    XL4096              IOAREA                                       
         DS    0D                                                               
         DC    CL8'DRGLOBAL'                                                    
GLAREA   DS    44000X              DRIVER GLOBAL AREA                           
         DS    0D                                                               
         EJECT                                                                  
*DRGLOBAL                                                                       
         PRINT OFF                                                              
       ++INCLUDE DRGLOBAL                                                       
         PRINT ON                                                               
*                                                                               
GLOPT1   EQU   GLOPTS+00                                                        
GLOPT2   EQU   GLOPTS+01                                                        
GLOPT3   EQU   GLOPTS+02                                                        
GLOPT4   EQU   GLOPTS+03                                                        
GLOPT5   EQU   GLOPTS+04                                                        
GLOPT6   EQU   GLOPTS+05                                                        
GLOPT7   EQU   GLOPTS+06                                                        
GLOPT8   EQU   GLOPTS+07                                                        
GLOPT9   EQU   GLOPTS+08                                                        
GLOPT10  EQU   GLOPTS+09                                                        
*                                                                               
         TITLE 'PPREPZ502 - EPIC - INVOICE CONVERSION - OTHER WORK'             
***********************************************************************         
*                                                                     *         
*        OTHER WORKAREAS                                              *         
*                                                                     *         
***********************************************************************         
         SPACE 2                                                                
         PRINT ON                                                               
*PPMODEQU                                                                       
         PRINT OFF                                                              
       ++INCLUDE PPMODEQU                                                       
         PRINT ON                                                               
*PPREPWORK                                                                      
         PRINT OFF                                                              
       ++INCLUDE PPREPWORK                                                      
         PRINT ON                                                               
*PPREPWORK2                                                                     
         PRINT OFF                                                              
       ++INCLUDE PPREPWORK2                                                     
         PRINT ON                                                               
         ORG   Q2USER                                                           
Q2RERUN  DS    CL6                 RE-RUN DATE                                  
*                                                                               
*PPDMWRKRD                                                                      
         PRINT OFF                                                              
       ++INCLUDE DMWRKFD                                                        
         PRINT ON                                                               
*PPDMWRKRK                                                                      
         PRINT OFF                                                              
       ++INCLUDE DMWRKFK                                                        
         PRINT ON                                                               
*PZBLOCK                                                                        
         PRINT OFF                                                              
       ++INCLUDE PZBLOCK                                                        
         PRINT ON                                                               
*PRGENFILE                                                                      
         PRINT OFF                                                              
       ++INCLUDE PRGENFILE                                                      
         PRINT ON                                                               
*DDPERVALD                                                                      
         PRINT OFF                                                              
       ++INCLUDE DDPERVALD                                                      
         PRINT ON                                                               
*PPGENPZ                                                                        
         PRINT OFF                                                              
       ++INCLUDE PPGENPZ                                                        
         PRINT ON                                                               
*DDCOMFACS                                                                      
         PRINT OFF                                                              
       ++INCLUDE DDCOMFACS                                                      
         PRINT ON                                                               
*DDMASTC                                                                        
         PRINT OFF                                                              
MASTD    DSECT                                                                  
       ++INCLUDE DDMASTC                                                        
         PRINT ON                                                               
*DDFASELIST                                                                     
         PRINT OFF                                                              
       ++INCLUDE FASELIST                                                       
         EJECT                                                                  
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'044PPREPZ502B12/27/94'                                      
         END                                                                    
