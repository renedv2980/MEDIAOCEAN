*          DATA SET DMISGQU    AT LEVEL 006 AS OF 02/28/19                      
*CATALP DMISGQU                                                                 
         TITLE 'ISGQ - QUERY ENQUEUES'                                          
         PRINT NOGEN                                                            
ISGQU    CSECT                                                                  
         NMOD1 WRKX-WRKD,*ISGQU**,CLEAR=YES                                     
         USING WRKD,RC                                                          
         LR    RA,R1               SAVE R1 FOR RETRIES                          
*                                                                               
         USING SSOOFF,R1           ONLINE MUST ABEND                            
         ICM   R1,15,=V(SSB)                                                    
         JZ    ISGQU001                                                         
         CLC   SSOCNTL,=X'0000'    IF ONLINE GOTO U990                          
         JNE   ISGQU999                                                         
         TM    SSOFLAG3,SSO3RUNR   RUNNER GOTO U990                             
         JO    ISGQU999                                                         
         DROP  R1                                                               
*                                                                               
         LA    R9,FULL             GET JOB NAME INTO MVSNAME                    
         EXTRACT (9),FIELDS=TIOT                                                
         L     R9,FULL                                                          
         MVC   MVSNAME,0(R9)                                                    
         CLC   MVSNAME(4),=C'MNTN' MNTN GOTO JUST U990                          
         JE    ISGQU999                                                         
*                                                                               
ISGQU001 LR    R1,RA                                                            
         MVC   MAJNAME,=CL8'SYSDSN  '                                           
*                                                                               
         XR    R9,R9               R1 POINTS TO TXTDSN                          
         IC    R9,5(R1)                                                         
         STC   R9,MINNAML          SET DSN LEN                                  
         AHI   R9,-1                                                            
         EX    R9,*+8                                                           
         B     *+10                                                             
         MVC   MINNAME(0),6(R1)    SET DSN                                      
*                                                                               
         ISGQUERY REQINFO=QSCAN,SCANACTION=START,SEARCH=BY_FILTER,     +        
               ANSAREA=ANSAREA,ANSLEN=AREALEN,ANSDETAIL=FULL,          +        
               QNAMEMATCH=SPECIFIC,QNAME=MAJNAME,                      +        
               RNAMEMATCH=PATTERN,RNAME=MINNAME,RNAMELEN=MINNAML,      +        
               RESUMETOKEN=RESTOKEN,RETCODE=RETCODE,RSNCODE=RSNCODE,   +        
               MF=(E,QLIST)                                                     
*                                                                               
         CLC   RETCODE,=A(ISGQUERYRC_OK)    SUCCESSFUL RETURN?                  
         BE    ISGQU010            YES, DIG OUT THE ENQUEUES                    
         CLC   RETCODE,=A(ISGQUERYRC_WARN)                                      
         JNE   *+2                 ISGQ ERROR                                   
         MVC   FULL,RSNCODE        ISGQ WARNINGS                                
         NC    FULL,=X'0000FFFF'                                                
         CLC   FULL,=A(ISGQUERYRSN_ANSWERAREAFULL)                              
         JE    ISGQU010                                                         
         CLC   FULL,=A(ISGQUERYRSN_NOMATCHINGRESOURCES)                         
         JE    ISGQUXX                                                          
         DC    H'0'                                                             
*                                                                               
ISGQU010 LA    R7,ANSAREA          A(ANSWER AREA FROM ISGQUERY)                 
         USING ISGYQUAAHDR,R7                                                   
*                                                                               
         ICM   R9,15,ISGYQUAAHDRNUMRECORDS  NUMBER OF RESOURCES                 
         BZ    ISGQUXX                                                          
         L     R4,ISGYQUAAHDRFIRSTRECORD31  A(FIRST RESOURCE)                   
         USING ISGYQUAARS,R4                                                    
*                                                                               
         LA    R9,OUTTABLE                                                      
ISGQU020 ICM   RF,15,ISGYQUAARSNUMRQ   NUMBER OF REQUESTERS                     
         BZ    ISGQUXX                                                          
         L     R5,ISGYQUAARSFIRSTRQ31  A(FIRST REQUESTER DATA RECORD)           
         USING ISGYQUAARQ,R5                                                    
*                                                                               
ISGQU030 L     R6,ISGYQUAARQRQX31      A(REQUESTER EXTENSION)                   
         USING ISGYQUAARQX,R6                                                   
         MVC   0(8,R9),ISGYQUAARQXJOBNAME                                       
         LA    R9,8(R9)                                                         
         LA    RF,OUTTABLX             COPY UP TO 5 JOBNAMES                    
         CR    R9,RF                                                            
         JNL   ISGQU050                                                         
         L     R5,ISGYQUAARQNEXT31                                              
         LTR   R5,R5                                                            
         JNZ   ISGQU030                NEXT REQUESTER                           
*                                                                               
ISGQU050 LA    R3,OUTTABLE                                                      
*                                                                               
         XC    MYWORK,MYWORK       YES - OUTPUT DD OVERRIDE MESSAGE             
         MVC   MYWORK,SPACES                                                    
         MVC   MYWORK(2),=X'0040'                                               
         MVC   MYWORK+2(20),=C'CONTENTION FOR DSN= '                            
         XR    R9,R9                                                            
         IC    R9,5(RA)                                                         
         AHI   R9,-1                                                            
         EX    R9,*+8                                                           
         B     *+10                                                             
         MVC   MYWORK+21(0),6(RA)  FILENAME                                     
*                                                                               
         LA    R4,MYWORK                                                        
         WTO   TEXT=(R4),MCSFLAG=HRDCPY                                         
*                                                                               
         LA    R5,16                                                            
ISGQU060 XC    MYWORK,MYWORK       YES - OUTPUT DD OVERRIDE MESSAGE             
         MVC   MYWORK(2),=X'0010'                                               
         MVC   MYWORK+2(16),=C'JOBNAME         '                                
         MVC   MYWORK+10(8),0(R3)                                               
*                                                                               
         LA    R4,MYWORK                                                        
         WTO   TEXT=(R4),MCSFLAG=HRDCPY                                         
         LA    R3,8(R3)                                                         
         CLI   0(R3),C' '                                                       
         JNH   ISGQU100                                                         
         JCT   R5,ISGQU060         NEXT UP TO 16 JOBS                           
*                                                                               
ISGQU100 XC    ECBAD,ECBAD                                                      
         WTOR  'FILE CONTENTION. CHECK OUTPUT THEN RETRY OR CANCEL',   X        
               REPLY,1,ECBAD,ROUTCDE=(1),MCSFLAG=HRDCPY                         
         WAIT  ECB=ECBAD                                                        
         CLI   REPLY,C'R'          BACK TO TRY AGAIN                            
         JE    ISGQUXX                                                          
*                                                                               
ISGQU999 ABEND 990                 ABEND NO WITH DUMP R2=DTF                    
*                                                                               
ISGQUXX  XMOD1                                                                  
*                                                                               
AREALEN  DC    A(AREALNQ)          L(ANSWER AREA)                               
ECBAD    DC    F'0'                WTOR FIELDS                                  
REPLY    DC    CL4'    '                                                        
SPACES   DC    80C' '                                                           
         SPACE 2                                                                
         EJECT                                                                  
******************************************************                          
*        WORK AREA                                   *                          
******************************************************                          
WRKD     DSECT                                                                  
DUB      DS    D                                                                
FULL     DS    F                                                                
MVSNAME  DS    CL8                                                              
MYWORK   DS    CL80                                                             
RETCODE  DS    F                   RETURN CODE                                  
RSNCODE  DS    F                   REASON CODE                                  
RESTOKEN DS    CL16                RESUME TOKEN                                 
*                                                                               
OUTTABLE DS    16CL8               TABLE OF JOBS WITH ENQS                      
OUTTABLX EQU   *                                                                
*                                                                               
MAJNAME  DS    CL8                 MAJOR NAME                                   
MINNAME  DS    CL40                MINOR NAME (COULD BE A PATTERN)              
MINNAML  DS    AL1                 TRUE LENGTH OF MINOR NAME                    
*                                                                               
*                                                                               
QLIST    ISGQUERY MF=(L,QPARMS)                                                 
*                                                                               
ANSAREA  DS    4XL1024                                                          
AREALNQ  EQU   *-ANSAREA                                                        
*                                                                               
WRKX     EQU   *                                                                
*                                                                               
       ++INCLUDE FASSBOFF                                                       
*                                                                               
*ISG MACROS                                                                     
         PRINT GEN                                                              
         ISGYQUAA                                                               
         ISGYCON                                                                
         PRINT NOGEN                                                            
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'006DMISGQU   02/28/19'                                      
         END                                                                    
