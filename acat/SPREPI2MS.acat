*          DATA SET SPREPI2MS  AT LEVEL 093 AS OF 12/11/07                      
*CATALP SPMSTAT                                                                 
         TITLE 'SPMSTAT - MATCHING STATUS RECORD UPDATE'                        
***********************************************************************         
*** THIS MODULE IS DEAD   THIS MODULE IS DEAD   THIS MODULE IS DEAD ***         
*** THIS MODULE IS DEAD   THIS MODULE IS DEAD   THIS MODULE IS DEAD ***         
*** THIS MODULE IS DEAD   THIS MODULE IS DEAD   THIS MODULE IS DEAD ***         
*** THIS MODULE IS DEAD   THIS MODULE IS DEAD   THIS MODULE IS DEAD ***         
*** THIS MODULE IS DEAD   THIS MODULE IS DEAD   THIS MODULE IS DEAD ***         
*** THIS MODULE IS DEAD   THIS MODULE IS DEAD   THIS MODULE IS DEAD ***         
*** THIS MODULE IS DEAD   THIS MODULE IS DEAD   THIS MODULE IS DEAD ***         
*** THIS MODULE IS DEAD   THIS MODULE IS DEAD   THIS MODULE IS DEAD ***         
*** THIS MODULE IS DEAD   THIS MODULE IS DEAD   THIS MODULE IS DEAD ***         
*** THIS MODULE IS DEAD   THIS MODULE IS DEAD   THIS MODULE IS DEAD ***         
*** THIS MODULE IS DEAD   THIS MODULE IS DEAD   THIS MODULE IS DEAD ***         
*** THIS MODULE IS DEAD   THIS MODULE IS DEAD   THIS MODULE IS DEAD ***         
*** THIS MODULE IS DEAD   THIS MODULE IS DEAD   THIS MODULE IS DEAD ***         
*** THIS MODULE IS DEAD   THIS MODULE IS DEAD   THIS MODULE IS DEAD ***         
*** THIS MODULE IS DEAD   THIS MODULE IS DEAD   THIS MODULE IS DEAD ***         
*** THIS MODULE IS DEAD   THIS MODULE IS DEAD   THIS MODULE IS DEAD ***         
*** THIS MODULE IS DEAD   THIS MODULE IS DEAD   THIS MODULE IS DEAD ***         
***********************************************************************         
*********************************************************************           
*        NOTE- CERTAIN ANOMALIES CAN ARISE WHEN TESTING                         
*              THIS MODULE WITHOUT MARKING THE FILE. IN                         
*              PARTICULAR, SPLITTING MINIO RECS CAN CAUSE PROBLEMS.             
*********************************************************************           
         SPACE  2                                                               
         PRINT NOGEN                                                            
MSTAT    CSECT                                                                  
         NMOD1 0,**MSTAT,(R8)                                                   
         USING SPWORKD,RA,R9                                                    
         LA    RC,SPACEND                                                       
         USING IMRWORK,RC                                                       
         LA    R5,MINBLK                                                        
         USING MINBLKD,R5                                                       
*                                                                               
         CLI   MSROPT,C'Y'         EXIT IF NOT DOING MSR RECS                   
         BNE   EXIT                                                             
         MVI   MSTRACE,C'N'      **PATCH TO ACTIVATE TRACE**                    
*                                                                               
         MVI   MSRCTL,X'FF'        BITS TO CONTROL WHAT ERRS TO TRACK           
         CLI   TRAFFRPT,C'Y'       FILM ANALYSIS ON?                            
         BE    *+8                 IF NO - DON'T SET COMML ERRORS               
         NI    MSRCTL,X'FF'-(MSICOQ+MSCROQ)                                     
*                                                                               
         CLI   MULTIMON,C'Y'       SKIP IF MULTIPLE MONTH REQUEST               
         BE    EXIT                                                             
*                                                                               
         BC    0,MP04            **NOOP THE NOOP                                
         CLI   BPRD,0              MSR'S ONLY FOR SINGLE PRODUCT                
         BE    EXIT                                                             
         CLI   BPRD,X'FF'          NOT POL                                      
         BE    EXIT                                                             
         CLI   BEST,0              AND SINGLE ESTIMATE                          
         BE    EXIT                                                             
         CLI   BESTEND,0                                                        
         BNE   EXIT                                                             
*                                                                               
MP04     DS    0H                                                               
         MVI   MSLINE,C' '                                                      
         MVC   MSLINE+1(L'MSLINE-1),MSLINE                                      
         MVC   MSMODE,0(R1)        MODE IS HOB OF PARM1                         
*                                                                               
         CLI   FRSTSW,0                                                         
         BNE   *+8                                                              
         BAS   RE,INIT                                                          
*                                                                               
         CLI   MSMODE,C'M'         MATCH STATUS                                 
         BE    MSTPROC                                                          
*                                                                               
         OC    SPTCOUNT,SPTCOUNT   SKIP IF NO INVOICE SPOTS                     
         BZ    EXIT                                                             
*                                                                               
         CLI   MSMODE,C'O'         ORDERED NOT RUN                              
         BE    ONRPROC                                                          
         CLI   MSMODE,C'R'         RUN NOT ORDERED                              
         BE    RNOPROC                                                          
         CLI   MSMODE,C'S'         SEPARATION                                   
         BE    SEPPROC                                                          
         CLI   MSMODE,C'H'         HORIZONTAL/VERTICAL                          
         BE    HVRPROC                                                          
         CLI   MSMODE,C'C'         COMMERCIAL ROTATION                          
         BE    CROPROC                                                          
         CLI   MSMODE,C'I'         INVALID COMMERCIALS                          
         BE    ICOPROC                                                          
         CLI   MSMODE,C'X'         CLOSE                                        
         BE    CLSPROC                                                          
         DC    H'0'                                                             
*                                                                               
EXIT     DS    0H                                                               
         XIT                                                                    
         EJECT                                                                  
***********************************************************************         
*                                                                               
*        INITIALIZE MINIO, ETC.                                                 
*                                                                               
***********************************************************************         
*                                                                               
INIT     NTR1                                                                   
         MVI   FRSTSW,1                                                         
         GOTO1 LOADER,MSDMCB,=CL8'T00A74',0,0                                   
         MVC   VMINIO,MSDMCB+4     SET MINIO ADDRESS                            
*                                                                               
*        INITIALIZE MINIO VALUES                                                
*                                                                               
         LA    R0,MINBLK           CLEAR MINBLOCK                               
         LA    R1,MINBLKL                                                       
         SR    RF,RF                                                            
         MVCL  R0,RE                                                            
*                                                                               
         MVC   MINRECUP,RECUP        A(RECUP)                                   
         MVC   MINCOMF,ACOMFACS      A(COMFACS)                                 
         MVI   MINOPEN,C'N'          SET NOT OPEN                               
         MVC   MINFIL,=CL8'XSPFIL'   FILE NAME                                  
         MVC   MINDIR,=CL8'XSPDIR'   DIR NAME                                   
         MVI   MINFKLEN,L'MSRKEY     KEY LENGTH                                 
         MVI   MINEKLEN,L'MSRKMINK   ELEMENT KEY LENGTH                         
         MVI   MINEKDSP,L'MSRKMAST   DISPLACEMENT TO ELEMENT KEY                
         MVI   MINNCTL,L'MSRDSTAT    NUMBER OF CONTROL BYTES                    
         MVC   MINFRCLM,MAXRECSZ     MAXIMUM RECORD LENGTH                      
*                                                                               
         L     RF,=A(MBUFF1)         POINT TO START OF MINIO BUFFERS            
         ST    RF,MINBUFF            A(FIRST BUFFER)                            
*                                                                               
         MVI   MINNBUF,2             USE TWO BUFFERS                            
*                                                                               
         L     RF,=A(MRTAB)                                                     
         ST    RF,MINRTAB            A(AREA FOR RECORD TABLE)                   
*                                                                               
         MVC   MINRTABL,=Y(L'MRTAB)  LENGTH OF RECORD TABLE                     
*                                                                               
         L     RF,=A(MELEM)          A(AREA FOR ELEM OR CLUSTER)                
         ST    RF,MINELEM                                                       
*                                                                               
         XC    0(L'MELEM,RF),0(RF)  CLEAR MINELEM AREA                          
*                                                                               
         MVC   MINMAXEL,=Y(L'MELEM)      MAX LENGTH OF ELEM OR CLUSTER          
*                                                                               
         MVI   MINDELSW,C'Y'       PROCESS DELETED RECORDS                      
*                                                                               
         CLI   RCWRITE,C'Y'        IF WRITES NOT ALLOWED                        
         BE    *+8                                                              
         MVI   MINWRITE,C'N'          DISABLE WRITES                            
*                                                                               
INITX    DS    0H                                                               
         XIT1                                                                   
         EJECT                                                                  
***********************************************************************         
*                                                                               
*        MATCH STATUS PROC (AND ACTIVITY AND INVOICE ELEMS)                     
*                                                                               
***********************************************************************         
*                                                                               
MSTPROC  DS    0H                                                               
         MVI   DDSFXSW,C'N'        SET DDS FIX SW                               
         CLC   QUESTOR(6),=C'DDSFIX'                                            
         BNE   *+8                                                              
         MVI   DDSFXSW,C'Y'                                                     
*                                                                               
         MVC   SPTCOUNT,ISPTS     SAVE SPOT COUNT                               
*                                                                               
         XC    MINMKEY,MINMKEY                                                  
         LA    R2,MINMKEY                                                       
         USING MSRKEYD,R2                                                       
*                                                                               
         MVI   MSRKTYPE,MSRKTYPQ                                                
         MVI   MSRKSUB,MSRKSUBQ                                                 
         MVC   MSRKAM,BAGYMD                                                    
         MVC   MSRKCLT,BCLT                                                     
         MVC   MSRKPRD,BPRD                                                     
         MVC   MSRKPRD2,BPRD2                                                   
         MVC   MSRKEST,BEST                                                     
         MVC   MSRKMKT,BMKT                                                     
         MVC   MSRKSTA,BMKTSTA+2                                                
*              NOTE- MSRKMOS IS COMPLEMENTED 2-BYTE BINARY FIRST                
*                    OF CALENDAR MONTH (AS IN NEW INVOICE RECORDS)              
         MVC   MSHALF,BRDMON                                                    
         GOTO1 DATCON,MSDMCB,(2,BRDMON),MSWORK                                  
         CLC   MSWORK+4(2),=C'01'  IF ALREADY HAVE FIRST OF MONTH               
         BE    MST04               OK                                           
         GOTO1 ADDAY,MSDMCB,MSWORK,MSWORK,7  ELSE BUMP INTO NEXT MONTH          
         MVC   MSWORK+4(2),=C'01'     AND RESET TO FIRST                        
         GOTO1 DATCON,MSDMCB,(0,MSWORK),(2,MSHALF)                              
MST04    DS    0H                                                               
         XC    MSHALF,=X'FFFF'                                                  
         MVC   MSRKMOS,MSHALF                                                   
         MVC   MSRDSTAT+2(2),AGENCY                                             
*                                                                               
         CLC   QUESTOR(4),=C'*DEL'   REQUEST FOR DELETE                         
         BNE   MST04D                                                           
         XC    SPTCOUNT,SPTCOUNT                                                
         GOTO1 VMINIO,MSDMCB,('MINOPN',MINBLKD)                                 
         CLI   MINERR,0                                                         
         BNE   MSTX                                                             
*                            MINIO SET EXISTS                                   
         TM    MINSTAT,MINDELQ     YES, IF MINIO SET IS DELETED, OK             
         BNZ   MSTX                                                             
         GOTO1 VMINIO,MSDMCB,('MINDLF',MINBLKD)  NO, DELETE MINIO               
         CLI   MINERR,0                                                         
         BE    MSTX                                                             
         DC    H'0'                                                             
*                                                                               
MST04D   DS    0H                                                               
         CLC   QUESTOR(4),=C'*RES'                                              
         BNE   MST04E                                                           
         XC    SPTCOUNT,SPTCOUNT                                                
         GOTO1 VMINIO,MSDMCB,('MINOPN',MINBLKD)                                 
         CLI   MINERR,0                                                         
         BNE   MSTX                                                             
*                            MINIO SET EXISTS                                   
         TM    MINSTAT,MINDELQ   YES, IF MINIO SET IS NOT DELETED, OK           
         BZ    MSTX                                                             
         GOTO1 VMINIO,MSDMCB,('MINRSF',MINBLKD)  NO, RESTORE SET                
         CLI   MINERR,0                                                         
         BE    MSTX                                                             
         DC    H'0'                                                             
*                                                                               
MST04E   DS    0H                                                               
         MVI   NEWREC,C'Y'                                                      
         MVI   SVICST,C' '                                                      
         XC    SVICSTP,SVICSTP                                                  
         XC    SVICSTD,SVICSTD                                                  
         MVI   SVIPST,C' '                                                      
         MVC   SVIPDAT,SPACES                                                   
         MVI   SVLOCKST,0          CLEAR LOCKIN STATUS                          
         MVI   SVLOCKS2,0          CLEAR LOCKIN STATUS                          
         MVI   SVLOCKS3,0                                                       
         GOTO1 VMINIO,MSDMCB,('MINOPN',MINBLKD)                                 
         CLI   MINERR,0                                                         
         BNE   MST09                                                            
         MVI   NEWREC,C'N'                                                      
*                            MINIO SET EXISTS                                   
         OC    SPTCOUNT,SPTCOUNT        ARE THERE ANY INVOICE ITEMS?            
         BNZ   MST05                                                            
         TM    MINSTAT,MINDELQ     YES, IF MINIO SET IS DELETED, OK             
         BNZ   MSTX                                                             
         GOTO1 VMINIO,MSDMCB,('MINDLF',MINBLKD)  NO, DELETE MINIO               
         CLI   MINERR,0                                                         
         BE    MSTX                                                             
         DC    H'0'                                                             
*                                                                               
MST05    DS    0H                 THERE ARE INVOICE ITEMS                       
         TM    MINSTAT,MINDELQ     IF MINIO SET IS DELETED, RESTORE             
         BZ    MST05D                                                           
         GOTO1 VMINIO,MSDMCB,('MINRSF',MINBLKD)                                 
         CLI   MINERR,0                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
         GOTO1 VMINIO,MSDMCB,('MINOPN',MINBLKD)                                 
         CLI   MINERR,0                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
*                                  CLEAR OUT SOME ELEMS                         
MST05D   DS    0H                                                               
         XC    MINEKEY,MINEKEY                                                  
         MVI   MINFILTL,0                                                       
*                                                                               
         GOTO1 VMINIO,MSDMCB,('MINHI',MINBLKD)                                  
         B     MST06B                                                           
*                                                                               
MST06    DS    0H                                                               
         GOTO1 VMINIO,MSDMCB,('MINSEQ',MINBLKD)                                 
MST06B   DS    0H                                                               
         CLI   MINERR,0            TREAT ALL ERRS HERE AS EOF                   
         BNE   MST07                                                            
         CLI   MELEM,X'F1'         PRESERVE ACTIVITY ELEM                       
         BE    MST06                                                            
         CLI   MELEM,MSRIPELQ      AND PAY STATUS ELEM                          
         BNE   MST06D                                                           
         MVC   SVIPST,MELEM+MSRIPST-MSRIPELD    SAVE STATUS                     
         MVC   SVIPDAT,MELEM+MSRIPDAT-MSRIPELD  AND DATE                        
         B     MST06                                                            
*                                                                               
MST06D   DS    0H                                                               
         CLI   MELEM,MSRICELQ      AND INCH STATUS ELEM                         
         BNE   MST06E                                                           
         MVC   SVICST,MELEM+MSRICST-MSRICELD    SAVE STATUS                     
         MVC   SVICSTP,MELEM+MSRICPER-MSRICELD    SAVE PERSON                   
         MVC   SVICSTD,MELEM+MSRICDAT-MSRICELD    SAVE DATE                     
         B     MST06                                                            
*                                                                               
MST06E   DS    0H                                                               
         CLI   MELEM,MSRTXCMQ      AND INCH STATUS COMMENT                      
         BE    MST06                                                            
         CLI   MELEM,MSRTXRCQ      AND REMITTANCE COMMENT                       
         BE    MST06                                                            
         CLI   MELEM,MSRINELQ      AND INVOICE ELEM                             
         BE    MST06                                                            
         CLI   MELEM,MSRSTELQ      FOR STATUS HEADER ELEM                       
         BNE   MST06F                                                           
         MVC   SVLOCKST,MELEM+MSRSTBLS-MSRSTELD   SAVE LOCKIN STATUS            
         MVC   SVLOCKS2,MELEM+MSRSTBGS-MSRSTELD   SAVE LOCKIN STAT2             
         MVC   SVLOCKS3,MELEM+MSRMPERR-MSRSTELD   SAVE ERRORS                   
         MVC   SVSTDAT,MELEM+MSRSTDAT-MSRSTELD    AND DATE                      
*                                                                               
MST06F   DS    0H                                                               
         GOTO1 VMINIO,MSDMCB,('MINDEL',MINBLKD)                                 
         CLI   MINERR,0                                                         
         BE    MST06                                                            
         DC    H'0'                                                             
*                                                                               
MST07    DS    0H                                                               
*                                  NEW RECORD                                   
MST09    DS    0H                                                               
         OC    SPTCOUNT,SPTCOUNT   ARE THERE ANY INVOICE ITEMS?                 
         BZ    MSTX                NO, DONE                                     
*                                                                               
         BAS   RE,INVERRS          CHECK FOR INVALID FILMCODE FOR PRD           
*                                  ADD NEW HEADER ELEM                          
         XC    MELEM,MELEM                                                      
         LA    R4,MELEM                                                         
         USING MSRSTELD,R4                                                      
*                                                                               
         MVI   MSRSTEL,MSRSTELQ                                                 
         MVI   MSRSTLEN,MSRSTLNQ                                                
         MVC   MSRSTDAT,TODAY                                                   
*                                                                               
         MVI   MSRSTSTA,C'S'       SUCCESSFUL                                   
         TM    SUSTATUS,X'40'                                                   
         BZ    *+8                                                              
         MVI   MSRSTSTA,C'U'       UN                                           
*                                                                               
         MVI   MSRSTMSD,C'N'       NO MISSING DATA, HAVE BUY+INV                
         TM    SUSTATUS,X'20'                                                   
         BZ    MST10                                                            
         MVI   MSRSTMSD,C'I'       INVOICE MISSING                              
         B     MST11                                                            
MST10    DS    0H                                                               
         TM    SUSTATUS,X'10'                                                   
         BZ    MST11                                                            
         MVI   MSRSTMSD,C'B'       BUYS MISSING                                 
*                                                                               
MST11    DS    0H                                                               
         MVC   MSDERRS,=6C'N'      CLEAR ERROR STATUS BYTES                     
         MVC   MSRSTERS,=6C'N'     ELEM BYTES TOO                               
*                                  (WILL BE RESET AT END)                       
         MVC   MSRSTTOS,MATSPTS    MOVE WHOLE BLOCK OF TOTALS                   
*                                                                               
         ICM   R0,15,TAXTOT        ADD TAX                                      
         ICM   R1,15,MSRSTORG      TO ORD GROSS                                 
         AR    R1,R0                                                            
         STCM  R1,15,MSRSTORG                                                   
         ICM   R1,15,MSRSTORN      AND ORD NET                                  
         AR    R1,R0                                                            
         STCM  R1,15,MSRSTORN                                                   
         ICM   R1,15,MSRSTING      AND INV GROSS                                
         AR    R1,R0                                                            
         STCM  R1,15,MSRSTING                                                   
         ICM   R1,15,MSRSTINN      AND INV NET                                  
         AR    R1,R0                                                            
         STCM  R1,15,MSRSTINN                                                   
*                                                                               
         MVC   MSRSTBLS,SVLOCKST   SET SAVED LOCKIN STATUS                      
         MVC   MSRSTBGS,SVLOCKS2   SET SAVED LOCKIN STATUS                      
         MVC   MSRMPERR,SVLOCKS3   SET SAVED MATCH/PAY ERRS                     
*                                                                               
         OC    ISPTS,ISPTS       SHOULDN'T GET HERE IF NO INV SPOTS             
         BNZ   *+6                                                              
         DC    H'0'                                                             
*                                                                               
         GOTO1 VMINIO,MSDMCB,('MINADD',MINBLKD)                                 
         CLI   RCWRITE,C'Y'        SKIP ADD IF NOT WRITING                      
         BNE   MST12                                                            
         CLI   MINERR,0                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
         DROP  R4                                                               
*                                  ACTIVITY ELEMENT                             
MST12    DS    0H                                                               
         LA    R2,MELEM                                                         
         USING ACTVD,R2                                                         
         CLI   NEWREC,C'Y'         IF NEW RECORD                                
         BE    MST32               BUILD NEW ELEM                               
         XC    MINEKEY,MINEKEY     ELSE FIND ONE ALREADY THERE                  
         MVI   MINEKEY,X'F1'                                                    
         MVI   MINFILTL,1                                                       
         GOTO1 VMINIO,MSDMCB,('MINHI',MINBLKD)                                  
         CLI   MINERR,0            TREAT ERROR AS NOT FOUND                     
         BE    MST34                                                            
*                                  NEW ACTIVITY ELEM                            
MST32    DS    0H                                                               
         XC    MELEM(ACTVLENQ+1),MELEM                                          
         MVI   ACTVEL,X'F1'                                                     
         MVI   ACTVLEN,ACTVLENQ                                                 
         MVC   ACTVADDT,TODAYB                                                  
         MVC   ACTVADID,RCORIGID                                                
         CLI   DDSFXSW,C'Y'        DDS FIX?                                     
         BNE   *+8                                                              
         OI    ACTVCHRE,X'80'      YES, SET AS 'REASON'                         
*                                                                               
         GOTO1 VMINIO,MSDMCB,('MINADD',MINBLKD)                                 
         CLI   MINERR,0                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
         B     MST40                                                            
*                                                                               
MST34    DS    0H                  UPDATE EXISTING ELEM                         
         MVC   ACTVCHDT,TODAYB                                                  
         MVC   ACTVCHID,RCORIGID                                                
         CLI   DDSFXSW,C'Y'        DDS FIX?                                     
         BNE   *+8                                                              
         OI    ACTVCHRE,X'80'      YES, SET AS 'REASON'                         
*                                                                               
         GOTO1 VMINIO,MSDMCB,('MINWRT',MINBLKD)                                 
         CLI   MINERR,0                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
         DROP  R2                                                               
         EJECT                                                                  
**********************************************************************          
*        MATCH RECORD INVOICE ELEMS TO I2 INVOICE LIST                          
**********************************************************************          
*                                                                               
*                                  INVOICE ELEMS VS I2 INV LIST                 
MST40    DS    0H                                                               
         MVI   CHGSWI,0            INVOICE CHANGE SWITCH                        
         MVI   CHGSWR,0            RECORD CHANGE SWITCH                         
         LA    R4,MELEM                                                         
         USING MSRINELD,R4                                                      
         L     R6,AINOLST          A(INVOICE LIST)                              
         USING INOLSTD,R6                                                       
         SR    R7,R7                                                            
*                                                                               
MST43    DS    0H                  COUNT INVOICES FOR XSORT                     
         CLI   0(R6),C' '                                                       
         BNH   MST43B                                                           
         LA    R6,INOLSTL(R6)                                                   
         LA    R7,1(R7)                                                         
         B     MST43                                                            
*                                                                               
MST43B   DS    0H                                                               
         L     R6,AINOLST                                                       
         LTR   R7,R7               GET INVOICES IN ASCENDING ORDER              
         BNP   MST43D                                                           
         GOTO1 XSORT,MSDMCB,AINOLST,(R7),INOLSTL,10,0                           
*                                 BUILD TABLE OF ELEMS (INV# AND SEQ#)          
MST43D   DS    0H                                                               
         LA    R7,ELTAB                                                         
         MVI   0(R7),C' '          SET EOL                                      
         XC    MINEKEY,MINEKEY   FIRST ELEM                                     
         MVI   MINEKEY,MSRINELQ                                                 
         MVI   MINFILTL,1                                                       
*                                                                               
         SR    R3,R3                                                            
         GOTO1 VMINIO,MSDMCB,('MINHI',MINBLKD)                                  
         B     MST45B                                                           
*                                                                               
MST45    DS    0H                                                               
         GOTO1 VMINIO,MSDMCB,('MINSEQ',MINBLKD)                                 
MST45B   DS    0H                                                               
         CLI   MINERR,0            TREAT ALL ERRS HERE AS EOF                   
         BNE   MST45D                                                           
*                                                                               
         MVC   0(10,R7),MSRININO   SET INVOICE #                                
         MVC   10(2,R7),MSRINSEQ   AND SEQ                                      
         MVC   SAVSEQ,MSRINSEQ     AND SAVE HIGHEST SEQ #                       
         LA    R7,12(R7)                                                        
         MVI   0(R7),C' '          SET EOL                                      
         LA    R3,1(R3)            BUMP ELEM COUNT                              
         LA    R0,MAXELSR          TEXT VS MAX                                  
         CR    R3,R0                                                            
         BL    MST45                                                            
*                                                                               
MST45D   DS    0H                                                               
         LA    R7,ELTAB                                                         
         LTR   R3,R3               ELEM COUNT                                   
         BNP   MST46                                                            
         GOTO1 XSORT,MSDMCB,ELTAB,(R3),12,10,0                                  
*                                                                               
MST46    DS    0H                  FIRST/NEXT ELEM                              
         MVC   HLDENI,0(R7)                                                     
         CLI   0(R7),C' '          EOL                                          
         BH    *+10                                                             
         MVC   HLDENI,=10X'FF'                                                  
*                                                                               
MST47    DS    0H                                                               
         MVC   HLDINI,0(R6)        FIRST/NEXT LIST ENTRY                        
         CLI   0(R6),C' '          EOL                                          
         BH    *+10                                                             
         MVC   HLDINI,=10X'FF'                                                  
*                                  YES, MARK INV ELEMS PAID                     
MST50    DS    0H                                                               
         CLC   HLDINI,HLDENI       COMPARE INVOICE LIST TO ELEMS                
         BH    MST52                                                            
         BL    MST54                                                            
*                                  EQUAL                                        
         OC    CHGSWR,CHGSWI       'OR' UP CHANGE BITS                          
         MVI   CHGSWI,0            CLEAR INVOICE CHANGE SW                      
*                                                                               
         CLI   HLDINI,X'FF'         DONE IF EOL                                 
         BE    MST58                                                            
*                                                                               
         XC    MINEKEY,MINEKEY      GET INVOICE ELEM                            
         MVI   MINEKEY,MSRINELQ                                                 
         MVC   MINEKEY+MSRINSEQ-MSRINELD-1(2),10(R7) SET SEQ #                  
         MVI   MINFILTL,3                                                       
*                                                                               
         GOTO1 VMINIO,MSDMCB,('MINHI',MINBLKD)                                  
         CLI   MINERR,0                                                         
         BE    *+6                                                              
         DC    H'0'                ELEM NOT FOUND                               
*                                                                               
         CLC   INOLAMT,MSRINAMT    ARE AMOUNTS =?                               
         BNE   MST50F                                                           
*                            **  NOTE- $0 INVOICE NEVER CAUSES PAYABLE          
MST50D   DS    0H                  EQUAL AMOUNTS                                
         OC    MSRINAMT,MSRINAMT   IS IT A $0 INVOICE?                          
         BNZ   *+8                                                              
         OI    MSRINST,MSRINPYQ    YES, SET PAID                                
         B     MST50J                                                           
*                                                                               
MST50F   DS    0H                  UNEQUAL AMOUNTS                              
         OI    CHGSWI,X'80'         SET HAVE CHG (FOR PAID STATUS)              
         OI    CHGSWI,X'08'         SET HAVE CHG (FOR APPROVAL STATUS)          
         NI    MSRINST,255-MSRINPYQ  SET NOT PAID                               
         MVC   MSRINAMT,INOLAMT      USE I2 INVOICE AMOUNT                      
         OC    MSRINAMT,MSRINAMT   IS IT A $0 INVOICE?                          
         BNZ   MST50J              NO, DONE                                     
         NI    CHGSWI,X'7F'         YES, SET NOT A PAYABLE CHG                  
         OI    MSRINST,MSRINPYQ    AND SET PAID                                 
*                                                                               
MST50J   DS    0H                                                               
         OC    CHGSWR,CHGSWI       'OR' UP CHANGE BITS                          
         MVI   CHGSWI,0            CLEAR INVOICE CHANGE SW                      
*                                                                               
         MVC   MSRINADT,INOLADAT                                                
*        MVC   MSRINCTL,INOLCTL                                                 
         TM    MSRINCTL,X'01'      INVOICE IS PAID                              
         BNO   *+8                                                              
         OI    MSRINST,MSRINPYQ    SET BOTH STATII AS PAID                      
         GOTO1 DATCON,MSDMCB,(3,INOLDAT),(0,MSRINIDT)                           
         GOTO1 VMINIO,MSDMCB,('MINWRT',MINBLKD)                                 
         CLI   MINERR,0                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
*                                  GET NEXT ELTAB ENTRY                         
         LA    R7,12(R7)                                                        
         MVC   HLDENI,0(R7)                                                     
         CLI   0(R7),C' '                                                       
         BH    *+10                                                             
         MVC   HLDENI,=10X'FF'                                                  
*                                                                               
         LA    R6,INOLSTL(R6)      AND BUMP TO NEXT LIST ENTRY                  
         B     MST47                                                            
*                                                                               
MST52    DS    0H                  LIST ENTRY HIGH - DELETE ELEM                
         OI    CHGSWR,X'20'                                                     
         XC    MINEKEY,MINEKEY   FIRST ELEM                                     
         MVI   MINEKEY,MSRINELQ                                                 
         MVC   MINEKEY+MSRINSEQ-MSRINELD-1(2),10(R7) SET SEQ #                  
         MVI   MINFILTL,3                                                       
*                                                                               
         GOTO1 VMINIO,MSDMCB,('MINHI',MINBLKD)                                  
         CLI   MINERR,0                                                         
         BE    *+6                                                              
         DC    H'0'                ELEM NOT FOUND                               
*                                                                               
         CLI   MSTRACE,C'Y'                                                     
         BNE   MST53                                                            
         ZIC   R2,MELEM+1                                                       
         MVC   P(3),=C'DEL'                                                     
         GOTO1 HEXOUT,MSDMCB,MELEM,P+4,(R2),=C'N'                               
         GOTO1 REPORT                                                           
*                                                                               
MST53    DS    0H                                                               
         GOTO1 VMINIO,MSDMCB,('MINDEL',MINBLKD)                                 
         CLI   MINERR,0                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         LA    R7,12(R7)           NEXT ELTAB ENTRY                             
         B     MST46                                                            
*                                                                               
MST54    DS    0H                  ELEMENT HIGH - ADD NEW ELEM                  
         XC    MELEM,MELEM                                                      
         MVI   MSRINEL,MSRINELQ                                                 
         MVI   MSRINLEN,MSRINLNQ                                                
         ICM   RF,3,SAVSEQ         NEW SEQ NUMBER                               
         LA    RF,1(RF)                                                         
         STCM  RF,3,SAVSEQ                                                      
         MVC   MSRINSEQ,SAVSEQ                                                  
         MVC   MSRININO,INOLINV                                                 
         MVI   MSRINGN,C'G'        GROSS NOT NET                                
         MVC   MSRINADT,INOLADAT                                                
*        MVC   MSRINCTL,INOLCTL                                                 
         TM    MSRINCTL,X'01'      INVOICE IS PAID                              
         BNO   *+8                                                              
         OI    MSRINST,MSRINPYQ    SET BOTH STATII AS PAID                      
         GOTO1 DATCON,MSDMCB,(3,INOLDAT),(0,MSRINIDT)                           
         MVC   MSRINAMT,INOLAMT                                                 
*                                                                               
         OI    CHGSWI,X'40'         SET HAVE ADD (FOR PAID STATUS)              
         OI    CHGSWI,X'04'         SET HAVE ADD (FOR APPROVAL STATUS)          
         OC    MSRINAMT,MSRINAMT   IS IT A $0 INVOICE?                          
         BNZ   MST55               NO, DONE                                     
         NI    CHGSWI,X'BB'        YES, SET OFF PAY/APR ADD                     
         OI    MSRINST,MSRINPYQ    AND SET PAID                                 
*                                                                               
MST55    DS    0H                                                               
         OC    CHGSWR,CHGSWI       'OR' UP CHANGE BITS                          
         MVI   CHGSWI,0            CLEAR INVOICE CHANGE SW                      
*                                                                               
         GOTO1 VMINIO,MSDMCB,('MINADD',MINBLKD)                                 
         CLI   MINERR,0                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         LA    R6,INOLSTL(R6)      NEXT INVOICE LIST ENTRY                      
         B     MST47                                                            
*                                                                               
MST58    DS    0H                  END OF ELEMS AND INVOICE LIST                
         TM    CHGSWR,X'0C'        ANY APPROVAL CHANGES?                        
         BZ    MST62               NO                                           
         CLI   SVICST,C'A'         ONLY CONSIDER IF APPROVED                    
         BNE   MST62                                                            
         CLC   SVICSTP(13),=C'AUTO APPROVED'                                    
         BNE   MST59                                                            
         CLC   SVICSTD,TODAY       SKIP IF AUTO-APP TODAY                       
         BE    MST64                                                            
*                                                                               
MST59    XC    MINEKEY,MINEKEY     SET APPROVAL STATUS TO P                     
         MVI   MINEKEY,MSRICELQ    (PREVIOUS, PARTIAL, WHATEVER)                
         MVI   MINFILTL,1                                                       
*                                                                               
         GOTO1 VMINIO,MSDMCB,('MINHI',MINBLKD)                                  
         CLI   MINERR,0            IF NO STATUS ELEM, DONE                      
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         MVI   MELEM+MSRICST-MSRICELD,C'P'   ELSE SET TO P                      
         OI    CHGSWR,X'10'                                                     
         OI    MELEM+MSRICST2-MSRICELD,MSRICUSQ  (PARTIAL APPRVD)               
         GOTO1 VMINIO,MSDMCB,('MINWRT',MINBLKD)                                 
         CLI   MINERR,0                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
MST62    DS    0H                                                               
         TM    CHGSWR,X'C0'        ANY PAY STATUS CHANGES?                      
         BZ    MST64               NO                                           
         CLI   SVIPST,C'Y'         YES, IS REC MARKED PAID?                     
         BNE   MST64               NO, OK                                       
*                                  YES, SET TO UNPAID                           
         XC    MINEKEY,MINEKEY                                                  
         MVI   MINEKEY,MSRIPELQ                                                 
         MVI   MINFILTL,1                                                       
*                                                                               
         GOTO1 VMINIO,MSDMCB,('MINHI',MINBLKD)                                  
         CLI   MINERR,0            PAY ELEM SHOULD BE THERE                     
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         MVI   MELEM+MSRIPST-MSRIPELD,C'N'   SET UNPAID                         
         OI    CHGSWR,X'01'                                                     
         GOTO1 VMINIO,MSDMCB,('MINWRT',MINBLKD)                                 
         CLI   MINERR,0                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
MST64    DS    0H                                                               
*                                  CLOSE AFTER NEW STATUS REC ADD               
*                                  (NOTE- FINAL CLOSE IS IN CLSPROC)            
MST70    DS    0H                                                               
         CLI   NEWREC,C'Y'                                                      
         BNE   MST80                                                            
         CLI   RCWRITE,C'Y'        BUT ONLY IF WRITING                          
         BNE   MST80                                                            
         GOTO1 VMINIO,MSDMCB,('MINCLS',MINBLKD)                                 
         CLI   MINERR,0                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
MST80    DS    0H                                                               
MSTX     DS    0H                                                               
         B     EXIT                                                             
         EJECT                                                                  
***********************************************************************         
*                                                                               
*        ORDERED NOT RUN PROC                                                   
*                                                                               
***********************************************************************         
*                                                                               
ONRPROC  DS    0H                                                               
         TM    MSRCTL,MSONRQ       DOING ONR?                                   
         BZ    ONRX                                                             
*                                                                               
         LA    R6,P+0                                                           
         USING LNAD,R6                                                          
         CLI   LNAIDTE,C'A'        IF HAVE INVOICE DATE                         
         BNL   ONRX                IS NOT AN ONR                                
*                                                                               
         MVC   MSLINE(LNAIDTE-LNAD),P+0     ONR LINE IS AT P+0                  
         MVI   ELCODE,MSRTXONQ                                                  
         MVI   MSONRE,C'Y'         SET HAVE ONR ERROR                           
         BAS   RE,GENEL            GENERIC TEXT ELEM ROUTINE                    
*                                                                               
ONRX     DS    0H                                                               
         B     EXIT                                                             
         DROP  R6                                                               
         EJECT                                                                  
***********************************************************************         
*                                                                               
*        RUN NOT ORDERED PROC                                                   
*                                                                               
***********************************************************************         
*                                                                               
RNOPROC  DS    0H                                                               
         TM    MSRCTL,MSRNOQ       DOING RNO?                                   
         BZ    RNOX                                                             
         MVI   MSRNOE,C'Y'         SET HAVE RNO ERROR                           
*                                                                               
         MVC   MSLINE(LNBADEM-LNBDTE),P+5   EXCLUDE DEMOS                       
         MVI   ELCODE,MSRTXRNQ                                                  
         BAS   RE,GENEL            GENERIC TEXT ELEM ROUTINE                    
*                                                                               
RNOX     DS    0H                                                               
         B     EXIT                                                             
         EJECT                                                                  
***********************************************************************         
*                                                                               
*        SEPARATION PROC                                                        
*                                                                               
***********************************************************************         
*                                                                               
SEPPROC  DS    0H                                                               
         TM    MSRCTL,MSSEPQ       DOING SEP?                                   
         BZ    SEPX                                                             
*                                                                               
         LA    R6,P                                                             
         USING LNBD,R6                                                          
         CLI   LNBTIM-1,C'='       VARIOUS SEPARATION ERRORS                    
         BE    SEP04                                                            
         CLI   LNBTIM-1,C'+'                                                    
         BE    SEP04                                                            
         CLI   LNBTIM-1,C'*'                                                    
         BE    SEP04                                                            
         B     SEPX                                                             
*                                                                               
SEP04    DS    0H                                                               
*                                  SEP LINE IS RNO LINE                         
         MVC   MSLINE(LNBADEM-LNBDTE),P+5   EXCLUDE DEMOS                       
         MVI   ELCODE,MSRTXSPQ                                                  
         MVI   MSSEPE,C'Y'         SET HAVE SEP ERROR                           
         BAS   RE,GENEL            GENERIC TEXT ELEM ROUTINE                    
*                                                                               
SEPX     DS    0H                                                               
         B     EXIT                                                             
         DROP  R6                                                               
         EJECT                                                                  
***********************************************************************         
*                                                                               
*        HORIZONTAL/VERTICAL PROC                                               
*                                                                               
***********************************************************************         
*                                                                               
HVRPROC  DS    0H                                                               
         TM    MSRCTL,MSHVRQ       DOING HVR?                                   
         BZ    HVRX                                                             
         LA    R6,P+0                                                           
         USING LNAD,R6                                                          
         CLI   LNAROT+4,C'*'       IDENTIFIES H/V PROBLEM                       
         BNE   HVRX                                                             
*                                                                               
         MVI   MSHVRE,C'Y'         SET HAVE HVR ERROR                           
         MVC   MSLINE(LNAIDTE-LNAD),P+0    ORDERED LINE AT P+0                  
         MVI   ELCODE,MSRTXHVQ                                                  
         BAS   RE,GENEL                                                         
*                                                                               
HVRX     DS    0H                                                               
         B     EXIT                                                             
         DROP  R6                                                               
         EJECT                                                                  
***********************************************************************         
*                                                                               
*        COMMERCIAL ROTATION PROC                                               
*                                                                               
***********************************************************************         
*                                                                               
CROPROC  DS    0H                                                               
         TM    MSRCTL,MSCROQ       DOING CRO?                                   
         BZ    CROX                                                             
         CLI   FILMDSCP,C'Y'       ANY ACTUAL PROBLEMS?                         
         BNE   CROX                NO, SKIP                                     
         MVI   MSCROE,C'Y'         SET HAVE CRO ERROR                           
*                                                                               
         MVI   ELCODE,MSRTXCRQ     ELEM CODE                                    
         L     R2,AFRPTBUF         FILM REPORT PRINT LINE BUFFER                
*                                                                               
CRO14    DS    0H                                                               
         CLI   0(R2),0             END                                          
         BE    CRO30                                                            
         CLI   0(R2),X'FF'         END                                          
         BE    CRO30                                                            
*                                                                               
         ZIC   R3,2(R2)            NUMBER OF LINES                              
         LA    R2,3(R2)                                                         
*                                                                               
CRO16    DS    0H                                                               
         ZIC   R4,0(R2)            LENGTH OF LINE + 1                           
         SH    R4,=H'2'                                                         
         BM    CRO18                                                            
         EX    R4,*+8                                                           
         B     *+10                                                             
         MVC   MSLINE(0),1(R2)                                                  
         BAS   RE,GENEL                                                         
         MVI   MSLINE,C' '                                                      
         MVC   MSLINE+1(L'MSLINE-1),MSLINE                                      
*                                                                               
CRO18    DS    0H                                                               
         LA    R2,2(R2)                                                         
         AR    R2,R4                                                            
         BCT   R3,CRO16                                                         
*                                                                               
         B     CRO14                                                            
*                                                                               
CRO30    DS    0H                                                               
CROX     DS    0H                                                               
         B     EXIT                                                             
         EJECT                                                                  
***********************************************************************         
*                                                                               
*        INVALID COMMERCIAL PROC                                                
*                                                                               
*        NOTE- DOESN'T HANDLE PIGGY BACK 2ND FILM                               
*        NOTE- CALLED BOTH WITH ORD+INV LINE AND WITH UNORD LINE                
*                                                                               
***********************************************************************         
*                                                                               
ICOPROC  DS    0H                                                               
         TM    MSRCTL,MSICOQ       DOING ICO?                                   
         BZ    ICOX                                                             
         MVI   MSBYTE,0                                                         
*                                                                               
         CLI   PAGSW,C'U'          UNORDERED LINE                               
         BNE   IC20                                                             
*                                                                               
         MVC   MSLINE(LNBADEM-LNBDTE),P+5   EXCLUDE DEMOS                       
         LA    R6,MSLINE                                                        
         USING LNBDTE,R6                                                        
*                                                                               
         CLI   LNBFILM,C' '        MISSING FILM IS ERROR                        
         BH    ICO3                                                             
         CLI   QMED,C'R'           MISSING NOT ERROR FOR RADIO                  
         BE    ICO6                                                             
         MVC   LNBFILM(9),=C'MISSING**'                                         
         MVI   MSBYTE,1                                                         
         B    ICO6                                                              
*                                                                               
ICO3     DS    0H                                                               
         LA    R2,LNBFILM+8                                                     
         CLI   LNBFILM+8,C'*'                                                   
         BNE   ICO4                                                             
         MVC   LNBFILM+9(03),=C'INV'                                            
         LA    R2,LNBFILM+12                                                    
         MVI   MSBYTE,1                                                         
*                                                                               
ICO4     DS    0H                                                               
         CLC   LNBFILM-2(2),=C'R*'                                              
         BNE   ICO6                                                             
         MVC   1(14,R2),=C'RELEASE/RECALL'                                      
         CLI   MSBYTE,1                                                         
         BNE   *+8                                                              
         MVI   0(R2),C','                                                       
         MVI   MSBYTE,1                                                         
*                                                                               
ICO6     DS    0H                                                               
         CLI   MSBYTE,0            ANY UNORDERED LINE FILM ERROR?               
         BE    ICOX                NO DONE                                      
         MVI   ELCODE,MSRTXICQ                                                  
         BAS   RE,GENEL            GENERIC TEXT ELEM ROUTINE                    
         MVI   MSICOE,C'Y'         SET HAVE ICO ERROR                           
         B     ICOX                                                             
*                                                                               
IC20     DS    0H                                                               
         CLI   PAGSW,C'O'          ORDERED LINE                                 
         BNE   IC30                                                             
*                                                                               
         MVC   MSLINE(LNAEDEM-LNAIDTE),P+LNAIDTE-LNAD  LNAIDTE+                 
         LA    R6,MSLINE                                                        
         USING LNAIDTE,R6                                                       
*                                                                               
         CLI   LNAIDTE,C' '        IF NO INVOICE MATCH                          
         BNH   ICOX                CANNOT HAVE FILM ERROR                       
*                                                                               
         CLI   LNAIFILM,C' '       MISSING FILM IS ERROR                        
         BH    IC23                                                             
         CLI   QMED,C'R'           MISSING NOT ERROR FOR RADIO                  
         BE    IC26                                                             
         MVC   LNAIFILM(9),=C'MISSING**'                                        
         MVI   MSBYTE,1                                                         
         B     IC26                                                             
*                                                                               
IC23     DS    0H                                                               
         LA    R2,LNAIFILM+8                                                    
         MVC   MSDUB(2),LNAIFILM+8                                              
         CLI   MSDUB,C'*'                                                       
         BNE   IC26                                                             
         MVC   LNAIFILM+9(03),=C'INV'                                           
         LA    R2,LNAIFILM+12                                                   
         MVI   MSBYTE,1                                                         
*                                                                               
IC24     DS    0H                                                               
         CLC   MSDUB(2),=C'*R'                                                  
         BNE   IC26                                                             
         MVC   1(14,R2),=C'RELEASE/RECALL'                                      
         CLI   MSBYTE,1                                                         
         BNE   *+8                                                              
         MVI   0(R2),C','                                                       
         MVI   MSBYTE,1                                                         
*                                                                               
IC26     DS    0H                                                               
         CLI   MSBYTE,0            ANY UNORDERED LINE FILM ERROR?               
         BE    ICOX                NO DONE                                      
         MVI   ELCODE,MSRTXICQ                                                  
         BAS   RE,GENEL            GENERIC TEXT ELEM ROUTINE                    
         MVI   MSICOE,C'Y'         SET HAVE ICO ERROR                           
         B     ICOX                                                             
*                                                                               
IC30     DS    0H                                                               
ICOX     DS    0H                                                               
         B     EXIT                                                             
         DROP  R6                                                               
         EJECT                                                                  
***********************************************************************         
*                                                                               
*        CLOSE PROC                                                             
*                                                                               
***********************************************************************         
*                                                                               
CLSPROC  DS    0H                                                               
         B     CLSP00                                                           
         ORG   MSTAT+4096                                                       
*                                                                               
CLSP00   DS    0H                                                               
         CLC   MSDERRS,=6C'N'      ANY ERROR BYTES TO SET?                      
         BE    CLSP01                                                           
*                                                                               
         XC    MINEKEY,MINEKEY                                                  
         MVI   MINEKEY,MSRSTELQ                                                 
         MVI   MINFILTL,1                                                       
         GOTO1 VMINIO,MSDMCB,('MINHI',MINBLKD)                                  
         CLI   MINERR,0                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         LA    R4,MELEM                                                         
         USING MSRSTELD,R4                                                      
         MVC   MSRSTERS,MSDERRS    SET DISCREPANCY/ERROR BYTES                  
         GOTO1 VMINIO,MSDMCB,('MINWRT',MINBLKD)                                 
         CLI   MINERR,0                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
CLSP01   DS    0H                                                               
         XC    MINEKEY,MINEKEY                                                  
         MVI   MINFILTL,0                                                       
         CLI   MSTRACE,C'Y'                                                     
         BNE   CLSP60                                                           
         GOTO1 VMINIO,MSDMCB,('MINHI',MINBLKD)                                  
         B     CLSP03B                                                          
*                                                                               
CLSP03   DS    0H                                                               
         GOTO1 VMINIO,MSDMCB,('MINSEQ',MINBLKD)                                 
CLSP03B  DS    0H                                                               
         CLI   MINERR,0                                                         
         BNE   CLSP50                                                           
*                                                                               
         L     R3,MINELEM                                                       
         ZIC   R6,1(R3)                                                         
         GOTO1 HEXOUT,MSDMCB,0(R3),P,(R6),=C'N'                                 
         GOTO1 REPORT                                                           
         B     CLSP03                                                           
*                                                                               
CLSP50   DS    0H                                                               
         MVC   P(3),=C'DA='                                                     
         GOTO1 HEXOUT,MSDMCB,MINRTAB,P+3,4,=C'N'                                
         GOTO1 REPORT                                                           
*                                                                               
         CLI   CHGSWR,0             ANY EFFECTIVE CHANGES ?                     
         BE    CLSP60                                                           
         MVC   P(6),=C'CHGSW='                                                  
         GOTO1 HEXOUT,MSDMCB,CHGSWR,P+6,1,=C'N'                                 
         GOTO1 REPORT                                                           
                                                                                
CLSP60   DS    0H                                                               
         GOTO1 VMINIO,MSDMCB,('MINCLS',MINBLKD)                                 
         CLI   MINERR,0                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
CLSX     DS    0H                                                               
         B     EXIT                                                             
*                                                                               
         EJECT                                                                  
***********************************************************************         
*                                                                               
*        GENEL - GENERIC TEXT ELEM ROUTINE                                      
*                                                                               
*                CREATE AN 'ELCODE' ELEM WITH DATA IN MSLINE                    
*                                                                               
***********************************************************************         
*                                                                               
GENEL    NTR1                                                                   
         LA    RF,MSLINE+L'MSLINE-1   STRIP TRAILING BLANKS                     
         CLI   0(RF),C' '                                                       
         BH    *+8                                                              
         BCT   RF,*-8                                                           
         LA    R0,MSLINE                                                        
         SR    RF,R0                                                            
         BNM   GENL10                                                           
         MVI   MSLINE,C'*'         BLANK LINE                                   
         SR    RF,RF                                                            
*                                                                               
GENL10   DS    0H                                                               
         LA    R4,MELEM                                                         
         USING MSRTXELD,R4                                                      
         XC    MELEM,MELEM                                                      
         MVC   MSRTXEL,ELCODE      SET ELEM CODE                                
         LA    R0,MSRTXTXT-MSRTXEL+1(RF)   ELEM LENGTH                          
         STC   R0,MSRTXLEN                                                      
         MVC   MSRTXSEQ,ELSEQ      SEQUENCE                                     
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   MSRTXTXT(0),MSLINE                                               
*                                                                               
         CLI   RCWRITE,C'Y'        IF NOT WRITING TO FILE,                      
         BNE   GENL40              SKIP ADDS (TO AVOID MINIO PROBLEMS)          
*                                                                               
         GOTO1 VMINIO,MSDMCB,('MINADD',MINBLKD)                                 
         CLI   MINERR,0                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
GENL40   DS    0H                                                               
         LH    RF,ELSEQ            BUMP ELEM SEQUENCE NUMBER                    
         LA    RF,1(RF)                                                         
         STH   RF,ELSEQ                                                         
*                                                                               
GENLX    DS    0H                                                               
         XIT1                                                                   
         SPACE 3                                                                
         EJECT                                                                  
***********************************************************************         
*        INVERRS - CHECK INVOICE DETAILS FOR FILMCODE ERROR OF                  
*                  INVALID FILM FOR PRD                                         
***********************************************************************         
*                                                                               
INVERRS  NTR1                                                                   
         USING IELEMD,R3           INVOICE TABLE                                
         L     R3,AFINV                                                         
IER10    C     R3,ALINV            LAST INVOICE                                 
         BNL   IERX                                                             
*                                                                               
         TM    ISTAT,ISTPRDF       INVALID FILM FOR PRD                         
         BO    IER20                                                            
         LA    R3,IELI2LEN(R3)                                                  
         B     IER10                                                            
*                                                                               
IER20    OI    SVLOCKS3,MSRMPFPR   SET FILM INV FOR PRD IN MSRMPERR             
IERX     XIT1                                                                   
         DROP  R3                                                               
***********************************************************************         
*        LTORG                                                                  
***********************************************************************         
         LTORG                                                                  
         SPACE 2                                                                
*                                                                               
*                                  EQUATES FOR MSRCTL                           
MSONRQ   EQU   X'80'               ONR                                          
MSRNOQ   EQU   X'40'               RNO                                          
MSHVRQ   EQU   X'20'               H/V                                          
MSSEPQ   EQU   X'10'               SEPARATION                                   
MSCROQ   EQU   X'08'               CMML ROTATION                                
MSICOQ   EQU   X'04'               INVALID COMERCIAL                            
*                                                                               
FRSTSW   DC    X'00'                                                            
ELSEQ    DC    H'1'                                                             
MAXRECSZ DC    H'3975'                                                          
SPTCOUNT DC    F'0'                                                             
VMINIO   DS    A                                                                
NEWREC   DS    CL1                                                              
SVLOCKST DS    XL1                                                              
SVLOCKS2 DS    XL1                                                              
SVLOCKS3 DS    XL1                                                              
SVSTDAT  DS    CL6                                                              
SVIPDAT  DS    CL6                                                              
SVIPST   DS    CL1                                                              
SVICST   DS    CL1                                                              
SVICSTP  DS    CL13                                                             
SVICSTD  DS    CL3                                                              
SVINVSEQ DS    XL2                                                              
DDSFXSW  DS    CL1                 Y=DDS FIX RUN                                
SAVSEQ   DS    XL2                                                              
HLDINI   DS    CL10                                                             
HLDENI   DS    CL10                                                             
CHGSWI   DS    X                                                                
CHGSWR   DS    X                                                                
*                                  BEST NOT TO USE I2'S FIELDS                  
MSDUB    DS    D                                                                
MSFULL   DS    F                                                                
MSHALF   DS    H                                                                
MSBYTE   DS    X                                                                
         DS    0F                                                               
MSWORK   DS    XL64                                                             
MSDMCB   DS    6F                                                               
MSMODE   DS    CL1                                                              
MSTRACE  DS    C                                                                
*                                  DATA ERROR TYPES (Y=YES)                     
MSDERRS  DS    0CL6                                                             
MSONRE   DS    CL1                 ORDERED NOT RUN                              
MSRNOE   DS    CL1                 RUN NOT ORDERED                              
MSHVRE   DS    CL1                 HORIZ/VERT ROTATION                          
MSCROE   DS    CL1                 CMML ROTATION                                
MSICOE   DS    CL1                 INVALID COMMERCIAL                           
MSSEPE   DS    CL1                 SEPARATION                                   
*                                                                               
MSLINE   DS    CL132                                                            
*                                                                               
ELTAB    DS    0X                                                               
         ORG   *+(12*MAXELSR)+1                                                 
MAXELSR  EQU   30                  MAX THAT MIGHT BE IN IN RECORD               
*                                  (SHOULDN'T BE MORE THAN 5)                   
         DS    0D                                                               
         DC    CL8'*MINBLK*'                                                    
MINBLK   DS    XL(MINBLKL)                                                      
         DS    0D                                                               
         DC    CL8'*MELEM**'                                                    
MELEM    DS    XL256                                                            
*                                                                               
         DS    0D                                                               
         DC    CL8'*MBUFF**'                                                    
MBUFF1   DS    XL4000                                                           
MBUFF2   DS    XL4000                                                           
         DS    0D                                                               
         DC    CL8'*MRTAB**'                                                    
MRTAB    DS    XL(200*(6+L'MSRKMAST))                                           
         EJECT                                                                  
       ++INCLUDE SPGENMSR                                                       
         SPACE 2                                                                
       ++INCLUDE DDMINBLK                                                       
         EJECT                                                                  
       ++INCLUDE SPREPI2WKN                                                     
*                                                                               
*++INCLUDE DDACTIVD/SPREPWORKD                                                  
         PRINT OFF                                                              
       ++INCLUDE DDACTIVD                                                       
       ++INCLUDE SPREPWORKD                                                     
*                                                                               
         PRINT ON                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'093SPREPI2MS 12/11/07'                                      
         END                                                                    
