*          DATA SET PPRDSHR    AT LEVEL 012 AS OF 05/01/02                      
*CATALP PPRDSHR                                                                 
         TITLE 'PPRDSHR - PRINTPAK- REG/DST SHARES'                             
         PRINT NOGEN                                                            
PPRDSHR  CSECT                                                                  
         NMOD1 6,**RDSHR                                                        
         SPACE 2                                                                
         USING WRKD,RC                                                          
         LM    R8,RA,0(R1)         R8= A(LTLREC)                                
*                                  R9=A(CLT/DIV/REG/DST)                        
*                                  RA= A(OUTPUT AREA)                           
         USING SHROUT,RA                                                        
         MVI   SHROUT,C' '                                                      
         MVC   SHROUT+1(L'SHROUT-1),SHROUT                                      
         MVI   SHRFRST,1                                                        
         MVI   SHRLNCNT,0                                                       
*                                                                               
*                             FIND THIS REG/DST                                 
         SR    R3,R3                                                            
         SR    R0,R0                                                            
*                                                                               
         LA    R2,33(R8)                                                        
         MVI   ELCODE,X'71'                                                     
         CLI   0(R2),X'71'                                                      
         BE    RDSHR4A                                                          
RDSHR4   BAS   RE,NEXTEL                                                        
         BNE   RDSHR6                                                           
         USING PUBDSTEL,R2                                                      
RDSHR4A  DS    0H                                                               
         CLC   PUBDCLT(6),0(R9)    TEST SAME CLT/DIV                            
         BNE   RDSHR4                                                           
         CLI   6(R9),0                                                          
         BE    RDSHR4B             NO REGIONS                                   
         CLC   PUBDREG,6(R9)                                                    
         BE    RDSHR4B                                                          
         BH    RDSHR4                                                           
         MVI   SHRFRST,0                                                        
         B     RDSHR4                                                           
RDSHR4B  DS    0H                                                               
         CLI   9(R9),0                                                          
         BE    RDSHR4D             NO DISTS                                     
         CLC   PUBDDST,9(R9)                                                    
         BE    RDSHR4D                                                          
         BH    RDSHR4                                                           
         MVI   SHRFRST,0                                                        
         B     RDSHR4                                                           
RDSHR4D  DS    0H                                                               
RDSHR40  DS    0H                                                               
         OC    PUBDSHR(2),PUBDSHR     NO SHARES                                 
         BZ    RDSHR50                                                          
         MVC   HALF,PUBDSHR                                                     
         AH    R3,HALF             CUM SHARES                                   
         B     RDSHR4                                                           
*                                                                               
RDSHR6   DS    0H                                                               
         STCM  R3,15,47(RA)        SET PCT TOTAL AT START OF LINE 2             
*                                  (RETURN IT IF NO OTHER ASGN LINES)           
         LTR   R3,R3                                                            
         BZ    RDSHR50                                                          
         MVC   2(21,RA),=C'SHARE FOR THIS REGION'                               
         CLI   6(R9),0                                                          
         BE    RDSHR8              NO REGIONS                                   
         LA    R5,24(RA)                                                        
         CLI   9(R9),0                                                          
         BE    RDSHR10             NO DISTS                                     
         MVC   23(9,RA),=C'/DISTRICT'                                           
         LA    R5,33(RA)                                                        
         B     RDSHR10                                                          
*                                                                               
RDSHR8   DS    0H                                                               
         MVC   17(8,RA),=C'DISTRICT'                                            
         LA    R5,26(RA)                                                        
*                                                                               
RDSHR10  DS    0H                                                               
         MVI   0(R5),C'='                                                       
         LA    R5,2(R5)                                                         
         EDIT  (R3),(6,0(R5)),2,ALIGN=LEFT                                      
*                                                                               
         AR    R5,R0                                                            
         LR    R6,R5                                                            
         SH    R6,=H'3'                                                         
         CLC   0(3,R6),=C'.00'                                                  
         BNE   *+12                                                             
         MVC   0(3,R6),=CL3' '                                                  
         LR    R5,R6                                                            
         MVC   1(3,R5),=C'PCT'                                                  
*                                                                               
*                                                                               
         MVI   SHRLNCNT,1                                                       
         LA    RA,47(RA)           2ND LINE                                     
         DROP  RA                                                               
         CLI   4(R1),0             DONT PRINT OTHERS                            
         BNE   RDSHR50                                                          
*                                                                               
         MVC   0(15,RA),=C'SEE ALSO REGION'                                     
         CLI   6(R9),0                                                          
         BE    RDSHR12             NO REGIONS                                   
         MVI   BYTE,6              6 PER LINE                                   
         CLI   9(R9),0                                                          
         BE    RDSHR14             NO DISTS                                     
         MVC   9(07,RA),=C'REG/DST'                                             
         MVI   BYTE,3              3 PER LINE                                   
         B     RDSHR14                                                          
*                                                                               
RDSHR12  DS    0H                                                               
         MVC   9(8,RA),=C'DISTRICT'                                             
         MVI   BYTE,6              6 PER LINE                                   
*                                                                               
RDSHR14  DS    0H                                                               
         LA    R6,4                NO. OF LINES                                 
         SR    R5,R5                                                            
         IC    R5,BYTE                                                          
         LA    R7,18(RA)                                                        
         ST    R7,LASTOUT                                                       
         LA    R2,33(R8)                                                        
         CLI   0(R2),X'71'                                                      
         BE    RDSHR17                                                          
RDSHR16  DS    0H                                                               
         BAS   RE,NEXTEL                                                        
         BNE   RDSHR30                                                          
*                                                                               
RDSHR17  DS    0H                                                               
         CLC   PUBDCLT(6),0(R9)    TEST SAME CLT/DIV                            
         BNE   RDSHR16                                                          
         CLI   6(R9),0                                                          
         BE    RDSHR20             NO REGIONS                                   
         CLI   9(R9),0                                                          
         BE    RDSHR24             NO DISTS                                     
         CLC   PUBDREG(6),6(R9)                                                 
         BE    RDSHR16             BYPASS THIS REG/DST                          
         MVC   0(3,R7),PUBDREG                                                  
         MVI   3(R7),C'/'                                                       
         MVC   4(3,R7),PUBDDST                                                  
         MVI   7(R7),C','                                                       
         ST    R7,LASTOUT          SAVE LAST ONE                                
         LA    R7,9(R7)                                                         
         BCT   R5,RDSHR16                                                       
         LA    RA,45(R4)           NEW LINE                                     
         LA    R7,18(R4)                                                        
         IC    R5,BYTE             RESET BCT                                    
         BCT   R6,RDSHR16                                                       
         B     RDSHR30                                                          
*                                  NO REGIONS                                   
RDSHR20  DS    0H                                                               
         CLC   PUBDDST,9(R9)                                                    
         BE    RDSHR16             BYPASS THIS DIST                             
         MVC   0(3,R7),PUBDDST                                                  
         MVI   3(R7),C','                                                       
         ST    R7,LASTOUT          SAVE LAST                                    
         LA    R7,4(R7)                                                         
         BCT   R5,RDSHR16                                                       
         LA    RA,45(RA)           NEW LINE                                     
         LA    R7,18(RA)                                                        
         IC    R5,BYTE             RESET BCT                                    
         BCT   R6,RDSHR16                                                       
         B     RDSHR30                                                          
*                                  NO DISTS                                     
RDSHR24  DS    0H                                                               
         CLC   PUBDREG,6(R9)                                                    
         BE    RDSHR16             BYPASS THIS REGION                           
         L     R4,LASTOUT                                                       
         CLC   PUBDREG,0(R4)       TEST VS LAST                                 
         BE    RDSHR16                                                          
         MVC   0(3,R7),PUBDREG                                                  
         MVI   3(R7),C','                                                       
         ST    R7,LASTOUT          SAVE LAST                                    
         LA    R7,4(R7)                                                         
         BCT   R5,RDSHR16                                                       
         LA    RA,45(RA)           NEW LINE                                     
         LA    R7,18(RA)                                                        
         IC    R5,BYTE             RESET BCT                                    
         BCT   R6,RDSHR16                                                       
         B     RDSHR30                                                          
RDSHR30  DS    0H                                                               
         L     R7,LASTOUT                                                       
         CLI   BYTE,3                                                           
         BNE   *+8                                                              
         LA    R7,4(R7)                                                         
         MVI   3(R7),C' '          GET RID OF LAST COMMA                        
*                                                                               
         CLI   18(R9),C' '                                                      
         BH    *+8                                                              
         LA    R6,1(R6)                                                         
         LA    R0,6                                                             
         SR    R0,R6                                                            
         L     RA,8(R1)                                                         
         USING SHROUT,RA                                                        
         STC   R0,SHRLNCNT                                                      
*                                                                               
RDSHR50  DS    0H                                                               
RDSHRX   XIT1                                                                   
         SPACE 3                                                                
         SPACE 2                                                                
NEXTEL   DS    0H                                                               
         SR    R0,R0                                                            
         IC    R0,1(R2)                                                         
         AR    R2,R0                                                            
         CLI   0(R2),0                                                          
         BE    NEXTEL2                                                          
         CLC   ELCODE,0(R2)                                                     
         BER   RE                                                               
         B     NEXTEL+2                                                         
NEXTEL2  DS    0H                                                               
         LTR   R2,R2                                                            
         BR    RE                                                               
         LTORG                                                                  
         SPACE 3                                                                
WRKD     DSECT                                                                  
DUB      DS    D                                                                
FULL     DS    F                                                                
HALF     DS    H                                                                
BYTE     DS    X                                                                
ELCODE   DS    X                                                                
LASTOUT  DS    A                                                                
WORK     DS    CL24                                                             
         SPACE 2                                                                
*                                                                               
DISTELD  DSECT                                                                  
       ++INCLUDE PUBDSTELN                                                      
*                                                                               
*                                                                               
SHROUTD  DSECT                                                                  
SHROUT   DS    0CL227                                                           
SHRFRST  DS    X                                                                
SHRLNCNT DS    X                                                                
SHRLNS   DS    5CL45                                                            
*                                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'012PPRDSHR   05/01/02'                                      
         END                                                                    
