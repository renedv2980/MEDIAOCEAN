*          DATA SET DDPHSCNT   AT LEVEL 005 AS OF 05/07/20                      
*CATALP PHSCNT                                                                  
         TITLE 'DDPHSCNT - DATA SPACE PHASE LOAD COUNTER'                       
         PRINT NOGEN                                                            
***********************************************************************         
* P1(1) ACTION                                                                  
*       "I" INITIALIZE, EXIT IF ALREADY INITIALIZED                             
*       "R" RESET TABLE, INITIALIZE EVEN IF ALREADY INITIALIZED                 
*       "A" INCREMENT COUNT OR ADD PHASE                                        
*       "C" CLEAR COUNTS                                                        
*       "K" KILL COMMAND WILL STOP COUNTING AND SKIP THE CODE                   
* P1(3) FOR ACTION "I" OR "R":                                                  
*        A(LIST OF PHASES USED TO INITIALIZE)                                   
* P1(3) FOR ACTION "A":                                                         
*        A(CL8 OF THE PHASE NAME TO BE COUNTED)                                 
* P2(1)  0                                                                      
* P2(3) FOR ACTION "I" OR "R":                                                  
*        NUMBER OF ENTRIES IN PHASE LIST                                        
* P2(3) FOR ACTION "A":                                                         
*        0                                                                      
***********************************************************************         
PHSCNT   CSECT                                                                  
         NMOD1 WORKL,*PHSCNT*,RR=RE,CLEAR=YES                                   
         USING WORKD,RC            RC=A(W/S)                                    
         ST    RE,RELO             SAVE RELOCATION FACTOR                       
         ST    R1,CALLR1           SAVE CALLERS R1                              
         LARL  RA,COMMON           CONSTANTS/LITERALS/COMMON ROUTINES           
         USING COMMON,RA                                                        
         USING PHPARD,INPARM                                                    
         USING DMSPACED,DSPHSCNT                                                
*                                                                               
         SAM31 ,                                                                
         MVC   PHPARD(PHPARLQ),0(R1) SAVE INPUT PARAMETERS                      
*                                                                               
         BRAS  RE,SETUP            SETUP FOR THIS MODULE                        
         BNE   PHSCNTX             SOMETHING NOT RIGHT, JUST EXIT               
*                                                                               
         SELECT CLI,PHPACT,EQ      COMPARE ACTION AND BRANCH TO ROUTINE         
         WHEN (PHPINIT)            "I" INITIALIZE (IF NOT ALREADY)              
          BRAS  RE,PHINI                                                        
         WHEN (PHPRESET)           "R" RESET (FORCE INITIALIZATION)             
          BRAS  RE,PHINI                                                        
         WHEN (PHPADD)             "A" ADD ENTRY OR INCREMENT COUNT             
          XR    R3,R3                                                           
          ICM   R3,7,PHPNAME        PICK UP THE PHASE NAME                      
          BZ    PHSCNTX                                                         
          MVC   PHASEIN,0(R3)       THE PHASE TO BE ADDED                       
          BRAS  RE,PHADD                                                        
         WHEN (PHPCLR)             "C" CLEAR COUNTS                             
          BRAS  RE,PHCLR                                                        
         WHEN (PHPKILL)            "K" KILL THE COUNTING                        
          BRAS  RE,PHKILL                                                       
         OTHRWISE                                                               
          DC    H'0'               INVALID ACTION                               
         ENDSEL                                                                 
*                                                                               
PHSCNTX  BRAS  RE,ARSOFF           TURN OFF ACCESS REGISTER MODE                
         SAM24 ,                   TURN OFF XA                                  
*                                                                               
         CLI   BADERR,0            DID WE DO SOMETHING BAD                      
         BE    PHSCNTXX            NO: JUST EXIT                                
         CLI   BADMSGP,C'Y'        DID WE ALREADY PUT OUT BAD MESSAGE           
         BE    PHSCNTXX            YES: EXIT                                    
*                                                                               
         MVC   MSG,SPACES                                                       
         MVC   MSG(2),=AL2(54)                                                  
         MVC   MSG+2(50),BADMSG                                                 
         EDIT  BADERR,(2,MSG+52),FILL=0                                         
         MVI   BADMSGP,C'Y'                                                     
         WTO   TEXT=MSG                                                         
*                                                                               
PHSCNTXX XMOD1 ,                   RETURN IN 24-BIT MODE WITH ARS OFF           
                                                                                
***********************************************************************         
* INITIALIZE PHASE TABLE                                                        
***********************************************************************         
PHINI    NTR1                                                                   
*                                                                               
         BRAS  RE,ARSR2                                                         
         ICM   R2,15,ATABHDR                                                    
         BZ    PHINIX                                                           
         USING PHSTABD,R2                                                       
*                                                                               
         CLI   PHPACT,PHPRESET     DO WE WANT A HARD RESET?                     
         BE    PI010                                                            
         CLC   PHSEYE,EYEHEAD      HAVE WE ALREADY INITED?                      
         BE    PHINIX              YES: DO NOT INIT AGAIN                       
*                                                                               
PI010    XC    PHSHDR,PHSHDR       CLEAR AND SET EYE CATCHER                    
         XC    0(PHSLNQ,R2),0(R2)                                               
         XC    PHASES(256),PHASES                                               
         MVC   PHSEYE,EYEHEAD                                                   
*                                                                               
         LA    R2,PHSLNQ(,R2)      BUILD TABLE INIT                             
         MVC   0(L'EYEBTAB,R2),EYEBTAB                                          
         LA    R2,L'EYEBTAB(,R2)                                                
         STCM  R2,15,ABLDTAB       START BUILD TABLE AFTER HEADER               
*                                                                               
BT       USING BSPARA,PHSBBIN                                                   
         ICM   R2,15,ATABHDR       SET BUILD TABLE BINSRCH PARAMETERS           
         MVC   BT.BSPSTRT,ABLDTAB                                               
         OI    BT.BSPLENR,X'01'    FOR INIT, INSERT INTO BUILD TABLE            
         OI    BT.BSPLENR+1,X'80'  ACCESS REGISTER IN PARAMETER 7               
         LHI   R1,PHASELNQ                                                      
         STCM  R1,3,BT.BSPLENR+2   LENGTH OF RECORD                             
         MVI   BT.BSPLENK,0        DISPLACEMENT OF KEY INTO RECORD              
         LHI   R1,L'PHASEKEY                                                    
         STCM  R1,7,BT.BSPLENK+1   LENGTH OF KEY                                
         LHI   R1,PHSBTMAX                                                      
         STCM  R1,15,BT.BSPEND     MAXIMUM NUMBER OF RECORDS                    
         MVC   BT.BSPARS,TBLET     ACCESS REGISTER VALUE                        
*                                                                               
         XR    R3,R3                                                            
         ICM   R3,7,PHPNAME        A(LIST OF PHASES)                            
         BZ    PI030                                                            
         ICM   R4,15,PHPAR2        NUMBER OF PHASES IN LIST                     
         BZ    PI030                                                            
PI020    MVC   PHASEIN,0(R3)       THE PHASE TO BE ADDED                        
         BRAS  RE,PHADD            ADD TO BUILD TABLE                           
         LA    R3,8(,R3)                                                        
         BCT   R4,PI020                                                         
*                                                                               
PI030    BRAS  RE,ARSR2                                                         
         MVI   BT.BSPLENR,0        BUILD TABLE NO LONGER INSERTING              
*                                                                               
         ICM   R1,15,BT.BSPNOR     NUMBER OF ENTRIES IN BUILD TABLE             
         MHI   R1,PHASELNQ                                                      
         ICM   R2,15,ABLDTAB                                                    
         AR    R2,R1               GET TO END OF BUILD TABLE                    
         MVC   0(L'EYEATAB,R2),EYEATAB                                          
         LA    R2,L'EYEATAB(,R2)                                                
         STCM  R2,15,AINSTAB       SAVE START OF ADDITION TABLE                 
*                                                                               
BA       USING BSPARA,PHSIBIN                                                   
         ICM   R2,15,ATABHDR       SET ADD TABLE BINSRCH PARAMETERS             
         MVC   BA.BSPSTRT,AINSTAB  ANY ADDITIONS WILL GO IN THIS TABLE          
         OI    BA.BSPLENR,X'01'    INSERT IF NOT FOUND                          
         OI    BA.BSPLENR+1,X'80'  ACCESS REGISTER IN PARAMETER 7               
         LHI   R1,PHASELNQ                                                      
         STCM  R1,3,BA.BSPLENR+2   LENGTH OF RECORD                             
         MVI   BA.BSPLENK,0        DISPLACEMENT OF KEY INTO RECORD              
         LHI   R1,L'PHASEKEY                                                    
         STCM  R1,7,BA.BSPLENK+1   LENGTH OF KEY                                
         LHI   R1,PHSATMAX                                                      
         STCM  R1,15,BA.BSPEND     MAXIMUM NUMBER OF RECORDS                    
         MVC   BA.BSPARS,TBLET     ACCESS REGISTER VALUE                        
         DROP  R2                                                               
*                                                                               
PHINIX   BRAS  RE,ARSOFF                                                        
         B     EXIT                                                             
                                                                                
***********************************************************************         
* ADD PHASE TO PHASE TABLE, OR ADD TO COUNTER                                   
***********************************************************************         
PHADD    NTR1                                                                   
*                                                                               
         BRAS  RE,CHKPHASE         CHECK PHASE NAME/HEX                         
         BNE   PHADDX                                                           
*                                                                               
         ICM   R2,15,ATABHDR       ADDRESS OF DATA SPACE TABLE                  
         BZ    PHADDX                                                           
*                                                                               
         USING PHSTABD,R2                                                       
         BRAS  RE,ARSR2                                                         
         MVC   P0(PARMSQ),PHSBBIN  MOVE BINSRCH PARAMETERS LOCALLY              
BT       USING BSPARA,P0                                                        
         LA    R3,PHASEIN                                                       
         STCM  R3,15,BT.BSPAREC                                                 
         MVC   BT.BSPARS,TBLET     ACCESS REGISTER VALUE                        
*                                                                               
         GOTOR VBINSRCH,P0                                                      
         TM    BT.BSPAREC,X'80'    RECORD NOT FOUND                             
         BZ    PHA020              RECORD WAS FOUND: GO INCREMENT               
         CLI   PHPACT,PHPADD       ACTION ADD?                                  
         BE    PHA050              YES: GO ADD TO INSERT TABLE                  
*                                                                               
         BRAS  RE,ARSR2            ACCESS REGISTERS, JUST IN CASE               
BD       USING BSPARA,PHSBBIN                                                   
         MVC   BD.BSPNOR,BT.BSPNOR UPDATE # OF ENTRIES NOW IN DATASPACE         
         B     PHADDX              ALL FINISHED FOR INIT                        
         DROP  BD                                                               
*                                                                               
PHA020   CLI   PHPACT,PHPADD       ACTION ADD?                                  
         BNE   PHADDX              NO: ACTION INIT, NO NEED TO TALLY            
*                                                                               
         ICM   R2,15,BT.BSPAREC    ADDRESS OF ENTRY                             
         BZ    PHADDX              ZERO MEANS TABLE IS FULL                     
         C     R2,ABLDTAB          PROTECTION: TABLE ENTRY TO START             
         BNL   PHA021              PROTECTION: ENTRY SHOULD BE HIGHER           
         MVI   BADERR,1                                                         
         B     PHADDX              PROTECTION: ENTRY SHOULD BE HIGHER           
*                                                                               
PHA021   BRAS  RE,ARSR2                                                         
         USING PHASED,R2                                                        
         CLI   OFFLINE,YES                                                      
         BE    PHA030                                                           
         ICM   R0,15,PHASEON       INCREMENT ONLINE COUNTER                     
PHA022   LR    R1,R0                                                            
         AHI   R1,1                                                             
         CS    R0,R1,PHASEON                                                    
         JNE   PHA022                                                           
         B     PHADDX                                                           
PHA030   ICM   R0,15,PHASEOFF      INCREMENT OFFLINE COUNTER                    
PHA032   LR    R1,R0                                                            
         AHI   R1,1                                                             
         CS    R0,R1,PHASEOFF                                                   
         JNE   PHA032                                                           
         B     PHADDX                                                           
*                                                                               
* INSERT INTO TABLE                                                             
*                                                                               
PHA050   CLI   PHPACT,PHPADD       ACTION ADD?                                  
         BNE   PHADDX              NO: THEN STOP HERE                           
*                                                                               
         LHI   RF,1                ON INSERT, START COUNT AT 1                  
         LA    RE,PHASEAOF         OFFINE COUNT                                 
         CLI   OFFLINE,YES                                                      
         BE    *+8                                                              
         LA    RE,PHASEAON         ONLINE COUNT                                 
         STCM  RF,15,0(RE)                                                      
*                                                                               
PHA056   L     R2,ATABHDR          NOT IN TABLE, INSERT INTO ADD TABLE          
         USING PHSTABD,R2                                                       
         BRAS  RE,ARSR2                                                         
*                                                                               
PHA060   XR    R0,R0               LOCK THE TABLE                               
         ICM   R1,15,LOCK                                                       
         CS    R0,R1,PHSILOCK                                                   
         JNE   PHA060                                                           
*                                                                               
         MVC   P0(PARMSQ),PHSIBIN                                               
BA       USING BSPARA,P0                                                        
         LA    R3,PHASEIN                                                       
         STCM  R3,15,BA.BSPAREC    PHASE KEY                                    
         MVC   BA.BSPARS,TBLET     ACCESS REGISTER VALUE                        
*                                                                               
         GOTOR VBINSRCH,P0                                                      
         TM    BA.BSPAREC,X'80'    RECORD NOT FOUND                             
         BZ    PHA070              RECORD WAS FOUND: GO INCREMENT               
*                                                                               
         BRAS  RE,ARSR2            ACCESS REGISTERS, JUST IN CASE               
BD       USING BSPARA,PHSIBIN                                                   
         MVC   BD.BSPNOR,BA.BSPNOR UPDATE # OF ENTRIES NOW IN DATASPACE         
         B     PHA090              NO NEED TO TALLY ON NEW ENTRY                
         DROP  BD                                                               
*                                                                               
PHA070   ICM   R2,15,BA.BSPAREC    ADDRESS OF ENTRY                             
         BZ    PHA090              ZERO MEANS TABLE IS FULL                     
         C     R2,AINSTAB          PROTECTION:COMPARE A(ENTRY)/A(TABLE)         
         BNL   PHA071              PROTECTION:ENTRY SHOULD BE HIGHER            
         MVI   BADERR,2            SET THAT WE WANT BAD MESSAGE                 
         B     PHA090                                                           
*                                                                               
PHA071   BRAS  RE,ARSR2            ACCESS REGISTERS, JUST IN CASE               
         USING PHASED,R2                                                        
         CLI   OFFLINE,YES                                                      
         BE    PHA074                                                           
         ICM   R0,15,PHASEON       INCREMENT ONLINE COUNTER                     
PHA072   LR    R1,R0                                                            
         AHI   R1,1                                                             
         CS    R0,R1,PHASEON                                                    
         JNE   PHA072                                                           
         B     PHA090                                                           
PHA074   ICM   R0,15,PHASEOFF      INCREMENT OFFLINE COUNTER                    
PHA076   LR    R1,R0                                                            
         AHI   R1,1                                                             
         CS    R0,R1,PHASEOFF                                                   
         JNE   PHA076                                                           
*                                                                               
PHA090   L     R2,ATABHDR                                                       
         USING PHSTABD,R2                                                       
         BRAS  RE,ARSR2                                                         
         XC    PHSILOCK,PHSILOCK   UNLOCK TABLE                                 
         DROP  R2                                                               
*                                                                               
PHADDX   BRAS  RE,ARSOFF                                                        
         B     EXIT                                                             
***********************************************************************         
* CLEAR PHASE COUNTS                                                            
***********************************************************************         
PHCLR    NTR1                                                                   
         BRAS  RE,ARSR2                                                         
*                                                                               
         L     R2,ATABHDR                                                       
         USING PHSTABD,R2                                                       
BT       USING BSPARA,PHSBBIN                                                   
         ICM   R1,15,BT.BSPNOR     NUMBER OF PHASES IN BUILD TABLE              
         BZ    PHC020              NOTHING IN TABLE                             
*                                                                               
         L     R2,ABLDTAB                                                       
         USING PHASED,R2                                                        
PHC010   XC    PHASEON,PHASEON     CLEAR ONLINE PHASE LOAD COUNT                
         XC    PHASEOFF,PHASEOFF   CLEAR OFFLINE PHASE LOAD COUNT               
         LA    R2,PHASELNQ(,R2)                                                 
         BCT   R1,PHC010                                                        
*                                                                               
PHC020   L     R2,ATABHDR                                                       
         USING PHSTABD,R2                                                       
BA       USING BSPARA,PHSIBIN                                                   
         ICM   R1,15,BA.BSPNOR     NUMBER OF PHASES IN INSERT TABLE             
         BZ    PHC050              NOTHING IN TABLE                             
*                                                                               
         L     R2,AINSTAB                                                       
         USING PHASED,R2                                                        
PHC030   XC    PHASEON,PHASEON     CLEAR ONLINE PHASE LOAD COUNT                
         XC    PHASEOFF,PHASEOFF   CLEAR OFFLINE PHASE LOAD COUNT               
         LA    R2,PHASELNQ(,R2)                                                 
         BCT   R1,PHC030                                                        
         DROP  R2                                                               
*                                                                               
PHC050   BRAS  RE,ARSOFF                                                        
         B     EXIT                                                             
                                                                                
***********************************************************************         
* KILL THE COUNTING                                                             
***********************************************************************         
PHKILL   NTR1                                                                   
*                                                                               
         BRAS  RE,ARSR2                                                         
         ICM   R2,15,ATABHDR                                                    
         BZ    PHKILLX                                                          
         USING PHSTABD,R2                                                       
         MVC   PHSLOCK,KILL        KILL THE CODE                                
         DROP  R2                                                               
*                                                                               
PHKILLX  BRAS  RE,ARSOFF                                                        
         B     EXIT                                                             
                                                                                
***********************************************************************         
* SETUP ROUTINE                                                                 
***********************************************************************         
SETUP    NTR1                                                                   
         MVI   BADERR,0            INIT ERROR FLAG                              
*                                                                               
         ICM   R2,15,ATABHDR       DO WE HAVE THE A(TABLE)                      
         BNZ   SUP030              YES: NO NEED TO DIG IT OUT                   
*                                                                               
         XC    DUB,DUB                                                          
         MVC   DUB(4),=AL4(DTPHSCNT)                                            
         OI    DUB,X'20'                                                        
         GOTO1 VLOCKSPC,DUB                                                     
         ICM   RF,15,4(R1)         PHASE LOAD TABLE DSPACE HEADER               
         BZ    SUPNOX              NO PHASE LOAD TABLE, STOP HERE               
         MVC   DSPHSCNT,0(RF)      SAVE HEADER FROM DSPACE                      
*                                                                               
         ICM   RF,15,VSSB          NEED THE SSB                                 
         BZ    SUPNOX              NO SSB, STOP HERE                            
*                                                                               
         USING SSBD,RF                                                          
         ICM   R1,3,SSBCNTL        OFFLINE?                                     
         BZ    SUP010              YES                                          
         MVC   TBLET,SSBTBLET                                                   
         MVI   OFFLINE,NO          ONLINE                                       
         B     SUP020                                                           
*                                                                               
         USING SSBOFFD,RF                                                       
SUP010   MVC   TBLET,SSOTBLET                                                   
         MVI   OFFLINE,YES         OFFLINE                                      
         DROP  RF                                                               
*                                                                               
SUP020   OC    TBLET,TBLET                                                      
         BZ    SUPNOX              NO TABS ALET: STOP HERE                      
*                                                                               
         ICM   R2,15,DSPTFRST      GRAB THE TABLE ADDRESS                       
         BZ    SUPNOX              NO TABLE: STOP HERE                          
         STCM  R2,15,ATABHDR                                                    
*                                                                               
SUP030   BRAS  RE,ARSR2                                                         
         USING PHSTABD,R2                                                       
*                                                                               
         CLC   PHSLOCK,KILL        DID WE KILL THE CODE?                        
         BE    SUPNOX              YES: THEN JUST EXIT                          
         CLC   PHSEYE,EYEHEAD      EYECATCHER? NOT INITIALIZED                  
         BNE   SUPINI              NO: CHECK ACTIONS                            
*                                                                               
BT       USING BSPARA,PHSBBIN                                                   
BA       USING BSPARA,PHSIBIN                                                   
         ICM   R1,15,BT.BSPSTRT    HAVE START OF PHASE LIST TABLE?              
         BZ    SUPINI              NO: NOT INITIALIZED                          
         STCM  R1,15,ABLDTAB                                                    
         ICM   R1,15,BA.BSPSTRT    HAVE START OF PHASE INSERT TABLE?            
         BZ    SUPINI              NO: NOT INITIALIZED                          
         STCM  R1,15,AINSTAB                                                    
         B     SUPOKX              EVERYTHING IS SET, GOOD TO GO                
         DROP  BA,BT                                                            
*                                                                               
SUPINI   CLI   PHPACT,PHPINIT      ARE WE ABOUT TO INITIALIZE?                  
         BE    SUPOKX              YES: THEN WE CAN PROCEED                     
         CLI   PHPACT,PHPRESET     ARE WE ABOUT TO RESET?                       
         BE    SUPOKX              YES: THEN WE CAN PROCEED                     
         B     SUPNOX              NO OTHER ACTIONS IF NOT INIT'ED              
         DROP  R2                                                               
*                                                                               
SUPOKX   BRAS  RE,ARSOFF                                                        
         B     EXITEQ                                                           
*                                                                               
SUPNOX   BRAS  RE,ARSOFF                                                        
         B     EXITNE                                                           
                                                                                
***********************************************************************         
* CONVERT ONLINE PHASE TO HEX                                                   
***********************************************************************         
CHKPHASE NTR1                                                                   
*                                                                               
         CLC   PHASEIN,ZEROS                                                    
         BE    CPNOX               NOTHING TO ADD                               
         CLC   PHASEIN,SPACES                                                   
         BE    CPNOX               NOTHING TO ADD                               
         XC    PHASEAC,PHASEAC     CLEAR ACCUMS IN CASE INSERTING               
*                                                                               
         CLI   PHASEIN,C'T'        ONLINE PHASE                                 
         BNE   CPOKX               NO: LEAVE AS IS                              
         MVI   PHASEIN,C'0'        ONLINE PHASE: CONVERT TO HEX                 
         GOTOR MYHEXIN,DMCB,PHASEIN,WORK,6                                      
         ICM   RF,15,12(R1)                                                     
         BNZ   CP010               RETURN LENGTH MEANS GOOD HEX                 
         MVI   PHASEIN,C'T'        NO LENGTH: PUT T BACK & LEAVE TEXT           
         B     CPOKX               RETURN LENGTH MEANS GOOD HEX                 
*                                                                               
CP010    XC    PHASEIN,PHASEIN                                                  
         MVC   PHASEIN(3),WORK                                                  
*                                                                               
CPOKX    B     EXITEQ                                                           
CPNOX    B     EXITNE                                                           
                                                                                
***********************************************************************         
* LOCAL HEXIN CODE TO AVOID INTERUPTION                                         
*   PARAM LIST VIA R1 CONTAINS FOLLOWING WORDS :-                               
*   1ST   A(SOURCE CHR STRING 8 BIT HEX) X'80' ON IF XA ADDR  (R2)              
*   2ND   A(DESTN  CHR STRING 4 BIT HEX)                      (R3)              
*   3RD   L'SOURCE CHR STRING                                 (R4)              
*   4TH   VALUE RETURNED ZERO  SOURCE STRING INVALID          (R5)              
*         NONZERO LENGTH DESTN STRING.                                          
***********************************************************************         
MYHEXIN  NTR1                                                                   
*                                                                               
         LM    R2,R4,0(R1)                                                      
         NILH  GR2,X'7FFF'                                                      
         NILH  GR3,X'7FFF'                                                      
         LTR   R4,R4                                                            
         JNP   HEXIERR             SOURCE LENGTH NOT POSITIVE                   
         MVI   SCERR,0                                                          
*                                                                               
         SR    R7,R7               TRANLATE SOURCE CHRS TWO AT A TIME           
         LR    R5,R4                                                            
HEXI1    SR    R6,R6               SET TO FIRST OF PAIR                         
HEXI2    IC    R7,0(R2,R6)                                                      
HEXI2B   CHI   R7,C'0'                                                          
         JL    HEXI3               SOURCE CHR LT C'0'                           
         CHI   R7,C'9'                                                          
         JH    HEXI6               SOURCE CHR GT C'9'                           
         STC   R7,SC(R6)                                                        
         J     HEXI4                                                            
*                                                                               
HEXI3    CHI   R7,C'A'                                                          
         JL    HEXI6               SOURCE CHR LT C'A'                           
         CHI   R7,C'F'                                                          
         JH    HEXI6               SOURCE CHR GT C'F'                           
         NILL  GR7,X'000F'         CHR FROM X'CN' TO X'0N'                      
         AHI   R7,9                C'A'=X'01' TO X'0A'                          
         STC   R7,SC(R6)                                                        
HEXI4    LTR   R6,R6                                                            
         JP    HEXI5                                                            
         CHI   R5,1                                                             
         JNE   HEXI4B                                                           
         LA    R5,1(R5)            LAST ODD SOURCE CHR                          
         LA    R7,240              SIMULATE C'0'                                
         LA    R6,1                                                             
         J     HEXI2B                                                           
*                                                                               
HEXI4B   LA    R6,1                SET TO SECOND OF PAIR                        
         J     HEXI2                                                            
*                                                                               
HEXI5    UNPK  SC+1(1),SC+1(1)     SC=X'.N.M' TO X'.NM.'                        
         LH    R6,SC                                                            
         SRL   R6,4                R6=X'.NM.' TO X'..NM'                        
         STC   R6,0(R3)                                                         
         LA    R2,2(R2)            UP SOURCE PTR BY 2                           
         LA    R3,1(R3)            UP DESTN            PTR BY 1                 
         AHI   R5,-1                                                            
         JCT   R5,HEXI1            DOWN SOURCE COUNTER BY 2                     
*                                                                               
         CLI   SCERR,1                                                          
         JE    HEXIERR                                                          
         LA    R5,1(R4)                                                         
         SRA   R5,1                RETURN DESTN LENGTH                          
         J     HEXIX                                                            
*                                                                               
HEXI6    MVI   SCERR,1             SET ERROR FLAG                               
         LHI   R7,C'0'             REPLACE BY C'0'                              
         J     HEXI2B                                                           
*                                                                               
HEXIERR  SR    R5,R5               RETURN ERROR VALUE OF ZERO 00071             
*                                                                               
HEXIX    ST    R5,12(R1)                                                        
         J     EXIT                                                             
                                                                                
***********************************************************************         
* COMMON STORAGE AND LITERALS                                                   
***********************************************************************         
COMMON   DS    0D                                                               
*                                                                               
EXITNE   DS    0H                                                               
EXITLO   MVI   EXITCC,0            EXIT LOW CC                                  
         J     EXITC                                                            
EXITHI   MVI   EXITCC,2            EXIT HIGH CC                                 
         J     EXITC                                                            
EXITEQ   MVI   EXITCC,1            EXIT EQUAL CC                                
EXITC    CLI   EXITCC,1                                                         
EXIT     XIT1                                                                   
*                                                                               
ARSR2    LAM   AR0,ARF,ARZERO      SET AR2 AND TURN ON ACCESS REG MODE          
         LAM   AR2,AR2,TBLET                                                    
         SAC   512                                                              
         BR    RE                                                               
*                                                                               
ARSOFF   SAC   0                   TURN OFF ACCESS REGISTER MODE                
         LAM   AR0,ARF,ARZERO      AND CLEAR ACCESS REGISTERS                   
         BR    RE                                                               
*                                                                               
VSSB     DC    V(SSB)                                                           
VDATAMGR DC    V(DATAMGR)                                                       
VBINSRCH DC    V(BINSR31)                                                       
VLOCKSPC DC    V(LOCKSPC)                                                       
VSMFOUT  DC    V(SMFOUT)                                                        
*                                                                               
TBLET    DC    A(0)                TABSDSP ALET                                 
ATABHDR  DC    A(0)                                                             
ABLDTAB  DC    A(0)                                                             
AINSTAB  DC    A(0)                                                             
*                                                                               
KILL     DC    C'KILL'                                                          
LOCK     DC    C'LOCK'                                                          
*                                                                               
ARZERO   DC    16F'0'              USED TO CLEAR ACCESS REGISTERS               
EYEHEAD  DC    CL16'*PHASELOADTABLE*'                                           
EYEBTAB  DC    CL16'*BLDTAB**BLDTAB*'                                           
EYEATAB  DC    CL16'*INSTAB**INSTAB*'                                           
*                                                                               
DSPHSCNT DC    XL64'00'            JOBS TABLE IN DATA SPACE                     
*                                                                               
OFFLINE  DC    C'?'                OFFLINE YES/NO                               
*                                                                               
ZEROS    DC    XL8'00'                                                          
SPACES   DC    CL64' '                                                          
*                                                                               
BADMSGP  DC    C'N'                BAD MESSAGE WAS PUT OUT                      
BADMSG   DC    CL50'*ERROR* BAD PHASE COUNT AVOIDED BY PROTECTION'              
         LTORG                                                                  
*                                                                               
YES      EQU   C'Y'                                                             
NO       EQU   C'N'                                                             
                                                                                
***********************************************************************         
* DSECT TO COVER WORKING STORAGE                                      *         
***********************************************************************         
WORKD    DSECT ,                                                                
DUB      DS    D                                                                
FULL     DS    F                                                                
HALF     DS    H                                                                
BYTE     DS    X                                                                
EXITCC   DS    X                                                                
RELO     DS    A                                                                
CALLR1   DS    A                                                                
*                                                                               
P0       DS    F                   FOR 31BIT BINSRCH NEED 7TH PARAMETER         
DMCB     DS    6F                                                               
PARMSQ   EQU   *-P0                                                             
*                                                                               
INPARM   DS    6F                  PHASEL INPUT PARAMETERS                      
INPARMQ  EQU   *-INPARM                                                         
*                                                                               
SC       DS    CL2                 PAIR OF MODIFIED SOURCE CHARS                
SCERR    DS    CL1                 HEXIN ERROR FLAG                             
BADERR   DS    CL1                 APPLICATION ERROR FLAG                       
*                                                                               
WORK     DS    XL64                                                             
MSG      DS    XL64                                                             
*                                                                               
PHASEIN  DS    XL8                 PHASE NAME INPUT                             
PHASEAC  DS    0XL8                SPACE FOR PHASE LOAD COUNT ACCUMS            
PHASEAON DS    XL4                 ONLINE COUNT                                 
PHASEAOF DS    XL4                 OFFLINE COUNT                                
*                                                                               
WORKL    EQU   *-WORKD                                                          
                                                                                
***********************************************************************         
* OTHER DSECTS                                                                  
***********************************************************************         
       ++INCLUDE DDPHSCNTD                                                      
                                                                                
* FASSB                                                                         
* DMSPACED                                                                      
* DDBSPARA                                                                      
* FATABSDEQU                                                                    
         PRINT OFF                                                              
SSBOFFD  DSECT                                                                  
       ++INCLUDE FASSBOFF                                                       
       ++INCLUDE FASSB                                                          
       ++INCLUDE DMSPACED                                                       
       ++INCLUDE DDBSPARA                                                       
       ++INCLUDE FATABSDEQU                                                     
         PRINT ON                                                               
                                                                                
***********************************************************************         
* ORG TO NEXT 1024 BYTES SO THAT IF PROGRAM GETS LARGER, IT WILL STILL          
* FIT IN THE SAME SLOT IN CORE (WITHIN REASON)                                  
***********************************************************************         
PHSCNT   CSECT                                                                  
         ORG   PHSCNT+(((*-PHSCNT)/1024)+1)*1024                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'005DDPHSCNT  05/07/20'                                      
         END                                                                    
