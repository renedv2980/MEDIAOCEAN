*          DATA SET REPAVSTA   AT LEVEL 017 AS OF 05/01/02                      
*CATALP PAVSTA                                                                  
         TITLE 'REPAVSTA - CATALOGED MOD - EDIT STATION FIELD'                  
*                                                                               
*******************************************************************             
*                                                                 *             
*              REPAVSTA --- EDIT A STATION FIELD                  *             
*                                                                 *             
* --------------------------------------------------------------- *             
*                                                                 *             
*              PARA 1    BYTE 1-3  A(INPUT DATA)                  *             
*              PARA 2    BYTE 1-3  A(6 BYTE OUTPUT FIELD)         *             
*                                  4 BYTE STATION                 *             
*                                  1 BYTE BAND                    *             
*                                  1 BYTE MEDIA                   *             
*                        ON RETURN BYTE 0 = FF NOT VALID          *             
*                                       = LENGTH OF INPUT         *             
*                                                                 *             
*                                                                 *             
* --------------------------------------------------------------- *             
* UPDATE HISTORY:                                                 *             
*                                                                 *             
* JUL06/89 (MRR) --- ALLOW IMBEDDED NUMERICS IN CALL LETTERS      *             
*                     (FOR LOW POWER STATION - SERVICE = SRC)     *             
*                                                                 *             
* JUL11/89 (MRR) --- MAKE SURE THAT THE FIRST CHARACTER IS A      *             
*                     LETTER OR RETURN INVALID                    *             
*                                                                 *             
*                                                                 *             
*                                                                 *             
*                                                                 *             
*******************************************************************             
*                                                                               
PAVSTA   CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 1,PAVSTA                                                         
         USING WORKD,RC                                                         
         LR    R2,R1               SAVE PARA ADDRESS                            
         L     R1,4(R2)            TO ADDRESS                                   
         MVI   0(R1),C' '                                                       
         MVC   1(5,R1),0(R1)                                                    
         L     R4,0(R2)            R4 TO DATA                                   
         LA    R5,6                NEVER MORE THAN 6                            
         BAS   RE,TESTAN                                                        
         SPACE 1                                                                
         TM    BYTE,X'04'          TEST ALPHA                                   
         BZ    PSTAINV             INVALID                                      
         CH    R5,=H'3'                                                         
         BL    PSTAINV                                                          
         SPACE 1                                                                
         L     R1,4(R2)            TO OUTPUT AREA                               
         CLI   0(R4),C' '                                                       
         BE    PAVSTRD             END OF INPUT - READ FOR STATION              
         CLI   0(R4),0                                                          
         BE    PAVSTRD                                                          
         CLI   0(R4),C','                                                       
         BE    PAVSTRD                                                          
         CLI   0(R4),C'-'                                                       
         BNE   PSTAINV                                                          
         MVC   4(1,R1),1(R4)       -T, -S, -A, -F                               
         LA    R5,2(R5)                                                         
         SPACE 1                                                                
PAVSTRD  STC   R5,4(R2)            RETURN LENGTH                                
         CLI   4(R1),C' '                                                       
         BNE   *+8                                                              
         MVI   4(R1),C'T'                                                       
         MVI   5(R1),C'T'          SET MEDIA CODE                               
         B     EXXMOD              LOOKS OK                                     
         EJECT                                                                  
* VALIDATE FIELD IS ALPHA/NUMERIC                                               
* FIELD IS DELIMITED BY BLANK,COMMA,OR DASH                                     
* R4 HAS FIELD ADDRESS. ON EXIT HAS STOP CHAR ADDRESS                           
* R5 HAS MAX LENGTH.    ON EXIT HAS CHAR COUNT.                                 
* R1 HAS 'TO' ADDRESS.  ON EXIT HAS NEXT CHAR ADDRESS                           
*                                                                               
TESTAN   EQU   *                                                                
         MVI   BYTE,X'0C'          SET VALID A (X'04') AND N (X'08')            
*                                  TEST FIRST CHARACTER                         
         LA    R0,6                                                             
         BAS   R8,GENTEST                                                       
         BCTR  R0,0                                                             
         CLI   0(R4),C'A'                                                       
         BL    TESTAN0A                                                         
         CLI   0(R4),C'Z'                                                       
         BNH   TESTAN0B                                                         
TESTAN0A NI    BYTE,X'08'          FIELD NOT ALPHA                              
         CLI   0(R4),C'0'                                                       
         BL    TESTAN0B                                                         
         CLI   0(R4),C'9'                                                       
         BNH   TESTAN1                                                          
TESTAN0B NI    BYTE,X'04'          FIELD NOT NUMERIC                            
*                                                                               
TESTAN1  EQU   *                                                                
         BAS   R8,GENTEST                                                       
TESTAN6  MVC   0(1,R1),0(R4)                                                    
         LA    R1,1(R1)                                                         
         LA    R4,1(R4)                                                         
         BCT   R0,TESTAN1                                                       
PSTAINV  MVI   4(R2),X'FF'                                                      
         B     EXXMOD                                                           
         SPACE 1                                                                
TESTANX  BCTR  R0,0                ADJUST COUNT                                 
         SR    R5,R0               GIVES CHARACTER COUNT                        
         BR    RE                                                               
         SPACE 3                                                                
GENTEST  EQU   *                                                                
         CLI   0(R4),C' '          LOOP FOR REST OF FIELD                       
         BE    TESTANX                                                          
         CLI   0(R4),0                                                          
         BE    TESTANX                                                          
         CLI   0(R4),C','                                                       
         BE    TESTANX                                                          
         CLI   0(R4),C'-'                                                       
         BE    TESTANX                                                          
*                                                                               
         BR    R8                                                               
         SPACE 2                                                                
EXXMOD   XMOD1 1                                                                
         SPACE 1                                                                
WORKD    DSECT                                                                  
BYTE     DS    CL1                                                              
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'017REPAVSTA  05/01/02'                                      
         END                                                                    
