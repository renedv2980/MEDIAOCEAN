*          DATA SET SPGETCOM   AT LEVEL 003 AS OF 05/01/02                      
*CATALP SPGETCOM                                                                
         TITLE 'SPGETCOM - RETRIEVE SPOTPAK COMMENTS'                           
SPGETCOM CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 10,**GTCM**                                                      
         USING CMWORKD,RC                                                       
*                                                                               
         L     RA,=V(SPWORKC)                                                   
         LA    R9,2048(RA)                                                      
         LA    R9,2048(R9)                                                      
         USING SPWORKD,RA,R9                                                    
*                                                                               
         MVC   CMSAVE,KEY          SAVE SPTDIR KEY                              
*                                                                               
         XC    CMKEY,CMKEY                                                      
         MVC   CMKEY(2),=X'0D0C'                                                
         MVC   CMKEY+2(1),BAGYMD                                                
         MVC   CMKEY+3(1),BCMTYPE                                               
         MVC   CMKEY+4(2),BCMCLT                                                
*                                                                               
         CLI   BCMPRD,X'FF'        TREAT 'POL' AS 'ALL'                         
         BNE   *+8                                                              
         MVI   BCMPRD,0                                                         
* DROP PRDGRP DIGITS BEYOND BRK 1 LEN                                           
         CLI   PGR1LEN,3                                                        
         BNL   GC2                                                              
         MVI   BCMPGR+2,0          DROP THIRD DIGIT                             
         CLI   PGR1LEN,2                                                        
         BNL   GC2                                                              
         NI    BCMPGR+1,X'F0'      DROP SECOND DIGIT                            
         EJECT                                                                  
* READ FOR CLT/PRD/EST                                                          
*                                                                               
GC2      MVC   KEY,CMKEY                                                        
         MVC   KEY+8(1),BCMPRD                                                  
         MVC   KEY+9(1),BCMEST                                                  
*                                                                               
         OC    BCMMKT(5),BCMMKT    TEST MKT OR STATION PRESENT                  
         BNZ   GC100                                                            
*                                                                               
         CLI   BCMEST,0            TEST ESTIMATE PRESENT                        
         BE    GC30                NO                                           
         CLI   BCMPRD,0            TEST PRODUCT PRESENT                         
         BZ    GC10                NO                                           
         BAS   R8,GCHI                                                          
* READ FOR CLT/PGR/EST                                                          
GC10     MVC   KEY,CMKEY                                                        
         MVC   KEY+6(3),BCMPGR                                                  
         MVC   KEY+9(1),BCMEST                                                  
         OC    BCMPGR,BCMPGR                                                    
         BZ    GC20                                                             
         BAS   R8,GCHI                                                          
* READ FOR CLT/---/EST                                                          
GC20     MVC   KEY,CMKEY                                                        
         MVC   KEY+9(1),BCMEST                                                  
         BAS   R8,GCHI                                                          
* READ FOR CLT/PRD                                                              
GC30     MVC   KEY,CMKEY                                                        
         MVC   KEY+8(1),BCMPRD                                                  
         CLI   BCMPRD,0                                                         
         BE    GC40                                                             
         BAS   R8,GCHI                                                          
* READ FOR CLT/PGR                                                              
GC40     MVC   KEY,CMKEY                                                        
         MVC   KEY+6(3),BCMPGR                                                  
         OC    BCMPGR,BCMPGR                                                    
         BZ    GC50                                                             
         BAS   R8,GCHI                                                          
* READ FOR CLIENT                                                               
GC50     MVC   KEY,CMKEY                                                        
         XC    KEY+6(7),KEY+6                                                   
         BAS   R8,GCHI                                                          
* READ FOR MEDIA                                                                
         MVC   KEY,CMKEY                                                        
         XC    KEY+4(9),KEY+4                                                   
         BAS   R8,GCHI                                                          
* COMMENT NOT FOUND - CLEAR KEY IN COMMENT AREA                                 
GC60     L     R6,ADCOMREC                                                      
         XC    0(13,R6),0(R6)                                                   
         B     EXIT                                                             
         EJECT                                                                  
* WHEN STATION SPECIFIED READ FOR A-M/CLT/PRD/EST/STA                           
* DEFAULT TO HIGHER LEVEL FOR MEDIA COMMENTS ONLY                               
         SPACE 1                                                                
GC100    CLI   BCMTYPE,C'P'        TEST PARAMOUNT COMMENT                       
         BE    GC112                                                            
*                                                                               
         MVC   KEY+10(3),BCMSTA                                                 
         BAS   R8,GCHI                                                          
         CLI   BCMTYPE,C'M'        TEST MARKET COMMENT                          
         BE    GC120                                                            
*                                                                               
GC110    CLI   BCMTYPE,C'T'        TEST STATION COMMENT                         
         BNE   GC60                                                             
         MVC   KEY,KEYSAVE                                                      
*                                                                               
GC112    MVI   KEY+10,0            TRY FOR MARKET COMMENT                       
         MVC   KEY+11(2),BCMMKT                                                 
         BAS   R8,GCHI                                                          
*                                                                               
GC120    MVC   KEY,KEYSAVE                                                      
         XC    KEY+10(3),KEY+10    TRY FOR ALL MARKET                           
         BAS   R8,GCHI                                                          
         B     GC60                EXIT IF NOT FOUND                            
         EJECT                                                                  
GCHI     DS    0H                                                               
         GOTO1 HIGH                                                             
         CLC   KEY(13),KEYSAVE                                                  
         BNER  R8                                                               
         L     R0,AREC             SAVE PREVIOUS                                
         L     R6,ADCOMREC                                                      
         ST    R6,AREC                                                          
         GOTO1 GET                                                              
         ST    R0,AREC                                                          
*                                                                               
EXIT     MVC   KEY,CMSAVE                                                       
         GOTO1 HIGH                RESET SPTDIR FOR SEQ READING                 
         XMOD1 1                                                                
         LTORG                                                                  
         EJECT                                                                  
CMWORKD  DSECT                                                                  
CMKEY    DS    CL32                                                             
CMSAVE   DS    CL32                                                             
         SPACE 2                                                                
       ++INCLUDE SPREPWORKD                                                     
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'003SPGETCOM  05/01/02'                                      
         END                                                                    
