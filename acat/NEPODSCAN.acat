*          DATA SET NEPODSCAN  AT LEVEL 002 AS OF 05/01/02                      
*CATALP NEPODSCN                                                                
***********************************************************************         
* THIS VERSION OF SCANNER CONTAINS 2 CHANGES NEED TO HANDLE FIELDS    *         
* THAT ARE GREATER THAN 20 BYTES IN LENGTH.                           *         
***********************************************************************         
*                                                                               
         TITLE 'VARIABLE SCAN MODULE'                                           
NEPODSCN CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 WORKX-WORKD,**SCAN**                                             
         USING WORKD,RC                                                         
         LR    R9,R1               R9=A(PARAMETER LIST)                         
         LM    R2,R3,0(R9)         R2=A(DATA STRING) R3=A(BLOCK)                
         MVC   MAXLINES,4(R9)                                                   
         NI    MAXLINES,X'7F'      DON'T WANT THE X'80'                         
*                                                                               
         CTRY  ,                   R0=X'OOAACCLL'                               
         STCM  R0,15,AGCOPT                                                     
         MVC   COMMA(2),COMAEQLS   SET DEFAULT TO ,=                            
         CLC   8(2,R9),COMAEQLS                                                 
         BNE   SCAN0                                                            
         MVC   COMMA(2),10(R9)     OVERRIDE SPECIAL CHARACTERS                  
         CLC   COMMA(2),=C'  '     TEST IF CALLER WANTS LANG DEFAULTS           
         BNE   SCAN0               NO                                           
         SR    RF,RF               YES GET LANGUAGE CODE                        
         IC    RF,LANG                                                          
         CH    RF,=H'7'                                                         
         BNH   *+6                                                              
         SR    RF,RF                                                            
         SLL   RF,1                                                             
         LA    RF,COMAEQLS(RF)     INDEX INTO COMMA EQUALS TABLE                
         MVC   COMMA(2),0(RF)      SET DEFAULT FOR LANGUAGE                     
*                                                                               
SCAN0    MVI   SPACES,C' '                                                      
         MVC   SPACES+1(L'SPACES-1),SPACES                                      
         XC    DISP,DISP                                                        
         SR    R4,R4                                                            
         IC    R4,5(R2)            L'DATA IF SCREEN FIELD                       
         LA    R2,8(R2)                                                         
         MVC   LROW,=H'32'         PRESET DEFAULT LENGTHS                       
         MVC   LRIGHT,=H'10'                                                    
         MVC   LBOTH,=H'20'                                                     
         CLI   0(R9),C'C'                                                       
         BE    SCAN1                                                            
         CLI   0(R9),10            USER CAN MODIFY STANDARD LENGTH              
         BL    SCAN4                                                            
         CLI   0(R9),80                                                         
         BH    SCAN4                                                            
         ZIC   RF,0(R9)                                                         
         STH   RF,LRIGHT                                                        
         LA    RF,10(RF)                                                        
         STH   RF,LBOTH                                                         
         LA    RF,12(RF)                                                        
         STH   RF,LROW                                                          
         B     SCAN4                                                            
         EJECT                                                                  
*FIND LENGTH OF INPUT CARDS                                                     
*                                                                               
SCAN1    SH    R2,=H'8'                                                         
         LA    R4,80                                                            
         CLC   0(80,R2),SPACES                                                  
         BE    ERROR2                                                           
         LA    R5,79(R2)                                                        
         SPACE 2                                                                
SCAN2    CLI   0(R5),C' '                                                       
         BNE   SCAN4                                                            
         BCTR  R5,0                                                             
         BCT   R4,SCAN2                                                         
         SPACE 2                                                                
SCAN4    LA    R5,0(R2,R4)         L'DATA IN R4                                 
         MVC   BORROW,0(R5)        SAVE THE NEXT CHARACTER                      
         MVC   0(1,R5),COMMA       AND POP IN A COMMA TO SIMPLIFY               
         SR    R6,R6               R6=NUMBER OF LINES USED                      
         EJECT                                                                  
*HANDLE LINES OF DATA                                                           
*                                                                               
SCAN6    XC    0(12,R3),0(R3)      PRESET A LINE                                
         LH    RF,LBOTH                                                         
         BCTR  RF,0                                                             
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   12(0,R3),SPACES                                                  
         MVC   2(2,R3),=X'E0E0'                                                 
         BAS   RE,GETL                                                          
         CLI   0(R3),0                                                          
         BNE   *+8                                                              
         MVI   2(R3),0                                                          
         CLI   1(R3),0                                                          
         BNE   *+8                                                              
         MVI   3(R3),0                                                          
         CLC   0(1,R3),LBOTH+1                                                  
         BH    ERROR                                                            
         CLC   1(1,R3),LRIGHT+1                                                 
         BH    ERROR                                                            
         CLI   1(R3),0                                                          
         BE    SCAN8               ************************                     
         CLI   0(R3),12            * MAX 12 INSTEAD OF 10 *                     
         BH    ERROR               ************************                     
         SPACE 2                                                                
SCAN8    SR    R7,R7                                                            
         IC    R7,0(R3)                                                         
         LTR   R7,R7                                                            
         BZ    SCAN18                                                           
         BCTR  R7,0                                                             
         EX    R7,*+8                                                           
         B     *+10                                                             
         MVC   12(0,R3),0(R2)                                                   
         TM    2(R3),X'80'                                                      
         BZ    SCAN10                                                           
         CH    R7,=H'8'                                                         
         BH    SCAN10                                                           
         EX    R7,VARPAK                                                        
         CVB   R8,DUB                                                           
         STCM  R8,7,5(R3)          STORE AT LEAST 3 BYTES BINARY                
         TM    4(R9),X'80'         THAT'S ALL IF RETURNING DISPS.               
         BO    SCAN10                                                           
         ST    R8,4(R3)                                                         
         SPACE 2                                                                
SCAN10   LA    R2,2(R2,R7)                                                      
         IC    R7,1(R3)                                                         
         LTR   R7,R7                                                            
         BZ    SCAN20                                                           
         BCTR  R7,0                                                             
         EX    R7,*+8                                                           
         B     *+10                ***************************                  
         MVC   24(0,R3),0(R2)      * OFFSET 24 INSTEAD OF 22 *                  
         TM    3(R3),X'80'         ***************************                  
         BZ    SCAN12                                                           
         CH    R7,=H'8'                                                         
         BH    SCAN12                                                           
         EX    R7,VARPAK                                                        
         CVB   R8,DUB                                                           
         STCM  R8,7,9(R3)          STORE AT LEAST 3 BYTES BINARY                
         TM    4(R9),X'80'         THAT'S ALL IF RETURNING DISPS.               
         BO    SCAN12                                                           
         ST    R8,8(R3)                                                         
         SPACE 2                                                                
SCAN12   LA    R2,2(R2,R7)                                                      
         B     SCAN20                                                           
         SPACE 2                                                                
VARPAK   PACK  DUB,0(0,R2)                                                      
         SPACE 2                                                                
SCAN18   LA    R2,1(R2)                                                         
         CLI   1(R3),0                                                          
         BNE   ERROR                                                            
         SPACE 2                                                                
SCAN20   LA    R6,1(R6)            BUMP N'LINES                                 
         AH    R3,LROW             BUMP TO NEXT LINE IN BLOCK                   
         CR    R2,R5               ARE WE NOW PAST LAST 'COMMA'                 
         BH    OK                                                               
         IC    R7,MAXLINES                                                      
         LTR   R7,R7                                                            
         BZ    SCAN6                                                            
         CR    R6,R7               HAVE WE REACHED MAX N'LINES                  
         BNE   SCAN6                                                            
         SPACE 2                                                                
OK       MVC   0(1,R5),BORROW      RETURN THE BYTE                              
         STC   R6,4(R9)            SET NUMBER OF LINES USED                     
         B     XIT                                                              
         SPACE 2                                                                
ERROR    MVI   4(R9),0                                                          
         MVC   0(1,R5),BORROW                                                   
         MVC   2(2,R3),=X'FFFF'                                                 
         B     XIT                                                              
         SPACE 2                                                                
ERROR2   MVI   4(R9),0                                                          
         MVC   2(2,R3),=X'FFFF'                                                 
         SPACE 2                                                                
XIT      XMOD1 1                                                                
         EJECT                                                                  
*VALIDATE AND GET LENGTHS                                                       
*                                                                               
GETL     NTR1                                                                   
         LR    R4,R3                                                            
         SR    R5,R5                                                            
         TM    4(R9),X'80'                                                      
         BZ    GETL2                                                            
         MVC   4(1,R4),DISP+1      DISPLACEMENT INTO FIELD                      
         SPACE 2                                                                
GETL2    CLC   0(1,R2),COMMA       TEST FIELD SEPERATOR                         
         BE    GETL12                                                           
         CLI   LANG,2                                                           
         BNH   *+12                                                             
         CLI   0(R2),C';'          TEST MULTI NATIONAL STANDARD CHR             
         BE    GETL12                                                           
         CLC   0(1,R2),EQUAL                                                    
         BE    GETL14                                                           
         LA    R5,1(R5)                                                         
         CLI   0(R2),C'9'                                                       
         BNH   *+8                                                              
         MVI   2(R4),0             (ALL INVALID)                                
         CLI   0(R2),C'0'                                                       
         BL    GETL4                                                            
         NI    2(R4),X'BF'         (INVALID ALPHA)                              
         B     GETL10                                                           
         SPACE 2                                                                
GETL4    NI    2(R4),X'7F'         (INVALID NUM)                                
         CLI   0(R2),C'Z'                                                       
         BNH   GETL6                                                            
         MVI   2(R4),0             Z-0 = ALL INVALID                            
         B     GETL10                                                           
         SPACE 2                                                                
GETL6    CLI   0(R2),C'A'          LESS THAN A = ALL INVALID                    
         BNL   GETL8                                                            
         MVI   2(R4),0                                                          
         B     GETL10                                                           
         SPACE 2                                                                
GETL8    CLI   0(R2),C'F'          OK FOR ALPHA                                 
         BNH   GETL10                                                           
         NI    2(R4),X'DF'         G-Z = INVALID HEX                            
         SPACE 2                                                                
GETL10   LA    R2,1(R2)                                                         
         B     GETL2                                                            
         SPACE 2                                                                
GETL12   STC   R5,0(R4)            COMMA FOUND                                  
         LA    R5,1(R5)                                                         
         AH    R5,DISP                                                          
         STH   R5,DISP                                                          
         B     XIT                                                              
         SPACE 2                                                                
GETL14   MVI   0(R4),30            EQUAL FOUND (PRESET INVALID AND              
         CR    R4,R3                            CHECK MORE THAN 1)              
         BNE   XIT                                                              
         STC   R5,0(R4)            NOW STORE L1                                 
         LA    R5,1(R5)                                                         
         AH    R5,DISP                                                          
         STH   R5,DISP                                                          
         TM    4(R9),X'80'                                                      
         BZ    GETL16                                                           
         MVC   8(1,R4),DISP+1      DISPLACEMENT INTO FIELD                      
         SPACE 2                                                                
GETL16   LA    R4,1(R4)            POINT TO FIELD2 DATA                         
         SR    R5,R5               CLEAR L2                                     
         LA    R2,1(R2)            POINT PAST EQUAL SIGN                        
         B     GETL2                                                            
         EJECT                                                                  
         LTORG                                                                  
         SPACE 2                                                                
COMAEQLS DC    C',=,=,=#=;=;=;=;='                                              
         SPACE 2                                                                
WORKD    DSECT                                                                  
DUB      DS    D                                                                
WORK     DS    CL32                                                             
SPACES   DS    CL80                                                             
LASTSTOP DS    F                                                                
BORROW   DS    CL1                                                              
MAXLINES DS    CL1                                                              
COMMA    DS    CL1                                                              
EQUAL    DS    CL1                                                              
LROW     DS    H                                                                
LRIGHT   DS    H                                                                
LBOTH    DS    H                                                                
DISP     DS    H                                                                
*                                                                               
AGCOPT   DS    XL1                                                              
AGCTRY   DS    XL1                                                              
CTRY     DS    XL1                                                              
LANG     DS    XL1                                                              
*                                                                               
WORKX    DS    0C                                                               
         SPACE 1                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'002NEPODSCAN 05/01/02'                                      
         END                                                                    
