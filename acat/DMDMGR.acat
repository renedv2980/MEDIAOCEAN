*          DATA SET DMDMGR     AT LEVEL 052 AS OF 09/09/20                      
*PROCESS USING(WARN(15))                                                        
*CATALP DMDMGR                                                                  
*&&UK                                                                           
*&&      SET   ACC=Y,CON=Y,CPP=Y,MPL=Y,PER=Y,SRV=Y                              
*&&      SET   MED=Y,PRT=N,REP=N,SPT=N,MBA=Y,TAL=N,FEE=Y,TRF=N                  
*&&      SET   STAMP=Y,TRC=N                                                    
*&&      SET   DMSMF=N                                                          
*&&                                                                             
*&&US                                                                           
*&&      SET   ACC=Y,CON=Y,DEM=Y,MPL=Y,PER=Y,SRV=Y,CPP=N                        
*&&      SET   MED=N,PRT=Y,REP=Y,SPT=Y,MBA=Y,TAL=Y,FEE=N,TRF=Y                  
*&&      SET   STAMP=Y,TRC=N                                                    
*&&      SET   DMSMF=N                                                          
*&&                                                                             
                                                                                
* DMCBW1 X'80' READ FOR UPDATE                                                  
*        X'40' I/O AREA AT DMCBW4 IS 31-BIT ADDRESS                             
*        X'20' CONTROL FLAG AVAILABLE FOR SPECIAL ACTIONS                       
*        X'10' FULL TRACK READ TO BUFFER IN DMCBW5                              
*        X'08' PASS BACK DELETED RECORDS                                        
*        X'04' SPECIAL ACTION FLAG USED WITH X'20'                              
*        X'02' SPECIAL ACTION FLAG USED WITH X'20'                              
*        X'01' PASS BACK RECORD LENGTH IN DMCBW6+2(2)                           
*                                                                               
*        X'24' READ FLUSH - FORCE READ OF RECORD FROM DISK                      
*        X'24' MAINTENANCE RECORD WRITE/ADD FOR SOX R/O CONNECT                 
*        X'24' OFFLINE DMADD/REQUEST - ADD REQUEST AS PASSED                    
*        X'28' WANT LOGICALLY DELETED RECORDS                                   
*        X'2C' DO NOT FOLLOW MVS RECOVERY FILES                                 
*              WAS REMOVE TASK ID WHICH IS NOW THE DEFAULT.                     
                                                                                
* DMCBW3 X'80' END OF FILE                                                      
*        X'40' IRRECOVERABLE DISK ERROR                                         
*        X'20' DUPLICATE KEY ON ADD                                             
*        X'10' RECORD NOT FOUND                                                 
*        X'02' RECORD IS DELETED                                                
*                                                                               
* MAX INDEX SEQUENTIAL KEYLEN SUPPORTED IS 48 BYTES (OLDKEY)                    
                                                                                
         TITLE 'DATAMGR - APPLICATION ENTRY POINT'                              
         PRINT NOGEN                                                            
DATAMGR  CSECT                                                                  
         ENTRY DMGRFLES                                                         
         ENTRY DMOD000                                                          
*                                                                               
         NMOD1 DMWORK-DMGRWORK+288,**DMGR**,RA                                  
         USING DMGRWORK,RC                                                      
         USING DMGRFRST,DMGRWORK                                                
         ST    R1,SAVER1           SAVE A(CALLERS DMCB)                         
         ST    RC,SAVERC                                                        
         SAM24                     GET INTO 24-BIT MODE                         
*                                                                               
         CLI   0(R1),X'FF'         TEST OLD FULL TRACK READ CONVENTION          
         BNE   *+8                                                              
         MVI   0(R1),X'39'                                                      
         MVC   SVDM1,0(R1)         SAVE ACTION MODIFIER                         
         MVC   SVPAGE,8(R1)        SAVE PAGE NUMBER                             
         MVI   8(R1),0             SET OK RETURN CODE                           
*                                                                               
         MVC   DMCBW1(24),0(R1)    COPY CALLERS DMCB TO MY W/S                  
         LR    R1,RC                                                            
         DROP  RC                                                               
                                                                                
         USING DMGRWORK,R1         R1=A(PARAM LIST FOR ACCESS METHODS)          
         USING DMGRFRST,DMGRWORK                                                
         LA    R2,DMCBW1           R2=A(DMCB)                                   
*                                                                               
         LT    RF,=V(PROTOFF)      DISABLE CALLERS STORAGE PROTECTION           
         BZ    *+6                                                              
         BASR  RE,RF                                                            
         MVI   TRACEON,C'N'                                                     
         XC    REPWRK(REPWRKL),REPWRK CLEAN OUT REP WORK AREA                   
*                                                                               
DMGR0    XC    XINFO,XINFO         SET A(TBC) AND A(UTL) IF ONLINE              
         ICM   RF,15,=V(SSB)                                                    
         BZ    DMGR0X                                                           
         OC    SSBCNTL-SSBD(2,RF),SSBCNTL-SSBD(RF)                              
         BZ    DMGR0X                                                           
         OI    XFLAG1,XFONLINE     SET ONLINE FLAG                              
         ICM   RF,15,SSBTKADR-SSBD(RF)                                          
         BZ    DMGR0X                                                           
         ST    RF,XTCBNTRY                                                      
         ICM   RF,15,TCBUTL-TCBD(RF)                                            
         BZ    DMGR0X                                                           
         ST    RF,XUTLNTRY                                                      
DMGR0X   EQU   *                                                                
         EJECT                                                                  
***********************************************************************         
* DMCBW1 IS ADDRESS OF ACTION                                         *         
***********************************************************************         
DMGR1    XR    R6,R6               SEARCH ACTION TABLE                          
         ICM   R6,7,DMCBW1+1                                                    
         MVI   ACTION,0                                                         
         L     R3,=A(DMACTS)                                                    
         BAS   RE,SETBXLE                                                       
         CLC   0(5,R6),0(R3)       MATCH ON FIVE CHRS                           
         BE    DMGR1B                                                           
         BXLE  R3,R4,*-10                                                       
*                                                                               
DMGR1A   DS    0H                  CHECK FOR SPECIAL ACTIONS                    
         MVI   TRACEON,C'X'        SUPPRESS TRACE                               
         L     RF,=A(SPCLTAB)                                                   
         B     DMGR1A10                                                         
*                                                                               
DMGR1A00 CLC   2(0,RF),0(R6)       EXECUTED                                     
*                                                                               
DMGR1A05 MVI   ACTION,X'F1'        SET DTFAD ACTION                             
         B     DMGR2                                                            
*                                                                               
DMGR1A10 LLC   R7,0(RF)            ACTION LEN LESS 1 (FOR EX)                   
         L     RE,8(RF)            A(PROCESS ROUTINE) IF ACTION MATCH           
         EX    R7,DMGR1A00                                                      
         BER   RE                  DO SPECIAL CODE                              
         LA    RF,12(RF)                                                        
         CLI   0(RF),0                                                          
         BNE   DMGR1A10            BACK FOR NEXT SPECIAL ACTION                 
*                                                                               
         MVI   ACTION,X'FD'                                                     
         CLC   0(6,R6),=C'DMTIME'  TEST TIMER INTERFACE REQUEST                 
         BE    DMTIMER                                                          
*                                                                               
         MVI   ACTION,X'FF'        SET INVALID ACTION                           
         B     DMGR2                                                            
                                                                                
DMGR1B   MVC   ACTION,7(R3)        SAVE ACTION NUMBER                           
         ST    R3,AACTN                                                         
         B     DMGR2                                                            
                                                                                
DMGR2TR  MVI   TRACEON,C'S'        SET SPECIAL TRACE COMMAND IND                
         B     DMGR2                                                            
*                                                                               
DMGR2TON L     RF,XUTLNTRY         TURN ON TRACE FLAG IN UTL                    
         OI    TSTAT5-UTLD(RF),TST5IOTR                                         
         B     DMGRX                                                            
*                                                                               
DMGR2TOF L     RF,XUTLNTRY         TURN OFF TRACE FLAG IN UTL                   
         NI    TSTAT5-UTLD(RF),255-TST5IOTR                                     
         B     DMGRX                                                            
*                                                                               
DMGR2    L     RF,=V(UTL)          TEST IF OFFLINE                              
         TM    XFLAG1,XFONLINE                                                  
         BZ    DMGR2A                                                           
         ICM   RE,15,=V(SSB)       RE=A(SSB)                                    
         ICM   RF,15,XTCBNTRY      RF=A(TCB)                                    
         BZ    DMGR2A                                                           
         CLI   ACTION,QDMFAST      DON'T BUMP COUNT FOR DMFAST                  
         BE    MAXLIOC+4                                                        
         CLI   TRACEON,C'S'        DON'T BUMP COUNT FOR DMTRACE                 
         BE    MAXLIOC+4                                                        
         L     R0,TCBLIOC-TCBD(RF) BUMP LOGICAL I/O COUNT                       
         AHI   R0,1                                                             
         ST    R0,TCBLIOC-TCBD(RF)                                              
         C     R0,MAXLIOC                                                       
         BNH   MAXLIOC+4                                                        
         CLI   SSBMTIND-SSBD(RE),0                                              
         JNE   *+2                 DIE=MAX LOGICAL I/OS                         
         XC    TCBLIOC-TCBD(4,RF),TCBLIOC-TCBD(RF)                              
         B     MAXLIOC+4                                                        
MAXLIOC  DC    A(20000)            MAX LOGICAL I/O'S PER PHYSICAL I/O           
*                                                                               
         ICM   RF,15,XUTLNTRY                                                   
         BZ    DMGR2A                                                           
         TM    TTEST-UTLD(RF),X'10' TERMINAL IN TEST MODE ?                     
         BZ    DMGR2A               NO                                          
         CLI   TRACEON,C'S'        TEST TRACE COMMAND                           
         BE    DMGR2A1              YES                                         
         TM    TSTAT5-UTLD(RF),TST5IOTR   TEST I/O TRACE ACTIVE                 
         BZ    DMGR2A                                                           
         CLI   TRACEON,C'X'        TEST SUPPRESS TRACE                          
         BE    DMGR2A                                                           
         LA    RE,TSVCREQ-UTLD(RF)                                              
         OC    0(2,RE),0(RE)       DO NOT TRACE SVC REQ                         
         BNZ   DMGR2A                                                           
         CLI   TRACEON,C'S'        TEST SPECIAL TRACE IND                       
         BE    *+8                                                              
         MVI   TRACEON,C'I'        INDICATE INPUT TRACE                         
*                                                                               
DMGR2A1  BRAS  RE,DMTRACE                                                       
*                                                                               
DMGR2A   CLI   TRACEON,C'S'        EXIT IF SPECIAL TRACE INDICATOR              
         BE    DMGRXX                                                           
*                                                                               
         LTR   RF,RF                                                            
         BZ    DMGR2B                                                           
         SR    RE,RE               GET SENUM FROM UTL ENTRY                     
         IC    RE,TSYS-UTLD(RF)                                                 
         STC   RE,SVSENUM                                                       
         ICM   RF,15,=V(SYSFTAB)   LOGICAL SYSTEM NUM FROM SYSFTAB              
         BZ    DMGR2B                                                           
         IC    RE,0(RE,RF)                                                      
         STC   RE,SVMAJSYS                                                      
         LTR   RE,RE                                                            
         BZ    DMGR2B                                                           
         SLL   RE,2                                                             
         L     RF,=A(DMSYSTS)                                                   
         L     RF,0(RE,RF)                                                      
         LTR   RF,RF               RF=A(START OF FILES FOR SYSTEM)              
         BNZ   DMGR2C                                                           
                                                                                
***********************************************************************         
* DMCBW2 IS ADDRESS OF FILE NAME                                      *         
***********************************************************************         
DMGR2B   XR    R6,R6                                                            
         ICM   R6,7,DMCBW2+1       NOTHING KNOWN SEARCH WHOLE TABLE             
         L     R3,=A(DMFILES)                                                   
         USING DMGRFLD,R3                                                       
         BAS   RE,SETBXLE                                                       
DMGR2B1  CLI   0(R3),0                                                          
         BNE   *+10                                                             
         MVC   ACMD,8(R3)          SAVE A(COMMANDS FOR FILES)                   
         TM    DMGIND,DMGI6CHR     TEST FULL NAME COMPARE                       
         BZ    *+14                                                             
         CLC   0(6,R6),DMGFNAME                                                 
         B     *+10                                                             
         CLC   0(5,R6),DMGFNAME                                                 
         BE    DMGR2F                                                           
         BXLE  R3,R4,DMGR2B1                                                    
         B     DMGR2E                                                           
*                                                                               
DMGR2C   XR    R6,R6                                                            
         ICM   R6,7,DMCBW2+1       POSN TO FIRST ENTRY FOR SYSTEM               
         L     R3,=A(DMFILES)                                                   
         BAS   RE,SETBXLE                                                       
         LR    R3,RF                                                            
         MVC   ACMD,8(R3)          SAVE A(COMMANDS FOR FILES)                   
         AR    R3,R4                                                            
*                                                                               
DMGR2C1  CLI   0(R3),0             END OF LIST FOR SYSTEM                       
         BE    DMGR2C2                                                          
         TM    DMGIND,DMGI6CHR     TEST FULL NAME COMPARE                       
         BZ    *+14                                                             
         CLC   0(6,R6),DMGFNAME    MATCH ON SIX CHRS                            
         B     *+10                                                             
         CLC   0(5,R6),DMGFNAME    MATCH ON FIVE CHRS                           
         BE    DMGR2F                                                           
         BXLE  R3,R4,DMGR2C1                                                    
         B     DMGR2E                                                           
DMGR2C2  L     R3,=A(COMFLES)      POINT TO COMMON AREA                         
         B     DMGR2B1             AND CONTINUE SEARCH                          
*                                                                               
DMGR2E   XC    INTFILE(2),INTFILE  FILE NOT FOUND IN TABLE                      
         XR    R6,R6                                                            
         ICM   R6,7,DMCBW1+1                                                    
         CLC   0(4,R6),=C'OPEN'                                                 
         BE    DMODOPEN                                                         
         CLC   0(4,R6),=C'DMUNLK'                                               
         BE    DMGRUNLK                                                         
         B     DMGR2BAD            INVALID FILE NAME                            
*                                                                               
DMGR2F   MVC   INTFILE(2),DMGFINT  SAVE INTERNAL/EXTERNAL FILE NUMS             
         ST    R3,AFILE                                                         
         CLI   ACTION,0            TEST FOR DMUNLK                              
         BE    DMGRUNLK                                                         
         CLI   ACTION,X'F1'        TEST FOR DTF ADDRESS                         
         BE    DMODTFAD                                                         
         CLI   ACTION,X'FF'        TEST FOR INVALID ACTION                      
         BNE   DMGR3                                                            
         TM    DMGIND,DMGIWRKR+DMGIPRTQ+DMGIWRKF  WORK/PRTQ/WRKF/WRKZ           
         BNZ   DMGR2F1                                                          
         XR    R6,R6               CHECK FOR OPEN                               
         ICM   R6,7,DMCBW1+1                                                    
         CLC   0(4,R6),=C'OPEN'                                                 
         BE    DMODOPEN                                                         
         B     DMGR2BAD                                                         
         DROP  R3                                                               
                                                                                
DMGR2F1  L     R3,=A(DMPRINT)      SET SPECIAL ACTION FOR WORK/PRTQ             
         MVC   ACTION,7(R3)                                                     
         ST    R3,AACTN                                                         
                                                                                
DMGR3    BRAS  RE,BINSRCH          SEARCH FOR MODULE                            
         CLC   ACTION(2),0(RF)                                                  
         BNE   DMGR2BAD                                                         
         MVC   PARAM1,2(RF)        PARAM1 A(ROUTINE) FROM DMDMGRTAB             
*                                                                               
DMGR3X   CLI   ACTION,X'FE'        EXIT IF TRACE ACTION                         
         BE    DATAMGRX                                                         
         L     RF,PARAM1                                                        
         BR    RF                                                               
*                                                                               
DMGR2BAD DC    H'0'                DIE=CONSOLE MSG                              
                                                                                
         LA    R1,DMGRNG           DISPLAY CONSOLE MSG AND DIE                  
TYPE     L     RF,=V(DMOD000)                                                   
         BASR  RE,RF                                                            
         DC    H'0'                DIE=CONSOLE MSG                              
                                                                                
DMGRNG   DC    V(WCTYPE)                                                        
         DC    A(BADCMD)                                                        
         DC    A(L'BADCMD)                                                      
BADCMD   DC    C'INVALID DATAMGR COMMAND '                                      
                                                                                
DATAMGRX MVC   DMCBW1(1),SVDM1     RESTORE ACTION MODIFIER                      
         L     RF,SAVER1                                                        
         MVC   0(24,RF),DMCBW1     RESTORE CALLERS DMCB                         
         CLI   TRACEON,C'I'        TEST DID INPUT TRACE                         
         BNE   DMGRX                                                            
         MVI   TRACEON,C'O'        INDICATE OUTPUT TRACE REQUEST                
         GOTO1 =A(DMTRACE),(R1)                                                 
*                                                                               
DMGRX    MVI   DMGRXFLG,X'FF'      FLAG TO TEST DMCB3 ON EXIT                   
         B     *+8                                                              
DMGRXX   MVI   DMGRXFLG,0          FLAG NOT TO TEST DMCB3 ON EXIT               
         LT    RF,=V(PROTON)       ENABLE CALLERS STORAGE PROTECTION            
         BZ    *+6                                                              
         BASR  RE,RF                                                            
*                                                                               
         CLI   DMGRXFLG,0                                                       
         BE    *+8                                                              
         CLI   DMCBW3,0            SET CC TO EQUAL IF ALL OK                    
         XIT1                                                                   
                                                                                
***********************************************************************         
* TICTOC CALL                                                         *         
***********************************************************************         
DMTIMER  LA    R1,4(R2)            ACTUAL PARM LIST IS DMCB+4                   
         ICM   RF,15,=V(TICTOC)                                                 
         BZ    DMTIME10            OFFLINE IF NO V(TICTOC)                      
         BASR  RE,RF                                                            
         B     DMGRX                                                            
*                                                                               
DMTIME10 CLC   0(4,R1),=C'WAIT'    ONLY SUPPORT WAIT OFFLINE                    
         BNE   DMGRX                                                            
         MVC   FULL,4(R1)          GET TUS FROM PARAMETER LIST                  
         STIMER WAIT,TUINTVL=FULL                                               
         B     DMGRX                                                            
         EJECT                                                                  
***********************************************************************         
* DMADD - INDEX SEQUENTIAL                                            *         
***********************************************************************         
DMOD1IS  DS    0H                                                               
         LA    R0,1                UPDATE ACTION                                
*&&US*&& BAS   RE,TSTTRF                                                        
*                                                                               
         LA    R7,DMWORK                                                        
         L     RF,=V(RKEY)         READ PRIME DATA BLOCK FOR UPDATE             
         BAS   RE,SETPR                                                         
         ST    R7,PARAM2                                                        
         MVC   PARAM5,DMCBW4                                                    
         OI    DMCBW1,X'80'                                                     
         BASR  RE,RF                                                            
         CLI   DMCBW3,0                                                         
         BNE   DATAMGRX                                                         
         L     RE,PARAM4           CHECK RECORD DOESNT ALREADY EXIST            
         USING ISDTF,RE                                                         
         L     R5,DMCBW4                                                        
         LH    R6,ISKEYLN1                                                      
         EX    R6,DMOD1CLC                                                      
         BNE   *+12                                                             
         MVI   DMCBW3,X'20'        SET DUPLICATE KEY ERROR                      
         B     DATAMGRX                                                         
*                                                                               
         MVC   PARAM2(4),DMCBW4    SET ADD IN RECOVERY FILE                     
         MVC   PARAM3+2(2),ISRECLN                                              
         MVC   PARAM3(1),EXTFILE                                                
         MVI   PARAM3+1,3                                                       
         BRAS  R5,DMGRRECV                                                      
*                                                                               
         MVI   DMCBW3,0            ADD RECORD TO IS FILE                        
         MVC   PARAM1,=V(AKEY)                                                  
         L     RF,=V(DMOD000)                                                   
         BASR  RE,RF                                                            
         B     DATAMGRX                                                         
         DROP  RE                                                               
*                                                                               
DMOD1CLC CLC   0(0,R7),0(R5)                                                    
         EJECT                                                                  
***********************************************************************         
* DMADD - VARIABLE LENGTH INDEX SEQUENTIAL                            *         
***********************************************************************         
DMOD1IV  BAS   RE,D1IV                                                          
         B     DATAMGRX                                                         
*                                                                               
D1IV     NTRL  WORK=(R7,532)       MAX REC SIZE=4256 BYTES                      
         LA    R7,24(R7)                                                        
         L     RF,=V(RKEY)         READ PRIME DATA BLOCK FOR UPDATE             
         BAS   RE,SETPR                                                         
         ST    R7,PARAM2                                                        
         MVC   PARAM5,DMCBW4                                                    
         OI    DMCBW1,X'80'                                                     
         BASR  RE,RF                                                            
         L     RE,PARAM4           CHECK RECORD DOESNT ALREADY EXIST            
         USING ISDTF,RE                                                         
         L     R5,DMCBW4                                                        
         LH    R6,ISKEYLN1                                                      
         EX    R6,DMOD1CLC                                                      
         BNE   *+12                                                             
         MVI   DMCBW3,X'20'        SET DUPLICATE KEY ERROR                      
         B     D1IVX                                                            
*                                                                               
         L     R6,DMCBW4           SET ADD IN RECOVERY FILE                     
         LA    R6,0(R6)                                                         
         ST    R6,PARAM2                                                        
         AH    R6,ISKEYLN                                                       
         SR    R5,R5                                                            
         ICM   R5,3,0(R6)          GET RECORD LENGTH                            
         JZ    *+2                 DIE=ZERO REC LEN                             
         STH   R5,PARAM3+2                                                      
         MVC   PARAM3(1),EXTFILE                                                
         MVI   PARAM3+1,3                                                       
         BRAS  R5,DMGRREC1                                                      
*                                                                               
         MVI   DMCBW3,0            ADD RECORD TO IS FILE                        
         MVC   PARAM1(4),=V(AKEY)                                               
         L     RF,=V(DMOD000)                                                   
         BASR  RE,RF                                                            
         DROP  RE                                                               
*                                                                               
D1IVX    XIT1                                                                   
                                                                                
***********************************************************************         
* DMADD - ACCOUNT                                                     *         
***********************************************************************         
DMOD1AC  DS    0H                                                               
         BAS   RE,ACCOEMU          TEST IF ACCOUNT FILE EMULATION               
         BNE   *+12                                                             
         BAS   RE,ACCOLEN          NO CHECK ACCOUNT FILE RECORD LEN             
         B     DMOD1IV                                                          
         BASR  RE,RF               YES GO TO EMULATION MODULE                   
         B     DATAMGRX                                                         
*                                                                               
ACCOEMU  ICM   R7,15,=V(DMACCEMU)  TEST ACCOUNT FILE EMULATION                  
         BER   RE                                                               
         LR    R0,RE               YES GET DTF FOR THIS ACCOUNT FILE            
         BAS   RE,SETDUM                                                        
         BASR  RE,RF                                                            
         LR    RE,R0                                                            
         L     RF,PARAM4           POINT TO ACCOUNT FILE DTF                    
         TM    20(RF),X'80'        TEST EMULATION REQUIRED BIT                  
         BO    *+6                                                              
         SR    R7,R7                                                            
         LTR   RF,R7               EXIT WITH RF=A(EMULATION MODULE)             
         BR    RE                  EXIT WITH CC=EQL IF NOT REQUIRED             
*                                                                               
ACCOLEN  L     RF,DMCBW4           TEST ACCOUNT FILE RECORD LENGTH              
         CLC   42(2,RF),=H'50'     MINIMUM RECORD SIZE                          
         JL    *+2                 DIE=MIN REC LEN                              
         CLC   42(2,RF),=H'2000'   MAXIMUM RECORD SIZE                          
         BNHR  RE                                                               
         DC    H'0'                DIE=MAX REC LEN                              
*                                                                               
DMOD1CT  L     R0,DMCBW4                                                        
         BAS   RE,D1IV                                                          
         CLI   DMCBW3,0            TEST ERROR ON ADD                            
         BNE   DATAMGRX                                                         
*                                                                               
         BAS   RE,CTCHKADD                                                      
         CLI   DMCBW3,X'FF'        TEST NO BUFFERING                            
         BNE   *+8                                                              
         MVI   DMCBW3,0            RESET TO NO ERROR                            
         B     DATAMGRX                                                         
         EJECT                                                                  
***********************************************************************         
* DMADD - SPTFILE                                                     *         
***********************************************************************         
*&&SPT                                                                          
DMOD121  BAS   RE,SETPRC           BUILD PARAM LIST                             
         L     RF,=V(DMDALINK)                                                  
         BASR  RE,RF                                                            
         TM    DMCBW3,X'FF'        TEST ERRORS                                  
         BNZ   DATAMGRX                                                         
*                                  BUILD SPTDIR ITEM IN RECOVERY AREA           
         L     R6,DMCBW4                                                        
         LA    R7,DMWORK           POINT TO A WORK AREA                         
         MVC   0(13,R7),0(R6)      KEY                                          
         MVC   13(1,R7),15(R6)     MOVE IN RECORD STATUS                        
         NI    13(R7),X'7F'        CONTROL                                      
         L     RE,DMCBW3           DISK ADDRESS                                 
         MVC   14(4,R7),0(RE)                                                   
*                                  ADD DIRECTORY ITEM                           
         MVC   PARAM1(20),DMCBW1   MOVE DMCB                                    
         L     R6,=A(DSPTDIR)                                                   
         ST    R6,PARAM2           A(SPTDIR)                                    
         ST    R7,PARAM3           A(KEY)                                       
         ST    R7,PARAM4           A(RECORD)                                    
         BRAS  RE,DATAMGR                                                       
*                                                                               
D121X    OC    DMCBW3(1),PARAM3    PASS ERROR INDICATORS                        
         B     DATAMGRX                                                         
*&&                                                                             
         EJECT                                                                  
***********************************************************************         
* DMADD - TRFFILE                                                     *         
***********************************************************************         
*&&TRF                                                                          
DMOD132  BAS   RE,SETPRC           BUILD PARAM LIST                             
         L     RF,=V(DMDALINK)                                                  
         BASR  RE,RF                                                            
         TM    DMCBW3,X'FF'        TEST ERRORS                                  
         BNZ   DATAMGRX                                                         
*                                  BUILD TRFDIR ITEM IN RECOVERY AREA           
         L     R6,DMCBW4                                                        
         LA    R7,DMWORK           POINT TO A WORK AREA                         
         MVC   0(13,R7),0(R6)      KEY                                          
         MVI   13(R7),0            CONTROL                                      
         L     RE,DMCBW3           DISK ADDRESS                                 
         MVC   14(4,R7),0(RE)                                                   
*                                  ADD DIRECTORY ITEM                           
         MVC   PARAM1(20),DMCBW1   MOVE DMCB                                    
         L     R6,=A(DTRFDIR)                                                   
         ST    R6,PARAM2           A(TRFDIR)                                    
         ST    R7,PARAM3           A(KEY)                                       
         ST    R7,PARAM4           A(RECORD)                                    
         BRAS  RE,DATAMGR                                                       
*                                                                               
         OC    DMCBW3(1),PARAM3    PASS ERROR INDICATORS                        
         B     DATAMGRX                                                         
*&&                                                                             
         EJECT                                                                  
***********************************************************************         
* DMADD - ACCDAY                                                      *         
***********************************************************************         
*&&ACC                                                                          
DMOD166  DS    0H                                                               
         L     RF,=V(WRTGET)       GET AND LOCK DNEXT                           
         BAS   RE,SETPR                                                         
         L     RE,DMCBW4                                                        
         MVC   PARAM3+2(2),0(RE)   SET RECORD LENGTH FROM RECORD                
         BASR  RE,RF                                                            
*                                                                               
         MVC   PARAM3(1),EXTFILE   SET ADD IN RECOVERY FILE                     
         MVI   PARAM3+1,3                                                       
         BRAS  R5,DMGRRECV                                                      
*                                                                               
         MVC   PARAM1,=V(WRTAFT)   ADD RECORD TO DA FILE                        
         L     RF,=V(DMOD000)                                                   
         BASR  RE,RF                                                            
         B     DATAMGRX                                                         
*&&                                                                             
                                                                                
*&&SRV                                                                          
DMOD1FB  DS    0H                  DMADD - KWXFILE                              
*                                                                               
         L     RF,=V(WRTGET)       GET AND LOCK DNEXT                           
         BAS   RE,SETPR                                                         
         L     RE,DMCBW4                                                        
         MVC   PARAM3+2(2),0(RE)   SET RECORD LENGTH FROM RECORD                
         BASR  RE,RF                                                            
*                                                                               
         MVC   PARAM1,=V(WRTAFT)   ADD RECORD TO DA FILE                        
         L     RF,=V(DMOD000)                                                   
         BASR  RE,RF                                                            
         B     DATAMGRX                                                         
*&&                                                                             
         EJECT                                                                  
***********************************************************************         
* DMADD - TSTRCVR - DMCB+8(4)=DATA LEN, DMCB+12(4)=DATA ADR           *         
***********************************************************************         
*&&SRV                                                                          
DMOD1F9  DS    0H                                                               
         ICM   RF,15,XUTLNTRY                                                   
         BZ    D1F9X                                                            
         TM    TTEST-UTLD(RF),TTESTTAC  ANY TSTTAB ENTRY                        
         BZ    D1F9X                                                            
         ICM   R7,15,TACCS-UTLD(RF)     MUST HAVE A TSTTAB                      
         BZ    D1F9X                                                            
         USING TSTTABD,R7                                                       
*                                                                               
         CLC   TNUM-UTLD(2,RF),TSTNUM TEST SAME TERMINAL                        
         BNE   D1F9X                  EXIT IF NOT ACTIVE                        
*                                                                               
         GOTO1 =V(DMOD000),(R1),V(DMEXT),,,(X'F9',0)                            
*                                                                               
         MVC   PARAM1(4),=V(ADDAFT)  WRITE RECORD TO TSTRCVR                    
         MVC   PARAM1+4(4),DMCBW4    DMCBW4=RECORD ADDRESS                      
*                                                                               
         LH    RE,DMCBW3+2         GET LENGTH FROM INCOMING DMCBW3              
         ST    RE,PARAM1+8         SET LEN IN P3                                
*                                                                               
         LA    RE,TSTLAST          SET LAST RECORD ADDED                        
         ST    RE,PARAM1+16                                                     
         MVC   PARAM1+20(4),TSTHIGH SET HIGH TRACK & CAPACITY                   
         L     RF,=V(DADDS)                                                     
         BASR  RE,RF                                                            
         MVC   TSTCPTY,PARAM1+22   SAVE TRACK CAPACITY                          
*                                                                               
         OC    PARAM1+8(2),PARAM1+8 TEST FOR ERRORS                             
         BZ    D1F9X                INHIBIT LOGGING                             
         NI    TSTFLAGS,255-TSTFSCRQ                                            
*                                                                               
D1F9X    B     DATAMGRX                                                         
         DROP  R7                                                               
*&&                                                                             
         EJECT                                                                  
***********************************************************************         
* DMREAD - INDEX SEQUENTIAL                                           *         
***********************************************************************         
DMOD2IS  DS    0H                                                               
         XR    R0,R0                                                            
*&&US*&& BAS   RE,TSTTRF                                                        
         L     R7,DMCBW3                                                        
         MVC   OLDKEY,0(R7)        SAVE KEY                                     
         L     RF,=V(RKEY)                                                      
         TM    DMCBW1,X'24'        TEST FOR READ FLUSH                          
         BNO   *+8                                                              
         L     RF,=V(RKEYFLS)                                                   
         BAS   RE,SETPR                                                         
         LA    R7,OLDKEY                                                        
         ST    R7,PARAM5                                                        
         BASR  RE,RF                                                            
         TM    DMCBW3,X'FF'                                                     
         BNZ   DATAMGRX                                                         
*                                                                               
         L     RE,PARAM4           A(DTF)                                       
         USING ISDTF,RE                                                         
         LH    R7,ISKEYLN1         GET KEY LEN-1                                
         DROP  RE                                                               
         L     R6,DMCBW4           REC ADDRESS                                  
         EX    R7,DMOD2CLC                                                      
         BE    *+12                                                             
DMOD2RNF EQU   *                   SET RECORD NOT FOUND BIT                     
         MVI   DMCBW3,X'10'                                                     
         B     DATAMGRX                                                         
         LA    R7,1(R7)            SET KEY LEN                                  
         BAS   R5,DMGRCTLI         TEST CONTROL STATUS                          
*&&REP                                                                          
         OC    REPWRK(4),REPWRK                                                 
         BNZ   DMOD281A                                                         
*&&                                                                             
         B     DATAMGRX                                                         
*                                                                               
DMOD2CLC CLC   OLDKEY(0),0(R6)     EXECUTED                                     
                                                                                
DMOD2AC  DS    0H                  DMREAD - ACCOUNT                             
         BAS   RE,ACCOEMU          TEST IF ACCOUNT FILE EMULATION               
         BE    DMOD2IS                                                          
         BASR  RE,RF               YES GO TO EMULATION MODULE                   
         B     DATAMGRX                                                         
         EJECT                                                                  
***********************************************************************         
* DMREAD - PAVDIR - VSAM INTERFACE                                    *         
***********************************************************************         
DMOD22C  DS    0H                                                               
*&&UK*&& B     DMOD2IS                                                          
*&&US                                                                           
         MVI   DMCBW7,5            VSAM FILESET NUMBER 5=PAVXXX                 
         MVI   DMCBW7+1,C'D'       VSAM MARK DIRECTORY                          
         BRAS  RE,DEMVSAM          CALL DEMO VSAM INTERFACE                     
         B     DATAMGRX            VSAM PROCESSED                               
         B     DMOD2IS             VSAM OFF, USE LEGACY CODE                    
*&&                                                                             
         EJECT                                                                  
***********************************************************************         
* DMREAD - DEMDIR - DEMDIRN=X'2D',DEMDIRA=X'2D',DEMDIRR=X'30' INTFILE *         
*                   DEMDIRN=X'2D',DEMDIRA=X'2F',DEMDIRR=X'30' EXTFILE *         
***********************************************************************         
DMOD22D  DS    0H                                                               
*&&UK*&& B     DMOD2IS                                                          
*&&US                                                                           
         MVI   DMCBW7+1,C'D'       VSAM MARK DIRECTORY                          
         L     RF,DMCBW3           USER KEY IN PARAM 3                          
         CLI   0(RF),DFUECODQ      FUSION UNIVERSE ESTIMATES TO DEMDIRR         
         BE    *+12                                                             
         CLI   1(RF),C'R'          RADIO TO DEMDIRR                             
         BNE   D22DA                                                            
         MVI   INTFILE,X'30'                                                    
         MVI   EXTFILE,X'30'                                                    
         MVI   DMCBW7,3            VSAM FILESET NUMBER 3=DEMXXXR                
         B     D22DVSAM            DEMDIRR TO VSAM                              
*                                                                               
D22DA    MVI   DMCBW7,2            VSAM FILESET NUMBER 2=DEMXXXN                
         CLI   2(RF),C'N'          NSI TO DEMDIRN                               
         BE    D22DC                                                            
         CLI   2(RF),C'S'          SRC TO DEMDIRN                               
         BNE   D22DE                                                            
*                                                                               
D22DC    DS    0H                                                               
*&&DMSMF                                                                        
         BRAS  RE,DEMSMF           LOG THIS MAJOR KEY VIA SMF                   
*&&                                                                             
         B     D22DVSAM                                                         
*                                                                               
D22DE    DS    0H                                                               
         MVI   EXTFILE,X'2F'       ALL OTHERS TO DEMDIRA                        
         MVI   DMCBW7,1            VSAM FILESET NUMBER 1=DEMXXXA                
*                                                                               
D22DVSAM BRAS  RE,DEMVSAM          CALL DEMO VSAM INTERFACE                     
         B     DATAMGRX            VSAM PROCESSED                               
         B     DMOD2IS             VSAM OFF, USE LEGACY CODE                    
*&&                                                                             
         EJECT                                                                  
***********************************************************************         
* DMREAD - NTIDIR - VSAM INTERFACE                                    *         
***********************************************************************         
DMOD238  DS    0H                                                               
*&&UK*&& B     DMOD2IS                                                          
*&&US                                                                           
         MVI   DMCBW7,4            VSAM FILESET NUMBER 4=NTIXXX                 
         MVI   DMCBW7+1,C'D'       VSAM MARK DIRECTORY                          
         BRAS  RE,DEMVSAM          CALL DEMO VSAM INTERFACE                     
         B     DATAMGRX            VSAM PROCESSED                               
         B     DMOD2IS             VSAM OFF, USE LEGACY CODE                    
*&&                                                                             
         EJECT                                                                  
***********************************************************************         
* DMREAD - CTFILE                                                     *         
***********************************************************************         
DMOD2CT  L     R0,DMCBW3                                                        
         BAS   RE,CTCHKPE          CHECK IF A PERSON RECORD                     
         BAS   RE,CTCHKRD                                                       
         BAS   RE,CTCOUNT                                                       
         CLI   DMCBW3,X'FF'        EXIT IF RECORD TYPE BUFFERED                 
         BNE   DATAMGRX                                                         
         MVI   DMCBW3,0                                                         
         B     DMOD2IS                                                          
         EJECT                                                                  
***********************************************************************         
* SPECIAL INTEREP CODE                                                *         
***********************************************************************         
*&&REP                                                                          
DMOD281  BRAS  RE,REPMOD1          SPECIAL CODE FOR INTEREP                     
         B     DMOD2IS                                                          
*                                                                               
DMOD281A BRAS  RE,REPMOD2          SPECIAL INTEREP PROCESSING                   
         CLI   REPRC,X'01'         TEST RECORD NOT FOUND                        
         BE    DMOD2RNF                                                         
         B     DATAMGRX                                                         
*&&                                                                             
         EJECT                                                                  
***********************************************************************         
* DMREAD - STATION                                                    *         
***********************************************************************         
*&&SPT                                                                          
DMOD222  L     R4,DMCBW3           A(KEY)                                       
         MVC   OLDKEY(17),0(R4)    SAVE KEY                                     
         L     R6,DMCBW4           A(RECORD)                                    
         L     RF,=V(RKEY)                                                      
         TM    DMCBW1,X'24'        TEST FOR READ FLUSH                          
         BNO   *+8                                                              
         L     RF,=V(RKEYFLS)                                                   
         BAS   RE,SETPR                                                         
         CLI   OLDKEY,C'S'                                                      
         BNE   D222B                                                            
         CLC   OLDKEY+9(3),=C'000' TEST IF TRY FOR CLT EXCEPTION REC            
         BE    D222B                                                            
         BASR  RE,RF                                                            
         TM    DMCBW3,X'C0'        TEST ERRORS                                  
         BNZ   DATAMGRX                                                         
         CLC   OLDKEY(15),0(R6)    TEST SAME KEY                                
         BNE   D222A                                                            
         L     R7,PARAM4                                                        
         LH    R7,ISKEYLN-ISDTF(R7)                                             
         BAS   R5,DMGRCTLI         TEST CONTROL                                 
         TM    DMCBW3,X'10'        TEST REC NOT FOUND                           
         BZ    DATAMGRX                                                         
D222A    MVC   OLDKEY+9(3),=C'000' ZERO CLIENT CODE AND TRY AGAIN               
         MVC   0(17,R4),OLDKEY                                                  
D222B    BASR  RE,RF                                                            
         TM    DMCBW3,X'C0'        TEST FOR ERRORS                              
         BNZ   DATAMGRX                                                         
         CLC   OLDKEY(15),0(R6)    TEST IF KEYS MATCH                           
         BE    D222C                                                            
         MVI   DMCBW3,X'10'        SET RECORD NOT FOUND                         
         B     DATAMGRX                                                         
D222C    DS    0H                                                               
         L     R7,PARAM4                                                        
         LH    R7,ISKEYLN-ISDTF(R7)                                             
         BAS   R5,DMGRCTLI         TEST CONTROL                                 
         B     DATAMGRX            EXIT                                         
*&&                                                                             
         EJECT                                                                  
***********************************************************************         
* DMREAD - DIRECT ACCESS FILES VIA DIRECTORY                          *         
***********************************************************************         
*&&SPT                                                                          
DMOD221  L     RE,=A(DSPTDIR)      DMREAD - SPTFILE                             
         LA    R3,14                                                            
         B     DMOD2XX                                                          
DMOD237  L     RE,=A(DXSPDIR)      DMREAD - XSPFILE                             
         LA    R3,36                                                            
         B     DMOD2XX                                                          
DMOD22A  L     RE,=A(DUNTDIR)      DMREAD - UNTFIL                              
         LA    R3,21                                                            
         B     DMOD2XX                                                          
*&&                                                                             
*&&TRF                                                                          
DMOD233  L     RE,=A(DTRFDIR)      DMREAD - TRFFILE                             
         LA    R3,14                                                            
         B     DMOD2XX                                                          
*&&                                                                             
*&&PRT                                                                          
DMOD242  L     RE,=A(DPRTDIR)      DMREAD - PRTFILE                             
         B     *+8                                                              
DMOD243  L     RE,=A(DPUBDIR)      DMREAD - PUBFILE                             
         LA    R3,27                                                            
         B     DMOD2XX                                                          
*&&                                                                             
*&&MED                                                                          
DMOD242  L     RE,=A(DMEDIR)       DMREAD - MEDFILE                             
         LA    R3,28                                                            
         B     DMOD2XX                                                          
DMOD247  L     RE,=A(DDMGDIR)      DMREAD - DMGFILE                             
         LA    R3,15                                                            
         B     DMOD2XX                                                          
DMOD249  L     RE,=A(DDMNDIR)      DMREAD - DMNNEW/DMNOLD                       
         LA    R3,15                                                            
         B     DMOD2XX                                                          
DMOD24C  L     RE,=A(DDMODIR)      DMREAD - DMO1FL/DMO2FL/DMO3FL/DMO4FL         
         LA    R3,15                                                            
         B     DMOD2XX                                                          
*                                                                               
DMODGFK  ICM   RF,15,=V(GFKIO)     DM???? - GFKFIL                              
         BZ    DMGRXX                                                           
         L     R1,SAVER1           PASS CONTROL TO =V(GFKIO)                    
         BASR  RE,RF                                                            
         B     DMGRXX                                                           
*&&                                                                             
*&&MPL                                                                          
DMOD252  L     RE,=A(DMPLDIR)      DMREAD - MPLFIL                              
         LA    R3,36                                                            
         B     DMOD2XX                                                          
DMOD259  L     RE,=A(DMPCDRS)      DMREAD - MPCFLS                              
         LA    R3,16                                                            
         B     DMOD2XX                                                          
*&&DO                                                                           
DMOD25B  L     RE,=A(DBUDDIR)      DMREAD - BUDFIL                              
         LA    R3,36                                                            
         B     DMOD2XX                                                          
*&&                                                                             
DMOD25D  L     RE,=A(DMPRDRB)      DMREAD - MPRFLB                              
         LA    R3,28                                                            
         B     DMOD2XX                                                          
DMOD25F  L     RE,=A(DMPCDRC)      DMREAD - MPCFLC                              
         LA    R3,16                                                            
         B     DMOD2XX                                                          
*&&                                                                             
*&&ACC                                                                          
DMOD26A  L     RE,=A(DACCDIR)      DMREAD - ACCMST                              
         LA    R3,50                                                            
         B     DMOD2XX                                                          
DMOD26B  L     RE,=A(DACCDIR)      DMREAD - ACCARC                              
         LA    R3,50                                                            
         B     DMOD2XX                                                          
*&&                                                                             
*&&TAL                                                                          
DMOD272  L     RE,=A(DTALDIR)      DMREAD - TALFIL                              
         LA    R3,34                                                            
         B     DMOD2XX                                                          
DMOD276  L     RE,=A(DCHKDIR)      DMREAD - CHKFIL                              
         LA    R3,34                                                            
         B     DMOD2XX                                                          
*&&                                                                             
*&&FEE                                                                          
DMOD272  L     RE,=A(DFEEDIR)      DMREAD - FEEFIL                              
         LA    R3,36                                                            
         B     DMOD2XX                                                          
*&&                                                                             
*&&REP                                                                          
DMOD282  L     RE,=A(DREPDIR)      DMREAD - REPFILE                             
         LA    R3,28                                                            
         B     DMOD2XX                                                          
*&&                                                                             
*&&MBA                                                                          
DMOD292  L     RE,=A(DMBADIR)      DMREAD - MBAFIL                              
         LA    R3,40                                                            
         B     DMOD2XX                                                          
DMOD297  L     RE,=A(DMBUDIR)      DMREAD - MBUFIL                              
         LA    R3,40                                                            
         B     DMOD2XX                                                          
*&&                                                                             
*&&CON                                                                          
DMOD2AF  L     RE,=A(DGENDIR)      DMREAD - GENFIL                              
         LA    R3,36                                                            
         B     DMOD2XX                                                          
*&&UK                                                                           
DMOD2A7  L     RE,=A(DCRADIR)      DMREAD - CRAFIL                              
         LA    R3,13                                                            
         B     DMOD2XX                                                          
*&&                                                                             
*&&                                                                             
*&&PER                                                                          
DMOD2E2  L     RE,=A(DPERDIR)      DMREAD - PERFIL                              
         LA    R3,38                                                            
         B     DMOD2XX                                                          
*&&                                                                             
                                                                                
DMOD2XX  MVC   PARAM1(24),DMCBW1   MOVE DMCB                                    
         ST    RE,PARAM2                                                        
         BRAS  RE,DATAMGR                                                       
*                                                                               
         TM    PARAM3,X'DC'        DO NOT TEST DELETES                          
         BNZ   DMOD2XXX                                                         
         OC    DMCBW3(1),PARAM3    PASS ERROR INDICATORS                        
*                                                                               
         L     R6,DMCBW3                                                        
         L     R7,DMCBW4                                                        
         AR    R7,R3               POINT TO DISK ADDR                           
         MVC   0(4,R6),0(R7)       MOVE OVER KEY                                
*                                                                               
         MVC   PARAM1(24),DMCBW1   NOW DO READ DIRECT TO FILE                   
         LARL  RE,GETREC                                                        
         ST    RE,PARAM1                                                        
         MVC   PARAM1(1),DMCBW1                                                 
         BRAS  RE,DATAMGR                                                       
                                                                                
DMOD2XXX OC    DMCBW3(1),PARAM3    PASS ERRORS                                  
         B     DATAMGRX                                                         
         EJECT                                                                  
***********************************************************************         
* DMRSEQ - INDEX SEQUENTIAL                                           *         
***********************************************************************         
DMOD3IS  DS    0H                                                               
         XR    R0,R0                                                            
*&&US*&& BAS   RE,TSTTRF                                                        
*                                                                               
         BAS   RE,SETDUM           GET DTF ADDR                                 
         BASR  RE,RF                                                            
         L     RE,PARAM4                                                        
         LA    RE,0(RE)                                                         
         USING ISDTF,RE                                                         
                                                                                
         SAM31                                                                  
         LH    RF,ISKEYLN1         GET KEY LEN-1                                
         L     RE,ISPDKEY                                                       
         DROP  RE                                                               
                                                                                
         EX    RF,*+8              SAVE OLD KEY                                 
         B     *+10                                                             
         MVC   OLDKEY(0),0(RE)                                                  
         SAM24                                                                  
*                                                                               
         L     RF,=V(RSEQ)                                                      
         TM    DMCBW1,X'24'        TEST FOR READ FLUSH                          
         BNO   *+8                                                              
         L     RF,=V(RSEQFLS)                                                   
         BAS   RE,SETPR                                                         
         LA    R7,OLDKEY                                                        
         ST    R7,PARAM5                                                        
         TM    DMCBW1,X'10'        TEST FULL TRACK READ                         
         BZ    *+14                                                             
         MVC   PARAM6,DMCBW5                                                    
         MVI   PARAM6,X'FF'                                                     
         BASR  RE,RF                                                            
         TM    DMCBW3,X'C0'                                                     
         BNZ   DATAMGRX                                                         
*                                                                               
DMOD3AB  L     RE,PARAM4                                                        
         USING ISDTF,RE                                                         
         LH    R7,ISKEYLN                                                       
         DROP  RE                                                               
                                                                                
         L     R6,DMCBW4                                                        
         BAS   R5,DMGRCTLI                                                      
         TM    DMCBW3,X'10'                                                     
         BO    DMOD3IS             READ AGAIN IF NOT FOUND                      
*&&REP                                                                          
         OC    REPWRK(4),REPWRK                                                 
         BNZ   DMOD381A                                                         
*&&                                                                             
         B     DATAMGRX                                                         
                                                                                
DMOD3AC  DS    0H                  DMRSEQ - ACCOUNT                             
         BAS   RE,ACCOEMU          TEST IF ACCOUNT FILE EMULATION               
         BE    DMOD3IS                                                          
         BASR  RE,RF               YES GO TO EMULATION MODULE                   
         B     DATAMGRX                                                         
         EJECT                                                                  
***********************************************************************         
* DMRSEQ - PAVDIR - VSAM INTERFACE                                    *         
***********************************************************************         
DMOD32C  DS    0H                                                               
*&&UK*&& B     DMOD3IS                                                          
*&&US                                                                           
         MVI   DMCBW7,5            VSAM FILESET NUMBER 5=PAVXXX                 
         MVI   DMCBW7+1,C'D'       VSAM MARK DIRECTORY                          
         BRAS  RE,DEMVSAM          CALL DEMO VSAM INTERFACE                     
         B     DATAMGRX            VSAM PROCESSED                               
         B     DMOD3IS             VSAM OFF, USE LEGACY CODE                    
*&&                                                                             
         EJECT                                                                  
***********************************************************************         
* DMRSEQ - DEMDIR - DEMDIRN=X'2D',DENDIRA=X'2D',DEMDIRR=X'30' INTFILE *         
*                   DEMDIRN=X'2D',DEMDIRA=X'2F',DEMDIRR=X'30' EXTFILE *         
***********************************************************************         
DMOD32D  DS    0H                                                               
*&&UK*&& B     DMOD3IS                                                          
*&&US                                                                           
         MVI   DMCBW7+1,C'D'       VSAM MARK DIRECTORY                          
         L     RF,DMCBW3           USER KEY IN PARAM 3                          
         CLI   0(RF),DFUECODQ      FUSION UNIVERSE ESTIMATES TO DEMDIRR         
         BE    *+12                                                             
         CLI   1(RF),C'R'          RADIO TO DEMDIRR                             
         BNE   D32DA                                                            
         MVI   INTFILE,X'30'                                                    
         MVI   EXTFILE,X'30'                                                    
         MVI   DMCBW7,3            VSAM FILESET NUMBER 3=DEMXXXR                
         B     D32DVSAM            DEMDIRR TO VSAM                              
*                                                                               
D32DA    MVI   DMCBW7,2            VSAM FILESET NUMBER 2=DEMXXXN                
         CLI   2(RF),C'N'          NSI TO DEMDIRN                               
         BE    D32DC                                                            
         CLI   2(RF),C'S'          SRC TO DEMDIRN                               
         BNE   D32DE                                                            
*                                                                               
D32DC    DS    0H                                                               
*&&DMSMF                                                                        
         BRAS  RE,DEMSMF           LOG THIS MAJOR KEY VIA SMF                   
*&&                                                                             
         B     D32DVSAM                                                         
*                                                                               
D32DE    DS    0H                                                               
         MVI   EXTFILE,X'2F'       ALL OTHERS TO DEMDIRA                        
         MVI   DMCBW7,1            VSAM FILESET NUMBER 1=DEMXXXA                
*                                                                               
D32DVSAM BRAS  RE,DEMVSAM          CALL DEMO VSAM INTERFACE                     
         B     DATAMGRX            VSAM PROCESSED                               
         B     DMOD3IS             VSAM OFF, USE LEGACY CODE                    
*&&                                                                             
         EJECT                                                                  
***********************************************************************         
* DMRSEQ - NTIDIR - VSAM INTERFACE                                    *         
***********************************************************************         
DMOD338  DS    0H                                                               
*&&UK*&& B     DMOD3IS                                                          
*&&US                                                                           
         MVI   DMCBW7,4            VSAM FILESET NUMBER 4=NTIXXX                 
         MVI   DMCBW7+1,C'D'       VSAM MARK DIRECTORY                          
         BRAS  RE,DEMVSAM          CALL DEMO VSAM INTERFACE                     
         B     DATAMGRX            VSAM PROCESSED                               
         B     DMOD3IS             VSAM OFF, USE LEGACY CODE                    
*&&                                                                             
         EJECT                                                                  
***********************************************************************         
* DMRSEQ - CTFILE                                                     *         
***********************************************************************         
DMOD3CT  L     R0,DMCBW3                                                        
         BAS   RE,CTCHKRD                                                       
         BAS   RE,CTCOUNT                                                       
         CLI   DMCBW3,X'FF'        EXIT IF RECORD TYPE BUFFERED                 
         BNE   DATAMGRX                                                         
         MVI   DMCBW3,0                                                         
         B     DMOD3IS                                                          
         EJECT                                                                  
***********************************************************************         
* SPECIAL CODE FOR INTEREP                                            *         
***********************************************************************         
*&&REP                                                                          
DMOD381  BRAS  RE,REPMOD1          SPECIAL INTERREP ROCESSING                   
         B     DMOD3IS                                                          
*                                                                               
DMOD381A BRAS  RE,REPMOD2          SPECIAL INTEREP PROCESSING                   
         CLI   REPRC,X'FF'         TEST IF NEED TO REPOINT KEY                  
         BE    DMOD481                                                          
         CLI   REPRC,X'01'         TEST IF NEED TO READ SEQ                     
         BE    DMOD3IS                                                          
         B     DATAMGRX                                                         
*&&                                                                             
         EJECT                                                                  
***********************************************************************         
* DMRSEQ - DIRECT ACCESS                                              *         
***********************************************************************         
DMOD3DA  DS    0H                                                               
         CLI   INTFILE,X'66'       TEST ACCDAY FILE                             
         BNE   *+12                                                             
         L     RE,DMCBW3                                                        
         NI    0(RE),X'7F'         TURN OFF DISK ADDR HIGH ORDER BIT            
         BAS   RE,SETDUM                                                        
         BASR  RE,RF               SET DTF ADDRESS IN PARAM4                    
*                                                                               
DSQRD0   SR    R0,R0               RESET NOT FOUND SWITCH                       
         L     R6,DMCBW3           GET LAST DISK ADDR READ                      
         OC    0(4,R6),0(R6)                                                    
         BZ    DSQRD4                                                           
*                                                                               
DSQRD1A  L     RE,PARAM4           GET DTF ADDRESS                              
DSQRD1A1 TM    DTFTYPE-DTFPHD(RE),DTFTBIGF                                      
         BZ    DSQRD1A2                                                         
         IC    R3,3(R6)            20-BIT AND 22-BIT BUMP RECORD NUM            
         LA    R3,1(R3)                                                         
         STC   R3,3(R6)                                                         
         B     DSQRD1B                                                          
*                                                                               
DSQRD1A2 TM    DTFTYPE-DTFPHD(RE),DTFTBIG                                       
         BZ    *+12                                                             
         TM    2(R6),X'3F'         18-BIT TEST LAST REC                         
         BO    DSQRD4                                                           
         IC    R3,2(R6)            16-BIT AND 18-BIT BUMP RECORD NUM            
         LA    R3,1(R3)                                                         
         STC   R3,2(R6)                                                         
*                                                                               
DSQRD1B  L     RF,=V(READ)         SET COMMAND                                  
         BAS   RE,SETPR            SET PARAM LIST                               
         TM    DMCBW1,X'10'        TEST FULL TRACK READ                         
         BZ    *+16                                                             
         MVC   PARAM1,=V(READTRK)                                               
         MVC   PARAM6,DMCBW5       MOVE BUFFER ADDRESS                          
*                                                                               
         BASR  RE,RF                                                            
         TM    PARAM3+1,X'04'      TEST EOF                                     
         BO    DSQRDX                                                           
         OC    PARAM3(2),PARAM3    TEST FOR ERRORS                              
         BNZ   DSQRD2                                                           
         OC    PARAM3+2(2),PARAM3+2                                             
         BZ    DSQRD0              IGNORE ZERO LENGTH RECORD                    
*                                                                               
         L     R3,AFILE            POINT TO FILE TBL ENTRY                      
         USING DMGRFLD,R3                                                       
         CLI   DMGLDELL,0          DOES FILE HAVE LOGICAL DELETED RECS          
         BE    DSQRD1C                                                          
         TM    DMCBW1,X'28'        YES TEST IF CALLER WANTS THEM                
         BO    DSQRD1C                                                          
         L     R6,DMCBW4           POINT TO RECORD                              
         SR    R9,R9                                                            
         IC    R9,DMGLDELD         DISPLACMENT                                  
         AR    R6,R9               POINT TO FIELD IN RECORD                     
         IC    R9,DMGLDELL         GET LEN OF FIELD                             
         BCTR  R9,0                                                             
         EX    R9,*+8                                                           
         B     *+10                                                             
         CLC   0(0,R6),=8X'FF'                                                  
         BE    DSQRD0              IGNORE IF FIELD IS ALL FF'S                  
         DROP  R3                                                               
*                                                                               
DSQRD1C  DS    0H                  REMOVE TASKID FROM RECOVERY RECORDS          
*&&DO                              WE MAY INSTALL                               
         USING DMGRFLD,R6                                                       
         L     R6,AFILE                                                         
         TM    DMGIND,DMGIRCV      TEST IF RECOVERY FILE                        
         BZ    DSQRD1D             NO                                           
         L     R6,DMCBW4                                                        
         USING RECVREC,R6                                                       
         CLI   RTASKID,X'FF'       LEAVE DELETED RECORDS AS DELETED             
         BE    DSQRD1D                                                          
         TM    XFLAG1,XFONLINE     ONLINE                                       
         BO    DSQRD1D             DON'T BOTHER IF ON-LINE                      
         LRL   RF,=V(SSB)                                                       
         USING SSBD,RF                                                          
         TM    SSOMTIND,SSORTSK#   PASS RTASKID TO APPLICATION                  
         BO    DSQRD1D             YES                                          
         MVI   RTASKID,0           MOST APPLICAITON DON'T NEED TASKID           
         DROP  R6,RF               RECVREC, SSBD                                
*&&                                                                             
DSQRD1D  MVC   DMCBW5+2(2),PARAM3+2                                             
         B     DATAMGRX                                                         
*                                                                               
DSQRD2   TM    PARAM3+1,X'08'      TEST NO REC FOUND                            
         BO    *+12                                                             
DSQRD3   OI    DMCBW3,X'40'        SET HARD ERROR                               
         B     DATAMGRX                                                         
         LTR   R0,R0               TEST PRIOR REC NOT FOUND                     
         BNZ   DSQRDX                                                           
         LA    R0,1                SET NFSW                                     
*                                                                               
DSQRD4   L     RE,PARAM4           NOT FOUND - TRY NEXT TRACK                   
         TM    DTFTYPE-DTFPHD(RE),DTFTBIGF+DTFTBIG                              
         BZ    DSQRD5                                                           
         BO    DSQRD4A                                                          
         L     R6,DMCBW3           20-BIT TRACK TTTTTBRR                        
         SR    RE,RE                                                            
         ICM   RE,7,0(R6)                                                       
         SRL   RE,4                                                             
         LA    RE,1(RE)            BUMP TO NEXT TRACK                           
         SLL   RE,4                                                             
         STCM  RE,7,0(R6)                                                       
         MVI   3(R6),1             SET RECORD 1                                 
         B     DSQRD1B                                                          
*                                                                               
DSQRD4A  L     R6,DMCBW3           22-BIT TRACK TTTTTXRR                        
         SR    RE,RE                                                            
         ICM   RE,7,0(R6)                                                       
         SRL   RE,2                                                             
         LA    RE,1(RE)            BUMP TO NEXT TRACK                           
         SLL   RE,2                                                             
         STCM  RE,7,0(R6)                                                       
         MVI   3(R6),1             SET RECORD 1                                 
         B     DSQRD1B                                                          
*                                                                               
DSQRD5   TM    DTFTYPE-DTFPHD(RE),DTFTBIG                                       
         BZ    DSQRD6                                                           
         L     R6,DMCBW3           18-BIT TRACK                                 
         SR    RE,RE                                                            
         ICM   RE,7,0(R6)                                                       
         SRL   RE,6                                                             
         LA    RE,1(RE)            BUMP TO NEXT TRACK                           
         SLL   RE,6                                                             
         LA    RE,1(RE)            SET RECORD 1                                 
         STCM  RE,7,0(R6)                                                       
         B     DSQRD1B                                                          
*                                                                               
DSQRD6   L     R6,DMCBW3           16-BIT TRACK NORMAL FILE                     
         SR    RE,RE                                                            
         ICM   RE,3,0(R6)          BUMP TO NEXT TRACK                           
         LA    RE,1(RE)                                                         
         SLL   RE,16                                                            
         ST    RE,0(R6)                                                         
         MVI   2(R6),1             SET RECORD 1                                 
         B     DSQRD1B                                                          
DSQRDX   MVI   DMCBW3,X'80'        SET EOF                                      
         B     DATAMGRX                                                         
         EJECT                                                                  
***********************************************************************         
* DMRSEQ - DIRECT ACCESS - BLOCKED FILES                              *         
***********************************************************************         
DMOD3DB  DS    0H                                                               
         TM    XFLAG1,XFONLINE     NO BLOCKED READ IF ONLINE                    
         BO    DMOD3DA                                                          
DMOD3DB1 BRAS  RE,DBSRD            BLOCKED SEQUENTIAL READ ROUTINE              
         B     DATAMGRX                                                         
         EJECT                                                                  
***********************************************************************         
* READ NEW SPLIT PAV FILES - MASTER DIRECTORY IS PAVDIR               *         
***********************************************************************         
*&&SPT                                                                          
DMOD3DI  DS    0H                                                               
DMOD4DI  DS    0H                                                               
         L     RF,DMCBW4           USER KEY IN PARAM 4                          
         USING PRKEY,RF                                                         
         MVC   DEMSTAT,PRRSTAT                                                  
         NI    DEMSTAT,X'3F'       ONLY ALLOWING 63 FILES MAX                   
         CLI   DEMSTAT,0                                                        
         JE    *+2                 DIE=NO STATUS FIELD                          
         DROP  RF                                                               
*                                                                               
         MVI   INTFILE,X'2E'                                                    
         MVI   EXTFILE,X'00'                                                    
*                                                                               
         CLI   DEMSTAT,1           PAVFIL (ORIGINAL FILE)                       
         BNE   *+8                                                              
         MVI   EXTFILE,X'2E'                                                    
*                                                                               
         CLI   DEMSTAT,2           PAVFILA                                      
         BNE   *+8                                                              
         MVI   EXTFILE,X'C5'                                                    
*                                                                               
         CLI   DEMSTAT,3           PAVFILB                                      
         BNE   *+8                                                              
         MVI   EXTFILE,X'C0'                                                    
*                                                                               
         CLI   DEMSTAT,4           PAVFILC                                      
         BNE   *+8                                                              
         MVI   EXTFILE,X'7E'                                                    
*                                                                               
         CLI   DEMSTAT,31          PAVFLTA TEST PAV FILE IS ALWAYS 31           
         BNE   *+8                                                              
         MVI   EXTFILE,X'55'                                                    
*                                                                               
         CLI   EXTFILE,X'00'                                                    
         JE    *+2                 DIE=ZERO FIL NUM                             
*                                                                               
         MVI   DMCBW7,5            VSAM FILESET NUMBER 5=PAVXXX                 
         MVI   DMCBW7+1,C'F'       VSAM MARK FILE                               
         BRAS  RE,DEMVSAM          CALL DEMO VSAM INTERFACE                     
         B     DMOD4DIX            VSAM PROCESSED                               
*                                  VSAM OFF, USE LEGACY CODE                    
         L     RF,DMCBW4           USER KEY IN PARAM 4                          
         BAS   RE,SETDUM                                                        
         BASR  RE,RF               SET DTF ADDRESS IN PARAM4                    
*                                                                               
         L     RF,=V(DMDANDX)                                                   
         BASR  RE,RF                                                            
*                                                                               
DMOD4DIX L     RF,DMCBW4           USER KEY IN PARAM 4                          
         USING PRKEY,RF                                                         
         OC    PRRSTAT,DEMSTAT                                                  
         B     DATAMGRX                                                         
         DROP  RF                                                               
*&&                                                                             
***********************************************************************         
* READ NEW SPLIT NTI FILES - NTIFIL N/O/P/Q/W/Y IN MASTER NTIDIR      *         
***********************************************************************         
*&&SPT                                                                          
DMOD339  DS    0H                  DMRSEQ - NTIFIL (SPLIT DEMO FILES)           
DMOD439  DS    0H                  DMRDHI - NTIFIL (SPLIT DEMO FILES)           
*                                                                               
         L     RF,DMCBW4           USER KEY IN PARAM 4                          
         USING PRKEY,RF                                                         
         MVC   DEMSTAT,PRRSTAT                                                  
         NI    DEMSTAT,X'3F'       ONLY ALLOWING 63 FILES MAX                   
         DROP  RF                                                               
*                                                                               
         MVI   INTFILE,X'39'                                                    
         SR    RE,RE                                                            
         ICM   RE,1,DEMSTAT                                                     
         JZ    *+2                 DIE=NO STATUS FIELD                          
         CLI   DEMSTAT,NTITABQ                                                  
         JH    *+2                 DIE=OUT OF RANGE                             
         LARL  R0,NTITAB-1                                                      
         AR    RE,R0                                                            
         CLI   0(RE),0                                                          
         JE    *+2                                                              
         MVC   EXTFILE,0(RE)                                                    
         B     DMOD439N                                                         
*                                                                               
DMOD439N MVI   DMCBW7,4            VSAM FILESET NUMBER 4=NTIXXX                 
         MVI   DMCBW7+1,C'F'       VSAM MARK FILE                               
         BRAS  RE,DEMVSAM          CALL DEMO VSAM INTERFACE                     
         B     DMOD439X            VSAM PROCESSED                               
*                                  VSAM OFF, USE LEGACY CODE                    
         L     RF,DMCBW4           USER KEY IN DMCBW4                           
         BRAS  RE,SETDUM                                                        
         BASR  RE,RF                                                            
*                                                                               
         L     RF,=V(DMDANDX)                                                   
         BASR  RE,RF                                                            
*                                                                               
DMOD439X L     RF,DMCBW4           USER KEY IN DMCBW4                           
         USING PRKEY,RF                                                         
         OC    PRRSTAT,DEMSTAT                                                  
         B     DATAMGRX                                                         
         DROP  RF                                                               
*&&                                                                             
***********************************************************************         
* DMRSEQ - DEMFIL - DEMFILN=X'26',DEMFILA=X'26',DEMFILR=X'31' INTFILE *         
* DMRDHI - DEMFIL   DEMFILN=X'26',DEMFILA=X'28',DEMFILR=X'31' EXTFILE *         
***********************************************************************         
*&&SPT                                                                          
DMOD326  DS    0H                  DMRSEQ - DEMFIL (SPLIT DEMO FILES)           
DMOD426  DS    0H                  DMRDHI - DEMFIL (SPLIT DEMO FILES)           
*                                                                               
         MVI   DMCBW7,2            VSAM FILESET NUMBER 2=DEMXXXN                
         MVI   DMCBW7+1,C'F'       VSAM MARK FILE                               
*                                                                               
         L     RF,DMCBW4           USER KEY IN PARAM 4                          
         USING DMKEY,RF                                                         
*                                                                               
         MVC   DEMSTAT,DMRSTAT                                                  
         NI    DEMSTAT,X'3F'       ONLY ALLOWING 63 FILES MAX                   
*                                                                               
         CLI   DMMEDIA,C'R'        RADIO TO DEMDIRR                             
         BNE   D426A                                                            
*                                                                               
         MVI   DMCBW7,3            VSAM FILESET NUMBER 3=DEMXXXR                
*                                                                               
         MVI   INTFILE,X'31'                                                    
         MVI   EXTFILE,X'31'       DEFAULT - DEMFILR                            
*                                                                               
         CLI   DEMSTAT,31          IF 31 - TEST FILE, DEMFLTR                   
         BNE   *+8                                                              
         MVI   EXTFILE,X'53'       DEFAULT - DEMFILR                            
*                                                                               
         B     D426VSAM                                                         
*                                                                               
D426A    CLI   DMSRC,C'N'          NSI TO DEMDIRN                               
         BNE   D426S                                                            
*                                                                               
         MVI   INTFILE,X'26'       DEMFILN                                      
*                                                                               
         SR    RE,RE                                                            
         ICM   RE,1,DEMSTAT                                                     
         JZ    *+2                 DIE=NO STATUS FIELD                          
         CLI   DEMSTAT,DEMTABQ                                                  
         JH    *+2                 DIE=OUT OF RANGE                             
         LARL  R0,DEMTAB-1                                                      
         AR    RE,R0                                                            
         CLI   0(R3),X'00'                                                      
         JE    *+2                                                              
         MVC   EXTFILE,0(RE)                                                    
         B     D426VSAM                                                         
*                                                                               
D426S    CLI   DMSRC,C'S'          SRC TO DEMDIRA                               
         BE    D426VSAM            IF 'S' - USE DEFAULT FOR DEMFILN(26)         
*                                                                               
         MVI   DMCBW7,1            VSAM FILESET NUMBER 1=DEMXXXA                
*                                                                               
         MVI   INTFILE,X'26'       DEMFILA                                      
         MVI   EXTFILE,X'00'                                                    
*                                                                               
         CLI   DEMSTAT,1           TEST STATUS                                  
         BNE   *+8                                                              
         MVI   EXTFILE,X'28'       DEMFILA                                      
*                                                                               
         CLI   DEMSTAT,2                                                        
         BNE   *+8                                                              
         MVI   EXTFILE,X'C2'       DEMFIL2                                      
*                                                                               
         CLI   DEMSTAT,3                                                        
         BNE   *+8                                                              
         MVI   EXTFILE,X'5F'       DEMFIL4                                      
*                                                                               
         CLI   DEMSTAT,4                                                        
         BNE   *+8                                                              
         MVI   EXTFILE,X'79'       DEMFILT                                      
*                                                                               
         CLI   DEMSTAT,31                                                       
         BNE   *+8                                                              
         MVI   EXTFILE,X'52'       DEMFLTA                                      
*                                                                               
         CLI   EXTFILE,0                                                        
         JE    *+2                                                              
*                                                                               
         DROP  RF                                                               
*                                                                               
D426VSAM BRAS  RE,DEMVSAM          CALL DEMO VSAM INTERFACE                     
         B     D426X1              VSAM PROCESSED                               
*                                  VSAM OFF, USE LEGACY CODE                    
D426X    L     RF,DMCBW4           USER KEY IN PARAM 4                          
         BAS   RE,SETDUM                                                        
         BASR  RE,RF                                                            
         L     RF,=V(DMDANDX)                                                   
         BASR  RE,RF                                                            
*                                                                               
D426X1   L     RF,DMCBW4           USER KEY IN PARAM 4                          
         USING DMKEY,RF                                                         
         OC    DMRSTAT,DEMSTAT                                                  
         B     DATAMGRX                                                         
         DROP  RF                                                               
         EJECT                                                                  
***********************************************************************         
* DMRDHI - PAVDIR - VSAM INTERFACE                                    *         
***********************************************************************         
DMOD42C  DS    0H                                                               
*&&UK*&& B     DMOD4IS                                                          
*&&US                                                                           
         MVI   DMCBW7,5            VSAM FILESET NUMBER 5=PAVXXX                 
         MVI   DMCBW7+1,C'D'       VSAM MARK DIRECTORY                          
         BRAS  RE,DEMVSAM          CALL DEMO VSAM INTERFACE                     
         B     DATAMGRX            VSAM PROCESSED                               
         B     DMOD4IS             VSAM OFF, USE LEGACY CODE                    
*&&                                                                             
         EJECT                                                                  
***********************************************************************         
* DMRDHI - DEMDIR - DEMDIRN=X'2D',DEMDIRA=X'2D',DEMDIRR=X'30' INTFILE *         
*                   DEMDIRN=X'2D',DEMDIRA=X'2F',DEMDIRR=X'30' EXTFILE *         
***********************************************************************         
DMOD42D  DS    0H                                                               
*&&UK*&& B     DMOD4IS                                                          
*&&US                                                                           
         MVI   DMCBW7+1,C'D'       VSAM MARK DIRECTORY                          
         L     RF,DMCBW3           USER KEY IN PARAM 3                          
         CLI   0(RF),DFUECODQ      FUSION UNIVERSE ESTIMATES TO DEMDIRR         
         BE    *+12                                                             
         CLI   1(RF),C'R'          RADIO TO DEMDIRR                             
         BNE   D42DA                                                            
         MVI   INTFILE,X'30'                                                    
         MVI   EXTFILE,X'30'                                                    
         MVI   DMCBW7,3            VSAM FILESET NUMBER 3=DEMXXXR                
         B     D42DVSAM            DEMFILR TO VSAM                              
*                                                                               
D42DA    MVI   DMCBW7,2            VSAM FILESET NUMBER 2=DEMXXXN                
         CLI   2(RF),C'N'          NSI TO DEMDIRN                               
         BE    D42DC                                                            
         CLI   2(RF),C'S'          SRC TO DEMDIRN                               
         BNE   D42DE                                                            
*                                                                               
D42DC    DS    0H                                                               
*&&DMSMF                                                                        
         BRAS  RE,DEMSMF           LOG THIS MAJOR KEY VIA SMF                   
*&&                                                                             
         B     D42DVSAM                                                         
*                                                                               
D42DE    DS    0H                                                               
         MVI   EXTFILE,X'2F'       ALL OTHERS TO DEMDIRA                        
         MVI   DMCBW7,1            VSAM FILESET NUMBER 1=DEMXXXA                
*                                                                               
D42DVSAM BRAS  RE,DEMVSAM          CALL DEMO VSAM INTERFACE                     
         B     DATAMGRX                                                         
         B     DMOD4IS             IF VSAM IS OFF                               
*&&                                                                             
*&&                                                                             
         EJECT                                                                  
***********************************************************************         
* DMRDHI - NTIDIR - VSAM INTERFACE                                    *         
***********************************************************************         
DMOD438  DS    0H                                                               
*&&UK*&& B     DMOD4IS                                                          
*&&US                                                                           
         MVI   DMCBW7,4            VSAM FILESET NUMBER 4=NTIXXX                 
         MVI   DMCBW7+1,C'D'       VSAM MARK DIRECTORY                          
         BRAS  RE,DEMVSAM          CALL DEMO VSAM INTERFACE                     
         B     DATAMGRX            VSAM PROCESSED                               
         B     DMOD4IS             VSAM OFF, USE LEGACY CODE                    
*&&                                                                             
         EJECT                                                                  
***********************************************************************         
* DMRDHI - INDEX SEQUENTIAL                                           *         
***********************************************************************         
DMOD4IS  DS    0H                                                               
         XR    R0,R0                                                            
*&&US*&& BAS   RE,TSTTRF                                                        
*                                                                               
         L     R7,DMCBW3                                                        
         MVC   OLDKEY,0(R7)        SAVE KEY                                     
         L     RF,=V(RKEY)                                                      
         TM    DMCBW1,X'24'        TEST FOR READ FLUSH                          
         BNO   *+8                                                              
         L     RF,=V(RKEYFLS)                                                   
         BAS   RE,SETPR                                                         
         LA    R7,OLDKEY                                                        
         ST    R7,PARAM5                                                        
         TM    DMCBW1,X'10'        TEST FULL TRACK READ                         
         BZ    *+14                                                             
         MVC   PARAM6,DMCBW5                                                    
         MVI   PARAM6,X'FF'                                                     
         BASR  RE,RF                                                            
         TM    DMCBW3,X'C0'                                                     
         BNZ   DATAMGRX                                                         
*                                                                               
DMOD4AB  NI    DMCBW3,X'EF'        RESET NOT FOUND                              
         L     RE,PARAM4                                                        
         USING ISDTF,RE                                                         
         LH    R7,ISKEYLN                                                       
         DROP  RE                                                               
         L     R6,DMCBW4           A(REC)                                       
         BAS   R5,DMGRCTLI                                                      
         TM    DMCBW3,X'10'                                                     
         BNZ   DMOD3IS             IF NOT FOUND, READ SEQ                       
*&&REP                                                                          
         OC    REPWRK(4),REPWRK                                                 
         BNZ   DMOD481A                                                         
*&&                                                                             
         B     DATAMGRX                                                         
                                                                                
DMOD4AC  DS    0H                  DMRDHI - ACCOUNT                             
         BAS   RE,ACCOEMU          TEST IF ACCOUNT FILE EMULATION               
         BE    DMOD4IS                                                          
         BASR  RE,RF               YES GO TO EMULATION MODULE                   
         B     DATAMGRX                                                         
*                                                                               
DMOD4CT  L     R0,DMCBW3           DMRDHI - CTFILE                              
         BAS   RE,CTCHKPE          CHECK IF A PERSON RECORD                     
         BAS   RE,CTCHKRD                                                       
         BAS   RE,CTCOUNT                                                       
         CLI   DMCBW3,X'FF'        EXIT IF RECORD TYPE BUFFERED                 
         BNE   DATAMGRX                                                         
         MVI   DMCBW3,0                                                         
         B     DMOD4IS                                                          
         EJECT                                                                  
***********************************************************************         
* THE FOLLOWING CODE COUNTS I/O CALLS TO CTFILE BY PROGRAM ONLINE     *         
* NTRY: R0=DMCBW3                                                     *         
***********************************************************************         
CTCOUNT  ST    RE,SAVERE                                                        
         ICM   RF,15,XUTLNTRY      RF=A(ONLINE UTL ENTRY)                       
         BZ    CTCOUNTX                                                         
*                                                                               
         L     RE,=V(SSB)          CHECK TAPRG IS DISPLACEMENT                  
         TM    SSBSTAT4-SSBD(RE),SSBPGDSP                                       
         BNZ   CTCOUNT1                                                         
*                                                                               
         ICM   RE,7,TASVC-UTLD(RF) GET ADDR OF PGMLST ENTRY IF S/R              
         BNZ   CTCOUNT5                                                         
         ICM   RE,7,TAPRG-UTLD(RF) GET ADDR OF PGMLST ENTRY                     
         BNZ   CTCOUNT5                                                         
         B     CTCOUNT4            ELSE SET CONNECT                             
*                                                                               
CTCOUNT1 ICM   RE,7,TASVC-UTLD(RF) GET ADDR OF PGMLST ENTRY IF S/R              
         BNZ   CTCOUNT5            DO NOT HAVE TO ALTER IF S/R                  
         ICM   RE,7,TAPRG-UTLD(RF) GET ADDR OF PGMLST ENTRY                     
         BNZ   CTCOUNT2                                                         
         B     CTCOUNT4            ELSE SET CONNECT                             
*                                                                               
CTCOUNT2 NILH  GRF,X'00FF'                                                      
         ICM   RF,7,TARSYS-UTLD(RF)                                             
         BZ    CTCOUNTX                                                         
         ICM   RF,15,SEPGMS-SELISTD(RF)                                         
         AR    RE,RF                                                            
         B     CTCOUNT5                                                         
*                                                                               
CTCOUNT4 ICM   RE,15,=V(SRPGMCT)   GET ADDR OF $CT PGMLST ENTRY                 
         BZ    CTCOUNTX                                                         
         USING PGMLSTD,RE                                                       
*                                                                               
CTCOUNT5 LR    RF,R0               POINT TO KEYARG                              
*NOP*    CLI   0(RF),C'U'                                                       
*NOP*    BNE   CTCOUNT6                                                         
         L     RF,PGMCNT1                                                       
         AHI   RF,1                                                             
         ST    RF,PGMCNT1                                                       
         B     CTCOUNTX                                                         
*                                                                               
CTCOUNT6 EQU   *                   ONLY ONE COUNTER AVAILABLE                   
*                                                                               
CTCOUNTX L     RE,SAVERE                                                        
         BR    RE                                                               
                                                                                
*&&REP                                                                          
DMOD481  BRAS  RE,REPMOD1          DMRDHI - REPDIR                              
         B     DMOD4IS                                                          
*                                                                               
DMOD481A BRAS  RE,REPMOD2          SPECIAL INTEREP PROCESSING                   
         CLI   REPRC,X'01'         TEST IF NEED TO READ SEQ                     
         BE    DMOD3IS                                                          
         B     DATAMGRX                                                         
*&&                                                                             
         EJECT                                                                  
***********************************************************************         
* CHECK IF TRYING TO READ A PERSON AUTH RECORD FOR A DDS PERSON       *         
* SWITCH THE AGENCY TO THE CORRECT DDS SECURITY AGENCY                *         
* TO INHIBIT THIS SWITCH SET DMCBW1(1)=X'22'                          *         
***********************************************************************         
CTCHKPE  ST    RE,SAVERE           POINT TO CALLERS KEY                         
         TM    DMCBW1,X'22'        TEST SPECIAL BITS TO INHIBIT THIS            
         BO    CTCHKPEX                                                         
         L     R4,DMCBW3           POINT TO CALLERS KEY                         
         CLI   0(R4),C'0'          RECORD TYPE                                  
         BNE   CTCHKPEX                                                         
         OC    3(20,R4),3(R4)      MUST BE ZEROS IF READING BY NUMBER           
         BNZ   CTCHKPEX                                                         
         CLC   23(2,R4),=H'4096'   DDS PERSON NUMBERS ARE LOW VALUES            
         BNL   CTCHKPEX                                                         
         OC    23(2,R4),23(R4)     PERSON ZERO IS NOT DEFINED                   
         BZ    CTCHKPEX                                                         
*&&UK*&& CLC   1(2,R4),=C'#E'                                                   
*&&US*&& CLC   1(2,R4),=C'#N'      TEST DDS SECURITY AGENCY                     
         BE    CTCHKPEX                                                         
*&&UK*&& MVC   1(2,R4),=C'#E'                                                   
*&&US*&& MVC   1(2,R4),=C'#N'      SWITCH TO DDS SECURITY AGENCY                
         ICM   RE,15,XUTLNTRY                                                   
         BZ    CTCHKPE1                                                         
         TM    TSTAT1-UTLD(RE),TSTATDDS                                         
         BNZ   CTCHKPEX                                                         
CTCHKPE1 MVC   23(2,R4),=H'1'      SET TO PERSON #1 ON DDS FILE                 
CTCHKPEX L     RE,SAVERE                                                        
         BR    RE                                                               
         EJECT                                                                  
***********************************************************************         
* FOR CONTROL FILE I/O, THIS ROUTINE CHECKS TO SEE IF                 *         
* RECORD TYPE IS MAINTAINED IN LOOK-UP BUFFER, AND IF IT IS,          *         
* RETURNS THE RECORD TO THE USER.                                     *         
* ON ENTRY R0 POINTS TO THE KEY OR RECORD AS REQUIRED                 *         
* ON EXIT, DMCBW3=X'FF' IF BUFFERING DID NOT APPLY TO THIS            *         
* RECTYPE, OR IF BUFFERING NOT ACTIVE                                 *         
* ELSE, DMCBW3 CONTAINS 0 FOR SUCCESS, X'10' FOR NOT FOUND            *         
***********************************************************************         
CTCHKWRT ST    RE,SAVERE                                                        
         L     R4,DMCBW4                                                        
         B     CTCHKALL                                                         
*                                                                               
CTCHKADD ST    RE,SAVERE                                                        
         L     R4,DMCBW4                                                        
         B     CTCHKALL                                                         
*                                                                               
CTCHKRD  ST    RE,SAVERE                                                        
         TM    DMCBW1,X'80'        TEST READ FOR UPDATE (ON READ)               
         BO    CTCHKNOP            YES MUST DO I/O SO SKIP BUFFER               
         LRL   RE,=V(SSB)          GET SSB                                      
         TM    XFLAG1,X'80'        ONLINE?                                      
         JO    CTCHKRD2            YES, SKIP                                    
         TM    SSOCTBOV-SSOOFF(RE),SSOCTBNQ CTBUFF LOCALLY DISABLED             
         JO    CTCHKNOP            YES SKIP BUFFER                              
         TM    SSOCTBOV-SSOOFF(RE),SSOCTBYQ CTBUFF LOCALLY ENABLED              
         JO    CTCHKRD4            YES, USE BUFFER IF ACTIVE                    
CTCHKRD2 LAM   ARE,ARE,SSBTBLET-SSBD(RE) TBLET SAME IN ON/OFFLINE SSBS          
         XR    RE,RE               0 = DISP TO TABSADDR IN TABS DSPC            
         LR    RF,RE               SAVE 0 TO CLEAR ARE                          
         SAC   512                                                              
         L     RE,TABSCOMM-FATABSD(,RE) CHECK GLOBAL CTBUFF SETTING             
         TM    TCOCTBFL-TCOMMOND(RE),TCOCTBXQ CTBUFF SEARCH NOOPED?             
         SAC   0                                                                
         SAR   ARE,RF              CLEAR ARE                                    
         JO    CTCHKNOP            YES, SKIP BUFFER                             
CTCHKRD4 L     R4,DMCBW3           POINT TO KEYARG                              
*                                                                               
CTCHKALL CLI   DMCBW3,0            TEST ERROR ON FILE I/O                       
         BNE   CTCHKNOP            YES DO NOT UPDATE BUFFER                     
*&&UK*&& NOP   CTCHKNOP            *NOP* B CTCHKNOP WHICH INHIBITS              
         GOTO1 =A(CTCHKBUF)        GET FROM BUFFER IF AVAILABLE                 
         SAFE  CLEAR=Y                                                          
         SAC   0                                                                
         B     *+8                                                              
CTCHKNOP MVI   DMCBW3,X'FF'        SET FLAG BUFFER NOT ACTIVE                   
*                                                                               
         L     RE,SAVERE                                                        
         BR    RE                                                               
         EJECT                                                                  
***********************************************************************         
* DMDEL - SPTFILE                                                     *         
* DMCBW3=SPTDIR KEY, DMCBW4=SPTFILE RECORD (MUST HAVE READ/UPDATE)    *         
***********************************************************************         
*&&SPT                                                                          
DMOD521  DS    0H                                                               
         MVC   PARAM1(20),DMCBW1   READ FOR UPDATE SRTDIR RECORD                
         LA    R7,DMWORK                                                        
         L     R0,=A(DSPTDIR)                                                   
         GOTOR DATAMGR,PARAM1,(X'80',DMREAD),(R0),,(R7)                         
         OC    DMCBW3(1),PARAM3    PASS ERROR NOTICE                            
         TM    8(R1),X'FF'                                                      
         BNZ   DATAMGRX                                                         
*                                                                               
         OI    13(R7),X'80'        TAG REC DELETED                              
         GOTOR DATAMGR,(R1),DMWRT                                               
         OC    DMCBW3(1),PARAM3    PASS ERROR NOTICE                            
         TM    DMCBW3,X'50'                                                     
         BNZ   DATAMGRX                                                         
*                                  SET PARAMETERS TO WRITE                      
         MVC   PARAM1(20),DMCBW1                                                
         LA    RE,DMWRT                                                         
         ST    RE,PARAM1                                                        
         LA    RE,14(R7)           SET A(DISK ADDRESS)                          
         ST    RE,PARAM3                                                        
         L     RE,PARAM4           POINT TO RECORD                              
         OI    15(RE),X'80'        TAG RECORD DELETED                           
         BRAS  RE,DATAMGR                                                       
         OC    DMCBW3(1),PARAM3    PASS ERROR NOTICE                            
         B     DATAMGRX                                                         
*&&                                                                             
         EJECT                                                                  
***********************************************************************         
* DMDEL - TRFFILE                                                     *         
* DMCBW3=TRFDIR KEY, DMCBW4=TRFFILE RECORD (MUST HAVE READ/UPDATE)    *         
***********************************************************************         
*&&TRF                                                                          
DMOD532  DS    0H                                                               
         MVC   PARAM1(20),DMCBW1   READ FOR UPDATE TRFDIR RECORD                
         LA    R7,DMWORK                                                        
         L     R0,=A(DTRFDIR)                                                   
         GOTOR DATAMGR,PARAM1,(X'80',DMREAD),(R0),,(R7)                         
         OC    DMCBW3(1),PARAM3    PASS ERROR NOTICE                            
         TM    8(R1),X'FF'                                                      
         BNZ   DATAMGRX                                                         
*                                                                               
         OI    13(R7),X'80'        TAG REC DELETED                              
         GOTOR DATAMGR,(R1),DMWRT                                               
         OC    DMCBW3(1),PARAM3    PASS ERROR NOTICE                            
         TM    DMCBW3,X'50'                                                     
         BNZ   DATAMGRX                                                         
*                                  SET PARAMETERS TO WRITE                      
         MVC   PARAM1(20),DMCBW1                                                
         LA    RE,DMWRT                                                         
         ST    RE,PARAM1                                                        
         LA    RE,14(R7)           SET A(DISK ADDRESS)                          
         ST    RE,PARAM3                                                        
         L     RE,PARAM4           POINT TO RECORD                              
         OI    15(RE),X'80'        TAG RECORD DELETED                           
         BRAS  RE,DATAMGR                                                       
         OC    DMCBW3(1),PARAM3    PASS ERROR NOTICE                            
         B     DATAMGRX                                                         
*&&                                                                             
         EJECT                                                                  
***********************************************************************         
* DMRDIR - DIRECT ACCESS                                              *         
***********************************************************************         
DMOD6DA  DS    0H                                                               
         CLI   INTFILE,X'66'       TEST ACCDAY FILE                             
         BNE   *+12                                                             
         L     RE,DMCBW3                                                        
         NI    0(RE),X'7F'         TURN OFF DISK ADDR HIGH ORDER BIT            
*                                                                               
         L     RF,=V(READ)                                                      
         BAS   RE,SETPR            SET PARAM LIST                               
         BASR  RE,RF                                                            
         MVC   DMCBW5+2(2),10(R1)  SET LENGTH                                   
         B     DATAMGRX                                                         
                                                                                
DMOD6DB  DS    0H                                                               
*                                                                               
         L     RF,=V(READ)                                                      
         TM    DMCBW1,X'24'        TEST FOR READ FLUSH                          
         BNO   *+8                                                              
         L     RF,=V(READFLS)                                                   
         BAS   RE,SETPR            SET PARAM LIST                               
*                                                                               
*&&DO                                                                           
* FOLLOWING INCOMPATIBLE WITH 20 BIT RECOVERY FILES. SERVES NO PURPOSE          
* BUT PREVENTS PFM READING WHOLE 20 BIT BLOCK WHEN REQUIRED                     
         TM    XFLAG1,X'80'        NO BLOCKED READ IF ONLINE                    
         BZ    DMOD6DB1                                                         
         L     RE,PARAM5                                                        
         MVI   3(RE),0                                                          
*&&                                                                             
*                                                                               
DMOD6DB1 BASR  RE,RF                                                            
         MVC   DMCBW5+2(2),10(R1)  SET LENGTH                                   
         B     DATAMGRX                                                         
         EJECT                                                                  
***********************************************************************         
* DMWRT - INDEX SEQUENTIAL                                            *         
***********************************************************************         
DMOD8IS  DS    0H                                                               
         LA    R0,1                UPDATE TYPE ACTION                           
*&&US*&& BAS   RE,TSTTRF                                                        
         LA    R7,DMWORK                                                        
         B     D8IS                                                             
*                                                                               
TYPE801  LA    R1,M801                                                          
         B     TYPE                                                             
M801     DC    V(WCTYPE)                                                        
         DC    A(D801MSG)                                                       
         DC    AL4(L'D801MSG)                                                   
D801MSG  DC    CL28'DMGR - USER CHANGING KEY'                                   
*                                                                               
D8IS     OI    DMCBW1,X'80'        READ FOR UPDATE THE COPY RECORD              
         L     RF,=V(RKEY)                                                      
         BAS   RE,SETPR                                                         
         ST    R7,PARAM2                                                        
         MVC   PARAM5,DMCBW4                                                    
         BASR  RE,RF                                                            
         OC    PARAM3(2),PARAM3                                                 
         BNZ   DATAMGRX                                                         
*                                                                               
         L     R4,PARAM4           MAKE SURE COPY EXISTS                        
         USING ISDTF,R4                                                         
         LH    RE,ISKEYLN1                                                      
         L     R3,DMCBW4                                                        
         EX    RE,D8ISA            KEY MUST EQUAL COPY                          
         BE    D8ISB                                                            
         B     TYPE801             DIE IF USER CHANGING KEYS                    
D8ISA    CLC   0(0,R3),0(R7)                                                    
*                                                                               
D8ISB    MVC   PARAM3(1),EXTFILE   SET COPY IN RECOVERY FILE                    
         MVI   PARAM3+1,1                                                       
         MVC   PARAM3+2(2),ISRECLN                                              
         BRAS  R5,DMGRRECV                                                      
*                                                                               
         MVC   PARAM2(4),DMCBW4    SET CHANGE IN RECOVERY FILE                  
         MVC   PARAM3(1),EXTFILE                                                
         MVI   PARAM3+1,2                                                       
         BRAS  R5,DMGRRECV                                                      
*                                                                               
         MVC   PARAM1,=V(WKEY)     WRITE RECORD TO IS FILE                      
         L     RF,=V(DMOD000)                                                   
         BASR  RE,RF                                                            
         B     DATAMGRX                                                         
         DROP  R4                                                               
         EJECT                                                                  
***********************************************************************         
* DMWRT - VARIABLE LENGTH INDEX SEQUENTIAL                            *         
***********************************************************************         
DMOD8IV  BAS   RE,D8IV                                                          
         B     DATAMGRX                                                         
*                                                                               
D8IV     NTRL  WORK=(R7,532)       MAX REC SIZE=4256 BYTES                      
         LA    R7,24(R7)                                                        
         OI    DMCBW1,X'80'        READ FOR UPDATE THE COPY RECORD              
         L     RF,=V(RKEY)                                                      
         BAS   RE,SETPR                                                         
         ST    R7,PARAM2                                                        
         MVC   PARAM5,DMCBW4                                                    
         BASR  RE,RF                                                            
         OC    PARAM3(2),PARAM3                                                 
         BNZ   D8IVX                                                            
*                                                                               
         L     R4,PARAM4           MAKE SURE COPY EXISTS                        
         USING ISDTF,R4                                                         
         LH    RE,ISKEYLN1                                                      
         L     R3,DMCBW4                                                        
         EX    RE,D8IVA            KEY MUST EQUAL COPY                          
         BE    D8IVB                                                            
         B     TYPE801             DIE IF USER CHANGING KEYS                    
D8IVA    CLC   0(0,R3),0(R7)                                                    
*                                                                               
D8IVB    MVC   PARAM3(1),EXTFILE   SET COPY IN RECOVERY FILE                    
         MVI   PARAM3+1,1                                                       
         MVC   PARAM3+2(2),ISRECLN                                              
         BRAS  R5,DMGRREC1                                                      
*                                                                               
         L     R6,DMCBW4           SET CHANGE IN RECOVERY FILE                  
         LA    R6,0(R6)                                                         
         ST    R6,PARAM2                                                        
         MVC   PARAM3(1),EXTFILE                                                
         MVI   PARAM3+1,2                                                       
         AH    R6,ISKEYLN                                                       
         MVC   PARAM3+2(2),0(R6)   SET RECORD LENGTH FROM RECORD                
         BRAS  R5,DMGRREC1                                                      
*                                                                               
         MVC   PARAM1,=V(WKEY)     WRITE RECORD TO IS FILE                      
         L     RF,=V(DMOD000)                                                   
         BASR  RE,RF                                                            
         DROP  R4                                                               
*                                                                               
D8IVX    XIT1                                                                   
                                                                                
DMOD8AC  DS    0H                  DMWRT - ACCOUNT                              
         BAS   RE,ACCOEMU          TEST IF ACCOUNT FILE EMULATION               
         BNE   *+12                                                             
         BAS   RE,ACCOLEN          NO CHECK ACCOUNT FILE RECORD LEN             
         B     DMOD8IV                                                          
         BASR  RE,RF               YES GO TO EMULATION MODULE                   
         B     DATAMGRX                                                         
                                                                                
DMOD8CT  L     R0,DMCBW4                                                        
         BAS   RE,D8IV                                                          
         BAS   RE,CTCHKWRT                                                      
         CLI   DMCBW3,X'FF'        TEST NO BUFFERING ON RETURN                  
         BNE   *+8                                                              
         MVI   DMCBW3,0            RESET                                        
         B     DATAMGRX                                                         
         EJECT                                                                  
***********************************************************************         
* DMWRT - SPTFILE                                                     *         
***********************************************************************         
*&&SPT                                                                          
DMOD821  DS    0H                                                               
         BAS   RE,SETPRC           BUILD PARAM LIST                             
         L     RF,=V(DMDALINK)                                                  
         BASR  RE,RF                                                            
         B     DATAMGRX                                                         
*&&                                                                             
         EJECT                                                                  
***********************************************************************         
* DMWRT - ACCDAY                                                      *         
***********************************************************************         
*&&ACC                                                                          
DMOD866  DS    0H                                                               
         L     RE,DMCBW3                                                        
         NI    0(RE),X'7F'         TURN OFF DISK ADDR HIGH ORDER BIT            
*                                                                               
         L     RF,=V(WRITE)        WRITE RECORD                                 
         BAS   RE,SETPR                                                         
         L     RE,DMCBW4                                                        
         MVC   PARAM3+2(2),0(RE)                                                
         BASR  RE,RF                                                            
         OC    PARAM3(2),PARAM3    TEST FOR ERRORS                              
         BZ    *+12                                                             
         OI    DMCBW3,X'40'                                                     
         B     DATAMGRX                                                         
*                                                                               
         BAS   RE,SETPRC           SET RECOVERY PARAMS                          
         XC    DMWORKH,DMWORKH     SET NO RECOVERY COPY                         
         MVC   PARAM3(1),EXTFILE                                                
         MVI   PARAM3+1,2                                                       
         L     RE,DMCBW4                                                        
         MVC   PARAM3+2(2),0(RE)                                                
         BRAS  R5,DMGRRECV                                                      
         B     DATAMGRX                                                         
*&&                                                                             
         EJECT                                                                  
***********************************************************************         
* DMWRT - STATS                                                       *         
***********************************************************************         
*&&SRV                                                                          
DMOD8F7  DS    0H                                                               
         L     RF,=V(WRITE)                                                     
         BAS   RE,SETPR                                                         
         MVC   PARAM3+2(2),=H'2416'                                             
         BASR  RE,RF                                                            
         OC    PARAM3(2),PARAM3    TEST FOR ERRORS                              
         BZ    *+8                                                              
         OI    DMCBW3,X'40'                                                     
         B     DATAMGRX                                                         
*&&                                                                             
         EJECT                                                                  
***********************************************************************         
* DMWRT - EDICT FILES - EDCTA/EDCTR                                   *         
***********************************************************************         
*&&SRV                                                                          
DMOD8ED  DS    0H                                                               
         L     RF,=V(WRITE)                                                     
         BAS   RE,SETPR                                                         
         MVC   PARAM3+2(2),DMCBW6+2                                             
         BASR  RE,RF                                                            
         OC    PARAM3(2),PARAM3    TEST FOR ERRORS                              
         BZ    *+8                                                              
         OI    DMCBW3,X'40'                                                     
         B     DATAMGRX                                                         
*&&                                                                             
         EJECT                                                                  
***********************************************************************         
* DMWRT - KWXFILE                                                     *         
***********************************************************************         
*&&SRV                                                                          
DMOD8FB  DS    0H                                                               
         L     RF,=V(WRITE)                                                     
         BAS   RE,SETPR                                                         
         L     RE,DMCBW4                                                        
         MVC   PARAM3+2(2),0(RE)                                                
         BASR  RE,RF                                                            
         OC    PARAM3(2),PARAM3    TEST FOR ERRORS                              
         BZ    *+8                                                              
         OI    DMCBW3,X'40'                                                     
         B     DATAMGRX                                                         
*&&                                                                             
         EJECT                                                                  
***********************************************************************         
* REQUEST FILE ACTIONS                                                *         
***********************************************************************         
DMOD1RQ  BAS   RE,REQSDMCB         DMADD - REQUEST                              
         BRAS  RE,D1RQ                                                          
         BAS   RE,REQRDMCB         RESTORE DMCB FOR OLD/NEW STYLE HDRS          
         B     DATAMGRX                                                         
                                                                                
DMOD3RQ  BAS   RE,REQSDMCB         DMRSEQ - REQUEST                             
         BRAS  RE,D3RQ                                                          
         BAS   RE,REQRDMCB         RESTORE DMCB FOR OLD/NEW STYLE HDRS          
         B     DATAMGRX                                                         
                                                                                
DMOD6RQ  BAS   RE,REQSDMCB         DMRDIR - REQUEST                             
         BRAS  RE,D6RQ                                                          
         BAS   RE,REQRDMCB         RESTORE DMCB FOR OLD/NEW STYLE HDRS          
         B     DATAMGRX                                                         
                                                                                
DMOD8RQ  BAS   RE,REQSDMCB         DMWRT - REQUEST                              
         BRAS  RE,D8RQ                                                          
         BAS   RE,REQRDMCB         RESTORE DMCB FOR OLD/NEW STYLE HDRS          
         B     DATAMGRX                                                         
                                                                                
REQSDMCB TM    DMCBW1,X'20'        TEST IF PROVIDING WHOLE RECORD               
         BOR   RE                                                               
         ST    RE,SAVERE                                                        
         L     RE,DMCBW4           NO BACK UP FOR NEW HEADER                    
         LA    RE,0(RE)                                                         
         AHI   RE,-54                                                           
         ST    RE,DMCBW4           FIX CALLERS DMCB                             
         MVC   SVDATA(54),0(RE)    AND SAVE HIS DATA                            
         XC    0(54,RE),0(RE)                                                   
         L     RE,SAVERE                                                        
         BR    RE                                                               
                                                                                
REQRDMCB TM    DMCBW1,X'20'        TEST IF CALLER PASSED WHOLE RECORD           
         BOR   RE                                                               
         ST    RE,SAVERE                                                        
         L     RE,DMCBW4           NO RESTORE HIS DMCB AND DATA                 
         MVC   0(54,RE),SVDATA                                                  
         LA    RE,0(RE)                                                         
         AHI   RE,54                                                            
         ST    RE,DMCBW4                                                        
         L     RE,SAVERE                                                        
         BR    RE                                                               
         EJECT                                                                  
***********************************************************************         
* GETREC/PUTREC/ADDREC - DIRECT ACCESS FILES                          *         
***********************************************************************         
DMODBDA  DS    0H                                                               
         SR    R7,R7               R7=0 FOR GENERAL DA FILE                     
         B     DBDA1                                                            
*                                                                               
DMODB21  EQU   *                                                                
         XR    R0,R0               NON-UPDATE ACTION                            
*&&US*&& BAS   RE,TSTTRF                                                        
         LA    R7,13               SPTFIL                                       
         B     DBDA1                                                            
DMODB2A  LA    R7,20               UNTFIL                                       
         B     DBDA1                                                            
DMODB42  LA    R7,25               PRTFILE                                      
         B     DBDA1                                                            
DMODB43  EQU   DMODB42             PUBFILE                                      
*                                                                               
DMODB82  LA    R7,27               REPFILE                                      
         B     DBDA1                                                            
DMODB88  LA    R7,31               ROIFIL                                       
         B     DBDA1                                                            
*                                                                               
DBDA1    BAS   RE,SETPRC           BUILD PARAM LIST TO SET FILE NUMBER          
         L     RF,=V(DMDALINK)                                                  
         BASR  RE,RF                                                            
         CLI   DMCBW3,0                                                         
         BNE   DATAMGRX                                                         
         LTR   R7,R7               CONTROL STATUS TEST                          
         BZ    DATAMGRX                                                         
         L     R6,DMCBW4                                                        
         BAS   R5,DMGRCTLD                                                      
         B     DATAMGRX                                                         
                                                                                
DMODDDA  DS    0H                  ADDREC                                       
DMODCDA  DS    0H                  PUTREC                                       
         LA    R0,1                UPDATE TYPE ACTION                           
*&&US*&& BAS   RE,TSTTRF                                                        
*                                                                               
         BAS   RE,SETPRC           BUILD PARAM LIST TO SET FILE NUMBER          
         L     RF,=V(DMDALINK)                                                  
         BASR  RE,RF                                                            
         CLI   DMCBW3,0                                                         
         BNE   DATAMGRX                                                         
*                                                                               
         L     RF,=V(DMDAPTRS)     ADD DIRECTORY POINTERS                       
         BASR  RE,RF                                                            
         B     DATAMGRX                                                         
                                                                                
DMODGDA  DS    0H                  ADFREC - FILE REC ONLY                       
*                                                                               
         BAS   RE,SETPRC           BUILD PARAM LIST TO SET FILE NUMBER          
         L     RF,=V(DMDALINK)                                                  
         BASR  RE,RF                                                            
         B     DATAMGRX                                                         
         EJECT                                                                  
***********************************************************************         
* READ AND WRITE TEMPSTR PAGES - PAGE NUM SAVED IN SVPAGE             *         
***********************************************************************         
*&&SRV                                                                          
DMOD2F2  DS    0H                  DMREAD - COMMAND TO READ TWA                 
DMOD6F2  DS    0H                  DMRDIR - COMMAND TO READ OLD TWA             
DMOD8F2  BRAS  RE,DMODF2           DMWRT  - COMMAND TO WRITE TWA                
         B     DATAMGRX                                                         
                                                                                
***********************************************************************         
* RESERVE/RELEASE AND READ/WRITE TEMPEST PAGES                        *         
***********************************************************************         
DMOD2FE  DS    0H                  DMREAD - COMMAND TO READ TEMPEST             
DMOD8FE  DS    0H                  DMWRT  - COMMAND TO WRITE TEMPEST            
DMODEFE  DS    0H                  DMRSRV - RESERVE TEMPEST PAGES               
DMODFFE  DS    0H                  DMRLSE - RELEASE TEMPEST PAGES               
         BRAS  RE,DMODFE                                                        
         B     DATAMGRX                                                         
*&&                                                                             
         EJECT                                                                  
***********************************************************************         
* WKFILE ACTIONS                                                      *         
***********************************************************************         
DMOD2WK  DS    0H                  DMREAD - WORK FILE                           
         L     RF,=V(READ)                                                      
         BAS   RE,SETPR                                                         
         BASR  RE,RF                                                            
         B     DATAMGRX                                                         
                                                                                
DMOD8WK  DS    0H                  DMWRT - WORK FILE                            
         BAS   RE,SETDUM                                                        
         BASR  RE,RF                                                            
         L     RE,PARAM4           GET DTF ADDRESS                              
         MVC   PARAM3+2(2),52(RE)  SET RECLEN FROM DTF                          
         NI    PARAM3+2,X'7F'                                                   
         XC    PARAM4+1(3),PARAM4+1                                             
         L     RE,=V(WRITE)                                                     
         ST    RE,PARAM1                                                        
         BASR  RE,RF                                                            
         B     DATAMGRX                                                         
*                                                                               
DMOD9WK  DS    0H                  XXXXXX - WORK FILE                           
         BAS   RE,SETDUM                                                        
         BASR  RE,RF               SET DTF ADDRESS IN PARAM4                    
         L     RF,=V(DMWRKR)                                                    
         BASR  RE,RF                                                            
         B     DATAMGRX                                                         
         EJECT                                                                  
***********************************************************************         
* NEW WORKER FILE ACTIONS                                             *         
***********************************************************************         
DMOD2WF  BRAS  RE,D2WF             DMREAD - WRKF                                
         B     DATAMGRX                                                         
                                                                                
DMOD8WF  BRAS  RE,D8WF             DMWRT  - WRKF                                
         B     DATAMGRX                                                         
                                                                                
DMOD9WF  BAS   RE,SETDUM           XXXXXX - WRKF                                
         BASR  RE,RF               SET DTF ADDRESS IN PARAM4                    
         ICM   RE,15,=V(SSB)                                                    
         JZ    *+2                 DIE=NO SSB                                   
         USING SSBD,RE                                                          
*                                                                               
DMOD9WF0 TM    XFLAG1,XFONLINE     TEST ONLINE                                  
         BZ    DMOD9WF2                                                         
DMOD9WF1 XR    RF,RF               ONLINE - TEST INDEX CALL                     
         ICM   RF,7,DMCBW1+1                                                    
         CLC   0(3,RF),=C'IND'                                                  
         BNE   DMOD9WF3                                                         
         ICM   RF,15,XTCBNTRY      CLEAR LOGICAL IO COUNTER                     
         BZ    DMOD9WF3                                                         
         XC    TCBLIOC-TCBD(L'TCBLIOC,RF),TCBLIOC-TCBD(RF)                      
         B     DMOD9WF3                                                         
*                                                                               
DMOD9WF2 CLI   SSOXTND,X'FF'       OFFLINE                                      
         JNE   *+2                 DIE=NO EXTENDED SSB                          
DMOD9WF3 ICM   RF,15,=V(DMWRKFSH)                                               
         JZ    *+2                 DIE=NO DMWRWFSH FOR SHARED MEMORY            
         BASR  RE,RF                                                            
         B     DATAMGRX                                                         
*                                                                               
DMOD9WF4 ICM   RF,15,=V(DMWRKF)                                                 
         JZ    *+2                 DIE=NO DMWRKF                                
         BASR  RE,RF                                                            
         B     DATAMGRX                                                         
*                                                                               
         DROP  RE                                                               
         EJECT                                                                  
***********************************************************************         
* EZ WORKER FILE (20-BIT ONLY) BIGGER KEYS                            *         
***********************************************************************         
*&&US                                                                           
DMOD2WZ  BRAS  RE,D2WZ             DMREAD - WRKZ                                
         B     DATAMGRX                                                         
                                                                                
DMOD8WZ  BRAS  RE,D8WZ             DMWRT  - WRKZ                                
         B     DATAMGRX                                                         
                                                                                
DMOD9WZ  BAS   RE,SETDUM           XXXXXX - WRKZ                                
         BASR  RE,RF               SET DTF ADDRESS IN PARAM4                    
         L     RF,=V(DMWRKZ)                                                    
         BASR  RE,RF                                                            
         B     DATAMGRX                                                         
*&&                                                                             
         EJECT                                                                  
***********************************************************************         
* PRTQ FILE ACTIONS                                                   *         
***********************************************************************         
DMOD2PQ  BRAS  RE,D2PQ             DMREAD - PRTQ                                
         B     DATAMGRX                                                         
*                                                                               
DMOD8PQ  BRAS  RE,D8PQ             DMWRT  - PRTQ                                
         B     DATAMGRX                                                         
*                                                                               
DMOD9PQ  DS    0H                  DMPRINT- PRTQ                                
DMOD3PQ  DS    0H                  DMRSEQ - PRTQ                                
         BRAS  RE,SETDUM                                                        
         BASR  RE,RF               SET DTF ADDRESS IN PARAM4                    
         L     RF,=V(DMPRTQUE)                                                  
         BASR  RE,RF                                                            
         B     DATAMGRX                                                         
*                                                                               
DMOD12PQ BRAS  RE,SETDUM           DMGCI  - PRTQ                                
         BASR  RE,RF               SET DTF ADDRESS IN PARAM4                    
         L     RF,=V(DMPQGCI)                                                   
         BASR  RE,RF                                                            
         B     DATAMGRX                                                         
         EJECT                                                                  
***********************************************************************         
* DMFAST SPECIAL CALLS FOR DIRECT ACCESS                              *         
***********************************************************************         
DMODADA  DS    0H                  DMFAST - DADDS FILE                          
         BAS   RE,SETDUM                                                        
         BASR  RE,RF                                                            
         MVC   DMCBW4,PARAM4       SET DMBCW4 TO FILNUM/A(DTF)                  
         MVC   DMCBW3,=V(DADDS)    SET DMCBW3 TO A(DADDS)                       
         B     DATAMGRX                                                         
*                                                                               
DMODAGR  DS    0H                  DMFAST - GETREC FILE                         
         BAS   RE,SETDUM                                                        
         BASR  RE,RF                                                            
         MVC   DMCBW4,PARAM4       SET DMBCW4 TO FILNUM/A(DTF)                  
         MVC   DMCBW3,=V(DMDALINK) SET DMCBW3 TO A(DMDALINK)                    
         B     DATAMGRX                                                         
         EJECT                                                                  
***********************************************************************         
* DMFAST ACTIONS FOR INDEX SEQUENTIAL                                 *         
***********************************************************************         
DMODAIS  DS    0H                  DMFAST - ISDDS FILE                          
         BAS   RE,SETDUM                                                        
         BASR  RE,RF                                                            
         MVC   DMCBW4,PARAM4       SET DMBCW4 TO FILNUM/A(DTF)                  
         MVC   DMCBW3,=V(ISDDS)    SET DMCBW3 TO A(ISDDS)                       
         B     DATAMGRX                                                         
*                                                                               
DMODKIS  DS    0H                  DMKEY - INDEX SEQUENTIAL FILE                
         BAS   RE,SETDUM                                                        
         BASR  RE,RF                                                            
         SR    R4,R4                                                            
         ICM   R4,7,PARAM4+1       R4=A(DTF)                                    
         USING ISDTF,R4                                                         
         SR    R3,R3                                                            
         ICM   R3,7,DMCBW3+1       R3=A(RETURN AREA IN DMCBW3)                  
*                                                                               
DMODKI0  CLI   SVPAGE,0            0=RETURN LAST KEY READ                       
         BNE   DMODKI1                                                          
         L     RE,ISPDKEY          RE=A(LAST KEY SAVE AREA)                     
         SR    R7,R7                                                            
         ICM   R7,3,ISKEYLN1       R7=KEY LENGTH - 1                            
         SAM31                                                                  
         EX    R7,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R3),0(RE)       EXTRACT LAST KEY READ                        
         SAM24                                                                  
         B     DATAMGRX                                                         
*                                                                               
DMODKI1  CLI   SVPAGE,1            1=RETURN FIRST PART OF DTFIS (112)           
         BNE   DMODKI2                                                          
         MVC   0(112,R3),ISDTF                                                  
         B     DATAMGRX                                                         
*                                                                               
DMODKI2  CLI   SVPAGE,2            2=RETURN MAX EXTENT MATRIX (226)             
         BNE   DMODKI3                                                          
         MVC   0(226,R3),ISXTNT                                                 
         B     DATAMGRX                                                         
*                                                                               
DMODKI3  CLI   SVPAGE,3            3=RETURN AVAILABLE/USEDTRKS/TOTLTRKS         
         BNE   DMODKI4                                                          
         ST    R3,PARAM5                                                        
         SR    RF,RF                                                            
         ICM   RF,7,PARAM2+1       A(DTF) IF CALLER SUPPLIED                    
         BZ    *+6                                                              
         LR    R4,RF                                                            
         BRAS  RE,ISTRACKS         CALCULATE                                    
         B     DATAMGRX                                                         
*                                                                               
DMODKI4  CLI   SVPAGE,4            4=CLEAR ALL BUFFERS TO FORCE I/OS            
         BNE   DMODKI5                                                          
         MVC   PARAM1,=A(ISCLEAR)  SET ISCLEAR ENTRY                            
         ST    R3,PARAM5                                                        
         LA    R1,PARAM1                                                        
         L     RF,=V(ISDDS)                                                     
         BASR  RE,RF                                                            
         B     DATAMGRX                                                         
         DROP  R4                                                               
*                                                                               
DMODKI5  B     DATAMGRX                                                         
         EJECT                                                                  
***********************************************************************         
* DMKEY SPECIAL CALLS FOR DA FILES                                    *         
***********************************************************************         
DMODKDA  DS    0H                  DMKEY - DIRECT ACCESS FILE                   
         BAS   RE,SETDUM                                                        
         BASR  RE,RF                                                            
         SR    R4,R4                                                            
         ICM   R4,7,PARAM4+1       SET R4=A(DTF)                                
         USING DTFPHD,R4                                                        
         SR    R3,R3                                                            
         ICM   R3,7,DMCBW3+1       R3=A(RETURN AREA IN DMCBW3)                  
*                                                                               
DMODKD0  CLI   SVPAGE,0            0=RETURN DISK ADR OF LAST BLOCK READ         
         BNE   DMODKD1                                                          
         MVC   0(4,R3),DBLKDA                                                   
         B     DATAMGRX                                                         
*                                                                               
DMODKD1  CLI   SVPAGE,1            1=RETURN FIRST PART OF DTFPH (56)            
         BNE   DMODKD2                                                          
         MVC   0(56,R3),DTFPHD                                                  
         B     DATAMGRX                                                         
*                                                                               
DMODKD2  CLI   SVPAGE,2            2=RETURN MAX EXTENT MATRIX (226)             
         BNE   DMODKD3                                                          
         SAM31                                                                  
         LA    RE,DMTX                                                          
         TM    DIND,DINDXAM        HIGH CORE EXTENT MATRIX                      
         BZ    *+8                                                              
         ICM   RE,15,DMTX                                                       
         MVC   0(226,R3),0(RE)                                                  
         SAM24                                                                  
         B     DATAMGRX                                                         
*                                                                               
DMODKD3  CLI   SVPAGE,3            3=RETURN DNEXT/USEDTRKS/TOTLTRKS             
         BNE   DMODKD4                                                          
         MVC   PARAM1,=V(DAINFO)   SET DAINFO ENTRY                             
         ST    R3,PARAM5                                                        
         MVC   0(4,R3),DNEXT       START EOF SEARCH FROM DNEXT                  
         LA    R1,PARAM1                                                        
         L     RF,=V(DADDS)                                                     
         BASR  RE,RF                                                            
         B     DATAMGRX                                                         
         DROP  R4                                                               
*                                                                               
DMODKD4  CLI   SVPAGE,4            4=CLEAR ALL BUFFERS TO FORCE I/OS            
         BNE   DMODKD5                                                          
         MVC   PARAM1,=V(DACLEAR)  SET DACLEAR ENTRY                            
         ST    R3,PARAM5                                                        
         LA    R1,PARAM1                                                        
         L     RF,=V(DADDS)                                                     
         BASR  RE,RF                                                            
         B     DATAMGRX                                                         
*                                                                               
DMODKD5  B     DATAMGRX                                                         
         EJECT                                                                  
***********************************************************************         
* DIRECT ACCESS TO ACCESS METHODS                                     *         
***********************************************************************         
DMODTFAD DS    0H                  TRANSLATE FILE ID TO DTF ADDR                
         BAS   RE,SETDUM                                                        
         BASR  RE,RF                                                            
         MVC   DMCBW4,PARAM4       SET DMBCW2 TO FILNUM/A(DTF)                  
         B     DATAMGRX                                                         
*                                                                               
DMOUPDID DS    0H                  RETURN DATAMGR UPDID ADDRESS                 
         L     RE,=A(UPDID)                                                     
         ST    RE,DMCBW4                                                        
         B     DATAMGRX                                                         
*                                                                               
DMODADDS DS    0H                  GO TO DADDS                                  
         LR    R0,R1               SAVE R1 FOR DADDS EXIT                       
         L     RE,SAVER1           POINT RE TO CALLER'S PLIST                   
         L     RF,=V(DADDS)                                                     
         TM    0(RE),X'80'         TEST 31-BIT CALLER                           
         BZ    *+14                                                             
         MVC   8(1,RE),SVPAGE      YES RESTORE HOB OF I/O ADDRESS               
         O     RF,=X'80000000'     AND SET 31-BIT MODE FOR DADDS                
         LA    R1,4(RE)            POINT R1 TO DADDS PLIST                      
         BASR  RE,RF                                                            
         LR    R1,R0               RESTORE BEFORE EXIT                          
         B     DMGRXX                                                           
*                                                                               
DMOISDDS LRL   RF,=V(ISDDS)        GO TO ISDDS                                  
         J     DMODGO                                                           
                                                                                
DMODQDQ  LRL   RF,=V(DMENQDEQ)     GO TO DMENQDEQ                               
         J     DMODGO                                                           
                                                                                
DMODQDC  LRL   RF,=V(DMENQCTL)     GO TO DMENQCTL                               
         J     DMODGO                                                           
                                                                                
DMODLSP  LRL   RF,=V(LOCKSPC)      GO TO LOCKSPC                                
         J     DMODGO                                                           
                                                                                
DMODLKT  LRL   RF,=V(LOCKUP)       GO TO LOCKUP                                 
         J     DMODGO                                                           
                                                                                
DMSHMUSS LRL   RF,=V(DMSHMUSS)     GO TO DMSHMUSS                               
         J     DMODGO                                                           
                                                                                
DMISGENQ LRL   RF,=V(DMISGENQ)     GO TO DMISGENQ                               
         J     DMODGO                                                           
                                                                                
DMRECOVR LRL   RF,=V(RECOVR)       GO TO RECOVER                                
         J     DMODGO                                                           
                                                                                
DMODGO   DS    0H                  GO TO ISDDS                                  
         LR    R0,R1               SAVE R1 FOR EXIT                             
         L     R1,SAVER1           MAKE ABSOLUTELY FOR SURE                     
         LA    R1,4(R1)            THAT R1 POINTS TO USERS P1                   
         BASR  RE,RF                                                            
         LR    R1,R0               RESTORE BEFORE EXIT                          
         B     DMGRXX                                                           
*                                                                               
DMODDDN  DS    0H                  GO TO DMDDNAME                               
         LR    R0,R1                                                            
         L     R1,SAVER1                                                        
         GOTO1 =V(DMDDNAME)        DMDDNAME HAS DUMMY FIRST PARAM               
         LR    R1,R0                                                            
         B     DMGRXX                                                           
*                                                                               
DMODEOFC DS    0H                  GO TO DMEOFCHK                               
         LR    R0,R1                                                            
         L     R1,SAVER1                                                        
         GOTO1 =V(DMEOFCHK)        DMEOFCHK HAS DUMMY FIRST PARAM               
         LR    R1,R0                                                            
         B     DMGRXX                                                           
*                                                                               
DMCOMMIT BRAS  RE,COMMIT           COMMIT ACTION                                
         B     DMGRXX                                                           
***********************************************************************         
* DMCB1  C'FINFO'                                                     *         
* DMCB2  0                                                            *         
* DMCB3  A(OUTPUT AREA) IS=AVAIL,USED,TOTAL DA=DNEXT,USED,TOTAL       *         
* DMCB4  EXTERNAL FILE NUMBER IN P4(1) OR A(DTF) IN P4                *         
***********************************************************************         
DMFINFO  DS    0H                                                               
         LR    R0,R1               SAVE R1 FOR EXIT                             
         L     R3,DMCBW3                                                        
         MVC   PARAM4,DMCBW4       FILE NUM OR A(DTF)                           
         XR    R4,R4                                                            
         ICM   R4,7,PARAM4+1       A(DTF) IF CALLER SUPPLIED                    
         BNZ   DMFINF10                                                         
         MVC   PARAM1,=V(DMEXT)                                                 
         L     RF,=V(DMOD000)                                                   
         BASR  RE,RF               GET A(DTF) FROM FILE NUM                     
                                                                                
         XR    R4,R4                                                            
         ICM   R4,7,PARAM4+1       RETURNED IN PARMA4                           
                                                                                
F        USING DTFPHD,R4                                                        
DMFINF10 TM    F.DTFTYPE,DTFTIS    IS OR DA                                     
         BZ    DMFINF20                                                         
         BRAS  RE,ISTRACKS         CALCULATE R3=A(DATA AREA)                    
         LR    R1,R0                                                            
         B     DMGRXX                                                           
*                                                                               
DMFINF20 MVC   PARAM1,=V(DAINFO)   DADDS ROUTINE DAINFO                         
         XC    PARAM2,PARAM2                                                    
         XC    PARAM3,PARAM3                                                    
         ST    R3,PARAM5           P5=A(VALUE OF DNEXT)                         
         MVC   0(4,R3),F.DNEXT     USE LATEST VALUE TO STRAT SEARCH             
         L     RF,=V(DADDS)                                                     
         LA    R1,PARAM1                                                        
         BASR  RE,RF                                                            
         LR    R1,R0               RESTORE R1 (DMGRWORK)                        
         B     DMGRXX                                                           
         DROP  F                                                                
*                                                                               
DMODMSG  DS    0H                  GO TO WCTYPE (CONSOLE AUTONOTE)              
         LR    R0,R1               SAVE R1 FOR EXIT                             
         L     R1,SAVER1           MAKE ABSOLUTELY SURE                         
         MVI   MSGPARM+4,0                                                      
         L     RE,4(R1)                                                         
***********************************************************************         
* THE AUTONOTE FEATURE DOES NOT SUPPORT ALL CHARACTERS. IF AN INVALID *         
* CHARACTER IS INCLUDED IN AN AUTONOTE MESSAGE,THE ENTIRE SUBJECT LINE*         
* OF THE AUTONOTE IS TRUNCATED ON ITS WAY TO LOTUS NOTES BY SOME      *         
* OBSCURE IBM TRANSLATION MODULE THAT IS NO LONGER SUPPORTED BY ANYONE*         
* SO AS A PREEMPTIVE STRIKE, WE TRANSLATE THE INVALID CHARACTERS TO   *         
* QUESTION MARKS.                                                     *         
***********************************************************************         
         CLC   0(8,RE),=C'AUTONOTE' MAINFRAME-TO-NOTES AUTONOTE?                
         BNE   DMODMSG5                                                         
         LR    RF,RE                                                            
         SRL   RF,24               RF=L'MESSAGE                                 
         BCTR  RF,0                                                             
         LARL  R1,AUTONOTE_TRTAB                                                
         EX    RF,*+8              CONVERT INVALID CHARACTERS                   
         B     *+10                                                             
         TR    0(0,RE),0(R1)                                                    
         LR    R1,R0               RESET R1 CAUSE SAVER1 IS OFF R1              
         L     R1,SAVER1                                                        
                                                                                
DMODMSG5 STCM  RE,7,MSGPARM+5      A(MESSAGE)                                   
         MVC   MSGPARM+11(1),4(R1) LEN OF MESSAGE                               
         MVC   MSGPARM+12(4),=C'LVL2'                                           
         CLC   8(3,R1),=C'LVL'     ANY OVERRIDE FOR ROUTE CODE?                 
         BNE   *+10                                                             
         MVC   MSGPARM+12(4),8(R1)                                              
*                                                                               
         LA    R1,MSGPARM          THAT R1 POINTS TO USERS P1                   
         L     RF,=V(DMOD000)                                                   
         BASR  RE,RF                                                            
         LR    R1,R0               RESTORE BEFORE EXIT                          
         MVC   DMCBW3(1),MSGPARM+4 SET RETURN CODE                              
         B     DATAMGRX                                                         
*                                                                               
MSGPARM  DC    V(WCTYPE)                                                        
         DS    A                   A(MESSAGE)                                   
         DC    XL4'80000000'       LENGTH OF MESAGE (ONLY BYTE3 USED)           
         DC    CL4'LVL2'           CL4 OF ROUTE CODE  LVL0/1/2/3                
         EJECT                                                                  
***********************************************************************         
* SPECIAL DATA MANAGER ACTIONS                                        *         
***********************************************************************         
DMOD2F0  DS    0H                  DMREAD - SYSFLES                             
         SR    RF,RF                                                            
         ICM   RF,1,DMCBW3+3       SENUM PASSED IN DMCBW3+3                     
         BNZ   DMOD2F0A                                                         
         ICM   RF,1,SVSENUM        IF ZERO USE SENUM SAVED FROM UTL             
         BNZ   DMOD2F0A                                                         
         CLI   DMCBW1,X'80'        IF ZERO MUST PASS DMCB1(1)=X'80'             
         JNE   *+2                 DIE=MUST PASS 80                             
         L     RF,=V(SYSFLES)      RETURN V(SYSFLES)                            
         B     DMOD2F0X                                                         
*                                                                               
DMOD2F0A SLL   RF,4                INDEX INTO 16-BYTE SYSTEM TABLE              
         A     RF,=V(SYSTABL)                                                   
         L     RF,DMSYASYF-SYSTABD(RF)                                          
*                                                                               
DMOD2F0X ST    RF,DMCBW4           RETURN SYSFLES ADDRESS IN DMCBW4             
         B     DATAMGRX                                                         
                                                                                
DMODOPEN BRAS  RE,DMODOP           DMOPEN - OPEN LIST OF FILES                  
         B     DATAMGRX                                                         
                                                                                
DMGRUNLK XC    PARAM1(20),PARAM1   DMUNLK - UNLOCK ALL RECS FOR A FILE          
         MVC   PARAM1(1),EXTFILE                                                
         LT    RF,XTCBNTRY         RF=A(ONLINE TCB)                             
         BZ    DMGRUNL1                                                         
X        USING TCBD,RF                                                          
         TM    X.TCBLOCK,TCBLDONE  DOES ONLINE TRANSACTION HAVE LOCKS           
         BZ    DMGRUNL1                                                         
         OI    X.TCBINDS2,TCBFUNLK YES-SET TRANSACTION DID DMUNLK               
         DROP  X                                                                
DMGRUNL1 LT    RF,=V(LOCKER)       CALL LOCKER TO UNLOCK                        
         BZ    DATAMGRX                                                         
         BASR  RE,RF                                                            
         B     DATAMGRX                                                         
         EJECT                                                                  
***********************************************************************         
* DMGRCTL - PERFORM CONTROL TESTS                                     *         
* R6=A(REC) R7=L'KEY                                                  *         
***********************************************************************         
DMGRCTLI AR    R7,R6                                                            
         L     RE,PARAM4           GET IS FILE DTF ADDRESS                      
         USING ISDTF,RE                                                         
         TM    ISCMPRSW,X'80'      TEST VARIABLE LEN FILE                       
         BZ    *+8                                                              
         LA    R7,2(R7)                                                         
*&&UK                                                                           
         CLI   ISFXNUM,X'A1'       IS IT CTFILE                                 
         BNE   DMGRCTLN                                                         
         ICM   RE,15,=V(SSB)       DO WE WANT TO HIDE HIDDEN RECORDS            
         BZ    DMGRCTLN                                                         
         OC    0(2,RE),0(RE)       ONLY VALID FOR ONLINE                        
         BZ    DMGRCTLN                                                         
         TM    SSBSTAT5-SSBD(RE),SSBCTHID                                       
         BZ    DMGRCTLN                                                         
         TM    0(R7),X'08'         IS IT HIDDEN                                 
         BO    DMGRCTLW            YES SO ACT LIKE IT DOESNT EXIST              
*&&                                                                             
         DROP  RE                                                               
DMGRCTLN TM    0(R7),X'80'                                                      
         BZR   R5                                                               
         B     DMGRCTL                                                          
                                                                                
DMGRCTLD AR    R7,R6               DIRECT ACCESS                                
         TM    2(R7),X'80'                                                      
         BZR   R5                                                               
         B     DMGRCTL                                                          
                                                                                
DMGRCTL  TM    DMCBW1,X'08'        USER WANT DELETES                            
         BO    DMGRCTLX                                                         
*&&SPT                                                                          
         CLI   EXTFILE,QSPTFILE    TEST SPTFILE                                 
         BE    DMGRCTLX                                                         
*&&                                                                             
DMGRCTLW MVI   DMCBW3,X'10'        NO SET RECORD NOT FOUND                      
         BR    R5                                                               
DMGRCTLX MVI   DMCBW3,X'02'        YES SET RECORD DELETED                       
         BR    R5                                                               
         EJECT                                                                  
***********************************************************************         
* THESE ROUTINES ARE CALLED FROM MAIN LOGIC AND ALSO FROM ROUTINES    *         
* THAT HAVE BEEN MOVED TO THE END TO GIVE MORE MAIN ADDRESSABILITY.   *         
* WE RELY UPON THESE ROUTINES TO START AT MORE THAT 4K FROM THE START *         
* OF THIS MODULE SO THAT THE SECOND BASE REGISTER (RA) WILL BE USED   *         
* FOR LOCAL ADDRESSING. EACH ROUTINE MOVED TO THE BACK WILL HAVE ITS  *         
* OWN RB WHICH WILL DIFFER FROM THE MAIN LOGIC'S RB.                  *         
***********************************************************************         
         ORG   DATAMGR+4096        ENSURE MINIMUM 4K FROM START                 
         ORG                                                                    
*                                                                               
SETBXLE  LH    R4,0(R3)            SET BXLE PARAMETERS                          
         L     R5,2(R3)                                                         
         LA    R3,6(R3)                                                         
         BR    RE                                                               
*                                                                               
SETPRC   SR    RF,RF               SET PARAM LIST FOR DMCNTL                    
SETPR    ST    RF,PARAM1                                                        
         MVC   PARAM2(4),DMCBW4    SET I/O AREA                                 
         XC    PARAM3(4),PARAM3                                                 
         XC    PARAM4(4),PARAM4                                                 
         MVC   PARAM4(1),EXTFILE   SET EXTERNAL FILE IDENT NUMBER               
         MVC   PARAM5(4),DMCBW3    SET A(DISK ADDR)                             
         XC    PARAM6,PARAM6                                                    
         L     RF,=V(DMOD000)                                                   
         BR    RE                                                               
*                                                                               
SETDUM   L     RF,=V(DMEXT)        SET EXIT TO GET DTF ADDRESS                  
         B     SETPR                                                            
*&&US                                                                           
TSTTRF   CLI   SVMAJSYS,X'0D'      TEST TRAFFIC SYSTEM                          
         BNE   TSTSPT                                                           
         LTR   R0,R0               EXIT IF NON-UPDATIVE ACTION                  
         BZR   RE                                                               
         CLI   INTFILE,QSPTDIR                                                  
         BE    TSTFAIL             CANT UPDATE SPTDIR FROM TRAFFIC SYS          
         CLI   INTFILE,QSPTFILE                                                 
         BE    TSTFAIL             CANT UPDATE SPTFIL FROM TRAFFIC SYS          
         CLI   INTFILE,QSTAFILE                                                 
         BE    TSTFAIL             CANT UPDATE STAFIL FROM TRAFFIC SYS          
         BR    RE                                                               
*                                                                               
TSTSPT   CLI   SVMAJSYS,X'02'      EXIT IF NOT A SPOT SYSTEM                    
         BNER  RE                                                               
         CLI   INTFILE,QTRFDIR     CHECK UPDATING IF TRFDIR                     
         BE    TSTSPT10                                                         
         CLI   INTFILE,QTRFFILE    CHECK UPDATING IF TRFFIL                     
         BNER  RE                                                               
*                                                                               
TSTSPT10 LLC   RF,SVSENUM          INDEX INTO OPEN LIST                         
         A     RF,=A(TRFOPLST)                                                  
         CLI   0(RF),C'T'          TEST IF TRAFFIC FILES LIVE                   
         BNE   TSTSPT20                                                         
         LTR   R0,R0               EXIT IF NON UPDATIVE ACTION                  
         BZR   RE                                                               
         B     TSTFAIL             CAN'T UPDATE FROM SPOT SYSTEM                
*                                                                               
TSTSPT20 CLI   INTFILE,QTRFDIR     SWITCH TRFDIR TO SPTDIR                      
         BNE   TSTSPT22                                                         
         MVI   INTFILE,QSPTDIR                                                  
         MVI   EXTFILE,QSPTDIR                                                  
         BR    RE                                                               
*                                                                               
TSTSPT22 MVI   INTFILE,QSPTFILE    SWITCH TRFFIL TO SPTFILE                     
         MVI   EXTFILE,QSPTFILE                                                 
         BR    RE                                                               
*                                                                               
TSTFAIL  DS    0H                                                               
*        L     RF,.....            A(DTF)                                       
*        USING DTFPHD,RF           WILL NEED TO DIG OUT DTF THEN                
*        TM    DTFFLAG,DTFGLOB                                                  
*        JNZ   *+2                 DIE=POTENTIAL ZAZZARD                        
*        DROP  RF                                                               
TSTFAOFF NOPR  RE                  USED TO TURN OFF AFTER 1ST HIT               
         STM   RE,RC,12(RD)        TEMP CHAIN TO DO WTO                         
         ST    RD,76(RD)                                                        
         LA    RE,72(RD)                                                        
         ST    RE,8(RD)                                                         
         LR    RD,RE               JUST INCASE A TIMER POP HAPPENS              
         MVC   CRISSYS,=C'SPT'                                                  
         MVC   CRISTYP,=C'PRG'                                                  
         CLI   SVMAJSYS,X'02'      SPOT SYSTEM?                                 
         BE    *+10                                                             
         MVC   CRISSYS,=C'TRF'                                                  
                                                                                
         TM    XFLAG1,XFONLINE     TEST ONLINE OR OFFLINE                       
         BZ    TSTFA10                                                          
         L     RE,=V(SSB)                                                       
         USING SSBD,RE                                                          
         ICM   RF,15,SSBTKADR                                                   
         USING TCBD,RF                                                          
         BZ    TSTFA20                                                          
         LLC   R2,TCBPRG           CONVERT PROGRAM NUMBER TO ALPHA              
         SRL   R2,4                                                             
         LA    R2,HEXTXT(R2)                                                    
         MVC   CRISPRG(1),0(R2)                                                 
         LLC   R2,TCBPRG                                                        
         NILL  GR2,X'000F'                                                      
         LA    R2,HEXTXT(R2)                                                    
         MVC   CRISPRG+1(1),0(R2)                                               
         B     TSTFA20                                                          
         DROP  RE,RF                                                            
                                                                                
TSTFA10  MVC   CRISTYP,=C'JOB'                                                  
         LA    R2,20(RD)           GET JOB NAME                                 
         EXTRACT (2),FIELDS=TIOT                                                
         L     R2,20(RD)                                                        
         MVC   CRISJOB,0(R2)                                                    
                                                                                
TSTFA20  LA    R2,CRISCROS                                                      
         WTO   TEXT=(R2)                                                        
*NOP*    OI    TSTFAOFF+1,X'F0'    TURN OFF MESSAGE                             
         L     RD,4(RD)                                                         
         LM    RE,RC,12(RD)                                                     
         DC    H'0'                DIE=ONLINE                                   
         BSM   0,RE                                                             
                                                                                
HEXTXT   DC    C'0123456789ABCDEF'                                              
                                                                                
CRISCROS DC    AL2(CRISLNQ)                                                     
         DC    C'AUTONOTE*AHYDN,RCRI:'                                          
         DC    C'CROSS SYSTEM UPDATE SYS='    SYSTEM VIOLATION                  
CRISSYS  DC    CL3' '              SPT OR TRF                                   
         DC    C' '                                                             
CRISTYP  DC    CL3' '              JOB OR PRG                                   
         DC    C'='                                                             
CRISPRG  DS    XL2                                                              
         ORG   CRISPRG                                                          
CRISJOB  DC    CL8' '                                                           
         ORG                                                                    
CRISLNQ  EQU   *-CRISCROS-2                                                     
*&&                                                                             
         EJECT                                                                  
***********************************************************************         
* MAIN ROUTINES LITERALS                                              *         
***********************************************************************         
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
* TABLES OF FILES/ACTIONS/COMMANDS                                    *         
***********************************************************************         
*DMDMGRTAB                                                                      
       ++INCLUDE DMDMGRTAB                                                      
         EJECT                                                                  
*&&US                                                                           
***********************************************************************         
* LISTS OF DEMO DA FILES PER DIRECTORY                                *         
* DUMMY FIRST ENTRIES REQUIRED TO HWORD ALIGN TABLE-1 FOR LARL        *         
***********************************************************************         
*                                                                               
* FILES ASSOCIATED WITH NTIDIR                                                  
         DS    0H                                                               
         DC    X'00'                       0,DUMMY                              
NTITAB   DC    X'3C'               DEMSTAT=1,NTIFILO                            
         DC    X'39'                       2,NTIFILN                            
         DC    X'3E'                       3,NTIFILP                            
         DC    X'29'                       4,NTIFILQ                            
         DC    X'3A'                       5,NTIFILW                            
         DC    X'47'                       6,NTIFILY                            
         DC    X'4F'                       7,NTIFILZ                            
         DC    X'5A'                       8,NTIFILA                            
         DC    X'5B'                       9,NTIFILB                            
         DC    X'5C'                      10,NTIFILC                            
         DC    X'5D'                      11,NTIFILD                            
         DC    X'5E'                      12,NTIFILE                            
         DC    X'77'                      13,NTIFILF                            
         DC    X'78'                      14,NTIFILG                            
         DC    X'7C'                      15,NTIFILH                            
         DC    X'7D'                      16,NTIFILI                            
         DC    X'BD'                      17,NTIFILJ                            
         DC    X'BE'                      18,NTIFILK                            
         DC    X'8B'                      19,NTIFILL                            
         DC    X'8C'                      20,NTIFILM                            
         DC    X'8D'                      21,NTIFILR                            
         DC    X'00'                      22,FUTURE USE                         
         DC    X'00'                      23,FUTURE USE                         
         DC    X'00'                      24,FUTURE USE                         
         DC    X'00'                      25,FUTURE USE                         
         DC    X'00'                      26,FUTURE USE                         
         DC    X'00'                      27,FUTURE USE                         
         DC    X'00'                      28,FUTURE USE                         
         DC    X'00'                      29,FUTURE USE                         
         DC    X'00'                      30,FUTURE USE                         
         DC    X'54'                      31,NTIFLTA                            
NTITABQ  EQU   *-NTITAB                                                         
*                                                                               
* FILES ASSOCIATED WITH DEMDIRN                                                 
         DS    0H                                                               
         DC    X'00'                         DUMMY                              
DEMTAB   DC    X'3B'               DEMFILO - DEMSTAT=1                          
         DC    X'26'               DEMFILN - DEMSTAT=2                          
         DC    X'3D'               DEMFILP - DEMSTAT=3                          
         DC    X'3F'               DEMFILQ - DEMSTAT=4                          
         DC    X'48'               DEMFILU - DEMSTAT=5                          
         DC    X'49'               DEMFILV - DEMSTAT=6                          
         DC    X'2B'               DEMFILW - DEMSTAT=7                          
         DC    X'4A'               DEMFILY - DEMSTAT=8                          
         DC    X'C9'               DEMFILI - DEMSTAT=9                          
         DC    X'CA'               DEMFILJ - DEMSTAT=10                         
         DC    X'CB'               DEMFILK - DEMSTAT=11                         
         DC    X'CC'               DEMFILL - DEMSTAT=12                         
         DC    X'CD'               DEMFILM - DEMSTAT=13                         
         DC    X'CE'               DEMFILS - DEMSTAT=14                         
         DC    X'CF'               DEMFILZ - DEMSTAT=15                         
         DC    X'4B'               DEMFILB - DEMSTAT=16                         
         DC    X'4C'               DEMFILC - DEMSTAT=17                         
         DC    X'4D'               DEMFILD - DEMSTAT=18                         
         DC    X'4E'               DEMFILE - DEMSTAT=19                         
         DC    X'C6'               DEMFILF - DEMSTAT=20                         
         DC    X'C7'               DEMFILG - DEMSTAT=21                         
         DC    X'C8'               DEMFILH - DEMSTAT=22                         
         DC    X'C3'               DEMFIL3 - DEMSTAT=23                         
         DC    X'C4'               DEMFIL5 - DEMSTAT=24                         
         DC    X'57'               DEMFIL7 - DEMSTAT=25                         
         DC    X'59'               DEMFIL9 - DEMSTAT=26                         
         DC    X'C1'               DEMFIL1 - DEMSTAT=27                         
         DC    X'56'               DEMFIL6 - DEMSTAT=28                         
         DC    X'58'               DEMFIL8 - DEMSTAT=29                         
         DC    X'7A'               DEMFIL$ - DEMSTAT=30                         
         DC    X'51'               DEMFLTN - DEMSTAT=31                         
         DC    X'7B'               DEMFLNA - DEMSTAT=32                         
         DC    X'98'               DEMFLNB - DEMSTAT=33                         
         DC    X'99'               DEMFLNC - DEMSTAT=34                         
         DC    X'9A'               DEMFLND - DEMSTAT=35                         
         DC    X'9B'               DEMFLNE - DEMSTAT=36                         
         DC    X'9C'               DEMFLNF - DEMSTAT=37                         
         DC    X'9D'               DEMFLNG - DEMSTAT=38                         
         DC    X'B7'               DEMFLNH - DEMSTAT=39                         
         DC    X'B8'               DEMFLNI - DEMSTAT=40                         
         DC    X'B9'               DEMFLNJ - DEMSTAT=41                         
         DC    X'BA'               DEMFLNK - DEMSTAT=42                         
         DC    X'BB'               DEMFLNL - DEMSTAT=43                         
         DC    X'BC'               DEMFLNM - DEMSTAT=44                         
DEMTABQ  EQU   *-DEMTAB                                                         
*&&                                                                             
         EJECT                                                                  
***********************************************************************         
* +0     X'LENGTH OF ACTION CODE LESS 1'                              *         
* +1     X'00'                                                        *         
* +2     CL6'ACTION CODE'                                             *         
* +8     AL4(SPECIAL PROCESS RTN FOR THIS ACTION)                     *         
***********************************************************************         
         DS    0F                                                               
SPCLTAB  DC    X'05',X'00',CL6'DMOPEN',A(DMODOPEN)                              
         DC    X'05',X'00',CL6'DMCLSE',A(DMODOPEN)                              
         DC    X'04',X'00',CL6'DADDS ',A(DMODADDS)                              
         DC    X'04',X'00',CL6'ISDDS ',A(DMOISDDS)                              
         DC    X'04',X'00',CL6'UPDID ',A(DMOUPDID)                              
         DC    X'04',X'00',CL6'DUMMY ',A(DMGRX)                                 
         DC    X'04',X'00',CL6'DTFAD ',A(DMGR1A05)                              
         DC    X'04',X'00',CL6'SSBAD ',A(DMODSSB)                               
         DC    X'05',X'00',CL6'DMUNLK',A(DMGR2)                                 
         DC    X'05',X'00',CL6'ENQDEQ',A(DMODQDQ)                               
         DC    X'05',X'00',CL6'ENQCTL',A(DMODQDC)                               
         DC    X'05',X'00',CL6'DMTRAC',A(DMGR2TR)                               
         DC    X'05',X'00',CL6'DMTRON',A(DMGR2TON)                              
         DC    X'05',X'00',CL6'DMTROF',A(DMGR2TOF)                              
         DC    X'05',X'00',CL6'LOCKSP',A(DMODLSP)                               
         DC    X'05',X'00',CL6'LOCKUP',A(DMODLKT)                               
         DC    X'05',X'00',CL6'RECOVR',A(DMRECOVR)                              
         DC    X'04',X'00',CL6'OPMSG ',A(DMODMSG)                               
         DC    X'05',X'00',CL6'DMISLK',A(DMISLK)                                
         DC    X'05',X'00',CL6'COMMIT',A(DMCOMMIT)                              
         DC    X'04',X'00',CL6'FINFO ',A(DMFINFO)                               
         DC    X'05',X'00',CL6'DDNAME',A(DMODDDN)                               
         DC    X'05',X'00',CL6'SHMUSS',A(DMSHMUSS)                              
         DC    X'05',X'00',CL6'ISGENQ',A(DMISGENQ)                              
         DC    X'05',X'00',CL6'SYSTAB',A(DMODSYST)                              
         DC    X'05',X'00',CL6'FILTAB',A(DMODFILT)                              
         DC    X'05',X'00',CL6'EOFCHK',A(DMODEOFC)                              
         DC    X'00',X'00'                                                      
                                                                                
***********************************************************************         
* SPECIAL CALLS FOR INFORMATION                                       *         
***********************************************************************         
DMODSSB  LRL   RE,=V(SSB)          RETURN A(SSB)                                
         J     DMODTAB                                                          
*                                                                               
DMODSYST LRL   RE,=V(SYSTABL)      RETURN A(DMSYSTAB)                           
         J     DMODTAB                                                          
*                                                                               
DMODFILT LRL   RE,=V(FILTABL)      RETURN A(DMFILTAB)                           
         J     DMODTAB                                                          
*                                                                               
DMODTAB  L     RF,SAVER1           RF=A(CALLERS DMCB)                           
         ST    RE,4(RF)            RETURN THE TABLE ADDRESS AT DMCBW2           
         J     DMGRXX                                                           
         EJECT                                                                  
*&&DMSMF                                                                        
***********************************************************************         
* SMF FOR DEMFILE                                                     *         
*                                                                     *         
* DEIS SEP/2018:                                                      *         
*  THIS CODE WAS ORIGINALLY ADDED CIRCA 2006 (GIVE OR TAKE). IT WAS   *         
*  USED TO DETERMINE WHICH AGENCIES WERE READING "OLD" DEMO DATA FROM *         
*  A "HISTORICAL" DEMO *FILE*, AND HOW OFTEN THAT WAS HAPPENING.      *         
*                                                                     *         
*  NOW WE ARE USING A SIMILAR APPROACH TO LOG DEMO DIRECTORY READS    *         
*  FROM FQA. WE'RE DOING THIS TO FIND OUT WHAT DATA IS CURRENTLY      *         
*  BEING READ, SO THAT WE KNOW WHICH DEMO DATA IS NEEDED TO POPULATE  *         
*  THE NEW VSAM DEMOFILE FOR FQA.                                     *         
*                                                                     *         
*  NOTE: THIS CODE IS *ASSEMBLED* IF AND ONLY IF THE &&SET "DMSMF"    *         
*  SWITCH IS SET TO "Y". EVEN WHEN THAT IS TRUE, LOGGING ONLY OCCURS  *         
*  WHEN THE SSGDMSMF FLAG IS SET TO "Y" IN THE GLOBAL SSB AREA OF THE *         
*  DATASPACE.                                                         *         
*                                                                     *         
***********************************************************************         
DEMSMF   NTR1  BASE=*,WORK=(R7,DSMFLNQ) <== CHECK IF YOU CHANGE DSMFD           
         USING DSMFD,R7                                                         
         XC    DSMFD(DSMFLENQ),DSMFD                                            
*                                                                               
         MVC   DSMFLEN,=AL2(DSMFLENQ)                                           
         MVI   DSMFCHR,C'D'        'D' (FOR DEMOS)                              
*                                                                               
         MVC   DSMACTN,ACTION      DATAMGR ACTION                               
*********MVC   DSMFFILE,DEMSTAT    SET DEMO FILE YOU WANT TO READ               
*********L     RF,DMCBW4           USER KEY IN PARAM 4                          
         L     RF,DMCBW3           USER KEY IN PARAM 3                          
         MVC   DSMMJKEY,0(RF)      SAVE MAJOR KEY                               
         MVC   DSMFDFL#,DMCBW7     (1=DEMA,2=DEMN,3=DEMR,4=NTI,5=PAV)           
*********MVC   DSMFYEAR,8(RF)      THIS ASSUMES IT'S AN "R" RECORD!             
*********XI    DSMFYEAR,X'FF'      SET YEAR FROM KEY                            
*                                                                               
         ICM   RF,15,XUTLNTRY                                                   
         BZ    DSMFOFFL                                                         
         MVI   DSMFONOF,C'N'       ONLINE                                       
         LRL   RE,=V(SSB)                                                       
         USING SSBD,RE                                                          
*********CLI   SSBDSPAC,C'Q'       MAKE SURE WE'RE ON FQA                       
*********BNE   DSMFLXIT                                                         
*                                                                               
         MVC   DSMFDSPC,SSBDSPAC   DSPACE (E.G., 'Q' = FQA)                     
         LR    R0,RE               SAVE A(SSB)                                  
         DROP  RE                                                               
*                                                                               
         SAM31 ,                   UTLS ARE IN XA STORAGE                       
         USING UTLD,RF                                                          
         MVC   DSMFAGY,TAGY                                                     
         MVC   DSMFUSER,TUSER                                                   
         MVC   DSMFSYS,TSYS                                                     
         MVC   DSMFPRG,TPRG                                                     
         MVC   DSMPERSN,TPERSON                                                 
         MVC   DSMAGYPR,TAGYPER                                                 
         OC    DSMAGYPR,DSMAGYPR                                                
         BNZ   *+10                                                             
         MVC   DSMAGYPR,TAGYSEC                                                 
         DROP  RF                                                               
         SAM24 ,                                                                
*                                                                               
         B     DSMFLOG                                                          
*                                                                               
DSMFOFFL DS    0H                  OFFLINE                                      
         MVI   DSMFONOF,C'F'                                                    
*                                                                               
         LRL   RF,=V(SSB)                                                       
         USING SSBD,RF                                                          
         CLI   SSOXTND,X'FF'       DO WE HAVE EXTENDED SSB?                     
         BNE   DSMFLXIT            NO: NO LOGGING                               
*********CLI   SSODSPAC,C'Q'       YES: MAKE SURE WE'RE ON FQA                  
*********BNE   DSMFLXIT                                                         
*                                                                               
         LR    R0,RF               SAVE A(SSB)                                  
         MVC   DSMFDSPC,SSODSPAC   DSPACE VALUE (E.G., 'Q' = FQA)               
         ICM   RF,15,SSOMASTC      GET DATA FROM MASTC IF WE HAVE IT            
         BZ    DSMFLOG                                                          
         USING MASTD,RF                                                         
         MVC   DSMFOUSR,MCUSER                                                  
         MVC   DSMFOSYS,MCSYSTEM                                                
         MVC   DSMFOPRG,MCPROG                                                  
         DROP  RF                                                               
*                                                                               
DSMFLOG  DS    0H                                                               
         LR    RF,R0               GET SAVED A(SSB)                             
         LAM   ARF,ARF,SSBALET-SSBD(RF)                                         
         SR    RF,RF                                                            
         SAC   512                                                              
         L     RF,DHASSBG-DMDHDR(,RF)     A(GLOBAL SSB)                         
         CLI   SSGDMSMF-FASSBG(RF),C'Y'   SMF LOGGING ENABLED?                  
         SAC   0                                                                
         LAM   ARF,ARF,=F'0'                                                    
         BNE   DSMFLXIT            ** CC IS SET BY PREVIOUS CLI **              
*                                                                               
         ICM   RF,15,=V(SMFOUT)                                                 
         JZ    *+2                 V(SMFOUT) MUST BE RESOLVED!                  
*                                                                               
*                                  CALL SFMOUT ROUTINE                          
         MVC   DSMFRTYP,=F'8'      TYPE '8': DDS "UTILITY" SMF RECORD           
         ST    R7,DSMFAREC         A(SMF RECORD) DSMFD                          
         LA    R1,DSMFPARM                                                      
         BASR  RE,RF                                                            
         JNE   *+2                 ERROR RETURNED FROM SMFOUT                   
*                                                                               
DSMFLXIT DS    0H                                                               
         XIT1  ,                                                                
         DROP  R7                                                               
*                                                                               
         LTORG                                                                  
*                                                                               
DSMFD    DSECT                                                                  
DSMFLEN  DS    AL2                                                              
DSMFCHR  DS    C                   'D' FOR DEMOS                                
DSMFDSPC DS    C                   DSPACE VALUE (E.G., 'Q' = FQA)               
DSMFONOF DS    C                   'N' = ONLINE, 'F' = OFFLINE                  
*                                  DEMO FILE SET #                              
DSMFDFL# DS    X                    (1=DEMA,2=DEMN,3=DEMR,4=NTI,5=PAV)          
**DSMFFILE DS    X                                                              
**DSMFYEAR DS    X                                                              
*                                                                               
DSMFAGY  DS    CL2                                                              
DSMFUSER DS    HL2                                                              
DSMFSYS  DS    X                                                                
DSMFPRG  DS    X                                                                
DSMPERSN DS    XL2                 PERSONAL ID NUMBER (PID)                     
DSMAGYPR DS    CL2                 AGENCY CODE FOR PID                          
         ORG   DSMFAGY                                                          
DSMFOUSR DS    CL2                                                              
DSMFOSYS DS    CL2                                                              
DSMFOPRG DS    CL2                                                              
         DS    XL4                 OFFLINE: SPARE                               
DSMACTN  DS    X                   X'02'=READ, X'03'=RSEQ, X'04'=RDHI           
DSMMJKEY DS    XL18                DEMOS: ENTIRE MAJOR KEY                      
DSMFLENQ EQU   *-DSMFD             LENGTH OF SMF RECORDS TO OUTPUT              
*                                                                               
DSMFPARM DS    2F                  PARAMETER LIST TO SMFOUT                     
         ORG   DSMFPARM                                                         
DSMFRTYP DS    XL4                 SMF RECORD TYPE                              
DSMFAREC DS    A                   A(SMF RECORD)                                
*                                                                               
DSMFLNQ  EQU   *-DSMFD             LENGTH OF STORAGE NEED TO ACQUIRE            
*&&                                                                             
DATAMGR  CSECT                                                                  
         EJECT                                                                  
***********************************************************************         
* THIS ROUTINE MUST START BEYOND SCOPE OF MAIN LOGIC'S RB BASE REG    *         
* (ABOVE PROBABLY NO LONGER TRUE AS RELATIVE ADDRESSING)              *         
* PARAMETER LIST IS AS FOLLOWS                                        *         
* PARAM1  NOT USED                                                    *         
* PARMM2  A(RECORD)                                                   *         
* PARAM3  XL1=EXTERNAL FILE NUMBER                                    *         
*         XL1=REC TYPE 01=COPY,02=CHANGE,03=ADD,81=PTRCPY,82=PTRCHG   *         
*         XL2=REC LENGTH                                              *         
* PARAM4  A(DTFPH)                                                    *         
* PARAM5  A(DISK ADDRESS)                                             *         
* PARAM6  A(COPY READ AREA)                                           *         
***********************************************************************         
DMGRRECV LA    RF,DMWORK           SET A(COPY READ AREA)                        
         J     *+6                                                              
*                                                                               
DMGRREC1 LR    RF,R7               ENTRY POINT R7=A(COPY READ AREA)             
         LRL   RE,=V(SSB)                                                       
         LTR   RE,RE                                                            
         JZ    DMGRRECX            NO RECOVERY IF NO SSB                        
         TM    SSBSTAT2-SSBD(RE),X'02' NO RECOVERY BIT IN SSB                   
         JO    DMGRRECX                                                         
*                                                                               
         MVC   FULL,PARAM6                                                      
         ST    RF,PARAM6                                                        
         NI    PARAM3+1,255-X'40'  RESET/SET SPECIAL UPDATE FLAG                
*                                                                               
         ICM   RF,15,XTCBNTRY      SPECIAL UPDATE FLAG IS IN TCB                
         JZ    DMGRREC2                                                         
X        USING TCBD,RF                                                          
         TM    X.TCBFLAG4,TCB4SUPD                                              
         JZ    *+8                                                              
         OI    PARAM3+1,X'40'      SPECIAL UPDATE ALLOWED                       
         DROP  X                                                                
*                                                                               
DMGRREC2 LRL   RF,=V(DMRCVR)                                                    
         BASR  RE,RF                                                            
         MVC   PARAM6(4),FULL                                                   
*                                                                               
DMGRRECX XC    8(2,R1),8(R1)       CLEAR FILE/ACTION                            
         BR    R5                                                               
         EJECT                                                                  
*&&US                                                                           
***********************************************************************         
* US DEMO VSAM INTERFACE                                              *         
* CALLER PASSES THE FOLLOWING IN DMCBW7/BYTE0-1:                      *         
*   AL1 - FILE SET NUMBER - 1=DEMA, 2=DEMN, 3=DEMR, 4=NTI, 5=PAV      *         
*   CL1 - DIR/FIL IND.    - C'D'=DIRECTORY, C'F'=FILE.                *         
* ROUTINE ADDS THE FOLLOWING TO DMCBW7/BYTE2 (BYTE3 UNUSED):          *         
*   XL1 - INTERNAL ACTION - 02=DMREAD, 03=DMRSEQ, 04=DMRDHI           *         
*   XL1 - UNUSED                                                      *         
* DMCBW1-6 = COPY OF CALLER'S DMCB                                    *         
* IF VSAM ACTIVE FOR THE FILE SET, ROUTINE CALLS DMVSAM AND RETURNS   *         
*    TO 0(RE).                                                        *         
* IF VSAM NOT ACTIVE FOR THE FILE SET, ROUTINE JUST RETURNS TO 4(RE)  *         
*                                                                     *         
* THE GLOBAL ACTIVITY FLAGS ARE LOCATED IN THE DATAMGR DATASPACE.     *         
* THEY ARE USED BOTH ONLINE AND OFFLINE.                              *         
*                                                                     *         
* FOR OFFLINE PROGRAMS THAT RUN UNDER DDMASTER:                       *         
* A "PARM=DE@XXXXX" CONTROL CARD (WITH POSITIONAL FLAGS) MAY BE       *         
* PROVIDED TO OVERRIDE THE FLAGS IN THE DATASPACE. THIS IS INTENDED   *         
* FOR USE DURING THE TRANSITIONAL PERIOD FOR STAGING, DEBUGGING, ETC. *         
* NOTE THAT THE "XVSAM" KILL SWITCH SUPERSEDES THE PARM= CARD.        *         
*                                                                     *         
***********************************************************************         
DEMVSAM  DS    0H                                                               
         ST    RE,SAVERE                                                        
*                                                                               
*======================================================================         
* TEMPORARY: FOR NOW, IF WE ARE READING FROM DEMDIRR, VSAM IS ELIGIBLE          
*            IF AND ONLY IF THE SECOND BYTE OF THE KEY IS C'R'. THIS            
*            MEANS THAT (FOR NOW) THE NCC C'GTF' EXTENDED PASSIVES              
*            (WHICH RESIDE ON DEMDIRR) WILL ALWAYS BE READ VIA DANDX.           
*            WE SHOULD REMOVE THIS CODE WHEN WE'RE MORE CONFIDENT.              
*                                                                               
*&&DO                                                                           
         CLI   DMCBW7+1,C'D'       DIRECTORY READ OF DEMDIRR?                   
         JNE   DEMVS05                                                          
         CLI   DMCBW7+0,3                                                       
         JNE   DEMVS05                                                          
         L     RF,DMCBW3           YES: RF = A(CALLER'S KEY)                    
         CLI   1(RF),C'R'          ARE WE READING RADIO DATA?                   
         BNE   4(,RE)              NO: INELIGIBLE FOR VSAM (FOR NOW)            
*                                                                               
DEMVS05  DS    0H                                                               
*&&                                                                             
*======================================================================         
         TM    XFLAG1,XFONLINE     ONLINE?                                      
         JO    DEMVS20             YES: EXTRACT DATASPACE FLAG FOR FILE         
*                                                                               
         LRL   RF,=V(SSB)                                                       
         USING SSBD,RF                                                          
         CLI   SSOXTND,X'FF'       OFFLINE: NEED EXTENDED SSB                   
         JNE   *+2                 WE GOTTA HAVE THIS                           
*                                                                               
* FIRST CHECK FOR AN OVERRIDE IN THE LOCAL SSB, WHICH OVERRIDES                 
* EVERYTHING. DURING THE TRANSITIONAL PARALLEL PERIOD, THE LOCAL SSB            
* MAY INDICATE THAT WE ARE RUNNING AN UPDATE, AND THAT WE WANT TO FORCE         
* READS USING A PARTICULAR ACCESS METHOD (TO MAINTAIN DATA INTEGRITY            
* WHEN WRITING TO THE FILE).                                                    
* NOTE THAT THIS VALUE OVERRIDES THE "XVSAM" KILL SWITCH. NO                    
* APPLICATION SHOULD BE SETTING SSODMSTA UNLESS IT KNOWS WHAT IT IS             
* DOING! THE EASIEST WAY TO SET THIS SWITCH IS VIA THE SPECIAL DDNAMES          
* $$VSAM$$ OR $$DNDX$$ (SEE DMDDSIO FOR DETAILS).                               
*                                                                               
         CLI   SSODMSTA,C' '       IS AN SSB OVERRIDE FLAG SET?                 
         JNH   *+12                NO                                           
         CLI   SSODMSTA,C'V'       YES: HONOR IT (SET CC)                       
         J     DEMVS50                                                          
*                                                                               
* SET THE DMGR DATASPACE ALET IF WE DON'T HAVE IT ALREADY.                      
*                                                                               
         OC    SSOALET,SSOALET     DO WE HAVE DATAMGR DATASPACE ALET?           
         JNZ   DEMVS20             YES: NO NEED TO GET IT AGAIN                 
         XC    DUB,DUB             NO: MUST GET ALET (FIRST TIME)               
         OI    DUB,X'20'           SET ENQUIRY ONLY                             
         LR    R0,R1               R1 IS WORK BASE. MUST SAVE IT                
         LA    R1,DUB              R1 = A(LOCKSPC PARAMETER LIST)               
         LRL   RF,=V(LOCKSPC)      SET THE DATAMGR DATASPACE ALET               
         BASR  RE,RF               GO TO LOCKSPC                                
         LR    R1,R0               RESTORE WORK BASE                            
*                                                                               
* EXTRACT THE DEMOS ACCESS METHOD FLAG FROM THE DATASPACE FOR THE               
* GIVEN DEMOFILE.                                                               
*                                                                               
DEMVS20  DS    0H                                                               
         LRL   RF,=V(SSB)                                                       
         LAM   ARF,ARF,SSBALET     NOTE: SSOALET HAS SAME DISPLACEMENT          
         DROP  RF                                                               
*                                                                               
         SR    RF,RF                                                            
         SAC   512                                                              
         L     RF,DHASSBG-DMDHDR(,RF)      A(GLOBAL SSB)                        
         LLC   RE,DMCBW7+0         GET FILE SET NUMBER FROM DMCB7/B0            
         LA    RF,SSGDMSTA-FASSBG-1(RE,RF) RF=A(FLAG<1-5>) FOR FILE SET         
         MVC   DUB(1),0(RF)        SAVE DATASPACE VALUE IN "DUB" H.O.B.         
*                                                                               
         SAC   0                                                                
         SR    R0,R0                                                            
         SAR   ARF,R0              CLEAR ARF (VIA CLEARED R0)                   
*                                                                               
         CLI   DUB,C'X'            IS THE VSAM KILL SWITCH SET?                 
         JNE   *+12                NO                                           
         MVI   DUB,C'D'            YES: IT'S EQUIVALENT TO "D" (DANDX)          
         J     DEMVS30             IGNORE ALL OTHER OVERRIDES                   
*                                                                               
* DEMO VSAM "VERSION CONTROL".                                                  
* ONLINE, THIS IS SET IN TSTATV IN THE UTL BY SRCON00.                          
* OFFLINE, IT'S SET IN SSODMST2 BY DDMASTER OR DDRUNNER AT THE START OF         
* EACH REQUEST.                                                                 
* THE FIRST FIVE BITS IN TSTATV AND SSODMST2 CORRESPOND TO THE FIVE             
* VSAM FILES AND MUST BE IN THE SAME SEQUENCE AS EACH OTHER AND DEMVSON         
* I.E. DEMXXXA, N, R, NTIXXX AND PAVXXX.                                        
* IF A BIT IS ON, VERSION CONTROL REQUIRES THE CURRENT CONNECT OR               
* REQUEST TO USE VSAM FOR THE CORRESPONDING FILE.                               
* VERSION CONTROL OVERRIDES THE ACCESS FLAG IN THE DATASPACE, UNLESS IT         
* IS SET AS A KILL SWITCH (ABOVE), BUT IS OVERRIDEN BY THE OFFLINE PARM         
* CARD (BELOW).                                                                 
* SINCE VERSION CONTROL CAN LEGITIMATLY CHANGE THE SETTING, WE DON'T            
* WANT THE CODE AT DEMVS30 TO KILL US WHEN OFFLINE AS WE KNOW IT WILL           
* ONLY CHANGE AT START OF REQUEST. WE THEREFORE CLEAR THE SAVED VALUES.         
* ONLINE, THEY ARE CLEARED AT START OF EACH TASK SO NO PROBLEM                  
*                                                                               
         ICM   RF,15,XUTLNTRY      ONLINE, VERSION CONTROL IS IN UTL            
         SAM31                                                                  
         LLC   RF,TSTATV-UTLD(,RF) RF=ONLINE VERSION CONTROL                    
         SAM24                                                                  
         TM    XFLAG1,XFONLINE     ONLINE?                                      
         JO    DEMVS24                                                          
         LRL   RF,=V(SSB)          NO, OFFLINE                                  
         TM    SSODMST2-SSOOFF(RF),SSODMSCH VERSION CONTROL CHANGED?            
         JZ    DEMVS22             NO, SKIP                                     
         NI    SSODMST2-SSOOFF(RF),255-SSODMSCH CLEAR CHANGED FLAG              
         LARL  RF,DEMVSON-1        OFFLINE SAVED STATUSES                       
         XC    1(5,RF),1(RF)       CLEAR THEM ALL (NOTE ALIGNMENT!)             
         LRL   RF,=V(SSB)          RESET RF                                     
DEMVS22  LLC   RF,SSODMST2-SSOOFF(,RF) RF=OFFLINE VERSION CONTROL               
DEMVS24  SLL   RF,0(RE)            SHIFT RELEVANT BIT INTO BIT 23               
         STCM  RF,B'0010',DUB+1    STORE BITS 16-23 IN DUB+1                    
         TM    DUB+1,X'01'         IS VERSION CONTROL SET IN BIT 23             
         JZ    *+8                                                              
         MVI   DUB,C'V'            YES:USE VSAM FOR THIS FILE                   
*                                                                               
* NEXT LOOK FOR A PARM= CARD OVERRIDE IN MASTC. THIS IS INTENDED FOR            
* STAGING OFFLINE REPORTS USING VSAM.                                           
*                                                                               
         LRL   RF,=V(SSB)                                                       
         USING SSBD,RF                                                          
         ICM   RF,15,SSOMASTC      A(MASTC)                                     
         JZ    DEMVS30             NO A(MASTC), SO NO PARM= CARD                
         USING MASTD,RF                                                         
         IILF  GR0,C'DE@ '         (NOTE: HIGH-HALF OF R0 IS UNCHANGED)         
         CLM   R0,B'1110',MCFFPARM DOES PARM FIELD START WITH "DE@" ?           
         JNE   DEMVS30             NO: IT'S NOT FOR US                          
         LA    RF,MCFFPARM+3-1(RE) RF=A(FLAG<1-5>) FOR FILE SET                 
         DROP  RF                                                               
         CLI   0(RF),C' '          IS AN OVERRIDE FLAG SET?                     
         JNH   DEMVS30             NO                                           
         MVC   DUB(1),0(RF)        SAVE OVERRIDE VALUE IN "DUB" H.O.B.          
*                                                                               
* MAKE SURE THAT THE FLAG HASN'T CHANGED SINCE WE WERE HERE LAST!               
*                                                                               
DEMVS30  DS    0H                                                               
         TM    XFLAG1,XFONLINE     ONLINE?                                      
         JO    DEMVS35             YES: FLAGS ARE SAVED IN TCB                  
*                                                                               
         LARL  R0,DEMVSON-1        POINT TO FLAG(0)                             
         J     DEMVS40                                                          
*                                                                               
DEMVS35  DS    0H                                                               
         ICM   R0,15,XTCBNTRY      R0 = A(TCB ENTRY)                            
         JZ    *+2                 WE MUST HAVE TCB ONLINE!                     
         AHI   R0,(TCBDMSTA-1)-TCBD  POINT TO FLAG(0)                           
*                                                                               
DEMVS40  DS    0H                                                               
         AR    RE,R0               POINT TO FLAG(1-5) FOR FILE SET              
         CLI   0(RE),C' '          FIRST CALL FOR TRANSACTION?                  
         JH    *+10                NO                                           
         MVC   0(1,RE),DUB         YES: SAVE THE FLAG VALUE                     
         CLC   0(1,RE),DUB         IS THE FLAG STILL THE SAME?                  
         JE    DEMVS45             YES: IT'S SAFE TO CONTINUE                   
         TM    XFLAG1,XFONLINE     ONLINE?                                      
         JO    *+2                 YES, SO FAIL                                 
         MVC   0(1,RE),DUB         RESET FLAG (FOR OFF-LINE STC JOBS)           
         DC    H'0'                NOW DIE                                      
*                                                                               
* IF DANDX, EXIT. IF VSAM, CONTINUE.                                            
*                                                                               
DEMVS45  DS    0H                                                               
         CLI   DUB,C'V'            CHECK VSAM/DANDX SWITCH: SET CC              
*                                                                               
DEMVS50  DS    0H                                                               
         L     RE,SAVERE                                                        
         BNE   4(,RE)              IF NOT VSAM, RETURN TO CALLER NOW            
*                                                                               
         TM    XFLAG1,XFONLINE     ONLINE?                                      
         JO    DEMVS60             YES: READ VIA VSAM                           
         LRL   RF,=V(SSB)                                                       
         USING SSBD,RF                                                          
         CLI   SSODMSTA,C' '       IS AN SSB OVERRIDE FLAG SET?                 
         JH    DEMVS60             YES: READ VIA VSAM                           
         TM    SSODMST3,SSODMFDX   FORCE DANDX (FOR DEBUGGING)?                 
         JZ    DEMVS60             NO                                           
         B     4(,RE)              RETURN TO CALLER                             
         DROP  RF                                                               
*                                                                               
DEMVS60  DS    0H                                                               
         MVC   DMCBW7+2(1),ACTION  PASS ACTION IN DMCBW7/B2                     
         LR    R0,R1               R1 IS WORK BASE. MUST SAVE IT                
         LA    R1,DMCBW1                                                        
         LRL   RF,VDMVSDEM         HANDLE I/O VIA VSAM                          
         BASR  RE,RF                                                            
         LR    R1,R0               RESTORE WORK BASE                            
         L     RE,SAVERE                                                        
         BR    RE                                                               
*                                                                               
         DS    0D                                                               
         DC    C'*DEMVSON:'        (NOTE CL9 SO LARL DEMVSON-1 WORKS)           
DEMVSON  DS    0C                  OFFLINE: SAVED PREVIOUS FLAG VALUES          
DEMVSDA  DC    C' '                DEMDIRA/FILA                                 
DEMVSDN  DC    C' '                DEMDIRN/FILA                                 
DEMVSDR  DC    C' '                DEMDIRR/FILA                                 
DEMVSNT  DC    C' '                NTIDIR/FIL                                   
DEMVSPV  DC    C' '                PAVDIR/FIL                                   
*                                                                               
VDMVSDEM DC    V(DMVSDEM)                                                       
*&&                                                                             
         EJECT                                                                  
***********************************************************************         
* BINARY SEARCH ROUTINE                                               *         
* NTRY: ACTION(2)=KEY                                                 *         
* EXIT: RF       =A(INSERT POINT)                                     *         
***********************************************************************         
BINSRCH  NTR1  BASE=*,LABEL=*                                                   
         XR    R2,R2               R2=LOW                                       
         LHI   R3,DMCMDS#-1        R3=HIGH                                      
*                                                                               
BSC02    CR    R2,R3               WHILE LOW <= HIGH                            
         BH    BSC04               R2 POINTS TO INSERTION POINT                 
*                                                                               
         LR    R4,R2               R4=MID                                       
         AR    R4,R3                                                            
         SRL   R4,1                MID=(HIGH+LOW)/2                             
*                                                                               
         LR    RF,R4                                                            
         MHI   RF,L'DMCMDS                                                      
         A     RF,=A(DMCMDS)                                                    
*                                                                               
         CLC   ACTION(2),0(RF)     COMPARE REQUESTED KEY WITH RECORD            
         BE    BSEXIT                                                           
         BL    *+12                                                             
         LA    R2,1(R4)            IF CC HIGH : LOW=MID+1                       
         B     BSC02                                                            
*                                                                               
         LR    R3,R4               IF CC LOW : HIGH=MID-1                       
         BCTR  R3,0                                                             
         B     BSC02                                                            
*                                                                               
BSC04    LR    RF,R2                                                            
         MHI   RF,L'DMCMDS                                                      
         A     RF,=A(DMCMDS)                                                    
*                                                                               
BSEXIT   XIT1  REGS=(RF)                                                        
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
* CALCULATE AVAILABLE TRACKS/USED TRACKS/TOTAL TRACKS                 *         
* R4=DTF, R3=RETURN DATA AREA PASSED BY ORIGINAL CALLER IN P3         *         
***********************************************************************         
         USING ISDTF,R4                                                         
ISTRACKS NTR1  BASE=*,LABEL=*                                                   
         LA    R2,ISXTNT                                                        
         USING EXTENTD,R2                                                       
         SR    R0,R0                                                            
         SR    R1,R1                                                            
ISTRACK1 CLI   0(R2),X'FF'                                                      
         BE    ISTRACK2                                                         
         TM    ISFTYPE,ISFTBIGF    TEST 20-BIT FILE                             
         BO    *+6                                                              
         SR    R0,R0               NO LAST EXTENT HOLDS TOTAL                   
         ICM   R1,3,EXT#TRKS       NUM OF TRACKS                                
         AR    R0,R1                                                            
         LA    R2,EXTLNQ(R2)                                                    
         B     ISTRACK1                                                         
                                                                                
ISTRACK2 ST    R0,8(R3)            SAVE TOTAL TRACKS                            
         SR    R1,R1                                                            
         L     R1,ISOVLAST                                                      
         SRL   R1,12               20-BIT                                       
         TM    ISFTYPE,ISFTBIGF                                                 
         BO    *+8                                                              
         SRL   R1,4                16-BIT                                       
         ST    R1,4(R3)            TOTAL TRACKS USED                            
         SR    R0,R1               TOTAL TRACKS - TRACKS USED                   
         ST    R0,0(R3)            TOTAL TRACKS AVAILABLE                       
         XIT1                                                                   
                                                                                
         LTORG                                                                  
         DROP  R2,R4                                                            
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO COMMIT UPDATES FOR SYSTEM IN UTL+4                       *         
***********************************************************************         
COMMIT   NMOD1 0,**DMCO**                                                       
         TM    XFLAG1,XFONLINE     IF ONLINE ONE CALL DOES ALL SYSTEMS          
         BO    COMMIT6                                                          
         ICM   RF,15,=V(UTL)       OFFLINE - MUST HAVE UTL                      
         JZ    *+2                 DIE=MUST HAVE UTL                            
         IC    R0,TSYS-UTLD(RF)    SAVE CURRENT SE NUM                          
*                                                                               
         L     R5,=V(SYSFLES)      COMMIT EACH OPEN SYSTEM OFFLINE              
         USING SYSFLSTD,R5                                                      
COMMIT1  L     RF,=V(UTL)                                                       
         ST    R5,FULL             SAVE A(HEADER FOR SYSTEM)                    
         MVC   TSYS-UTLD(1,RF),SYSFSYS#                                         
         CLM   R0,1,SYSFSYS#       COMMIT IF UTL SYSTEM                         
         BE    COMMIT6                                                          
         TM    SYSFSTAT,SYSFSINT   SKIP IF SYSTEM NEVER BEEN OPENED             
         BZ    COMMIT7                                                          
         SR    RE,RE                                                            
         ICM   RE,3,SYSF#FLS                                                    
         BZ    COMMIT7             SKIP IF SYSTEM HAS NO FILES                  
*                                                                               
         LA    R5,SYSFLIST         R5=A(FIRST FILE)                             
COMMIT3  TM    SYSFIND1,SFRCV      TEST IF THIS IS THE RECOVERY FILE            
         BO    COMMIT5                                                          
         LA    R5,SYSFLNQ(R5)      BUMP TO NEXT FILE                            
         BCT   RE,COMMIT3                                                       
         B     COMMIT7             JUST UNLOCK IF NO RECOVERY FILE              
*                                                                               
COMMIT5  XR    RE,RE               RE=A(RECOVERY FILE)                          
         ICM   RE,7,SYSFADTF                                                    
         TM    DIND-DTF(RE),DINDVRA    IS VERMONT RECOVERY ACTIVE?              
         BZ    COMMIT7                                                          
         TM    DTFOPEN-DTF(RE),DTF_OPN IS RECOVERY FILE OPEN?                   
         BZ    COMMIT7                                                          
         TM    DTFOPEN-DTF(RE),DTF_RO  IS RECOVERY FILE READONLY?               
         BO    COMMIT7                                                          
*                                                                               
COMMIT6  XC    PARAM1(24),PARAM1   CALL DMRCVR TO COMMIT TRANSACTION            
         MVI   PARAM3+1,X'FE'                                                   
         GOTO1 =V(DMRCVR)                                                       
*                                                                               
COMMIT7  XC    PARAM1(20),PARAM1   CALL LOCKER TO UNLOCK EVERYTHING             
         ICM   RF,15,=V(LOCKER)                                                 
         BZ    *+6                                                              
         BASR  RE,RF                                                            
*                                                                               
COMMIT8  TM    XFLAG1,XFONLINE     EXIT IF ONLINE - ALL SYSTEMS DONE            
         BO    COMMITX                                                          
         L     R5,FULL             RESTORE A(HEADER FOR SYSTEM)                 
         LH    RF,SYSF#FLS                                                      
         SLL   RF,3                                                             
         LA    R5,SYSFLIST(RF)     BUMP TO NEXT SYSTEM                          
         CLI   0(R5),X'FF'                                                      
         BNE   COMMIT1                                                          
*                                                                               
         ICM   RF,15,=V(SSB)       ALL OFFLINE SYSTEMS DONE                     
         BZ    COMMIT9                                                          
         USING SSOOFF,RF                                                        
         CLI   SSOXTND,X'FF'       TEST EXTENDED SSB                            
         BNE   COMMIT9                                                          
         XC    SSOGIN,SSOGIN       CLEAR GIN AFTER OFFLINE COMMIT               
         DROP  RF                                                               
*                                                                               
COMMIT9  L     RF,=V(UTL)                                                       
         STC   R0,TSYS-UTLD(RF)    RESTORE CURRENT SE NUM                       
COMMITX  XIT1                                                                   
         DROP  R5                                                               
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO OPEN/CLOSE LIST OF FILES FOR THE NAMED SYSTEM            *         
***********************************************************************         
DMODOP   NMOD1 0,**DMOP**                                                       
         LR    RC,R1                                                            
         MVC   PARAM1(4),=V(FINDSYS)                                            
         XC    PARAM2(4),PARAM2                                                 
         XR    R6,R6                                                            
         ICM   R6,7,DMCBW2+1                                                    
         LA    R7,DMODOSYS         CHECK SYSTEM NAME                            
         CLC   0(3,R6),0(R7)                                                    
         BE    DMODOP0                                                          
         LA    R7,4(R7)                                                         
         CLI   0(R7),C' '                                                       
         BNE   *-18                                                             
         DC    H'0'                DIE=SYS NOT IN LIST                          
*                                                                               
DMODOP0  MVC   PARAM2(1),3(R7)     GET SYS NUMBER FROM TABLE                    
         ICM   RE,15,=V(UTL)                                                    
         JZ    *+2                 DIE=MUST HAVE UTL                            
         CLI   3(R7),0                                                          
         BNE   DMODOP1                                                          
         MVC   PARAM2(1),4(RE)     ELSE GET SYS NUMBER FROM UTL                 
*                                                                               
DMODOP1  L     RF,=V(DMOD000)      GET A(SYSFILE LIST)                          
         BASR  RE,RF                                                            
         XR    R6,R6                                                            
         ICM   R6,7,DMCBW1+1                                                    
         CLC   0(6,R6),=C'DMCLSE'                                               
         BNE   *+14                                                             
         MVC   PARAM1(4),=V(CLSESYS)   SET DMOD000 COMMAND                      
         B     DMODOPX                                                          
*                                                                               
DMODOP1A MVI   BYTE,0              INITIALIZE                                   
         CLC   0(3,R7),=C'UTL'     UTL SPECIAL FOR SYSTEM                       
         BNE   DMODOP1B                                                         
         L     R7,DMCBW3           R7=A(USERS LIST OF FILES)                    
         CLC   1(5,R7),=CL5'ALL'   MATCH ON NALL OR UALL                        
         BNE   DMODOP1B                                                         
         OI    BYTE,SYSALL         PROCESS ALL FILES                            
                                                                                
DMODOP1B L     R6,PARAM2           R6=A(SYSFILE LIST)                           
         USING SYSFLSTD,R6                                                      
         LH    R0,SYSF#FLS         R0=NUM OF FILES IN LIST                      
         TM    SYSFSTAT,SYSFSINT                                                
         BO    DMODOP1X                                                         
         LA    R6,SYSFLIST                                                      
         MVI   FULL,0              USED TO HOLD OFFLINE SSB FLAGS               
                                                                                
         ICM   RF,15,=V(SSB)                                                    
         BZ    DMODOP1C                                                         
         USING SSBD,RF                                                          
         CLC   0(3,RF),=X'0000FF'  TEST OFFLINE EXTENDED SSB                    
         BNE   DMODOP1C                                                         
         MVC   FULL(1),SSOFLAG1    EXTRACT OFFLINE SSB FLAGS                    
*                                                                               
DMODOP1C OI    SYSFIND2,SF_RO      SET ALL TO READONLY                          
         TM    BYTE,SYSALL         TEST TO PROCESS ALL FILES                    
         BO    *+8                                                              
         OI    SYSFIND1,SFNOP      SET ALL FILES TO NOP                         
                                                                                
         SR    RE,RE               RE=A(DTF)                                    
         ICM   RE,7,SYSFADTF                                                    
         USING DTFPHD,RE                                                        
         CLI   PARAM2,X'0A'        TEST CONTROL SYSTEM                          
         BE    DMODOP1G                                                         
         CLI   SYSFILE#,X'A0'      NO IGNORE CONTROL SYSTEM FILES               
         BL    DMODOP1G                                                         
         CLI   SYSFILE#,X'AF'                                                   
         BNH   DMODOP1I                                                         
                                                                                
DMODOP1G TM    FULL,SSOFLOCL       TEST GLOBAL=N                                
         BZ    DMODOP1H                                                         
         NI    DTFFLAG,255-DTFGLOB SET NOT A GLOBAL FILE                        
         B     DMODOP1I                                                         
                                                                                
DMODOP1H TM    FULL,SSOFGLOB       TEST GLOBAL=Y                                
         BZ    *+12                                                             
         OI    DTFFLAG,DTFGLOB     SET GLOBAL FILE                              
         B     DMODOP1I                                                         
         DROP  RE                                                               
                                                                                
DMODOP1I LA    R6,SYSFLNQ(R6)      BUMP TO NEXT FILE                            
         BCT   R0,DMODOP1C                                                      
                                                                                
         L     R6,PARAM2           RELOAD A(FILE LIST FOR SYSTEM)               
         OI    SYSFSTAT,SYSFSINT   SET FIRST OPEN INITIALIZE DONE               
         DROP  RF                                                               
                                                                                
DMODOP1X L     R7,DMCBW3           R7=A(FILE OPEN LIST)                         
*                                                                               
DMODOP2  CLI   0(R7),C'Z'          TEST IGNORE THIS ITEM                        
         BE    DMODOP5                                                          
*                                                                               
         CLC   1(2,R7),=C'L='      OPEN PRESET LIST OF FILES                    
         BNE   DMODOP3                                                          
         LA    RF,OPENLSTS         TABLE OF LISTS                               
*                                                                               
DMODOP2D CLI   0(RF),X'FF'         TEST END OF SYSFLES LIST                     
         JE    *+2                 DIE=OPEN LIST NAME                           
*                                                                               
         CLC   3(5,R7),0(RF)       MATCH ON LIST NAME?                          
         BE    DMODOP2J                                                         
         LA    RF,5(RF)                                                         
DMODOP2G CLI   0(RF),X'FE'         ANY MORE DDNAMES IN THIS LIST?               
         BNE   *+12                                                             
         LA    RF,1(RF)                                                         
         B     DMODOP2D                                                         
         LA    RF,7(RF)            BUMP TO NEXT DDNAME                          
         B     DMODOP2G                                                         
*                                                                               
DMODOP2J DS    0H                  RF=A(THE DDNAME LIST TO OPEN)                
         LA    RF,5(RF)                                                         
DMODOP2L MVC   DUB(1),0(R7)        UPDATIVE FLAG FROM CALLER'S LIST             
         MVC   DUB+1(7),0(RF)      DDNAME FROM HARD-CODED TABLE ENTRY           
         BAS   RE,OPEN1FIL         OPEN ONE FILE                                
         LA    RF,7(RF)            DUMP TO NEXT DDNAME                          
         CLI   0(RF),X'FE'         ANY MORE DDNAMES IN LIST?                    
         BNE   DMODOP2L                                                         
         B     DMODOP5                                                          
*                                                                               
DMODOP3  MVC   DUB,0(R7)           COPY LIST ENTRY INTO DUB                     
         BAS   RE,OPEN1FIL         OPEN ONE FILE                                
*                                                                               
DMODOP5  LA    R7,8(R7)            BUMP FILE OPEN LIST                          
         CLI   0(R7),C'X'                                                       
         BNE   DMODOP2             PROCESS NEXT DDNAME                          
*                                                                               
         MVC   PARAM1(4),=V(OPENSYS)                                            
*                                                                               
DMODOPX  MVC   PARAM2+1(3),DMCBW4+1                                             
         XC    PARAM3(2),PARAM3                                                 
         L     RF,=V(DMOD000)                                                   
         BASR  RE,RF               OPEN (OR CLOSE) OPERATIONAL FILES            
*                                                                               
         OC    PARAM3(2),PARAM3                                                 
         BZ    *+8                                                              
         MVI   DMCBW3,X'08'        RETURN MAINT ERROR                           
         XIT1                                                                   
         EJECT                                                                  
***********************************************************************         
* FIELD 'DUB' MUST CONTAIN AN 8-BYTE FILE OPEN TABLE ENTRY            *         
* R6=A(SYSTEM HEADER). WHERE TO SEARCH / CROSS REFERENCE              *         
* VALIDATES FILE EXIST AND TURNS ON/OFF SEARCH FOR EOF AND NOP        *         
***********************************************************************         
OPEN1FIL NTR1                                                                   
         LH    RE,SYSF#FLS         RE=NUMBER OF FILES IN SYSTEM                 
         LA    RF,SYSFLIST         RF=A(LIST OF FILES FOR SYSTEM)               
*&&US                                                                           
         CLC   DUB+1(3),=C'DEM'    US DEMO FILES MATCH ON 7 CHRS                
         BE    OPNFL30                                                          
         CLC   DUB+1(3),=C'PAV'    US DEMO FILES MATCH ON 7 CHRS                
         BE    OPNFL30                                                          
         CLC   DUB+1(3),=C'NTI'    US DEMO FILES MATCH ON 7 CHRS                
         BE    OPNFL30                                                          
*&&                                                                             
         CLC   DUB+1(4),=C'PRTQ'   PRTQ FILES MATCH ON 4 CHRS                   
         BE    OPNFL40                                                          
*                                                                               
SRCH     USING SYSFLSTD,RF                                                      
         CLC   DUB+1(5),=CL5'ALL'  TEST TO OPEN ALL FILES                       
         BNE   OPNFL10                                                          
                                                                                
OPNFL08  CLI   DUB,C'U'            TEST OPEN UPDATIVE                           
         BNE   *+8                                                              
         NI    SRCH.SYSFIND2,255-SF_RO                                          
         LA    RF,SYSFLNQ(RF)                                                   
         BCT   RE,OPNFL08          BUMP TO NEXT FILE                            
         B     OPNFLXIT                                                         
                                                                                
OPNFL10  L     R5,SRCH.SYSFADTF-1  TRY MATCH ON FIRST 6 CHRS                    
         CLC   DUB+1(6),22(R5)                                                  
         BE    OPNFL70                                                          
         LA    RF,SYSFLNQ(RF)                                                   
         BCT   RE,OPNFL10                                                       
                                                                                
         LH    RE,SYSF#FLS         IF FAIL TRY TO MATCH ON 4 CHRS               
         LA    RF,SYSFLIST                                                      
OPNFL20  L     R5,SRCH.SYSFADTF-1                                               
         CLC   DUB+1(4),22(R5)                                                  
         BE    OPNFL70                                                          
         LA    RF,SYSFLNQ(RF)                                                   
         BCT   RE,OPNFL20                                                       
         DROP  R6                                                               
                                                                                
         MVC   DUB+0(1),PARAM2     GET SE NUM                                   
         LM    R3,R4,DUB                                                        
         ABEND 103,DUMP            FILE NAME NOT IN LIST                        
*                                                                               
*&&US                                                                           
OPNFL30  LR    R5,RE               SAVE RE FOR NOW                              
*        REPEAT FOR DEMDIRA, NTIDIR, PAVDIR WHEN VSAM VERSION AVAILABLE         
         CLC   DUB+1(7),=C'DEMDIRN' 'N' FILES USING VSAM                        
         BNE   *+16                                                             
         L     R4,=V(DEMVSMN)                                                   
         ICM   R4,B'1000',=AL1(2)  N FILES ARE FILE SET 2                       
         BRAS  RE,OPNFLVSM         OPEN DEMVSMN IF AVAILABLE                    
         CLC   DUB+1(7),=C'DEMDIRR' 'R' FILES USING VSAM                        
         BNE   *+16                                                             
         L     R4,=V(DEMVSMR)                                                   
         ICM   R4,B'1000',=AL1(3)  R FILES ARE FILE SET 3                       
         BRAS  RE,OPNFLVSM         OPEN DEMVSMR IF AVAILABLE                    
OPNFL32  LR    RE,R5               RESTORE RE                                   
OPNFL34  L     R5,SRCH.SYSFADTF-1  MATCH ON FIRST 7 CHRS FOR US DEMOS           
         CLC   DUB+1(7),22(R5)                                                  
         BE    OPNFL70                                                          
         LA    RF,SYSFLNQ(RF)                                                   
         BCT   RE,OPNFL34                                                       
         B     OPNFLXIT            SKIP IF NOT FOUND                            
*                                                                               
* FILE OPEN SUBROUTINE FOR VSAM DEMO FILES OPENED WHEN CORRESPONDING            
* DANDX DIRECTORY IS OPENED                                                     
*                                                                               
OPNFLVSM NTR1  WORK=(R5,4)         4*8 BYTES FOR PARMS                          
         L     RF,=V(SSB)                                                       
         USING SSBD,RF                                                          
         TM    XFLAG1,XFONLINE     ONLINE?                                      
         JO    OPNFLV20            YES: EXTRACT DATASPACE FLAG FOR FILE         
*                                                                               
         CLI   SSOXTND,X'FF'       OFFLINE: NEED EXTENDED SSB                   
         JNE   *+2                 WE GOTTA HAVE THIS                           
*                                                                               
* FIRST CHECK FOR AN OVERRIDE IN THE LOCAL SSB, SEE DEMVSAM ROUTINE             
* FOR FULL EXPLANATION                                                          
*                                                                               
         CLI   SSODMSTA,C' '       IS AN SSB OVERRIDE FLAG SET?                 
         JNH   OPNFLV10            NO                                           
         CLI   SSODMSTA,C'V'       YES: HONOR IT                                
         JE    OPNFLV50            EITHER MUST OPEN VSAM                        
         J     OPNFLVX             OR MUST NOT OPEN IT                          
*                                                                               
OPNFLV10 OC    SSOALET,SSOALET     DO WE HAVE DATAMGR DATASPACE ALET?           
         JNZ   OPNFLV20            YES: NO NEED TO GET IT AGAIN                 
         LR    R0,R1               SAVE R1                                      
         LR    R1,R5                                                            
         XC    0(8,R1),0(R1)       NO: MUST GET ALET (FIRST TIME)               
         OI    0(R1),X'20'         SET ENQUIRY ONLY                             
         L     RF,=V(LOCKSPC)      SET THE DATAMGR DATASPACE ALET               
         BASR  RE,RF               GO TO LOCKSPC                                
         LR    R1,R0               RESTORE R1                                   
         L     RF,=V(SSB)          RESET RF                                     
*                                                                               
* EXTRACT THE DEMOS ACCESS METHOD FLAG FROM THE DATASPACE FOR THE               
* GIVEN DEMOFILE.                                                               
*                                                                               
OPNFLV20 DS    0H                                                               
         LAM   ARF,ARF,SSBALET     NOTE: SSOALET HAS SAME DISPLACEMENT          
         DROP  RF                                                               
*                                                                               
         SR    RF,RF                                                            
         SAC   512                                                              
         L     RF,DHASSBG-DMDHDR(,RF)      A(GLOBAL SSB)                        
         LR    RE,R4               GET FILE SET NUMBER FROM R4 HOB              
         SRL   RE,24                                                            
         LA    RF,SSGDMSTA-FASSBG-1(RE,RF) RF=A(FLAG<1-5>) FOR FILE SET         
         MVC   0(1,R5),0(RF)       SAVE DATASPACE VALUE                         
*                                                                               
         SAC   0                                                                
         SR    R0,R0                                                            
         SAR   ARF,R0              CLEAR ARF (VIA CLEARED R0)                   
*                                                                               
         CLI   0(R5),C'X'          IS THE VSAM KILL SWITCH SET?                 
         JE    OPNFLVX             YES, DON'T OPEN THE VSAM FILE                
*                                                                               
OPNFLV50 LR    R0,R1               SAVE R1                                      
         LR    R1,R5                                                            
         MVC   0(4,R1),=V(VSOPEN)  P1=A(VSOPEN)                                 
         XC    4(8*3,R1),4(R1)                                                  
         STCM  R4,B'0111',12+1(R1) P4=A(DTF) (WITHOUT FILE NUMBER)              
         L     RF,=V(DMVSAM)       DMVSAM OPENS VSAM FILES                      
         GOTOR (RF)                                                             
         OC    8(2,R1),8(R1)       TEST ERRORS                                  
         JNZ   *+2                 CAN'T OPEN FILE                              
         LR    R1,R0               RESTORE R1                                   
OPNFLVX  XIT1  ,                   EXIT                                         
*&&                                                                             
*                                                                               
OPNFL40  L     R5,SRCH.SYSFADTF-1  MATCH ON FIRST 4 CHRS FOR PRTQ               
         CLC   DUB+1(4),22(R5)                                                  
         BE    OPNFL50             FILE NAME STARTS WITH C'PRTQ'                
         LA    RF,SYSFLNQ(RF)                                                   
         BCT   RE,OPNFL40                                                       
         B     OPNFLXIT                                                         
                                                                                
OPNFL50  CLI   DUB+5,C'U'          TEST IF SPECIFIC PRTQ NAMED                  
         BE    OPNFL60                                                          
         CLC   DUB+1(5),22(R5)     MATCH ON SPECIFIC NAME                       
         BE    OPNFL70                                                          
         LA    RF,SYSFLNQ(RF)                                                   
         BCT   RE,OPNFL40                                                       
                                                                                
OPNFL60  NI    SRCH.SYSFIND1,255-SFNOP TURN OFF NOP,GENERIC MATCH               
         LA    RF,SYSFLNQ(RF)                                                   
         BCT   RE,OPNFL40          CONTINUE SEARCH FOR OTHER PRTQS              
         B     OPNFLXIT                                                         
*                                                                               
OPNFL70  NI    SRCH.SYSFIND1,255-SFNOP TURN OFF NOP                             
         CLI   DUB,C'U'                                                         
         BNE   *+8                                                              
         NI    SRCH.SYSFIND2,255-SF_RO TURN OFF READONLY                        
         DROP  SRCH                                                             
*                                                                               
OPNFLXIT DS    0H                                                               
         XIT1                                                                   
                                                                                
         LTORG                                                                  
*                                                                               
DMODOSYS DC    C'FIL',X'00'                                                     
         DC    C'SER',X'01'                                                     
*&&US*&& DC    C'SPO',X'00'                                                     
*&&US*&& DC    C'NET',X'00'                                                     
*&&US*&& DC    C'PRI',X'00'                                                     
*&&US*&& DC    C'PRN',X'00'                                                     
*&&UK*&& DC    C'MED',X'00'                                                     
         DC    C'MPL',X'00'                                                     
         DC    C'ACC',X'00'                                                     
*&&US*&& DC    C'TAL',X'00'                                                     
*&&UK*&& DC    C'FEE',X'00'                                                     
*&&US*&& DC    C'REP',X'00'                                                     
         DC    C'MBA',X'00'                                                     
         DC    C'CON',X'00'                                                     
*&&US*&& DC    C'CPP',X'00'                                                     
*&&US*&& DC    C'DEM',X'00'                                                     
         DC    C'PER',X'00'                                                     
*&&US*&& DC    C'STR',X'00'                                                     
         DC    C'UTL',X'00'                                                     
         DC    C'   ',X'00'                                                     
*&&US                                                                           
TRFOPLST DC    256CL1'S'           INDEXED BY SENUM                             
*                                  S ENTRY=READ SPOT FILES                      
*                                  T ENTRY=READ TRAFFIC FILES                   
*&&                                                                             
                                                                                
OPENLSTS DC    0C                  LIST OF PREDEFINED FILE OPEN LISTS           
*                                                                               
* THIS IS A SERIES OF PREDEFINED, HARD-CODED FILE OPEN LISTS. EACH              
* LIST NAME CAN BE UP TO 5 CHARACTERS LONG, AND IS REQUESTED IN THE             
* CALLER'S FILE OPEN LIST WITH A SYNTAX OF L=<LIST NAME>. FOR EXAMPLE,          
* IF AN ENTRY IN THE CALLER'S LIST SAYS C'NL=DEMFL', THAT MEANS THAT WE         
* MUST OPEN ALL OF THE DTFS READONLY WHOSE NAMES APPEAR IN THE "DEMFL"          
* LIST BELOW. EACH DDNAME LIST IS TERMINATED BY A X'RE', AND THE ENTIRE         
* TABLE IS TERMINATED BY A X'FF'.                                               
*                                                                               
*&&US                                                                           
         DC    CL5'DEMFN'          L=DEMFN: DIRECTORY DEMDIRN                   
         DC    CL7'DEMFILN'                                                     
         DC    CL7'DEMFILO'                                                     
         DC    CL7'DEMFILP'                                                     
         DC    CL7'DEMFILQ'                                                     
         DC    CL7'DEMFILU'                                                     
         DC    CL7'DEMFILV'                                                     
         DC    CL7'DEMFILW'                                                     
         DC    CL7'DEMFILY'                                                     
         DC    CL7'DEMFILI'                                                     
         DC    CL7'DEMFILJ'                                                     
         DC    CL7'DEMFILK'                                                     
         DC    CL7'DEMFILL'                                                     
         DC    CL7'DEMFILM'                                                     
         DC    CL7'DEMFILS'                                                     
         DC    CL7'DEMFILZ'                                                     
         DC    CL7'DEMFILB'                                                     
         DC    CL7'DEMFILC'                                                     
         DC    CL7'DEMFILD'                                                     
         DC    CL7'DEMFILE'                                                     
         DC    CL7'DEMFILF'                                                     
         DC    CL7'DEMFILG'                                                     
         DC    CL7'DEMFILH'                                                     
         DC    CL7'DEMFIL3'                                                     
         DC    CL7'DEMFIL5'                                                     
         DC    CL7'DEMFIL7'                                                     
         DC    CL7'DEMFIL9'                                                     
         DC    CL7'DEMFLTN'                                                     
         DC    CL7'DEMFIL1'                                                     
         DC    CL7'DEMFIL6'                                                     
         DC    CL7'DEMFIL8'                                                     
         DC    CL7'DEMFIL$'                                                     
         DC    CL7'DEMFLNA'                                                     
         DC    CL7'DEMFLNB'                                                     
         DC    CL7'DEMFLNC'                                                     
         DC    CL7'DEMFLND'                                                     
         DC    CL7'DEMFLNE'                                                     
         DC    CL7'DEMFLNF'                                                     
         DC    CL7'DEMFLNG'                                                     
         DC    CL7'DEMFLNH'                                                     
         DC    CL7'DEMFLNI'                                                     
         DC    CL7'DEMFLNJ'                                                     
         DC    CL7'DEMFLNK'                                                     
         DC    CL7'DEMFLNL'                                                     
         DC    CL7'DEMFLNM'                                                     
         DC    X'FE'                                                            
*                                                                               
         DC    CL5'NTIFL'          L=NTIFL: DIRECTORY NTIDIR                    
         DC    CL7'NTIFILA'                                                     
         DC    CL7'NTIFILB'                                                     
         DC    CL7'NTIFILC'                                                     
         DC    CL7'NTIFILD'                                                     
         DC    CL7'NTIFILE'                                                     
         DC    CL7'NTIFILF'                                                     
         DC    CL7'NTIFILG'                                                     
         DC    CL7'NTIFILH'                                                     
         DC    CL7'NTIFILI'                                                     
         DC    CL7'NTIFILJ'                                                     
         DC    CL7'NTIFILK'                                                     
         DC    CL7'NTIFILL'                                                     
         DC    CL7'NTIFILM'                                                     
         DC    CL7'NTIFILN'                                                     
         DC    CL7'NTIFILO'                                                     
         DC    CL7'NTIFILP'                                                     
         DC    CL7'NTIFILQ'                                                     
         DC    CL7'NTIFILR'                                                     
         DC    CL7'NTIFILW'                                                     
         DC    CL7'NTIFILY'                                                     
         DC    CL7'NTIFILZ'                                                     
         DC    CL7'NTIFLTA'                                                     
         DC    X'FE'                                                            
*                                                                               
         DC    CL5'PAVFL'          L=PAVFL: DIRECTORY PAVDIR                    
         DC    CL7'PAVFIL '                                                     
         DC    CL7'PAVFILA'                                                     
         DC    CL7'PAVFILB'                                                     
         DC    CL7'PAVFILC'                                                     
         DC    CL7'PAVFLTA'                                                     
         DC    X'FE'                                                            
*                                                                               
         DC    CL5'DEMFA'          L=DEMFA: DIRECTORY DEMDIRA                   
         DC    CL7'DEMFILA'                                                     
         DC    CL7'DEMFIL2'                                                     
         DC    CL7'DEMFIL4'                                                     
         DC    CL7'DEMFILT'                                                     
         DC    CL7'DEMFLTA'                                                     
         DC    X'FE'                                                            
*                                                                               
         DC    CL5'DEMFR'          L=DEMFR: DIRECTORY DEMDIRR                   
         DC    CL7'DEMFILR'                                                     
         DC    CL7'DEMFLTR'                                                     
         DC    X'FE'                                                            
*&&                                                                             
         DC    X'FF'               EOT                                          
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO READ BLOCKED DA FILES (RECOVERY)                         *         
***********************************************************************         
DBSRD    NMOD1 0,**BSRD**                                                       
         LR    RC,R1                                                            
         BAS   RE,SETDUM           SET DTF ADDRESS IN PARAM4                    
         BASR  RE,RF                                                            
*                                                                               
DBSRD0   SR    R0,R0               RESET NOT FOUND SWITCH                       
         L     R6,DMCBW3           GET LAST DISK ADDR READ                      
         OC    0(4,R6),0(R6)       TEST FOR FIRST CALL                          
         BZ    DBSRD4                                                           
*                                                                               
         CLI   TXTDSN,C' '         ANY SEQ DATASET OPEN                         
         JE    DBSRD1                                                           
         GET   MVSFILE             GET A RECORD                                 
         LR    RE,R1                                                            
         LR    R1,RC               RESTORE R1                                   
         LA    R0,4(RE)            MOVE FROM REC+4                              
         L     RF,0(RE)                                                         
         SRL   RF,16                                                            
         AHI   RF,-4               LEN = REC-4                                  
         STCM  RF,3,DMCBW5+2       SAVE LENTH IN P5                             
         L     RE,DMCBW4                                                        
         LR    R1,RF                                                            
         MVCL  RE,R0               COPY RECORD TO IOAREA                        
*                                                                               
         LR    R1,RC               RESTORE R1 AGAIN                             
         CLI   JOBHDR,C'X'         WAS IS AN XJOB HEADER                        
         JNE   DBSRDX                                                           
         L     RE,DMCBW4                                                        
         MVI   4(RE),RBACKOUT      SET AS DELETED                               
         B     DBSRDX                                                           
*                                                                               
DBSRD1   L     RE,PARAM4           GET DTF ADDRESS                              
         TM    DTFTYPE-DTFPHD(RE),DTFTBIGF                                      
         BZ    DBSRD1B                                                          
DBSRD1A  SR    R3,R3               20-BIT TRACK                                 
         IC    R3,3(R6)                                                         
         LA    R3,1(R3)                                                         
         STC   R3,3(R6)            BUMP REC NUMBER                              
         CLI   3(R6),0                                                          
         BNE   DBSRD1C                                                          
         IC    R3,2(R6)            GET BLK NUMBER FROM TTTTTBRR                 
         SLL   R3,28                                                            
         SRL   R3,28                                                            
         LA    R3,1(R3)                                                         
         STC   R3,3(R6)                                                         
         NI    2(R6),X'F0'                                                      
         OC    2(1,R6),3(R6)       SET BLK NUM                                  
         MVI   3(R6),1             SET REC 1                                    
         B     DBSRD1C                                                          
DBSRD1B  LH    R3,2(R6)            16-BIT BUMP BLK/REC NUMBER                   
         LA    R3,1(R3)                                                         
         STH   R3,2(R6)                                                         
         CLI   3(R6),0                                                          
         BNE   *+8                                                              
         MVI   3(R6),1                                                          
*                                                                               
DBSRD1C  L     RF,=V(READ)         SET COMMAND                                  
         TM    DMCBW1,X'24'        TEST FOR READ FLUSH                          
         BNO   *+8                                                              
         L     RF,=V(READFLS)                                                   
         BAS   RE,SETPR            SET PARAM LIST                               
         BASR  RE,RF                                                            
*                                                                               
DBSRD1F  TM    PARAM3+1,X'04'      TEST EOF                                     
         BO    DBSRDX                                                           
         OC    PARAM3(2),PARAM3    TEST FOR ERRORS                              
         BNZ   DBSRD2                                                           
         OC    PARAM3+2(2),PARAM3+2                                             
         BZ    DBSRD0              IGNORE ZERO LENGTH RECORD                    
*                                                                               
         L     RF,PARAM2                                                        
         USING RECVREC,RF                                                       
         CLC   RCVHTYP,=C'XXSJOBXX'     IS THIS AN XXSJOBXX HEADER              
         JE    *+14                                                             
         CLC   RCVHTYP,=C'**SJOB**'     IS THIS AN **SJOB** HEADER              
         JNE   DBSRD1G                                                          
         CLC   PARAM3+2(2),=Y(RCVHDSNL) WITH RECOVERY FILE DSN                  
         JNE   DBSRD1G                                                          
         TM    DMCBW1,X'2C'             IF 2C DO NOT FOLLOW MVS FILE            
         BO    DBSRD1G                                                          
         TM    DMCBW1,X'28'             DO WE WANT DELETED                      
         BO    *+14                                                             
         CLC   RCVHTYP,=C'XXSJOBXX'     NO DON'T FOLLOW XXSJOB FILE             
         JE    DBSRD0                                                           
         MVC   TXTDSN,RCVHDSN           SET DSN IN DYNALLOC BLOCK               
         MVC   JOBHDR,RCVHTYP           SAVE SJOB TYPE FOR DELETES              
         MVC   RCVHTYP,=C'**SJOB**'     ALWAYS RETURN AS **SJOB**               
         DROP  RF                                                               
*                                                                               
         LA    R1,ARBLKCTA                                                      
         DYNALLOC                  CALL DYNALLOC TO GET SEQ FILE                
         LTR   RF,RF                                                            
         JZ    DBSRD1FA                                                         
*                                                                               
         MVC   MSGDSN,TXTDSN                                                    
         LR    R1,RC                                                            
         GOTO1 =V(HEXOUT),DUB,RBLKERR,ALLOCERR+28,2                             
         LR    R1,RC                                                            
         GOTO1 =V(DDWTO),DUB,ALLOCERR                                           
         LR    R1,RC                                                            
         MVI   TXTDSN,C' '         CLEAN UP FIELDS AND CONTINUE                 
         MVC   TXTDSN+1(L'TXTDSN-1),TXTDSN                                      
         MVI   JOBHDR,C' '                                                      
         MVC   JOBHDR+1(L'JOBHDR-1),JOBHDR                                      
         J     DBSRD1G                                                          
*                                                                               
DBSRD1FA OPEN  (MVSFILE,INPUT)     OPEN SEQ FILE FOR INPUT                      
         LR    R1,RC                                                            
*                                                                               
DBSRD1G  L     R3,AFILE            POINT TO FILE TBL ENTRY                      
         USING DMGRFLD,R3                                                       
         CLI   DMGLDELL,0          DOES FILE HAVE LOGICAL DELETED RECS          
         BE    DBSRD1H                                                          
         TM    DMCBW1,X'28'        YES TEST IF CALLER WANTS THEM                
         BO    DBSRD1H                                                          
         L     R6,DMCBW4           POINT TO RECORD                              
         SR    R9,R9                                                            
         IC    R9,DMGLDELD         DISPLACEMENT TO DELETE INFO                  
         AR    R6,R9               POINT TO FIELD IN RECORD                     
         IC    R9,DMGLDELL         GET LEN OF FIELD                             
         BCTR  R9,0                                                             
         EX    R9,*+8                                                           
         B     *+10                                                             
         CLC   0(0,R6),=8X'FF'                                                  
         BE    DBSRD0              IGNORE IF FIELD IS ALL FF'S                  
         DROP  R3                                                               
*                                                                               
DBSRD1H  DS    0H                  REMOVE TASKID FROM RECOVERY RECORDS          
*&&DO                              WE MAY INSTALL                               
         USING DMGRFLD,R6                                                       
         L     R6,AFILE                                                         
         TM    DMGIND,DMGIRCV      TEST IF RECOVERY FILE                        
         BZ    DBSRD1J                                                          
         L     R6,DMCBW4                                                        
         USING RECVREC,R6                                                       
         CLI   RTASKID,X'FF'       LEAVE DELETED RECORDS AS DELETED             
         BE    DBSRD1J                                                          
         TM    XFLAG1,XFONLINE     ONLINE                                       
         BO    DBSRD1J             DON'T BOTHER IF ON-LINE                      
         LRL   RF,=V(SSB)                                                       
         USING SSBD,RF                                                          
         TM    SSOMTIND,SSORTSK#   PASS RTASKID TO APPLICATION                  
         BO    DBSRD1J             YES                                          
         MVI   RTASKID,0           MOST APPLICAITON DON'T NEED TASKID           
         DROP  R6,RF               RECVREC, SSBD                                
*&&                                                                             
DBSRD1J  MVC   DMCBW5+2(2),PARAM3+2                                             
         B     DBSRDX                                                           
*                                                                               
DBSRD2   TM    PARAM3+1,X'08'      TEST NO REC FOUND                            
         BO    *+12                                                             
DBSRD3   OI    DMCBW3,X'40'        SET HARD ERROR                               
         B     DBSRDX                                                           
         LTR   R0,R0               TEST PRIOR REC NOT FOUND                     
         BNZ   DBSRD3C                                                          
         LA    R0,2                SET NFSW                                     
         L     RE,PARAM4           BUMP TO NEXT BLOCK                           
         TM    DTFTYPE-DTFPHD(RE),DTFTBIGF                                      
         BZ    DBSRD3B                                                          
DBSRD3A  IC    R3,2(R6)            20-BIT TRACK                                 
         SLL   R3,28                                                            
         SRL   R3,28               GET BLOCK NUMBER FROM TTTTTBRR               
         LA    R3,1(R3)                                                         
         STC   R3,3(R6)                                                         
         NI    2(R6),X'F0'                                                      
         OC    2(1,R6),3(R6)       SET BLK NUM                                  
         MVI   3(R6),1             SET REC 1                                    
         B     DBSRD1C                                                          
DBSRD3B  SR    R3,R3               16-BIT TRACK                                 
         IC    R3,2(R6)                                                         
         LA    R3,1(R3)                                                         
         STC   R3,2(R6)                                                         
         MVI   3(R6),1             SET REC1                                     
         B     DBSRD1C                                                          
DBSRD3C  BCT   R0,DBSRD4           NOT FOUND - TRY NEXT TRACK?                  
         MVI   DMCBW3,X'80'        SET EOF                                      
         B     DBSRDX                                                           
*                                                                               
DBSRD4   L     RE,PARAM4           TRY NEXT TRACK                               
         TM    DTFTYPE-DTFPHD(RE),DTFTBIGF                                      
         BZ    DBSRD4B                                                          
DBSRD4A  L     R6,DMCBW3           20-BIT TRACK                                 
         SR    R9,R9                                                            
         ICM   R9,7,0(R6)                                                       
         SRL   R9,4                                                             
         LA    R9,1(R9)                                                         
         SLL   R9,12                                                            
         ST    R9,0(R6)                                                         
         OI    2(R6),1             SET BLK 1                                    
         MVI   3(R6),1             SET REC 1                                    
         B     DBSRD1C                                                          
DBSRD4B  L     R6,DMCBW3           16-BIT TRACK                                 
         SR    R9,R9                                                            
         ICM   R9,3,0(R6)                                                       
         LA    R9,1(R9)                                                         
         SLL   R9,16                                                            
         ST    R9,0(R6)                                                         
         MVI   2(R6),1             SET BLK 1                                    
         MVI   3(R6),1             SET REC 1                                    
         B     DBSRD1C                                                          
*                                                                               
DBSRDX   XIT1                                                                   
*                                                                               
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
* ALLOCATE MVS DATASET - DYNAMIC ALLOCATION                           *         
***********************************************************************         
         DS    0F                                                               
ARBLKCTA DC    X'80',AL3(RBLKCATA) R1 POINTS TO THIS BEFORE DYNALLOC            
*                                                                               
RBLKCATA DC    X'14010000'                                                      
RBLKERR  DC    X'0000'                                                          
         DC    X'0000',A(ACATALLT),X'0000000000000000'                          
*                                                                               
ACATALLT DC    X'00',AL3(TXTDD)                                                 
         DC    X'00',AL3(TXTDSNH)                                               
         DC    X'00',AL3(TXTDISP)                                               
         DC    X'80',AL3(TXTCLOSE) FREE=CLOSE                                   
*                                                                               
TXTDD    DC    AL2(DALDDNAM),X'00010008',CL8'MVSFILE ' DDNAME=........          
TXTDSNH  DC    AL2(DALDSNAM),X'0001002C'               DSN=............         
TXTDSN   DC    CL44' '                                 DSN=............         
TXTDISP  DC    AL2(DALSTATS),X'0001000108'             DISP=(SHR,.....)         
TXTCLOSE DC    AL2(DALCLOSE),X'00000000'               FREE=CLOSE               
*                                                                               
JOBHDR   DC    C'        '                                                      
*                                                                               
         DS    0D                                                               
*                                                                               
MVSFILE  DCB   DDNAME=MVSFILE,MACRF=(GL),DSORG=PS,                     *        
               LRECL=8200,EODAD=ENDOFMVS                                        
                                                                                
ENDOFMVS CLOSE MVSFILE             CLOSE MVS FILE                               
         MVI   TXTDSN,C' '         CLEAN UP FIELDS AND CONTINUE                 
         MVC   TXTDSN+1(L'TXTDSN-1),TXTDSN                                      
         MVI   JOBHDR,C' '                                                      
         MVC   JOBHDR+1(L'JOBHDR-1),JOBHDR                                      
         LR    R1,RC                                                            
         J     DBSRD0                                                           
*                                                                               
ALLOCERR DC    C'<ERR0001D>/ALLOCATION ERROR 0000 DSN='                         
MSGDSN   DC    CL44' '                                                          
         DC    C'//H'                                                           
         EJECT                                                                  
***********************************************************************         
* DMADD - REQUEST - MAX REC SIZE 80+15*80 BYTE CARDS                  *         
***********************************************************************         
D1RQ     NMOD1 165,**D1RQ**        ENOUGH FOR 15 REQUEST CARDS                  
         STAR  CLEAR=YES,ARS=OFF                                                
         LR    R7,RC               R7=A(AREA TO BUILD REQUEST RECORD)           
         LR    RC,R1                                                            
*                                                                               
         ICM   RF,15,XTCBNTRY      SET DMADD/REQ FLAG                           
         BZ    *+8                                                              
         OI    TCBFLAG4-TCBD(RF),TCB4CADD                                       
*                                                                               
         LA    R7,24(R7)           ALLOW FOR RECV HDR                           
         L     R4,DMCBW4                                                        
         USING RQHHDRD,R4          R4=A(REQUEST RECORD)                         
         MVC   DUB(1),RQHFLAG1                                                  
         NI    DUB,255-RQHFRFPG-RQHFOSI                                         
         TM    DMCBW1,X'24'        TEST SPECIAL OFFLINE ADD                     
         BO    *+10                                                             
         XC    0(RQHWHY-RQHHDR,R4),0(R4)                                        
         MVC   RQHFLAG1,DUB                                                     
         ICM   RE,15,=V(SSB)                                                    
         BZ    D1RQ1                                                            
         OC    0(2,RE),0(RE)                                                    
         BZ    D1RQ1                                                            
*                                                                               
D1RQ0    MVC   RQHSYSID,SSBSYSID-SSBD(RE) EXTRACT FACPAK SYSTEM ID              
         L     R3,SSBTKADR-SSBD(RE)                                             
         L     R3,TCBUTL-TCBD(R3)  EXTRACT INFO FROM UTL ENTRY                  
         USING UTLD,R3                                                          
         OI    RQHFLAG1,RQHFNEW    SET NEW STYLE TERMINAL INFO                  
         MVC   RQHSIN,TSIN+1                                                    
         OI    RQHINFO,RQHIONL     SET REQUEST ADDED ONLINE                     
         TM    TSTAT1,TSTATDDS                                                  
         BZ    *+8                                                              
         OI    RQHINFO,RQHIDDS     SET REQUEST ADDED BY DDS TERMINAL            
         TM    TFLAG,TFLAGSEC                                                   
         BZ    *+8                                                              
         OI    RQHINFO1,RQHISEC    SET USERID REQUIRES A PASSWORD               
         TM    TSTATB,TSTATPPS                                                  
         BZ    *+8                                                              
         OI    RQHINFO1,RQHIPPS    SET AGENCY USES PPS SECURITY                 
         MVC   RQHSAGN,TSAGN                                                    
         MVC   RQHSAGYP,TAGYPER    SET PERSON SECURITY AGY                      
         OC    RQHSAGYP,RQHSAGYP                                                
         BNZ   *+10                                                             
         MVC   RQHSAGYP,TAGYSEC                                                 
         MVC   RQHSAGY,TAGYSEC     SET AGENCY SECURITY AGENCY                   
         MVC   RQHACCS,TACCS                                                    
         MVC   RQHPSWD,TPASSWD                                                  
         MVC   RQHSYS,TSYS                                                      
         MVC   RQHPRG,TPRG                                                      
         MVC   RQHCTRY,TCTRY                                                    
         MVC   RQHLANG,TLANG                                                    
         MVC   RQHAGCTY,TAGCTRY                                                 
*                                                                               
         MVC   RQHOFF,TOFFCODE     OFFICE NUMBER                                
*&&US                                                                           
         CLI   RQHCTRL,X'40'       TEST IF LOW BINARY NUM SET IN CTRL           
         BL    *+8                 YES-LEAVE IT THERE                           
*&&                                                                             
         MVI   RQHCTRL,0                                                        
*                                                                               
         TM    RQHFLAG,RQHFLNK                                                  
         BZ    D1RQ0A                                                           
         ICM   RF,15,XTCBNTRY      SET THIS IS A LINKED REQ REC                 
         BZ    *+8                                                              
         OI    TCBFLAG4-TCBD(RF),TCB4LREQ                                       
         OC    RQHAGY,RQHAGY       TEST IF ONLINE PGM SET AGENCY ALPHA          
         BNZ   *+10                                                             
D1RQ0A   MVC   RQHAGY,TAGY         MOVE ORIGINATING AGENCY ALPHA                
*                                                                               
         TM    RQHFLAG,RQHFLNK                                                  
         BZ    *+14                                                             
         OC    RQHORIG,RQHORIG     TEST IF ONLINE PGM SET USER ID               
         BNZ   *+10                                                             
         MVC   RQHORIG,TUSER       MOVE ORIGINATING USER ID NUM                 
         B     D1RQ2                                                            
         DROP  R3                                                               
*                                                                               
D1RQ1    NI    RQHFLAG,255-RQHFLNK NO LINK IF OFFLINE                           
         TM    DMCBW1,X'24'                                                     
         BO    D1RQ3               SPECIAL OFFLINE ADD                          
*                                                                               
D1RQ2    TIME  DEC                                                              
         SRL   R1,4                R0=X'HHMMSSTH',R1=X'00CYYDDD'                
         STCM  R1,7,RQHPJDT                                                     
         STCM  R0,14,RQHPHMS                                                    
         LR    R1,RC               RESTORE R1                                   
         OC    RQHDEST,RQHDEST                                                  
         BZ    *+8                                                              
         OI    RQHFLAG1,RQHFDEST   SET REQUEST ADDED WITH DEST ID               
         OC    RQHOUT,RQHOUT                                                    
         BZ    *+8                                                              
         OI    RQHFLAG1,RQHFOUT    SET REQUEST ADDED WITH OUTPUT TYPE           
         OC    RQHWHY(2),RQHWHY                                                 
         BZ    *+8                                                              
         OI    RQHFLAG1,RQHFWHY    SET REQUEST ADDED WITH REASON/WHY            
*                                                                               
D1RQ2A   MVC   DUB(1),RQHINFO      SET REST OF RQHINFO FLAGS                    
         MVC   RQHINFO,RQHFLAG1                                                 
         NI    RQHINFO,X'1F'                                                    
         OC    RQHINFO,DUB                                                      
*                                                                               
D1RQ3    BAS   RE,SETDUM           GET DTF ADDRESS                              
         BASR  RE,RF                                                            
         XC    RQHLINK,RQHLINK                                                  
         TM    RQHFLAG,RQHFLNK     TEST FOR LINKED REQUEST                      
         BZ    D1RQ5                                                            
*                                                                               
         XC    DUB,DUB             LOCK RESOURCE                                
         MVC   DUB+3(1),RQHSYS                                                  
         GOTO1 =V(LOCKSPC),DUB                                                  
         LR    R1,RC               RESTORE R1                                   
*                                                                               
         L     R3,DUB+4                                                         
         USING DMSPACED,R3                                                      
         ICM   R3,15,DSPECB                                                     
         SLL   R3,2                TURN OFF X'40' BIT                           
         SRL   R3,2                                                             
*                                                                               
         SAC   512                                                              
         L     RE,=V(SSB)                                                       
         LAM   AR3,AR3,SSBALET-SSBD(RE)                                         
         USING DMSYSHDR,R3                                                      
         L     R3,DSYAREQT         LEAVE R2 ALONE                               
*                                                                               
D1RQ4    LH    RE,0(,R3)                                                        
         ICM   RF,15,2(R3)                                                      
         LA    R3,6(,R3)                                                        
         LR    R0,R3               SAVE A(FIRST ENTRY)                          
*                                                                               
D1RQ4A   CLC   0(2,R3),RQHCARD     FIND THIS REQ NUM IN LIST                    
         BE    D1RQ4C                                                           
         OC    0(2,R3),0(R3)       OR FIND AN EMPTY SLOT                        
         BNZ   D1RQ4B                                                           
         MVC   0(2,R3),RQHCARD                                                  
         CR    RB,RB               ENSURE CC EQUAL                              
         B     D1RQ4C                                                           
*                                                                               
D1RQ4B   BXLE  R3,RE,D1RQ4A                                                     
         LR    R3,R0               USE SUNDRY ENTRY IF PRESENT                  
         CLC   0(2,R3),=C'00'                                                   
*                              *** LEAVE R3 ALONE FROM HERE TO END ***          
D1RQ4C   IPM   R0                  SAVE CC                                      
         XC    DUB,DUB                                                          
         MVC   DUB+3(1),RQHSYS     UNLOCK RESOURCE                              
         OI    DUB,X'10'                                                        
         GOTO1 =V(LOCKSPC),DUB                                                  
         SAFE                      CALL LOCKSPC SAFELY!                         
         SAC   0                                                                
         LR    R1,RC                                                            
         SPM   R0                  RESTORE OUR CC                               
         BE    D1RQ5               FOUND A GAP                                  
*                                                                               
D1RQERR  DC    H'0'                DIE=REQUEST TABLE                            
*                                                                               
D1RQ5    SAC   0                                                                
         XR    RE,RE               GET NUMBER OF CARDS IN REQUEST               
         IC    RE,RQHFLAG                                                       
         SRL   RE,4                NUMOFCARDS-1 IS IN HIGH NIBBLE               
         LA    RE,1(RE)                                                         
         CHI   RE,RQHCMAX          MAX NUMOFCARDS IS 15                         
         JH    *+2                 DIE=MAX REQ CARDS                            
         MHI   RE,80               RECLEN=NUMOFCARDS*80+80                      
         LA    RE,80(RE)                                                        
         ST    RE,PARAM3           SET REC LEN AND SAVE IN DUB+0(4)             
         ST    RE,DUB                                                           
         LA    RE,PARAM7                                                        
         ST    RE,PARAM5           SET PARAM7 AS RETURNED DNEXT                 
         XC    PARAM7,PARAM7                                                    
         MVC   PARAM1,=V(WRTGET)   GET AND LOCK DNEXT                           
         L     RF,=V(DMOD000)                                                   
         BASR  RE,RF               R2 MUST BE UNTOUCHED HERE                    
         SAFE                                                                   
         SAC   0                                                                
*                                                                               
         MVC   DUB+4(4),PARAM7     SAVE PARAM7=DNEXT IN DUB+4(4)                
         OC    PARAM3(2),PARAM3                                                 
         BNZ   D1RQX                                                            
*                                                                               
D1RQ6    MVC   PARAM3(1),EXTFILE   SET ADD IN RECOVERY FILE                     
         MVI   PARAM3+1,3                                                       
         STAR  CLEAR=Y,ARS=OFF     SAVE & RESTORE ARS AROUND CALL               
         BRAS  R5,DMGRREC1                                                      
         REAR  ARS=ON                                                           
*                                                                               
D1RQ7    MVC   PARAM1,=V(WRTAFT)   ADD RECORD TO DA FILE                        
         GOTO1 =V(DMOD000)                                                      
         SAFE                                                                   
         L     R6,DMCBW3           RETURN DISK ADDRESS OF NEW REQ REC           
         MVC   0(4,R6),PARAM7                                                   
         TM    RQHFLAG,RQHFLNK     EXIT IF NOT LINKED REQUEST                   
         BZ    D1RQX                                                            
*                                                                               
D1RQ8    OC    PARAM7,PARAM7       UPDATE REQLIST ADDR                          
         BZ    D1RQX                                                            
*                                                                               
         MVC   DUB,0(R3)           SAVE OLD REQLIST POINTER                     
         MVC   5(3,R3),PARAM7      SET AS LAST                                  
         OC    2(3,R3),2(R3)                                                    
         BNZ   *+10                                                             
         MVC   2(3,R3),PARAM7      SET AS FIRST                                 
*                                                                               
D1RQA    OC    DUB+5(3),DUB+5      IS THIS THE FIRST REQ                        
         BZ    D1RQX                                                            
         OI    DMCBW1,X'80'        NO SET TO READ OLD LAST REQ                  
         MVC   PARAM1,=V(READ)                                                  
         ST    R7,PARAM2           SET I/O AREA FOR LAST REQUEST                
         XC    PARAM3,PARAM3                                                    
         XC    PARAM7,PARAM7                                                    
         MVC   PARAM7(3),DUB+5     SET DISK ADDR FOR LAST REQUEST               
         GOTO1 =V(DMOD000)                                                      
         SAFE                                                                   
         OC    PARAM3(2),PARAM3    IF READ OK CHAIN TO NEXT NEW REQ             
         BNZ   D1RQA1                                                           
         MVC   RQHLINK-RQHHDRD(3,R7),5(R3)                                      
*                                                                               
         MVC   PARAM1,=V(WRITE)                                                 
         GOTO1 (RF)                                                             
         SAFE                                                                   
         OC    PARAM3(2),PARAM3                                                 
         BZ    D1RQX                                                            
*                                                                               
D1RQA1   MVC   0(8,R3),DUB         ERROR CHAIN - RESTORE ADDR LIST              
         B     D1RQX                                                            
*                                                                               
D1RQX    REAR  ARS=OFF                                                          
         XIT1                                                                   
*                                                                               
         DROP  R3,R4                                                            
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
* DMRSEQ - REQUEST                                                    *         
***********************************************************************         
D3RQ     NMOD1 0,**D3RQ**                                                       
         STAR  CLEAR=YES,ARS=OFF                                                
         LR    RC,R1                                                            
         BAS   RE,SETDUM                                                        
         BASR  RE,RF               SET DTF ADDRESS IN PARAM4                    
*                                                                               
D3RQ1    L     R6,DMCBW3           GET LAST DISK ADDR READ                      
         SR    R0,R0               RESET NOT FOUND SWITCH                       
         OC    0(4,R6),0(R6)                                                    
         BZ    D3RQ4                                                            
*                                                                               
D3RQ1A   L     RE,PARAM4           GET DTF ADDRESS                              
         TM    DTFTYPE-DTFPHD(RE),DTFTBIGF                                      
         BZ    D3RQ1A1                                                          
         IC    R3,3(R6)            BUMP RECORD NUM                              
         LA    R3,1(R3)                                                         
         STC   R3,3(R6)                                                         
         B     D3RQ1B                                                           
D3RQ1A1  IC    R3,2(R6)            BUMP RECORD NUM                              
         LA    R3,1(R3)                                                         
         STC   R3,2(R6)                                                         
*                                                                               
D3RQ1B   L     RF,=V(READ)         SET COMMAND                                  
         BAS   RE,SETPR            SET PARAM LIST                               
*                                                                               
         TM    DMCBW1,X'10'        TEST FULL TRACK READ                         
         BZ    *+16                                                             
         MVC   PARAM1,=V(READTRK)                                               
         MVC   PARAM6,DMCBW5       MOVE BUFFER ADDRESS                          
         BASR  RE,RF                                                            
*                                                                               
         TM    PARAM3+1,X'04'      TEST EOF                                     
         BO    D3RQ5                                                            
         OC    PARAM3(2),PARAM3    TEST FOR ERRORS                              
         BNZ   D3RQ2                                                            
         OC    PARAM3+2(2),PARAM3+2                                             
         BZ    D3RQ1               IGNORE ZERO LENGTH RECORD                    
*                                                                               
         L     R6,DMCBW4           POINT TO RECORD                              
         CLI   RQHNUMB-RQHHDR(R6),X'FF'                                         
         BE    D3RQ1               IGNORE IF DELETED RECORD                     
         LH    RE,PARAM3+2         GET ACTUAL RECORD LENGTH                     
         TM    DMCBW1,X'20'        TEST IF CALLER WANTS WHOLE RECORD            
         BO    *+8                                                              
         SHI   RE,54                                                            
         STH   RE,DMCBW5+2         SET RECORD LENGTH                            
         B     D3RQX                                                            
*                                                                               
D3RQ2    TM    PARAM3+1,X'08'      TEST NO REC FOUND                            
         BO    *+12                                                             
D3RQ3    OI    DMCBW3,X'40'        SET HARD ERROR                               
         B     D3RQX               GO TO EXIT                                   
*                                                                               
         LTR   R0,R0               TEST PRIOR REC NOT FOUND                     
         BNZ   D3RQ5                                                            
         LA    R0,1                SET NFSW                                     
*                                                                               
D3RQ4    L     R6,DMCBW3           NOT FOUND - BUMP TO NEXT TRACK               
         SR    R9,R9                                                            
         L     RE,PARAM4           GET DTF ADDRESS                              
         TM    DTFTYPE-DTFPHD(RE),DTFTBIGF                                      
         BZ    D3RQ4A                                                           
         ICM   R9,7,0(R6)          20-BIT TRACK TTTTT0RR                        
         SRL   R9,4                                                             
         LA    R9,1(R9)                                                         
         SLL   R9,4                                                             
         STCM  R9,7,0(R6)                                                       
         MVI   3(R6),1                                                          
         B     D3RQ1B                                                           
D3RQ4A   ICM   R9,3,0(R6)          16-BIT TRACK TTTTRR00                        
         LA    R9,1(R9)                                                         
         SLL   R9,16                                                            
         ST    R9,0(R6)                                                         
         MVI   2(R6),1                                                          
         B     D3RQ1B                                                           
*                                                                               
D3RQ5    MVI   DMCBW3,X'80'        SET EOF                                      
*                                                                               
D3RQX    REAR  ARS=OFF                                                          
         XIT1                                                                   
*                                                                               
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
* DMRDIR - REQUEST                                                    *         
***********************************************************************         
D6RQ     NMOD1 0,**D6RQ**                                                       
         LR    RC,R1                                                            
*                                                                               
         BAS   RE,SETDUM           DTF-4 IS A(REQADDR LIST)                     
         BASR  RE,RF               ON RETURN PARAM4 IS DTF ADDRESS              
*                                                                               
         L     R6,DMCBW3           R6=A(DISK ADDR)                              
         CLI   3(R6),X'FF'         TEST REQUEST FOR FIRST RECORD                
         BNE   D6RQC                                                            
*                                                                               
         CLI   1(R6),0             ONE BYTE BINARY REQUEST NUMBER               
         BNE   D6RQA               NO ASSUME TWO BYTE ALPHA                     
         XR    RE,RE               YES CONVERT TO TWO BYTE ALPHA                
         IC    RE,0(R6)                                                         
         CVD   RE,DUB                                                           
         UNPK  0(2,R6),DUB                                                      
         OI    1(R6),X'F0'                                                      
*                                                                               
D6RQA    ICM   RE,15,XUTLNTRY                                                   
         XR    R3,R3                                                            
         ICM   R3,1,TSYS-UTLD(RE)                                               
         JZ    *+2                 DIE=ZERO SE NUMBER                           
         SLL   R3,6                MULTIPLY BY 64 AS INDEX INTO SE HDR          
         STAR  CLEAR=YES,ARS=ON                                                 
         L     RE,=V(SSB)                                                       
         LAM   AR3,AR3,SSBALET-SSBD(RE)                                         
         SAC   512                                                              
         ICM   R3,07,DSPECB+1-DMSPACED(R3)                                      
         ICM   R3,15,DSYAREQT-DMSYSHDR(R3)                                      
*                                                                               
         LH    R4,0(,R3)           SET UP BXLE                                  
         ICM   R5,15,2(R3)                                                      
         LA    R3,6(,R3)                                                        
         LR    RE,R3               SAVE A(FIRST ENTRY)                          
         CLC   0(2,R3),0(R6)       FIRST TWO BYTES OF ADDR ARE REQ ID           
         BE    D6RQB                                                            
         BXLE  R3,R4,*-10                                                       
         LR    R3,RE               USE SUNDRY ENTRY IF PRESENT                  
         CLC   0(2,R3),=C'00'                                                   
         BE    D6RQB               PRESENT                                      
         REAR  ARS=OFF                                                          
         B     D6RQW                                                            
*                                                                               
D6RQB    MVC   0(3,R6),2(R3)       SET DISK ADR TO FIRST IN REQ LIST            
         MVI   3(R6),0             RESET WANT FIRST FLAG IN DISK ADR            
         REAR  ARS=OFF                                                          
*                                                                               
D6RQC    OC    0(2,R6),0(R6)       TEST FOR NO MORE REQUESTS                    
         BZ    D6RQW                                                            
*                                                                               
         MVC   PARAM1,=V(READ)     SET COMMAND                                  
         TM    DMCBW1,X'10'        TEST FULL TRACK READ                         
         BZ    *+16                                                             
         MVC   PARAM1,=V(READTRK)                                               
         MVC   PARAM6,DMCBW5       SET TRACK BUFFER ADDRESS                     
         BASR  RE,RF                                                            
         LH    RE,PARAM3+2         GET ACTUAL RECORD LENGTH                     
         TM    DMCBW1,X'20'        TEST IF CALLER WANTS WHOLE RECORD            
         BO    *+8                                                              
         SHI   RE,54                                                            
         STH   RE,DMCBW5+2         SET RECORD LENGTH                            
         B     D6RQX                                                            
*                                                                               
D6RQW    MVI   DMCBW3,X'80'        SET EOF ON ZERO DISK ADDR                    
*                                                                               
D6RQX    XIT1                                                                   
*                                                                               
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
* DMWRT - REQUEST                                                     *         
***********************************************************************         
D8RQ     NMOD1 165,**D8RQ**        ENOUGH FOR 15 REQUEST CARDS                  
         LR    R7,RC               R7=A(AREA TO HOLD REQUEST RECORD)            
         LR    RC,R1                                                            
         ICM   RF,15,XTCBNTRY      SET DMWRT/REQ FLAG                           
         BZ    *+8                                                              
         OI    TCBFLAG4-TCBD(RF),TCB4CCHG                                       
*                                                                               
         XC    0(24,R7),0(R7)      SET NO RECOVERY COPY                         
         LA    R7,24(R7)           ALLOW FOR RECV HDR                           
         L     RF,=V(READ)                                                      
         BAS   RE,SETPR                                                         
         ST    R7,PARAM2                                                        
         BASR  RE,RF               READ COPY RECORD                             
         OC    PARAM3(2),PARAM3    TEST FOR ERRORS                              
         BZ    *+12                                                             
         OI    DMCBW3,X'40'                                                     
         B     D8RQX                                                            
*                                                                               
         L     R4,DMCBW4           R4=A(REQUEST RECORD)                         
         USING RQHHDRD,R4                                                       
         TM    RQHFLAG,RQHFLNK                                                  
         BZ    D8RQ1                                                            
         ICM   RF,15,XTCBNTRY      LINKED REQUEST                               
         BZ    *+8                                                              
         OI    TCBFLAG4-TCBD(RF),TCB4LREQ                                       
*                                                                               
D8RQ1    SR    RE,RE               GET NUMBER OF CARDS IN REQUEST               
         IC    RE,RQHFLAG                                                       
         SRL   RE,4                NUMOFCARDS-1 IS IN HIGH NIBBLE               
         LA    RE,1(RE)                                                         
         MHI   RE,80               RECLEN=NUMOFCARDS*80+80                      
         LA    RE,80(RE)                                                        
         CH    RE,PARAM3+2                                                      
         JNE   *+2                 DIE=CHANGE REC LEN                           
         ST    RE,DUB                                                           
         MVC   0(54,R4),0(R7)      COPY SYSTEM REQ HDR DATA                     
*                                                                               
         L     RF,=V(WRITE)                                                     
         BAS   RE,SETPR                                                         
         MVC   PARAM3,DUB          SET REC LEN                                  
         BASR  RE,RF                                                            
         OC    PARAM3(2),PARAM3    TEST FOR ERRORS                              
         BZ    *+12                                                             
         OI    DMCBW3,X'40'                                                     
         B     D8RQX                                                            
*                                                                               
         BAS   RE,SETPRC           SET RECOVERY PARAMETERS                      
         MVC   PARAM3(1),EXTFILE                                                
         MVI   PARAM3+1,2                                                       
         MVC   PARAM3+2(2),DUB+2                                                
         BRAS  R5,DMGRREC1                                                      
*                                                                               
D8RQX    XIT1                                                                   
*                                                                               
         DROP  R4                                                               
         LTORG                                                                  
       ++INCLUDE DMDMGRREP                                                      
         LTORG                                                                  
         EJECT                                                                  
*&&SRV                                                                          
***********************************************************************         
* READ AND WRITE TEMPSTR PAGES - PAGE NUM SAVED IN SVPAGE             *         
* TEMPSTR IS DIVIDED INTO TMPSEGS LOGICAL SEGMENTS SO THAT THE 1ST    *         
* TERM GOES IN THE 1ST SEG, THE 2ND TERM GOES IN THE 2ND SEG, ETC     *         
*                                                                     *         
* RECNUM =(TRMNUM-1)/TMPSEGS WITH REMAINDER=SEGNUM                    *         
* ACTPAGE=(TSESSION*TMPSSPGS)*SVPAGE + 2                              *         
* LOGICAL=SEGNUM*TMPRPSEG + RECNUM*TMPRPTRM + ACTPAGE                 *         
* DISKADR=LOGICAL/TMPRPTRK + 1                                        *         
***********************************************************************         
DMODF2   NMOD1 0,**DMF2**                                                       
         LR    RC,R1                                                            
         MVC   PARAM4,=V(TEMPSTR)  SET A(DTF)                                   
         MVI   PARAM4,X'F2'                                                     
         LH    R4,TMPRPTRK         R4=RECORDS PER TRACK                         
         LTR   R4,R4               TEST FIRST TIME                              
         BNZ   DMF2B                                                            
*                                                                               
DMF2A    ICM   RE,15,=V(SSB)       TEST IF ANY SSB OVERRIDES                    
         BZ    DMF2A2                                                           
         USING SSBD,RE                                                          
         OC    0(2,RE),0(RE)                                                    
         BZ    DMF2A2                                                           
         OC    SSBTWAS,SSBTWAS                                                  
         BZ    *+10                                                             
         MVC   TMPRPTRM,SSBTWAS                                                 
         OC    SSBTWAM,SSBTWAM                                                  
         BZ    *+10                                                             
         MVC   TMPSEGS,SSBTWAM                                                  
         OC    SSBTWAL,SSBTWAL                                                  
         BZ    *+10                                                             
         MVC   TMPRLEN,SSBTWAL                                                  
         OC    SSBSSMXP,SSBSSMXP                                                
         BZ    *+10                                                             
         MVC   TMPSSMAX,SSBSSMXP                                                
         OC    SSBSSPGS,SSBSSPGS                                                
         BZ    *+10                                                             
         MVC   TMPSSPGS,SSBSSPGS                                                
         DROP  RE                                                               
*                                                                               
DMF2A1   SR    RF,RF               TEST NEW STYLE TEMPSTR SESSION               
         ICM   RF,3,TMPSSMAX                                                    
         BZ    DMF2A2                                                           
         MH    RF,TMPSSPGS         COMPUTE RECORDS PER TERMINAL                 
         LTR   RF,RF                                                            
         JZ    *+2                 DIE=ZERO RECS/TRM                            
         LA    RF,2(RF)                                                         
         STH   RF,TMPRPTRM         RPT=2+(SESSIONS*PAGES)                       
*                                                                               
DMF2A2   LH    R0,TMPRLEN          COMPUTE RECORDS PER TRACK                    
         GOTO1 =V(DADDS),PARAM1,V(DARPT),,(R0)                                  
         ICM   R4,3,PARAM3+2                                                    
         JZ    *+2                 DIE=ZERO RECS/TRK                            
         STH   R4,TMPRPTRK         SAVE RECORDS PER TRACK                       
*                                                                               
DMF2A3   L     RF,=V(TEMPSTR)      COMPUTE NUMBER OF TRACKS AND TRMS            
         BRAS  RE,GETTTRKS         GET TOTAL TRACKS.                            
*                                                                               
         L     RE,=V(TEMPSTR)                                                   
         USING DTFPHD,RE                                                        
         TM    DTFTYPE,DTFTBIGF                                                 
         BO    *+12                                                             
         STH   RF,TMPTRKS          SAVE NUMBER OF TRACKS                        
         B     DMF2A3X                                                          
         DROP  RE                                                               
*                                                                               
         MVC   TMPTRKS,=XL2'FFFF'                                               
         ST    RF,TMPTRK20         SAVE NUMBER OF TRACKS IF 20-BIT              
*                                                                               
DMF2A3X  SR    RE,RE                                                            
         MH    RF,TMPRPTRK                                                      
         LH    R0,TMPRPTRM                                                      
         DR    RE,R0               RF=(TMPTRKS*TMPRPTRK)/TMPRPTRM               
*                                                                               
DMF2A4   SR    RE,RE               COMPUTE SEGMENT INFO                         
         LH    R0,TMPSEGS                                                       
         DR    RE,R0               RF=TERMINALS PER SEGMENT                     
         LR    R0,RF                                                            
         MH    R0,TMPSEGS                                                       
         STH   R0,TMPTRMS          SET NUM TRMS MODULO NUM SEGS                 
         MH    RF,TMPRPTRM                                                      
         STH   RF,TMPRPSEG         SAVE RECORDS PER SEGMENT                     
*                                                                               
DMF2B    CLI   ACTION,6            PASS CONTROL FOR ACTION                      
         BL    DMF2B2                                                           
         BE    DMF2B6                                                           
         B     DMF2B8                                                           
*                                                                               
DMF2B2   MVC   PARAM7,=V(READ)     DMREAD - COMMAND TO READ TWA                 
         MVC   FULL(2),=C'L='                                                   
         MVC   FULL+2(2),=H'6144'                                               
         CLC   DMCBW6(2),=C'L='    TEST IF NON STANDARD READ LENGTH             
         BNE   DMF2C                                                            
         MVC   FULL,DMCBW6                                                      
         OC    FULL+2(2),FULL+2    LEN MUST BE GT ZERO AND LE TMPRLEN           
         JZ    *+2                 DIE=ZERO REC LEN                             
         CLC   FULL+2(2),TMPRLEN                                                
         BNH   DMF2C                                                            
         DC    H'0'                DIE=MAX REC LEN                              
*                                                                               
DMF2B6   MVC   PARAM7,=V(READ)     DMRDIR - COMMAND TO READ OLD TWA             
         MVC   FULL(2),=C'L='                                                   
         MVC   FULL+2(2),=H'2304'                                               
         CLI   DMCBW3+1,X'FF'      TEST FOR EXTRA 256 BYTES                     
         BNE   DMF2C                                                            
         MVC   FULL+2(2),=H'2560'  SET READ LENGTH FOR OLDLEN+256               
         B     DMF2C                                                            
*                                                                               
DMF2B8   MVC   PARAM7,=V(WRITE)    DMWRT  - COMMAND TO WRITE TWA                
         XC    FULL(2),FULL                                                     
         MVC   FULL+2(2),TMPRLEN                                                
*                                                                               
DMF2C    MVI   SESS,0              VALIDATE TERMINAL AND PAGE NUMBER            
         MVI   BYTE,0                                                           
         SR    RF,RF                                                            
         ICM   RF,3,DMCBW3+2       GET TERMINAL NUMBER                          
         BZ    DMF2C2                                                           
         CLM   RF,3,=X'FFFF'       TEST SPECIAL CALL FOR TEMPSTR INFO           
         BNE   DMF2C1                                                           
         L     RE,DMCBW4           POINT TO CALLERS RECORD AREA                 
         MVC   0(L'TMPINFO,RE),TMPINFO                                          
         B     DMODF2X             EXIT WITH TEMPSTR INFO RETURNED              
*                                                                               
DMF2C1   OI    BYTE,X'01'          SET TERMINAL NUMBER PASSED                   
         ICM   RE,15,=V(SSB)                                                    
         BZ    DMF2EOF             TEST SSB FOR AOR FLAG                        
         TM    SSBSTAT4-SSBD(RE),SSBSAOR                                        
         BNZ   DMF2C2              MUST BE OUR UTL IF IN AOR MODE               
*                                                                               
         SAM31                                                                  
         L     RE,=V(SYSFAC)       USE TERMINAL NUM TO GET UTL ENTRY            
         L     RE,VUTL-SYSFACD(RE)                                              
         BCTR  RF,0                                                             
         MH    RF,0(RE)            RF=(TRM#-1)*L'UTLENTRY                       
         LA    RE,6(RE,RF)                                                      
         ST    RE,XUTLNTRY         SAVE THIS UTL FOR LATER COMPARE              
         B     DMF2C3                                                           
*                                                                               
DMF2C2   SAM31                                                                  
         ICM   RE,15,XUTLNTRY      GET TERMINAL NUMBER FROM UTL                 
         BZ    DMF2EOF                                                          
         TM    BYTE,X'01'          IF NUM WAS PASSED WE MUST BE IN AOR          
         BZ    DMF2C3                                                           
         CH    RF,TNUM-UTLD(RE)    AND THIS MUST BE OUR TERMINAL                
         BNE   DMF2EOF                                                          
*                                                                               
DMF2C3   MVC   SESS,TSESSION-UTLD(RE)                                           
         OC    TPRNT-UTLD(3,RE),TPRNT-UTLD(RE)                                  
         BZ    *+8                                                              
         OI    BYTE,X'02'          SET DEVICE IS A PRINTER                      
         CLC   TSYS-UTLD(2,RE),=X'0A0C'                                         
         BZ    *+8                                                              
         OI    BYTE,X'08'          SET CONTROL/MAD                              
         SR    RF,RF                                                            
         ICM   RF,3,TNUM-UTLD(RE)                                               
         BZ    DMF2EOF                                                          
         STH   RF,DMCBW3+2         STORE IN DMCB                                
         CLM   RF,3,TMPTRMS        TEST TERMINAL NUM WITH MAXIMUM               
         BH    DMF2EOF                                                          
         SAM24                                                                  
*                                                                               
DMF2C4   MVC   ACTPAGE,SVPAGE      OLD TEMPSTR                                  
         CLI   TMPSSMAX+1,0                                                     
         BNE   DMF2C5                                                           
         NI    ACTPAGE,X'7F'       ACTUAL PAGE SAME AS PASSED PAGE              
         CLI   ACTPAGE,5                                                        
         BNE   *+16                                                             
         TM    BYTE,X'02'          PAGE 5 ONLY VALID FOR PRINTERS               
         BO    DMF2C10                                                          
         B     DMF2NF                                                           
         CLI   ACTPAGE,11                                                       
         BNE   DMF2C10                                                          
         TM    BYTE,X'02'                                                       
         BO    DMF2C10                                                          
         OI    BYTE,X'04'          SET TERMINAL S/R SAVE PAGE                   
         B     DMF2C10                                                          
*                                                                               
DMF2C5   OI    BYTE,X'80'          NEW TEMPSTR                                  
         TM    ACTPAGE,X'80'                                                    
         BZ    DMF2C6                                                           
         NI    ACTPAGE,X'7F'       X'80' ON MEANS ACTUAL PAGE PASSED            
         TM    BYTE,X'02'                                                       
         BO    DMF2C10                                                          
         CLI   ACTPAGE,1                                                        
         BNE   DMF2C10                                                          
         OI    BYTE,X'04'          SET TERMINAL S/R SAVE PAGE                   
         B     DMF2C10                                                          
DMF2C6   TM    BYTE,X'02'          PRINTERS USE PAGES 1 THRU 10 TO              
         BO    DMF2C10             STORE LAST 10 BUFFERS                        
         CLI   ACTPAGE,5           CONVERT ACTUAL PAGE FOR NEW FILE             
         BL    DMF2C9                                                           
         BE    DMF2NF              PAGE 5 DOESNT EXIST FOR TERMINALS            
DMF2C7   CLI   ACTPAGE,11          TERMINAL PAGE 11 MAPS TO PAGE 1              
         BH    DMF2NF                                                           
         BL    DMF2C8                                                           
         OI    BYTE,X'04'          SET S/R SAVE PAGE                            
         MVI   ACTPAGE,1                                                        
         B     DMF2C10                                                          
DMF2C8   TM    BYTE,X'08'          TEST CONTROL/MAD                             
         NOP   DMF2NF              *NOP* WAS BZ (ANY PGM CAN DO THIS)           
         SR    RE,RE                                                            
         IC    RE,SESS             BUMP TO NEXT SESSION AFTER THIS              
         LA    RE,1(RE)                                                         
         CH    RE,TMPSSMAX                                                      
         BL    *+6                                                              
         SR    RE,RE                                                            
         MH    RE,TMPSSPGS                                                      
         SR    RF,RF                                                            
         IC    RF,ACTPAGE          PAGES 6 THRU 10 BECOME 0 THRU 4              
         AHI   RF,-6                                                            
         LA    RF,2(RF,RE)                                                      
         STC   RF,ACTPAGE                                                       
         B     DMF2C10                                                          
*                                                                               
DMF2C9   CLI   ACTPAGE,0           ZERO ALWAYS MEANS FIRST RECORD ????          
         BE    DMF2C10                                                          
         SR    RE,RE                                                            
         IC    RE,SESS             GET SESSION NUMBER                           
         CH    RE,TMPSSMAX                                                      
         JNL   *+2                 DIE=NUM > MAX SESSIONS                       
         MH    RE,TMPSSPGS                                                      
         SR    RF,RF                                                            
         IC    RF,ACTPAGE                                                       
         LA    RF,2(RF,RE)                                                      
         STC   RF,ACTPAGE                                                       
DMF2C10  CLC   ACTPAGE,TMPRPTRM+1  PAGE NUMBER MUST BE IN RANGE                 
         BNL   DMF2NF                                                           
*                                                                               
DMF2E    LH    RF,DMCBW3+2         COMPUTE REC NUM FROM TRM NUM                 
         BCTR  RF,0                                                             
         LH    R0,TMPSEGS                                                       
         SR    RE,RE                                                            
         DR    RE,R0               RE=SEG NUM AND RF=REC NUM IN SEG             
         SR    R6,R6                                                            
         ICM   R6,3,TMPRPSEG                                                    
         LR    R7,RE                                                            
         MR    R6,R6               R7=SEGNUM*RECPSEG                            
         MH    RF,TMPRPTRM         RF=RECNUM*RECPTRM                            
         AR    R7,RF                                                            
         SR    R6,R6                                                            
         IC    R6,ACTPAGE                                                       
         AR    R6,R7               R6=LOGICAL REC NUM (FIRST=ZERO)              
*                                                                               
DMF2F    SRDA  R6,32               COMPUTE DISK ADDR TTTTRR00                   
         DR    R6,R4                                                            
*                                                                               
         L     RE,=V(TEMPSTR)      TEST 20-BIT TEMPSTR                          
         TM    DTFTYPE-DTFPHD(RE),DTFTBIGF                                      
         BO    DMF2FA                                                           
         AHI   R7,1                R7=TRKNUM                                    
         SLL   R7,16                                                            
         ST    R7,PARAM6                                                        
         AHI   R6,1                R6=RECNUM                                    
         STC   R6,PARAM6+2                                                      
         B     DMF2G                                                            
*                                                                               
DMF2FA   AHI   R7,1                R7=000TTTTT                                  
         SLL   R7,4                R7=00TTTTT0                                  
         STCM  R7,7,PARAM6                                                      
         AHI   R6,1                R6=0000000R                                  
         STC   R6,PARAM6+3                                                      
*                                                                               
DMF2G    TM    BYTE,X'04'          TEST S/R SAVE PAGE                           
         BZ    DMF2H                                                            
         TIME  BIN                 R0=TIME(1/100 SEC),R1=DATE(JULIAN)           
         LR    RE,R1                                                            
         LR    R1,RC               RESTORE R1                                   
         ST    R0,DUB+4                                                         
         ST    RE,DUB                                                           
         CLI   ACTION,8            TEST DMWRT ACTION                            
         BNE   DMF2H                                                            
         L     R4,DMCBW4           YES STAMP WITH LUID/DATE/TIME                
         USING SRSD,R4                                                          
         SAM31                                                                  
         L     RE,XUTLNTRY                                                      
         MVC   SRSLUID,TSYM-UTLD(RE)                                            
         MVC   SRSDATE(8),DUB                                                   
         MVC   SRSPID(2),TAGYPER-UTLD(RE)                                       
         MVC   SRSPID+2(2),TPERSON-UTLD(RE)                                     
         SAM24                                                                  
*                                                                               
DMF2H    MVC   PARAM1,PARAM7       SET ACTION                                   
         MVC   PARAM2,DMCBW4       SET I/O AREA                                 
         MVC   PARAM3,FULL         SET REC LEN                                  
         LA    RE,PARAM6           SET DISK ADDR                                
         ST    RE,PARAM5                                                        
         GOTO1 =V(DMOD000),PARAM1                                               
         OC    PARAM3(2),PARAM3    TEST ERROR FREE                              
         BNZ   DMF2ERR                                                          
*                                                                               
DMF2J    TM    BYTE,X'04'          TEST READING S/R SAVE PAGE                   
         BZ    DMODF2X                                                          
         CLI   ACTION,8                                                         
         BE    DMODF2X                                                          
         SAM31                                                                  
         L     RE,XUTLNTRY         GET TERMINAL LUID FROM UTL ENTRY             
         MVC   OLDKEY(8),TSYM-UTLD(RE)                                          
         SAM24                                                                  
*                                                                               
DMF2L    L     R4,DMCBW4           R4=A(TEMPSTR RECORD)                         
         USING SRSD,R4                                                          
         LH    R5,TMPRLEN          R5=REQUESTED READ LENGTH                     
         CLC   FULL(2),=C'L='                                                   
         BNE   DMF2L1                                                           
         CLC   FULL+2(2),=H'6144'                                               
         BNE   DMF2L1                                                           
         LH    R5,FULL+2                                                        
DMF2L1   CLC   SRSLUID(8),OLDKEY   INIT IF DIFFERENT TERMINAL LUID              
         BNE   DMF2M                                                            
         ICM   RE,15,=V(SSB)       OR IF PRE FACPAK START DATE/TIME             
         BZ    DMF2M                                                            
         CLC   SRSDATE(8),SSBSDATE-SSBD(RE)                                     
         BNL   DMODF2X                                                          
*                                                                               
DMF2M    LR    RE,R4               R4=A(TEMPSTR RECORD)                         
         LR    RF,R5                                                            
         SR    R1,R1                                                            
         MVCL  RE,R0               SET TO ALL ZEROS                             
         LR    R1,RC                                                            
         MVC   SRSLUID,OLDKEY      STAMP WITH LUID/DATE/TIME                    
         MVC   SRSDATE(8),DUB                                                   
         B     DMODF2X                                                          
         DROP  R4                                                               
*                                                                               
DMF2EOF  MVI   DMCBW3,X'80'        RETURN EOF ERROR                             
         B     DMODF2X                                                          
DMF2NF   MVI   DMCBW3,X'10'        RETURN NOT FOUND                             
         B     DMODF2X                                                          
DMF2ERR  MVI   DMCBW3,X'40'        RETURN DISK ERROR                            
         B     DMODF2X                                                          
*                                                                               
DMODF2X  XIT1                                                                   
*                                                                               
         DS    0D                                                               
         DS    CL16'TEMPSTR INFO===>'                                           
TMPINFO  DS    0XL24               TEMPSTR - INFO                               
*                                                                               
TMPRPTRK DC    H'0'                TEMPSTR - RECS PER TRACK                     
TMPRPSEG DC    H'0'                TEMPSTR - RECS PER SEGMENT                   
TMPTRKS  DC    H'0'                TEMPSTR - NUMBER OF TRACKS                   
TMPTRMS  DC    H'0'                TEMPSTR - NUMBER OF TERMINALS                
TMPSEGS  DC    H'1'                TEMPSTR - NUMBER OF SEGMENTS                 
TMPRPTRM DC    H'12'               TEMPSTR - RECS PER TERMINAL                  
TMPRLEN  DC    H'18432'            TEMPSTR - RECORD LEN                         
TMPSSMAX DC    H'0'               *TEMPSTR - MAX SESSIONS PER TERMINAL          
TMPSSPGS DC    H'0'                TEMPSTR - PAGES PER SESSION                  
         DC    H'0'                TEMPSTR - N/D                                
*                                 *          ZERO FOR OLD STYLE TEMPSTR         
TMPTRK20 DC    F'0'                TEMPSTR - 20BIT COUNT OF TRACKS              
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
* THESE ROUTINES HANDLE READS/WRITES TO WRKF FILES                    *         
* THEY ARE SPECIAL BECAUSE THE WRKF INDEX RECORDS CAN BE BUFFERED     *         
***********************************************************************         
D2WF     NMOD1 0,**D2WF**          DMREAD - WKRF                                
         L     RF,=V(LOCKDA)                                                    
         BAS   RE,SETPR                                                         
         BASR  RE,RF               LOCK RECORD IF READ FOR UPDATE               
         LA    RE,72(RD)                                                        
         MVC   OLDKEY(24),K1-P1(RE) SAVE LOCKER'S PARAM LIST                    
         AP    D2WF#LC,=P'1'       *DEBUG*                                      
         TM    OLDKEY+8,X'04'      *DEBUG* TEST LOCK DONE THIS CALL             
         BZ    *+10                *DEBUG*                                      
         AP    D2WF#LD,=P'1'       *DEBUG*                                      
*                                                                               
         ICM   RE,15,=V(WKFLAG)    TEST IF BUFFERING WRKF INDEX RECS            
         BZ    D2WF2                                                            
         CLI   0(RE),C'N'                                                       
         BE    D2WF2                                                            
*                                                                               
         L     RE,PARAM4           TEST IF THIS WRKF IS BUFFERED                
*&&US*&& AHI   RE,-64                                                           
*&&UK*&& AHI   RE,-40                                                           
         USING CIDATA,RE                                                        
         TM    CIRSNF,CIXDSPQ                                                   
         BZ    D2WF2                                                            
         L     RF,DMCBW3           TEST IF THIS IS AN INDEX RECORD              
         CLC   CIFDTRK,0(RF)                                                    
         BNH   D2WF2                                                            
         DROP  RE                                                               
*                                                                               
         ICM   RF,15,XTCBNTRY      CLEAR LOGICAL I/O COUNTER                    
         BZ    D2WF1                                                            
         XC    TCBLIOC-TCBD(L'TCBLIOC,RF),TCBLIOC-TCBD(RF)                      
*                                                                               
D2WF1    MVC   DMCBW3(1),EXTFILE   GET RECORD FROM WRKF BUFFER POOL             
         LR    R0,R1                                                            
         GOTO1 =V(WKREAD),DMCBW1                                                
         LR    R1,R0                                                            
         AP    D2WF#RB,=P'1'       *DEBUG*                                      
         CLI   DMCBW3,X'FF'        TEST IF INDEX RECORD AVAILABLE               
         BNE   D2WFX                                                            
         MVI   DMCBW3,0            NO RESET TO READ RECORD FROM DISK            
         SP    D2WF#RB,=P'1'       *DEBUG*                                      
*                                                                               
D2WF2    LA    RE,72(RD)           RESTORE LOCKER'S PARAM LIST                  
         MVC   K1-P1(24,RE),OLDKEY                                              
         L     RF,=V(READDA)       READ RECORD FROM WRKF DISK                   
         BAS   RE,SETPR                                                         
         BASR  RE,RF                                                            
         AP    D2WF#RD,=P'1'       *DEBUG*                                      
*                                                                               
D2WFX    XIT1                                                                   
*                                                                               
         LTORG                                                                  
*                                                                               
         ENTRY D2WF#S              *DEBUG*                                      
         DS    0F                  *DEBUG*                                      
D2WF#S   DC    C'WF#S'             *DEBUG*                                      
D2WF#LC  DC    PL4'0'              *DEBUG* LOCKER CALLS                         
D2WF#LD  DC    PL4'0'              *DEBUG* LOCKER LOCKS                         
D2WF#RB  DC    PL4'0'              *DEBUG* READS FROM BUFF                      
D2WF#RD  DC    PL4'0'              *DEBUG* READS FROM DISK                      
D8WF#WB  DC    PL4'0'              *DEBUG* WRITES TO BUFF                       
D8WF#WD  DC    PL4'0'              *DEBUG* WRITES TO DISK                       
         EJECT                                                                  
***********************************************************************         
* WRITE TO WRKF AND OR CORE IN WKBUFFER                               *         
***********************************************************************         
D8WF     NMOD1 0,**D8WF**                                                       
         BAS   RE,SETDUM                                                        
         BASR  RE,RF               SET DTF ADDRESS IN PARAM4                    
*                                                                               
         TM    XFLAG1,XFONLINE     TEST ONLINE                                  
         BO    D8WF1                                                            
         ICM   RE,15,=V(SSB)       REQUIRES EXTENDED SSB                        
         BZ    D8WF1                                                            
         USING SSBD,RE                                                          
         CLI   SSOXTND,X'FF'                                                    
         BNE   D8WF1                                                            
         MVC   DSPACEV,SSODSPAC    SAVE DSPACE VALUE                            
*                                                                               
D8WF0    L     RE,PARAM4           MAKE SURE STAMP MATCHES                      
         USING CIDATA,RE                                                        
*&&US*&& AHI   RE,-64                                                           
*&&UK*&& AHI   RE,-40                                                           
         LLC   RF,CFPQINUM         GET INTERNAL NUMBER REALLY CFWKINUM          
         SLL   RF,1                                                             
D8WF0A   ICM   RE,15,=V(WKSHSTMP)  VERIFY STAMP                                 
         BZ    D8WF1                                                            
         B     D8WF0C                                                           
D8WF0B   ICM   RE,15,=V(WRKFSTMP)  VERIFY BEFORE WRITING ANYTHING               
         BZ    D8WF1                                                            
D8WF0C   AR    RE,RF               POINT TO STAMP DSPACE VALUE                  
         CLC   1(1,RE),DSPACEV                                                  
         BE    D8WF1                                                            
*&&US*&& CLI   DSPACEV,C'R'        DIE IF NOT REP SYSTEM                        
*&&US*&& BNE   *+12                                                             
*&&US*&& CLI   1(RE),C'A'          MUST BE LIVE FILE                            
*&&US*&& BE    D8WF1                                                            
         DC    H'0'                DIE=DOESNT MATCH                             
                                                                                
D8WF1    ICM   RE,15,=V(WKFLAG)    TEST IF BUFFERING WRKF INDEX RECS            
         BZ    D8WF2                                                            
         CLI   0(RE),C'N'                                                       
         BE    D8WF2                                                            
*&&US*&& B     D8WF2                                                            
         L     RE,PARAM4           TEST IF THIS WRKF IS BUFFERED                
         USING CIDATA,RE                                                        
*&&US*&& AHI   RE,-64                                                           
*&&UK*&& AHI   RE,-40                                                           
         TM    CIRSNF,CIXDSPQ                                                   
         BZ    D8WF2                                                            
         L     RF,DMCBW3           TEST IF THIS IS AN INDEX RECORD              
         CLC   CIFDTRK,0(RF)                                                    
         BNH   D8WF2                                                            
         DROP  RE                                                               
*&&US                                                                           
         XC    DUB,DUB             LOCK WRKF DATASPACE DATA                     
         LLC   RF,EXTFILE                                                       
         CLI   EXTFILE,X'19'       1 TO 9 OR A TO G                             
         BNH   *+8                                                              
         SHI   RF,X'E8'-X'1A'      X'E8'-X'EE' TO X'1A'-X'20'                   
*                                                                               
* 05/27/2016 TZIH                                                               
* DEFINITION FOR DTWRKF1 HAS BEEN REMOVED FROM FATABSDEQU BY AHYD               
* TEMPORARILY ADDING IT HERE, SO FOLLOWING AHI STATEMENT WOULD ASSEMBLE         
* THIS CODE WILL EITHER NEED TO BE REMOVED, OR EQU ADDED BACK                   
DTWRKF1  EQU   X'8000'+035         WKF1 - LOCK INDEX BY WORKER FILE NUM         
         AHI   RF,DTWRKF1-X'8011'                                               
         OILL  GRF,X'8000'                                                      
         ST    RF,DUB              SET WHICH ENTRY TO LOCK                      
         LR    R0,R1                                                            
         GOTO1 =V(LOCKSPC),DUB                                                  
         LR    R1,R0                                                            
         LAM   AR0,ARF,=16F'0'                                                  
*&&                                                                             
         MVC   DMCBW3(1),EXTFILE   SET WRKF FILE NUMBER                         
         LR    R0,R1                                                            
         GOTO1 =V(WKWRITE),DMCBW1  MOVE WRKF INDEX RECORD TO BUFFER             
         LR    R1,R0                                                            
         L     RE,=V(D2WF#S)       *DEBUG*                                      
         AP    20(4,RE),=P'1'      *DEBUG*                                      
*&&US                                                                           
         OI    DUB,X'10'           DUB STILL SET FROM 1ST LOCKSPC CALL          
         LR    R0,R1                                                            
         GOTO1 =V(LOCKSPC),DUB                                                  
         LR    R1,R0                                                            
         SAC   0                                                                
         LAM   AR0,ARF,=16F'0'                                                  
         MVI   DMCBW3,0            RESET DMCB RETURN FLAGS                      
*&&                                                                             
D8WF2    L     RE,=V(WRITE)        WRITE WRKF RECORD TO DISK                    
         ST    RE,PARAM1                                                        
         L     RE,PARAM4           GET DTF ADDRESS                              
         MVC   PARAM3+2(2),52(RE)  SET RECLEN FROM DTF                          
         NI    PARAM3+2,X'7F'                                                   
         XC    PARAM4+1(3),PARAM4+1                                             
         L     RF,=V(DMOD000)                                                   
         BASR  RE,RF                                                            
         L     RE,=V(D2WF#S)       *DEBUG*                                      
         AP    24(4,RE),=P'1'      *DEBUG*                                      
*                                                                               
D8WFX    XIT1                                                                   
*                                                                               
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
* DMREAD - WKRZ - NOT BUFFERED                                        *         
***********************************************************************         
*&&US                                                                           
D2WZ     NMOD1 0,**D2WZ**                                                       
         L     RF,=V(LOCKDA)                                                    
         BAS   RE,SETPR                                                         
         BASR  RE,RF               LOCK RECORD IF READ FOR UPDATE               
*                                                                               
         L     RF,=V(READDA)       READ RECORD FROM WRKZ DISK                   
         BAS   RE,SETPR                                                         
         BASR  RE,RF                                                            
         XIT1                                                                   
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
* DMWRT - WRKZ                                                        *         
***********************************************************************         
D8WZ     NMOD1 0,**D8WZ**                                                       
         BAS   RE,SETDUM                                                        
         BASR  RE,RF               SET DTF ADDRESS IN PARAM4                    
*                                                                               
         TM    XFLAG1,XFONLINE     TEST ONLINE                                  
         BO    D8WZ2                                                            
*                                                                               
         ICM   RE,15,=V(SSB)       NEED EXTENDED SSB                            
         BZ    D8WZ2                                                            
         USING SSBD,RE                                                          
         CLI   SSOXTND,X'FF'                                                    
         BNE   D8WZ2                                                            
         MVC   DSPACEV,SSODSPAC    SAVE DSPACE VALUE                            
*                                                                               
         L     RE,PARAM4           MAKE SURE STAMP MATCHES                      
         USING ZCIDATA,RE                                                       
         AHI   RE,-64                                                           
         LLC   RF,ZCFWKINUM        GET INTERNAL NUMBER REALLY CFWKINUM          
         SLL   RF,1                                                             
         ICM   RE,15,=V(WRKZSTMP)  VERIFY BEFORE WRITING ANYTHING               
         BZ    D8WZ2                                                            
         AR    RE,RF               POINT TO STAMP DSPACE VALUE                  
         CLC   DSPACEV,1(RE)                                                    
         BE    D8WZ2                                                            
*&&US*&& CLI   DSPACEV,C'R'        DIE IF NOT REP SYSTEM                        
*&&US*&& BNE   *+12                                                             
*&&US*&& CLI   1(RE),C'A'          MUST BE LIVE FILE                            
*&&US*&& BE    D8WZ2                                                            
         DC    H'0'                DIE=DOESNT MATCH                             
         DROP  RE                                                               
*                                                                               
D8WZ2    L     RE,=V(WRITE)        WRITE WRKZ RECORD TO DISK                    
         ST    RE,PARAM1                                                        
         L     RE,PARAM4                                                        
         MVC   PARAM3+2(2),52(RE)  SET RECLEN FROM DTF                          
         NI    PARAM3+2,X'7F'                                                   
         XC    PARAM4+1(3),PARAM4+1                                             
         L     RF,=V(DMOD000)                                                   
         BASR  RE,RF                                                            
*                                                                               
D8WZX    XIT1                                                                   
         LTORG                                                                  
*&&                                                                             
DSPACEV  DC    C' '                DSPACE=CARD VALUE                            
         EJECT                                                                  
***********************************************************************         
* DMREAD - PRTQ - PRTQ INDEX RECORDS CAN BE BUFFERED                  *         
***********************************************************************         
D2PQ     NMOD1 0,**D2PQ**                                                       
         L     RF,=V(LOCKDA)                                                    
         BAS   RE,SETPR                                                         
         BASR  RE,RF               LOCK RECORD IF READ FOR UPDATE               
         LA    RE,72(RD)                                                        
         MVC   OLDKEY(24),K1-P1(RE) SAVE LOCKER'S PARAM LIST                    
         AP    D2PQ#LC,=P'1'       *DEBUG*                                      
         TM    OLDKEY+8,X'04'      *DEBUG* TEST LOCK DONE THIS CALL             
         BZ    *+10                *DEBUG*                                      
         AP    D2PQ#LD,=P'1'       *DEBUG*                                      
                                                                                
         ICM   RE,15,=V(PQFLAG)    TEST IF BUFFERING PRTQ INDEX RECS            
         BZ    D2PQ2                                                            
         CLI   0(RE),C'N'                                                       
         BE    D2PQ2                                                            
*                                                                               
D2PQ0    L     RE,PARAM4           TEST IF THIS PRTQ IS BUFFERED                
         AHI   RE,-1                                                            
         CLI   0(RE),64                                                         
         BE    D2PQ0A                                                           
         AHI   RE,-39              OLD STYLE 40-BYTE HEADER                     
         USING PQWFOHDR,RE                                                      
         TM    PQWFOFLG,X'02'                                                   
         BZ    D2PQ2                                                            
         L     RF,DMCBW3           TEST IF THIS IS AN INDEX RECORD              
         CLC   PQWFOFDT,0(RF)                                                   
         BNH   D2PQ2                                                            
         B     D2PQ0X                                                           
*                                                                               
D2PQ0A   AHI   RE,-63              NEW STYLE 64-BYTE HEADER                     
         USING PQHDRDTF,RE                                                      
         TM    PQHDRFLG,X'02'      TEST IF INDEX RECS IN DATA SPACE             
         BZ    D2PQ2                                                            
         L     RF,DMCBW3           TEST IF THIS IS AN INDEX RECORD              
         CLC   PQHDRFDT,0(RF)                                                   
         BNH   D2PQ2                                                            
D2PQ0X   EQU   *                                                                
         DROP  RE                                                               
*                                                                               
         ICM   RF,15,XTCBNTRY      CLEAR LOGICAL I/O COUNTER                    
         BZ    D2PQ1                                                            
         XC    TCBLIOC-TCBD(L'TCBLIOC,RF),TCBLIOC-TCBD(RF)                      
*                                                                               
D2PQ1    MVC   DMCBW3(1),EXTFILE   GET RECORD FROM PRTQ BUFFER POOL             
         LR    R0,R1                                                            
         GOTO1 =V(PQREAD),DMCBW1                                                
         LR    R1,R0                                                            
         AP    D2PQ#RB,=P'1'       *DEBUG*                                      
         CLI   DMCBW3,X'FF'        TEST IF INDEX RECORD AVAILABLE               
         BNE   D2PQX                                                            
         MVI   DMCBW3,0            NO RESET TO READ RECORD FROM DISK            
         SP    D2PQ#RB,=P'1'       *DEBUG*                                      
*                                                                               
D2PQ2    LA    RE,72(RD)           RESTORE LOCKERS PARAM LIST                   
         MVC   K1-P1(24,RE),OLDKEY                                              
         L     RF,=V(READDA)       READ RECORD FROM PRTQ DISK                   
         BAS   RE,SETPR                                                         
         BASR  RE,RF                                                            
         AP    D2PQ#RD,=P'1'       *DEBUG*                                      
*                                                                               
D2PQX    XIT1                                                                   
*                                                                               
         LTORG                                                                  
         ENTRY D2PQ#S              *DEBUG*                                      
         DS    0F                  *DEBUG*                                      
D2PQ#S   DC    C'PQ#S'             *DEBUG*                                      
D2PQ#LC  DC    PL4'0'              *DEBUG* LOCKER CALLS                         
D2PQ#LD  DC    PL4'0'              *DEBUG* LOCKER LOCKS                         
D2PQ#RB  DC    PL4'0'              *DEBUG* READS FROM BUFF                      
D2PQ#RD  DC    PL4'0'              *DEBUG* READS FROM DISK                      
D8PQ#WB  DC    PL4'0'              *DEBUG* WRITES TO BUFF                       
D8PQ#WD  DC    PL4'0'              *DEBUG* WRITES TO DISK                       
         EJECT                                                                  
***********************************************************************         
* DMWRT - PRTQ - PRTQ INDEX RECORDS CAN BE BUFFERED                   *         
***********************************************************************         
D8PQ     NMOD1 0,**D8PQ**                                                       
         BAS   RE,SETDUM                                                        
         BASR  RE,RF               SET DTF ADDRESS IN PARAM4                    
*                                                                               
         ICM   RE,15,=V(WQFLAG)    TEST IF BUFFERING PRTQ INDEX RECS            
         BZ    D8PQ2                                                            
         CLI   0(RE),C'N'                                                       
         BE    D8PQ2                                                            
*                                                                               
D8PQ0    L     RE,PARAM4           TEST IF THIS PRTQ IS BUFFERED                
         AHI   RE,-1                                                            
         CLI   0(RE),64                                                         
         BE    D8PQ0A                                                           
         AHI   RE,-39              OLD STYLE 40-BYTE DTF HEADER                 
         USING PQWFOHDR,RE                                                      
         TM    PQWFOFLG,X'02'                                                   
         BZ    D8PQ2                                                            
         L     RF,DMCBW3           TEST IF THIS IS AN INDEX RECORD              
         CLC   PQWFOFDT,0(RF)                                                   
         BNH   D8PQ2                                                            
         B     D8PQ0X                                                           
*                                                                               
D8PQ0A   AHI   RE,-63              NEW STYLE 64-BYTE HEADER                     
         USING PQHDRDTF,RE                                                      
         TM    PQHDRFLG,X'02'      TEST IF INDEX RECS IN DATA SPACE             
         BZ    D8PQ2                                                            
         L     RF,DMCBW3           TEST IF THIS IS AN INDEX RECORD              
         CLC   PQHDRFDT,0(RF)                                                   
         BNH   D8PQ2                                                            
D8PQ0X   EQU   *                                                                
         DROP  RE                                                               
*                                                                               
         XC    DUB,DUB             LOCK PRTQ DATASPACE DATA                     
         MVC   DUB(4),=AL4(500)                                                 
         LR    R0,R1                                                            
         GOTO1 =V(LOCKSPC),DUB                                                  
         LR    R1,R0                                                            
*                                                                               
         MVC   DMCBW3(1),EXTFILE   SET PRTQ FILE NUMBER                         
         LR    R0,R1                                                            
         GOTO1 =V(PQWRITE),DMCBW1  MOVE PRTQ INDEX RECORD TO BUFFER             
         LR    R1,R0                                                            
         L     RE,=V(D2PQ#S)       *DEBUG*                                      
         AP    20(4,RE),=P'1'      *DEBUG*                                      
*                                                                               
         XC    DUB,DUB             UNLOCK PRTQ DATASPACE DATA                   
         MVC   DUB(4),=AL4(500)                                                 
         OI    DUB,X'10'                                                        
         LR    R0,R1                                                            
         GOTO1 =V(LOCKSPC),DUB                                                  
         LR    R1,R0                                                            
         MVI   DMCBW3,0            RESET DMCB RETURN FLAGS                      
*                                                                               
D8PQ1    EQU   *                   CODE TO CHECK FIRST INDEX REC IS OK          
*&&US*&& B     D8PQ1X                                                           
*&&UK*&& B     *+4                                                              
         L     RF,DMCBW3           TEST IF WRITING FIRST INDEX REC              
         CLC   0(3,RF),=X'000101'                                               
         BNE   D8PQ1X                                                           
         L     RF,DMCBW4           RF=A(RECORD)                                 
         L     RE,PARAM4                                                        
         AHI   RE,-1                                                            
         CLI   0(RE),0                                                          
         BNE   D8PQ1X                                                           
         AHI   RE,-39              OLD STYLE 40-BYTE DTF HEADER                 
         USING PQWFOHDR,RE                                                      
         CLI   0(RF),X'80'                                                      
         BE    *+12                                                             
         CLI   0(RF),X'00'                                                      
         BNE   D8PQ1E                                                           
         CLC   01(13,RF),01(RE)    PART#1 DEFINITION                            
         BNE   D8PQ1E                                                           
         CLC   24(16,RF),20(RE)    PART#2 DEFINITION                            
         BNE   D8PQ1E                                                           
         B     D8PQ1X                                                           
D8PQ1E   B     *+4                                                              
         WTO   '+PQHEADER+ JOB IS TRYING TO MODIFY HEADER '                     
         DC    H'0'                DIE=MODIFY HEADER                            
D8PQ1X   EQU   *                                                                
*                                                                               
D8PQ2    L     RE,=V(WRITE)        WRITE PRTQ RECORD TO DISK                    
         ST    RE,PARAM1                                                        
         L     RE,PARAM4           GET DTF ADDRESS                              
         MVC   PARAM3+2(2),52(RE)  SET RECLEN FROM DTF                          
         NI    PARAM3+2,X'7F'                                                   
         XC    PARAM4+1(3),PARAM4+1                                             
         L     RF,=V(DMOD000)                                                   
         BASR  RE,RF                                                            
         L     RE,=V(D2PQ#S)       *DEBUG*                                      
         AP    24(4,RE),=P'1'      *DEBUG*                                      
*                                                                               
D8PQ2X   XIT1 1                                                                 
*                                                                               
         LTORG                                                                  
         EJECT                                                                  
* TRACE TABLE HEADER FORMAT IS                                                  
*                                                                               
* +00  *TRACEBUF*TRACEBUF*TRACEBUF*TRACEBUF                                     
* +24  TOTAL NUMBER OF I/O'S THIS TRANSACTION                                   
* +28  INTERVAL BETWEEN I/O'S FOR TRACE                                         
* +32  CURRENT SYSTEM INPUT NUMBER                                              
* +36  CURRENT NUMBER OF ENTRIES IN BUFFER                                      
* +40  A(BUFFER END)                                                            
* +44  AL1(TRACE STATUS BYTES)  X'80'=SUSPEND TRACE                             
*      AL3(RD OF FIRST CALLER FOR RECURSION LOCK)                               
*                                                                               
* TRACE TABLE DATA ENTRIES LOOK LIKE  (LENGTH 144)                              
*                                                                               
* +48 ... IS FIRST ENTRY                                                        
*                                                                               
* +00(8)  COMMAND                                                               
* +08(8)  FILENAME                                                              
* +16(1)  DMCBW1(1) ON INPUT                                                    
* +17(63) DATA AT DMCBW3 ON INPUT                                               
* +80(1)  DMCBW3(1) ON OUTPUT                                                   
* +81(63) DATA AT DMCBW4 ON OUTPUT                                              
                                                                                
         DS    0D                                                               
         USING DMGRWORK,R1                                                      
         USING DMGRFRST,DMGRWORK                                                
*                                                                               
DMTRACE  NMOD1 0,**DMTR**                                                       
         ICM   R3,15,XUTLNTRY      GET UTL ENTRY ADDRESS                        
         BZ    DMTRACEX                                                         
         USING UTLD,R3                                                          
         SAM31                                                                  
*                                                                               
         TM    TTEST,X'10'         TEST TRM IN TEST MODE                        
         BZ    DMTRACEX                                                         
         CLI   TPRG,1              TEST PFM (THANK YOU DOODLE)                  
         BE    DMTRACEX                                                         
         ICM   R4,15,TACCS         GET TSTTAB ENTRY ADDRESS                     
         BZ    DMTRACEX                                                         
         USING TSTTABD,R4                                                       
         ICM   R6,15,TSTTRC        GET TRACE TABLE ADDRESS                      
         BNP   DMTRACEX                                                         
*                                                                               
DMTRACE0 CLC   TSIN,32(R6)         TEST SAME SIN                                
         BE    DMTRACE1                                                         
         XC    24(4,R6),24(R6)     CLEAR TOTAL I/O COUNT                        
         MVC   32(4,R6),TSIN       SAVE SIN                                     
         XC    36(4,R6),36(R6)     RESET ENTRY COUNT                            
         XC    44(4,R6),44(R6)     CLEAR FLAGS/RD LOCK                          
         XC    TSTLAST,TSTLAST                                                  
         MVC   TSTLAST(2),TSTLOW                                                
*                                                                               
DMTRACE1 CLI   TRACEON,C'S'        TEST 'DMTRACE' COMMAND                       
         BNE   DMTRACE2                                                         
         XC    45(3,R6),45(R6)     CLEAR RD LOCK                                
         SR    RE,RE                                                            
         ICM   RE,7,5(R2)          GET DMCBW2                                   
         CLC   0(3,RE),=C'OFF'                                                  
         BNE   *+12                                                             
         OI    44(R6),X'80'        SET 'SUSPEND TRACE'                          
         B     DMTRACEX                                                         
*                                                                               
         CLC   0(2,RE),=C'ON'                                                   
         BNE   *+12                                                             
         NI    44(R6),X'7F'        UNSET 'SUSPEND TRACE'                        
         B     DMTRACEX                                                         
*                                                                               
         CLC   0(4,RE),=C'DATA'                                                 
         BNE   DMTRACEX                                                         
         B     DMTRACE3                                                         
*                                                                               
DMTRACE2 TM    44(R6),X'80'        TEST USER SUSPENDED TRACE                    
         BO    DMTRACEX                                                         
         OC    45(3,R6),45(R6)     TEST LOCK ACTIVE                             
         BNZ   *+12                                                             
         STCM  RD,7,45(R6)         SET LOCK RD                                  
         B     DMTRACE3                                                         
*                                                                               
         CLM   RD,7,45(R6)         MATCH LOCK ADDRESS                           
         BNE   DMTRACEX            NO IGNORE                                    
         XC    45(3,R6),45(R6)     RELEASE LOCK                                 
*                                                                               
DMTRACE3 TM    TSTAT9,TST9PROF     TRACE USER PROFILE? 1=NO, 0=YES              
         BZ    DMTRAC3A                                                         
         SR    RE,RE                                                            
         ICM   RE,7,5(R2)          GET FILENAME ADDRESS (DMCBW2)                
         CLC   0(6,RE),=C'CTFILE'                                               
         BNE   DMTRAC3A                                                         
         ICM   RE,7,9(R2)          GET KEY ADDRESS (DMCBW3)                     
         CLI   0(RE),C'U'          SKIP USER PROFILE RECORD                     
         BE    DMTRACEX                                                         
*                                                                               
DMTRAC3A L     R7,36(R6)           GET CURRENT ENTRY COUNT                      
         CH    R7,TSTTRCIO         COMPARE TO REQUESTED START COUNT             
         BL    DMTRACE8            LOW - DO NOT TRACE (BUT DO COUNT)            
         SR    RE,RE                                                            
         L     RF,24(R6)           GET TOTAL I/O COUNT                          
         SH    RF,TSTTRCIO         RELATIVE I/O COUNT FROM START COUNT          
         ICM   R0,15,28(R6)        GET INTERVAL                                 
         BZ    *+12                                                             
         DR    RE,R0               DIVIDE COUNT BY INTERVAL                     
         LTR   RE,RE               TEST REMAINDER                               
         BNZ   DMTRACEA            THIS IS NOT ONE OF THE CHOSEN FEW            
         SH    R7,TSTTRCIO         SUBT REQ START TO GET ENTRY NUMBER           
         MH    R7,=Y(TSTTRCLN)     COUNT X ENTRY LENGTH                         
         LA    R7,48(R7,R6)        POINT TO NEXT SLOT                           
         LA    R0,TSTTRCLN(R7)     POINT TO END OF ENTRY                        
         BCTR  R0,0                BACK UP ONE BYTE                             
         C     R0,40(R6)           TEST PAST END OF TABLE                       
         BH    DMTRACEA            IF HIGH JUST ADD TO TRANS COUNT              
         CLI   TRACEON,C'I'        TEST INPUT TRACE                             
         BNE   DMTRACE4                                                         
         SR    RE,RE                                                            
         ICM   RE,7,5(R2)          GET FILENAME ADDRESS                         
         CLC   0(6,RE),=C'TSTRCV'  DO NOT TRACE TSTRCVR                         
         BE    DMTRACEX                                                         
         CLI   TRACEON,C'I'        TEST INPUT TRACE                             
         BNE   DMTRACE4                                                         
         MVC   8(8,R7),0(RE)       MOVE FILE NAME                               
         ICM   RE,7,1(R2)          GET COMMAND ADDRESS                          
         MVC   0(8,R7),0(RE)       MOVE COMMAND                                 
         ICM   RE,7,5(R2)                                                       
         MVC   8(8,R7),0(RE)       MOVE FILE NAME                               
         MVC   16(1,R7),0(R2)      MOVE DMINBTS                                 
*                                                                               
         LA    RF,8(R2)            POINT TO DMCBW3                              
         CLC   0(4,R7),=C'DMFA'    TEST DMFAST COMMAND                          
         BE    DMTRACEX                                                         
         CLC   0(4,R7),=C'DMAD'    TEST DMADD COMMAND                           
         BE    *+14                                                             
         CLC   0(4,R7),=C'ADDR'    TEST ADDREC COMMAND                          
         BNE   *+8                                                              
         LA    RF,12(R2)           POINT TO DMCBW4                              
*                                                                               
         ICM   RE,15,0(RF)         ALLOW FOR 31-BIT ADDRESSES                   
         TM    0(R2),X'40'         TEST 31-BIT CALLER                           
         BO    *+10                                                             
         SR    RE,RE                                                            
         ICM   RE,7,1(RF)          USE 24-BIT ADDRESS                           
*                                                                               
         MVC   17(63,R7),0(RE)     MOVE INPUT ARG                               
         CLC   8(4,R7),=C'TEMP'    TEST TEMPSTR/TEMPEST                         
         BNE   *+10                                                             
         MVC   17(1,R7),SVPAGE     SAVE PAGE NUMBER FOR TEMPSTR                 
         B     DMTRACEX                                                         
*                                                                               
DMTRACE4 CLI   TRACEON,C'O'        TEST OUTPUT TRACE                            
         BNE   DMTRACE5                                                         
         MVC   80(1,R7),8(R2)      MOVE DMOUTBTS                                
*                                                                               
         LA    RF,12(R2)           POINT TO DMCBW4                              
         CLC   0(4,R7),=C'DMAD'    TEST DMADD COMMAND                           
         BE    *+14                                                             
         CLC   0(4,R7),=C'ADDR'    TEST ADDREC COMMAND                          
         BNE   *+8                                                              
         LA    RF,8(R2)            POINT TO DMCBW3                              
*                                                                               
         ICM   RE,15,0(RF)         ALLOW FOR 31-BIT ADDRESSES                   
         TM    0(R2),X'40'         TEST 31-BIT CALLER                           
         BO    *+10                                                             
         SR    RE,RE                                                            
         ICM   RE,7,1(RF)          USE 24-BIT ADDRESS                           
         MVC   81(63,R7),0(RE)     MOVE OUTPUT DATA                             
         B     DMTRACE8                                                         
*                                                                               
DMTRACE5 MVC   0(8,R7),=CL8'DMTRACE'                                            
         MVC   8(8,R7),=CL8'DATA'                                               
         ICM   RE,7,9(R2)          GET DATA ADDRESS                             
         MVC   16(127,R7),0(RE)    MOVE USER TRACE LEN AND DATA                 
*                                                                               
DMTRACE8 CLI   TRACEON,C'I'        TEST INPUT TRACE                             
         BE    DMTRACEX                                                         
*                                                                               
DMTRACE9 L     RE,36(R6)           GET ENTRY COUNT                              
         LA    RE,1(RE)                                                         
         ST    RE,36(R6)                                                        
*                                                                               
DMTRACEA CLI   TRACEON,C'I'                                                     
         BE    DMTRACEX                                                         
         L     RE,24(R6)           ADD TO TRANSACTION I/O COUNT                 
         LA    RE,1(RE)                                                         
         ST    RE,24(R6)                                                        
*                                                                               
DMTRACEX XIT1                                                                   
*                                                                               
         DROP  R3,R4                                                            
         LTORG                                                                  
         EJECT                                                                  
*&&SRV                                                                          
***********************************************************************         
* ROUTINES TO PROCESS TEMPEST FILES. PAGE NUM SAVED IN SVPAGE         *         
* ACTIONS ARE DMREAD/DMWRT/DMRSRV/DMRLSE                              *         
* NOTE: RESTORE R2 IF YOU EVER CALL DMOD000                           *         
***********************************************************************         
         DROP  R1                                                               
DMODFE   NMOD1 0,**DMFE**                                                       
         STAR  CLEAR=Y,ARS=OFF                                                  
         LR    RC,R1                                                            
         USING DMGRWORK,RC                                                      
         USING DMGRFRST,DMGRWORK                                                
*                                                                               
         ST    R2,SAVER2                                                        
*                                                                               
         MVC   PARAM4,=V(TEMPEST)  SET A(DTF)                                   
         MVI   PARAM4,X'FE'                                                     
*                                                                               
         ICM   RF,15,TMANTRY       ALREADY INITIALISED?                         
         BNZ   *+8                                                              
         BRAS  RE,SETTMSR          SET UP INITIAL DETAILS IF REQUIRED           
*                                                                               
         ICM   R3,15,=V(SSB)       R3=A(SSB)                                    
         JZ    *+2                 DIE=NO SSB                                   
         USING SSBD,R3                                                          
         ICM   R4,15,SSBTKADR      R4=A(TCB)                                    
         JZ    *+2                 DIE=NO TCB                                   
         USING TCBD,R4                                                          
         ICM   R5,15,TCBUTL        R5=A(UTL)                                    
         JZ    *+2                 DIE=NO UTL                                   
         USING UTLD,R5                                                          
*                                                                               
         CLI   ACTION,X'0E'        RESERVE                                      
         BE    DEFE                                                             
         CLI   ACTION,X'0F'        RELEASE                                      
         BE    DFFE                                                             
         CLI   ACTION,X'02'        READ                                         
         BE    D2FE                                                             
         CLI   ACTION,X'08'        WRITE                                        
         BE    D8FE                                                             
         DC    H'0'                DIE=INVALID ACTION                           
         EJECT                                                                  
***********************************************************************         
* DMRSRV - RESERVE SPACE IN TEMPEST                                   *         
***********************************************************************         
DEFE     XC    SVDATA,SVDATA                                                    
         MVC   SVDATA+1(L'SVPAGE),SVPAGE                                        
*                                                                               
         LAM   AR0,ARF,SARZERO                                                  
         LAM   AR2,AR2,SSBTBLET                                                 
         L     R2,TMANTRY          GET DATASPACE AREA                           
         SAC   512                                                              
         USING TTSSHDRD,R2                                                      
         L     RF,TTSLINK          UP ALLOCATION COUNTER                        
         AHI   RF,1                                                             
         ST    RF,TTSLINK                                                       
*                                                                               
         LH    R0,TTSRPTRK         PHYSICAL RECORDS PER TRACK                   
         STH   R0,DMCBW3+2         RETURN RECS/TRK IN DMCBW3+2(2)               
*                                                                               
         LH    RF,SVDATA           RF=NUM OF PAGES REQUESTED                    
         CLI   DMCBW3+1,C'P'       TEST IF WANTED PAGES                         
         BNE   DEFE02                                                           
*                                                                               
         XR    RE,RE               CONVERT PAGES TO TRACKS                      
         DR    RE,R0                                                            
         LTR   RE,RE               ROUND UP                                     
         BZ    *+8                                                              
         LA    RF,1(RF)                                                         
         STH   RF,SVDATA           SVDATA= NUM OF TARCKS                        
*                                                                               
DEFE02   STH   RF,DMCBW3           SET THS NUM TRACKS IN DMCBW3+0(2)            
         LH    R0,TCBXTNUM                                                      
         STH   R0,DMCBW4           SET PRV NUM TRACKS IN DMCBW4+0(2)            
         LH    RE,TTSCITRK                                                      
         STH   RE,DMCBW4+2         SET MAX NUM TRACKS IN DMCBW4+2(2)            
*                                                                               
         LTR   RF,RF               TEST IF WANT TO ALLOCATE ANYTHING            
         BZ    DEFEX                                                            
*                                                                               
         AR    R0,RF               CAN WE ALLOCATE THIS MUCH?                   
         CR    R0,RE                                                            
         BH    D2FEEOF             NO TOO GREEDY                                
*                                                                               
         ICM   R0,15,TCBXTNDX      TEMPEST BUFFER RESERVED ALREADY              
         BNZ   DEFE16                                                           
*                                                                               
DEFE04   ICM   RE,15,TTSUSECI      BUMP CURRENT ALLOCATION COUNT                
         LA    RF,1(RE)                                                         
         C     RF,TTSMAXCI                                                      
         BH    D2FEEOF             NO MORE ALLOCATIONS AVAILABLE                
         CS    RE,RF,TTSUSECI      REPLACE TTSUSECI WITH NEW NUMBER             
         BNE   DEFE04              COULDN'T SOME ONE GOT TO IT FIRST            
*                                                                               
DEFE06   LA    R6,TTSSHDRL(,R2)    R6=A(ALLOCATION BLOCK)                       
         CPYA  AR6,AR2                                                          
         L     R7,TTSMAXCI                                                      
*                                                                               
DEFE08   ICM   RE,15,0(R6)         GET AN EMPTY SLOT                            
         BNZ   DEFE10                                                           
         ICM   RF,15,4(R6)                                                      
         BNZ   DEFE10                                                           
         LM    R0,R1,TLUID                                                      
         CDS   RE,R0,0(R6)         COMPARE VALUES IN RE,RF TO LOC 0(R6)         
         BE    DEFE12              REPLACED 0(R6) WITH VALUES IN R0,R1          
*                                                                               
DEFE10   AHI   R6,L'TLUID                                                       
         BCT   R7,DEFE08                                                        
         B     D2FEEOF             NO EMPTY SLOTS RIGHT NOW                     
*                                                                               
DEFE12   LR    RE,R6               NOW GET INDEX NUMBER                         
         LA    RF,TTSSHDRL(R2)                                                  
         SR    RE,RF                                                            
         SRDL  RE,32                                                            
         LHI   R0,L'TLUID                                                       
         DR    RE,R0                                                            
         LTR   RE,RE               REMAINDER?                                   
         JNZ   *+2                 DIE=MUST BE DIVISABLE                        
*                                                                               
         AHI   RF,1                MAKE THIS BASE 1 (1=FIRST ENTRY)             
         STCM  RF,15,TCBXTNDX      SAVE INDEX IN TCB                            
*                                                                               
         ICM   RE,15,TTSHICI       GET CURRENT HIGH WATER MARK                  
DEFE14   CR    RE,RF                                                            
         BH    DEFE16                                                           
         CS    RE,RF,TTSHICI                                                    
         BNE   DEFE14                                                           
         B     DEFE18                                                           
         DROP  R2                                                               
*                                                                               
DEFE16   ICM   RF,15,TCBXTNDX      NOW MAKE SURE LUIDS MATCH OK                 
         BCTR  RF,0                                                             
         MHI   RF,L'TLUID                                                       
         LA    RF,TTSSHDRL(RF,R2)                                               
         CPYA  ARF,AR2                                                          
         CLC   TLUID,0(RF)                                                      
         BE    DEFE18              TEMPEST BUFFER MATCHES STILL                 
         MVC   DMTAWHY,TALLOC                                                   
         B     DFE2BAD                                                          
*                                                                               
DEFE18   OI    TFLAG,TFLAGRTS      SET TEMPEST ASSIGNED                         
         LH    RE,TCBXTNUM                                                      
         LH    RF,SVDATA           UPDATE NUMBER OF TRACKS USED                 
         AR    RE,RF                                                            
         STH   RE,TCBXTNUM         AND SAVE                                     
*                                                                               
DEFEX    B     DMODFEX                                                          
         EJECT                                                                  
***********************************************************************         
* DMRLSE - RELEASE ALLOCATED TEMPEST PAGES                            *         
***********************************************************************         
DFFE     XC    SVDATA,SVDATA                                                    
         MVC   SVDATA+1(1),SVPAGE                                               
*                                                                               
         LAM   AR0,ARF,SARZERO                                                  
         LAM   AR2,AR2,SSBTBLET                                                 
         L     R2,TMANTRY          GET DATASPACE AREA                           
         SAC   512                                                              
         USING TTSSHDRD,R2                                                      
*                                                                               
         LH    R0,TTSRPTRK         SET NUMBER OF RECORDS PER TRACK              
         STH   R0,DMCBW3+2         RETURN RECS/TRK IN DMCBW3+2(2)               
*                                                                               
         LH    RF,SVDATA                                                        
         CHI   RF,X'FF'            X'FF' MEANS RELEASE ALL                      
         BE    DFFE02                                                           
         CLI   DMCBW3+1,C'P'       RELEASE PAGES?                               
         BNE   DFFE02                                                           
         XR    RE,RE               CONVERT PAGES TO TRACKS                      
         DR    RE,R0                                                            
         LTR   RE,RE                                                            
         BZ    *+8                                                              
         LA    RF,1(RF)                                                         
*                                                                               
DFFE02   LH    RE,TCBXTNUM                                                      
         CR    RF,RE               RELEASE MORE THAN RESERVED?                  
         BNH   *+6                                                              
         LR    RF,RE               ONLY RELEASE THOSE RESERVED                  
         STH   RF,SVDATA                                                        
         STH   RF,DMCBW3           THIS NUM TRACKS IN DMCBW3+0(2)               
*                                                                               
         STH   RE,DMCBW4           PREV NUM TRACKS IN DMCBW4+0(2)               
         LH    R0,TTSCITRK                                                      
         STH   R0,DMCBW4+2         MAX  NUM TRACKS IN DMCBW4+2(2)               
         LTR   RE,RE                                                            
         BZ    DFFEX                                                            
         LTR   RF,RF                                                            
         BZ    DFFEX                                                            
         SR    RE,RF                                                            
         BNZ   *+8                                                              
         NI    TFLAG,255-TFLAGRTS                                               
         STH   RE,TCBXTNUM                                                      
*                                                                               
         TM    TFLAG,TFLAGRTS      RELEASED ALL TEMPEST FOR THIS UTL            
         BNZ   DFFEX                                                            
*                                                                               
         ICM   RF,15,TCBXTNDX                                                   
         BCTR  RF,0                                                             
         MHI   RF,L'TLUID                                                       
         LA    RF,TTSSHDRL(RF,R2)                                               
         CPYA  ARF,AR2                                                          
         CLC   TLUID,0(RF)                                                      
         BNE   DFFE08              IGNORE LUID MISMATCH                         
*NOP*    BE    DFFE04              TEMPEST BUFFER MATCHES STILL                 
*NOP*    MVC   DMTAWHY,TDALLOC                                                  
*NOP     B     DFE2BAD                                                          
*                                                                               
DFFE04   XC    0(L'TLUID,RF),0(RF)                                              
         LAM   ARF,ARF,SARZERO                                                  
         ICM   RE,15,TTSUSECI                                                   
*                                                                               
DFFE06   LTR   RF,RE                                                            
         BZ    DFFE08                                                           
         BCTR  RF,0                                                             
         CS    RE,RF,TTSUSECI                                                   
         BNZ   DFFE06                                                           
*                                                                               
DFFE08   SAC   0                                                                
         LAM   AR0,ARF,SARZERO                                                  
         XC    TCBXTNDX,TCBXTNDX   CLEAR INDEX NUMBER IN TCB                    
*                                                                               
DFFEX    B     DMODFEX                                                          
         DROP  R2                                                               
         EJECT                                                                  
***********************************************************************         
* PROBLEM WITH EITHER ALLOCATE OR DEALLOCATE - OUTPUT DEBUG INFO      *         
***********************************************************************         
DFE2BAD  MVC   DMTAOWN,TLUID                                                    
         MVC   DMTAGOT,0(RF)                                                    
         ICM   R0,15,TCBXTNDX                                                   
         CVD   R0,DUB                                                           
         OI    DUB+L'DUB-1,X'0F'                                                
         UNPK  DMTNUM,DUB                                                       
*                                                                               
         SAC   0                   OUTPUT BAD ALLOCATE MESSAGE                  
         LAM   AR0,ARF,SARZERO                                                  
         L     R2,SAVER2                                                        
         GOTO1 =V(DMOD000),DMGABAD                                              
*                                                                               
         ICM   R0,15,TCBXTNDX      DO SOME CLEANUP                              
         XC    TCBXTNDX,TCBXTNDX                                                
         NI    TFLAG,255-TFLAGRTS                                               
*                                                                               
         OC    TSVCREQ,TSVCREQ     IGNORE ERROR FOR SERVICE REQUEST             
         BNZ   DMODFEX                                                          
*                                                                               
         ICM   RE,15,TCBTIA        COPY BLOCK AS IS INTO TCBWRK                 
         BZ    DFE2BX              AFTER MONITOR                                
         MVC   0(4,RE),=CL4'*SOB'                                               
         MVC   4(28,RE),0(RE)                                                   
         AHI   RE,32                                                            
         LR    R0,RE               SAVE START OF BUFFER                         
         LR    R1,RE                                                            
*                                                                               
         LAM   AR0,ARF,SARZERO                                                  
         LAM   AR6,AR6,SSBTBLET                                                 
         L     R6,TMANTRY          GET DATASPACE AREA                           
         SAC   512                                                              
         USING TTSSHDRD,R6                                                      
         ICM   R7,15,TTSMAXCI                                                   
         MHI   R7,L'TLUID                                                       
         AHI   R7,TTSSHDRL                                                      
         LR    RF,R7                                                            
         AR    R1,RF               POINT TO END OF MOVE BLOCK                   
         MVCL  RE,R6                                                            
*                                                                               
         MVC   0(4,R1),=CL4'*EOB'  FLAG END OF BUFFER                           
         MVC   4(28,R1),0(R1)                                                   
         SAC   0                                                                
         LAM   AR0,AR6,SARZERO                                                  
         DROP  R6                                                               
*                                                                               
DFE2BX   DC    H'0'                DIE=DEBUG DATA                               
         EJECT                                                                  
***********************************************************************         
* DMREAD - READ TEMPEST PAGE                                          *         
***********************************************************************         
D2FE     MVC   PARAM7,=V(READ)                                                  
         LAM   AR0,ARF,SARZERO                                                  
         LAM   AR2,AR2,SSBTBLET                                                 
         L     R2,TMANTRY          GET DATASPACE AREA                           
         SAC   512                                                              
         USING TTSSHDRD,R2                                                      
*                                                                               
         MVC   FULL(2),=C'L='                                                   
         MVC   FULL+2(2),=H'6144'                                               
*                                                                               
         CLC   DMCBW6(2),=C'L='    TEST IF NON STANDARD READ LENGTH             
         BNE   D2FE02                                                           
         MVC   FULL,DMCBW6                                                      
         OC    FULL+2(2),FULL+2    LEN MUST BE GT ZERO AND LE TMSRLEN           
         JZ    *+2                 DIE=ZERO REC LEN                             
         CLC   FULL+2(2),TTSRLEN   MAX RECORD LENGTH                            
         JH    *+2                 DIE=MAX REC LEN                              
*                                                                               
D2FE02   CLC   DMCBW3,=X'0000FFFF' SPECIAL CALL FOR TEMPEST INFO                
         BNE   D2FEIO                                                           
         L     RE,DMCBW4           POINT TO CALLERS RECORD AREA                 
         MVC   0(TTSSHDRL,RE),TTSSHDRD                                          
         B     DMODFEX                                                          
         EJECT                                                                  
***********************************************************************         
* DMWRT - WRITE TEMPEST PAGE                                          *         
***********************************************************************         
D8FE     MVC   PARAM7,=V(WRITE)                                                 
         LAM   AR0,ARF,SARZERO                                                  
         LAM   AR2,AR2,SSBTBLET                                                 
         L     R2,TMANTRY          GET DATASPACE AREA                           
         SAC   512                                                              
         USING TTSSHDRD,R2                                                      
*                                                                               
         XC    FULL,FULL                                                        
         MVC   FULL+2(2),TTSRLEN   SET RECORD LENGTH                            
         B     D2FEIO                                                           
         EJECT                                                                  
***********************************************************************         
* PERFORM I/O TO TEMPEST FILE                                         *         
***********************************************************************         
D2FEIO   LH    R0,TTSRPTRK         GET NUMBER OF RECORDS PER TRACK              
*                                                                               
         XR    R6,R6                                                            
         ICM   R6,1,SVPAGE         FIRST PAGE IS PAGE 1                         
         BZ    D2FEEOF                                                          
         BCTR  R6,0                ZERO BASE PAGE NUMBER                        
         SRDL  R6,32                                                            
         DR    R6,R0               R7=TRACK TO READ IN                          
         CH    R7,TCBXTNUM         IS RECORD WITHIN CURRENT ALLOCATION?         
         BNL   D2FEEOF                                                          
*                                                                               
         ICM   RF,15,TCBXTNDX      ZERO INDEX NOT ALLOCATED                     
         BZ    D2FEEOF                                                          
         BCTR  RF,0                ZERO BASE INDEX NUMBER                       
         MH    RF,TTSCITRK                                                      
         SAC   0                   NO USE FOR TMSHDR ANYMORE                    
         DROP  R2                                                               
*                                                                               
         LA    R7,1(RF,R7)         FIND CORRECT TRACK TO READ                   
         SLL   R7,16                                                            
         ST    R7,PARAM6           SET RELATIVE TRACK NUMBER                    
         LA    R6,1(R6)            R6=RECORD (BLOCK) READ                       
         STC   R6,PARAM6+2         SET RELATIVE RECORD ON TRACK (BB)            
*                                                                               
         MVC   PARAM1,PARAM7       SET ACTION                                   
         MVC   PARAM2,DMCBW4       SET I/O AREA                                 
         MVC   PARAM3,FULL         SET REC LENGTH                               
         LA    RE,PARAM6           SET DISK ADDR                                
         ST    RE,PARAM5                                                        
*                                                                               
         L     R2,SAVER2           RESTORE R2 FOR DMOD000 CALL                  
         GOTO1 =V(DMOD000),PARAM1                                               
         SAFE                                                                   
         OC    PARAM3(2),PARAM3    TEST FOR ERRORS                              
         BZ    DMODFEX                                                          
*                                                                               
D2FEERR  MVI   DMCBW3,X'40'        RETURN DISK ERROR                            
         B     DMODFEX                                                          
*                                                                               
D2FEEOF  MVI   DMCBW3,X'80'        RETURN EOF                                   
         B     DMODFEX                                                          
*                                                                               
DMODFEX  REAR  ARS=OFF                                                          
         XIT1                                                                   
         DROP  R3,R4,R5                                                         
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO INITIALISE TEMPEST BLOCK IN TABS DATASPACE AS REQUIRED   *         
* THIS WILL SUSPEND TIMERS AND ENQ SYSTEM FOR A SHORT PERIOD          *         
***********************************************************************         
SETTMSR  NTR1                      SET TEMPEST INFORMATION                      
         L     R3,=V(SSB)          REQUIRES SSB                                 
         USING SSBD,R3                                                          
         XR    R0,R0                                                            
         ICM   R0,1,SSBSYSID       MUST HAVE SYSTEM ID                          
         N     R0,=X'0000000F'                                                  
         JZ    *+2                 DIE=SYSID NOT 1-15                           
*                                                                               
         LAM   AR0,ARF,SARZERO                                                  
         LAM   AR2,AR2,SSBTBLET                                                 
         XR    R2,R2                                                            
         USING FATABSD,R2                                                       
         SAC   512                                                              
         ICM   R2,15,TABSTMS       GET A(TEMPEST BLOCK)                         
         USING TTMSHDRD,R2                                                      
         ICM   RE,15,TTMSNTRY      GET MAX DSPACE ALLOCATIONS ALLOWED           
         JZ    *+2                 DIE=MAX TABS DSPACE                          
*                                                                               
         SAC   0                   GET OUT OF AR MODE                           
         LAM   AR2,AR2,SARZERO                                                  
         DROP  R2                                                               
*                                                                               
         ST    RE,TMALLOC                                                       
         MHI   RE,L'TLUID          EACH ENTRY HOLDS LUID                        
         AHI   RE,TTSSHDRL                                                      
         SRDL  RE,32                                                            
         MR    RE,R0               INDEX INTO BLOCK BY FACPAK ID                
         AHI   RF,TTMSHDRL                                                      
         AR    RF,R2                                                            
         ST    RF,TMANTRY          SET A(HEADER)                                
*                                                                               
         LAM   AR2,AR2,SSBTBLET                                                 
         L     R2,TMANTRY                                                       
         SAC   512                                                              
         USING TTSSHDRD,R2                                                      
         ICM   RF,15,TTSFAC        ALREADY INITIALISED BLOCK TODAY?             
         BNZ   SETTMSRX            YES MUST BE A RESTART                        
         SAC   0                                                                
         LAM   AR2,AR2,SARZERO     CLEAR ALL ARS                                
         DROP  R2                                                               
*                                                                               
         OC    SSBTMSL,SSBTMSL     SET TEMPEST PAGE LENGTH OVERRIDE             
         BZ    *+10                                                             
         MVC   TMRLEN,SSBTMSL                                                   
*                                                                               
         LH    RF,TMRLEN                                                        
         ST    RF,PARAM3                                                        
*                                                                               
         L     R2,SAVER2           NEEDED IF YOU EVER CALL DADDS                
         GOTO1 =V(DADDS),PARAM1,V(DARPT)                                        
         MVC   TMRPTRK,PARAM3+2    SAVE RECORDS PER TRACK                       
*                                                                               
         LAM   AR2,AR2,SSBTBLET                                                 
         L     R2,TMANTRY                                                       
         SAC   512                                                              
         USING TTSSHDRD,R2                                                      
*                                                                               
         MVC   TTSCITRK,TMCITRK    NUMBER OF TRACKS PER ALLOCATION              
         MVC   TTSRLEN,TMRLEN      DEFAULT RECORD LENGTH                        
         MVC   TTSRPTRK,TMRPTRK    RECORDS PER TRACK                            
*                                                                               
         LH    RF,TMRPTRK                                                       
         MH    RF,TTSCITRK         (RECS PER TRK)*(TRKS PER ALLOCN)             
         STCM  RF,3,TTSMAX         IS MAXIMUM PAGE NUMBER                       
*                                                                               
         LR    R4,R2                                                            
         AHI   R4,TTSSHDRL         BIT TABLE FOLLOWS HEADER                     
         CPYA  AR4,AR2                                                          
         L     R5,TMALLOC                                                       
         MHI   R5,L'TLUID          R5=NUMBER OF BYTES REQUIRED                  
         XR    RF,RF                                                            
         MVCL  R4,RE               CLEAR BIT TABLE                              
         LAM   AR4,AR4,SARZERO                                                  
*                                                                               
         L     RF,=V(TEMPEST)      FIND NUMBER OF TRACKS                        
         BRAS  RE,GETTTRKS                                                      
         STCM  RF,3,TTSTRKS        SET NUMBER OF TRACKS                         
*                                                                               
         XR    RE,RE                                                            
         LH    R0,TTSCITRK         TRACKS PER ALLOCATION COUNTER                
         DR    RE,R0                                                            
*                                                                               
         C     RF,TMALLOC          FILE BIGGER THAN DATASPACE SUPPORT?          
         BL    *+8                                                              
         L     RF,TMALLOC                                                       
         STCM  RF,15,TTSMAXCI      SET MAX ALLOCATIONS                          
*                                                                               
         MVC   TTMXSESS,NUMTSONL   SET SESSIONS PER ALLOCATION                  
         XC    TTSHICI,TTSHICI     CLEAR COUNTERS                               
         XC    TTSUSECI,TTSUSECI                                                
         XC    TTSLINK,TTSLINK                                                  
         MVC   TTSFAC(L'SSBSYSN4),SSBSYSN4                                      
*                                                                               
SETTMSRX SAC   0                                                                
         LAM   AR0,ARF,SARZERO                                                  
         XIT1                                                                   
         DROP  R2,R3                                                            
         EJECT                                                                  
         DS    0D                                                               
SARZERO  DC    16F'0'                                                           
*                                                                               
         DC    C'**TEMPEST INFO**'                                              
TMANTRY  DC    A(0)                                                             
TMALLOC  DC    F'0'                TEMPEST - MAX DSPACE ALLOCATIONS             
TMRLEN   DC    H'6144'             TEMPEST - DEFAULT RECORD LEN                 
TMCITRK  DC    H'16'               TEMPEST - TRACK ALLOCATION PER C.I.          
TMRPTRK  DC    H'0'                TEMPEST - RECORDS PER TRACK                  
NUMTSONL DC    H'2'                TEMPSET - NUMBER OF ONLINE BUFFERS           
*                                                                               
DMGABAD  DC    V(WCTYPE)                                                        
         DC    A(DMTMBAD)                                                       
         DC    A(DMTMLEN)                                                       
*                                                                               
DMTMBAD  DC    C'+TEMPEST '                                                     
DMTAWHY  DC    C'          '                                                    
         DC    C' FAILURE '                                                     
DMTAGOT  DC    CL8'        '                                                    
         DC    C' WAS IN SLOT '                                                 
DMTNUM   DC    CL5'     '                                                       
         DC    C' HELD BY '                                                     
DMTAOWN  DC    CL8'        '                                                    
DMTMLEN  EQU   *-DMTMBAD                                                        
*                                                                               
TALLOC   DC    C'ALLOCATE  '                                                    
TDALLOC  DC    C'DEALLOCATE'                                                    
*                                                                               
         LTORG                                                                  
         DROP  RB                                                               
*&&                                                                             
         EJECT                                                                  
***********************************************************************         
* GET TOTAL TRACKS FOR FILE PASS RF=DTF, RETURN RF=TOTAL TRACKS       *         
***********************************************************************         
         USING DTFPHD,RF                                                        
GETTTRKS ST    R4,SVR4                                                          
         LA    R4,DMTX                                                          
         USING EXTENTD,R4                                                       
         TM    DIND,DINDXAM        HIGH CORE EXTENT MATRIX                      
         JZ    *+8                                                              
         ICM   R4,15,DMTX                                                       
         DROP  RF                                                               
*                                                                               
         SAM31                                                                  
         OC    EXT#TRKS,EXT#TRKS                                                
         JZ    *+2                 DIE=FILE NOT INIT                            
         SR    RF,RF               RF=TOTAL NUMBER OF TRACKS                    
         SR    R0,R0                                                            
GETTTRK1 CLI   0(R4),X'FF'         TEST LAST EXTENT                             
         JE    GETTTRKX                                                         
         ICM   R0,3,EXT#TRKS                                                    
         AR    RF,R0               BUMP TOTAL TRACKS                            
         LA    R4,EXTLNQ(R4)                                                    
         J     GETTTRK1                                                         
                                                                                
GETTTRKX L     R4,SVR4                                                          
         BSM   0,RE                                                             
         DROP  R4                                                               
         EJECT                                                                  
***********************************************************************         
* ROUTINES ARE CALLED FOR ALL CTFILE I/O.                             *         
* NOTE: MUST BE CALLED USING 'SAFE' MACRO                             *         
* ON ENTRY R4 POINTS TO RECORD OR KEY AS APPROPRIATE                  *         
* IF RECORD TYPE IS MAINTAINED IN BUFFER, READ/WRITE/ADD REC IN BUFFER*         
* IF NOT, SET DMCBW3=X'FF' TO FORCE FILE I/O                          *         
***********************************************************************         
*&&SRV                                                                          
CTCHKBUF NTR1  BASE=*,LABEL=*,WORK=(R7,CHKWORKL)                                
         USING CHKWORKD,R7                                                      
         USING LIBUFFD,CTBBUFF                                                  
HDR      USING DMSPACED,CTBHDR                                                  
         XC    CHKWORKD(CTBBUFF-CHKWORKD),CHKWORKD                              
         XC    CTBBUFF,CTBBUFF                                                  
         LR    RC,R1                                                            
         USING DMGRWORK,RC                                                      
         USING DMGRFRST,DMGRWORK                                                
*                                                                               
         TIMEUSED STORADR=CTBCPU,LINKAGE=SYSTEM,CPU=MIC                         
         LR    R1,RC                                                            
*                                                                               
         ICM   RF,15,=V(SSB)       REQUIRES SSB                                 
         BZ    CTBNO                                                            
         USING SSBD,RF                                                          
         OC    SSOCNTL,SSOCNTL     TEST OFFLINE                                 
         BNZ   CTB02                                                            
         CLI   SSOXTND,X'FF'       REQUIRES EXTENDED SSB                        
         BNE   CTBNO                                                            
*                                                                               
         ICM   RE,15,=V(AMSOON)    DDSIO SOON JOB FLAG                          
         BZ    CTBNO                                                            
*&&US*&& CLI   0(RE),C'Y'          SOON JOBS USE FILE                           
*&&US*&& BE    CTBNO                                                            
         TM    1(RE),X'01'         TEST TO USE CTFILE                           
         BO    CTBNO                                                            
         ICM   RE,15,SSOTBLET                                                   
         BZ    CTBNO                                                            
         ST    RE,CTBTBLET         SET TABS DATASPACE ALET                      
         B     CTB04                                                            
*                                                                               
CTB02    MVC   CTBTBLET,SSBTBLET   SET TABS DATASPACE ALET                      
         OI    CTBFLAG,CTBONL      SET ONLINE                                   
         SAM31                                                                  
         ICM   RE,15,SSBTKADR      IF IN TASK CHECK UTL ALLOWS UPDATES          
         BZ    CTB04                                                            
         ICM   RE,15,TCBUTL-TCBD(RE)                                            
         BZ    CTB04                                                            
         TM    TTEST-UTLD(RE),TTESTNOU                                          
         BZ    CTB04                                                            
         CLI   TSVCREQ+1-UTLD(RE),X'1E' IGNORE IF =PASS S/R                     
         BE    CTB04                                                            
         CLI   TSVCREQ+1-UTLD(RE),X'21' AND    IF =PC   S/R                     
         BE    CTB04                                                            
         DROP  RF                                                               
         CLI   ACTION,QDMWRT       WANT TO WRITE A RECORD?                      
         BE    CTBNO                                                            
         CLI   ACTION,QDMADD       WANT TO ADD A RECORD?                        
         BE    CTBNO                                                            
*                                                                               
CTB04    SAM24                                                                  
         BRAS  RE,CTBINIT          INITIALIZE CTBUFFER DATA                     
         TM    CTBFLAG,CTBACT                                                   
         BZ    CTBNO               EXIT IF CTBUFFER NOT ACTIVE                  
*                                                                               
         LR    R1,RC                                                            
         BRAS  RE,SETDUM           GET CTFILE DTF                               
         BASR  RE,RF                                                            
         XR    RF,RF                                                            
         ICM   RF,7,PARAM4+1                                                    
         USING ISDTF,RF                                                         
         ICM   RE,15,ISPDKEY                                                    
         BZ    CTBNO                                                            
         ST    RF,CTADTF                                                        
         DROP  RF                                                               
*                                                                               
         LAM   AR0,ARF,ARSCLR                                                   
         LAM   AR2,AR2,CTBTBLET                                                 
         ICM   R2,15,HDR.DSPTFRST                                               
         SAC   512                                                              
         USING CTBUFFD,R2                                                       
         LA    R2,CTBUFFP                                                       
         USING CTBUFFP,R2                                                       
CTB06    CLI   0(R2),X'FF'         SCAN LIST LOOKING FOR THIS TYPE              
         BE    CTB07                                                            
         CLC   CTBUFFPI(1),0(R4)   TEST A MATCH OF RECORD TYPE                  
         BE    CTB08                                                            
         AHI   R2,CTBUFFPL                                                      
         B     CTB06                                                            
*                                                                               
CTB07    TM    CTBFLAG,CTBONL      TEST ONLINE                                  
         BZ    CTB07B                                                           
CTB07A   LG    GRE,CTBUFFCT        BUMP ONLINE RECORD TYPE MISSES               
         LGR   GRF,GRE                                                          
         AGF   GRF,=F'1'                                                        
         CSG   GRE,GRF,CTBUFFCT                                                 
         BNE   CTB07A                                                           
*&&DO                                                                           
CTB07A1  CLI   0(R4),X'9B'         TEST ONLINE MISS ON X'9B' REC                
         BNE   CTB07A1X                                                         
         LG    GRE,CTBUFFPP+0                                                   
         LGR   GRF,GRE                                                          
         AGF   GRF,=F'1'                                                        
         CSG   GRE,GRF,CTBUFFPP+0                                               
         BNE   CTB07A1+8                                                        
CTB07A1X EQU   *                                                                
*&&                                                                             
         LLC   RE,0(R4)            INDEX INTO ONLINE MISS TABLE                 
         SLL   RE,2                                                             
         LA    RE,CTBMISS(RE)      RE=A(ENTRY FOR RECORD TYPE)                  
         L     R0,0(RE)                                                         
         AHI   R0,1                                                             
         ST    R0,0(RE)                                                         
         B     CTBNO                                                            
*&&DO                                                                           
         CLI   0(R4),X'9B'         TEST X'9B' USERID AGENCY PASSIVE             
         BNE   CTBNO                                                            
         ICM   RF,15,XUTLNTRY                                                   
         BZ    CTBNO                                                            
         USING UTLD,RF                                                          
         LA    RE,CTBMISS          USE FIRST SLOT TO RECORD UTL DATA            
         MVC   0(2,RE),TSYS                                                     
         MVC   2(1,RE),TOVSYS                                                   
         MVC   3(1,RE),TSVCREQ+1                                                
         B     CTBNO                                                            
         DROP  RF                                                               
*&&                                                                             
CTB07B   LG    GRE,CTBUFFOL        BUMP OFFLINE RECORD TYPE MISSES              
         LGR   GRF,GRE                                                          
         AGF   GRF,=F'1'                                                        
         CSG   GRE,GRF,CTBUFFOL                                                 
         BNE   CTB07B                                                           
         B     CTBNO                                                            
*                                                                               
CTB08    ST    R2,CTBRTA           SAVE A(RECORD TYPE TABLE ENTRY)              
         MVC   OLDKEY(28),0(R4)    SAVE INCOMING KEY (+LEN+STA)                 
         MVC   CTBBUFF,CTBUFFPP    SAVE PARAMETER LIST                          
         NI    LIFLAG1,255-(LIF1INS+LIF1MKY)                                    
         OI    LIFLAG1,LIF1ARS                                                  
         MVC   LIALET,CTBALET                                                   
         XC    LIKEY,LIKEY                                                      
*                                                                               
CTB10    TM    CTBFLAG,CTBONL      TEST ONLINE                                  
         BZ    CTB10B                                                           
CTB10A   LG    GRE,CTBUFFCT        BUMP ONLINE RECORD TYPE HITS                 
         LGR   GRF,GRE                                                          
         AGF   GRF,=F'1'                                                        
         CSG   GRE,GRF,CTBUFFCT                                                 
         BNE   CTB10A                                                           
         B     CTB11                                                            
CTB10B   LG    GRE,CTBUFFOL        BUMP OFFLINE RECORD TYPE HITS                
         LGR   GRF,GRE                                                          
         AGF   GRF,=F'1'                                                        
         CSG   GRE,GRF,CTBUFFOL                                                 
         BNE   CTB10B                                                           
*                                                                               
CTB11    ICM   R2,15,HDR.DSPTFRST                                               
         USING CTBUFFD,R2                                                       
CTB12    LG    GRE,CTBUFFIV        BUMP TOTAL RECORD TYPE HITS                  
         LGR   GRF,GRE                                                          
         AGF   GRF,=F'1'                                                        
         CSG   GRE,GRF,CTBUFFIV                                                 
         BNE   CTB12                                                            
         SAC   0                                                                
         LAM   AR0,ARF,ARSCLR                                                   
         B     CTB14                                                            
         DROP  R2                                                               
*                                                                               
CTBNO    SAC   0                                                                
         LAM   AR0,ARF,ARSCLR                                                   
         MVI   DMCBW3,X'FF'        SET REAL I/O FLAG FOR CALLER                 
         B     CTBXIT                                                           
         EJECT                                                                  
***********************************************************************         
* SINGLE KEY RECORD MAINTENANCE                                       *         
* NTRY:  R4=A(KEY TO READ)                                            *         
***********************************************************************         
CTB14    LLC   RF,0(R4)            INDEX INTO RECORD TYPE TABLE                 
         LA    RF,CTBRTT(RF)                                                    
         SR    RE,RE                                                            
         ICM   RE,1,0(RF)          INDEX INTO KEY LEN TABLE                     
         BZ    CTBNO                                                            
         SLL   RE,2                FOUR BYTE ENTRIES                            
         LA    RE,CTBKLT-4(RE)                                                  
         MVC   CTBDSPK(4),0(RE)    CTBDSPK AND CTBLENK                          
         B     CTSK02                                                           
*                                                                               
CTBRTT   DC    X'00010000000000000000000000000000'  00-0F  01                   
         DC    X'00000000000000000000000000000000'  10-1F                       
         DC    X'00000000000000000000000000000000'  22-2F                       
         DC    X'00000000000000000000000000000000'  30-3F                       
         DC    X'00000000000000000000000000000000'  40-4F                       
         DC    X'00000000000000000000000000000000'  50-5F                       
         DC    X'00000000000000000000000000000000'  60-6F                       
         DC    X'00000000000000000000000000000000'  70-7F                       
         DC    X'00000000000000000000000000000000'  80-8F                       
         DC    X'00000000000000000002000300000000'  90-9F  99/9B                
         DC    X'00000000000000000000000000000000'  A0-AF                       
         DC    X'00000000000000000000000000000000'  B0-BF                       
         DC    X'00000000000004000005000000000000'  C0-CF  F/I                  
         DC    X'00000000000000060000000000000000'  D0-DF  P                    
         DC    X'00000007080009000000000000000000'  E0-EF  T/U/W                
         DC    X'0A00000B000C00000000000000000000'  F0-FF  0/3/5                
*                                                                               
CTBKLT   DC    AL2(CT01AGID-CT01KEY,CT01LEN-CT01AGID)      ./01=01              
         DC    AL2(L'CTKID,L'CTKREST)                      ./99=02              
         DC    AL2(L'CTKID,L'CTKREST)                      ./9B=03              
         DC    AL2(L'CTKID,L'CTKREST)                      F/C6=04              
         DC    AL2(CTIKID-CTIREC,CTILEN-CTIKID)            I/C9=05              
         DC    AL2(CTPKSYS-CTPREC,CTPLEN-CTPKSYS)          P/D7=06              
         DC    AL2(CTTKTID-CTTREC-1,CTTLEN-CTTKTID+1)      T/E3=07              
         DC    AL2(CTUKSYS-CTUREC,CTULEN-CTUKSYS)          U/E4=08              
         DC    AL2(CTWKAGY-CTWREC,CTWLEN-CTWKAGY)          W/E6=09              
         DC    AL2(L'CTKID,L'CTKREST)                      0/F0=0A              
         DC    AL2(L'CTKID,L'CTKREST)                      3/F3=0B              
         DC    AL2(CT5KALPH-CT5REC,CT5LEN-CT5KALPH)        5/F5=0C              
*                                                                               
CTSK02   L     R6,DMCBW4           R6=A(OUTPUT BUFFER)                          
REC      USING CTDSECT,R6                                                       
KEY      USING CTDSECT,OLDKEY                                                   
*                                                                               
         CLI   ACTION,QDMRSEQ      TEST READ SEQUENTIAL                         
         BNE   CTSK04                                                           
*                                                                               
         L     RF,CTADTF                                                        
         USING ISDTF,RF                                                         
         OC    ISPDDA,ISPDDA       CHECK WE SAVED LAST KEY                      
         BNZ   CTSK04              NO - DONT KNOW WHAT TO DO HERE               
*                                                                               
         SAM31                                                                  
         ICM   RE,15,ISPDKEY                                                    
         MVC   KEY.CTKEY,0(RE)     GET LAST READ KEY                            
         SAM24                                                                  
         DROP  RF                                                               
*                                                                               
CTSK04   LH    RE,CTBDSPK          RE=DISPLACEMENT TO START OF KEY              
         AR    RE,R6                                                            
         ST    RE,LIAREC           SET A(RECORD)                                
*                                                                               
         MVI   LIACTN,LIAADD                                                    
         CLI   ACTION,QDMADD       TEST ADD                                     
         BE    CTSK06                                                           
         MVI   LIACTN,LIAWRT                                                    
         CLI   ACTION,QDMWRT       TEST WRITE                                   
         BE    CTSK06                                                           
                                                                                
* ON READ/READHI/READSEQ MUST USE REC AS KEYARG SO MOVE KEY                     
*                                                                               
         MVI   LIACTN,LIAHIGH                                                   
         MVC   REC.CTKEY,KEY.CTKEY                                              
         MVC   REC.CTLEN,KEY.CTLEN                                              
*                                                                               
CTSK06   XR    R0,R0                                                            
         ICM   R0,3,REC.CTLEN      SET LENGTH=RECLEN-DISP TO KEY                
         SH    R0,CTBDSPK                                                       
         STCM  R0,3,LIRECL                                                      
*                                                                               
         OI    LIFLAG2,LIF2SZE     SET WANT BUFFER SIZE RETURNED                
         GOTO1 AARREDIT,CTBPLST,CTBBUFF                                         
         TM    LIFLAG2,LIF2SZEU                                                 
         BZ    *+10                                                             
         MVC   CTBRCURL(8),LICURL  SAVE NEW CURRENT/MAXIMUM SIZES               
         NI    LIFLAG2,255-LIF2SZE-LIF2SZEU                                     
*                                                                               
         CLI   ACTION,QDMRSEQ      TEST READ SEQ                                
         BNE   CTSK08                                                           
         CLI   LIACTN,LIASEQ       HAVE WE GOT TO SEQ PART YET?                 
         BE    CTSK08                                                           
         MVI   LIACTN,LIASEQ       NO DO IT NOW                                 
         B     CTSK06                                                           
*                                                                               
CTSK08   CLI   LIRTN,LIRNF         TEST RECORD NOT FOUND                        
         BNE   CTSK10                                                           
         CLI   LIACTN,LIAHIGH      TEST READHI ACTION                           
         BE    CTSK10              YES IGNORE NOT FOUND                         
                                                                                
* IF GET NOT FOUND ON A WRITE, ADD RECORD TO BUFFER.                            
* IT MUST HAVE BEEN ADDED ON ANOTHER SYSTEM.                                    
*                                                                               
         CLI   ACTION,QDMWRT       TEST DMWRT ACTION                            
         BNE   CKBUFNF                                                          
         MVI   LIACTN,LIAADD       ADD RECORD IF NOT FOUND                      
         B     CTSK06                                                           
*                                                                               
CTSK10   CLI   LIRTN,LIREOF                                                     
         BE    CKBUFEOF                                                         
         CLI   ACTION,QDMREAD      TEST DMREAD ACTION                           
         BNE   CTSK12                                                           
                                                                                
* NEED TO COMPARE KEYARG TO REC FOR DMREAD                                      
*                                                                               
         LH    RE,CTBDSPK                                                       
         LA    RE,OLDKEY(RE)       RE=KEYARG                                    
         LH    R2,CTBDSPK                                                       
         LA    R2,REC.CTKEY(R2)    R2=RECORD READ                               
*                                                                               
         LH    RF,CTBLENK          RF=KEY LENGTH                                
         BCTR  RF,0                                                             
         EX    RF,*+8                                                           
         BNE   CKBUFNF                                                          
         CLC   0(0,R2),0(RE)                                                    
*                                                                               
CTSK12   TM    REC.CTSTAT,X'80'    TEST RECORD DELETED                          
         BZ    CHKBUFX                                                          
         CLI   ACTION,QDMWRT       TEST IF WANT TO WRITE A RECORD               
         BE    CHKBUFX                                                          
         CLI   ACTION,QDMADD       TEST IF WANT TO ADD A RECORD                 
         BE    CHKBUFX                                                          
         TM    DMCBW1,X'08'        TEST USER WANTS DELETES                      
         BO    CHKBUFX                                                          
                                                                                
* RECORD IS DELETED AND USER DOESN'T WANT DELETES                               
*                                                                               
         CLI   ACTION,QDMRSEQ      TEST RDSEQ ACTION                            
         BE    CTSK14                                                           
         CLI   ACTION,QDMRDHI      TEST RDHI ACTION                             
         BE    CTSK14                                                           
         B     CKBUFNF                                                          
*                                                                               
CTSK14   MVI   LIACTN,LIASEQ       SET TO GET NEXT RECORD                       
         B     CTSK06                                                           
         EJECT                                                                  
***********************************************************************         
* CTBBUFF EXIT POINTS                                                 *         
***********************************************************************         
REC      USING CTDSECT,R6                                                       
KEY      USING CTDSECT,OLDKEY                                                   
*                                                                               
CKBUFNF  MVI   DMCBW3,X'10'        SET RECORD NOT FOUND                         
         B     CHKBUFX                                                          
*                                                                               
CKBUFEOF CLI   ACTION,QDMRDHI      FOR RDHI/RDSEQ RETURN X'FF' REC              
         BE    CKBUFEO2                                                         
         CLI   ACTION,QDMRSEQ                                                   
         BE    CKBUFEO2                                                         
         B     CKBUFEO4                                                         
*                                                                               
CKBUFEO2 MVI   REC.CTKEY,X'FF'                                                  
         MVC   REC.CTKEY+1(L'CTKEY-1),REC.CTKEY                                 
         B     CHKBUFX                                                          
*                                                                               
CKBUFEO4 MVI   DMCBW3,X'80'        FOR READ/WRITE/ADD SET EOF                   
*                                                                               
CHKBUFX  L     RF,CTADTF           SAVE OFF LAST READ KEY                       
         USING ISDTF,RF                                                         
*                                                                               
         SAM31                                                                  
         XC    ISPDDA(8),ISPDDA    INVALIDATE BUFFERS                           
         ICM   RE,15,ISPDKEY       SAVE CURRENT READ KEY (FOR RSEQ)             
         MVC   0(L'CTKEY,RE),REC.CTKEY                                          
         SAM24                                                                  
*                                                                               
         CLI   ACTION,QDMRSEQ      TEST READ SEQUENTIAL                         
         BNE   CTBXIT                                                           
*                                                                               
         ICM   RF,15,XTCBNTRY      RESET TCBLIOC IF SEQUENTIAL READ             
         BZ    CTBXIT              ONLINE AND IN A TASK                         
         XC    TCBLIOC-TCBD(4,RF),TCBLIOC-TCBD(RF)                              
         B     CTBXIT                                                           
         DROP  RF                                                               
*                                                                               
CTBXIT   TM    CTBFLAG,CTBINI+CTBACT MUST BE INITILIZED AND ACTIVE              
         BNO   CTBXIT4                                                          
         OC    CTBTBLET,CTBTBLET   EXIT IF ALET NOT DEFINED                     
         BZ    CTBXIT4                                                          
*                                                                               
CTBXIT1  OC    CTBRMAXL,CTBRMAXL   TEST IF LATEST SPACE RETURNED                
         BZ    CTBXIT2                                                          
         LT    R2,CTBRTA           R2=A(RECORD TYPE TABLE ENTRY)                
         BZ    CTBXIT2                                                          
         LAM   AR2,AR2,CTBTBLET                                                 
         SAC   512                                                              
         USING CTBUFFP,R2                                                       
         MVC   CTBUFFCU(8),CTBRCURL                                             
*                                                                               
CTBXIT2  TIMEUSED STORADR=CTBDUB,LINKAGE=SYSTEM,CPU=MIC                         
         LR    R1,RC                                                            
HDR      USING DMSPACED,CTBHDR                                                  
         LAM   AR2,AR2,CTBTBLET                                                 
         ICM   R2,15,HDR.DSPTFRST                                               
         SAC   512                                                              
         USING CTBUFFD,R2                                                       
CTBXIT3  LG    GRF,CTBDUB                                                       
         SG    GRF,CTBCPU          GRF=CPU USED BY CTBUFFER CODE                
         LG    GRE,CTBUFFCP                                                     
         AGR   GRF,GRE                                                          
         CSG   GRE,GRF,CTBUFFCP                                                 
         BNE   CTBXIT3                                                          
*                                                                               
CTBXIT4  SAC   0                                                                
         LAM   AR0,ARF,ARSCLR                                                   
         DROP  HDR                                                              
*                                                                               
CTBXITX  XIT1                                                                   
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO INITIALISE CTBUFFER INFORMATION                          *         
***********************************************************************         
CTBINIT  NTR1                                                                   
         OI    CTBFLAG,CTBINI      SET INIT ROUTINE CALLED                      
         SAC   0                                                                
         LAM   AR0,ARF,ARSCLR                                                   
*                                                                               
CTBI02   XC    CTBDUB,CTBDUB       BUILD PARMS TO LOCKSPC                       
         MVC   CTBDUB(4),=AL4(DTCTB)                                            
         LA    RF,CTBHDR                                                        
         ST    RF,CTBDUB+4                                                      
         MVI   CTBDUB,X'20'        SET ENQUIRE                                  
         GOTO1 ALOCKSPC,CTBDUB     GET TABLE                                    
*                                                                               
HDR      USING DMSPACED,CTBHDR                                                  
         NC    HDR.DSPTFRST,=XL4'0FFFFFFF' GET RID OF POST BIT                  
         OC    HDR.DSPLOCK,HDR.DSPLOCK                                          
         BZ    CTBI06              TABLE IS NOT LOCKED                          
*                                                                               
         CLI   ACTION,QDMADD       WAIT IF ACTION ADD                           
         BE    CTBI04                                                           
         CLI   ACTION,QDMWRT       WAIT IF ACTION WRITE                         
         BNE   CTBI06                                                           
*                                                                               
CTBI04   XC    CTBDUB,CTBDUB       SUSPEND PROCESSING THIS REQUEST              
         MVC   CTBDUB(4),=AL4(DTCTB)                                            
         LA    RF,CTBHDR                                                        
         ST    RF,CTBDUB+4                                                      
         GOTO1 ALOCKSPC,CTBDUB     LOCKSPC RETURNS WHEN TABLE FREED             
*                                                                               
         XC    CTBDUB,CTBDUB       WE OWN TABLE NOW                             
         MVC   CTBDUB(4),=AL4(DTCTB)                                            
         LA    RF,CTBHDR                                                        
         ST    RF,CTBDUB+4                                                      
         MVI   CTBDUB,X'10'        FREE IT SO NOONE ELSE GETS HUNG UP           
         GOTO1 ALOCKSPC,CTBDUB     THEN CONTINUE                                
         B     CTBI02                                                           
*                                                                               
CTBI06   SAC   0                                                                
         LAM   AR0,ARF,ARSCLR                                                   
*                                                                               
CTBI08   LAM   AR2,AR2,CTBTBLET    MAKE SURE YOU CAN DO BUFFER I/O              
         ICM   R2,15,HDR.DSPTFRST                                               
         SAC   512                                                              
         USING CTBUFFD,R2                                                       
         CLI   CTBUFFID,0          TEST CTBUFFER ACTIVE                         
         BE    CTBINITX                                                         
*                                                                               
         OI    CTBFLAG,CTBACT      SET CTBUFFER ACTIVE                          
         MVC   CTBDUB,CTBUFFSV     SAVE STOKEN                                  
         SAC   0                                                                
         LAM   AR0,ARF,ARSCLR                                                   
         DROP  R2                                                               
*                                                                               
         CLC   CTBSTOKN,CTBDUB     TEST IF STOKEN HAS CHANGED                   
         BE    *+10                                                             
         XC    CTBALET,CTBALET                                                  
         MVC   CTBSTOKN,CTBDUB                                                  
         OC    CTBALET,CTBALET     TEST IF HAVE THE CTBUFFER ALET               
         BNZ   CTBINITX                                                         
*                                                                               
         XC    CTBWORK,CTBWORK                                                  
         MVC   CTBWORK+00(04),=C'PALE'                                          
         MVC   CTBWORK+04(12),=C'TESTDATAMGRT'                                  
         MVC   CTBWORK+28(08),CTBDUB                                            
         LHI   R0,-21                                                           
         LA    R1,CTBWORK                                                       
         SVC   247                                                              
         LTR   RF,RF               TEST GOOD RETURN                             
         BZ    *+12                                                             
         NI    CTBFLAG,255-CTBACT  SET BUFFER NOT ACTIVE                        
         B     CTBINITX                                                         
         L     RF,CTBWORK+24       SAVE ALET FOR CTBUFFER LOCALLY               
         ST    RF,CTBALET                                                       
*                                                                               
CTBINITX SAC   0                                                                
         LAM   AR0,ARF,ARSCLR                                                   
         XIT1                                                                   
         EJECT                                                                  
***********************************************************************         
* LITERALS AND CONSTANTS                                              *         
***********************************************************************         
         LTORG                                                                  
ALOCKSPC DC    V(LOCKSPC)                                                       
AHELLO   DC    V(HELLO)                                                         
AARREDIT DC    V(ARREDIT)                                                       
ARSCLR   DC    16F'0'                                                           
*                                                                               
CTBALET  DC    A(0)                                                             
CTBSTOKN DC    XL8'00'                                                          
         DROP  KEY,REC,R7,RC                                                    
*                                                                               
         DS    0D                                                               
         DC    C'*CTMISS*'                                                      
CTBMISS  DC    256F'0'                                                          
*                                                                               
CHKWORKD DSECT                                                                  
CTBDUB   DS    D                                                                
CTBDSPK  DS    H                   DISPLACEMENT TO KEY FIELD                    
CTBLENK  DS    H                   LENGTH OF KEY FIELDS                         
CTBTBLET DS    A                                                                
CTADTF   DS    A                                                                
CTBPLST  DS    6F                                                               
CTBHDR   DS    XL(L'DSPHDR)                                                     
IDKLIST  DS    XL11                                                             
*                                                                               
CTBFLAG  DS    X                                                                
CTBINI   EQU   X'80'               CTBUFF INIT ROUTINE CALLED                   
CTBACT   EQU   X'40'               CTBUFF ACTIVE                                
CTBONL   EQU   X'20'               ONLINE                                       
*                                                                               
         DS    F                   N/D                                          
CTBRTA   DS    A                   A(RECORD TABLE ENTRY)                        
CTBRCURL DS    F                   CURRENT SPACE USED FOR RECORD                
CTBRMAXL DS    F                   MAXIMUM SPACE USED FOR RECORD                
CTBCPU   DS    D                                                                
CTBWORK  DS    CL64                                                             
CTBBUFF  DS    XL(LIBUFFL)                                                      
CHKWORKL EQU   *-CHKWORKD                                                       
*                                                                               
CTDSECT  DSECT                                                                  
CTKEY    DS    0XL25                                                            
CTKID    DS    X                                                                
CTKREST  DS    XL24                                                             
CTLEN    DS    XL2                                                              
CTSTAT   DS    XL1                                                              
CTREC    EQU   *                                                                
*                                                                               
DATAMGR  CSECT                                                                  
*&&                                                                             
         EJECT                                                                  
***********************************************************************         
* DMOD000 - INTERFACE TO DATAMGR                                      *         
***********************************************************************         
DMOD000  DS    0D                                                               
         ENTRY UPDID               UPDATE ID VALUE                              
         ENTRY DMEXT               EXIT WITH A(DTF)                             
         ENTRY DMUPDEXT            EXIT WITH A(DTF) AND UPDATE CHECK            
         ENTRY WCTYPE              WRITE TO CONSOLE                             
         ENTRY RCTYPE              READ CONSOLE                                 
         ENTRY RKEY                IS READ (HIGH)                               
         ENTRY RKEYFLS             IS READ (HIGH) FLUSH                         
         ENTRY RSEQ                IS READ SEQ                                  
         ENTRY RSEQFLS             IS READ SEQ FLUSH                            
         ENTRY WKEY                IS WRITE                                     
         ENTRY AKEY                IS ADD                                       
         ENTRY EKEY                IS ERASE                                     
         ENTRY READ                DA READ                                      
         ENTRY READFLS             DA READ FLUSH                                
         ENTRY READTRK             DA READ TRACK                                
         ENTRY READTRB             DA READ TRACK FOR BLOCKED FILES              
         ENTRY WRITE               DA WRITE                                     
         ENTRY WRTAFT              DA ADD                                       
         ENTRY WRTADR              DA ADD AFTER ADR                             
         ENTRY WRTGET              DA GET DNEXT                                 
         ENTRY LOCKDA              DA LOCK RECORD                               
         ENTRY READDA              DA READ RECORD AFTER LOCK                    
*                                                                               
         ENTRY OPEN                DA OPEN                                      
         ENTRY OPENIS              IS OPEN                                      
         ENTRY CLOSE               DA CLOSE                                     
         ENTRY CLOSEIS             IS CLOSE                                     
         ENTRY OPENSYS             OPEN ALL FILES FOR A SYSTEM                  
         ENTRY CLSESYS             CLSE ALL FILES FOR A SYSTEM                  
         ENTRY FINDSYS             GET ADDRESS OF SYSTEM FILES                  
                                                                                
* R2 POINTS TO DMCB                                                             
* R1 POINTS TO PARAM LIST                                                       
*                                                                               
* P1 A(ACTION)                                                                  
* P2 A(RECORD)                                                                  
* P3 ERRORS/LENGTH                                                              
* P4 FILE NUMBER (FIRST BYTE)                                                   
         EJECT                                                                  
DMCNTL   DS    0H                                                               
         NMOD1 DMWSTRX-DMWSTR,DMCNTL,R9                                         
         USING DMWSTR,RC           RC=A(LOCAL WORKING STORAGE)                  
         SAM24                                                                  
*                                                                               
         XC    DATCB,DATCB         LOCAL TCB ADDRESS                            
         ICM   RF,15,=V(SSB)                                                    
         BZ    *+10                                                             
         MVC   DATCB,SSBTKADR-SSBD(RF)                                          
*                                                                               
         USING DMCBD,R2            R2=A(CALLER'S DMCB)                          
         LR    RA,R1                                                            
         USING DMGRFRST,RA         RA=A(DMDMGR'S WORKING STORAGE)               
*                                                                               
         OC    PARAM1+1(2),PARAM1+1 TEST PASSED ROUTINE NUMBER                  
         BNZ   DMOD0                                                            
         NC    PARAM1,=X'0000001F' MAX 32 ROUTINES                              
         L     R7,PARAM1                                                        
         SLL   R7,2                                                             
         LA    R7,DMOD0L(R7)                                                    
         MVC   PARAM1,0(R7)                                                     
         B     DMOD0                                                            
*                                                                               
DMOD0L   DC    AL4(0)              0 LEAVE THIS 0 TO CATCH MISSING              
         DC    AL4(DMEXT)          1 GET A(DTF)                                 
         DC    AL4(FINDSYS)        2 GET A(SYSFLES) FOR THIS SYS                
         DC    AL4(RKEY)           3 IS READ (HIGH)                             
         DC    AL4(RSEQ)           4 IS READ SEQUENTIAL                         
         DC    AL4(WKEY)           5 IS WRITE                                   
         DC    AL4(AKEY)           6 IS ADD                                     
         DC    AL4(EKEY)           7 IS ERASE                                   
         DC    AL4(READ)           8 DA READ                                    
         DC    AL4(READTRK)        9 DA READ TRACK                              
         DC    AL4(READTRB)        A DA READ TRACK FOR BLOCKED FILES            
         DC    AL4(WRITE)          B DA WRITE                                   
         DC    AL4(WRTAFT)         C DA ADD                                     
         DC    AL4(DMGRFLS)        D GET A(DMGRFLES)                            
         DC    AL4(OPENSYS)        E OPEN ALL FILES FOR A SYSTEM                
         DC    AL4(CLSESYS)        F CLSE ALL FILES FOR A SYSTEM                
         DC    AL4(DMUPDEXT)       10CALL UPDATE ROUTINE                        
         DC    AL4(LOCKDA)         11LOCK A DA RECORD                           
         DC    AL4(READDA)         12READ A DA RECORD AFTER LOCKING             
*                                                                               
DMOD0    XC    SENUM(4),SENUM      RESET SE NUMBER AND UTL ADDR                 
         SR    R7,R7                                                            
         EJECT                                                                  
***********************************************************************         
* SET A(UTL) ENTRY OR ZERO FOR OFF LINE                               *         
***********************************************************************         
         ICM   R6,15,=V(SSB)                                                    
         BZ    DMOD1               IF V(SSB) UNRESOLVED OFFLINE                 
         OC    0(2,R6),0(R6)                                                    
         BZ    DMOD1               IF NO CONTROL TRM OFFLINE                    
         ICM   R7,15,SSBTKADR-SSBD(R6)                                          
         BZ    *+8                                                              
         L     R7,TCBUTL-TCBD(R7)                                               
         USING UTLD,R7                                                          
         ST    R7,AUTL             SET UTL ADDRESS                              
         MVI   SENUM,0                                                          
                                                                                
***********************************************************************         
* CHECK LIST OF COMMANDS THAT DONT REQUIRE FILE TRANSLATION           *         
***********************************************************************         
DMOD1    DS    0H                                                               
         LA    R3,CMDLIST                                                       
DMOD2    CLC   1(3,R3),PARAM1+1    TEST ADDRESSES MATCH                         
         BNE   *+12                                                             
         L     R1,PARAM4                                                        
         B     DMOD22                                                           
         CLI   0(R3),0                                                          
         BNZ   DMOD4                                                            
         LA    R3,4(R3)                                                         
         B     DMOD2                                                            
*                                                                               
DMOD4    LTR   R7,R7               TEST ONLINE                                  
         BZ    DMOD6                                                            
         MVC   SENUM,TSYS                                                       
         OC    TSVCREQ,TSVCREQ     S/R USE SE1 FILES                            
         BZ    DMOD4A                                                           
         TM    TFLAG,TFLAGSSW      TEST S/R SWITCHED                            
         BNZ   *+8                 YES LEAVE IN SWITCHED SYSTEM                 
         MVI   SENUM,1             NO FORCE S/R TO SERVICE SYSTEM               
                                                                                
DMOD4A   OC    PARAM4+1(3),PARAM4+1 TEST IF A(DTF) IS SET                       
         BZ    DMOD10                                                           
         LLC   RF,PARAM4           YES TEST IF A SERVICE SYSTEM FILE            
         SLL   RF,5                INDEX INTO 32-BYTE FILE TABLE                
         A     RF,=V(FILTABL)                                                   
         TM    DMFLSTYP-FILTABD(RF),DMFLSRV                                     
         BZ    DMOD20                                                           
         MVI   SENUM,1             SET SERVICE SYSTEM IF SERVICE FILE           
         B     DMOD20                                                           
                                                                                
***********************************************************************         
* IF OFFLINE WITH NO FILE ADDRESS                                     *         
* V(UTL) MUST BE RESOLVED WITH SE AT UTL+4                            *         
***********************************************************************         
DMOD6    OC    PARAM4+1(3),PARAM4+1                                             
         BNZ   DMOD7               OK IF FILE ADDRESS ALREADY SET               
         ICM   R7,15,=V(UTL)                                                    
         JZ    *+2                 DIE=NO UTL                                   
         MVC   SENUM,TSYS-UTLD(R7)                                              
         B     DMOD10                                                           
*                                                                               
DMOD7    ICM   R7,15,=V(UTL)       SET SENUM FROM UTL IF AVAILABLE              
         BZ    *+10                                                             
         MVC   SENUM,TSYS-UTLD(R7)                                              
         B     DMOD20                                                           
                                                                                
***********************************************************************         
* USE FILTABL AND SYSTABL TO FIND A(DTF) FROM FILE NUMBER AND SENUM   *         
***********************************************************************         
DMOD10   LLC   RF,PARAM4           RF=FILE NUMBER                               
         SLL   RF,5                INDEX INTO 32-BYTE FILE TABLE                
         A     RF,=V(FILTABL)                                                   
         TM    DMFLSTYP-FILTABD(RF),DMFLSRV                                     
         BZ    *+8                                                              
         MVI   SENUM,1             SET SERVICE SYSTEM IF SERVICE FILE           
*                                                                               
         LLC   R5,SENUM            INDEX INTO 16-BYTE SYSTEM TABLE              
         SLL   R5,4                                                             
         A     R5,=V(SYSTABL)                                                   
         SR    RE,RE                                                            
         ICM   RE,7,DMSYAFNO-SYSTABD(R5) RE=A(FILE NUM INDEX TABLE)             
         L     R5,DMSYASYF-SYSTABD(R5)   R5=A(SYSFLES LIST ENTRY)               
*                                                                               
         LLC   RF,PARAM4           RF=FILE NUMBER                               
         AR    RE,RF                                                            
         IC    RF,0(RE)            RF=ENTRY NUMBER IN SYSFLES LIST              
         AHI   RF,-1                                                            
         SLL   RF,3                                                             
         LA    RE,4(RF,R5)         INDEX INTO 8-BYTE SYSFLES FILE ENTRY         
         USING SYSFLSTD,RE                                                      
         CLC   PARAM4(1),SYSFILE#  CHECK FILE NUMBERS MATCH                     
         JNE   *+2                 DIE=NO MATCH FIL NUM                         
         MVC   PARAM4+1(3),SYSFADTF EXTRACT AND SET A(DTF)                      
         DROP  RE                                                               
                                                                                
DMOD20   L     R1,PARAM4           GET DTF ADDRESS                              
         USING DTFPHD,R1                                                        
         LA    R1,0(R1)                                                         
         TM    DTFOPEN,DTF_NOP     EXIT IF FILE IS NOP                          
         BO    DMEXT                                                            
         DROP  R1                                                               
                                                                                
DMOD22   L     RF,PARAM1           GET ACTION ADDRESS                           
         BASR  RE,RF                                                            
         B     DMEXT                                                            
                                                                                
DMUPDEXT BAS   RE,UPDATE                                                        
DMEXT    XIT1                                                                   
         DROP  R7                                                               
         EJECT                                                                  
***********************************************************************         
* LIST OF COMMANDS THAT DONT REQUIRE FILE NUMBER TRANSLATION          *         
***********************************************************************         
CMDLIST  DC    A(WCTYPE)                                                        
         DC    A(RCTYPE)                                                        
         DC    A(OPENSYS)                                                       
         DC    A(CLSESYS)                                                       
         DC    A(FINDSYS)                                                       
         DC    A(DMGRFLS)                                                       
         ORG   *-4                                                              
         DC    X'80'               LAST ENTRY FLAG                              
         ORG                                                                    
         EJECT                                                                  
***********************************************************************         
* DMOD000 - CONSOLE MESSAGE COMMANDS                                  *         
***********************************************************************         
RCTYPE   MVI   P1,0                SET READ FLAG                                
         BAS   RE,RWC                                                           
         B     DMEXT                                                            
WCTYPE   MVI   P1,1                SET WRITE FLAG                               
         BAS   RE,RWC                                                           
         B     DMEXT                                                            
                                                                                
RWC      NTR1  WORK=(R5,16)        R5=A(AREA TO BUILD SVC PARAM LIST)           
         XC    0(125,R5),0(R5)                                                  
         L     R6,PARAM2           R6=A(MESSAGE)                                
         LH    R7,PARAM3+2         R7=L'MESSAGE                                 
         LTR   R7,R7                                                            
         BP    *+8                                                              
         LA    R7,1                MIN LEN IS 1                                 
         CHI   R7,105                                                           
         BNH   *+8                                                              
         LHI   R7,105              MAX LEN IS 105                               
*                                                                               
RCW0     CLI   P1,0                READ FROM CONSOLE                            
         BNE   RCW1                                                             
         LA    R4,P2               R4=A(JOB NAME IN TIOT)                       
         EXTRACT (4),FIELDS=TIOT                                                
         L     R4,P2                                                            
         STC   R7,0(R5)            SET REPLY LEN                                
         STCM  R6,7,1(R5)          SET REPLY ADR                                
         LA    R1,P2                                                            
         ST    R1,4(R5)            SET ECB ADR                                  
         XC    0(4,R1),0(R1)       CLEAR ECB                                    
         LA    R1,34                                                            
         STH   R1,8(R5)            SET MSG LEN + 4                              
         MVC   12(08,R5),0(R4)     SET JOB NAME                                 
         MVC   22(20,R5),=C'** AWAITING REPLY **'                               
         LR    R1,R5                                                            
         SVC   35                  ISSUE WTOR SVC                               
         L     R1,4(R5)                                                         
         WAIT  ECB=(1)                                                          
         B     RCWX                                                             
*                                                                               
RCW1     CLI   P1,1                WRITE TO CONSOLE                             
         BNE   RCWX                                                             
*                                                                               
         TM    PARAM3,X'80'        OPMSG CALL                                   
         BNO   RCW50               NO SKIP CHECKING                             
*                                                                               
         ICM   RF,15,=V(SSB)       DO WTO IF OFFLINE                            
         BZ    RCW50                                                            
         USING SSBD,RF                                                          
         OC    0(2,RF),0(RF)                                                    
         BZ    RCW50                                                            
*                                                                               
         ICM   RF,15,SSBTKADR      TEST IF TASK SET                             
         BZ    RCW50               NO MUST BE ONE OF FA??? PGMS                 
         USING TCBD,RF                                                          
         ICM   RF,15,TCBUTL                                                     
         BZ    RCW50                                                            
         USING UTLD,RF                                                          
*                                                                               
         OC    TSVCREQ,TSVCREQ     TEST SERVICE SYSTEM                          
         BZ    RCW05                                                            
         MVI   SYSPGM,X'01'        SET SERVICE SYSTEM                           
         MVC   SYSPGM+1(1),TSVCREQ+1                                            
         B     RCW10                                                            
*                                                                               
RCW05    MVC   SYSPGM(1),TOVSYS                                                 
         MVC   SYSPGM+1(1),TPRG                                                 
         DROP  RF                                                               
*                                                                               
RCW10    LA    RE,MSGTABQ          SEARCH MESSAGE TABLE                         
         LA    RF,MSGTAB                                                        
RCW20    OC    0(L'MSGTAB,RF),0(RF) TEST EMPTY ENTRY                            
         BZ    RCW30                                                            
         CLC   SYSPGM,0(RF)        MATCH ON SYS/PRG                             
         BE    RCW40                                                            
         AHI   RF,L'MSGTAB                                                      
         BCT   RE,RCW20                                                         
         DC    H'0'                DIE=MAX TABLE SIZE                           
*                                                                               
RCW30    MVC   0(2,RF),SYSPGM      ADD THIS ENTRY AND DO WTO                    
         MVI   2(RF),X'01'                                                      
         B     RCW50                                                            
*                                                                               
RCW40    CLI   2(RF),MSGLMT        TEST EXCEEDED THE LIMIT                      
         BL    *+12                                                             
         MVI   PARAM2,X'40'        MARK ERROR AND DON'T WTO                     
         B     RCWX                                                             
         LLC   RE,2(RF)            ADD 1 TO COUNTER                             
         AHI   RE,1                                                             
         STC   RE,2(RF)                                                         
*                                                                               
RCW50    BCTR  R7,0                                                             
         EX    R7,*+8                                                           
         B     *+10                                                             
         MVC   4(0,R5),0(R6)       MOVE MSG TO SVC PARAM LIST                   
         LA    R7,5(R7)                                                         
         STH   R7,0(R5)            SET LEN OF MSG + 4                           
         LR    R1,R5                                                            
         SVC   35                  ISSUE WTO SVC                                
         B     RCWX                                                             
*                                                                               
RCWX     XIT1                                                                   
*                                                                               
MSGLMT   EQU   32                  MAX NUM OF MESSAGES PER SYS/PGM              
MSGTABQ  EQU   20                                                               
MSGTAB   DS    (MSGTABQ)XL3                                                     
SYSPGM   DS    XL2                 SYS/PGM                                      
         DS    0H                                                               
         EJECT                                                                  
***********************************************************************         
* DMOD000 - INDEX SEQUENTIAL COMMANDS                                 *         
***********************************************************************         
OPENIS   MVC   P1,=A(ISOPEN)                                                    
         MVC   P2(20),PARAM2                                                    
         GOTO1 =V(ISDDS),P1                                                     
         L     R1,P4                                                            
         USING ISDTF,R1                                                         
         TM    ISFDEVF,X'0C'       TEST MIXED XTNTS/NOT CYL BDY                 
         BZ    *+8                                                              
         OI    DM3,X'40'           SET DISK ERROR                               
         B     DMEXT                                                            
                                                                                
CLOSEIS  MVC   P1,=A(ISCLOSE)                                                   
         MVC   P2(20),PARAM2                                                    
         GOTO1 =V(ISDDS),P1                                                     
         B     DMEXT                                                            
                                                                                
* ROUTINE TO READ INDEX SEQUENTIAL KEY                                          
*                                                                               
RKEY     BRAS  RE,DMISLKC          CHECK IF LOCK REQUIRED FOR THIS KEY          
         TM    K3,X'04'                                                         
         BO    RKEY1                                                            
         MVC   P1,=A(ISREAD)       READ KEY HIGH                                
         BRAS  RE,DMISGO                                                        
         BRAS  RE,DMERRIS                                                       
         B     DMEXT                                                            
RKEY1    BRAS  RE,DMISLKEB         ENQUEUE AND BUILD LOCKER PLIST               
         MVC   P1,=A(ISREAD)                                                    
         OI    P1,X'80'            ISDDS WILL CALL LOCKER FOR ISPDDA            
         BRAS  RE,DMISGO                                                        
         BRAS  RE,DMERRIS                                                       
         B     DMEXT                                                            
*                                                                               
* ROUTINE TO READ INDEX SEQUENTIAL KEY AND FLUSH BUFFERS                        
*                                                                               
RKEYFLS  BRAS  RE,DMISLKC          CHECK IF LOCK REQUIRED FOR THIS KEY          
         TM    K3,X'04'                                                         
         BO    RKEYFLS1                                                         
         MVC   P1,=A(ISRDFLS)      READ KEY HIGH WITH FLUSH                     
         BRAS  RE,DMISGO                                                        
         BRAS  RE,DMERRIS                                                       
         B     DMEXT                                                            
RKEYFLS1 BRAS  RE,DMISLKEB         ENQUEUE AND BUILD LOCKER PLIST               
         MVC   P1,=A(ISRDFLS)                                                   
         OI    P1,X'80'            ISDDS WILL CALL LOCKER FOR ISPDDA            
         BRAS  RE,DMISGO                                                        
         BRAS  RE,DMERRIS                                                       
         B     DMEXT                                                            
*                                                                               
* ROUTINE TO READ SEQUENTIAL I/S KEY                                            
*                                                                               
RSEQ     BRAS  RE,DMISLKC          CHECK IF LOCK REQUIRED FOR THIS KEY          
         TM    K3,X'04'                                                         
         BO    RSEQ1                                                            
         MVC   P1,=A(ISRDSEQ)      READ KEY SEQUENTIALLY                        
         BRAS  RE,DMISGO                                                        
         BRAS  RE,DMERRIS                                                       
         B     DMEXT                                                            
RSEQ1    BRAS  RE,DMISLKEB         ENQUEUE AND BUILD LOCKER PLIST               
         MVC   P1,=A(ISRDSEQ)                                                   
         OI    P1,X'80'            ISDDS WILL CALL LOCKER FOR ISPDDA            
         BRAS  RE,DMISGO           DO SEQUENTIAL READ                           
         BRAS  RE,DMERRIS                                                       
         B     DMEXT                                                            
*                                                                               
* ROUTINE TO READ SEQUENTIAL I/S KEY WITH BUFFER FLUSH                          
*                                                                               
RSEQFLS  BRAS  RE,DMISLKC          CHECK IF LOCK REQUIRED FOR THIS KEY          
         TM    K3,X'04'                                                         
         BO    RSEQFLS1                                                         
         MVC   P1,=A(ISRSFLS)      READ KEY SEQUENTIALLY                        
         BRAS  RE,DMISGO                                                        
         BRAS  RE,DMERRIS                                                       
         B     DMEXT                                                            
RSEQFLS1 BRAS  RE,DMISLKEB         ENQUEUE AND BUILD LOCKER PLIST               
         MVC   P1,=A(ISRSFLS)                                                   
         OI    P1,X'80'            ISDDS WILL CALL LOCKER FOR ISPDDA            
         BRAS  RE,DMISGO           DO SEQUENTIAL READ                           
         BRAS  RE,DMERRIS                                                       
         B     DMEXT                                                            
*                                                                               
* ROUTINE TO WRITE RECORD WITH THIS KEY                                         
*                                                                               
WKEY     BAS   RE,UPDATE           WRITE RECORD WITH THIS KEY                   
         MVC   P1,=A(ISWRITE)                                                   
         BAS   RE,DMISGO                                                        
         BAS   RE,DMERRIS                                                       
         B     DMEXT                                                            
                                                                                
AKEY     BAS   RE,UPDATE           ADD RECORD WITH NEW KEY                      
         MVC   P1,=A(ISADD)                                                     
         BAS   RE,DMISGO                                                        
         BAS   RE,DMERRIS                                                       
         B     DMEXT                                                            
                                                                                
EKEY     BAS   RE,UPDATE           ERASE RECORD WITH THIS KEY                   
         MVC   P1,=A(ISERASE)                                                   
         BAS   RE,DMISGO                                                        
         BAS   RE,DMERRIS                                                       
         B     DMEXT                                                            
         EJECT                                                                  
DMERRIS  MVI   DM3,0               RESET DMCB ERROR BYTE                        
         TM    P3,X'80'            TEST UNRECOVERABLE ERROR                     
         BZ    *+12                                                             
         MVI   DM3,X'40'                                                        
         B     DMEXT                                                            
         TM    P3,X'20'            DUP KEY                                      
         BZ    *+12                                                             
         MVI   DM3,X'20'                                                        
         B     DMEXT                                                            
         TM    P3+1,X'08'          REC NOT FOUND                                
         BZ    *+12                                                             
         MVI   DM3,X'10'                                                        
         B     DMEXT                                                            
         TM    P3+1,X'04'          EOF                                          
         BZ    *+12                                                             
         MVI   DM3,X'80'                                                        
         B     DMEXT                                                            
         BR    RE                                                               
         EJECT                                                                  
DMISGO   ST    RE,SAVRE            PASS CONTROL TO ISDDS                        
         MVC   P2(20),PARAM2                                                    
         L     R1,PARAM4                                                        
         LA    R1,0(R1)                                                         
         MVC   LASTBLK,ISPDDA      SAVE DISK ADDRESS OF CURRENT BLOCK           
         TM    36(R1),X'04'        TEST FILE QUIESCED                           
         BZ    DMISGO1                                                          
         TM    36(R1),X'20'        TEST FILE OPEN                               
         BZ    INHIBITR                                                         
DMISGO1  L     RF,=V(ISDDS)        16-BIT I/S FILE                              
         TM    ISFTYPE,ISFTBIGF                                                 
         BZ    *+8                                                              
         L     RF,=V(ISDDS20)      20-BIT I/S FILE                              
         GOTO1 (RF),P1                                                          
         L     RE,SAVRE                                                         
         BR    RE                                                               
         EJECT                                                                  
DMISLKB  MVI   K6,X'01'            SET BUILD LOCK PLIST                         
         B     DMISLK1                                                          
DMISLKD  MVI   K6,X'02'            SET DO LOCK                                  
         B     DMISLK1                                                          
DMISLKU  MVI   K6,X'04'            SET CHECK UNCONVERTED PROGRAM                
         B     DMISLK1                                                          
DMISLKC  MVI   K6,X'08'            SET CHECK LOCK REQUIRED                      
         B     DMISLK1                                                          
DMISLKE  MVI   K6,X'10'            SET ENQUEUE                                  
         B     DMISLK1                                                          
DMISLKEB MVI   K6,X'11'            SET ENQUEUE AND BUILD PLIST                  
         B     DMISLK1                                                          
DMISLK   MVI   K6,X'12'            SET LOCK REQUIRED                            
*                                                                               
DMISLK1  MVI   K3,0                SET RETURN FROM LOCKER                       
         MVI   K6+1,0              SET RETURN CODE FROM DMENQDEQ                
         ST    RE,SAVRE                                                         
         ICM   RF,15,=V(LOCKER)                                                 
         BZ    DMISLK1A                                                         
         CLI   0(RF),X'07'         TEST DISABLED OFFLINE                        
         BE    DMISLK1A                                                         
         CLI   PARAM6,X'FF'        NO LOCKOUTS ON FULL TRACK READ               
         BNE   DMISLK2                                                          
DMISLK1A NI    K6,X'F0'            SET NO LOCKING ACTIONS                       
*                                                                               
DMISLK2  TM    K6,X'10'            TEST IF ENQUEUE REQUIRED                     
         BZ    DMISLK5                                                          
         TM    0(R2),X'80'         TEST IF READ FOR UPDATE                      
         BZ    DMISLK5                                                          
         L     R1,PARAM4                                                        
         TM    36(R1),X'09'        TEST IF XCPU AND ENQ PROT                    
         BNO   DMISLK5                                                          
         ICM   RF,15,=V(SSB)       TEST FOR FACWRK RECOVERY                     
         BZ    DMISLK3                                                          
         CLI   SSOXTND-SSOOFF(RF),X'FF'                                         
         BNE   DMISLK3                                                          
         TM    SSOSTAT2-SSOOFF(RF),SSOSRWRK                                     
         BO    DMISLK5                                                          
*                                                                               
DMISLK3  ICM   RF,15,=V(DMENQCTL)                                               
         BZ    DMISLK3X                                                         
         CLI   PARAM4,X'A0'        ONLY FOR CONTROL SYSTEM                      
         BL    DMISLK3X                                                         
         CLI   PARAM4,X'AF'                                                     
         BH    DMISLK3X                                                         
         GOTO1 (RF),K1,(C'E',=C'CTRL')                                          
         MVC   K6+1(1),K2          SAVE ENQDEQ RETURN CODE                      
         ICM   RF,15,=V(SYSAFLES)                                               
         BZ    DMISLK5                                                          
         L     RF,0(RF)                                                         
         OI    1(RF),X'02'         SET WE HAVE DONE DDS ENQ OF CTRL             
         B     DMISLK5                                                          
DMISLK3X EQU   *                                                                
*                                                                               
DMISLK5  TM    K6,X'0F'            TEST ANY LOCKING FUNCTIONS                   
         BZ    DMISLKX                                                          
         XC    K1(20),K1           BUILD LOCKER PARAM LIST                      
         MVC   K1(1),PARAM4        SET EXT FILE NUM                             
         MVC   K1+1(3),ISPDDA      SET BLK ADDRESS (PROVISIONAL)                
         TM    K6,X'08'            SET WANT TO TEST IF LOCK REQUIRED            
         BZ    *+8                                                              
         OI    K2,X'80'                                                         
         TM    K6,X'04'            SET WANT TO TEST IF UNCONVERTED              
         BZ    *+8                                                              
         OI    K2,X'40'                                                         
         MVC   K2+1(3),PARAM2+1    SET A(RECORD)                                
         SR    RE,RE                                                            
         ICM   RE,7,DM1+1                                                       
         CLC   0(4,RE),=C'DMREAD'                                               
         BE    DMISLK5A                                                         
         CLC   0(4,RE),=C'DMRDHI'                                               
         BNE   DMISLK5B                                                         
DMISLK5A ICM   RE,7,DM3+1          SET A(KEY) FOR DMREAD/DMRDHI                 
         BZ    DMISLK5B                                                         
         STCM  RE,7,K2+1                                                        
DMISLK5B MVC   K3+1(3),PARAM1+1    SET A(COMMAND)                               
         MVC   K5+1(3),PARAM4+1    SET A(DTF)                                   
*                                                                               
DMISLK6  TM    K6,X'01'            TEST TO BUILD PLIST ONLY                     
         BZ    DMISLK7                                                          
         LA    RF,K1               PASS LOCKER PLIST IN P7                      
         ST    RF,P7                                                            
         LA    RF,DM1              PASS CALLERS DMCB IN P8                      
         ST    RF,P8                                                            
         B     DMISLKX                                                          
*                                                                               
DMISLK7  GOTO1 =V(LOCKER),K1       CALL LOCKER                                  
*                                                                               
DMISLKX  OC    K3(1),K6+1          SET COMPOSITE RETURN CODE                    
         L     RE,SAVRE                                                         
         BR    RE                                                               
         EJECT                                                                  
***********************************************************************         
* DMOD000 - DIRECT ACCESS COMMANDS                                    *         
***********************************************************************         
         USING DTFPHD,R8                                                        
OPEN     MVC   P1,=V(DAOPEN)                                                    
         MVC   P2(20),PARAM2                                                    
         GOTO1 =V(DADDS),P1                                                     
         B     DMEXT                                                            
                                                                                
CLOSE    MVC   P1,=V(DACLOSE)                                                   
         MVC   P2(20),PARAM2                                                    
         GOTO1 =V(DADDS),P1                                                     
         B     DMEXT                                                            
                                                                                
READ     ICM   RE,15,=V(SSB)       READ DA RECORD                               
         BZ    READ1                                                            
         OC    0(2,RE),0(RE)                                                    
         BZ    READ1                                                            
         BAS   RE,DMDALKU          TEST IF CONVERTED ONLINE PROGRAM             
         TM    K3,X'04'                                                         
         BZ    READ1               PROGRAM CONVERTED TO DO RFU                  
         MVC   P1,=V(RDID)                                                      
         BAS   RE,DMDAGO           UNCONVERTED MUST READ RECORD                 
         BAS   RE,DMDALK                                                        
         TM    K3,X'04'            TEST IF LOCK DONE THIS TIME                  
         BO    READ2               YES NEED TO RE-READ FOR LATEST COPY          
         BAS   RE,DMERRDA                                                       
         B     DMEXT                                                            
READ1    BAS   RE,DMDALK           LOCK DA RECORD                               
         MVC   P1,=V(RDID)                                                      
         TM    K3,X'04'            TEST IF LOCK DONE THIS TIME                  
         BZ    *+10                                                             
READ2    MVC   P1,=V(RDIDFLS)      READ FLUSH TO GET LATEST COPY                
         BAS   RE,DMDAGO                                                        
         BAS   RE,DMERRDA                                                       
         B     DMEXT                                                            
*                                                                               
LOCKDA   BAS   RE,DMDALK           LOCK DA RECORD AND SET K1 THRU K6            
         B     DMEXT                                                            
*                                                                               
READDA   MVC   P1,=V(RDID)         READ DA REC AFTER DOING A LOCK               
         TM    K3,X'04'            TEST IF LOCK DONE BY LOCKDA                  
         BZ    *+10                                                             
         MVC   P1,=V(RDIDFLS)      READ FLUSH TO GET LATEST COPY                
         BAS   RE,DMDAGO                                                        
         BAS   RE,DMERRDA                                                       
         B     DMEXT                                                            
*                                                                               
READFLS  ICM   RE,15,=V(SSB)       READ DA RECORD AND FLUSH BUFFER              
         BZ    READFLS1                                                         
         OC    0(2,RE),0(RE)                                                    
         BZ    READFLS1                                                         
         BAS   RE,DMDALKU          TEST IF CONVERTED ONLINE PROGRAM             
         TM    K3,X'04'                                                         
         BZ    READFLS1            PROGRAM CONVERTED TO DO RFU                  
         MVC   P1,=V(RDIDFLS)                                                   
         BAS   RE,DMDAGO           UNCONVERTED MUST READ RECORD                 
         BAS   RE,DMDALK                                                        
         TM    K3,X'04'            TEST IF LOCK DONE THIS TIME                  
         BO    READFLS2            YES NEED TO RE-READ FOR LATEST COPY          
         BAS   RE,DMERRDA                                                       
         B     DMEXT                                                            
READFLS1 BAS   RE,DMDALK           LOCK DA RECORD                               
READFLS2 MVC   P1,=V(RDIDFLS)      READ FLUSH TO GET LATEST COPY                
         BAS   RE,DMDAGO                                                        
         BAS   RE,DMERRDA                                                       
         B     DMEXT                                                            
*                                                                               
READTRK  MVC   P1,=V(RDTRK)        READ TRACK                                   
         BAS   RE,DMDAGO                                                        
         BAS   RE,DMERRDA          NO LOCKOUTS ON FULL TRK READ                 
         B     DMEXT                                                            
*                                                                               
READTRB  MVC   P1,=V(RDIDTRK)      READ TRACK                                   
         BAS   RE,DMDAGO                                                        
         BAS   RE,DMERRDA          NO LOCKOUTS ON FULL TRK READ                 
         B     DMEXT                                                            
                                                                                
WRITE    BAS   RE,UPDATE           WRITE RECORD                                 
         MVC   P1,=V(WTID)                                                      
         BAS   RE,DMDAGO                                                        
         BAS   RE,DMERRDA                                                       
         B     DMEXT                                                            
                                                                                
WRTAFT   BAS   RE,UPDATE           ADD RECORD AFTER DISK ADDRESS                
         MVC   P1,=V(WTCKD)                                                     
         BAS   RE,DMDAGO                                                        
         OC    AUTL+1(3),AUTL+1    TEST ONLINE                                  
         BZ    WRTAFT2                                                          
         L     R6,=V(SSB)          YES FREE UP TCB IF ITS ME                    
         L     R7,SSBTKADR-SSBD(R6)                                             
         CLC   TCBDTFLK+1-TCBD(3,R7),PARAM4+1                                   
         BNE   *+10                                                             
         XC    TCBDTFLK-TCBD(4,R7),TCBDTFLK-TCBD(R7)                            
*                                                                               
WRTAFT2  BAS   RE,DMERRDA                                                       
         BAS   RE,DMDALK                                                        
         B     DMEXT                                                            
                                                                                
WRTADR   BAS   RE,UPDATE           ADD RECORD AFTER GIVEN ADDRESS               
         MVC   P1,=V(ADDAFT)                                                    
         BAS   RE,DMDAGO                                                        
         BAS   RE,DMERRDA                                                       
         B     DMEXT                                                            
                                                                                
WRTGET   BAS   RE,UPDATE           GET NEXT RECORD ADDRESS                      
         MVC   P1,=V(ADDADR)                                                    
         OC    AUTL+1(3),AUTL+1    TEST ONLINE                                  
         BZ    WRTGET2                                                          
         L     R6,=V(SSB)          YES SET A(DTF) IN TCB                        
         L     R7,SSBTKADR-SSBD(R6)                                             
         MVC   TCBDTFLK-TCBD(L'TCBDTFLK,R7),PARAM4                              
WRTGET2  BAS   RE,DMDAGO                                                        
         BAS   RE,DMERRDA                                                       
         B     DMEXT                                                            
         EJECT                                                                  
DMDAGO   ST    RE,SAVRE            PASS CONTROL TO DADDS                        
         MVC   P2(20),PARAM2                                                    
         L     R8,PARAM4                                                        
         LA    R8,0(R8)                                                         
         MVC   LASTBLK,DBLKDA      SAVE DISK ADDRESS OF CURRENT BLOCK           
         TM    36(R8),X'04'        TEST FILE QUIESCED                           
         BZ    DMDAGO1                                                          
         TM    36(R8),X'20'        TEST FILE OPEN                               
         BZ    INHIBITR                                                         
DMDAGO1  L     RF,=V(DADDS)                                                     
         TM    DM1,X'40'           TEST 31-BIT IOA                              
         BZ    *+8                                                              
         O     RF,=X'80000000'     SET 31-BIT CALLER                            
         GOTO1 (RF),P1                                                          
         SAM24                                                                  
         MVC   PARAM3,P3           PASS ERROR STATUS/LEN TO CALLER              
         TM    DM1,X'01'           TEST USER WANTS LEN IN DMCB                  
         BZ    *+10                                                             
         MVC   DM6+2(2),P3+2                                                    
         L     RE,SAVRE                                                         
         BR    RE                                                               
         EJECT                                                                  
DMDALKB  MVI   K6,X'01'            SET BUILD LOCK PLIST                         
         B     DMDALK1                                                          
DMDALKD  MVI   K6,X'02'            SET DO LOCK                                  
         B     DMDALK1                                                          
DMDALKU  MVI   K6,X'04'            SET CHECK UNCONVERTED PROGRAM                
         B     DMDALK1                                                          
DMDALKC  MVI   K6,X'08'            SET CHECK LOCK REQUIRED                      
         B     DMDALK1                                                          
DMDALKE  MVI   K6,X'10'            SET ENQUEUE                                  
         B     DMDALK1                                                          
DMDALKEB MVI   K6,X'11'            SET ENQUEUE AND BUILD PLIST                  
         B     DMDALK1                                                          
DMDALK   MVI   K6,X'12'            SET LOCK REQUIRED                            
*                                                                               
DMDALK1  MVI   K3,0                SET RETURN FROM LOCKER                       
         MVI   K6+1,0              SET RETURN CODE FROM DMENQDEQ                
         ST    RE,SAVRE                                                         
         ICM   RF,15,=V(LOCKER)    NO LOCKOUTS IF OFFLINE                       
         BZ    DMDALK1A                                                         
         CLI   0(RF),X'07'         TEST DISSABLED OFFLINE                       
         BNE   DMDALK2                                                          
DMDALK1A NI    K6,X'F0'            SET NO LOCKING ACTIONS                       
*                                                                               
DMDALK2  TM    K6,X'10'            TEST IF ENQUEUE REQUIRED                     
         BZ    DMDALK5                                                          
         TM    0(R2),X'80'         TEST IF READ FOR UPDATE                      
         BZ    DMDALK5                                                          
         L     R8,PARAM4                                                        
         TM    36(R8),X'09'        YES TEST IF XCPU AND ENQ PROT                
         BNO   DMDALK5                                                          
         ICM   RF,15,=V(SSB)       TEST FOR FACWRK RECOVERY                     
         BZ    DMDALK3                                                          
         CLI   SSOXTND-SSOOFF(RF),X'FF'                                         
         BNE   DMDALK3                                                          
         TM    SSOSTAT2-SSOOFF(RF),SSOSRWRK                                     
         BO    DMDALK5                                                          
*                                                                               
DMDALK3  ICM   RF,15,=V(DMENQCTL)                                               
         BZ    DMDALK3X                                                         
         CLI   PARAM4,X'A0'        TEST FOR CONTROL SYSTEM                      
         BL    DMDALK3X                                                         
         CLI   PARAM4,X'AF'                                                     
         BH    DMDALK3X                                                         
         GOTO1 (RF),K1,(C'E',=C'CTRL')                                          
         MVC   K6+1(1),K2          SAVE ENQDEQ RETURN CODE                      
         ICM   RF,15,=V(SYSAFLES)                                               
         BZ    DMDALK5                                                          
         L     RF,0(RF)                                                         
         OI    1(RF),X'02'         SET WE HAVE DONE DDS ENQ OF CTRL             
         B     DMDALK5                                                          
DMDALK3X EQU   *                                                                
*                                                                               
DMDALK5  TM    K6,X'0F'            TEST ANY LOCKING FUNCTIONS                   
         BZ    DMDALKX                                                          
         XC    K1(20),K1           BUILD LOCKER PARAM LIST                      
         MVC   K1(1),PARAM4        SET EXT FILE NUM                             
         MVC   K1+1(3),PARAM5+1    SET A(DISK ADDR)                             
         OI    K2,X'20'            SET PASSING A(DISK ADDR)                     
         TM    K6,X'08'            SET WANT TO TEST IF LOCK REQUIRED            
         BZ    *+8                                                              
         OI    K2,X'80'                                                         
         TM    K6,X'04'            SET WANT TO TEST IF UNCONVERTED              
         BZ    *+8                                                              
         OI    K2,X'40'                                                         
         MVC   K2+1(3),PARAM2+1    SET A(RECORD)                                
         MVC   K3+1(3),PARAM1+1    SET A(COMMAND)                               
         MVC   K5+1(3),PARAM4+1    SET A(DTF)                                   
*&&UK*&& SR    RE,RE                                                            
*&&UK*&& ICM   RE,7,PARAM5+1                                                    
*&&UK*&& OC    0(4,RE),0(RE)                                                    
*&&UK*&& JZ    *+2                 DIE=ZERO DISK ADDR                           
         GOTO1 =V(LOCKER),K1                                                    
*                                                                               
DMDALKX  OC    K3(1),K6+1          SET COMPOSITE RETURN CODE                    
         L     RE,SAVRE                                                         
         BR    RE                                                               
         EJECT                                                                  
DMERRDA  MVI   DM3,0                                                            
         LA    R7,P3                                                            
         TM    0(R7),X'40'         WRONG LEN REC                                
         BZ    *+8                                                              
         OI    DM3,X'40'                                                        
         TM    0(R7),X'08'         NO ROOM                                      
         BZ    *+8                                                              
         OI    DM3,X'01'                                                        
         TM    0(R7),X'01'         REF OUTSIDE EXTENTS                          
         BZ    *+8                                                              
         OI    DM3,X'80'           SET EOF                                      
         TM    1(R7),X'90'         DATA CHECKS                                  
         BZ    *+8                                                              
         OI    DM3,X'40'           SET UNREC ERROR                              
         TM    1(R7),X'08'         REC NOT FOUND                                
         BZ    *+8                                                              
         OI    DM3,X'10'                                                        
         TM    1(R7),X'04'         END OF FILE                                  
         BZ    *+8                                                              
         OI    DM3,X'80'                                                        
         TM    DM3,X'40'           TEST UNRCV ERROR                             
         BZR   RE                                                               
         B     DMEXT               EXIT                                         
         EJECT                                                                  
***********************************************************************         
* DMOD000 - SYSTEM FILES COMMANDS                                     *         
* RETURN ADDRESS OF DMGRFLES IN 1ST PARAMETER                         *         
* NOTE 31-BIT MODE AS BUFFER IN P2 CAN BE ABOVE THE LINE              *         
***********************************************************************         
DMGRFLS  MVC   PARAM1,=V(DMGRFLES)                                              
         B     DMEXT                                                            
         EJECT                                                                  
***********************************************************************         
* OPEN ALL FILES FOR A SYSTEM                                         *         
***********************************************************************         
OPENSYS  MVC   P1(8),PARAM1        COPY PARAM LIST FOR IOAREA                   
         LR    R0,RA                                                            
         LA    RA,P1                                                            
         BAS   RE,FINDSYS                                                       
         USING SYSFLSTD,R5                                                      
         MVC   SENUM,SYSFSYS#      SET SE NUM FROM LIST HEADER                  
         LR    RA,R0                                                            
         MVI   P7,0                P7 USED FOR FLAGS                            
         ICM   R1,15,=V(SSB)                                                    
         BZ    OP0                                                              
         OC    0(2,R1),0(R1)                                                    
         BZ    OP0                                                              
         OI    P7,X'80'            SET ONLINE                                   
*                                                                               
OP0      LH    R6,SYSF#FLS         R6=NUMBER OF FILES                           
         ST    R5,FULL0            FULL0=A(SYSFLES LIST HEADER)                 
                                                                                
***********************************************************************         
* FIRST PASS SET DTF FLAGS AND OPEN ALL OPERATIONAL FILES             *         
***********************************************************************         
OP1      MVC   FULL1,0(R5)         FULL1=SYSFLES LIST HEADER                    
         LA    R5,SYSFLIST         POINT TO 1ST FILE IN LIST                    
*                                                                               
OP1A     MVC   P1,=A(OPEN)         COMMAND FOR DA FILE                          
         TM    SYSFIND1,SFISF                                                   
         BZ    *+10                                                             
         MVC   P1,=A(OPENIS)       COMMAND FOR IS FILE                          
         BAS   RE,BLDOPCL                                                       
                                                                                
         L     R8,SYSFADTF-1       R8=A(DTF)                                    
         USING DTFPHD,R8                                                        
         MVC   DTFXNUM,SYSFILE#    SET DTF EXT NUM                              
*                                                                               
OP1B     TM    SYSFIND1,SFNOP      IF NOP SET DTF NOP AND SKIP OPEN             
         BZ    OP1C                                                             
         TM    SYSFIND2,SFALIAS    IS THIS AN ALIAS FILE                        
         BZ    OP1B1               NO SET NOP IN DTF                            
         TM    DTFOPEN,DTF_OPN     YES TEST ALREADY OPEN                        
         BO    OP1C                YES DONT NO-OP SINCE OPEN OTHER              
OP1B1    OI    DTFOPEN,DTF_NOP                                                  
         B     OP1X                                                             
                                                                                
OP1C     NI    DTFOPEN,255-DTF_NOP TURN OFF NOP BIT IN DTF                      
         TM    DTFOPEN,DTF_OPN     BYPASS IF ALREADY OPEN                       
         BO    OP1S                                                             
         TM    P7,X'80'            TEST OFFLINE                                 
         BZ    OP1C1                                                            
         TM    SYSFIND2,SFALIAS    IS THIS NON NATIVE FILE?                     
         BZ    OP1D                                                             
         B     OP1X                                                             
                                                                                
OP1C1    C     R8,=V(SYS1FLEW)     OFFLINE SYS1 FILES ALWAYS UPDATIVE           
         BL    OP1D                                                             
         TM    SYSFIND2,SF_RO                                                   
         BZ    OP1D                                                             
         OI    DTFOPEN,DTF_RO      SET DTF TO READONLY                          
*                                                                               
OP1D     MVC   DTFSNUM,SENUM       SET DTF SENUM                                
         TM    SYSFIND1,SFRCV      TEST IF RECOVERY FILE                        
         BZ    OP1E                                                             
         SR    R0,R0                                                            
         ICM   R0,3,DBLKSZ         TEST IF BLOCKED                              
         BZ    OP1E                                                             
         OC    DBLK,DBLK           TEST BUFFER ALREADY ALLOCATED                
         BNZ   OP1E                                                             
         TM    DTFOPEN,DTF_RO      TEST READONLY                                
         BO    OP1E                                                             
         GETMAIN RU,LV=(0)         RECOVERY BUFFER BELOW THE LINE               
         ST    R1,DBLK                                                          
         XC    0(6,R1),0(R1)                                                    
*                                                                               
OP1E     LA    R1,P1               OPEN THE FILE                                
         BRAS  RE,DMOD000                                                       
         TM    P7,X'80'            TEST IF ONLINE                               
         BO    OP1G                                                             
         TM    DTFFLAG,DTFROWRN    SWITCHED FROM READONLY TO UPDATIVE           
         BZ    *+12                                                             
         NI    SYSFIND2,255-SF_RO  SET TO UPDATIVE                              
         B     OP1G                                                             
         TM    DTFOPEN,DTF_RO      TEST READ ONLY FILE IN DTF                   
         BZ    OP1G                                                             
         OI    SYSFIND2,SF_RO      SET TO READONLY                              
                                                                                
OP1G     DS    0H                                                               
*&&US                                                                           
         CLC   DTFDD(6),=C'STRFDR' IF WE OPENED A TRAFFIC FILE                  
         BE    OP1H                                                             
         CLC   DTFDD(6),=C'STRFFL' IN THE SPOT SYSTEM                           
         BNE   OP1J                                                             
OP1H     SR    RE,RE                                                            
         IC    RE,SENUM                                                         
         A     RE,=A(TRFOPLST)                                                  
         MVI   0(RE),C'T'          SET SO THAT WE CAN READ IT LATER             
OP1J     EQU   *                                                                
*&&                                                                             
OP1K     TM    SYSFIND1,SFCPU      TEST/SET FILE CROSS CPU PROT IN DTF          
         BZ    OP1L                                                             
         OI    DTFOPEN,DTF_CPU                                                  
         TM    FULL1+1,SYSFQDP     TEST/SET DDSQDQ PROT IN DTF                  
         BZ    OP1L                                                             
         OI    DTFOPEN,DTF_ENQ                                                  
*                                                                               
OP1L     CLI   FULL1,X'0A'         TEST IF CONTROL SYSTEM                       
         BE    OP1M                                                             
         CLI   SYSFILE#,X'A0'      NO TEST FOR CONTROL SYSTEM FILE              
         BL    OP1M                                                             
         CLI   SYSFILE#,X'AF'                                                   
         BH    OP1M                                                             
         OI    DTFOPEN,DTF_RO      SET READ ONLY FILE                           
         B     OP1X                                                             
*                                                                               
OP1M     TM    SYSFIND1,SFISF      TEST IF DA FILE                              
         BO    OP1S                                                             
                                                                                
         TM    SYSFIND2,SF_RO      TEST READ ONLY                               
         BO    OP1S                                                             
         TM    DTFFLAG,DTFGLOB     TEST GLOBAL                                  
         BZ    OP1R                                                             
         MVC   P1,=V(DACPUID)      GET/SET DSPACE ID                            
         LA    R1,P1                                                            
         L     RF,=V(DADDS)                                                     
         BASR  RE,RF                                                            
                                                                                
SET      USING SYSFLSTD,RF         SET UPDATIVE GLOBAL FILE                     
         L     RF,FULL0            POINT TO SYSFLES HEADER                      
         OI    SET.SYSFSTAT,SYSFUGBL                                            
         OI    FULL1+1,SYSFUGBL    SET UPDATIVE GLOBALS IN SYSTEM               
         B     OP1S                                                             
*                                  SET UPDATIVE LOCAL FILE                      
OP1R     L     RF,FULL0            POINT TO SYSFLES HEADER                      
         OI    SET.SYSFSTAT,SYSFULCL                                            
         OI    FULL1+1,SYSFULCL    SET UPDATIVE LOCALS IN SYSTEM                
         DROP  SET                                                              
*                                                                               
OP1S     TM    DTFOPEN,DTF_RO+DTF_NOP TEST NOP/READONLY FILE                    
         BNZ   OP1X                                                             
         TM    P7,X'80'            TEST ONLINE                                  
         BZ    OP1X                                                             
         OI    P7,X'01'            SET ONLINE UPDATIVE FILES                    
*                                                                               
OP1X     LA    R5,SYSFLNQ(R5)      BUMP TO NEXT FILE IN LIST                    
         BCT   R6,OP1A                                                          
                                                                                
         TM    P7,X'80'            TEST ONLINE                                  
         BZ    OP2                                                              
         TM    P7,X'01'            TEST ONLINE UPDATIVE FILES                   
         BO    OP2                                                              
                                                                                
SET      USING SYSFLSTD,RF                                                      
         L     RF,FULL0            FASTART SETS FILE DTFS TO R/O                
         NI    SET.SYSFSTAT,255-(SYSFULCL+SYSFUGBL)  RESET NOT UPDATIVE         
         NI    FULL1+1,255-(SYSFULCL+SYSFUGBL)                                  
         DROP  SET                                                              
                                                                                
***********************************************************************         
* SECOND PASS - OPEN GLOBAL SYSTEM IN DATA SPACE                      *         
***********************************************************************         
OP2      L     R5,FULL0            R5=A(SYSLIST HEADER)                         
         L     R8,SYSFLIST+(SYSFADTF-SYSFLSTD-1)                                
         TM    DTFFLAG,DTFGLOB     IS FIRST FILE DEFINED AS GLOBAL              
         BZ    OP2X                                                             
         DROP  R8                                                               
                                                                                
         XC    FULL1(8),FULL1      SET PLIST FOR LOCKSPC                        
         MVC   FULL1+3(1),SENUM                                                 
         MVI   FULL1+1,1           SET OPEN SYSTEM                              
         TM    SYSFSTAT,SYSFUGBL   TEST IF UPDATIVE GLOBALS IN SYSTEM           
         BO    *+8                                                              
         MVI   FULL1+1,10          SET OPEN AS READ ONLY SYSTEM                 
         ICM   R1,15,=V(SSB)                                                    
         JZ    *+2                 DIE=NO SSB                                   
         OC    0(2,R1),0(R1)       TEST IF ONLINE OR OFFLINE                    
         BNZ   OP2B                                                             
*                                                                               
OP2A     CLI   2(R1),X'FF'         OFFLINE MUST HAVE EXTENDED SSB               
         JNE   *+2                 DIE=SSB NOT EXTENDED                         
         CLI   FULL1+1,1           TEST UPDATIVE SYSTEM                         
         BE    OP2C                YES CALL LOCKSPC WITH OPEN COMMAND           
         B     OP2X                DONT BOTHER WITH READ ONLY SYSTEMS           
*                                                                               
OP2B     EQU   *                   ONLINE - ALWAYS CALL LOCKSPC                 
*                                                                               
OP2C     GOTO1 =V(LOCKSPC),FULL1   OPEN SYSTEM CALL                             
         TM    4(R1),X'80'                                                      
         BZ    OP2D                                                             
         OI    PARAM3+1,X'08'      FLAG OPEN FAILED DUE MAINT                   
         B     DMEXT                                                            
                                                                                
OP2D     OI    SYSFSTAT,SYSFLSPC   SET LOCKSPC OPEN DONE                        
*                                                                               
OP2X     EQU   *                                                                
                                                                                
***********************************************************************         
* THIRD PASS - SEARCH FOR EOF ON OPEN UPDATIVE FILES                  *         
***********************************************************************         
OP3      OI    P7,X'40'            SET CALLED BY OPEN FLAG                      
         BAS   RE,SRCHEOF                                                       
*                                                                               
OP4      TM    FULL1+1,X'0C'       TEST ANY UPDATIVE FILES                      
         BZ    DMEXT                                                            
         BAS   RE,UPDATE           GET CONTROL FOR UPDATIVE SYSTEM              
         B     DMEXT                                                            
         DROP  R5                                                               
         EJECT                                                                  
***********************************************************************         
*  CLOSE SYSTEM - CLOSES ALL FILES FOR SYSTEM REGARDLESS OF LIST      *         
***********************************************************************         
CLSESYS  MVC   P1(8),PARAM1        COPY PARAM LIST FOR IOAREA                   
         LR    R0,RA                                                            
         LA    RA,P1                                                            
         BAS   RE,FINDSYS          RETURN R5 WITH SYSTEM LIST                   
         ST    R5,FULL0            FULL0=A(SYSFLES LIST HEADER)                 
         USING SYSFLSTD,R5                                                      
         MVC   SENUM,SYSFSYS#                                                   
         LR    RA,R0                                                            
*                                                                               
         LA    R1,FULL1            PLIST FULL1/2/3                              
         XC    0(12,R1),0(R1)                                                   
         ICM   RF,15,=V(LOCKER)                                                 
         BZ    *+6                                                              
         BASR  RE,RF               CALL LOCKER TO UNLOCK ANYTHING               
*                                                                               
         LH    R6,SYSF#FLS         R6=NUM OF FILES                              
         LA    R5,SYSFLIST                                                      
CLSYS02  TM    SYSFIND1,SFRCV      TEST IF RECOVERY FILE                        
         BO    CLSYS08                                                          
         LA    R5,SYSFLNQ(R5)                                                   
         BCT   R6,CLSYS02                                                       
         B     CLSYS10             SKIP IF NO RECOVERY FILE                     
*                                                                               
CLSYS08  L     R8,SYSFADTF-1       R8=A(DTF)                                    
         USING DTFPHD,R8                                                        
         TM    DTFOPEN,DTF_OPN     TEST IF FILE OPEN                            
         BZ    CLSYS10                                                          
         TM    DTFOPEN,DTF_RO      TEST IF READ ONLY                            
         BO    CLSYS10                                                          
         DROP  R8                                                               
                                                                                
         LA    R1,FULL1            PLIST FULL1/2/3                              
         XC    0(12,R1),0(R1)                                                   
         MVC   8(1,R1),SENUM       P3=SYS/FF                                    
         MVI   9(R1),X'FF'                                                      
         GOTO1 =V(DMRCVR)          CLOSE CALL TO DMRCVR                         
*                                                                               
CLSYS10  L     R5,FULL0            R5=A(SYSTEM LIST HEADER)                     
         LH    R6,SYSF#FLS         R6=NUM OF FILES TO PROCESS                   
         MVC   FULL1,0(R5)                                                      
         LA    R5,SYSFLIST         A(SYSTEM FILE LIST)                          
                                                                                
**********************************************************************          
* CLOSE THE ACTUAL FILES IN SYSTEM                                              
**********************************************************************          
CLSYS20  L     R8,SYSFADTF-1       GET DTF ADDR                                 
         USING DTFPHD,R8                                                        
         TM    SYSFIND1,SFNOP      SKIP NOP FILES                               
         BO    CLSYS28                                                          
         TM    SYSFIND2,SFALIAS    DONT CLOSE ALIAS FILES                       
         BO    CLSYS28                                                          
         TM    SYSFIND1,SFISF      SKIP IS FILES                                
         BO    CLSYS22                                                          
         TM    DIND,DINDBWP        SKIP IF BUFFER EMPTY (BLOCK PENDING)         
         BZ    CLSYS22                                                          
         BAS   RE,BLDOPCL          SET FOR DUMMY READ                           
         MVC   P1,=V(RDID)                                                      
         MVC   P6,=4X'FF'                                                       
         L     RF,=V(DADDS)                                                     
         BASR  RE,RF                                                            
         OC    P3(2),P3                                                         
         JNZ   *+2                 DIE=DADDS READ ERR                           
*                                                                               
CLSYS22  MVC   P1,=A(CLOSE)                                                     
         TM    SYSFIND1,SFISF      TEST IS FILE                                 
         BZ    *+10                                                             
         MVC   P1,=A(CLOSEIS)                                                   
         BAS   RE,BLDOPCL                                                       
         BASR  RE,RF                                                            
*                                                                               
CLSYS28  LA    R5,SYSFLNQ(R5)      NEXT FILE                                    
         BCT   R6,CLSYS20                                                       
*                                                                               
         L     R5,FULL0            R5=A(SYSFLES HEADER)                         
         L     R8,SYSFLIST+(SYSFADTF-SYSFLSTD-1)  A(DTF 1ST FILE)               
         CLI   SENUM,1                                                          
         BNH   CLSYSXIT            MUST HAVE BEEN SERVICE SYSTEM                
                                                                                
***********************************************************************         
* CLOSE SYSTEM WITHIN DATASPACE FOR GLOBAL FILES                      *         
***********************************************************************         
         TM    DTFFLAG,DTFGLOB     IS FIRST FILE GLOBAL                         
         BZ    CLSYS30                                                          
         TM    SYSFSTAT,SYSFLSPC   TEST IF LOCKSPC OPEN WAS DONE                
         BZ    CLSYS30                                                          
         LA    R1,FULL1            PLIST=FULL1/2                                
         XC    0(8,R1),0(R1)                                                    
         MVI   1(R1),2                                                          
         MVC   3(1,R1),SENUM                                                    
         GOTO1 =V(LOCKSPC)         CLOSE SYSTEM IN DATA SPACE                   
         NI    SYSFSTAT,255-SYSFLSPC-SYSFDDSQ                                   
                                                                                
***********************************************************************         
* DEQUEUE MVS ENQUEUE IF APPLICABLE                                   *         
***********************************************************************         
CLSYS30  TM    SYSFSTAT,SYSFMVSQ   WAS SYSTEM MVS ENQUEUED                      
         BZ    CLSYS40                                                          
         LLC   RE,SENUM            YES BUILD ENQ ID FROM SENUM                  
         CVD   RE,K1                                                            
         UNPK  K1(4),K1+6(2)                                                    
         OI    K1+3,X'F0'                                                       
         MVC   UPDMIN+4(4),K1                                                   
         CLC   UPDMAJ(2),=C'00'    TEST USER REQUESTED NO PROTECTION            
         BE    CLSYS34                                                          
         ICM   R1,15,=V(SSB)       OFFLINE ALWAYS DOES MVS DEQ                  
         BZ    CLSYS32                                                          
         OC    0(2,R1),0(R1)                                                    
         BZ    CLSYS32                                                          
         TM    SSBSTAT4-SSBD(R1),SSBSAOR                                        
         BO    CLSYS34             AOR NEVER DOES DEQ                           
                                                                                
CLSYS32  EQU   *                                                                
         DEQ   (UPDMAJ,UPDMIN,8,SYSTEM),RET=HAVE                                
CLSYS34  NI    SYSFSTAT,255-SYSFMVSQ                                            
         B     CLSYSXIT                                                         
                                                                                
***********************************************************************         
* DEQUEUE DDS ENQUEUE FOR CONTROL SYSTEM                              *         
***********************************************************************         
CLSYS40  CLI   SYSFSYS#,X'0A'      TEST CONTROL SYSTEM                          
         BNE   CLSYS60                                                          
         TM    SYSFSTAT,SYSFQDP    TEST PROTECTED BY DDS ENQ                    
         BZ    CLSYS60                                                          
         TM    SYSFSTAT,SYSFDDSQ   TEST IF ENQUEUED VIA DDS ENQDEQ              
         BZ    CLSYS60                                                          
         LA    RF,=C'CTRL'                                                      
         GOTO1 =V(DMENQCTL),K1,(C'D',(RF))                                      
         NI    SYSFSTAT,255-SYSFDDSQ                                            
CLSYS60  EQU   *                                                                
                                                                                
***********************************************************************         
* RESET FLAGS SET BY OPENSYS                                          *         
***********************************************************************         
CLSYSXIT NI    SYSFSTAT,255-(SYSFDSP+SYSFLSPC+SYSFULCL+SYSFUGBL)                
         B     DMEXT                                                            
         DROP  R5,R8                                                            
         EJECT                                                                  
***********************************************************************         
* ROUTINES - READ FIRST RECORD OF DA FILE                             *         
***********************************************************************         
RDHDR    ST    RE,SAVRE            R8=A(DTF)                                    
         L     RF,=V(DADDS)                                                     
         MVC   P1,=V(RDID)                                                      
         MVC   P2,PARAM2           SET I/O ADDRESS                              
         XC    P3,P3                                                            
         MVC   P6,=X'00010101'     SET TO TRK1/BLK1/REC1                        
         TM    DTFTYPE-DTF(R8),DTFTBIGF+DTFTBIG                                 
         BZ    RDHDR1                                                           
         BO    RDHDR0                                                           
         TM    DTFTYPE-DTF(R8),DTFTBIGF                                         
         BO    *+14                                                             
         MVC   P6,=X'00004101'     SET TO TRK1/BLK1/REC1 18-BIT TRACK           
         B     RDHDR1                                                           
         MVC   P6,=X'00001101'     SET TO TRK1/BLK1/REC1 20-BIT TRACK           
         B     RDHDR1                                                           
RDHDR0   MVC   P6,=X'00000501'     SET TO TRK1/BLK1/REC1 22-BIT TRACK           
RDHDR1   BASR  RE,RF                                                            
         OC    P3(2),P3                                                         
         L     RE,SAVRE                                                         
         BR    RE                                                               
                                                                                
***********************************************************************         
* SET UP PARMS FOR OPEN OR CLOSE                                      *         
***********************************************************************         
BLDOPCL  MVC   P2,PARAM2           DATA ADDRESS                                 
         XC    P3,P3                                                            
         MVC   P4,4(R5)            DTF ADDRESS                                  
         MVI   P4,0                                                             
         XC    P6,P6               CLEAR DISK ADDRESS                           
         LA    R0,P6                                                            
         ST    R0,P5               SET A(DISK ADDRESS)                          
         LA    R1,P1                                                            
         L     RF,=A(DMOD000)                                                   
         BR    RE                                                               
                                                                                
***********************************************************************         
* FIND SYSFLES LIST ADDRESS FOR SYSTEM USING SYSTEM TABLE DMSYSTAB    *         
***********************************************************************         
FINDSYS  LLC   R5,PARAM2           INDEX INTO 16-BYTE SYSTEM TABLE              
         SLL   R5,4                                                             
         A     R5,=V(SYSTABL)                                                   
         L     R5,DMSYASYF-SYSTABD(R5)                                          
         ST    R5,P7                                                            
         STCM  R5,7,PARAM2+1       MOVE ADDRESS TO CALLERS PLIST                
         BR    RE                                                               
         EJECT                                                                  
***********************************************************************         
* THIS ROUTINE IS CALLED BEFORE EVERY ATTEMPT TO UPDATE A FILE        *         
* EXIT VIA XMOD1 2 IF READ ONLY FILE OR UPDATES INHIBITED             *         
*                                                                     *         
* NORMAL PROTECTION VIA MVS ENQUEUE FOR DMGR/SYS00NN ON 1ST OFFLINE   *         
* UPDATE TO THIS SYSTEM WHERE NN IS SE NUM. THIS ENQUEUE IS DONE AT   *         
* SYSTEM OPEN TIME FOR ONLINE . ALSO CROSS CPU PROTECTION VIA STAMP   *         
*                                                                     *         
* SPECIAL CODE FOR CONTROL SYSTEM.                                    *         
* IF 01 BIT ON IN SYSFLES LIST HDR THEN EACH I/O WILL BE PRECEEDED BY *         
* A CALL TO DDS ENQ/DEQ SYSTEM TO PROTECT CONTROL SYSTEM FILES FROM   *         
* CONCURRENT UPDATING FROM OTHER ONLINE SYSTEMS OR OFFLINE PROGRAMS.  *         
***********************************************************************         
UPDATE   NTR1                                                                   
         XC    K1(12),K1           CLEAR RETURN AREA                            
*                                                                               
UPDNO1   XC    P7,P7               FIRST CHECK IF UPDATING ALLOWED              
         L     R7,AUTL                                                          
         LA    R7,0(R7)                                                         
         USING UTLD,R7             R7=A(UTL) IF ONLINE                          
         LTR   R7,R7                                                            
         BZ    UPDNO2                                                           
         OI    P7,X'80'            SET ONLINE FLAG IN P7                        
*                                                                               
UPDNO2   CLC   PARAM1+1(3),=AL3(OPENSYS)                                        
         BNE   UPDNO2A                                                          
         OI    P7,X'40'            SET CALLED-BY-OPEN FLAG IN P7                
         TM    P7,X'80'                                                         
         BZ    UPDX                EXIT IF OFFLINE CALLED BY OPEN               
         B     UPDNOX              TEST OWNERSHIP AT ONLINE OPEN                
*                                                                               
UPDNO2A  TM    P7,X'80'                                                         
         BZ    UPDNO5              RULES FOR OFFLINE                            
*                                                                               
UPDNO3   L     R1,PARAM4           ONLINE - A(DTF)                              
         LA    R1,0(R1)                                                         
         USING DTFPHD,R1                                                        
         TM    DTFOPEN,DTF_QUI     TEST QUIESCED FILE                           
         BO    INHIBITW                                                         
         TM    DTFOPEN,DTF_RO      TEST READ ONLY FILE                          
         BO    UPDNONO                                                          
*                                                                               
UPDNO4   TM    TTEST,TTESTNOU      TEST IF DO NOT UPDATE BIT ON                 
         BZ    UPDNOX                                                           
*                                                                               
UPDNO4A  C     R1,=V(SYS1FLEX)     IGNORE IF SY1 FILES                          
         BL    UPDNOX                                                           
         CLI   TSVCREQ+1,X'1E'     IGNORE IF =PASS S/R                          
         BE    UPDNOOKX                                                         
         CLI   TSVCREQ+1,X'21'     AND    IF =PC   S/R                          
         BE    UPDNOOKX                                                         
*                                                                               
UPDNO4B  ICM   RF,15,DATCB         CAN ADD/CHANGE REQUESTS                      
         BZ    UPDNO4C                                                          
X        USING TCBD,RF                                                          
         TM    X.TCBFLAG4,TCB4CCHG+TCB4CADD                                     
         BZ    UPDNO4C                                                          
*                                                                               
         TM    TTEST,TTESTURQ      ALLOWED TO ADD/CHG REQ WITH R/O              
         BO    *+12                                                             
         TM    TSTAT1,TSTATDDS     TEST DDS TERMINAL                            
         BZ    UPDNO4C                                                          
         TM    TSTATB,TSTATROS     UNLESS SYSTEM IS READ ONLY                   
         BO    UPDNO4C                                                          
         TM    TTEST,TTESTUEN      UNLESS CONNECTED WITH U=N                    
         BO    UPDNO4C                                                          
         TM    X.TCBFLAG4,TCB4LREQ IS REQUEST IS LINKED?                        
         BZ    UPDNO4C                                                          
         TM    TSTAT5,TST5NONO     UNLESS ALREADY HAD A PREV IGNORE             
         BZ    UPDNOOKX                                                         
         DROP  X                                                                
*                                                                               
UPDNO4C  TM    0(R2),X'24'         TEST DMCB1 FOR SPECIAL WRITE/ADD             
         BNO   UPDNO4D                                                          
         TM    TSTATB,TSTATROS     SYSTEM MUST BE UPDATIVE                      
         BO    UPDNO4D                                                          
         TM    TTEST,TTESTUEN      UNLESS CONNECTED WITH U=N                    
         BO    UPDNO4D                                                          
         B     UPDNOOKX            ALLOW MAINTENANCE UPDATE                     
*                                                                               
UPDNO4D  B     UPDNO6              UPDATE IS NOT ALLOWED                        
*                                                                               
UPDNO5   L     R1,PARAM4           OFFLINE - A(DTF)                             
         LA    R1,0(R1)                                                         
         C     R1,=V(SYS1FLEW)     ALWAYS DO UPDATES TO SYS1 FILES              
         BL    UPDNOX                                                           
         ICM   RE,15,=V(SSB)                                                    
         BZ    UPDNOX                                                           
         CLI   2(RE),X'FF'         MUST HAVE EXTENDED OFFLINE V(SSB)            
         BNE   UPDNOX                                                           
         TM    3(RE),X'80'         NO UPDATES IF RECOVERY TO FACWRK             
         BO    UPDNO6                                                           
         TM    4(RE),X'04'         NO UPDATES IF WRITE=N INPUT                  
         BO    UPDNO6                                                           
         TM    DTFFLAG,DTFROWRN    TEST IF SET UPDATIVE BY MARKER=Y             
         BO    *+12                                                             
         TM    DTFOPEN,DTF_RO      TEST READ ONLY FILE                          
         BZ    UPDNOX                                                           
         MVC   WRNDATAF,DTFDD                                                   
         LA    R4,WRNF                                                          
         EXTRACT (4),FIELDS=TIOT                                                
         L     R4,WRNF                                                          
         MVC   WRNDATAJ,0(R4)                                                   
         L     R1,PARAM4           RESET A(DTF)                                 
         LA    R1,0(R1)                                                         
         TM    DTFFLAG,DTFROWRN    TEST IF SET UPDATIVE BY MARKER=Y             
         BO    UPNO5W                                                           
*                                                                               
UPNO5C   MVC   WRNDATAA,=CL9'ABEND 671'                                         
         GOTO1 =A(DMOD000),WRNMSG                                               
         ABEND 671,DUMP                                                         
*                                                                               
UPNO5W   GOTO1 =A(DMOD000),WRNMSG  OUTPUT WARNING MESSAGE                       
         L     R1,PARAM4           RESET A(DTF)                                 
         LA    R1,0(R1)                                                         
         NI    DTFFLAG,255-DTFROWRN  TURN OFF WARNING FLAG                      
         B     UPDNOX                                                           
         DROP  R1                                                               
*                                                                               
UPDNO6   CLC   PARAM1,=A(WKEY)     SET REC LEN IN DTF FOR V/L IS                
         BE    *+14                                                             
         CLC   PARAM1,=A(AKEY)                                                  
         BNE   UPDNO7                                                           
         USING ISDTF,R1                                                         
         TM    ISCMPRSW,X'80'                                                   
         BZ    UPDNONO                                                          
         L     RE,PARAM2                                                        
         AH    RE,ISKEYLN                                                       
         MVC   ISRECLN,0(RE)                                                    
         B     UPDNONO                                                          
         DROP  R1                                                               
*                                                                               
UPDNO7   CLC   PARAM1,=A(WRTGET)   SET ZERO DA FOR DA FILES                     
         BE    UPDNO7A                                                          
         CLC   PARAM1,=A(WRTAFT)                                                
         BNE   UPDNONO                                                          
UPDNO7A  ICM   RE,15,PARAM5                                                     
         BZ    UPDNONO                                                          
         XC    0(4,RE),0(RE)                                                    
*                                                                               
UPDNONO  LTR   R7,R7               NO UPDATES ALLOWED TO THIS FILE              
         BZ    UPDNONOX                                                         
         OI    TSTAT5,TST5NONO     FLAG TESTED IN TO3270 AND MSG SET            
         OC    TSVCREQ,TSVCREQ                                                  
         BNZ   UPDNONOX                                                         
*                                                                               
UPDNONO1 CLI   TSYS,0              TEST IF CONNECTED TO NORA PROGRAM            
         BE    UPDNONOX                                                         
         TM    TTEST,TTESTUEN      UNLESS CONNECTED WITH U=N                    
         BO    UPDNONOX                                                         
         XR    RF,RF                                                            
         ICM   RF,7,TARSYS         A(CONNECTED SELIST)                          
         BZ    UPDNONOX                                                         
         ICM   RF,15,SEPGMS-SELISTD(RF)                                         
         XR    R0,R0                                                            
         ICM   R0,7,TAPRG          DISPLACEMENT TO CONNECTED PROGRAM            
         BZ    UPDNONOX                                                         
         AR    RF,R0               RF=A(PROGRAM ENTRY)                          
         TM    PGMIND2-PGMLSTD(RF),PGMINORA                                     
         BZ    UPDNONOX            NORMAL PROGRAM HANDLING                      
         ICM   RE,15,=V(SSB)                                                    
         TM    SSBSYSFL-SSBD(RE),X'80'                                          
         BO    UPDNONOX            TEST ADV - NORMAL PROGRAM HANDLING           
         DC    H'0',C'$ABEND'      DIE=PROD - EXIT ON FIRST UPDATE              
*                                                                               
UPDNONOX XMOD1 2                   EXIT RIGHT BACK TO CALLER                    
*                                                                               
UPDNOOKX ICM   RF,15,DATCB         SET FLAG FOR SPECIAL UPDATE ALLOWED          
         BZ    UPD1                                                             
         OI    TCBFLAG4-TCBD(RF),TCB4SUPD                                       
         B     UPD1                                                             
*                                                                               
UPDNOX   ICM   RF,15,DATCB         UPDATES OK - CHECK OWNERSHIP                 
         BZ    *+8                                                              
         NI    TCBFLAG4-TCBD(RF),255-TCB4SUPD                                   
*                                                                               
UPD1     CLI   SENUM,1             FIND SYSTEM IN SYSFLES LIST                  
         BNH   UPDX                                                             
         LLC   R3,SENUM                                                         
         SLL   R3,4                INDEX INTO 16-BYTE SYSTEM TABLE              
         A     R3,=V(SYSTABL)                                                   
         L     R3,DMSYASYF-SYSTABD(R3)                                          
UPD1X    ST    R3,FULL0            SAVE A(SYSFLES LIST HEADER)                  
         USING SYSFLSTD,R3                                                      
*                                                                               
UPD2     TM    SYSFSTAT,SYSFDSP    TEST DATASPACE PROTECTED                     
         BZ    UPD2X                                                            
         TM    SYSFSTAT,SYSFDDSQ   TEST IF DDS ENQUEUED ALREADY                 
         BO    UPDX                                                             
         XC    K1(8),K1            SET TO UPDATIVE IN DSPACE                    
         MVC   K1+3(1),SENUM                                                    
         MVI   K1+1,8                                                           
         GOTO1 =V(LOCKSPC),K1                                                   
         OI    SYSFSTAT,SYSFDDSQ   SET 1ST TIME, AS DDS ENQUED                  
         ICM   R1,15,=V(SSB)                                                    
         BZ    UPD9                                                             
         OC    0(2,R1),0(R1)       TEST OFFLINE                                 
         BZ    UPD9                                                             
         TM    SYSFSTAT,SYSFSGBL   IF LOCAL ONLINE FILES SET GLOBAL             
         BO    UPD5                WE ALSO NEED TO DO AN MVS ENQ                
         B     UPD9                                                             
UPD2X    EQU   *                                                                
*                                                                               
UPD3     CLI   SENUM,X'0A'         SPECIAL CODE FOR CONTROL SYSTEM              
         BNE   UPD3X                                                            
         TM    SYSFSTAT,SYSFQDP    TEST IF DDS ENQ/DEQ PROTECTION               
         BZ    UPD3X                                                            
         TM    P7,X'40'            EXIT IF CALLED BY OPEN                       
         BO    UPDX                                                             
         L     R1,PARAM4           A(DTF)                                       
         LA    R1,0(R1)                                                         
         C     R1,=V(SYS1FLEX)     IGNORE SYS1 FILES                            
         BL    UPDX                                                             
         GOTO1 =V(DMENQCTL),K1,(C'E',=C'CTRL')                                  
         OI    SYSFSTAT,SYSFDDSQ   SET WE HAVE DONE DDS ENQ OF CTRL             
         B     UPDX                                                             
UPD3X    EQU   *                                                                
*                                                                               
UPD5     TM    SYSFSTAT,SYSFMVSQ   IS SYSTEM ALREADY ENQUEUED                   
         BO    UPDX                                                             
         ICM   R1,15,=V(SSB)       OFFLINE ALWAYS DOES MVS ENQ                  
         BZ    UPD5A                                                            
         OC    0(2,R1),0(R1)                                                    
         BZ    UPD5A                                                            
         TM    SSBSTAT4-SSBD(R1),SSBSAOR                                        
         BO    UPD5X               AOR NEVER DOES ENQ                           
*                                                                               
UPD5A    LLC   RE,SENUM            BUILD ENQUEUE ID FROM SENUM                  
         CVD   RE,K1                                                            
         UNPK  K1(4),K1+6(2)                                                    
         OI    K1+3,X'F0'                                                       
         MVC   UPDMIN+4(4),K1                                                   
         XC    K1(12),K1                                                        
         CLC   UPDMAJ(2),=C'00'    TEST USER REQUESTED NO PROTECTION            
         BE    UPD5X                                                            
         ENQ   (UPDMAJ,UPDMIN,E,8,SYSTEM),RET=USE                               
         LTR   RF,RF                                                            
         BZ    UPD5X                                                            
         CLI   3(RF),X'04'         TEST IF RESOURCE NOT AVAIL                   
         BNE   UPD5X                                                            
         TM    P7,X'80'            OFFLINE ERROR CONCURRENT UPDATING            
         BZ    UPDERR0                                                          
         TM    SYSFSTAT,SYSFSGBL   ONLINE OK IF LOCAL SET TO GLOBAL             
         BZ    UPDERR0                                                          
UPD5X    OI    SYSFSTAT,SYSFMVSQ   SET SYSTEM ENQ VIA MVS ENQUEUE               
         DROP  R3                                                               
*                                                                               
UPD9     EQU   *                   FIRST UPDATE ENQUEUE NOW DONE                
*&&STAMP                                                                        
         L     R4,=V(SYSFLES)      CHECK IF FILE OWNED BY CPU                   
         USING SYSFLSTD,R4                                                      
UPD10    CLI   0(R4),X'FF'                                                      
         BE    UPD30                                                            
         MVC   P7+1(1),SYSFSYS#    SAVE SYS NUM                                 
         LH    R0,SYSF#FLS         GET NUMBER OF FILES                          
         LA    R4,SYSFLIST         POINT TO FIRST                               
*                                                                               
UPD12    TM    SYSFIND1,SFCPU      TEST PROTECTED FILE                          
         BZ    UPD12B                                                           
         L     RE,SYSFADTF-1       RE=A(DTF)                                    
         USING DTFPHD,RE                                                        
         LA    RE,0(RE)                                                         
         CR    RB,RE               IGNORE NON LINK EDITED FILES                 
         BH    UPD12B                                                           
         TM    DTFOPEN,DTF_RO      IGNORE READONLY FILES                        
         BO    UPD12B                                                           
         TM    P7,X'C0'                                                         
         BNO   UPD12A                                                           
         CLC   SENUM,P7+1          SYSTEM MATCH FOR ONLINE OPEN                 
         BNE   UPD12B                                                           
         TM    DTFTYPE,DTFTEMU     IGNORE EMULATED FILES                        
         BO    UPD12B                                                           
         MVC   PARAM4+1(3),SYSFADTF SAVE ADDR OF 1ST PROTECTED FILE             
*                                                                               
UPD12A   CLC   PARAM4+1(3),SYSFADTF MATCH DTF ADDRESS                           
         BE    UPD14                                                            
*                                                                               
UPD12B   LA    R4,SYSFLNQ(R4)      LOOP FOR EACH FILE                           
         BCT   R0,UPD12                                                         
         B     UPD10               TRY NEXT SYSTEM                              
         DROP  RE                                                               
*                                                                               
UPD14    XC    P1(20),P1           DA FILE                                      
         MVC   P4,PARAM4                                                        
         TM    SYSFIND1,SFISF      TEST IS FILE                                 
         BO    UPD16                                                            
         MVC   P1,=V(DACPUID)                                                   
         L     RF,=V(DADDS)                                                     
         B     UPD20                                                            
         DROP  R4                                                               
*                                                                               
UPD16    L     R1,PARAM4           IS FILE (DTF)                                
         USING ISDTF,R1                                                         
         MVC   P1,=A(ISCPUID)                                                   
         L     RF,=V(ISDDS)                                                     
         B     UPD20                                                            
*                                                                               
UPD20    LA    R1,P1               GO CHECK CPUID/DSPACE IN RECORD ZERO         
         BASR  RE,RF                                                            
         TM    P3+1,X'80'                                                       
         BZ    UPD20A              CPU ID OK                                    
         CLI   P3+2,0                                                           
         BE    UPDERR2             CPU ID ZERO                                  
         B     UPDERR3             CPU ID WRONG                                 
UPD20A   TM    P3+1,X'10'                                                       
         BZ    UPD30               DSPACE ID OK                                 
         CLI   P3+3,0                                                           
         BE    UPDERR4             DSPACE ID ZERO                               
         B     UPDERR5             DSPACE ID WRONG                              
*&&                                                                             
                                                                                
UPD30    TM    P7,X'80'            RE-SEARCH FOR EOF IF OFFLINE                 
         BO    UPDX                                                             
         BAS   RE,SRCHEOF                                                       
                                                                                
UPDX     TM    K2,X'08'            TEST IF WAIT OCCURRED                        
         XIT1                      EXIT WITH CC SET NON ZERO IF FUNNY           
*                                                                               
UPDERR0  OI    PARAM3+1,X'20'      CONCURRENT FILE UPDATING                     
         LHI   R5,670                                                           
         B     UPDERRX                                                          
*                                                                               
UPDERR1  B     UPDERRX             UPDATE TO READ ONLY FILE (WRNDATA)           
*                                                                               
UPDERR2  OI    PARAM3+1,X'40'                                                   
         MVC   UPDDATAM,=CL25'NO CPU ID STAMP'                                  
         LHI   R5,672                                                           
         B     UPDERRX                                                          
*                                                                               
UPDERR3  OI    PARAM3+1,X'80'                                                   
         MVC   UPDDATAM,=CL25'CPU ID STAMP=X MISMATCH'                          
         MVC   UPDDATAM+13(1),P3+2                                              
         LHI   R5,673                                                           
         B     UPDERRX                                                          
*                                                                               
UPDERR4  OI    PARAM3+1,X'40'                                                   
         MVC   UPDDATAM,=CL25'NO DSPACE STAMP'                                  
         LHI   R5,674                                                           
         B     UPDERRX                                                          
*                                                                               
UPDERR5  OI    PARAM3+1,X'80'                                                   
         MVC   UPDDATAM,=CL25'DSPACE STAMP=X MISMATCH'                          
         MVC   UPDDATAM+13(1),P3+3                                              
         LHI   R5,675                                                           
*                                                                               
UPDERRX  TM    P7,X'C0'            RETURN ERROR FLAG IF ONLINE OPEN             
         BO    UPDX                                                             
         L     R1,PARAM4           GET FILE NAME                                
         MVC   UPDDATAF,DTFDD-DTF(R1)                                           
         LA    R4,WRNF                                                          
         EXTRACT (4),FIELDS=TIOT   GET JOB NAME                                 
         L     R4,WRNF                                                          
         MVC   UPDDATAJ,0(R4)                                                   
         CVD   R5,UPDDUB           GET ABEND CODE                               
         OI    UPDDUB+7,X'0F'                                                   
         UNPK  UPDDATAA+6(3),UPDDUB                                             
         GOTO1 =A(DMOD000),UPDMSG                                               
         LR    R1,R5                                                            
         ABEND (1),DUMP                                                         
                                                                                
UPDDUB   DS    D                                                                
UPDID    DS    0CL16                                                            
UPDMAJ   DC    CL8'DMGR    '                                                    
UPDMIN   DC    CL8'SNUM0000'                                                    
                                                                                
UPDMSG   DC    A(WCTYPE)                                                        
         DC    A(UPDDATA)                                                       
         DC    A(UPDDATAL)                                                      
*                                                                               
UPDDATA  EQU   *                                                                
UPDDATAF DC    CL8' '                                                           
         DC    C' '                                                             
UPDDATAM DC    CL25'CONCURRENT FILE UPDATING '                                  
         DC    C' '                                                             
UPDDATAJ DC    CL8' '                                                           
         DC    C' '                                                             
UPDDATAA DC    CL9'ABEND 670'                                                   
UPDDATAL EQU   *-UPDDATA                                                        
                                                                                
WRNMSG   DC    A(WCTYPE)                                                        
         DC    A(WRNDATA)                                                       
         DC    A(WRNDATAL)                                                      
*                                                                               
WRNF     DC    A(0)                                                             
WRNDATA  EQU   *                                                                
WRNDATAF DC    CL8' '                                                           
         DC    C' '                                                             
         DC    CL25'UPDATE TO READ ONLY FILE '                                  
         DC    C' '                                                             
WRNDATAJ DC    CL8' '                                                           
         DC    C' '                                                             
WRNDATAA DC    CL9'WARNING'                                                     
WRNDATAL EQU   *-WRNDATA                                                        
         EJECT                                                                  
***********************************************************************         
* SEARCH FOR EOF ON ALL UPDATIVE FILES IN A SYSTEM                    *         
* CALLED BY OPENSYS AND BY UPDATE ON FIRST UPDATE                     *         
* FULL0=A(SYSFLES HEADER)                                             *         
* P7=X'80'=ONLINE P7=X'40'=CALLED-BY-OPEN                             *         
* SEARCH FOR EOF ONLY DONE ON FIRST UPDATE IF OFFLINE                 *         
***********************************************************************         
SRCHEOF  NTR1                                                                   
         XC    P1(24),P1           SET PARAM LIST FOR EOF SEARCH                
         MVC   P2,PARAM2           I/O AREA                                     
         MVC   P4,PARAM4           A(DTF)                                       
         LA    R0,P6                                                            
         ST    R0,P5               SET A(DISK ADDRESS)                          
         L     R5,FULL0            R5=A(SYSFLES LIST HEADER)                    
         USING SYSFLSTD,R5                                                      
         LH    R6,SYSF#FLS         R6=NUMBER OF FILES                           
         L     R8,SYSFLIST+(SYSFADTF-SYSFLSTD-1) A(DTF 1ST FILE)                
         USING DTFPHD,R8                                                        
*                                                                               
SEOF1    TM    P7,X'40'            TEST IF CALLED BY OPEN                       
         BZ    SEOF1B                                                           
         TM    DTFFLAG,DTFGLOB     TEST IF FIRST FILE IS GLOBAL                 
         BZ    SEOF1A                                                           
         NI    SYSFSTAT,255-(SYSFMVSQ+SYSFQDP)  MVS AND DDS ENQ OFF             
         OI    SYSFSTAT,SYSFDSP    DATASPACE ON                                 
         B     SEOF1B                                                           
*                                                                               
SEOF1A   TM    SYSFSTAT,SYSFUGBL   TEST SET TO LOCAL FROM GLOBAL                
         BZ    SEOF1B                                                           
         NI    SYSFSTAT,255-SYSFUGBL                                            
         OI    SYSFSTAT,SYSFULCL   SET UPDATIVE LOCAL FILES IN SYSTEM           
SEOF1B   MVC   FULL1,0(R5)         FULL1=SAVED SYSFLES HEADER INFO              
         TM    FULL1+1,SYSFULCL+SYSFUGBL                                        
         BZ    SEOFX               EXIT IF NO UPDATIVE FILES                    
*                                                                               
         LA    R5,SYSFLIST         POINT TO FIRST FILE                          
SEOF2    L     R8,SYSFADTF-1       R8=A(DTF)                                    
         TM    DTFOPEN,DTF_RO+DTF_NOP  IGNORE NOP AND READONLY FILES            
         BNZ   SEOF10                                                           
         TM    SYSFIND1,SFNOEOF    IGNORE NO-EOF-SEARCH FILES                   
         BO    SEOF10                                                           
         BAS   RE,BLDOPCL                                                       
         TM    SYSFIND1,SFISF      TEST IS FILE                                 
         BZ    SEOF4                                                            
*                                                                               
SEOF3    TM    P7,X'80'            IS FILE - TEST ONLINE                        
         BO    SEOF10                                                           
         TM    P7,X'40'            OFFLINE - TEST CALLED BY OPEN                
         BO    SEOF10                                                           
         MVC   P1,=A(ISFNDEOF)     SEARCH EOF ON FIRST OFFLINE UPDATE           
         LA    R1,P1                                                            
         L     RF,=V(ISDDS)                                                     
         BASR  RE,RF                                                            
         B     SEOF10                                                           
*                                                                               
SEOF4    TM    P7,X'80'            DA FILE - TEST ONLINE                        
         BO    *+12                                                             
         TM    P7,X'40'            OFFLINE - TEST CALLED BY OPEN                
         BO    SEOF10                                                           
*                                                                               
SEOF5    TM    DTFFLAG,DTFGLOB     EOF PROCESSING FOR GLOBAL FILE               
         BZ    SEOF6                                                            
         MVC   P1,=V(CHKEOF)       GET EOF POINTERS FROM DATA SPACE             
         LA    R1,P1                                                            
         L     RF,=V(DADDS)                                                     
         BASR  RE,RF                                                            
         B     SEOF8                                                            
*                                                                               
SEOF6    TM    P7,X'40'            TEST IF CALLED BY OPEN                       
         BZ    *+14                                                             
         OC    DNEXT,DNEXT         YES CHECK IF ALREADY FOUND EOF               
         BNZ   SEOF10                                                           
         MVC   DNEXT,=X'00010000'  SET TO TRK1/BLK0 16-BIT TRACK                
SEOF6A   TM    DTFTYPE,DTFTBIGF+DTFTBIG                                         
         BZ    SEOF6C                                                           
         BO    SEOF6B                                                           
         TM    DTFTYPE,DTFTBIGF                                                 
         BO    *+14                                                             
         MVC   DNEXT,=X'00004000'  SET TO TRK1/BLK0 18-BIT TRACK                
         B     SEOF6C                                                           
         MVC   DNEXT,=X'00001000'  SET TO TRK1/BLK0 20-BIT TRACK                
         B     SEOF6C                                                           
SEOF6B   MVC   DNEXT,=X'00000400'  SET TO TRK1/BLK0 22-BIT TRACK                
*                                                                               
SEOF6C   TM    SYSFIND1,SFHDR      TEST FILE HAS HEADER                         
         BZ    SEOF7                                                            
         TM    P7,X'40'            TEST CALLED BY OPEN                          
         BZ    SEOF7                                                            
         BAS   RE,RDHDR            YES READ HEADER RECORD ON OPEN               
         BNZ   SEOF7                                                            
         L     RE,PARAM2           GET A(HEADER)                                
         SR    R0,R0               GET LOW ORDER 16 BITS                        
         ICM   R0,3,92(RE)                                                      
         CLI   94(RE),X'FF'                                                     
         BE    *+8                                                              
         ICM   R0,4,94(RE)         GET HIGH ORDER 8 BITS                        
*                                                                               
         TM    DTFTYPE,DTFTBIG+DTFTBIGF                                         
         BZ    SEOF6E                                                           
         BO    SEOF6D                                                           
         TM    DTFTYPE,DTFTBIGF                                                 
         BO    *+12                                                             
         SLL   R0,32-18            ADJUST TO 18-BIT TRACK ADDRESS               
         B     SEOF6F                                                           
         SLL   R0,32-20            ADJUST TO 20-BIT TRACK ADDRESS               
         B     SEOF6F                                                           
SEOF6D   SLL   R0,32-22            ADJUST TO 22-BIT TRACK ADDRESS               
         B     SEOF6F                                                           
SEOF6E   SLL   R0,32-16            ADJUST TO 16-BIT TRACK ADDRESS               
SEOF6F   ST    R0,DNEXT                                                         
*                                                                               
SEOF7    MVC   P1,=V(FNDEOF)       SEARCH FOR EOF STARTING AT DNEXT             
         MVC   P6,DNEXT                                                         
         LA    R1,P1                                                            
         L     RF,=V(DADDS)                                                     
         BASR  RE,RF                                                            
*                                                                               
SEOF8    TM    SYSFIND1,SFRCV      TEST IF RECOVERY FILE                        
         BZ    SEOF10                                                           
         TM    DTFOPEN,DTF_RO+DTF_NOP TEST NOP OR READONLY FILE                 
         BNZ   SEOF10                                                           
         SR    R0,R0                                                            
         ICM   R0,3,DBLKSZ         TEST IF BLOCKED                              
         BZ    SEOF10                                                           
SEOF8A   TM    DTFTYPE,DTFTBIGF                                                 
         BO    SEOF8B                                                           
         CLC   DNEXT,=X'00010000'  TEST IF EMPTY 16-BIT FILE                    
         BNH   SEOF10                                                           
         B     SEOF8C                                                           
SEOF8B   CLC   DNEXT,=X'00001000'  TEST IF EMPTY 20-BIY FILE                    
         BNH   SEOF10                                                           
SEOF8C   MVC   P1,=V(RDID)         SET TO READ LAST BLOCK                       
         MVC   P2,DBLK                                                          
         XC    P3,P3                                                            
         MVC   P6,DNEXT                                                         
         LA    R1,P1                                                            
         L     RF,=V(DADDS)                                                     
         O     RF,=X'80000000'     SET FOR 31-BIT MODE                          
         BASSM RE,RF                                                            
         OC    P3(2),P3            TEST FOR ERRORS                              
         BZ    SEOF8X                                                           
         L     R1,DBLK             YES SET EMPTY BLOCK                          
         XC    0(6,R1),0(R1)                                                    
SEOF8X   SAM24                                                                  
*                                                                               
SEOF10   LA    R5,SYSFLNQ(R5)      BUMP TO NEXT FILE IN LIST                    
         BCT   R6,SEOF2                                                         
SEOFX    XIT1                                                                   
         DROP  R5,R8                                                            
         EJECT                                                                  
***********************************************************************         
* DMOD000 - EXIT BACK TO MONITOR                                      *         
***********************************************************************         
INHIBITR LA    R0,7                ATTEMPT TO READ A CLOSED FILE                
         B     *+8                                                              
INHIBITW LA    R0,6                ATTEMPT TO UPDATE A QUIESCED FILE            
         ICM   R5,15,=V(SSB)                                                    
         JZ    *+2                 DIE=NO SSB                                   
         OC    0(2,R5),0(R5)                                                    
         JZ    *+2                 DIE=MUST BE ONLINE                           
*                                                                               
         GOTO1 =V(GETTXT),K1,(X'FF',(R0)) SET MESSAGE IN TWA HDR LINE           
*                                                                               
         L     RE,SSBTKADR-SSBD(R5)                                             
         L     RE,TCBWRKA-TCBD(RE) POINT TO START OF W/S CHAIN                  
         L     RD,8(RE)                                                         
         L     RD,8(RD)            FIX RD TO EXIT BACK TO MONITOR               
*                                                                               
         XIT1                                                                   
         EJECT                                                                  
***********************************************************************         
* DMOD000 - LITERAL POOL                                              *         
***********************************************************************         
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
* DATAMGR WORK AREAS                                                  *         
***********************************************************************         
DMGRWORK DSECT                                                                  
         DS    8F                  COVERED BY DMGRFRST BELOW                    
*                                                                               
DMCBW1   DS    F                   COPY OF CALLERS ACTUAL DMCB                  
DMCBW2   DS    F                                                                
DMCBW3   DS    F                                                                
DMCBW4   DS    F                                                                
DMCBW5   DS    F                                                                
DMCBW6   DS    F                                                                
DMCBW7   DS    F                                                                
DMCBW8   DS    F                                                                
*                                                                               
DUB      DS    D                                                                
FULL     DS    F                                                                
SAVER1   DS    A                   A(CALLERS ACTUAL DMCB)                       
SAVER2   DS    A                   FOR TEMPEST                                  
SAVERC   DS    A                                                                
SAVERE   DS    A                                                                
*                                                                               
SVR4     DS    A                   USED FOR GETTTRKS CODE                       
*                                                                               
XINFO    DS    0XL12               EXTRA INFO FOR SOX CONTROLS                  
XFLAG1   DS    X                                                                
XFONLINE EQU   X'80'               ON-LINE                                      
XFOKUPDT EQU   X'40'               OKAY TO UPDATE                               
*                                  04=LNKREQ,02=WRT/REQ,01=ADD/REQ              
XFLAG2   DS    X                                                                
XFLAG3   DS    X                                                                
XFLAG4   DS    X                                                                
XTCBNTRY DS    A                   A(TCB) IF ONLINE                             
XUTLNTRY DS    A                   A(UTL) IF ONLINE                             
*                                                                               
ATRCTAB  DS    A                   I/O TRACE STORAGE                            
TRACETIM DS    F                                                                
TRACEIO  DS    H                                                                
TRACESYS DS    X                                                                
TRACEON  DS    C                                                                
AACTN    DS    A                                                                
AFILE    DS    A                                                                
ACMD     DS    A                                                                
ACTION   DS    X                                                                
INTFILE  DS    X                                                                
EXTFILE  DS    X                                                                
NFSW     DS    X                                                                
DMTSAVE  DS    X                                                                
SVPAGE   DS    X                                                                
SVDM1    DS    X                                                                
SESS     DS    X                                                                
BYTE     DS    X                   BITS USED IN TEMPSTR                         
SYSALL   EQU   X'80'               ALL FILES WITHIN SYSTEM DMOPEN               
*                                                                               
ACTPAGE  DS    X                                                                
SVMAJSYS DS    X                                                                
SVSENUM  DS    X                                                                
DMGRXFLG DS    X                                                                
DEMSTAT  DS    X                                                                
         DS    0H                                                               
SVDATA   DS    CL54                SVDATA NEEDS TO BE ON HALF WORD              
*                                                                               
REPWRK   DS    0F                  INTEREP WORK AREA                            
REPTABA  DS    A                   A(REP TABLE ENTRY)                           
REPRC    DS    H                   REP RETURN CODE 00=OK,FF=SEQ BREAK           
REPWRKL  EQU   *-REPWRK                                                         
*                                                                               
HALF     DS    H                                                                
OLDKEY   DS    0CL48                                                            
*                                                                               
DMWORKH  DS    CL24                                                             
DMSRCH   DS    8F                                                               
DMWORK   EQU   *                   MAX LEN DEFINED BY NMOD.                     
DMWORKX  EQU   OLDKEY+48           REAL END OF DMWORK                           
         EJECT                                                                  
***********************************************************************         
* DMGREQUS - EQUATES FOR DATA MANAGER ROUTINES - MODIFIED             *         
***********************************************************************         
ISREAD   EQU   1                                                                
ISRDSEQ  EQU   2                                                                
ISADD    EQU   3                                                                
ISERASE  EQU   4                                                                
ISWRITE  EQU   5                                                                
ISCPUID  EQU   6                                                                
ISFNDEOF EQU   7                                                                
ISOPEN   EQU   8                                                                
ISCLOSE  EQU   9                                                                
ISOPOLD  EQU   10                                                               
ISRDFLS  EQU   11                                                               
ISRSFLS  EQU   12                                                               
ISCLEAR  EQU   13                                                               
*                                                                               
RDID     EQU   1                                                                
RDIDTRK  EQU   2                                                                
RDTRK    EQU   3                                                                
WTID     EQU   4                                                                
WTCKD    EQU   5                                                                
WTTRK    EQU   6                                                                
DATRNS   EQU   7                                                                
WTERASE  EQU   8                                                                
DABACK   EQU   9                                                                
DACPUID  EQU   10                                                               
FNDEOF   EQU   11                                                               
ADDADR   EQU   12                                                               
ADDAFT   EQU   13                                                               
DAOPEN   EQU   14                                                               
DACLOSE  EQU   15                                                               
DARPT    EQU   16                                                               
DAOPOLD  EQU   17                                                               
RDIDFLS  EQU   18                                                               
DAINFO   EQU   19                                                               
CHKEOF   EQU   20                                                               
DACLEAR  EQU   21                                                               
*                                                                               
DMEXTQ   EQU   1                   ADDED Q TO AVOID DUPLICATE                   
FINDSYSQ EQU   2                   ADDED Q TO AVOID DUPLICATE                   
DMODEXT  EQU   1                                                                
DMODFSYS EQU   2                                                                
DMODRKEY EQU   3                                                                
DMODRSEQ EQU   4                                                                
DMODWKEY EQU   5                                                                
DMODAKEY EQU   6                                                                
DMODEKEY EQU   7                                                                
DMODREAD EQU   8                                                                
DMODRTRK EQU   9                                                                
DMODRTRB EQU   10                                                               
DMODWRI  EQU   11                                                               
DMODWAF  EQU   12                                                               
DMODFLS  EQU   13                                                               
DMODOSYQ EQU   14                  CHANGED S TO Q TO AVOID DUPLICATE            
DMODCSYS EQU   15                                                               
DMODUEXT EQU   16                                                               
         EJECT                                                                  
***********************************************************************         
* DMOD000 - DSECTS FOR WORK/FILES/FACILITIES                          *         
***********************************************************************         
DMWSTR   DSECT                                                                  
P1       DS    F                                                                
P2       DS    F                                                                
P3       DS    F                                                                
P4       DS    F                                                                
P5       DS    F                                                                
P6       DS    F                                                                
P7       DS    F                                                                
P8       DS    F                                                                
*                                                                               
SENUM    DS    0CL1                                                             
AUTL     DS    A                                                                
DATCB    DS    A                                                                
*                                                                               
DDUB     DS    D                                                                
*                                                                               
K1       DS    F                                                                
K2       DS    F                                                                
K3       DS    F                                                                
K4       DS    F                                                                
K5       DS    F                                                                
K6       DS    F                                                                
*                                                                               
LASTBLK  DS    F                                                                
SAVRE    DS    A                                                                
FULL0    DS    F                                                                
FULL1    DS    F                                                                
FULL2    DS    F                                                                
FULL3    DS    F                                                                
DMWSTRX  EQU   *                                                                
                                                                                
DMCBD    DSECT                     CALLER'S DMCB POINTED TO BY R2               
DM1      DS    F                   A(COMMAND)                                   
DM2      DS    F                   A(FILE)                                      
DM3      DS    F                   A(KEY OR DA)                                 
DM4      DS    F                   A(DATA)                                      
DM5      DS    F                   TRM NUMBER/A(WORK AREA)                      
DM6      DS    F                                                                
                                                                                
DMGRFRST DSECT                                                                  
PARAM1   DS    F                   PARAM LIST FOR ACCESS METHODS                
PARAM2   DS    F                                                                
PARAM3   DS    F                                                                
PARAM4   DS    F                                                                
PARAM5   DS    F                                                                
PARAM6   DS    F                                                                
PARAM7   DS    F                                                                
PARAM8   DS    F                                                                
                                                                                
         IEFZB4D0                                                               
         IEFZB4D2                                                               
                                                                                
RECVREC  DSECT                                                                  
*DMRCVRHDR                                                                      
       ++INCLUDE DMRCVRHDR                                                      
RCVHTYP  DS    CL8                 HEADER TYPE **SJOB** OR **EJOB**             
RCVHJOB  DS    CL8                 JOBNAME                                      
RCVHJES  DS    CL8                 JESNAME                                      
RCVHDSN  DS    CL44                DSN                                          
RCVHDSNL EQU   *-RECVREC           HEADER LENGTH WITH DSN                       
                                                                                
RQHHDRD  DSECT                                                                  
*DMREQHDRA                                                                      
         PRINT OFF                                                              
       ++INCLUDE DMREQHDRA                                                      
         PRINT ON                                                               
*DMDTFPH                                                                        
         PRINT OFF                                                              
       ++INCLUDE DMDTFPH                                                        
         PRINT ON                                                               
*DMXTNTD                                                                        
         PRINT OFF                                                              
       ++INCLUDE DMXTNTD                                                        
         PRINT ON                                                               
*DMSYSFD                                                                        
         PRINT OFF                                                              
       ++INCLUDE DMSYSFD                                                        
         PRINT ON                                                               
*DMFILTABD                                                                      
         PRINT OFF                                                              
       ++INCLUDE DMFILTABD                                                      
         PRINT ON                                                               
*DMSYSTABD                                                                      
         PRINT OFF                                                              
       ++INCLUDE DMSYSTABD                                                      
         PRINT ON                                                               
*FATABSTMS                                                                      
         PRINT  OFF                                                             
       ++INCLUDE FATABSTMS                                                      
         PRINT  ON                                                              
*FATABSD                                                                        
         PRINT  OFF                                                             
       ++INCLUDE FATABSD                                                        
         PRINT  ON                                                              
*FATABSCOM                                                                      
         PRINT  OFF                                                             
       ++INCLUDE FATABSCOM                                                      
         PRINT  ON                                                              
*DMDTFIS                                                                        
         PRINT OFF                                                              
       ++INCLUDE DMDTFIS                                                        
         PRINT ON                                                               
*FADSECTS                                                                       
         PRINT OFF                                                              
       ++INCLUDE FADSECTS                                                       
         PRINT ON                                                               
*DDTEMPREC                                                                      
         PRINT OFF                                                              
       ++INCLUDE DDTEMPREC                                                      
                                                                                
*DMDSYSHDR                                                                      
       ++INCLUDE DMDSYSHDR                                                      
                                                                                
*DMDSHDR                                                                        
       ++INCLUDE DMDSHDR                                                        
                                                                                
*DMSPACED                                                                       
       ++INCLUDE DMSPACED                                                       
                                                                                
*FATABSDEQU                                                                     
         PRINT  OFF                                                             
       ++INCLUDE FATABSDEQU                                                     
         PRINT  ON                                                              
*CTGENFILE                                                                      
         PRINT  OFF                                                             
       ++INCLUDE CTGENFILE                                                      
         PRINT  ON                                                              
*DMPRTQW                                                                        
         PRINT  OFF                                                             
       ++INCLUDE DMPRTQW                                                        
         PRINT  ON                                                              
*DMWRKZW                                                                        
         PRINT  OFF                                                             
*PREFIX=Z                                                                       
       ++INCLUDE DMWRKZW                                                        
*PREFIX=                                                                        
         PRINT  ON                                                              
*DMPQWFHDR                                                                      
         PRINT  OFF                                                             
       ++INCLUDE DMPQWFHDR                                                      
         PRINT  ON                                                              
*FAPLO                                                                          
         PRINT  OFF                                                             
       ++INCLUDE FAPLO                                                          
         PRINT  ON                                                              
*FATABSPQ                                                                       
         PRINT  OFF                                                             
       ++INCLUDE FATABSPQ                                                       
         PRINT  ON                                                              
*DDARREDITD                                                                     
         PRINT  OFF                                                             
       ++INCLUDE DDARREDITD                                                     
         PRINT  ON                                                              
*CTBUFFERD                                                                      
         PRINT  OFF                                                             
       ++INCLUDE CTBUFFERD                                                      
         PRINT  ON                                                              
*DDMASTD                                                                        
         PRINT  OFF                                                             
       ++INCLUDE DDMASTD                                                        
         PRINT  ON                                                              
*DMTABFD                                                                        
         PRINT  OFF                                                             
       ++INCLUDE DMTABFD                                                        
         PRINT  ON                                                              
*&&US                                                                           
*DEDEMFILE                                                                      
         PRINT OFF                                                              
       ++INCLUDE DEDEMFILE                                                      
         PRINT ON                                                               
*&&                                                                             
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'052DMDMGR    09/09/20'                                      
         END                                                                    
