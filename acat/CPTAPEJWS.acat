*          DATA SET CPTAPEJWS  AT LEVEL 050 AS OF 05/01/02                      
*CATALP CPTAPEJW                                                                
         TITLE 'CP40 - JWT AGENCY CPP INTERFACE TAPE'                           
*       A(CPWORKD)                                                              
*       A(BUFFALO RECORD)                                                       
CPTAPEJW CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 0,CPTAPEJW                                                       
         L     RA,0(R1)                                                         
         LA    RC,2048(RA)                                                      
         LA    RC,2048(RC)                                                      
         USING CPWORKD,RA,RC                                                    
         L     R2,4(R1)                                                         
         USING CP40D,R2                                                         
         LA    R9,RECORD                                                        
         USING JWTD,R9                                                          
         CLI   0(R1),X'FF'                                                      
         BE    CLOSTAPE                                                         
         CLC   QSTART(12),SVQSTART                                              
         BE    CHKOPN                                                           
         MVC   SVQSTART(12),QSTART                                              
         MVC   MYSTART(4),QSTART                                                
         MVC   MYSTART+4(2),=C'14'                                              
         MVC   MYEND(4),QEND                                                    
         MVC   MYEND+4(2),=C'01'                                                
         GOTO1 ADDAY,DMCB,MYEND,WORK,32                                         
         MVC   MYEND,WORK                                                       
         GOTO1 MOBILE,DMCB,(12,MYSTART),(1,RECORD),0,0                          
         GOTO1 DATCON,DMCB,(2,RECORD),(0,MYSTART)                               
         GOTO1 DATCON,DMCB,(2,RECORD+10),(0,MYEND)                              
CHKOPN   CLI   OPENSW,0                                                         
         BNE   OPENOK                                                           
         XC    RECORD,RECORD                                                    
         BAS   RE,FORMATT                                                       
         CLC   QUESTOR(7),=C'NO*TAPE'                                           
         BE    NTOPN                                                            
         GOTO1 =V(DYNALLOC),DMCB,DDM1,DSNM1                                     
         OPEN  (OUT,(OUTPUT))                                                   
NTOPN    MVI   OPENSW,1                                                         
         B     OPENOK                                                           
DDM1     DC    CL8'OUT'                                                         
DSNM1    DC    CL20'CPPTAPE.CP04TJW1'                                           
         DS    0H                  ALIGN IT                                     
         SPACE 2                                                                
OPENOK   CLI   CPHDR,1                                                          
         BNE   EXIT                                                             
         CLC   CPDPT,=C'22'                                                     
         BE    WRREC                                                            
         PACK  DUB,QSELECT         GET DEMO NAME                                
         L     RF,ADDEMBUF                                                      
         SR    R1,R1                                                            
         CVB   R1,DUB                                                           
         BCTR  R1,0                                                             
         MH    R1,=H'7'                                                         
         AR    R1,RF                                                            
         MVC   JWDEMO,0(R1)        SEND TO OUTPUT                               
         MVI   JWMED,C'T'                                                       
         SR    RE,RE                                                            
         ICM   RE,3,MARKET                                                      
         CVD   RE,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  JWMKT(4),DUB+5(3)                                                
*                                                                               
         MVC   JWQSTART,MYSTART    SET RECORD DATES                             
         MVC   JWQEND,MYEND                                                     
         MVC   JWYR(2),=C'19'                                                   
         MVC   JWYR+2(2),MYEND                                                  
         PACK  DUB,MYEND+2(2)                                                   
         CVB   R1,DUB                                                           
         BCTR  R1,0                                                             
         LA    R1,QTRTAB(R1)                                                    
         MVC   JWQTR(1),0(R1)                                                   
         OI    JWQTR,X'F0'                                                      
*                                                                               
         ZIC   RF,NEXTBUC          SET DATA SLOT                                
         LA    RE,JWDPLN                                                        
         MR    RE,RE                                                            
         LA    RF,JWDPBUC(RF)                                                   
         USING JWDPD,RF                                                         
*                                                                               
         LA    RE,DPTTAB           MOVE IN DATA                                 
RC2      CLI   0(RE),X'FF'                                                      
         BNE   *+6                                                              
         DC    H'0'                                                             
         CLC   CPDPT,0(RE)                                                      
         BE    *+12                                                             
         LA    RE,5(RE)                                                         
         B     RC2                                                              
         MVC   JWDPDP,2(RE)                                                     
*                                                                               
         ICM   RE,15,CPSPT                                                      
         CVD   RE,DUB                                                           
         MVC   JWDPSPT,DUB+5       SPOTS                                        
         SR    R0,R0               GET AVERAGE DOLLARS                          
         ICM   R1,15,CPDOL                                                      
         M     R0,=F'200'          ADD PENNY PRECISION                          
         DR    R0,RE                                                            
         LTR   R1,R1                                                            
         BM    *+8                                                              
         AH    R1,=H'1'                                                         
         SRA   R1,1                                                             
         CVD   R1,DUB                                                           
         MVC   JWDPADOL,DUB+3                                                   
*                                                                               
         SR    R0,R0               GET AVERAGE POINTS                           
         ICM   R1,15,CPRTG                                                      
         SLDA  R0,1                                                             
         DR    R0,RE                                                            
         LTR   R1,R1                                                            
         BM    *+8                                                              
         AH    R1,=H'1'                                                         
         SRA   R1,1                                                             
         CVD   R1,DUB                                                           
         MVC   JWDPARTG,DUB+6                                                   
         ZIC   RF,NEXTBUC                                                       
         LA    RF,1(RF)                                                         
         STC   RF,NEXTBUC                                                       
         B     EXIT                                                             
*                                                                               
WRREC    CLI   NEXTBUC,0                                                        
         BE    EXIT                                                             
         L     R3,=A(OUT)                                                       
         CLC   QUESTOR(7),=C'NO*TAPE'                                           
         BE    NTWRREC                                                          
         PUT   (R3),RECORD                                                      
         B     TWRREC                                                           
***********************************FOR TESTING*************                     
NTWRREC  MVC   P1(130),RECORD                             *                     
*        GOTO1 HEXOUT,DMCB,RECORD,P2,60,0,0               *                     
*        GOTO1 HEXOUT,DMCB,RCD+60,P3,60,0,0               *                     
*        GOTO1 HEXOUT,DMCB,RCD+120,P4,60,0,0              *                     
         GOTO1 REPORT                                     *                     
         SPACE 2                                                                
TWRREC   MVI   NEXTBUC,0                                                        
         BAS   RE,FORMATT                                                       
         B     EXIT                                                             
*                                                                               
CLOSTAPE CLC   QUESTOR(7),=C'NO*TAPE'                                           
         BE    EXIT                                                             
         CLOSE (OUT)                                                            
         B     EXIT                                                             
*                                                                               
FORMATT  LA    R1,14               FORMAT FOR COBOL                             
         LA    RF,JWDPBUC                                                       
FORMATT2 MVC   JWDPDP,=C'   '                                                   
         MVC   JWDPSPT,=PL3'0'                                                  
         MVC   JWDPARTG,=PL2'0'                                                 
         MVC   JWDPADOL,=PL5'0'                                                 
         LA    RF,JWDPLN(RF)                                                    
         BCT   R1,FORMATT2                                                      
         BR    RE                                                               
*                                                                               
EXIT     XMOD1 1                                                                
         LTORG                                                                  
         EJECT                                                                  
DPTTAB   DC    C'02',C'EAM'        DAYPART TABLE                                
         DC    C'04',C'DAY'                                                     
         DC    C'06',C'EFR'                                                     
         DC    C'08',C'ENW'                                                     
         DC    C'10',C'ACC'                                                     
         DC    C'12',C'PRI'                                                     
         DC    C'13',C'IPR'                                                     
         DC    C'14',C'LFR'                                                     
         DC    C'16',C'LNW'                                                     
         DC    C'18',C'KID'                                                     
         DC    C'20',C'SPO'                                                     
         DC    X'FF'                                                            
*                                                                               
QTRTAB   DC    AL1(1,1,1,2,2,2,3,3,3,4,4,4)                                     
*                                                                               
NEXTBUC  DC    X'00'                                                            
OPENSW   DC    X'00'                                                            
SVQSTART DS    CL12                                                             
MYSTART  DS    CL6                                                              
MYEND    DS    CL6                                                              
RCD      DS    0C                                                               
RECORD   DS    CL(JWTLN)                                                        
OUT      DCB   DDNAME=OUT,                                             X        
               DSORG=PS,                                               X        
               RECFM=FB,                                               X        
               LRECL=00211,                                            X        
               BLKSIZE=02110,                                          X        
               MACRF=PM                                                         
         EJECT                                                                  
CP40D    DSECT                     DSECT FOR CP40 BUFFALO RECS                  
CPHDR    DS    CL5                 HEADER                                       
CPDPT    DS    CL2                 DAYPART NUMBER                               
CPSPT    DS    XL4                 SPOTS                                        
CPDOL    DS    XL4                 DOLLARS                                      
CPRTG    DS    XL4                 RATINGS                                      
CPIMP    DS    XL4                 IMPRESSIONS                                  
         SPACE 2                                                                
JWTD     DSECT                                                                  
JWMED    DS    CL1                 MEDIA                                        
JWDEMO   DS    CL7                 DEMO                                         
JWMKT    DS    CL4                 MARKET                                       
JWYR     DS    CL4                 YEAR                                         
JWQTR    DS    CL1                 QUARTER                                      
JWQSTART DS    CL6                 QUARTER START DATE                           
JWQEND   DS    CL6                 QUARTER END DATE                             
JWDPBUC  DS    14CL13              DAYPART DATA (JWDPD)                         
JWTLN    EQU   *-JWMED                                                          
         SPACE 2                                                                
JWDPD    DSECT                     DSECT FOR DAYPART DATA                       
JWDPDP   DS    CL3                 DAYPART                                      
JWDPSPT  DS    CL3                 SPOTS     9(5)    COMP-3                     
JWDPARTG DS    CL2                 AVG. RTG. 9(2)V9  COMP-3                     
JWDPADOL DS    CL5                 AVG. DOL. 9(7)V99 COMP-3                     
JWDPLN   EQU   *-JWDPDP                                                         
       ++INCLUDE CPREPWORKD                                                     
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'050CPTAPEJWS 05/01/02'                                      
         END                                                                    
