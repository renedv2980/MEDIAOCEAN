*          DATA SET AXROUTS    AT LEVEL 057 AS OF 04/30/10                      
****************************                                                    
*CATALP AXROUTS                                                                 
         TITLE 'EXTRACT ROUTINES FOR AXTRACT'                                   
         PRINT NOGEN                                                            
AXROUTS  CSECT                                                                  
         ENTRY AXADRC              ADJUSTED RATE                                
         ENTRY AXCALC              CALENDAR                                     
         ENTRY AXCLIC              CLIENT                                       
         ENTRY AXCPYC              COMPANY                                      
         ENTRY AXCSAC              COSTING ACCOUNT                              
         ENTRY AXDEPC              DEPARTMENT                                   
         ENTRY AXDEGC              DEPARTMENT GROUP                             
         ENTRY AXINAC              INCOME ACCOUNT                               
         ENTRY AXISAC              INCOME SUSPENSE ACCOUNT                      
         ENTRY AXJOBC              JOB                                          
         ENTRY AXLEDC              LEDGER                                       
         ENTRY AXLESC              LEDGER STRUCTURE                             
         ENTRY AXLOCC              LOCALITY                                     
         ENTRY AXLCIC              LOCALITY INFORMATION                         
         ENTRY AXMEDC              MEDIA                                        
         ENTRY AXMGRC              MEDIA GROUP                                  
         ENTRY AXNCTC              NON-CLIENT TIME                              
         ENTRY AXOFFC              OFFICE                                       
         ENTRY AXOFLC              OFFICE LIST                                  
         ENTRY AXOLEC              OFFICE LIST ENTRY                            
         ENTRY AXOGRC              OFFICE GROUP                                 
         ENTRY AXOPTC              OPTIONS                                      
         ENTRY AXPEDC              PERIOD                                       
         ENTRY AXPERC              PERSON                                       
         ENTRY AXPEAC              PERSON ASSIGNMENT                            
         ENTRY AXPROC              PRODUCT                                      
         ENTRY AXRATC              RATE                                         
         ENTRY AXSTHC              STANDARD HOURS                               
         ENTRY AXSBDC              SUB-DEPARTMENT                               
         ENTRY AXP1RC              PERSON1R                                     
         ENTRY AXTSKC              TASK                                         
         ENTRY AXTISC              TIMESHEET                                    
         ENTRY AXTALC              TIMESHEET ALLOCATION LINE                    
         ENTRY AXTGRC              WORKCODE GROUP                               
         ENTRY AXPOFC              PRODUCTION OFFICE                            
         ENTRY AXEDTC              EDIT HOURS                                   
         ENTRY AXCAPC              COST ALLOCATION PROFILE                      
         ENTRY AXPHIC              PAYROLL HISTORY                              
         ENTRY AXDTMC              DAILY TIME                                   
         ENTRY AXBOSC              BRAND OCEAN STATUS                           
*                                                                               
EXIT     XMOD1 ,                                                                
         EJECT                                                                  
***********************************************************************         
* AXADRC - EXTRACT ADJUSTED RATE RECORDS FOR AXTRACT                  *         
*                                                                     *         
* AL4 A(EXTRACT RECORD)                                               *         
* AL4 A(ACCFILE RECORD)                                               *         
* AL1 0=BUILD 'K' RECORD, 1=BUILD ALL RECORDS, 2=PASS BACK NEXT       *         
*     X'FF' RETURNED IF NO MORE RECORDS TO PROCESS                    *         
* AL3 0                                                               *         
* AL4 A(SXDTABD)                                                      *         
***********************************************************************         
         SPACE 1                                                                
AXADRC   NMOD1 WORKL,*AXADRC*,CLEAR=Y                                           
         USING WORKD,RC                                                         
         L     RA,AXADRLIT                                                      
         USING LITERAL,RA                                                       
         ST    R1,APARM                                                         
         MVC   PARM,0(R1)          EXTRACT PARAMETER LIST                       
         LM    R2,R3,0(R1)                                                      
         L     R4,12(R1)                                                        
         USING AXADRD,R2           R2=A(EXTRACT RECORD)                         
         USING PAJRECD,R3          R3=A(ACCFILE RECORD)                         
         USING SXDTABD,R4          R4=A(SXDXTAB)                                
*                                                                               
         CLI   8(R1),1                                                          
         BL    AXADRKIL            BUILD KILL RECORD                            
         BE    AXADRSER            BUILD SERIES OF RECORDS                      
         B     AXADRNXT            OUTPUT NEXT RECORD                           
*                                                                               
AXADRLIT DC    A(LITERAL)                                                       
         EJECT                                                                  
***********************************************************************         
* GET NEXT ADJUSTED RATE RECORD IN SERIES AND RETURN IT               *         
***********************************************************************         
         SPACE 1                                                                
AXADRNXT L     RE,ANEXT                                                         
         OC    0(8,RE),0(RE)       RECORD TO PASS BACK?                         
         JNZ   *+12                                                             
         MVI   8(R1),X'FF'         NO MORE RECORDS                              
         J     EXIT                                                             
*                                                                               
         LA    R0,AXADRD           RETURN BUFFER                                
         L     RE,ANEXT            A(NEXT RECORD IN SERIES)                     
         LH    RF,=Y(AXADRDL)      LENGTH OF ADJUSTED RATE RECORD               
         LR    RF,R1                                                            
         MVCL  R0,RE                                                            
*                                                                               
         L     RE,ANEXT            GO ON TO NEXT BLOCK IN BUFFER                
         LH    RF,=Y(AXADRDL)                                                   
         AR    RE,RF                                                            
         ST    RE,ANEXT                                                         
         J     EXIT                                                             
         SPACE 1                                                                
***********************************************************************         
* BUILD 'K' RECORD FOR ADJUSTED RATE RECORD AND EXIT                  *         
***********************************************************************         
         SPACE 1                                                                
AXADRKIL BRAS  RE,AXADRBLK                                                      
         J     EXIT                                                             
         SPACE 1                                                                
***********************************************************************         
* BUILD SERIES OF RECORDS FOR THIS RECORD                             *         
***********************************************************************         
         SPACE 1                                                                
AXADRSER BRAS  RE,AXADRBLK         SET UP BLANK RECORD (KEY DETAIL)             
*                                                                               
         LH    RF,=Y(AXADRDL)      LENGTH OF AN ADR RECORD                      
         STH   RF,RECLEN                                                        
         L     R2,AXADRCC          WHERE RECORDS TO BE BUILT                    
         ST    R2,ANEXT            FOR NEXT TIME IN                             
         XC    0(8,R2),0(R2)                                                    
         LA    R6,PAJRFST          FIRST ONE RECORD DATA                        
*                                                                               
AXADRS02 CLI   0(R6),0                                                          
         JE    EXIT                                                             
         CLI   0(R6),TCIELQ                                                     
         JE    AXADRS06                                                         
*                                                                               
AXADRS04 XR    R1,R1                                                            
         IC    R1,1(R6)                                                         
         AR    R6,R1                                                            
         B     AXADRS02                                                         
*                                                                               
AXADRS06 L     RE,AXREC            BLANK RECORD                                 
         LR    R0,R2                                                            
         LH    RF,RECLEN           LENGTH OF A BLANK RECORD                     
         LR    R1,RF                                                            
         MVCL  R0,RE               COPY IN BLANK RECORD                         
*                                                                               
         LH    RF,RECLEN                                                        
         AR    RF,R2                                                            
         XC    0(8,RF),0(RF)       BLANK OFF START OF NEXT ONE                  
*                                                                               
         USING TCIELD,R6                                                        
         EDIT  TCIAMT,(10,AXADRPCT),ZERO=NOBLANK,FILL=0,FLOAT=-                 
         GOTO1 VDATCON,PLIST,(1,TCIDTE),(10,AXADRDTE),0                         
*&&US                                                                           
         XC    AXADRDTE(2),AXADRDTE+3 SWAP MM AND DD ABOUT                      
         XC    AXADRDTE+3(2),AXADRDTE                                           
         XC    AXADRDTE(2),AXADRDTE+3                                           
*&&                                                                             
         TR    AXADRDTE,MXTRT      TRANSLATE DATE                               
         AH    R2,RECLEN           NEXT IN BUFFER                               
         B     AXADRS04                                                         
         EJECT                                                                  
***********************************************************************         
* BUILD BLANK RECORD AND INPUT KEY FIELDS                             *         
***********************************************************************         
         SPACE 1                                                                
AXADRBLK XC    AXADRLEN,AXADRLEN   RECORD LENGTH/TYPE/ENDOFREC                  
         MVC   AXADRLEN(2),=Y(AXADRDL)                                          
         MVC   AXADRTYP,AXDADRQ                                                 
         MVI   AXADRDX,MXTRTQ                                                   
         MVI   AXADRDX+1,MXTRTQ                                                 
         MVI   AXADRCCD-1,MXTRTQ   2-BYTE AGY ALPHA                             
         MVI   AXADRHST-1,MXTRTQ   HOST KEY                                     
         MVI   AXADRCOC-1,MXTRTQ   CLIENT OFFICE                                
         MVI   AXADRCLI-1,MXTRTQ   CLIENT                                       
         MVI   AXADRPRO-1,MXTRTQ   PRODUCT                                      
         MVI   AXADRJOB-1,MXTRTQ   JOB                                          
         MVI   AXADRPOC-1,MXTRTQ   PERSON OFFICE CODE                           
         MVI   AXADRDPT-1,MXTRTQ   DEPARTMENT                                   
         MVI   AXADRSBD-1,MXTRTQ   SUB DEPARTMENT                               
         MVI   AXADRCSP-1,MXTRTQ   STAFF                                        
         MVI   AXADRTSK-1,MXTRTQ   TASK                                         
         MVI   AXADRDTE-1,MXTRTQ   EFFECTIVE DATE                               
         MVI   AXADRPCT-1,MXTRTQ   PERCENT                                      
*                                                                               
         MVC   AXADRCCD,SXDTAGY    AGENCY 2-BYTE ALPHA                          
         TR    AXADRCCD,MXTRT                                                   
         MVC   AXADRHST,PAJKOFF    HOST KEY FROM FILE                           
         TR    AXADRHST,MXTRT                                                   
         MVC   AXADRCOC,PAJKOFF    CLIENT OFFICE                                
         TR    AXADRCOC,MXTRT                                                   
         MVC   AXADRCLI,SPACES                                                  
         MVC   AXADRCLI(L'PAJKCLI),PAJKCLI    CLIENT                            
         TR    AXADRCLI,MXTRT                                                   
         MVC   AXADRPRO,SPACES                                                  
         MVC   AXADRPRO(L'PAJKPRO),PAJKPRO    PRODUCT                           
         TR    AXADRPRO,MXTRT                                                   
         MVC   AXADRJOB,SPACES                                                  
         MVC   AXADRJOB(L'PAJKJOB),PAJKJOB    JOB                               
         TR    AXADRJOB,MXTRT                                                   
         MVC   AXADRPOC,SPACES                                                  
         MVC   AXADRPOC(L'PAJKDOF),PAJKDOF    PERSON OFFICE                     
         TR    AXADRPOC,MXTRT                                                   
         MVC   AXADRDPT,SPACES                                                  
         MVC   AXADRDPT(L'PAJKDEP),PAJKDEP    DEPARTMENT                        
         TR    AXADRDPT,MXTRT                                                   
         MVC   AXADRSBD,SPACES                                                  
         MVC   AXADRSBD(L'PAJKSUB),PAJKSUB    SUB-DEPT                          
         TR    AXADRSBD,MXTRT                                                   
         MVC   AXADRCSP,SPACES                                                  
         MVC   AXADRCSP(L'PAJKSTF),PAJKSTF    STAFF CODE                        
         TR    AXADRCSP,MXTRT                                                   
         MVC   AXADRTSK,PAJKTSK    TASK                                         
         TR    AXADRTSK,MXTRT                                                   
         BR    RE                                                               
*                                                                               
         DROP  R2,R3,R4,R6                                                      
*                                                                               
AXADRCC  DC    A(BUFFER1)          ADJUSTED RATE RECORD SAVE AREA               
         EJECT                                                                  
***********************************************************************         
* AXCALC - EXTRACT CALENDAR RECORDS FOR AXTRACT                       *         
*                                                                     *         
* AL4 A(EXTRACT RECORD)                                               *         
* AL4 A(ACCFILE RECORD)                                               *         
* AL4 0=ACCFILE-TO-EXTRACT                                            *         
* AL4 A(SXDTABD)                                                      *         
***********************************************************************         
         SPACE 1                                                                
AXCALC   NMOD1 WORKL,*AXCALC*,CLEAR=Y                                           
         USING WORKD,RC                                                         
         L     RA,AXCALLIT                                                      
         USING LITERAL,RA                                                       
         ST    R1,APARM                                                         
         MVC   PARM,0(R1)          EXTRACT PARAMETER LIST                       
         LM    R2,R3,0(R1)                                                      
         L     R4,12(R1)                                                        
         USING AXCALD,R2           R2=A(EXTRACT RECORD)                         
         USING CASRECD,R3          R3=A(ACCFILE RECORD)                         
         USING SXDTABD,R4          R4=A(SXDTABD)                                
*                                                                               
         XC    AXCALLEN,AXCALLEN   RECORD LENGTH/TYPE/ENDOFREC                  
         MVC   AXCALLEN(2),=AL2(AXCALDL)                                        
         MVC   AXCALTYP,AXDCALQ                                                 
         MVI   AXCALDX,MXTRTQ                                                   
         MVI   AXCALDX+1,MXTRTQ                                                 
         MVI   AXCALOFC-1,MXTRTQ                                                
         MVI   AXCALEYC-1,MXTRTQ                                                
         MVI   AXCALSYC-1,MXTRTQ                                                
         MVI   AXCALCCD-1,MXTRTQ                                                
*                                                                               
         MVC   AXCALOFC,CASKOFC    OFFICE CODE                                  
         TR    AXCALOFC,MXTRT                                                   
         XR    R6,R6                                                            
         ICM   R6,2,CASKEMOA       END YY                                       
         C     R6,=A(Y2000)        YEAR 2000+                                   
         BL    *+8                 NO                                           
         S     R6,=A(Y2000)        ZERO BASE IT                                 
         ICM   R6,1,CASKEMOA+1     END YYMM                                     
         SRDL  R6,4                MXXXXXXX                                     
         SRL   R7,4                0MXXXXXX                                     
         SRDL  R6,4                M0MXXXXX                                     
         SRL   R7,4                0M0MXXXX                                     
         SRDL  R6,4                M0M0MXXX                                     
         SRL   R7,4                0M0M0MXX                                     
         SRDL  R6,4                M0M0M0MX                                     
         SRL   R7,4                0M0M0M0M                                     
         STCM  R7,15,AXCALEYC                                                   
         OC    AXCALEYC,=C'0000'   END MONTH                                    
*                                                                               
         XR    R6,R6                                                            
         ICM   R6,2,CASKSMOA       START YY                                     
         C     R6,=A(Y2000)        YEAR 2000+                                   
         BL    *+8                 NO                                           
         S     R6,=A(Y2000)        ZERO BASE IT                                 
         ICM   R6,1,CASKSMOA+1     START YYMM                                   
         SRDL  R6,4                MXXXXXXX                                     
         SRL   R7,4                0MXXXXXX                                     
         SRDL  R6,4                M0MXXXXX                                     
         SRL   R7,4                0M0MXXXX                                     
         SRDL  R6,4                M0M0MXXX                                     
         SRL   R7,4                0M0M0MXX                                     
         SRDL  R6,4                M0M0M0MX                                     
         SRL   R7,4                0M0M0M0M                                     
         STCM  R7,15,AXCALSYC                                                   
         OC    AXCALSYC,=C'0000'   START MONTH                                  
         MVC   AXCALCCD,SXDTAGY                                                 
         TR    AXCALCCD,MXTRT                                                   
         J     EXIT                                                             
*                                                                               
AXCALLIT DC    A(LITERAL)                                                       
         EJECT                                                                  
***********************************************************************         
* AXCLIC - EXTRACT CLIENT CODES RECORDS FOR AXTRACT                   *         
*                                                                     *         
* AL4 A(EXTRACT RECORD)                                               *         
* AL4 A(ACCFILE RECORD)                                               *         
* AL4 0=ACCFILE-TO-EXTRACT                                            *         
* AL4 A(SXDTABD) - CAN GET 2 BYTE ALPHA FROM THIS...                  *         
* AL4 A(LEVELS) - CAN GET LENGTHS OF LEVELS FROM HERE                 *         
***********************************************************************         
         SPACE 1                                                                
AXCLIC   NMOD1 WORKL,*AXCLIC*,CLEAR=Y                                           
         USING WORKD,RC                                                         
         L     RA,AXCLILIT                                                      
         USING LITERAL,RA                                                       
         ST    R1,APARM                                                         
         MVC   PARM,0(R1)          EXTRACT PARAMETER LIST                       
         MVC   ALEVELS,16(R1)                                                   
         LM    R2,R3,0(R1)                                                      
         L     R4,12(R1)                                                        
         USING AXCLID,R2           R2=A(EXTRACT RECORD)                         
         USING ACTKEY,R3           R3=A(ACCFILE RECORD)                         
         USING SXDTABD,R4          R5=A(SXDTABD)                                
*                                                                               
         XC    AXCLILEN,AXCLILEN   RECORD LENGTH/TYPE/ENDOFREC                  
         MVC   AXCLILEN(2),=Y(AXCLIDL)                                          
         MVC   AXCLITYP,AXDCLIQ                                                 
         MVI   AXCLIDX,MXTRTQ                                                   
         MVI   AXCLIDX+1,MXTRTQ                                                 
         MVI   AXCLICCD-1,MXTRTQ   2-BYTE AGY ALPHA                             
         MVI   AXCLICLC-1,MXTRTQ   CLIENT CODE                                  
         MVI   AXCLINAM-1,MXTRTQ   CLIENT NAME                                  
         MVI   AXCLIF1-1,MXTRTQ    ACCOUNT FILTER 1                             
         MVI   AXCLIF2-1,MXTRTQ    ACCOUNT FILTER 2                             
         MVI   AXCLIF3-1,MXTRTQ    ACCOUNT FILTER 3                             
         MVI   AXCLIF4-1,MXTRTQ    ACCOUNT FILTER 4                             
         MVI   AXCLIF5-1,MXTRTQ    ACCOUNT FILTER 5                             
         MVI   AXCLIOFC-1,MXTRTQ   OFFICE CODE                                  
         MVI   AXCLICAC-1,MXTRTQ   COSTING ACCOUNT CODE                         
         MVI   AXCLILCK-1,MXTRTQ   ACCOUNT IS LOCKED                            
*                                                                               
         MVC   AXCLICCD,SXDTAGY    2-BYTE AGY ALPHA                             
         TR    AXCLICCD,MXTRT                                                   
         MVC   AXCLICLC,SPACES     CLIENT                                       
         L     R6,ALEVELS                                                       
         XR    RF,RF                                                            
         IC    RF,0(R6)                                                         
         BCTR  RF,0                                                             
         MVC   AXCLICLC(0),ACTKACT                                              
         EX    RF,*-6                                                           
         TR    AXCLICLC,MXTRT                                                   
*                                                                               
         LA    R5,ACTRFST          R5=A(FIRST ELEMENT)                          
AXCLI02  CLI   0(R5),0             GET ELEMENT FROM RECORD                      
         JE    EXIT                                                             
         CLI   0(R5),NAMELQ        NAME ELEMENT                                 
         JE    AXCLI06                                                          
         CLI   0(R5),RSTELQ        RECORD STATUS ELEMENT                        
         JE    AXCLI08                                                          
         CLI   0(R5),PPRELQ                                                     
         JE    AXCLI12                                                          
*                                                                               
AXCLI04  XR    RF,RF               BUMP TO NEXT ELEMENT                         
         ICM   RF,1,1(R5)                                                       
         JZ    EXIT                EXIT IF FUNNY ELEMENT LENGTH                 
         AR    R5,RF                                                            
         J     AXCLI02                                                          
*                                                                               
         USING NAMELD,R5                                                        
AXCLI06  MVC   AXCLINAM,SPACES                                                  
         XR    RF,RF                                                            
         IC    RF,NAMLN                                                         
         SH    RF,=H'3'                                                         
         BM    AXCLI04                                                          
         MVC   AXCLINAM(0),NAMEREC                                              
         EX    RF,*-6                                                           
         TR    AXCLINAM,MXTRT                                                   
         B     AXCLI04                                                          
*                                                                               
         USING RSTELD,R5                                                        
AXCLI08  MVC   AXCLIF1,RSTFILT1   ACCOUNT FILTER 1                              
         TR    AXCLIF1,MXTRT                                                    
         MVC   AXCLIF2,RSTFILT2   ACCOUNT FILTER 2                              
         TR    AXCLIF2,MXTRT                                                    
         MVC   AXCLIF3,RSTFILT3   ACCOUNT FILTER 3                              
         TR    AXCLIF3,MXTRT                                                    
         MVC   AXCLIF4,RSTFILT4   ACCOUNT FILTER 4                              
         TR    AXCLIF4,MXTRT                                                    
         MVC   AXCLIF5,RSTFILT5   ACCOUNT FILTER 5                              
         TR    AXCLIF5,MXTRT                                                    
         MVI   AXCLILCK,C'N'                                                    
         TM    RSTSTAT1,RSTSACIL   ACCOUNT IS LOCKED                            
         JO    AXCLI10                                                          
         CLI   RSTLN,RSTLN3Q       ENSURE EL LONG ENOUGH                        
         JL    AXCLI04                                                          
         TM    RSTSTAT5,RSTSNOTS   EXCLUDE FROM TEMPO                           
         JZ    AXCLI04                                                          
*                                                                               
AXCLI10  MVI   AXCLILCK,C'Y'                                                    
         B     AXCLI04                                                          
*                                                                               
         USING PPRELD,R5                                                        
AXCLI12  MVC   AXCLIOFC,PPRGAOFF  OFFICE CODE                                   
         TR    AXCLIOFC,MXTRT                                                   
         MVC   AXCLICAC,PPRCOSTA  COSTING ACCOUNT CODE                          
         TR    AXCLICAC,MXTRT                                                   
         J     AXCLI04                                                          
*                                                                               
AXCLILIT DC    A(LITERAL)                                                       
*                                                                               
         DROP  R2,R3,R4,R5                                                      
         EJECT                                                                  
***********************************************************************         
* AXCPYC - EXTRACT COMPANY INFORMATION FOR AXTRACT                    *         
*                                                                     *         
* AL4 A(EXTRACT RECORD)                                               *         
* AL4 A(ACCFILE RECORD)                                               *         
* AL4 0=ACCFILE-TO-EXTRACT                                            *         
* AL4 A(SXDTABD)                                                      *         
***********************************************************************         
         SPACE 1                                                                
AXCPYC   NMOD1 WORKL,*AXCPYC*,CLEAR=Y                                           
         USING WORKD,RC                                                         
         L     RA,AXCPYLIT                                                      
         USING LITERAL,RA                                                       
         ST    R1,APARM                                                         
         MVC   PARM,0(R1)          EXTRACT PARAMETER LIST                       
         LM    R2,R3,0(R1)                                                      
         L     R4,12(R1)                                                        
         USING AXCPYD,R2           R2=A(EXTRACT RECORD)                         
         USING ACTKEY,R3           R3=A(ACCFILE RECORD)                         
         USING SXDTABD,R4         R3=A(SXDXTABD)                                
*                                                                               
         XC    AXCPYLEN,AXCPYLEN   RECORD LENGTH/TYPE/ENDOFREC                  
         MVC   AXCPYLEN(2),=Y(AXCPYDL)                                          
         MVC   AXCPYTYP,AXDCPYQ                                                 
         MVI   AXCPYDX,MXTRTQ                                                   
         MVI   AXCPYDX+1,MXTRTQ                                                 
         MVI   AXCPYCCD-1,MXTRTQ   AGENCY ALPHA                                 
         MVI   AXCPYCPN-1,MXTRTQ   COMPANY NAME                                 
         MVI   AXCPYTMS-1,MXTRTQ   TMS STATE                                    
         MVI   AXCPYCGC-1,MXTRTQ   COMPANY GROUP CODE                           
         MVI   AXCPYLSF-1,MXTRTQ   ENABLE LEDGER SECURITY FLAG                  
         MVI   AXCPYOSF-1,MXTRTQ   ENABLE OFFICE SECURITY FLAG                  
         MVI   AXCPYLYR-1,MXTRTQ   MOA LOCK YEAR                                
         MVI   AXCPYLMN-1,MXTRTQ   MOA LOCK MONTH                               
*                                                                               
         MVC   AXCPYCCD,SXDTAGY    AGENCY ALPHA                                 
         TR    AXCPYCCD,MXTRT                                                   
*                                                                               
         LA    R5,ACTRFST          R5=A(FIRST ELEMENT)                          
AXCPY02  CLI   0(R5),0             GET ELEMENT FROM RECORD                      
         JE    EXIT                                                             
         CLI   0(R5),NAMELQ        NAME ELEMENT                                 
         JE    AXCPY06                                                          
         CLI   0(R5),CPYELQ        COMPANY ELEMENT                              
         JE    AXCPY08                                                          
         CLI   0(R5),MSLELQ        MOS LOCK ELEMENT                             
         JE    AXCPY12                                                          
*                                                                               
AXCPY04  XR    RF,RF               BUMP TO NEXT ELEMENT                         
         ICM   RF,1,1(R5)                                                       
         JZ    EXIT                EXIT IF FUNNY ELEMENT LENGTH                 
         AR    R5,RF                                                            
         J     AXCPY02                                                          
*                                                                               
         USING NAMELD,R5                                                        
AXCPY06  MVC   AXCPYCPN,SPACES     COMPANY NAME                                 
         XR    RF,RF                                                            
         IC    RF,NAMLN                                                         
         SH    RF,=H'3'                                                         
         JM    AXCPY04                                                          
         MVC   AXCPYCPN(0),NAMEREC                                              
         EX    RF,*-6                                                           
         TR    AXCPYCPN,MXTRT                                                   
         J     AXCPY04                                                          
*                                                                               
         USING CPYELD,R5                                                        
AXCPY08  MVI   AXCPYTMS,C'N'                                                    
         CLI   CPYLN,CPYLN2Q                                                    
         JL    AXCPY10                                                          
         TM    CPYSTAT7,CPYSTMSY   COMPANY TMS STATE                            
         JZ    *+8                                                              
         MVI   AXCPYTMS,C'Y'                                                    
*                                                                               
AXCPY10  MVI   AXCPYCGC,C'N'                                                    
         MVI   AXCPYEHF,C'N'                                                    
         CLI   CPYLN,CPYLN3Q                                                    
         JL    AXCPY04                                                          
         TM    CPYSTAT9,CPYSEDHO   RECOGNIZE EDIT HOURS                         
         JZ    *+12                                                             
         MVI   AXCPYCGC,C'Y'                                                    
         MVI   AXCPYEHF,C'Y'                                                    
         J     AXCPY04                                                          
*                                                                               
         USING MSLELD,R5                                                        
AXCPY12  XR    RF,RF                                                            
         IC    RF,MSLNUM           N'SUB-ELEMENTS                               
         LA    RE,MSLVALS                                                       
         USING MSLVALS,RE                                                       
*                                                                               
AXCPY14  CLI   MSLVTYP,0           ALL (ALWAYS COMES BEFORE TYPE 49)            
         BE    *+12                                                             
         CLI   MSLVTYP,49          TIME SHEETS USE TYPE 49                      
         JNE   AXCPY16                                                          
*                                                                               
         MVI   AXCPYLSF,C'N'       ENABLE LEDGER SECURITY FLAG                  
         TM    MSLVSEC,MSLVSSEC                                                 
         JZ    *+8                                                              
         MVI   AXCPYLSF,C'Y'                                                    
         MVI   AXCPYOSF,C'N'       ENABLE OFFICE SECURITY FLAG                  
         TM    MSLVSEC,MSLVSOFF                                                 
         JZ    *+8                                                              
         MVI   AXCPYOSF,C'Y'                                                    
         MVI   AXCPYLMN,C'0'       LOCK MONTH                                   
         MVC   AXCPYLMN+1(1),MSLVMON                                            
         CLI   MSLVMON,C'A'                                                     
         JNE   *+10                                                             
         MVC   AXCPYLMN,=C'10'                                                  
         CLI   MSLVMON,C'B'                                                     
         JNE   *+10                                                             
         MVC   AXCPYLMN,=C'11'                                                  
         CLI   MSLVMON,C'C'                                                     
         JNE   *+10                                                             
         MVC   AXCPYLMN,=C'12'                                                  
         TM    MSLVSEC,MSLVSXTN    TEST LOCK YEAR IS AROUND                     
         JZ    AXCPY16                                                          
         ZAP   DUB,=P'0'           CVT PWOS TO EBCDIC                           
         OI    DUB+7,X'0F'                                                      
         MVO   DUB,MSLVYEAR                                                     
         UNPK  AXCPYLYR,DUB                                                     
*                                                                               
AXCPY16  TM    MSLVSEC,MSLVSXTN    TEST EXTENDED SUB-EL LENGTH                  
         JZ    *+12                                                             
         LA    RE,L'MSLVALS2(RE)   DIFFERENT SUB-EL LENGTH                      
         J     *+8                                                              
         LA    RE,L'MSLVALS(RE)                                                 
         BCT   RF,AXCPY14                                                       
         J     AXCPY04                                                          
         DROP  RE                                                               
*                                                                               
AXCPYLIT DC    A(LITERAL)                                                       
*                                                                               
         DROP  R2,R3,R4,R5                                                      
         EJECT                                                                  
***********************************************************************         
* AXCSAC - EXTRACT COSTING ACCOUNT DETAILS FOR AXTRACT                *         
*                                                                     *         
* AL4 A(EXTRACT RECORD)                                               *         
* AL4 A(ACCFILE RECORD)                                               *         
* AL4 0=ACCFILE-TO-EXTRACT                                            *         
* AL4 A(SXDTABD) - CAN GET 2 BYTE ALPHA FROM THIS...                  *         
***********************************************************************         
         SPACE 1                                                                
AXCSAC   NMOD1 WORKL,*AXCSAC*,CLEAR=Y                                           
         USING WORKD,RC                                                         
         L     RA,AXCSALIT                                                      
         USING LITERAL,RA                                                       
         ST    R1,APARM                                                         
         MVC   PARM,0(R1)          EXTRACT PARAMETER LIST                       
         LM    R2,R3,0(R1)                                                      
         L     R4,12(R1)                                                        
         USING AXCSAD,R2           R2=A(EXTRACT RECORD)                         
         USING ACTKEY,R3           R3=A(ACCFILE RECORD)                         
         USING SXDTABD,R4          R4=A(SXDTABD)                                
*                                                                               
         XC    AXCSALEN,AXCSALEN   RECORD LENGTH/TYPE/ENDOFREC                  
         MVC   AXCSALEN(2),=Y(AXCSADL)                                          
         MVC   AXCSATYP,AXDCSAQ                                                 
         MVI   AXCSADX,MXTRTQ                                                   
         MVI   AXCSADX+1,MXTRTQ                                                 
         MVI   AXCSACCD-1,MXTRTQ   2-BYTE AGY ALPHA                             
         MVI   AXCSACAC-1,MXTRTQ   COSTING ACCOUNT CODE                         
         MVI   AXCSANAM-1,MXTRTQ   INCOME ACCOUNT NAME                          
         MVI   AXCSALCK-1,MXTRTQ   ACCOUNT IS LOCKED                            
         MVI   AXCSAI1-1,MXTRTQ    PRODUCT REQUIRED                             
         MVI   AXCSAI2-1,MXTRTQ    JOB REQUIRED                                 
*                                                                               
         MVC   AXCSACCD,SXDTAGY    2-BYTE AGY ALPHA                             
         TR    AXCSACCD,MXTRT                                                   
         MVC   AXCSACAC,ACTKACT    INCOME ACCOUNT CODE                          
         TR    AXCSACAC,MXTRT                                                   
*                                                                               
         LA    R5,ACTRFST          R5=A(FIRST ELEMENT)                          
AXCSA02  CLI   0(R5),0             GET ELEMENT FROM RECORD                      
         JE    EXIT                                                             
         CLI   0(R5),NAMELQ        NAME ELEMENT                                 
         JE    AXCSA06                                                          
         CLI   0(R5),RSTELQ        NAME ELEMENT                                 
         JE    AXCSA08                                                          
*                                                                               
AXCSA04  XR    RF,RF               BUMP TO NEXT ELEMENT                         
         ICM   RF,1,1(R5)                                                       
         JZ    EXIT                EXIT IF FUNNY ELEMENT LENGTH                 
         AR    R5,RF                                                            
         B     AXCSA02                                                          
*                                                                               
         USING NAMELD,R5                                                        
AXCSA06  MVC   AXCSANAM,SPACES     ACCOUNT NAME                                 
         XR    RF,RF                                                            
         IC    RF,NAMLN                                                         
         SH    RF,=H'3'                                                         
         BM    AXCSA04                                                          
         MVC   AXCSANAM(0),NAMEREC                                              
         EX    RF,*-6                                                           
         TR    AXCSANAM,MXTRT                                                   
         B     AXCSA04                                                          
*                                                                               
         USING RSTELD,R5                                                        
AXCSA08  MVI   AXCSALCK,C'N'                                                    
         TM    RSTSTAT1,RSTSACIL   ACCOUNT IS LOCKED                            
         JZ    *+8                                                              
         MVI   AXCSALCK,C'Y'                                                    
         MVI   AXCSAI1,C'N'                                                     
         MVI   AXCSAI2,C'N'                                                     
         CLI   RSTLN,RSTLN3Q       ENSURE ELEMENT LONG ENOUGH                   
         JL    AXCSA04                                                          
*                                                                               
         TM    RSTSTAT5,RSTSPROD   PRODUCT REQUIRED                             
         JZ    *+8                                                              
         MVI   AXCSAI1,C'Y'                                                     
         TM    RSTSTAT5,RSTSPRJB   PRODUCT/JOB REQUIRED                         
         JZ    *+8                                                              
         MVI   AXCSAI2,C'Y'                                                     
         J     AXCSA04                                                          
*                                                                               
AXCSALIT DC    A(LITERAL)                                                       
*                                                                               
         DROP  R2,R3,R4,R5                                                      
         EJECT                                                                  
***********************************************************************         
* AXDEPC - EXTRACT DEPATMENT DETAILS FOR AXTRACT                      *         
*                                                                     *         
* AL4 A(EXTRACT RECORD)                                               *         
* AL4 A(ACCFILE RECORD)                                               *         
* AL4 0=ACCFILE-TO-EXTRACT                                            *         
* AL4 A(SXDTABD) - CAN GET 2 BYTE ALPHA FROM THIS...                  *         
* AL4 A(LEVELS) - CAN GET LENGTH OF EACH SUB-LEVEL FROM THESE         *         
***********************************************************************         
         SPACE 1                                                                
AXDEPC   NMOD1 WORKL,*AXDEPC*,CLEAR=Y                                           
         USING WORKD,RC                                                         
         L     RA,AXDEPLIT                                                      
         USING LITERAL,RA                                                       
         ST    R1,APARM                                                         
         MVC   PARM,0(R1)          EXTRACT PARAMETER LIST                       
         MVC   ALEVELS,16(R1)      SAVE LENGTHS                                 
         LM    R2,R3,0(R1)                                                      
         L     R4,12(R1)                                                        
         USING AXDEPD,R2           R2=A(EXTRACT RECORD)                         
         USING ACTKEY,R3           R3=A(ACCFILE RECORD)                         
         USING SXDTABD,R4          R5=A(SXDTABD)                                
*                                                                               
         XC    AXDEPLEN,AXDEPLEN   RECORD LENGTH/TYPE/ENDOFREC                  
         MVC   AXDEPLEN(2),=Y(AXDEPDL)                                          
         MVC   AXDEPTYP,AXDDEPQ                                                 
         MVI   AXDEPDX,MXTRTQ                                                   
         MVI   AXDEPDX+1,MXTRTQ                                                 
         MVI   AXDEPCCD-1,MXTRTQ   2-BYTE AGY ALPHA                             
         MVI   AXDEPOFC-1,MXTRTQ   OFFICE CODE                                  
         MVI   AXDEPDEC-1,MXTRTQ   DEPARTMENT CODE                              
         MVI   AXDEPNAM-1,MXTRTQ   DEPARTMENT NAME                              
         MVI   AXDEPANC-1,MXTRTQ   ANALYSIS CODE                                
         MVI   AXDEPIAC-1,MXTRTQ   INCOME ACCOUNT CODE                          
         MVI   AXDEPTC-1,MXTRTQ    TASK CODE                                    
         MVI   AXDEPLCK-1,MXTRTQ   ACCOUNT IS LOCKED                            
         MVI   AXDEPI1-1,MXTRTQ    PRODUCT REQUIRED                             
         MVI   AXDEPI2-1,MXTRTQ    JOB REQUIRED                                 
*                                                                               
         MVC   AXDEPCCD,SXDTAGY    2-BYTE AGY ALPHA                             
         TR    AXDEPCCD,MXTRT                                                   
         MVC   AXDEPOFC,SPACES     OFFICE CODE                                  
         L     R6,ALEVELS                                                       
         XR    RF,RF                                                            
         IC    RF,0(R6)                                                         
         BCTR  RF,0                                                             
         MVC   AXDEPOFC(0),ACTKACT                                              
         EX    RF,*-6                                                           
         TR    AXDEPOFC,MXTRT                                                   
*                                                                               
         LA    RF,ACTKACT+1(RF)    A(DEPARTMENT CODE)                           
         MVC   AXDEPDEC,SPACES     DEPARTMENT CODE                              
         XR    RE,RE                                                            
         IC    RE,1(R6)                                                         
         BCTR  RE,0                                                             
         MVC   AXDEPDEC(0),0(RF)                                                
         EX    RE,*-6                                                           
         TR    AXDEPDEC,MXTRT                                                   
*                                                                               
         LA    R5,ACTRFST          R5=A(FIRST ELEMENT)                          
AXDEP02  CLI   0(R5),0             GET ELEMENT FROM RECORD                      
         JE    EXIT                                                             
         CLI   0(R5),NAMELQ        NAME ELEMENT                                 
         JE    AXDEP06                                                          
         CLI   0(R5),RSTELQ        RECORD STATUS ELEMENT                        
         JE    AXDEP08                                                          
         CLI   0(R5),SPAELQ        SPAEL                                        
         JE    AXDEP10                                                          
*                                                                               
AXDEP04  XR    RF,RF               BUMP TO NEXT ELEMENT                         
         ICM   RF,1,1(R5)                                                       
         JZ    EXIT                EXIT IF FUNNY ELEMENT LENGTH                 
         AR    R5,RF                                                            
         J     AXDEP02                                                          
*                                                                               
         USING NAMELD,R5                                                        
AXDEP06  MVC   AXDEPNAM,SPACES     ACCOUNT NAME                                 
         XR    RF,RF                                                            
         IC    RF,NAMLN                                                         
         SH    RF,=H'3'                                                         
         BM    AXDEP04                                                          
         MVC   AXDEPNAM(0),NAMEREC                                              
         EX    RF,*-6                                                           
         TR    AXDEPNAM,MXTRT                                                   
         B     AXDEP04                                                          
*                                                                               
         USING RSTELD,R5                                                        
*&&UK                                                                           
AXDEP08  MVC   AXDEPANC,RSTANAL    ANALYSIS CODE                                
*&&                                                                             
*&&US                                                                           
AXDEP08  MVC   AXDEPANC,RSTCOSTG   ANALYSIS CODE                                
*&&                                                                             
         TR    AXDEPANC,MXTRT                                                   
         MVI   AXDEPLCK,C'N'                                                    
         TM    RSTSTAT1,RSTSACIL   ACCOUNT IS LOCKED                            
         JZ    *+8                                                              
         MVI   AXDEPLCK,C'Y'                                                    
         MVI   AXDEPI1,C'N'                                                     
         MVI   AXDEPI2,C'N'                                                     
         CLI   RSTLN,RSTLN3Q       ENSURE ELEMENT LONG ENOUGH                   
         JL    AXDEP04                                                          
*                                                                               
         MVC   AXDEPTC,RSTDFTSK    TASK CODE                                    
         TR    AXDEPTC,MXTRT                                                    
         TM    RSTSTAT5,RSTSPROD   PRODUCT REQUIRED                             
         JZ    *+8                                                              
         MVI   AXDEPI1,C'Y'                                                     
         TM    RSTSTAT5,RSTSPRJB   PRODUCT/JOB REQUIRED                         
         JZ    *+8                                                              
         MVI   AXDEPI2,C'Y'                                                     
         J     AXDEP04                                                          
*                                                                               
         USING SPAELD,R5                                                        
AXDEP10  CLI   SPATYPE,SPATINCO                                                 
         JNE   AXDEP04                                                          
         MVC   AXDEPIAC,SPAAULA   INCOME ANALYSIS ACCOUNT                       
         TR    AXDEPIAC,MXTRT                                                   
         J     AXDEP04                                                          
*                                                                               
AXDEPLIT DC    A(LITERAL)                                                       
*                                                                               
         DROP  R2,R3,R4,R5                                                      
         EJECT                                                                  
***********************************************************************         
* AXDEGC - EXTRACT DEPARTMENT GROUP RECORDS FOR AXTRACT               *         
*                                                                     *         
* AL4 A(EXTRACT RECORD)                                               *         
* AL4 A(ACCFILE RECORD)                                               *         
* AL4 0=ACCFILE-TO-EXTRACT                                            *         
* AL4 A(SXDTABD)                                                      *         
***********************************************************************         
         SPACE 1                                                                
AXDEGC   NMOD1 WORKL,*AXDEGC*,CLEAR=Y                                           
         USING WORKD,RC                                                         
         L     RA,AXDEGLIT                                                      
         USING LITERAL,RA                                                       
         ST    R1,APARM                                                         
         MVC   PARM,0(R1)          EXTRACT PARAMETER LIST                       
         LM    R2,R3,0(R1)                                                      
         L     R4,12(R1)                                                        
         USING AXDEGD,R2           R2=A(EXTRACT RECORD)                         
         USING ACTRECD,R3          R3=A(ACCFILE RECORD)                         
         USING SXDTABD,R4          R4=A(SXDXTABD)                               
*                                                                               
         XC    AXDEGLEN,AXDEGLEN   RECORD LENGTH/TYPE/ENDOFREC                  
         MVC   AXDEGLEN(2),=Y(AXDEGDL)                                          
         MVC   AXDEGTYP,AXDDEGQ                                                 
         MVI   AXDEGDX,MXTRTQ                                                   
         MVI   AXDEGDX+1,MXTRTQ                                                 
         MVI   AXDEGCCD-1,MXTRTQ   AGENCY ALPHA                                 
         MVI   AXDEGDGC-1,MXTRTQ   GROUP CODE                                   
         MVI   AXDEGNAM-1,MXTRTQ   DEPARTMENT GROUP NAME                        
*                                                                               
         MVC   AXDEGCCD,SXDTAGY    AGENCY ALPHA                                 
         TR    AXDEGCCD,MXTRT                                                   
         MVC   AXDEGDGC,ACTKACT    GROUP CODE                                   
         TR    AXDEGDGC,MXTRT                                                   
*                                                                               
         XR    RF,RF                                                            
         LA    R5,ACTRFST          R5=A(FIRST ELEMENT)                          
AXDEG02  CLI   0(R5),0             GET ELEMENT FROM RECORD                      
         JE    EXIT                                                             
         CLI   0(R5),NAMELQ        NAME ELEMENT                                 
         JE    AXDEG04                                                          
         ICM   RF,1,1(R5)                                                       
         JZ    EXIT                EXIT IF FUNNY ELEMENT LENGTH                 
         AR    R5,RF                                                            
         J     AXDEG02                                                          
*                                                                               
         USING NAMELD,R5                                                        
AXDEG04  MVC   AXDEGNAM,SPACES     DEPARTMENT GROUP NAME                        
         XR    RF,RF                                                            
         IC    RF,NAMLN                                                         
         SH    RF,=H'3'                                                         
         JM    EXIT                                                             
         MVC   AXDEGNAM(0),NAMEREC                                              
         EX    RF,*-6                                                           
         TR    AXDEGNAM,MXTRT                                                   
         J     EXIT                                                             
*                                                                               
AXDEGLIT DC    A(LITERAL)                                                       
*                                                                               
         DROP  R2,R3,R4,R5                                                      
         EJECT                                                                  
***********************************************************************         
* AXINAC - EXTRACT INCOME ACCOUNT RECORDS FOR AXTRACT                 *         
*                                                                     *         
* AL4 A(EXTRACT RECORD)                                               *         
* AL4 A(ACCFILE RECORD)                                               *         
* AL4 0=ACCFILE-TO-EXTRACT                                            *         
* AL4 A(SXDTABD) - CAN GET 2 BYTE ALPHA FROM THIS...                  *         
* AL4 A(LEVELS) - CAN GET LENGTH OF EACH SUB-LEVEL FROM THESE         *         
***********************************************************************         
         SPACE 1                                                                
AXINAC   NMOD1 WORKL,*AXINAC*,CLEAR=Y                                           
         USING WORKD,RC                                                         
         L     RA,AXINALIT                                                      
         USING LITERAL,RA                                                       
         ST    R1,APARM                                                         
         MVC   PARM,0(R1)          EXTRACT PARAMETER LIST                       
         LM    R2,R3,0(R1)                                                      
         L     R4,12(R1)                                                        
         USING AXINAD,R2           R2=A(EXTRACT RECORD)                         
         USING ACTKEY,R3           R3=A(ACCFILE RECORD)                         
         USING SXDTABD,R4          R5=A(SXDTABD)                                
*                                                                               
         XC    AXINALEN,AXINALEN   RECORD LENGTH/TYPE/ENDOFREC                  
         MVC   AXINALEN(2),=Y(AXINADL)                                          
         MVC   AXINATYP,AXDINAQ                                                 
         MVI   AXINADX,MXTRTQ                                                   
         MVI   AXINADX+1,MXTRTQ                                                 
         MVI   AXINACCD-1,MXTRTQ   2-BYTE AGY ALPHA                             
         MVI   AXINAIAC-1,MXTRTQ   INCOME ACCOUNT CODE                          
         MVI   AXINANAM-1,MXTRTQ   INCOME ACCOUNT NAME                          
         MVI   AXINALCK-1,MXTRTQ   LOCKED                                       
         MVI   AXINAACD-1,MXTRTQ   ANALYSIS CODE                                
         MVC   AXINAACD,SPACES                                                  
         XC    COSTG,COSTG                                                      
*                                                                               
         MVC   AXINACCD,SXDTAGY    2-BYTE AGY ALPHA                             
         TR    AXINACCD,MXTRT                                                   
         MVC   AXINAIAC,ACTKACT    INCOME ACCOUNT CODE                          
         TR    AXINAIAC,MXTRT                                                   
         MVI   AXINALCK,C'N'       LOCKED STATUS                                
         TM    ACTRSTAT,ACTSLOCK                                                
         BZ    *+8                                                              
         MVI   AXINALCK,C'Y'                                                    
*                                                                               
         XR    RF,RF                                                            
         LA    R5,ACTRFST          R5=A(FIRST ELEMENT)                          
AXINA02  CLI   0(R5),0             GET ELEMENT FROM RECORD                      
         JE    AXINA10                                                          
         CLI   0(R5),NAMELQ        NAME ELEMENT                                 
         JE    AXINA04                                                          
         CLI   0(R5),RSTELQ                                                     
         JE    AXINA06                                                          
         CLI   0(R5),SPAELQ                                                     
         JNE   AXINA03                                                          
         CLI   SPATYPE-SPAELD(R5),SPATANAL                                      
         JE    AXINA08                                                          
*                                                                               
AXINA03  XR    RF,RF                                                            
         ICM   RF,1,1(R5)                                                       
         JZ    EXIT                EXIT IF FUNNY ELEMENT LENGTH                 
         AR    R5,RF                                                            
         J     AXINA02                                                          
*                                                                               
         USING NAMELD,R5                                                        
AXINA04  MVC   AXINANAM,SPACES     ACCOUNT NAME                                 
         XR    RF,RF                                                            
         IC    RF,NAMLN                                                         
         SH    RF,=H'3'                                                         
         JM    AXINA03                                                          
         MVC   AXINANAM(0),NAMEREC                                              
         EX    RF,*-6                                                           
         TR    AXINANAM,MXTRT                                                   
         J     AXINA03                                                          
*                                                                               
         USING RSTELD,R5                                                        
AXINA06  MVC   COSTG,RSTCOSTG      SAVE FOR LATER                               
         J     AXINA03                                                          
*                                                                               
         USING SPAELD,R5                                                        
AXINA08  MVC   AXINAACD,SPAAANAL                                                
         J     AXINA03                                                          
*                                                                               
AXINA10  CLC   AXINAACD,SPACES                                                  
         JH    EXIT                                                             
         OC    COSTG,COSTG                                                      
         JZ    EXIT                                                             
         MVC   AXINAACD(1),COSTG                                                
         J     EXIT                                                             
*                                                                               
AXINALIT DC    A(LITERAL)                                                       
*                                                                               
         DROP  R2,R3,R4,R5                                                      
         EJECT                                                                  
***********************************************************************         
* AXISAC - EXTRACT INCOME SUSPENSE ACCOUNT DETAILS FOR AXTRACT        *         
*                                                                     *         
* AL4 A(EXTRACT RECORD)                                               *         
* AL4 A(ACCFILE RECORD)                                               *         
* AL4 0=ACCFILE-TO-EXTRACT                                            *         
* AL4 A(SXDTABD)                                                      *         
***********************************************************************         
         SPACE 1                                                                
AXISAC   NMOD1 WORKL,*AXISAC*,CLEAR=Y                                           
         USING WORKD,RC                                                         
         L     RA,AXISALIT                                                      
         USING LITERAL,RA                                                       
         ST    R1,APARM                                                         
         MVC   PARM,0(R1)          EXTRACT PARAMETER LIST                       
         MVC   ALEVELS,16(R1)      SAVE LENGTHS                                 
         LM    R2,R3,0(R1)                                                      
         L     R4,12(R1)                                                        
         USING AXISAD,R2           R2=A(EXTRACT RECORD)                         
         USING ACTKEY,R3           R3=A(ACCFILE RECORD)                         
         USING SXDTABD,R4          R5=A(SXDTABD)                                
*                                                                               
         XC    AXISALEN,AXISALEN   RECORD LENGTH/TYPE/ENDOFREC                  
         MVC   AXISALEN(2),=Y(AXISADL)                                          
         MVC   AXISATYP,AXDISAQ                                                 
         MVI   AXISADX,MXTRTQ                                                   
         MVI   AXISADX+1,MXTRTQ                                                 
         MVI   AXISACCD-1,MXTRTQ   2-BYTE AGY ALPHA                             
         MVI   AXISAIAC-1,MXTRTQ   INCOME ACCOUNT CODE                          
         MVI   AXISANAM-1,MXTRTQ   INCOME ACCOUNT NAME                          
         MVI   AXISALCK-1,MXTRTQ   LOCKED                                       
*                                                                               
         MVC   AXISACCD,SXDTAGY    2-BYTE AGY ALPHA                             
         TR    AXISACCD,MXTRT                                                   
         MVC   AXISAIAC,ACTKACT    ACCOUNT CODE                                 
         TR    AXISAIAC,MXTRT                                                   
         MVI   AXISALCK,C'N'       ACCOUNT STATUS                               
         TM    ACTRSTAT,ACTSLOCK                                                
         BZ    *+8                                                              
         MVI   AXISALCK,C'Y'                                                    
*                                                                               
         LA    R5,ACTRFST          R5=A(FIRST ELEMENT)                          
AXISA02  CLI   0(R5),0             GET ELEMENT FROM RECORD                      
         JE    EXIT                                                             
         CLI   0(R5),NAMELQ        NAME ELEMENT                                 
         JE    AXISA04                                                          
         XR    RF,RF               BUMP TO NEXT ELEMENT                         
         ICM   RF,1,1(R5)                                                       
         JZ    EXIT                EXIT IF FUNNY ELEMENT LENGTH                 
         AR    R5,RF                                                            
         J     AXISA02                                                          
*                                                                               
         USING NAMELD,R5                                                        
AXISA04  MVC   AXISANAM,SPACES     ACCOUNT NAME                                 
         XR    RF,RF                                                            
         IC    RF,NAMLN                                                         
         SH    RF,=H'3'                                                         
         JM    EXIT                                                             
         MVC   AXISANAM(0),NAMEREC                                              
         EX    RF,*-6                                                           
         TR    AXISANAM,MXTRT                                                   
         J     EXIT                                                             
*                                                                               
AXISALIT DC    A(LITERAL)                                                       
*                                                                               
         DROP  R2,R3,R4,R5                                                      
         EJECT                                                                  
***********************************************************************         
* AXJOBC - EXTRACT JOB RECORDS FOR AXTRACT                            *         
*                                                                     *         
* AL4 A(EXTRACT RECORD)                                               *         
* AL4 A(ACCFILE RECORD)                                               *         
* AL4 0=ACCFILE-TO-EXTRACT                                            *         
* AL4 A(SXDTABD) - CAN GET 2 BYTE ALPHA FROM THIS...                  *         
* AL4 A(LEVELS) - LENGTH OF INDIVIDUAL SUB LEVELS                     *         
***********************************************************************         
         SPACE 1                                                                
AXJOBC   NMOD1 WORKL,*AXJOBC*,CLEAR=Y                                           
         USING WORKD,RC                                                         
         L     RA,AXJOBLIT                                                      
         USING LITERAL,RA                                                       
         ST    R1,APARM                                                         
         MVC   PARM,0(R1)          EXTRACT PARAMETER LIST                       
         LM    R2,R3,0(R1)                                                      
         L     R4,12(R1)                                                        
         MVC   ALEVELS,16(R1)                                                   
         USING AXJOBD,R2           R2=A(EXTRACT RECORD)                         
         USING ACTRECD,R3          R3=A(ACCFILE RECORD)                         
         USING SXDTABD,R4          R4=A(SXDTABD)                                
*                                                                               
         XC    AXJOBLEN,AXJOBLEN   RECORD LENGTH/TYPE/ENDOFREC                  
         MVC   AXJOBLEN(2),=Y(AXJOBDL)                                          
         MVC   AXJOBTYP,AXDJOBQ                                                 
         MVI   AXJOBDX,MXTRTQ                                                   
         MVI   AXJOBDX+1,MXTRTQ                                                 
         SPACE 1                                                                
*                                  BUILD DELIMETERS                             
         SPACE 1                                                                
         MVI   AXJOBCCD-1,MXTRTQ   2-BYTE AGY ALPHA                             
         MVI   AXJOBCLI-1,MXTRTQ   CLIENT                                       
         MVI   AXJOBPRO-1,MXTRTQ   PRODUCT                                      
         MVI   AXJOBJOB-1,MXTRTQ   JOB CODE                                     
         MVI   AXJOBNAM-1,MXTRTQ   JOB NAME                                     
         MVI   AXJOBF1-1,MXTRTQ    ACCOUNT FILTER 1                             
         MVI   AXJOBF2-1,MXTRTQ    ACCOUNT FILTER 2                             
         MVI   AXJOBF3-1,MXTRTQ    ACCOUNT FILTER 3                             
         MVI   AXJOBF4-1,MXTRTQ    ACCOUNT FILTER 4                             
         MVI   AXJOBF5-1,MXTRTQ    ACCOUNT FILTER 5                             
         MVI   AXJOBOD-1,MXTRTQ    JOB OPEN DATE                                
         MVI   AXJOBCD-1,MXTRTQ    JOB CLOSE DATE                               
         MVI   AXJOBIRA-1,MXTRTQ   IS RATE ADJUSTED                             
         MVI   AXJOBIWO-1,MXTRTQ   IS WRITE OFF                                 
         MVI   AXJOBSTA-1,MXTRTQ   STATUS                                       
         MVI   AXJOBOFC-1,MXTRTQ   OFFICE CODE                                  
         MVI   AXJOBCAC-1,MXTRTQ   COSTING ACCOUNT CODE                         
*                                                                               
         MVC   JOBSTAT,ACTRSTAT    SAVE STATUS FOR LATER                        
*                                                                               
         MVC   AXJOBCCD,SXDTAGY    2-BYTE AGY ALPHA                             
         TR    AXJOBCCD,MXTRT                                                   
         MVC   AXJOBCLI,SPACES     CLIENT                                       
         L     R6,ALEVELS                                                       
         XR    RF,RF                                                            
         IC    RF,0(R6)                                                         
         BCTR  RF,0                                                             
         MVC   AXJOBCLI(0),ACTKACT                                              
         EX    RF,*-6                                                           
         TR    AXJOBCLI,MXTRT                                                   
         LA    RF,ACTKACT+1(RF)                                                 
*                                                                               
         MVC   AXJOBPRO,SPACES     PRODUCT                                      
         XR    RE,RE                                                            
         IC    RE,1(R6)                                                         
         BCTR  RE,0                                                             
         MVC   AXJOBPRO(0),0(RF)                                                
         EX    RE,*-6                                                           
         TR    AXJOBPRO,MXTRT                                                   
         LA    RF,1(RE,RF)         RF HOLDS A(JOB) NOW                          
*                                                                               
         MVC   AXJOBJOB,SPACES     JOB CODE                                     
         IC    RE,2(R6)                                                         
         BCTR  RE,0                                                             
         MVC   AXJOBJOB(0),0(RF)                                                
         EX    RE,*-6                                                           
         TR    AXJOBJOB,MXTRT                                                   
*                                                                               
         LA    R5,ACTRFST          R5=A(FIRST ELEMENT)                          
AXJOB02  CLI   0(R5),0             GET ELEMENT FROM RECORD                      
         JE    EXIT                                                             
         CLI   0(R5),NAMELQ        NAME ELEMENT                                 
         JE    AXJOB06                                                          
         CLI   0(R5),RSTELQ        RECORD STATUS ELEMENT                        
         JE    AXJOB08                                                          
         CLI   0(R5),JOBELQ                                                     
         JE    AXJOB10                                                          
         CLI   0(R5),PPRELQ                                                     
         JE    AXJOB14                                                          
*                                                                               
AXJOB04  XR    RF,RF               BUMP TO NEXT ELEMENT                         
         ICM   RF,1,1(R5)                                                       
         JZ    EXIT                EXIT IF FUNNY ELEMENT LENGTH                 
         AR    R5,RF                                                            
         J     AXJOB02                                                          
*                                                                               
         USING NAMELD,R5                                                        
AXJOB06  MVC   AXJOBNAM,SPACES                                                  
         XR    RF,RF                                                            
         IC    RF,NAMLN                                                         
         SH    RF,=H'3'                                                         
         JM    AXJOB04                                                          
         MVC   AXJOBNAM(0),NAMEREC                                              
         EX    RF,*-6                                                           
         TR    AXJOBNAM,MXTRT                                                   
         J     AXJOB04                                                          
*                                                                               
         USING RSTELD,R5                                                        
AXJOB08  MVC   AXJOBF1,RSTFILT1    ACCOUNT FILTER 1                             
         TR    AXJOBF1,MXTRT                                                    
         MVC   AXJOBF2,RSTFILT2    ACCOUNT FILTER 2                             
         TR    AXJOBF2,MXTRT                                                    
         MVC   AXJOBF3,RSTFILT3    ACCOUNT FILTER 3                             
         TR    AXJOBF3,MXTRT                                                    
         MVC   AXJOBF4,RSTFILT4    ACCOUNT FILTER 4                             
         TR    AXJOBF4,MXTRT                                                    
         MVC   AXJOBF5,RSTFILT5    ACCOUNT FILTER 5                             
         TR    AXJOBF5,MXTRT                                                    
*                                                                               
         CLI   RSTLN,RSTLN3Q       ENSURE EL LONG ENOUGH                        
         JL    *+16                                                             
         TM    RSTSTAT5,RSTSNOTS   EXCLUDE FROM TEMPO                           
         JZ    *+8                                                              
         OI    JOBSTAT,ACTSLOCK    TREAT AS LOCKED                              
         EDIT  JOBSTAT,(3,AXJOBSTA),FILL=0,ZERO=NOBLANK                         
         J     AXJOB04                                                          
*                                                                               
         USING JOBELD,R5                                                        
AXJOB10  MVC   AXJOBOD,SPACES      OLD DATA ON SHORT ELEMENT                    
         CLI   JOBLN,JOBLN1Q       DOESN'T HOLD OPEN DATE                       
         JNH   AXJOB12                                                          
*                                                                               
         GOTO1 VDATCON,PLIST,(1,JOBODATE),(10,AXJOBOD),0                        
*&&US                                                                           
         XC    AXJOBOD(2),AXJOBOD+3                                             
         XC    AXJOBOD+3(2),AXJOBOD                                             
         XC    AXJOBOD(2),AXJOBOD+3                                             
*&&                                                                             
         TR    AXJOBOD,MXTRT                                                    
*                                                                               
AXJOB12  GOTO1 VDATCON,PLIST,(1,JOBCDATE),(10,AXJOBCD),0                        
*&&US                                                                           
         XC    AXJOBCD(2),AXJOBCD+3                                             
         XC    AXJOBCD+3(2),AXJOBCD                                             
         XC    AXJOBCD(2),AXJOBCD+3                                             
*&&                                                                             
         TR    AXJOBCD,MXTRT                                                    
         MVI   AXJOBIRA,C'N'       IS RATE ADJUSTED                             
         MVI   AXJOBIWO,C'N'       IS WRITE OFF                                 
*                                                                               
         CLI   JOBLN,JOBLN2Q       IS ELEMENT LONG ENOUGH?                      
         JNH   AXJOB04             NO                                           
         TM    JOBSTA1,JOBSART     RATE ADJUSTMENT?                             
         JZ    *+8                                                              
         MVI   AXJOBIRA,C'Y'                                                    
         TM    JOBSTA1,JOBSXJOB    WRITTEN OFF?                                 
         JZ    *+8                                                              
         MVI   AXJOBIWO,C'Y'                                                    
         J     AXJOB04                                                          
*                                                                               
         USING PPRELD,R5                                                        
AXJOB14  MVC   AXJOBOFC,PPRGAOFF   OFFICE CODE                                  
         TR    AXJOBOFC,MXTRT                                                   
         MVC   AXJOBCAC,PPRCOSTA   COSTING ACCOUNT CODE                         
         TR    AXJOBCAC,MXTRT                                                   
         J     AXJOB04                                                          
*                                                                               
AXJOBLIT DC    A(LITERAL)                                                       
*                                                                               
         DROP  R2,R3,R4,R5                                                      
         EJECT                                                                  
***********************************************************************         
* AXLEDC - EXTRACT LEDGER RECORDS FOR AXTRACT                         *         
*                                                                     *         
* AL4 A(EXTRACT RECORD)                                               *         
* AL4 A(ACCFILE RECORD)                                               *         
* AL4 0=ACCFILE-TO-EXTRACT                                            *         
* AL4 A(SXDTABD)                                                      *         
***********************************************************************         
         SPACE 1                                                                
AXLEDC   NMOD1 WORKL,*AXLEDC*,CLEAR=Y                                           
         USING WORKD,RC                                                         
         L     RA,AXLEDLIT                                                      
         USING LITERAL,RA                                                       
         ST    R1,APARM                                                         
         MVC   PARM,0(R1)          EXTRACT PARAMETER LIST                       
         LM    R2,R3,0(R1)                                                      
         L     R4,12(R1)                                                        
         USING AXLEDD,R2           R2=A(EXTRACT RECORD)                         
         USING ACTRECD,R3          R3=A(ACCFILE RECORD)                         
         USING SXDTABD,R4          R5=A(SXDTABD)                                
*                                                                               
         XC    AXLEDLEN,AXLEDLEN   RECORD LENGTH/TYPE/ENDOFREC                  
         MVC   AXLEDLEN(2),=Y(AXLEDDL)                                          
         MVC   AXLEDTYP,AXDLEDQ                                                 
         MVI   AXLEDDX,MXTRTQ                                                   
         MVI   AXLEDDX+1,MXTRTQ                                                 
         MVI   AXLEDCCD-1,MXTRTQ   2-BYTE AGY ALPHA                             
         MVI   AXLEDLC-1,MXTRTQ    LEDGER CODE                                  
         MVI   AXLEDNAM-1,MXTRTQ   LEDGER NAME                                  
         MVI   AXLEDSL-1,MXTRTQ    SECURITY LEVEL                               
*                                                                               
         MVC   AXLEDCCD,SXDTAGY    2-BYTE AGY ALPHA                             
         TR    AXLEDCCD,MXTRT                                                   
         MVC   AXLEDLC,ACTKUNT     LEDGER CODE                                  
         TR    AXLEDLC,MXTRT                                                    
*                                                                               
         LA    R5,ACTRFST          R5=A(FIRST ELEMENT)                          
AXLED02  CLI   0(R5),0             GET ELEMENT FROM RECORD                      
         JE    EXIT                                                             
         CLI   0(R5),NAMELQ        NAME ELEMENT                                 
         JE    AXLED06                                                          
         CLI   0(R5),RSTELQ        RECORD STATUS ELEMENT                        
         JE    AXLED08                                                          
*                                                                               
AXLED04  XR    RF,RF               BUMP TO NEXT ELEMENT                         
         ICM   RF,1,1(R5)                                                       
         JZ    EXIT                EXIT IF FUNNY ELEMENT LENGTH                 
         AR    R5,RF                                                            
         J     AXLED02                                                          
*                                                                               
         USING NAMELD,R5                                                        
AXLED06  MVC   AXLEDNAM,SPACES     ACCOUNT NAME                                 
         XR    RF,RF                                                            
         IC    RF,NAMLN                                                         
         SH    RF,=H'3'                                                         
         JM    AXLED04                                                          
         MVC   AXLEDNAM(0),NAMEREC                                              
         EX    RF,*-6                                                           
         TR    AXLEDNAM,MXTRT                                                   
         J     AXLED04                                                          
*                                                                               
         USING RSTELD,R5                                                        
AXLED08  EDIT  RSTSECY,(5,AXLEDSL),ZERO=NOBLANK,FILL=0                          
         J     AXLED04                                                          
*                                                                               
AXLEDLIT DC    A(LITERAL)                                                       
*                                                                               
         DROP  R2,R3,R4,R5                                                      
         EJECT                                                                  
***********************************************************************         
* AXLESC - EXTRACT LEDGER STRUCTURE RECORDS FOR AXTRACT               *         
*                                                                     *         
* AL4 A(EXTRACT RECORD)                                               *         
* AL4 A(ACCFILE RECORD)                                               *         
* AL1 0=BUILD 'K' RECORD, 1=BUILD ALL RECORDS, 2=PASS BACK NEXT       *         
*     X'FF' RETURNED IF NO MORE RECORDS TO PROCESS                    *         
* AL3 0                                                               *         
* AL4 A(SXDTABD)                                                      *         
***********************************************************************         
         SPACE 1                                                                
AXLESC   NMOD1 WORKL,*AXLESC*,CLEAR=Y                                           
         USING WORKD,RC                                                         
         L     RA,AXLESLIT                                                      
         USING LITERAL,RA                                                       
         ST    R1,APARM                                                         
         MVC   PARM,0(R1)          EXTRACT PARAMETER LIST                       
         LM    R2,R3,0(R1)                                                      
         L     R4,12(R1)                                                        
         USING AXLESD,R2           R2=A(EXTRACT RECORD)                         
         USING LDGRECD,R3          R3=A(ACCFILE RECORD)                         
         USING SXDTABD,R4          R4=A(SXDXTAB)                                
*                                                                               
         CLI   8(R1),1                                                          
         JL    AXLESKIL            BUILD K RECORD                               
         JE    AXLESSER            BUILD SERIES OF RECORDS                      
         J     AXLESNXT            OUTPUT NEXT RECORD                           
*                                                                               
AXLESLIT DC    A(LITERAL)                                                       
         EJECT                                                                  
***********************************************************************         
* BUILD 'K' RECORD AND EXIT                                           *         
***********************************************************************         
         SPACE 1                                                                
AXLESKIL BRAS  RE,AXLESBLK                                                      
         J     EXIT                                                             
         SPACE 1                                                                
***********************************************************************         
* BUILD SERIES OF RECORDS FOR THIS RECORD                             *         
***********************************************************************         
         SPACE 1                                                                
AXLESSER BRAS  RE,AXLESBLK         SET UP BLANK RECORD (KEY DETAIL)             
*                                                                               
         XR    RF,RF                                                            
         LH    RF,=Y(AXLESDL)      LENGTH OF A LEDGER STRUCTURE RECORD          
         STH   RF,RECLEN                                                        
         L     R2,AXLESCC          WHERE RECORDS TO BE BUILT                    
         ST    R2,ANEXT            FOR NEXT TIME IN                             
         XC    0(8,R2),0(R2)                                                    
*                                                                               
         XR    R1,R1                                                            
         LA    R6,LDGRFST          FIRST ONE RECORD DATA                        
AXLES02  CLI   0(R6),0                                                          
         JE    EXIT                                                             
         CLI   0(R6),ACLELQ                                                     
         JE    AXLES04                                                          
         IC    R1,1(R6)                                                         
         AR    R6,R1                                                            
         J     AXLES02                                                          
*                                                                               
         USING ACLELD,R6                                                        
AXLES04  LA    R7,ACLVALS          A(FIRST LEDGER)                              
         XR    RE,RE                                                            
         IC    RE,ACLLN                                                         
         SRL   RE,4                                                             
         LA    R9,1                LEVEL NUMBER IN R9                           
         XC    LASTLEN,LASTLEN                                                  
         XC    LASTLEN1,LASTLEN1                                                
*                                                                               
AXLES06  STH   RE,HALF             FOR BCT LOOP BELOW                           
*                                                                               
         L     RE,AXREC            BLANK RECORD                                 
         LR    R0,R2                                                            
         LH    RF,RECLEN           LENGTH OF A BLANK RECORD                     
         LR    R1,RF                                                            
         MVCL  R0,RE                                                            
*                                                                               
         LH    RF,RECLEN           LENGTH OF A BLANK RECORD                     
         AR    RF,R2                                                            
         XC    0(8,RF),0(RF)       CLEAR START OF NEXT DATA SPACE               
*                                                                               
         EDIT  (R9),(3,AXLESNUM),ZERO=NOBLANK,FILL=0                            
*                                                                               
         XR    RF,RF                                                            
         IC    RF,0(R7)                                                         
         STH   RF,LASTLEN1                                                      
         SH    RF,LASTLEN                                                       
         MVC   LASTLEN,LASTLEN1                                                 
         EDIT  ((RF)),(3,AXLESLLN),ZERO=NOBLANK,FILL=0                          
*                                                                               
         MVC   AXLESNAM,1(R7)      MOVE IN LEDGER NAME                          
*                                                                               
         LA    R9,1(R9)            BUMP LEVEL NUMBER                            
         LA    R7,16(R7)           NEXT LEVEL IN                                
         AH    R2,RECLEN           NEXT IN BUFFER                               
         CLI   0(R7),0             ANY DATA?                                    
         JE    EXIT                                                             
*                                                                               
         LH    RE,HALF                                                          
         BCT   RE,AXLES06                                                       
         J     EXIT                                                             
         EJECT                                                                  
***********************************************************************         
* GET NEXT LEDGER STRUCTURE RECORD AND RETURN IT                      *         
***********************************************************************         
         SPACE 1                                                                
AXLESNXT L     RE,ANEXT                                                         
         OC    0(8,RE),0(RE)                                                    
         JNZ   *+12                                                             
         MVI   8(R1),X'FF'         FLAG NO MORE RECORDS                         
         J     EXIT                                                             
*                                                                               
         LA    R0,AXLESD           COPY IN NEXT RECORD                          
         L     RE,ANEXT                                                         
         LH    RF,=Y(AXLESDL)                                                   
         LR    R1,RF                                                            
         MVCL  R0,RE                                                            
*                                                                               
         LH    RF,=Y(AXLESDL)      UPDATE POINTER                               
         L     RE,ANEXT                                                         
         AR    RE,RF                                                            
         ST    RE,ANEXT                                                         
         J     EXIT                                                             
         SPACE 1                                                                
***********************************************************************         
* BUILD BLANK RECORD AND INPUT KEY FIELDS                             *         
***********************************************************************         
         SPACE 1                                                                
AXLESBLK XC    AXLESLEN,AXLESLEN   RECORD LENGTH/TYPE/ENDOFREC                  
         MVC   AXLESLEN(2),=Y(AXLESDL)                                          
         MVC   AXLESTYP,AXDLESQ                                                 
         MVI   AXLESDX,MXTRTQ                                                   
         MVI   AXLESDX+1,MXTRTQ                                                 
         MVI   AXLESCCD-1,MXTRTQ   2-BYTE AGY ALPHA                             
         MVI   AXLESLC-1,MXTRTQ    LEDGER CODE                                  
         MVI   AXLESNUM-1,MXTRTQ   LEVEL NUMBER                                 
         MVI   AXLESNAM-1,MXTRTQ   LEVEL NAME                                   
         MVI   AXLESLLN-1,MXTRTQ   LEVEL LENGTH                                 
         MVC   AXLESCCD,SXDTAGY    AGENCY 2-BYTE ALPHA                          
         TR    AXLESCCD,MXTRT                                                   
         MVC   AXLESLC,LDGKUNT     LEDGER CODE                                  
         TR    AXLESLC,MXTRT                                                    
         BR    RE                                                               
*                                                                               
AXLESCC  DC    A(BUFFER1)          LOCALITY INFORMATION SAVE AREA               
*                                                                               
         DROP  R2,R3,R4,R6                                                      
         EJECT                                                                  
***********************************************************************         
* AXLOCC - EXTRACT LOCALITY RECORDS FOR AXTRACT                       *         
*                                                                     *         
* AL4 A(EXTRACT RECORD)                                               *         
* AL4 A(ACCFILE RECORD)                                               *         
* AL4 0=ACCFILE-TO-EXTRACT                                            *         
* AL4 A(SXDTABD)                                                      *         
***********************************************************************         
         SPACE 1                                                                
AXLOCC   NMOD1 WORKL,*AXLOCC*,CLEAR=Y                                           
         USING WORKD,RC                                                         
         L     RA,AXLOCLIT                                                      
         USING LITERAL,RA                                                       
         ST    R1,APARM                                                         
         MVC   PARM,0(R1)          EXTRACT PARAMETER LIST                       
         LM    R2,R3,0(R1)                                                      
         L     R4,12(R1)                                                        
         USING AXLOCD,R2           R2=A(EXTRACT RECORD)                         
         USING SUTRECD,R3          R3=A(ACCFILE RECORD)                         
         USING SXDTABD,R4          R4=A(SXDXTAB)                                
*                                                                               
         XC    AXLOCLEN,AXLOCLEN   RECORD LENGTH/TYPE/ENDOFREC                  
         MVC   AXLOCLEN(2),=Y(AXLOCDL)                                          
         MVC   AXLOCTYP,AXDLOCQ                                                 
         MVI   AXLOCDX,MXTRTQ                                                   
         MVI   AXLOCDX+1,MXTRTQ                                                 
         MVI   AXLOCCCD-1,MXTRTQ   2-BYTE AGY ALPHA                             
         MVI   AXLOCLCD-1,MXTRTQ   LOCALITY CODE                                
         MVI   AXLOCNAM-1,MXTRTQ   LOCALITY NAME                                
*                                                                               
         MVC   AXLOCCCD,SXDTAGY    AGENCY 2-BYTE ALPHA                          
         TR    AXLOCCCD,MXTRT                                                   
         MVC   AXLOCLCD,SUTKLOC    LOCALITY CODE                                
         TR    AXLOCLCD,MXTRT                                                   
*                                                                               
         XR    RF,RF                                                            
         LA    R5,SUTRFST          R5=A(FIRST ELEMENT)                          
AXLOC02  CLI   0(R5),0             GET ELEMENT FROM RECORD                      
         JE    EXIT                                                             
         CLI   0(R5),NAMELQ        NAME ELEMENT                                 
         JE    AXLOC04                                                          
         IC    RF,1(R5)                                                         
         JZ    EXIT                EXIT IF FUNNY ELEMENT LENGTH                 
         AR    R5,RF                                                            
         J     AXLOC02                                                          
*                                                                               
         USING NAMELD,R5                                                        
AXLOC04  MVC   AXLOCNAM,SPACES     LOCALITY NAME                                
         XR    RF,RF                                                            
         IC    RF,NAMLN                                                         
         SH    RF,=H'3'                                                         
         JM    EXIT                                                             
         MVC   AXLOCNAM(0),NAMEREC                                              
         EX    RF,*-6                                                           
         TR    AXLOCNAM,MXTRT                                                   
         J     EXIT                                                             
*                                                                               
AXLOCLIT DC    A(LITERAL)                                                       
*                                                                               
         DROP  R2,R3,R4,R5                                                      
         EJECT                                                                  
***********************************************************************         
* AXLCIC - EXTRACT LOCALITY INFORMATION RECORDS FOR AXTRACT           *         
*                                                                     *         
* AL4 A(EXTRACT RECORD)                                               *         
* AL4 A(ACCFILE RECORD)                                               *         
* AL1 0=BUILD 'K' RECORD, 1=BUILD ALL RECORDS, 2=PASS BACK NEXT       *         
*     X'FF' RETURNED IF NO MORE RECORDS TO PROCESS                    *         
* AL3 0                                                               *         
* AL4 A(SXDTABD)                                                      *         
***********************************************************************         
         SPACE 1                                                                
AXLCIC   NMOD1 WORKL,*AXLCIC*,CLEAR=Y                                           
         USING WORKD,RC                                                         
         L     RA,AXLCILIT                                                      
         USING LITERAL,RA                                                       
         ST    R1,APARM                                                         
         MVC   PARM,0(R1)          EXTRACT PARAMETER LIST                       
         LM    R2,R3,0(R1)                                                      
         L     R4,12(R1)                                                        
         USING AXLCID,R2           R2=A(EXTRACT RECORD)                         
         USING SUTRECD,R3          R3=A(ACCFILE RECORD)                         
         USING SXDTABD,R4          R4=A(SXDXTAB)                                
*                                                                               
         CLI   8(R1),1                                                          
         JL    AXLCIKIL            BUILD K RECORD                               
         JE    AXLCISER            BUILD SERIES OF RECORDS                      
         JH    AXLCINXT            OUTPUT NEXT RECORD                           
*                                                                               
AXLCILIT DC    A(LITERAL)                                                       
         SPACE 1                                                                
***********************************************************************         
* BUILD 'K' RECORD AND EXIT                                           *         
***********************************************************************         
         SPACE 1                                                                
AXLCIKIL BRAS  RE,AXLCIBLK                                                      
         J     EXIT                                                             
         SPACE 1                                                                
***********************************************************************         
* GET NEXT RECORD AND PASS IT BACK                                    *         
***********************************************************************         
         SPACE 1                                                                
AXLCINXT L     RE,ANEXT                                                         
         OC    0(8,RE),0(RE)                                                    
         JNZ   *+12                                                             
         MVI   8(R1),X'FF'         NOTHING MORE TO RETURN                       
         J     EXIT                                                             
*                                                                               
         L     RE,ANEXT            MOVE IN RECORD                               
         LA    R0,AXLCID                                                        
         LH    RF,=Y(AXLCIDL)                                                   
         LR    R1,RF                                                            
         MVCL  R0,RE                                                            
*                                                                               
         LH    RF,=Y(AXLCIDL)      UPDATE POINTER                               
         L     RE,ANEXT                                                         
         AR    RE,RF                                                            
         ST    RE,ANEXT                                                         
         J     EXIT                                                             
         SPACE 1                                                                
***********************************************************************         
* BUILD SERIES OF RECORDS FOR THIS RECORD                             *         
***********************************************************************         
         SPACE 1                                                                
AXLCISER BRAS  RE,AXLCIBLK         SET UP BLANK RECORD (KEY DETAIL)             
*                                                                               
         LH    RF,=Y(AXLCIDL)                                                   
         STH   RF,RECLEN                                                        
         L     R2,AXLCICC          WHERE RECORDS TO BE BUILT                    
         XC    0(8,R2),0(R2)                                                    
         ST    R2,ANEXT            FOR NEXT TIME IN                             
*                                                                               
         LA    R6,SUTRFST          FIRST ONE RECORD DATA                        
AXLCI02  CLI   0(R6),0                                                          
         JE    EXIT                                                             
         CLI   0(R6),SUTELQ                                                     
         JE    AXLCI06                                                          
*                                                                               
AXLCI04  XR    R1,R1                                                            
         IC    R1,1(R6)                                                         
         AR    R6,R1                                                            
         J     AXLCI02                                                          
*                                                                               
AXLCI06  L     RE,AXREC            BLANK RECORD                                 
         LR    R0,R2               NEXT IN BUFFER                               
         LH    RF,RECLEN           LENGTH OF A BLANK RECORD                     
         LR    R1,RF                                                            
         MVCL  R0,RE               COPY IN BLANK RECORD                         
*                                                                               
         LH    RF,RECLEN                                                        
         AR    RF,R2                                                            
         XC    0(8,RF),0(RF)       CLEAR START OF NEXT DATA SPACE               
*                                                                               
         USING SUTELD,R6                                                        
         GOTO1 VDATCON,PLIST,(1,SUTEFF),(10,AXLCIEFF),0                         
*&&US                                                                           
         XC    AXLCIEFF(2),AXLCIEFF+3                                           
         XC    AXLCIEFF+3(2),AXLCIEFF                                           
         XC    AXLCIEFF(2),AXLCIEFF+3                                           
*&&                                                                             
         TR    AXLCIEFF,MXTRT                                                   
*                                                                               
         EDIT  SUTRTE,(10,AXLCITAX),ZERO=NOBLANK,FILL=0                         
*                                                                               
         AH    R2,RECLEN           NEXT IN BUFFER                               
         B     AXLCI04                                                          
         EJECT                                                                  
***********************************************************************         
* BUILD BLANK RECORD AND INPUT KEY FIELDS                             *         
***********************************************************************         
         SPACE 1                                                                
AXLCIBLK XC    AXLCILEN,AXLCILEN   RECORD LENGTH/TYPE/ENDOFREC                  
         MVC   AXLCILEN(2),=Y(AXLCIDL)                                          
         MVC   AXLCITYP,AXDLCIQ                                                 
         MVI   AXLCIDX,MXTRTQ                                                   
         MVI   AXLCIDX+1,MXTRTQ                                                 
         MVI   AXLCICCD-1,MXTRTQ   2-BYTE AGY ALPHA                             
         MVI   AXLCILCD-1,MXTRTQ   LOCALITY CODE                                
         MVI   AXLCIEFF-1,MXTRTQ   EFFECTIVE DATE                               
         MVI   AXLCITAX-1,MXTRTQ   TAX RATE                                     
         MVC   AXLCICCD,SXDTAGY    AGENCY 2-BYTE ALPHA                          
         TR    AXLCICCD,MXTRT                                                   
         MVC   AXLCILCD,SUTKLOC    LOCALITY CODE                                
         TR    AXLCILCD,MXTRT                                                   
         BR    RE                                                               
*                                                                               
AXLCICC  DC    A(BUFFER1)          LOCALITY INFORMATION SAVE AREA               
*                                                                               
         DROP  R2,R3,R4,R6                                                      
         EJECT                                                                  
***********************************************************************         
* AXMEDC - EXTRACT MEDIA RECORDS FOR AXTRACT                          *         
*                                                                     *         
* AL4 A(EXTRACT RECORD)                                               *         
* AL4 A(ACCFILE RECORD)                                               *         
* AL4 0=ACCFILE-TO-EXTRACT                                            *         
* AL4 A(SXDTABD)                                                      *         
***********************************************************************         
         SPACE 1                                                                
AXMEDC   NMOD1 WORKL,*AXMEDC*,CLEAR=Y                                           
         USING WORKD,RC                                                         
         L     RA,AXMEDLIT                                                      
         USING LITERAL,RA                                                       
         ST    R1,APARM                                                         
         MVC   PARM,0(R1)          EXTRACT PARAMETER LIST                       
         LM    R2,R3,0(R1)                                                      
         L     R4,12(R1)                                                        
         USING AXMEDD,R2           R2=A(EXTRACT RECORD)                         
         USING PMDRECD,R3          R3=A(ACCFILE RECORD)                         
         USING SXDTABD,R4          R4=A(SXDXTABD)                               
*                                                                               
         XC    AXMEDLEN,AXMEDLEN   RECORD LENGTH/TYPE/ENDOFREC                  
         MVC   AXMEDLEN(2),=Y(AXMEDDL)                                          
         MVC   AXMEDTYP,AXDMEDQ                                                 
         MVI   AXMEDDX,MXTRTQ                                                   
         MVI   AXMEDDX+1,MXTRTQ                                                 
         MVI   AXMEDCCD-1,MXTRTQ   AGENCY ALPHA                                 
         MVI   AXMEDMCD-1,MXTRTQ   MEDIA CODE                                   
         MVI   AXMEDMDS-1,MXTRTQ   MEDIA DESCRIPTION                            
         MVI   AXMEDGRP-1,MXTRTQ   MEDIA GROUP                                  
*                                                                               
         MVC   AXMEDCCD,SXDTAGY    AGENCY ALPHA                                 
         TR    AXMEDCCD,MXTRT                                                   
         MVC   AXMEDMCD,PMDKMED    MEDIA CODE                                   
         TR    AXMEDMCD,MXTRT                                                   
*                                                                               
         XR    RF,RF                                                            
         LA    R5,PMDRFST          R5=A(FIRST ELEMENT)                          
AXMED02  CLI   0(R5),0             GET ELEMENT FROM RECORD                      
         JE    EXIT                                                             
         CLI   0(R5),PMDELQ                                                     
         JE    AXMED04                                                          
         ICM   RF,1,1(R5)                                                       
         JZ    EXIT                                                             
         AR    R5,RF                                                            
         J     AXMED02                                                          
*                                                                               
         USING PMDELD,R5                                                        
AXMED04  MVC   AXMEDMDS,PMDDESC    MEDIA DESCRIPTION                            
         TR    AXMEDMDS,MXTRT                                                   
         MVC   AXMEDGRP,PMDGRP     MEDIA GROUP                                  
         TR    AXMEDGRP,MXTRT                                                   
         J     EXIT                                                             
*                                                                               
AXMEDLIT DC    A(LITERAL)                                                       
*                                                                               
         DROP  R2,R3,R4,R5                                                      
         EJECT                                                                  
***********************************************************************         
* AXTGRC - EXTRACT WORKCODE GROUP RECORDS FOR AXTRACT                 *         
*                                                                     *         
* AL4 A(EXTRACT RECORD)                                               *         
* AL4 A(ACCFILE RECORD)                                               *         
* AL4 0=ACCFILE-TO-EXTRACT                                            *         
* AL4 A(SXDTABD)                                                      *         
***********************************************************************         
         SPACE 1                                                                
AXMGRC   NMOD1 WORKL,*AXMGRC*,CLEAR=Y                                           
         USING WORKD,RC                                                         
         L     RA,AXMGRLIT                                                      
         USING LITERAL,RA                                                       
         ST    R1,APARM                                                         
         MVC   PARM,0(R1)          EXTRACT PARAMETER LIST                       
         LM    R2,R3,0(R1)                                                      
         L     R4,12(R1)                                                        
         MVC   ALEVELS,16(R1)                                                   
         USING AXMGRD,R2           R2=A(EXTRACT RECORD)                         
         USING MGRRECD,R3          R3=A(ACCFILE RECORD)                         
         USING SXDTABD,R4          R4=A(SXDXTAB)                                
*                                                                               
         XC    AXMGRLEN,AXMGRLEN   RECORD LENGTH/TYPE/ENDOFREC                  
         MVC   AXMGRLEN(2),=Y(AXMGRDL)                                          
         MVC   AXMGRTYP,AXDMGRQ                                                 
         MVI   AXMGRDX,MXTRTQ                                                   
         MVI   AXMGRDX+1,MXTRTQ                                                 
         MVI   AXMGRCCD-1,MXTRTQ   2-BYTE AGY ALPHA                             
         MVI   AXMGRGC-1,MXTRTQ    OFFICE GROUP CODE                            
         MVI   AXMGRNAM-1,MXTRTQ   OFFICE NAME                                  
*                                                                               
         MVC   AXMGRCCD,SXDTAGY    AGENCY 2-BYTE ALPHA                          
         TR    AXMGRCCD,MXTRT                                                   
         MVC   AXMGRGC,MGRKCODE    MEDIA GROUP                                  
         TR    AXMGRGC,MXTRT                                                    
*                                                                               
         XR    RF,RF                                                            
         LA    R5,MGRRFST          R5=A(FIRST ELEMENT)                          
AXMGR02  CLI   0(R5),0             GET ELEMENT FROM RECORD                      
         JE    EXIT                                                             
         CLI   0(R5),NAMELQ        NAME ELEMENT                                 
         JE    AXMGR04                                                          
         IC    RF,1(R5)                                                         
         AR    R5,RF                                                            
*                                                                               
         USING NAMELD,R5                                                        
AXMGR04  MVC   AXMGRNAM,SPACES      MEDIA GROUP NAME                            
         XR    RF,RF                                                            
         IC    RF,NAMLN                                                         
         SH    RF,=H'3'                                                         
         JM    EXIT                                                             
         MVC   AXMGRNAM(0),NAMEREC                                              
         EX    RF,*-6                                                           
         TR    AXMGRNAM,MXTRT                                                   
         J     EXIT                                                             
*                                                                               
AXMGRLIT DC    A(LITERAL)                                                       
*                                                                               
         DROP  R2,R3,R4,R5                                                      
         EJECT                                                                  
***********************************************************************         
* AXNCTC - EXTRACT NON-CLIENT TIME RECORDS FOR AXTRACT                *         
*                                                                     *         
* AL4 A(EXTRACT RECORD)                                               *         
* AL4 A(ACCFILE RECORD)                                               *         
* AL4 0=ACCFILE-TO-EXTRACT                                            *         
* AL4 A(SXDTABD)                                                      *         
***********************************************************************         
         SPACE 1                                                                
AXNCTC   NMOD1 WORKL,*AXNCTC*,CLEAR=Y                                           
         USING WORKD,RC                                                         
         L     RA,AXNCTLIT                                                      
         USING LITERAL,RA                                                       
         ST    R1,APARM                                                         
         MVC   PARM,0(R1)          EXTRACT PARAMETER LIST                       
         LM    R2,R3,0(R1)                                                      
         L     R4,12(R1)                                                        
         USING AXNCTD,R2           R2=A(EXTRACT RECORD)                         
         USING ACTKEY,R3           R3=A(ACCFILE RECORD)                         
         USING SXDTABD,R4          R5=A(SXDTABD)                                
*                                                                               
         XC    AXNCTLEN,AXNCTLEN   RECORD LENGTH/TYPE/ENDOFREC                  
         MVC   AXNCTLEN(2),=Y(AXNCTDL)                                          
         MVC   AXNCTTYP,AXDNCTQ                                                 
         MVI   AXNCTDX,MXTRTQ                                                   
         MVI   AXNCTDX+1,MXTRTQ                                                 
         MVI   AXNCTCCD-1,MXTRTQ   2-BYTE AGY ALPHA                             
         MVI   AXNCTTAC-1,MXTRTQ   NON CLIENT TIME ACCT CODE                    
         MVI   AXNCTNAM-1,MXTRTQ   ACCOUNT NAME                                 
         MVI   AXNCTF1-1,MXTRTQ    ACCOUNT FILTER 1                             
         MVI   AXNCTF2-1,MXTRTQ    ACCOUNT FILTER 2                             
         MVI   AXNCTF3-1,MXTRTQ    ACCOUNT FILTER 3                             
         MVI   AXNCTF4-1,MXTRTQ    ACCOUNT FILTER 4                             
         MVI   AXNCTF5-1,MXTRTQ    ACCOUNT FILTER 5                             
         MVI   AXNCTANC-1,MXTRTQ   ANALYSIS CODE                                
         MVI   AXNCTISL-1,MXTRTQ   IS LOCKED                                    
*                                                                               
         MVC   AXNCTCCD,SXDTAGY    2-BYTE AGY ALPHA                             
         TR    AXNCTCCD,MXTRT                                                   
         MVC   AXNCTTAC,ACTKACT    NCT ACCT CODE                                
         TR    AXNCTTAC,MXTRT                                                   
         MVI   AXNCTISL,C'N'       IS LOCKED                                    
         TM    ACTRSTAT,ACTSLOCK                                                
         JZ    *+8                                                              
         MVI   AXNCTISL,C'Y'                                                    
*                                                                               
         LA    R5,ACTRFST          R5=A(FIRST ELEMENT)                          
AXNCT02  CLI   0(R5),0             GET ELEMENT FROM RECORD                      
         JE    EXIT                                                             
         CLI   0(R5),NAMELQ        NAME ELEMENT                                 
         BE    AXNCT06                                                          
         CLI   0(R5),RSTELQ        RECORD STATUS ELEMENT                        
         BE    AXNCT08                                                          
*                                                                               
AXNCT04  XR    RF,RF               BUMP TO NEXT ELEMENT                         
         ICM   RF,1,1(R5)                                                       
         JZ    EXIT                EXIT IF FUNNY ELEMENT LENGTH                 
         AR    R5,RF                                                            
         J     AXNCT02                                                          
*                                                                               
         USING NAMELD,R5                                                        
AXNCT06  MVC   AXNCTNAM,SPACES                                                  
         XR    RF,RF                                                            
         IC    RF,NAMLN                                                         
         SH    RF,=H'3'                                                         
         JM    AXNCT04                                                          
         MVC   AXNCTNAM(0),NAMEREC                                              
         EX    RF,*-6                                                           
         TR    AXNCTNAM,MXTRT                                                   
         J     AXNCT04                                                          
*                                                                               
         USING RSTELD,R5                                                        
AXNCT08  MVC   AXNCTF1,RSTFILT1    ACCOUNT FILTER 1                             
         TR    AXNCTF1,MXTRT                                                    
         MVC   AXNCTF2,RSTFILT2    ACCOUNT FILTER 2                             
         TR    AXNCTF2,MXTRT                                                    
         MVC   AXNCTF3,RSTFILT3    ACCOUNT FILTER 3                             
         TR    AXNCTF3,MXTRT                                                    
         MVC   AXNCTF4,RSTFILT4    ACCOUNT FILTER 4                             
         TR    AXNCTF4,MXTRT                                                    
         MVC   AXNCTF5,RSTFILT5    ACCOUNT FILTER 5                             
         TR    AXNCTF5,MXTRT                                                    
         MVC   AXNCTANC,RSTANAL    ANALYSIS CODE                                
         TR    AXNCTANC,MXTRT                                                   
         J     AXNCT04                                                          
*                                                                               
AXNCTLIT DC    A(LITERAL)                                                       
*                                                                               
         DROP  R2,R3,R4,R5                                                      
         EJECT                                                                  
***********************************************************************         
* AXOFFC - EXTRACT OFFICE RECORDS FOR AXTRACT                         *         
*                                                                     *         
* AL4 A(EXTRACT RECORD)                                               *         
* AL4 A(ACCFILE RECORD)                                               *         
* AL4 0=ACCFILE-TO-EXTRACT                                            *         
* AL4 A(SXDTABD)                                                      *         
* AL4 A(LEVELS) - CAN GET LENGTHS OF LEVELS FROM HERE                 *         
***********************************************************************         
         SPACE 1                                                                
AXOFFC   NMOD1 WORKL,*AXOFFC*,CLEAR=Y                                           
         USING WORKD,RC                                                         
         L     RA,AXOFFLIT                                                      
         USING LITERAL,RA                                                       
         ST    R1,APARM                                                         
         MVC   PARM,0(R1)          EXTRACT PARAMETER LIST                       
         LM    R2,R3,0(R1)                                                      
         L     R4,12(R1)                                                        
         MVC   ALEVELS,16(R1)                                                   
         USING AXOFFD,R2           R2=A(EXTRACT RECORD)                         
         USING ACTKEY,R3           R3=A(ACCFILE RECORD)                         
         USING SXDTABD,R4          R4=A(SXDXTAB)                                
*                                                                               
         XC    AXOFFLEN,AXOFFLEN   RECORD LENGTH/TYPE/ENDOFREC                  
         MVC   AXOFFLEN(2),=Y(AXOFFDL)                                          
         MVC   AXOFFTYP,AXDOFFQ                                                 
         MVI   AXOFFDX,MXTRTQ                                                   
         MVI   AXOFFDX+1,MXTRTQ                                                 
         MVI   AXOFFCCD-1,MXTRTQ   2-BYTE AGY ALPHA                             
         MVI   AXOFFCDE-1,MXTRTQ   OFFICE CODE                                  
         MVI   AXOFFNAM-1,MXTRTQ   OFFICE NAME                                  
         MVI   AXOFFIAC-1,MXTRTQ   INCOME ACCOUNT                               
         MVI   AXOFFTC-1,MXTRTQ    TASK CODE                                    
         MVI   AXOFFLCK-1,MXTRTQ   ACCOUNT IS LOCKED                            
         MVI   AXOFFI1-1,MXTRTQ    PRODUCT REQUIRED                             
         MVI   AXOFFI2-1,MXTRTQ    JOB REQUIRED                                 
*                                                                               
         MVC   AXOFFCCD,SXDTAGY    AGENCY 2-BYTE ALPHA                          
         TR    AXOFFCCD,MXTRT                                                   
         MVC   AXOFFCDE,SPACES     OFFICE CODE                                  
         L     R6,ALEVELS                                                       
         XR    RF,RF                                                            
         IC    RF,0(R6)                                                         
         BCTR  RF,0                                                             
         MVC   AXOFFCDE(0),ACTKACT                                              
         EX    RF,*-6                                                           
         TR    AXOFFCDE,MXTRT                                                   
*                                                                               
         LA    R5,ACTRFST          R5=A(FIRST ELEMENT)                          
AXOFF02  CLI   0(R5),0             GET ELEMENT FROM RECORD                      
         JE    EXIT                                                             
         CLI   0(R5),SPAELQ        SPAEL                                        
         JE    AXOFF06                                                          
         CLI   0(R5),RSTELQ        RSTEL                                        
         JE    AXOFF08                                                          
         CLI   0(R5),NAMELQ        NAME ELEMENT                                 
         JE    AXOFF10                                                          
*                                                                               
AXOFF04  XR    RF,RF               BUMP TO NEXT ELEMENT                         
         ICM   RF,1,1(R5)                                                       
         JZ    EXIT                EXIT IF FUNNY ELEMENT LENGTH                 
         AR    R5,RF                                                            
         J     AXOFF02                                                          
*                                                                               
         USING SPAELD,R5                                                        
AXOFF06  CLI   SPATYPE,SPATINCO                                                 
         JNE   AXOFF04                                                          
         MVC   AXOFFIAC,SPAAULA    INCOME ACCOUNT                               
         TR    AXOFFIAC,MXTRT                                                   
         J     AXOFF04                                                          
*                                                                               
         USING RSTELD,R5                                                        
AXOFF08  MVI   AXOFFLCK,C'N'                                                    
         TM    RSTSTAT1,RSTSACIL   ACCOUNT IS LOCKED                            
         JZ    *+8                                                              
         MVI   AXOFFLCK,C'Y'                                                    
         MVI   AXOFFI1,C'N'                                                     
         MVI   AXOFFI2,C'N'                                                     
         CLI   RSTLN,RSTLN3Q       ENSURE ELEMENT LONG ENOUGH                   
         JL    AXOFF04                                                          
*                                                                               
         MVC   AXOFFTC,RSTDFTSK    TASK CODE                                    
         TR    AXOFFTC,MXTRT                                                    
         TM    RSTSTAT5,RSTSPROD   PRODUCT REQUIRED                             
         JZ    *+8                                                              
         MVI   AXOFFI1,C'Y'                                                     
         TM    RSTSTAT5,RSTSPRJB   PRODUCT/JOB REQUIRED                         
         JZ    *+8                                                              
         MVI   AXOFFI2,C'Y'                                                     
         J     AXOFF04                                                          
*                                                                               
         USING NAMELD,R5                                                        
AXOFF10  MVC   AXOFFNAM,SPACES     OFFICE NAME                                  
         XR    RF,RF                                                            
         IC    RF,NAMLN                                                         
         SH    RF,=H'3'                                                         
         JM    AXOFF04                                                          
         MVC   AXOFFNAM(0),NAMEREC                                              
         EX    RF,*-6                                                           
         TR    AXOFFNAM,MXTRT                                                   
         J     AXOFF04                                                          
*                                                                               
AXOFFLIT DC    A(LITERAL)                                                       
*                                                                               
         DROP  R2,R3,R4,R5                                                      
         EJECT                                                                  
***********************************************************************         
* AXOGRC - EXTRACT OFFICE GROUP RECORDS FOR AXTRACT                   *         
*                                                                     *         
* AL4 A(EXTRACT RECORD)                                               *         
* AL4 A(ACCFILE RECORD)                                               *         
* AL4 0=ACCFILE-TO-EXTRACT                                            *         
* AL4 A(SXDTABD)                                                      *         
***********************************************************************         
         SPACE 1                                                                
AXOGRC   NMOD1 WORKL,*AXOGRC*,CLEAR=Y                                           
         USING WORKD,RC                                                         
         L     RA,AXOGRLIT                                                      
         USING LITERAL,RA                                                       
         ST    R1,APARM                                                         
         MVC   PARM,0(R1)          EXTRACT PARAMETER LIST                       
         LM    R2,R3,0(R1)                                                      
         L     R4,12(R1)                                                        
         MVC   ALEVELS,16(R1)                                                   
         USING AXOGRD,R2           R2=A(EXTRACT RECORD)                         
         USING OGRRECD,R3          R3=A(ACCFILE RECORD)                         
         USING SXDTABD,R4          R4=A(SXDXTAB)                                
*                                                                               
         XC    AXOGRLEN,AXOGRLEN   RECORD LENGTH/TYPE/ENDOFREC                  
         MVC   AXOGRLEN(2),=Y(AXOGRDL)                                          
         MVC   AXOGRTYP,AXDOGRQ                                                 
         MVI   AXOGRDX,MXTRTQ                                                   
         MVI   AXOGRDX+1,MXTRTQ                                                 
         MVI   AXOGRCCD-1,MXTRTQ   2-BYTE AGY ALPHA                             
         MVI   AXOGRGC-1,MXTRTQ    OFFICE GROUP CODE                            
         MVI   AXOGRNAM-1,MXTRTQ   OFFICE NAME                                  
*                                                                               
         MVC   AXOGRCCD,SXDTAGY    AGENCY 2-BYTE ALPHA                          
         TR    AXOGRCCD,MXTRT                                                   
         MVC   AXOGRGC,OGRKCODE    OFFICE GROUP                                 
         TR    AXOGRGC,MXTRT                                                    
*                                                                               
         XR    RF,RF                                                            
         LA    R5,OGRRFST          R5=A(FIRST ELEMENT)                          
AXOGR02  CLI   0(R5),0             GET ELEMENT FROM RECORD                      
         JE    EXIT                                                             
         CLI   0(R5),NAMELQ        NAME ELEMENT                                 
         JE    AXOGR04                                                          
         ICM   RF,1,1(R5)                                                       
         JZ    EXIT                EXIT IF FUNNY ELEMENT LENGTH                 
         AR    R5,RF                                                            
         J     AXOGR02                                                          
*                                                                               
         USING NAMELD,R5                                                        
AXOGR04  MVC   AXOGRNAM,SPACES     OFFICE GROUP NAME                            
         XR    RF,RF                                                            
         IC    RF,NAMLN                                                         
         SH    RF,=H'3'                                                         
         JM    EXIT                                                             
         MVC   AXOGRNAM(0),NAMEREC                                              
         EX    RF,*-6                                                           
         TR    AXOGRNAM,MXTRT                                                   
         J     EXIT                                                             
*                                                                               
AXOGRLIT DC    A(LITERAL)                                                       
*                                                                               
         DROP  R2,R3,R4,R5                                                      
         EJECT                                                                  
***********************************************************************         
* AXOFLC - EXTRACT OFFICE LIST RECORDS FOR AXTRACT                    *         
*                                                                     *         
* AL4 A(EXTRACT RECORD)                                               *         
* AL4 A(ACCFILE RECORD)                                               *         
* AL4 0=ACCFILE-TO-EXTRACT                                            *         
* AL4 A(SXDTABD)                                                      *         
***********************************************************************         
         SPACE 1                                                                
AXOFLC   NMOD1 WORKL,*AXOFLC*,CLEAR=Y                                           
         USING WORKD,RC                                                         
         L     RA,AXOFLLIT                                                      
         USING LITERAL,RA                                                       
         ST    R1,APARM                                                         
         MVC   PARM,0(R1)          EXTRACT PARAMETER LIST                       
         LM    R2,R3,0(R1)                                                      
         L     R4,12(R1)                                                        
         MVC   ALEVELS,16(R1)                                                   
         USING AXOFLD,R2           R2=A(EXTRACT RECORD)                         
         USING OFFRECD,R3          R3=A(ACCFILE RECORD)                         
         USING SXDTABD,R4          R4=A(SXDXTAB)                                
*                                                                               
         XC    AXOFLLEN,AXOFLLEN   RECORD LENGTH/TYPE/ENDOFREC                  
         MVC   AXOFLLEN(2),=Y(AXOFLDL)                                          
         MVC   AXOFLTYP,AXDOFLQ                                                 
         MVI   AXOFLDX,MXTRTQ                                                   
         MVI   AXOFLDX+1,MXTRTQ                                                 
         MVI   AXOFLCCD-1,MXTRTQ   2-BYTE AGY ALPHA                             
         MVI   AXOFLGC-1,MXTRTQ    OFFICE LIST CODE                             
         MVI   AXOFLSC-1,MXTRTQ    OFFICE LIST SUB CODE                         
         MVI   AXOFLSC,C' '        SET TO A ' ' IN EXTRACT                      
         MVI   AXOFLNAM-1,MXTRTQ   OFFICE NAME                                  
*                                                                               
         MVC   AXOFLCCD,SXDTAGY    AGENCY 2-BYTE ALPHA                          
         TR    AXOFLCCD,MXTRT                                                   
         MVC   AXOFLGC,OFFKOFF     OFFICE LIST                                  
         TR    AXOFLGC,MXTRT                                                    
*                                                                               
         XR    RF,RF                                                            
         LA    R5,OFFRFST          R5=A(FIRST ELEMENT)                          
AXOFL02  CLI   0(R5),0             GET ELEMENT FROM RECORD                      
         JE    EXIT                                                             
         CLI   0(R5),NAMELQ        NAME ELEMENT                                 
         JE    AXOFL04                                                          
         IC    RF,1(R5)                                                         
         AR    R5,RF                                                            
         J     AXOFL02                                                          
*                                                                               
         USING NAMELD,R5                                                        
AXOFL04  MVC   AXOFLNAM,SPACES     OFFICE LIST NAME                             
         XR    RF,RF                                                            
         IC    RF,NAMLN                                                         
         AHI   RF,-3                                                            
         JM    EXIT                                                             
         MVC   AXOFLNAM(0),NAMEREC                                              
         EX    RF,*-6                                                           
         TR    AXOFLNAM,MXTRT                                                   
         J     EXIT                                                             
*                                                                               
AXOFLLIT DC    A(LITERAL)                                                       
*                                                                               
         DROP  R2,R3,R4,R5                                                      
         EJECT                                                                  
***********************************************************************         
* AXOLEC - EXTRACT OFFICE LIST ENTRY RECORDS FOR AXTRACT              *         
*                                                                     *         
* AL4 A(EXTRACT RECORD)                                               *         
* AL4 A(ACCFILE RECORD)                                               *         
* AL1 0=BUILD 'K' RECORD, 1=BUILD ALL RECORDS, 2=PASS BACK NEXT       *         
*     X'FF' RETURNED IF NO MORE RECORDS TO PROCESS                    *         
* AL3 0                                                               *         
* AL4 A(SXDTABD)                                                      *         
***********************************************************************         
         SPACE 1                                                                
AXOLEC   NMOD1 WORKL,*AXOLEC*,CLEAR=Y                                           
         USING WORKD,RC                                                         
         L     RA,AXOLELIT                                                      
         USING LITERAL,RA                                                       
         ST    R1,APARM                                                         
         MVC   PARM,0(R1)          EXTRACT PARAMETER LIST                       
         LM    R2,R3,0(R1)                                                      
         L     R4,12(R1)                                                        
         USING AXOLED,R2           R2=A(EXTRACT RECORD)                         
         USING OFFRECD,R3          R3=A(ACCFILE RECORD)                         
         USING SXDTABD,R4          R4=A(SXDXTAB)                                
*                                                                               
         CLI   8(R1),1                                                          
         JL    AXOLEKIL            BUILD K RECORD                               
         JE    AXOLESER            BUILD SERIES OF RECORDS                      
         JH    AXOLENXT            OUTPUT NEXT RECORD                           
         EJECT                                                                  
***********************************************************************         
* BUILD 'K' RECORD AND EXIT                                           *         
***********************************************************************         
         SPACE 1                                                                
AXOLEKIL BRAS  RE,AXOLEBLK                                                      
         J     EXIT                                                             
         SPACE 1                                                                
***********************************************************************         
* GET NEXT RECORD AND PASS IT BACK                                    *         
***********************************************************************         
         SPACE 1                                                                
AXOLENXT L     RE,ANEXT                                                         
         OC    0(8,RE),0(RE)                                                    
         JNZ   *+12                                                             
         MVI   8(R1),X'FF'         NO MORE RECORDS                              
         J     EXIT                                                             
*                                                                               
         L     RE,ANEXT            MOVE IN NEXT RECORD                          
         LA    R0,AXOLED                                                        
         LH    RF,=Y(AXOLEDL)                                                   
         LR    R1,RF                                                            
         MVCL  R0,RE                                                            
*                                                                               
         LH    RF,=Y(AXOLEDL)      UPDATE POINTER                               
         L     RE,ANEXT                                                         
         AR    RE,RF                                                            
         ST    RE,ANEXT                                                         
         J     EXIT                                                             
         SPACE 1                                                                
***********************************************************************         
* BUILD SERIES OF RECORDS FOR THIS RECORD                             *         
***********************************************************************         
         SPACE 1                                                                
AXOLESER BRAS  RE,AXOLEBLK         SET UP BLANK RECORD (KEY DETAIL)             
*                                                                               
         LH    RF,=Y(AXOLEDL)                                                   
         STH   RF,RECLEN                                                        
         L     R2,AXOLECC          WHERE RECORDS TO BE BUILT                    
         XC    0(8,R2),0(R2)                                                    
         ST    R2,ANEXT            FOR NEXT TIME IN                             
*                                                                               
         LA    R6,OFFRFST          FIRST ONE RECORD DATA                        
AXOLE02  CLI   0(R6),0                                                          
         JE    EXIT                                                             
         CLI   0(R6),OFLELQ                                                     
         JE    AXOLE06                                                          
*                                                                               
AXOLE04  XR    R1,R1                                                            
         IC    R1,1(R6)                                                         
         AR    R6,R1                                                            
         J     AXOLE02                                                          
*                                                                               
         USING OFLELD,R6                                                        
AXOLE06  LA    R7,OFLNTRY          A(FIRST OFFICE)                              
         XR    R0,R0                                                            
         IC    R0,OFLLN                                                         
         LA    RF,OFLLN1Q                                                       
         SR    R0,RF                                                            
         JNP   AXOLE04                                                          
*                                                                               
AXOLE08  STH   R0,HALF                                                          
         L     RE,AXREC            BLANK RECORD HERE                            
         LR    R0,R2                                                            
         LH    RF,RECLEN                                                        
         LR    R1,RF                                                            
         MVCL  R0,RE               COPY IN A BLANK RECORD                       
*                                                                               
         LH    RF,RECLEN                                                        
         AR    RF,R2                                                            
         XC    0(8,RF),0(RF)       CLEAR START OF NEXT DATA SPACE               
*                                                                               
         MVC   AXOLEOFC,0(R7)      MOVE IN OFFICE CODE                          
         TR    AXOLEOFC,MXTRT                                                   
*                                                                               
         LA    R7,2(R7)            NEXT OFFICE                                  
         AH    R2,RECLEN                                                        
         LH    R0,HALF                                                          
         SH    R0,=H'2'                                                         
         JP    AXOLE08                                                          
         J     AXOLE04             REACHED THE END                              
         SPACE 1                                                                
***********************************************************************         
* BUILD BLANK RECORD AND INPUT KEY FIELDS                             *         
***********************************************************************         
         SPACE 1                                                                
AXOLEBLK XC    AXOLELEN,AXOLELEN   RECORD LENGTH/TYPE/ENDOFREC                  
         MVC   AXOLELEN(2),=Y(AXOLEDL)                                          
         MVC   AXOLETYP,AXDOLEQ                                                 
         MVI   AXOLEDX,MXTRTQ                                                   
         MVI   AXOLEDX+1,MXTRTQ                                                 
         MVI   AXOLECCD-1,MXTRTQ   2-BYTE AGY ALPHA                             
         MVI   AXOLEGC-1,MXTRTQ    OFFICE LIST CODE                             
         MVI   AXOLESC-1,MXTRTQ    OFFICE LIST SUB CODE                         
         MVI   AXOLESC,C' '        SET TO A ' ' IN THIS EXTRACT                 
         MVI   AXOLEOFC-1,MXTRTQ   OFFICE CODE                                  
         MVC   AXOLECCD,SXDTAGY    AGENCY 2-BYTE ALPHA                          
         TR    AXOLECCD,MXTRT                                                   
         MVC   AXOLEGC,OFFKOFF     OFFICE LIST CODE                             
         TR    AXOLEGC,MXTRT                                                    
         BR    RE                                                               
*                                                                               
AXOLECC  DC    A(BUFFER1)          OFFICE LIST ENTRY SAVE AREA                  
AXOLELIT DC    A(LITERAL)                                                       
*                                                                               
         DROP  R2,R3,R4,R6                                                      
         EJECT                                                                  
**********************************************************************          
* AXOPTC - EXTRACT OPTION RECORDS FOR AXTRACT                        *          
*                                                                    *          
* AL4 A(EXTRACT RECORD)                                              *          
* AL4 A(ACCFILE RECORD)                                              *          
* AL1 0=BUILD 'K' RECORD, 1=BUILD ALL RECORDS, 2=PASS BACK NEXT      *          
*     X'FF' RETURNED IF NO MORE RECORDS TO PROCESS                   *          
* AL3 0                                                              *          
* AL4 A(SXDTABD)                                                     *          
**********************************************************************          
         SPACE 1                                                                
AXOPTC   NMOD1 WORKL,*AXOPTC*,CLEAR=Y                                           
         USING WORKD,RC                                                         
         L     RA,AXOPTLIT                                                      
         USING LITERAL,RA                                                       
         ST    R1,APARM                                                         
         MVC   PARM,0(R1)          EXTRACT PARAMETER LIST                       
         LM    R2,R3,0(R1)                                                      
         L     R4,12(R1)                                                        
         USING AXOPTD,R2           R2=A(EXTRACT RECORD)                         
         USING POPRECD,R3          R3=A(ACCFILE RECORD)                         
         USING SXDTABD,R4          R4=A(SXDXTAB)                                
*                                                                               
         XC    AXOPTLEN,AXOPTLEN   RECORD LENGTH/TYPE/ENDOFREC                  
         MVC   AXOPTLEN(2),=Y(AXOPTDL)                                          
         MVC   AXOPTTYP,AXDOPTQ                                                 
         MVI   AXOPTDX,MXTRTQ                                                   
         MVI   AXOPTDX+1,MXTRTQ                                                 
         MVI   AXOPTKEY-1,MXTRTQ   COMPOSITE KEY                                
         MVI   AXOPTCCD-1,MXTRTQ   2-BYTE AGY ALPHA                             
         MVI   AXOPTTGC-1,MXTRTQ   WORKCODE GROUP CODE                          
         MVI   AXOPTTFC-1,MXTRTQ   WORKCODE                                     
         MVI   AXOPTOGC-1,MXTRTQ   OFFICE GROUP CODE                            
         MVI   AXOPTOFC-1,MXTRTQ   OFFICE CODE                                  
         MVI   AXOPTCLI-1,MXTRTQ   CLIENT CODE                                  
         MVI   AXOPTPRO-1,MXTRTQ   PRODUCT CODE                                 
         MVI   AXOPTJOB-1,MXTRTQ   JOB CODE                                     
         MVI   AXOPTMGC-1,MXTRTQ   MEDIA GROUP CODE                             
         MVI   AXOPTMCD-1,MXTRTQ   MEDIA CODE                                   
         MVI   AXOPTIPR-1,MXTRTQ   IS PRODUCT REQUIRED                          
         MVI   AXOPTIJR-1,MXTRTQ   IS JOB REQUIRED                              
         MVI   AXOPTITR-1,MXTRTQ   IS TAX REQUIRED                              
         MVI   AXOPTITR,C' '                                                    
         MVI   AXOPTLOC-1,MXTRTQ   LOCALITY CODE                                
         MVI   AXOPTTTC-1,MXTRTQ   TAX TASK CODE                                
         MVI   AXOPTIAC-1,MXTRTQ   INCOME ACCOUNT CODE                          
         MVI   AXOPTDTT-1,MXTRTQ   DEFAULT TIME TYPE                            
*                                                                               
         MVC   AXOPTCCD,SXDTAGY    AGENCY 2-BYTE ALPHA                          
         TR    AXOPTCCD,MXTRT                                                   
*                                                                               
         MVC   AXOPTTGC,POPKWGR    WORKGROUP CODE                               
         TR    AXOPTTGC,MXTRT                                                   
         MVC   AXOPTK1,AXOPTTGC    COMPOSITE KEY #1                             
*                                                                               
         MVC   AXOPTTFC,POPKWRK    WORKCODE                                     
         TR    AXOPTTFC,MXTRT                                                   
         MVC   AXOPTK2,AXOPTTFC    COMPOSITE KEY #2                             
*                                                                               
         MVC   AXOPTOGC,POPKOFG    OFFICE GROUP CODE                            
         TR    AXOPTOGC,MXTRT                                                   
         MVC   AXOPTK3,AXOPTOGC    COMPOSITE KEY #3                             
*                                                                               
         MVC   AXOPTOFC,POPKOFC    OFFICE CODE                                  
         TR    AXOPTOFC,MXTRT                                                   
         MVC   AXOPTK4,AXOPTOFC    COMPOSITE KEY #4                             
*                                                                               
         MVC   AXOPTCLI,POPKCLI    CLIENT CODE                                  
         TR    AXOPTCLI,MXTRT                                                   
         MVC   AXOPTK5,AXOPTCLI    COMPOSITE KEY #5                             
*                                                                               
         MVC   AXOPTPRO,POPKPRO    PRODUCT CODE                                 
         TR    AXOPTPRO,MXTRT                                                   
         MVC   AXOPTK6,AXOPTPRO    COMPOSITE KEY #6                             
*                                                                               
         MVC   AXOPTJOB(L'POPKJOB),POPKJOB    JOB CODE                          
         TR    AXOPTJOB,MXTRT                                                   
         MVC   AXOPTK7,AXOPTJOB    COMPOSITE KEY #7                             
*                                                                               
         MVC   AXOPTMGC,POPKMGR    MEDIA GROUP CODE                             
         TR    AXOPTMGC,MXTRT                                                   
         MVC   AXOPTK8,AXOPTMGC    COMPOSITE KEY #8                             
*                                                                               
         MVC   AXOPTMCD,POPKMED    MEDIA CODE                                   
         TR    AXOPTMCD,MXTRT                                                   
         MVC   AXOPTK9,AXOPTMCD    COMPOSITE KEY #9                             
*                                                                               
         LA    R6,POPRFST          FIRST RECORD DATA                            
AXOPT02  CLI   0(R6),0                                                          
         JE    EXIT                                                             
         CLI   0(R6),OPDELQ                                                     
         JE    AXOPT06                                                          
*                                                                               
AXOPT04  XR    R1,R1                                                            
         IC    R1,1(R6)                                                         
         AR    R6,R1                                                            
         J     AXOPT02                                                          
*                                                                               
         USING OPDELD,R6                                                        
AXOPT06  CLI   OPDNUM,OPNFPT       PRODUCT                                      
         JNE   AXOPT08                                                          
         MVC   AXOPTIPR,OPDDATA                                                 
         TR    AXOPTIPR,MXTRT                                                   
*                                                                               
AXOPT08  CLI   OPDNUM,OPNFJT       JOB                                          
         JNE   AXOPT10                                                          
         MVC   AXOPTIJR,OPDDATA                                                 
         TR    AXOPTIJR,MXTRT                                                   
*                                                                               
AXOPT10  CLI   OPDNUM,OPNTOT       DEFAULT TIME TYPE                            
         JNE   AXOPT12                                                          
         MVC   AXOPTDTT,OPDDATA                                                 
         TR    AXOPTDTT,MXTRT                                                   
*                                                                               
AXOPT12  CLI   OPDNUM,OPNTAX       TAX                                          
         JNE   AXOPT14                                                          
         MVI   AXOPTITR,C'0'       NO                                           
         CLI   OPDDATA,C'Y'                                                     
         JNE   *+8                                                              
         MVI   AXOPTITR,C'1'       YES                                          
*                                                                               
AXOPT14  XR    RF,RF                                                            
         CLI   OPDNUM,OPNTXLOC     LOCALITY                                     
         JNE   AXOPT16                                                          
         IC    RF,OPDLN                                                         
         SH    RF,=Y(OPDLN1Q+1)                                                 
         BM    AXOPT16                                                          
         MVC   AXOPTLOC(0),OPDDATA                                              
         EX    RF,*-6                                                           
         TR    AXOPTLOC,MXTRT                                                   
*                                                                               
AXOPT16  CLI   OPDNUM,OPNTXWC      WORK CODE                                    
         JNE   AXOPT18                                                          
         IC    RF,OPDLN                                                         
         SH    RF,=Y(OPDLN1Q+1)                                                 
         JM    AXOPT18                                                          
         MVC   AXOPTTTC(0),OPDDATA                                              
         EX    RF,*-6                                                           
         TR    AXOPTTTC,MXTRT                                                   
*                                                                               
AXOPT18  DS    0H                  INCOME ACCOUNT                               
*&&US*&& CLI   OPDNUM,OPNICA                                                    
*&&UK*&& CLI   OPDNUM,OPNINCAC                                                  
         JNE   AXOPT20                                                          
         IC    RF,OPDLN                                                         
         SH    RF,=Y(OPDLN1Q+2)                                                 
         JM    AXOPT20                                                          
         MVC   AXOPTIAC(0),OPDDATA+1                                            
         EX    RF,*-6                                                           
         TR    AXOPTIAC,MXTRT                                                   
*                                                                               
AXOPT20  J     AXOPT04                                                          
*                                                                               
AXOPTLIT DC    A(LITERAL)                                                       
*                                                                               
         DROP  R2,R3,R4,R6                                                      
         EJECT                                                                  
***********************************************************************         
* AXPEDC - EXTRACT PERIOD RECORDS FOR AXTRACT                         *         
*                                                                     *         
* AL4 A(EXTRACT RECORD)                                               *         
* AL4 A(ACCFILE RECORD)                                               *         
* AL1 0=BUILD 'K' RECORD, 1=BUILD ALL RECORDS, 2=PASS BACK NEXT       *         
*     X'FF' RETURNED IF NO MORE RECORDS TO PROCESS                    *         
* AL3 0                                                               *         
* AL4 A(SXDTABD)                                                      *         
***********************************************************************         
         SPACE 1                                                                
AXPEDC   NMOD1 WORKL,*AXPEDC*,CLEAR=Y                                           
         USING WORKD,RC                                                         
         L     RA,AXPEDLIT                                                      
         USING LITERAL,RA                                                       
         ST    R1,APARM                                                         
         MVC   PARM,0(R1)          EXTRACT PARAMETER LIST                       
         LM    R2,R3,0(R1)                                                      
         L     R4,12(R1)                                                        
         USING AXPEDD,R2           R2=A(EXTRACT RECORD)                         
         USING CASRECD,R3          R3=A(ACCFILE RECORD)                         
         USING SXDTABD,R4          R4=A(SXDXTAB)                                
*                                                                               
         CLI   8(R1),1                                                          
         JL    AXPEDKIL            BUILD K RECORD                               
         JE    AXPEDSER            BUILD SERIES OF RECORDS                      
         JH    AXPEDNXT            OUTPUT NEXT RECORD                           
         EJECT                                                                  
***********************************************************************         
* BUILD 'K' RECORD AND EXIT                                           *         
***********************************************************************         
         SPACE 1                                                                
AXPEDKIL BRAS  RE,AXPEDBLK                                                      
         J     EXIT                                                             
         SPACE 1                                                                
***********************************************************************         
* BUILD SERIES OF RECORDS FOR THIS RECORD                             *         
***********************************************************************         
         SPACE 1                                                                
AXPEDSER BRAS  RE,AXPEDBLK         SET UP BLANK RECORD (KEY DETAIL)             
*                                                                               
         LH    RF,=Y(AXPEDDL)      LENGTH OF A PERIOD RECORD                    
         STH   RF,RECLEN                                                        
         L     R2,AXPEDCC          WHERE RECORDS TO BE BUILT                    
         ST    R2,ANEXT            FOR NEXT TIME IN                             
         XC    0(8,R2),0(R2)                                                    
*                                                                               
         LA    R6,CASRFST          FIRST ONE RECORD DATA                        
AXPED02  CLI   0(R6),0                                                          
         JE    EXIT                                                             
         CLI   0(R6),TMPELQ                                                     
         JE    AXPED06                                                          
*                                                                               
AXPED04  XR    R1,R1                                                            
         IC    R1,1(R6)                                                         
         AR    R6,R1                                                            
         J     AXPED02                                                          
*                                                                               
AXPED06  L     RE,AXREC            BLANK RECORD HERE                            
         LR    R0,R2                                                            
         LH    RF,RECLEN           LENGTH OF A BLANK RECORD                     
         LR    R1,RF                                                            
         MVCL  R0,RE               COPY BLANK RECORD INTO BLOCK                 
*                                                                               
         LH    RF,RECLEN                                                        
         AR    RF,R2                                                            
         XC    0(8,RF),0(RF)       CLEAR NEXT IN BUFFER                         
*                                                                               
         USING TMPELD,R6                                                        
         EDIT  TMPNUMB,(3,AXPEDPN),FILL=0                                       
         GOTO1 VDATCON,PLIST,(1,TMPSTART),(10,AXPEDSDT),0                       
*&&US                                                                           
         XC    AXPEDSDT(2),AXPEDSDT+3                                           
         XC    AXPEDSDT+3(2),AXPEDSDT                                           
         XC    AXPEDSDT(2),AXPEDSDT+3                                           
*&&                                                                             
         TR    AXPEDSDT,MXTRT                                                   
         GOTO1 VDATCON,PLIST,(1,TMPEND),(10,AXPEDEDT),0                         
*&&US                                                                           
         XC    AXPEDEDT(2),AXPEDEDT+3                                           
         XC    AXPEDEDT+3(2),AXPEDEDT                                           
         XC    AXPEDEDT(2),AXPEDEDT+3                                           
*&&                                                                             
         TR    AXPEDEDT,MXTRT                                                   
*                                                                               
         MVC   WORK(L'TMPMTH),TMPMTH                                            
         MVI   WORK+L'TMPMTH,X'01'                                              
         GOTO1 VDATCON,PLIST,(1,WORK),(X'20',WORK+6)                            
         MVI   AXPEDMAY,C'0'                                                    
         MVC   AXPEDMAY+1(2),WORK+6                                             
         MVI   AXPEDMAM,C'0'                                                    
         MVC   AXPEDMAM+1(2),WORK+8                                             
*                                                                               
*        ZAP   DUB,=P'0'                                                        
*        OI    DUB+L'DUB-1,X'0F'                                                
*        MVO   DUB,TMPMTH(1)       MOA YEAR                                     
*        UNPK  AXPEDMAY,DUB                                                     
*        MVO   DUB,TMPMTH+1(1)     MOA MONTH                                    
*        UNPK  AXPEDMAM,DUB                                                     
*                                                                               
         AH    R2,RECLEN           NEXT EMPTY SPACE IN BUFFER                   
         J     AXPED04                                                          
         SPACE 1                                                                
***********************************************************************         
* GET NEXT RECORD AND PASS IT BACK                                    *         
***********************************************************************         
         SPACE 1                                                                
AXPEDNXT L     RE,ANEXT                                                         
         OC    0(8,RE),0(RE)       RECORD TO PASS BACK?                         
         JNZ   *+12                                                             
         MVI   8(R1),X'FF'         NO MORE RECORDS                              
         J     EXIT                                                             
*                                                                               
         L     RE,ANEXT            COPY IN NEXT RECORD                          
         LA    R0,AXPEDD                                                        
         LH    RF,=Y(AXPEDDL)                                                   
         LR    R1,RF                                                            
         MVCL  R0,RE                                                            
*                                                                               
         LH    RF,=Y(AXPEDDL)      UPDATE POINTER                               
         L     RE,ANEXT                                                         
         AR    RE,RF                                                            
         ST    RE,ANEXT                                                         
         J     EXIT                                                             
         SPACE 1                                                                
***********************************************************************         
* BUILD BLANK RECORD AND INPUT KEY FIELDS                             *         
***********************************************************************         
         SPACE 1                                                                
AXPEDBLK XC    AXPEDLEN,AXPEDLEN   RECORD LENGTH/TYPE/ENDOFREC                  
         MVC   AXPEDLEN(2),=Y(AXPEDDL)                                          
         MVC   AXPEDTYP,AXDPEDQ                                                 
         MVI   AXPEDDX,MXTRTQ                                                   
         MVI   AXPEDDX+1,MXTRTQ                                                 
         MVI   AXPEDCCD-1,MXTRTQ   2-BYTE AGY ALPHA                             
         MVI   AXPEDOFC-1,MXTRTQ   OFFICE CODE                                  
         MVI   AXPEDPN-1,MXTRTQ    PERIOD NUMBER                                
         MVI   AXPEDSDT-1,MXTRTQ   START DATE                                   
         MVI   AXPEDEDT-1,MXTRTQ   END DATE                                     
         MVI   AXPEDMAY-1,MXTRTQ   MOA YEAR                                     
         MVI   AXPEDMAM-1,MXTRTQ   MOA MONTH                                    
         MVC   AXPEDCCD,SXDTAGY    AGENCY 2-BYTE ALPHA                          
         TR    AXPEDCCD,MXTRT                                                   
         MVC   AXPEDOFC,CASKOFC    OFFICE CODE                                  
         TR    AXPEDOFC,MXTRT                                                   
         XR    R6,R6                                                            
         ICM   R6,2,CASKEMOA       END YY                                       
         C     R6,=A(Y2000)                                                     
         BL    *+8                                                              
         S     R6,=A(Y2000)                                                     
         ICM   R6,1,CASKEMOA+1     END YYMM                                     
         SRDL  R6,4                MXXXXXXX                                     
         SRL   R7,4                0MXXXXXX                                     
         SRDL  R6,4                M0MXXXXX                                     
         SRL   R7,4                0M0MXXXX                                     
         SRDL  R6,4                M0M0MXXX                                     
         SRL   R7,4                0M0M0MXX                                     
         SRDL  R6,4                M0M0M0MX                                     
         SRL   R7,4                0M0M0M0M                                     
         STCM  R7,15,AXPEDEYM                                                   
         OC    AXPEDEYM,=C'0000'   END MONTH                                    
         XR    R6,R6                                                            
         ICM   R6,2,CASKSMOA       START YY                                     
         C     R6,=A(Y2000)                                                     
         BL    *+8                                                              
         S     R6,=A(Y2000)                                                     
         ICM   R6,1,CASKSMOA+1     START YYMM                                   
         SRDL  R6,4                MXXXXXXX                                     
         SRL   R7,4                0MXXXXXX                                     
         SRDL  R6,4                M0MXXXXX                                     
         SRL   R7,4                0M0MXXXX                                     
         SRDL  R6,4                M0M0MXXX                                     
         SRL   R7,4                0M0M0MXX                                     
         SRDL  R6,4                M0M0M0MX                                     
         SRL   R7,4                0M0M0M0M                                     
         STCM  R7,15,AXPEDSYM                                                   
         OC    AXPEDSYM,=C'0000'   START MONTH                                  
         BR    RE                                                               
*                                                                               
AXPEDCC  DC    A(BUFFER1)          PERIOD SAVE AREA                             
AXPEDLIT DC    A(LITERAL)                                                       
*                                                                               
         DROP  R2,R3,R4,R6                                                      
         EJECT                                                                  
***********************************************************************         
* AXPERC - EXTRACT PERSON RECORDS FOR AXTRACT                         *         
*                                                                     *         
* AL4 A(EXTRACT RECORD)                                               *         
* AL4 A(ACCFILE RECORD)                                               *         
* AL4 0=ACCFILE-TO-EXTRACT                                            *         
* AL4 A(SXDTABD)                                                      *         
***********************************************************************         
         SPACE 1                                                                
AXPERC   NMOD1 WORKL,*AXPERC*,CLEAR=Y                                           
         USING WORKD,RC                                                         
         L     RA,AXPERLIT                                                      
         USING LITERAL,RA                                                       
         ST    R1,APARM                                                         
         MVC   PARM,0(R1)          EXTRACT PARAMETER LIST                       
         LM    R2,R3,0(R1)                                                      
         L     R4,12(R1)                                                        
         USING AXPERD,R2           R2=A(EXTRACT RECORD)                         
         USING PERRECD,R3          R3=A(ACCFILE RECORD)                         
         USING SXDTABD,R4          R4=A(SXDTABD)                                
*                                                                               
         XC    AXPERLEN,AXPERLEN   RECORD LENGTH/TYPE/ENDOFREC                  
         MVC   AXPERLEN(2),=Y(AXPERDL)                                          
         MVC   AXPERTYP,AXDPERQ                                                 
         MVI   AXPERDX,MXTRTQ                                                   
         MVI   AXPERDX+1,MXTRTQ                                                 
         MVI   AXPERCCD-1,MXTRTQ   COMPANY CODE                                 
         MVI   AXPERCSC-1,MXTRTQ   CONSYSPERCODE                                
         MVI   AXPERPC-1,MXTRTQ    PERSON CODE                                  
         MVI   AXPERFN-1,MXTRTQ    FIRST NAME                                   
         MVI   AXPERLN-1,MXTRTQ    LAST NAME                                    
         MVI   AXPERHDT-1,MXTRTQ   HIRE DATE                                    
         MVI   AXPERTDT-1,MXTRTQ   TERMINATION DATE                             
*                                                                               
         MVC   AXPERCCD,SXDTAGY    COMPANY CODE                                 
         TR    AXPERCCD,MXTRT                                                   
         MVC   AXPERPC,PERKCODE    PERSON CODE                                  
         TR    AXPERPC,MXTRT                                                    
*                                                                               
         LA    R5,PERRFST          R5=A(FIRST ELEMENT)                          
AXPER02  CLI   0(R5),0             GET ELEMENT FROM RECORD                      
         JE    EXIT                                                             
         CLI   0(R5),EMPELQ        EMPLOYEE STATUS ELEMENT                      
         JE    AXPER06                                                          
         CLI   0(R5),PIDELQ        PERSON ID ELEMENT                            
         JE    AXPER08                                                          
         CLI   0(R5),GPNELQ        NAME ELEMENT                                 
         JE    AXPER10                                                          
*                                                                               
AXPER04  XR    RF,RF                                                            
         IC    RF,1(R5)                                                         
         AR    R5,RF                                                            
         J     AXPER02                                                          
*                                                                               
         USING EMPELD,R5                                                        
AXPER06  GOTO1 VDATCON,PLIST,(1,EMPHIR),(10,AXPERHDT),0                         
*&&US                                                                           
         XC    AXPERHDT(2),AXPERHDT+3                                           
         XC    AXPERHDT+3(2),AXPERHDT                                           
         XC    AXPERHDT(2),AXPERHDT+3                                           
*&&                                                                             
         TR    AXPERHDT,MXTRT                                                   
         GOTO1 VDATCON,PLIST,(1,EMPTRM),(10,AXPERTDT),0                         
*&&US                                                                           
         XC    AXPERTDT(2),AXPERTDT+3                                           
         XC    AXPERTDT+3(2),AXPERTDT                                           
         XC    AXPERTDT(2),AXPERTDT+3                                           
*&&                                                                             
         TR    AXPERTDT,MXTRT                                                   
         J     AXPER04                                                          
*                                                                               
         USING PIDELD,R5                                                        
AXPER08  XR    R0,R0                                                            
         ICM   R0,3,PIDNO          GET PID NUMBER                               
         CVD   R0,DUB                                                           
         OI    DUB+L'DUB-1,X'0F'                                                
         UNPK  AXPERCSC,DUB                                                     
         J     AXPER04                                                          
*                                                                               
         USING GPNELD,R5                                                        
AXPER10  XR    RF,RF                                                            
         IC    RF,GPNLN                                                         
         SH    RF,=H'4'                                                         
         JM    AXPER04                                                          
         CLI   GPNTYP,GPNTLST      WHICH TYPE OF NAME?                          
         JE    AXPER12                                                          
*                                                                               
         MVC   AXPERFN,SPACES                                                   
         MVC   AXPERFN(0),GPNNME                                                
         EX    RF,*-6                                                           
         TR    AXPERFN,MXTRT                                                    
         J     AXPER04                                                          
*                                                                               
AXPER12  MVC   AXPERLN,SPACES                                                   
         MVC   AXPERLN(0),GPNNME                                                
         EX    RF,*-6                                                           
         TR    AXPERLN,MXTRT                                                    
         J     AXPER04                                                          
*                                                                               
AXPERLIT DC    A(LITERAL)                                                       
*                                                                               
         DROP  R2,R3,R4,R5                                                      
         EJECT                                                                  
***********************************************************************         
* AXPEAC - EXTRACT PERSON ASSIGNMENT RECORDS FOR AXTRACT              *         
*                                                                     *         
* AL4 A(EXTRACT RECORD)                                               *         
* AL4 A(ACCFILE RECORD)                                               *         
* AL1 0=BUILD 'K' RECORD, 1=BUILD ALL RECORDS, 2=PASS BACK NEXT       *         
*     X'FF' RETURNED IF NO MORE RECORDS TO PROCESS                    *         
* AL3 0                                                               *         
* AL4 A(SXDTABD)                                                      *         
***********************************************************************         
         SPACE 1                                                                
AXPEAC   NMOD1 WORKL,*AXPEAC*,CLEAR=Y                                           
         USING WORKD,RC                                                         
         L     RA,AXPEALIT                                                      
         USING LITERAL,RA                                                       
         ST    R1,APARM                                                         
         MVC   PARM,0(R1)          EXTRACT PARAMETER LIST                       
         LM    R2,R3,0(R1)                                                      
         L     R4,12(R1)                                                        
         USING AXPEAD,R2           R2=A(EXTRACT RECORD)                         
         USING PERRECD,R3          R3=A(ACCFILE RECORD)                         
         USING SXDTABD,R4          R4=A(SXDXTAB)                                
*                                                                               
         CLI   8(R1),1                                                          
         JL    AXPEAKIL            BUILD K RECORD                               
         JE    AXPEASER            BUILD SERIES OF RECORDS                      
         J     AXPEANXT            OUTPUT NEXT RECORD                           
*                                                                               
AXPEALIT DC    A(LITERAL)                                                       
         EJECT                                                                  
***********************************************************************         
* BUILD 'K' RECORD AND EXIT                                           *         
***********************************************************************         
         SPACE 1                                                                
AXPEAKIL BRAS  RE,AXPEABLK                                                      
         J     EXIT                                                             
         SPACE 1                                                                
***********************************************************************         
* BUILD SERIES OF RECORDS FOR THIS RECORD                             *         
***********************************************************************         
         SPACE 1                                                                
AXPEASER BRAS  RE,AXPEABLK         SET UP BLANK RECORD (KEY DETAIL)             
*                                                                               
         LH    RF,=Y(AXPEADL)                                                   
         STH   RF,RECLEN                                                        
         L     R2,AXPEACC          WHERE RECORDS TO BE BUILT                    
         ST    R2,ANEXT            FOR NEXT TIME IN                             
         XC    0(8,R2),0(R2)                                                    
*                                                                               
         LA    R6,PERRFST          FIRST ONE RECORD DATA                        
AXPEA02  CLI   0(R6),0                                                          
         JE    EXIT                                                             
         CLI   0(R6),LOCELQ                                                     
         JE    AXPEA06                                                          
*                                                                               
AXPEA04  XR    R1,R1                                                            
         IC    R1,1(R6)                                                         
         AR    R6,R1                                                            
         J     AXPEA02                                                          
*                                                                               
AXPEA06  L     RE,AXREC            BLANK RECORD                                 
         LA    R0,AXPEAD                                                        
         LH    RF,RECLEN                                                        
         LR    R1,RF                                                            
         MVCL  R0,RE               COPY IN BLANK RECORD                         
*                                                                               
         LH    RF,RECLEN                                                        
         AR    RF,R2                                                            
         XC    0(8,RF),0(RF)       BLANK OFF START OF NEXT SLOT                 
*                                                                               
         USING LOCELD,R6                                                        
         GOTO1 VDATCON,PLIST,(1,LOCSTART),(10,AXPEASTD),0                       
*&&US                                                                           
         XC    AXPEASTD(2),AXPEASTD+3                                           
         XC    AXPEASTD+3(2),AXPEASTD                                           
         XC    AXPEASTD(2),AXPEASTD+3                                           
*&&                                                                             
         TR    AXPEASTD,MXTRT                                                   
         GOTO1 VDATCON,PLIST,(1,LOCEND),(10,AXPEAEND),0                         
*&&US                                                                           
         XC    AXPEAEND(2),AXPEAEND+3                                           
         XC    AXPEAEND+3(2),AXPEAEND                                           
         XC    AXPEAEND(2),AXPEAEND+3                                           
*&&                                                                             
         TR    AXPEAEND,MXTRT                                                   
         MVC   AXPEAOFC,LOCOFF                                                  
         TR    AXPEAOFC,MXTRT                                                   
         MVC   AXPEADEC,LOCDEPT                                                 
         TR    AXPEADEC,MXTRT                                                   
         MVC   AXPEASDC,LOCSUB                                                  
         TR    AXPEASDC,MXTRT                                                   
         EDIT  LOCATTR,(3,AXPEAATT),ZERO=NOBLANK,FILL=0                         
         EDIT  LOCSTAT,(3,AXPEASTA),ZERO=NOBLANK,FILL=0                         
*                                                                               
         AH    R2,RECLEN           NEXT SLOT                                    
         J     AXPEA04                                                          
         EJECT                                                                  
***********************************************************************         
* GET NEXT RECORD AND PASS IT BACK                                    *         
***********************************************************************         
         SPACE 1                                                                
AXPEANXT L     RE,ANEXT                                                         
         OC    0(8,RE),0(RE)       RECORD TO PASS BACK?                         
         JNZ   *+12                                                             
         MVI   8(R1),X'FF'         NO MORE RECORDS                              
         J     EXIT                                                             
*                                                                               
         L     RE,ANEXT            MOVE IN RECORD                               
         LA    R0,AXPEAD                                                        
         LH    RF,=Y(AXPEADL)                                                   
         LR    R1,RF                                                            
         MVCL  R0,RE                                                            
*                                                                               
         LH    RF,=Y(AXPEADL)      UPDATE POINTER                               
         L     RE,ANEXT                                                         
         AR    RE,RF                                                            
         ST    RE,ANEXT                                                         
         J     EXIT                                                             
         SPACE 1                                                                
***********************************************************************         
* BUILD BLANK RECORD AND INPUT KEY FIELDS                             *         
***********************************************************************         
         SPACE 1                                                                
AXPEABLK XC    AXPEALEN,AXPEALEN   RECORD LENGTH/TYPE/ENDOFREC                  
         MVC   AXPEALEN(2),=Y(AXPEADL)                                          
         MVC   AXPEATYP,AXDPEAQ                                                 
         MVI   AXPEADX,MXTRTQ                                                   
         MVI   AXPEADX+1,MXTRTQ                                                 
         MVI   AXPEACCD-1,MXTRTQ   2-BYTE AGY ALPHA                             
         MVI   AXPEACSC-1,MXTRTQ   CONSYSPERSONCODE                             
         MVI   AXPEAPC-1,MXTRTQ    PERSON CODE                                  
         MVI   AXPEASTD-1,MXTRTQ   START DATE                                   
         MVI   AXPEAEND-1,MXTRTQ   END DATE                                     
         MVI   AXPEAOFC-1,MXTRTQ   OFFICE CODE                                  
         MVI   AXPEADEC-1,MXTRTQ   DEPARTMENT CODE                              
         MVI   AXPEASDC-1,MXTRTQ   SUB-DEPT CODE                                
*        MVI   AXPEAMGC-1,MXTRTQ   MANAGER GROUP CODE                           
         MVI   AXPEAATT-1,MXTRTQ   ATTRIBUTES                                   
         MVI   AXPEASTA-1,MXTRTQ   STATUS                                       
         MVC   AXPEACCD,SXDTAGY    AGENCY 2-BYTE ALPHA                          
         TR    AXPEACCD,MXTRT                                                   
         MVC   AXPEAPC,PERKCODE    PERSON CODE                                  
         TR    AXPEAPC,MXTRT                                                    
*                                                                               
         ZAP   DUB,=P'0'           DUB HOLDS PID NUMBER                         
         XR    RF,RF                                                            
         LA    R5,PERRFST          R5=A(FIRST ELEMENT)                          
AXBPEA02 CLI   0(R5),0             GET ELEMENT FROM RECORD                      
         JE    AXBPEA06                                                         
         CLI   0(R5),PIDELQ        PERSON ID ELEMENT                            
         JE    AXBPEA04                                                         
         IC    RF,1(R5)                                                         
         AR    R5,RF                                                            
         J     AXBPEA02                                                         
*                                                                               
         USING PIDELD,R5                                                        
AXBPEA04 XR    R0,R0                                                            
         ICM   R0,3,PIDNO                                                       
         CVD   R0,DUB                                                           
         OI    DUB+L'DUB-1,X'0F'                                                
*                                                                               
AXBPEA06 UNPK  AXPEACSC,DUB        OUTPUT CONSYSPERCODE                         
         BR    RE                                                               
*                                                                               
AXPEACC  DC    A(BUFFER1)          PERSON ASSIGNMENT SAVE AREA                  
*                                                                               
         DROP  R2,R3,R4,R5,R6                                                   
         EJECT                                                                  
***********************************************************************         
* AXPROC - EXTRACT PRODUCT RECORDS FOR AXTRACT                        *         
*                                                                     *         
* AL4 A(EXTRACT RECORD)                                               *         
* AL4 A(ACCFILE RECORD)                                               *         
* AL4 0=ACCFILE-TO-EXTRACT                                            *         
* AL4 A(SXDTABD)                                                      *         
* AL4 A(LEVELS) - CAN GET LENGTHS OF LEVELS FROM HERE                 *         
***********************************************************************         
         SPACE 1                                                                
AXPROC   NMOD1 WORKL,*AXPROC*,CLEAR=Y                                           
         USING WORKD,RC                                                         
         L     RA,AXPROLIT                                                      
         USING LITERAL,RA                                                       
         ST    R1,APARM                                                         
         MVC   PARM,0(R1)          EXTRACT PARAMETER LIST                       
         LM    R2,R3,0(R1)                                                      
         L     R4,12(R1)                                                        
         MVC   ALEVELS,16(R1)                                                   
         USING AXPROD,R2           R2=A(EXTRACT RECORD)                         
         USING ACTKEY,R3           R3=A(ACCFILE RECORD)                         
         USING SXDTABD,R4          R5=A(SXDTABD)                                
*                                                                               
         XC    AXPROLEN,AXPROLEN   RECORD LENGTH/TYPE/ENDOFREC                  
         MVC   AXPROLEN(2),=Y(AXPRODL)                                          
         MVC   AXPROTYP,AXDPROQ                                                 
         MVI   AXPRODX,MXTRTQ                                                   
         MVI   AXPRODX+1,MXTRTQ                                                 
         MVI   AXPROCCD-1,MXTRTQ   2-BYTE AGY ALPHA                             
         MVI   AXPROCLC-1,MXTRTQ   PROENT CODE                                  
         MVI   AXPROPRO-1,MXTRTQ   PRODUCT CODE                                 
         MVI   AXPRONAM-1,MXTRTQ   PRODUCT NAME                                 
         MVI   AXPROF1-1,MXTRTQ    ACCOUNT FILTER 1                             
         MVI   AXPROF2-1,MXTRTQ    ACCOUNT FILTER 2                             
         MVI   AXPROF3-1,MXTRTQ    ACCOUNT FILTER 3                             
         MVI   AXPROF4-1,MXTRTQ    ACCOUNT FILTER 4                             
         MVI   AXPROF5-1,MXTRTQ    ACCOUNT FILTER 5                             
         MVI   AXPROOFC-1,MXTRTQ   OFFICE CODE                                  
         MVI   AXPROCAC-1,MXTRTQ   COSTING ACCOUNT CODE                         
         MVI   AXPROLCK-1,MXTRTQ   ACCOUNT IS LOCKED                            
*                                                                               
         MVC   AXPROCCD,SXDTAGY    2-BYTE AGY ALPHA                             
         TR    AXPROCCD,MXTRT                                                   
         MVC   AXPROCLC,SPACES     CLIENT                                       
         L     R6,ALEVELS                                                       
         XR    RF,RF                                                            
         IC    RF,0(R6)                                                         
         BCTR  RF,0                                                             
         MVC   AXPROCLC(0),ACTKACT                                              
         EX    RF,*-6                                                           
         TR    AXPROCLC,MXTRT                                                   
*                                                                               
         LA    RE,ACTKACT+1(RF)                                                 
         MVC   AXPROPRO,SPACES     PRODUCT                                      
         XR    RF,RF                                                            
         IC    RF,1(R6)                                                         
         BCTR  RF,0                                                             
         MVC   AXPROPRO(0),0(RE)                                                
         EX    RF,*-6                                                           
         TR    AXPROPRO,MXTRT                                                   
*                                                                               
         LA    R5,ACTRFST          R5=A(FIRST ELEMENT)                          
AXPRO02  CLI   0(R5),0             GET ELEMENT FROM RECORD                      
         JE    EXIT                                                             
         CLI   0(R5),NAMELQ        NAME ELEMENT                                 
         JE    AXPRO06                                                          
         CLI   0(R5),RSTELQ        RECORD STATUS ELEMENT                        
         JE    AXPRO08                                                          
         CLI   0(R5),PPRELQ                                                     
         JE    AXPRO10                                                          
*                                                                               
AXPRO04  XR    RF,RF               BUMP TO NEXT ELEMENT                         
         IC    RF,1(R5)                                                         
         AR    R5,RF                                                            
         J     AXPRO02                                                          
*                                                                               
         USING NAMELD,R5                                                        
AXPRO06  MVC   AXPRONAM,SPACES                                                  
         SR    RF,RF                                                            
         IC    RF,NAMLN                                                         
         SH    RF,=H'3'                                                         
         JM    AXPRO04                                                          
         MVC   AXPRONAM(0),NAMEREC                                              
         EX    RF,*-6                                                           
         TR    AXPRONAM,MXTRT                                                   
         J     AXPRO04                                                          
*                                                                               
         USING RSTELD,R5                                                        
AXPRO08  MVC   AXPROF1,RSTFILT1    ACCOUNT FILTER 1                             
         TR    AXPROF1,MXTRT                                                    
         MVC   AXPROF2,RSTFILT2    ACCOUNT FILTER 2                             
         TR    AXPROF2,MXTRT                                                    
         MVC   AXPROF3,RSTFILT3    ACCOUNT FILTER 3                             
         TR    AXPROF3,MXTRT                                                    
         MVC   AXPROF4,RSTFILT4    ACCOUNT FILTER 4                             
         TR    AXPROF4,MXTRT                                                    
         MVC   AXPROF5,RSTFILT5    ACCOUNT FILTER 5                             
         TR    AXPROF5,MXTRT                                                    
         MVI   AXPROLCK,C'Y'                                                    
         TM    RSTSTAT1,RSTSACIL   ACCOUNT IS LOCKED                            
         JO    AXPRO04                                                          
*                                                                               
         MVI   AXPROLCK,C'N'                                                    
         CLI   RSTLN,RSTLN3Q       ENSURE ELEMENT LONG ENOUGH                   
         JL    AXPRO04                                                          
         TM    RSTSTAT5,RSTSNOTS   EXCLUDE FROM TEMPO                           
         JZ    *+8                                                              
         MVI   AXPROLCK,C'Y'                                                    
         J     AXPRO04                                                          
*                                                                               
         USING PPRELD,R5                                                        
AXPRO10  MVC   AXPROOFC,PPRGAOFF  OFFICE CODE                                   
         TR    AXPROOFC,MXTRT                                                   
         MVC   AXPROCAC,PPRCOSTA  COSTING ACCOUNT CODE                          
         TR    AXPROCAC,MXTRT                                                   
         J     AXPRO04                                                          
*                                                                               
AXPROLIT DC    A(LITERAL)                                                       
*                                                                               
         DROP  R2,R3,R4,R5                                                      
         EJECT                                                                  
***********************************************************************         
* AXRATC - EXTRACT RATE RECORDS FOR AXTRACT                           *         
*                                                                     *         
* AL4 A(EXTRACT RECORD)                                               *         
* AL4 A(ACCFILE RECORD)                                               *         
* AL1 0=BUILD 'K' RECORD, 1=BUILD ALL RECORDS, 2=PASS BACK NEXT       *         
*     X'FF' RETURNED IF NO MORE RECORDS TO PROCESS                    *         
* AL3 0                                                               *         
* AL4 A(SXDTABD)                                                      *         
* AL4 A(LEVELS) - CAN GET LENGTHS OF LEVELS FROM HERE                 *         
***********************************************************************         
         SPACE 1                                                                
AXRATC   NMOD1 WORKL,*AXRATC*,CLEAR=Y                                           
         USING WORKD,RC                                                         
         L     RA,AXRATLIT                                                      
         USING LITERAL,RA                                                       
         ST    R1,APARM                                                         
         MVC   PARM,0(R1)          EXTRACT PARAMETER LIST                       
         MVC   ALEVELS,16(R1)      LEVEL STRUCTURE                              
         MVC   ASJLVLS,20(R1)      LEVEL STRUCTURE FOR PRODUCTION               
         LM    R2,R3,0(R1)                                                      
         L     R4,12(R1)                                                        
         USING AXRATD,R2           R2=A(EXTRACT RECORD)                         
         USING SXDTABD,R4          R4=A(SXDXTAB)                                
*                                                                               
         CLI   8(R1),1                                                          
         JL    AXRATKIL            BUILD K RECORD                               
         JH    AXRATNXT            OUTPUT NEXT RECORD                           
         JE    AXRATSER            BUILD SERIES OF RECORDS                      
*                                                                               
AXRATLIT DC    A(LITERAL)                                                       
         EJECT                                                                  
***********************************************************************         
* BUILD 'K' RECORD AND EXIT                                           *         
***********************************************************************         
         SPACE 1                                                                
AXRATKIL BRAS  RE,AXRATBLK                                                      
         J     EXIT                                                             
         SPACE 1                                                                
***********************************************************************         
* BUILD SERIES OF RECORDS FOR THIS RECORD                             *         
***********************************************************************         
         SPACE 1                                                                
         USING PCRRECD,R3          R3=A(ACCFILE RECORD)                         
AXRATSER BRAS  RE,AXRATBLK         SET UP BLANK RECORD (KEY DETAIL)             
*                                                                               
         LH    RF,=Y(AXRATDL)                                                   
         STH   RF,RECLEN                                                        
         L     R2,AXRATCC          WHERE RECORDS TO BE BUILT                    
         ST    R2,ANEXT            FOR NEXT TIME IN                             
         XC    0(8,R2),0(R2)                                                    
*                                                                               
         LA    R6,PCRRFST          FIRST ONE RECORD DATA                        
AXRAT02  CLI   0(R6),0                                                          
         JE    EXIT                                                             
         CLI   0(R6),TCIELQ                                                     
         JE    AXRAT06                                                          
*                                                                               
AXRAT04  XR    R1,R1                                                            
         IC    R1,1(R6)                                                         
         AR    R6,R1                                                            
         J     AXRAT02                                                          
*                                                                               
AXRAT06  L     RE,AXREC            BLANK RECORD                                 
         LR    R0,R2                                                            
         LH    RF,RECLEN           LENGTH OF A BLANK RECORD                     
         LR    R1,RF                                                            
         MVCL  R0,RE               COPY IN BLANK RECORD                         
*                                                                               
         LH    RF,RECLEN           LENGTH OF A BLANK RECORD                     
         AR    RF,R2                                                            
         XC    0(8,RF),0(RF)       BLANK OFF START OF NEXT ONE                  
*                                                                               
         USING TCIELD,R6                                                        
         EDIT  TCIAMT,(10,AXRATE),ZERO=NOBLANK,FILL=0                           
         GOTO1 VDATCON,PLIST,(1,TCIDTE),(10,AXRATEFD),0                         
*&&US                                                                           
         XC    AXRATEFD(2),AXRATEFD+3                                           
         XC    AXRATEFD+3(2),AXRATEFD                                           
         XC    AXRATEFD(2),AXRATEFD+3                                           
*&&                                                                             
         AH    R2,RECLEN           NEXT IN BUFFER                               
         J     AXRAT04                                                          
         SPACE 1                                                                
***********************************************************************         
* GET NEXT RECORD AND PASS IT BACK                                    *         
***********************************************************************         
         SPACE 1                                                                
AXRATNXT L     RE,ANEXT                                                         
         OC    0(8,RE),0(RE)       RECORD TO PASS BACK?                         
         JNZ   *+12                                                             
         MVI   8(R1),X'FF'         NO MORE RECORDS                              
         J     EXIT                                                             
*                                                                               
         L     RE,ANEXT                                                         
         LA    R0,AXRATD                                                        
         LH    RF,=Y(AXRATDL)                                                   
         LR    R1,RF                                                            
         MVCL  R0,RE                                                            
*                                                                               
         LH    RF,=Y(AXRATDL)                                                   
         L     RE,ANEXT                                                         
         AR    RE,RF                                                            
         ST    RE,ANEXT                                                         
         J     EXIT                                                             
         SPACE 1                                                                
***********************************************************************         
* BUILD BLANK RECORD AND INPUT KEY FIELDS                             *         
***********************************************************************         
         SPACE 1                                                                
AXRATBLK XC    AXRATLEN,AXRATLEN   RECORD LENGTH/TYPE/ENDOFREC                  
         MVC   AXRATLEN(2),=Y(AXRATDL)                                          
         MVC   AXRATTYP,AXDRATQ                                                 
         MVI   AXRATDX,MXTRTQ                                                   
         MVI   AXRATDX+1,MXTRTQ                                                 
         MVI   AXRATCCD-1,MXTRTQ   2-BYTE AGY ALPHA                             
         MVI   AXRATHST-1,MXTRTQ   HOST KEY                                     
         MVI   AXRATOFC-1,MXTRTQ   OFFICE CODE                                  
         MVI   AXRATDEC-1,MXTRTQ   DEPARTMENT                                   
         MVI   AXRATSDC-1,MXTRTQ   SUB DEPARTMENT                               
         MVI   AXRATPC-1,MXTRTQ    PERSON                                       
         MVI   AXRATTSK-1,MXTRTQ   TASK                                         
         MVI   AXRATCOF-1,MXTRTQ   CLIENT OFFICE                                
         MVI   AXRATCLI-1,MXTRTQ   CLIENT                                       
         MVI   AXRATPRO-1,MXTRTQ   PRODUCT                                      
         MVI   AXRATEFD-1,MXTRTQ   EFFECTIVE DATE                               
         MVI   AXRATE-1,MXTRTQ     RATE                                         
*&&US                                                                           
         USING PCHRECD,R3          R3=A(ACCFILE RECORD)                         
         MVC   AXRATCCD,SXDTAGY    AGENCY 2-BYTE ALPHA                          
         TR    AXRATCCD,MXTRT                                                   
         MVC   AXRATHST,PCHKDOF    HOST KEY DETAIL                              
         TR    AXRATHST,MXTRT      TRANSLATE REMOVES X'00' OFF THE END          
         MVC   AXRATOFC(L'PCHKDOF),PCHKDOF    OFFICE CODE                       
         TR    AXRATOFC,MXTRT                                                   
         MVC   AXRATDEC,SPACES                                                  
         MVC   AXRATDEC(L'PCHKDEP),PCHKDEP    DEPARTMENT CODE                   
         TR    AXRATDEC,MXTRT                                                   
         MVC   AXRATSDC,SPACES                                                  
         MVC   AXRATSDC(L'PCHKSUB),PCHKSUB    SUB DEPT                          
         TR    AXRATSDC,MXTRT                                                   
         MVC   AXRATPC,SPACES                                                   
         MVC   AXRATPC(L'PCHKSTF),PCHKSTF     PERSON CODE                       
         TR    AXRATPC,MXTRT                                                    
         MVC   AXRATTSK(L'PCHKTSK),PCHKTSK    TASK CODE                         
         TR    AXRATTSK,MXTRT                                                   
         MVC   AXRATCOF(L'PCHKOFF),PCHKOFF    CLIENT OFFICE                     
         TR    AXRATCOF,MXTRT                                                   
         MVC   AXRATCLI,SPACES                                                  
         MVC   AXRATCLI(L'PCHKCLI),PCHKCLI    CLIENT                            
         TR    AXRATCLI,MXTRT                                                   
         MVC   AXRATPRO,SPACES                                                  
         MVC   AXRATPRO(L'PCHKPRO),PCHKPRO    PRODUCT                           
         TR    AXRATPRO,MXTRT                                                   
         BR    RE                                                               
*&&                                                                             
*&&UK                                                                           
         USING PCRRECD,R3          R3=A(ACCFILE RECORD)                         
         MVC   AXRATCCD,SXDTAGY    AGENCY 2-BYTE ALPHA                          
         TR    AXRATCCD,MXTRT                                                   
         MVC   AXRATHST,PCRKPER    HOST KEY DETAIL                              
         TR    AXRATHST,MXTRT                                                   
*                                                                               
         LA    R1,PCRKPER          POINT TO BEGINNING OF 1R ACCOUNT             
         L     R6,ALEVELS                                                       
         XR    RF,RF                                                            
         IC    RF,0(R6)                                                         
         BCTR  RF,0                                                             
         MVC   AXRATOFC(0),0(R1)   OFFICE CODE                                  
         EX    RF,*-6                                                           
         TR    AXRATOFC,MXTRT                                                   
*                                                                               
         LA    R1,1(RF,R1)                                                      
         MVC   AXRATDEC,SPACES                                                  
         IC    RF,1(R6)                                                         
         BCTR  RF,0                                                             
         MVC   AXRATDEC(0),0(R1)   DEPARTMENT CODE                              
         EX    RF,*-6                                                           
         TR    AXRATDEC,MXTRT                                                   
*                                                                               
         LA    R1,1(RF,R1)                                                      
         MVC   AXRATSDC,SPACES                                                  
         IC    RF,2(R6)                                                         
         BCTR  RF,0                                                             
         MVC   AXRATSDC(0),0(R1)   SUB DEPT                                     
         EX    RF,*-6                                                           
         TR    AXRATSDC,MXTRT                                                   
*                                                                               
         LA    R1,1(RF,R1)                                                      
         MVC   AXRATPC,SPACES                                                   
         IC    RF,3(R6)                                                         
         BCTR  RF,0                                                             
         MVC   AXRATPC(0),0(R1)               PERSON CODE                       
         EX    RF,*-6                                                           
         TR    AXRATPC,MXTRT                                                    
*                                                                               
         MVC   AXRATTSK,SPACES                                                  
         MVC   AXRATTSK(L'PCRKTSK),PCRKTSK    TASK CODE                         
         TR    AXRATTSK,MXTRT                                                   
         MVC   AXRATCOF,SPACES                                                  
         MVC   AXRATCOF(L'PCRKOFF),PCRKOFF    CLIENT OFFICE                     
         TR    AXRATCOF,MXTRT                                                   
*                                                                               
         MVC   AXRATCLI,SPACES                                                  
         LA    R1,PCRKJOB          POINT TO BEGINNING OF 1R ACCOUNT             
         L     R6,ASJLVLS                                                       
         XR    RF,RF                                                            
         IC    RF,0(R6)                                                         
         BCTR  RF,0                                                             
         MVC   AXRATCLI(0),0(R1)   CLIENT CODE                                  
         EX    RF,*-6                                                           
         TR    AXRATCLI,MXTRT                                                   
*                                                                               
         LA    R1,1(RF,R1)                                                      
         MVC   AXRATPRO,SPACES                                                  
         IC    RF,1(R6)                                                         
         BCTR  RF,0                                                             
         MVC   AXRATPRO(0),0(R1)   PRODUCT CODE                                 
         EX    RF,*-6                                                           
         TR    AXRATPRO,MXTRT                                                   
         BR    RE                                                               
*&&                                                                             
*                                                                               
AXRATCC  DC    A(BUFFER1)          LOCALITY INFORMATION SAVE AREA               
*                                                                               
         DROP  R2,R3,R4,R6                                                      
         EJECT                                                                  
***********************************************************************         
* AXSTHC - EXTRACT STANDARD HOURS RECORDS FOR AXTRACT                 *         
*                                                                     *         
* AL4 A(EXTRACT RECORD)                                               *         
* AL4 A(ACCFILE RECORD)                                               *         
* AL1 0=BUILD 'K' RECORD, 1=BUILD ALL RECORDS, 2=PASS BACK NEXT       *         
*     X'FF' RETURNED IF NO MORE RECORDS TO PROCESS                    *         
* AL3 0                                                               *         
* AL4 A(SXDTABD)                                                      *         
***********************************************************************         
         SPACE 1                                                                
AXSTHC   NMOD1 WORKL,*AXSTHC*,CLEAR=Y                                           
         USING WORKD,RC                                                         
         L     RA,AXSTHLIT                                                      
         USING LITERAL,RA                                                       
         ST    R1,APARM                                                         
         MVC   PARM,0(R1)          EXTRACT PARAMETER LIST                       
         LM    R2,R3,0(R1)                                                      
         L     R4,12(R1)                                                        
         USING AXSTHD,R2           R2=A(EXTRACT RECORD)                         
         USING STDRECD,R3          R3=A(ACCFILE RECORD)                         
         USING SXDTABD,R4          R4=A(SXDXTAB)                                
*                                                                               
         CLI   8(R1),1                                                          
         JL    AXSTHKIL            BUILD K RECORD                               
         JE    AXSTHSER            BUILD SERIES OF RECORDS                      
         J     AXSTHNXT            OUTPUT NEXT RECORD                           
*                                                                               
AXSTHLIT DC    A(LITERAL)                                                       
         EJECT                                                                  
***********************************************************************         
* BUILD 'K' RECORD AND EXIT                                           *         
***********************************************************************         
         SPACE 1                                                                
AXSTHKIL BRAS  RE,AXSTHBLK                                                      
         J     EXIT                                                             
         SPACE 1                                                                
***********************************************************************         
* GET NEXT RECORD AND PASS IT BACK                                    *         
***********************************************************************         
         SPACE 1                                                                
AXSTHNXT L     RE,ANEXT                                                         
         OC    0(8,RE),0(RE)       RECORD TO PASS BACK?                         
         JNZ   *+12                                                             
         MVI   8(R1),X'FF'         NO MORE RECORDS                              
         J     EXIT                                                             
*                                                                               
         L     RE,ANEXT                                                         
         LA    R0,AXSTHD                                                        
         LH    RF,=Y(AXSTHDL)                                                   
         LR    R1,RF                                                            
         MVCL  R0,RE                                                            
*                                                                               
         LH    RF,=Y(AXSTHDL)                                                   
         L     RE,ANEXT                                                         
         AR    RE,RF                                                            
         ST    RE,ANEXT                                                         
         J     EXIT                                                             
         SPACE 1                                                                
***********************************************************************         
* BUILD SERIES OF RECORDS FOR THIS RECORD                             *         
***********************************************************************         
         SPACE 1                                                                
AXSTHSER BRAS  RE,AXSTHBLK         SET UP BLANK RECORD (KEY DETAIL)             
*                                                                               
         LH    RF,=Y(AXSTHDL)                                                   
         STH   RF,RECLEN                                                        
         L     R2,AXSTHCC          WHERE RECORDS TO BE BUILT                    
         ST    R2,ANEXT            FOR NEXT TIME IN                             
         XC    0(8,R2),0(R2)                                                    
*                                                                               
         LA    R6,STDRFST          FIRST ONE RECORD DATA                        
AXSTH02  CLI   0(R6),0                                                          
         JE    EXIT                                                             
         CLI   0(R6),SHRELQ                                                     
         JE    AXSTH06                                                          
*                                                                               
AXSTH04  XR    R1,R1                                                            
         IC    R1,1(R6)                                                         
         AR    R6,R1                                                            
         J     AXSTH02                                                          
*                                                                               
AXSTH06  L     RE,AXREC            BLANK RECORD                                 
         LR    R0,R2                                                            
         LH    RF,RECLEN           LENGTH OF A BLANK RECORD                     
         LR    R1,RF                                                            
         MVCL  R0,RE               COPY IN BLANK RECORD                         
*                                                                               
         LH    RF,RECLEN           LENGTH OF A BLANK RECORD                     
         AR    RF,R2                                                            
         XC    0(8,RF),0(RF)       BLANK OFF START OF NEXT ONE                  
*                                                                               
         USING SHRELD,R6                                                        
         EDIT  SHRHOURS,(10,AXSTHHRS),FILL=0,ZERO=NOBLANK                       
         GOTO1 VDATCON,PLIST,(1,SHRSTART),(10,AXSTHSDT),0                       
*&&US                                                                           
         XC    AXSTHSDT(2),AXSTHSDT+3                                           
         XC    AXSTHSDT+3(2),AXSTHSDT                                           
         XC    AXSTHSDT(2),AXSTHSDT+3                                           
*&&                                                                             
         TR    AXSTHSDT,MXTRT                                                   
         GOTO1 VDATCON,PLIST,(1,SHREND),(10,AXSTHEDT),0                         
*&&US                                                                           
         XC    AXSTHEDT(2),AXSTHEDT+3                                           
         XC    AXSTHEDT+3(2),AXSTHEDT                                           
         XC    AXSTHEDT(2),AXSTHEDT+3                                           
*&&                                                                             
         TR    AXSTHEDT,MXTRT                                                   
*                                                                               
         AH    R2,RECLEN           NEXT SLOT IN BUFFER                          
         J     AXSTH04                                                          
         EJECT                                                                  
***********************************************************************         
* BUILD BLANK RECORD AND INPUT KEY FIELDS                             *         
***********************************************************************         
         SPACE 1                                                                
AXSTHBLK NTR1  ,                                                                
         XC    AXSTHLEN,AXSTHLEN   RECORD LENGTH/TYPE/ENDOFREC                  
         MVC   AXSTHLEN(2),=Y(AXSTHDL)                                          
         MVC   AXSTHTYP,AXDSTHQ                                                 
         MVI   AXSTHDX,MXTRTQ                                                   
         MVI   AXSTHDX+1,MXTRTQ                                                 
         MVI   AXSTHCCD-1,MXTRTQ   2-BYTE AGY ALPHA                             
         MVI   AXSTHOFC-1,MXTRTQ   OFFICE CODE                                  
         MVI   AXSTHDEC-1,MXTRTQ   DEPARTMENT CODE                              
         MVI   AXSTHSDC-1,MXTRTQ   SUB DEPT CODE                                
         MVI   AXSTHPER-1,MXTRTQ   PERSON CODE                                  
         MVI   AXSTHFDT-1,MXTRTQ   FIRST DATE                                   
         MVI   AXSTHLDT-1,MXTRTQ   LAST DATE                                    
         MVI   AXSTHSDT-1,MXTRTQ   START DATE                                   
         MVI   AXSTHEDT-1,MXTRTQ   END DATE                                     
         MVI   AXSTHHRS-1,MXTRTQ   HOURS                                        
*                                                                               
         MVC   AXSTHCCD,SXDTAGY    AGENCY 2-BYTE ALPHA                          
         TR    AXSTHCCD,MXTRT                                                   
         MVC   AXSTHOFC,STDKOFC    OFFICE CODE                                  
         TR    AXSTHOFC,MXTRT                                                   
         MVC   AXSTHDEC(L'STDKDPT),STDKDPT    DEPARTMENT CODE                   
         TR    AXSTHDEC,MXTRT                                                   
         MVC   AXSTHSDC(L'STDKSBD),STDKSBD    SUB DEPARTMENT CODE               
         TR    AXSTHSDC,MXTRT                                                   
         MVC   AXSTHPER,STDKPER    PERSON CODE                                  
         TR    AXSTHPER,MXTRT                                                   
         GOTO1 VDATCON,PLIST,(1,STDRSTDT),(10,AXSTHFDT),0                       
*&&US                                                                           
         XC    AXSTHFDT(2),AXSTHFDT+3                                           
         XC    AXSTHFDT+3(2),AXSTHFDT                                           
         XC    AXSTHFDT(2),AXSTHFDT+3                                           
*&&                                                                             
         GOTO1 VDATCON,PLIST,(1,STDRENDT),(10,AXSTHLDT),0                       
*&&US                                                                           
         XC    AXSTHLDT(2),AXSTHLDT+3                                           
         XC    AXSTHLDT+3(2),AXSTHLDT                                           
         XC    AXSTHLDT(2),AXSTHLDT+3                                           
*&&                                                                             
         XIT1  ,                                                                
*                                                                               
AXSTHCC  DC    A(BUFFER1)          STANDARD HOURS SAVE AREA                     
*                                                                               
         DROP  R2,R3,R4,R6                                                      
         EJECT                                                                  
***********************************************************************         
* AXSBDC - EXTRACT SUB-DEPARTMENT RECORDS FOR AXTRACT                 *         
*                                                                     *         
* AL4 A(EXTRACT RECORD)                                               *         
* AL4 A(ACCFILE RECORD)                                               *         
* AL4 0=ACCFILE-TO-EXTRACT                                            *         
* AL4 A(SXDTABD)                                                      *         
* AL4 A(LEVELS) - CAN GET LENGTH OF EACH SBD-LEVEL FROM THESE         *         
***********************************************************************         
         SPACE 1                                                                
AXSBDC   NMOD1 WORKL,*AXSBDC*,CLEAR=Y                                           
         USING WORKD,RC                                                         
         L     RA,AXSBDLIT                                                      
         USING LITERAL,RA                                                       
         ST    R1,APARM                                                         
         MVC   PARM,0(R1)          EXTRACT PARAMETER LIST                       
         LM    R2,R3,0(R1)                                                      
         L     R4,12(R1)                                                        
         MVC   ALEVELS,16(R1)      SAVE LENGTHS                                 
         USING AXSBDD,R2           R2=A(EXTRACT RECORD)                         
         USING ACTKEY,R3           R3=A(ACCFILE RECORD)                         
         USING SXDTABD,R4          R5=A(SXDTABD)                                
*                                                                               
         XC    AXSBDLEN,AXSBDLEN   RECORD LENGTH/TYPE/ENDOFREC                  
         MVC   AXSBDLEN(2),=Y(AXSBDDL)                                          
         MVC   AXSBDTYP,AXDSBDQ                                                 
         MVI   AXSBDDX,MXTRTQ                                                   
         MVI   AXSBDDX+1,MXTRTQ                                                 
         MVI   AXSBDCCD-1,MXTRTQ   2-BYTE AGY ALPHA                             
         MVI   AXSBDOFC-1,MXTRTQ   OFFICE CODE                                  
         MVI   AXSBDDEC-1,MXTRTQ   DEPARTMENT CODE                              
         MVI   AXSBDSDC-1,MXTRTQ   SUB-DEPARTMENT CODE                          
         MVI   AXSBDNAM-1,MXTRTQ   SUB-DEPARTMENT NAME                          
         MVI   AXSBDIAC-1,MXTRTQ   INCOME ACCOUNT CODE                          
         MVI   AXSBDTC-1,MXTRTQ    TASK CODE                                    
         MVI   AXSBDLCK-1,MXTRTQ   ACCOUNT IS LOCKED                            
         MVI   AXSBDI1-1,MXTRTQ    PRODUCT REQUIRED                             
         MVI   AXSBDI2-1,MXTRTQ    JOB REQUIRED                                 
*                                                                               
         MVC   AXSBDCCD,SXDTAGY    2-BYTE AGY ALPHA                             
         TR    AXSBDCCD,MXTRT                                                   
         L     R6,ALEVELS                                                       
         MVC   AXSBDOFC,SPACES     OFFICE CODE                                  
         XR    RF,RF                                                            
         IC    RF,0(R6)                                                         
         BCTR  RF,0                                                             
         MVC   AXSBDOFC(0),ACTKACT                                              
         EX    RF,*-6                                                           
         TR    AXSBDOFC,MXTRT                                                   
*                                                                               
         LA    RF,ACTKACT+1(RF)    A(DEPARTMENT CODE)                           
         MVC   AXSBDDEC,SPACES     DEPARTMENT CODE                              
         XR    RE,RE                                                            
         IC    RE,1(R6)                                                         
         BCTR  RE,0                                                             
         MVC   AXSBDDEC(0),0(RF)                                                
         EX    RE,*-6                                                           
         TR    AXSBDDEC,MXTRT                                                   
         LA    RF,1(RE,RF)                                                      
*                                                                               
         MVC   AXSBDSDC,SPACES     SUB-DEPARTMENT CODE                          
         IC    RE,2(R6)                                                         
         BCTR  RE,0                                                             
         MVC   AXSBDSDC(0),0(RF)                                                
         EX    RE,*-6                                                           
         TR    AXSBDSDC,MXTRT                                                   
*                                                                               
         LA    R5,ACTRFST          R5=A(FIRST ELEMENT)                          
AXSBD02  CLI   0(R5),0             GET ELEMENT FROM RECORD                      
         JE    EXIT                                                             
         CLI   0(R5),NAMELQ        NAME ELEMENT                                 
         JE    AXSBD06                                                          
         CLI   0(R5),RSTELQ        RSTEL                                        
         JE    AXSBD08                                                          
         CLI   0(R5),SPAELQ        SPAEL                                        
         JE    AXSBD10                                                          
*                                                                               
AXSBD04  XR    RF,RF               BUMP TO NEXT ELEMENT                         
         IC    RF,1(R5)                                                         
         AR    R5,RF                                                            
         J     AXSBD02                                                          
*                                                                               
         USING NAMELD,R5                                                        
AXSBD06  MVC   AXSBDNAM,SPACES     SUB-DEPARTMENT NAME                          
         XR    RF,RF                                                            
         IC    RF,NAMLN                                                         
         SH    RF,=H'3'                                                         
         JM    AXSBD04                                                          
         MVC   AXSBDNAM(0),NAMEREC                                              
         EX    RF,*-6                                                           
         TR    AXSBDNAM,MXTRT                                                   
         J     AXSBD04                                                          
*                                                                               
         USING RSTELD,R5                                                        
AXSBD08  MVI   AXSBDLCK,C'N'                                                    
         TM    RSTSTAT1,RSTSACIL   ACCOUNT IS LOCKED                            
         JZ    *+8                                                              
         MVI   AXSBDLCK,C'Y'                                                    
         MVI   AXSBDI1,C'N'                                                     
         MVI   AXSBDI2,C'N'                                                     
*                                                                               
         CLI   RSTLN,RSTLN3Q       ENSURE ELEMENT LONG ENOUGH                   
         JL    AXSBD04                                                          
         MVC   AXSBDTC,RSTDFTSK    TASK CODE                                    
         TR    AXSBDTC,MXTRT                                                    
         TM    RSTSTAT5,RSTSPROD   PRODUCT REQUIRED                             
         JZ    *+8                                                              
         MVI   AXSBDI1,C'Y'                                                     
         TM    RSTSTAT5,RSTSPRJB   PRODUCT/JOB REQUIRED                         
         JZ    *+8                                                              
         MVI   AXSBDI2,C'Y'                                                     
         J     AXSBD04                                                          
*                                                                               
         USING SPAELD,R5                                                        
AXSBD10  CLI   SPATYPE,SPATINCO                                                 
         JNE   AXSBD04                                                          
         MVC   AXSBDIAC,SPAAULA    INCOME ACCOUNT CODE                          
         TR    AXSBDIAC,MXTRT                                                   
         J     AXSBD04                                                          
*                                                                               
AXSBDLIT DC    A(LITERAL)                                                       
*                                                                               
         DROP  R2,R3,R4,R5                                                      
         EJECT                                                                  
***********************************************************************         
* AXP1RC - EXTRACT PERSON1R RECORDS FOR AXTRACT                       *         
*                                                                     *         
* AL4 A(EXTRACT RECORD)                                               *         
* AL4 A(ACCFILE RECORD)                                               *         
* AL4 0=ACCFILE-TO-EXTRACT                                            *         
* AL4 A(SXDTABD)                                                      *         
* AL4 A(LEVELS) - CAN GET LENGTH OF EACH P1R-LEVEL FROM THESE         *         
***********************************************************************         
         SPACE 1                                                                
AXP1RC   NMOD1 WORKL,*AXP1RC*,CLEAR=Y                                           
         USING WORKD,RC                                                         
         L     RA,AXP1RLIT                                                      
         USING LITERAL,RA                                                       
         ST    R1,APARM                                                         
         MVC   PARM,0(R1)          EXTRACT PARAMETER LIST                       
         LM    R2,R3,0(R1)                                                      
         L     R4,12(R1)                                                        
         MVC   ALEVELS,16(R1)      SAVE LENGTHS                                 
         USING AXP1RD,R2           R2=A(EXTRACT RECORD)                         
         USING ACTKEY,R3           R3=A(ACCFILE RECORD)                         
         USING SXDTABD,R4          R5=A(SXDTABD)                                
*                                                                               
         XC    AXP1RLEN,AXP1RLEN   RECORD LENGTH/TYPE/ENDOFREC                  
         MVC   AXP1RLEN(2),=Y(AXP1RDL)                                          
         MVC   AXP1RTYP,AXDP1RQ                                                 
         MVI   AXP1RDX,MXTRTQ                                                   
         MVI   AXP1RDX+1,MXTRTQ                                                 
         MVI   AXP1RCCD-1,MXTRTQ   2-BYTE AGY ALPHA                             
         MVI   AXP1ROFC-1,MXTRTQ   OFFICE CODE                                  
         MVI   AXP1RDEC-1,MXTRTQ   DEPARTMENT CODE                              
         MVI   AXP1RSDC-1,MXTRTQ   SUB-DEPARTMENT CODE                          
         MVI   AXP1RPC-1,MXTRTQ    PERSON CODE                                  
         MVI   AXP1RIAC-1,MXTRTQ   INCOME ACCOUNT CODE                          
         MVI   AXP1RTC-1,MXTRTQ    TASK CODE                                    
*                                                                               
         MVC   AXP1RCCD,SXDTAGY    2-BYTE AGY ALPHA                             
         TR    AXP1RCCD,MXTRT                                                   
         L     R6,ALEVELS                                                       
         MVC   AXP1ROFC,SPACES     OFFICE CODE                                  
         XR    RF,RF                                                            
         IC    RF,0(R6)                                                         
         BCTR  RF,0                                                             
         MVC   AXP1ROFC(0),ACTKACT                                              
         EX    RF,*-6                                                           
         TR    AXP1ROFC,MXTRT                                                   
         LA    RF,ACTKACT+1(RF)    A(DEPARTMENT CODE)                           
*                                                                               
         MVC   AXP1RDEC,SPACES     DEPARTMENT CODE                              
         XR    RE,RE                                                            
         IC    RE,1(R6)                                                         
         BCTR  RE,0                                                             
         MVC   AXP1RDEC(0),0(RF)                                                
         EX    RE,*-6                                                           
         TR    AXP1RDEC,MXTRT                                                   
         LA    RF,1(RE,RF)                                                      
*                                                                               
         MVC   AXP1RSDC,SPACES     SUB-DEPARTMENT CODE                          
         IC    RE,2(R6)                                                         
         BCTR  RE,0                                                             
         MVC   AXP1RSDC(0),0(RF)                                                
         EX    RE,*-6                                                           
         TR    AXP1RSDC,MXTRT                                                   
         LA    RF,1(RE,RF)                                                      
*                                                                               
         MVC   AXP1RPC,SPACES      PERSON CODE                                  
         IC    RE,3(R6)                                                         
         BCTR  RE,0                                                             
         MVC   AXP1RPC(0),0(RF)                                                 
         EX    RE,*-6                                                           
         TR    AXP1RPC,MXTRT                                                    
*                                                                               
         LA    R5,ACTRFST          R5=A(FIRST ELEMENT)                          
AXP1R02  CLI   0(R5),0             GET ELEMENT FROM RECORD                      
         JE    EXIT                                                             
         CLI   0(R5),RSTELQ        RSTEL                                        
         JE    AXP1R06                                                          
         CLI   0(R5),SPAELQ        SPAEL                                        
         JE    AXP1R08                                                          
*                                                                               
AXP1R04  XR    RF,RF               BUMP TO NEXT ELEMENT                         
         IC    RF,1(R5)                                                         
         AR    R5,RF                                                            
         J     AXP1R02                                                          
*                                                                               
         USING RSTELD,R5                                                        
AXP1R06  CLI   RSTLN,RSTLN3Q       ENSURE ELEMENT LONG ENOUGH                   
         JL    AXP1R04                                                          
         MVC   AXP1RTC,RSTDFTSK    TASK CODE                                    
         TR    AXP1RTC,MXTRT                                                    
         J     AXP1R04                                                          
*                                                                               
         USING SPAELD,R5                                                        
AXP1R08  CLI   SPATYPE,SPATINCO                                                 
         JNE   AXP1R04                                                          
         MVC   AXP1RIAC,SPAAULA    INCOME ACCOUNT CODE                          
         TR    AXP1RIAC,MXTRT                                                   
         J     AXP1R04                                                          
*                                                                               
AXP1RLIT DC    A(LITERAL)                                                       
*                                                                               
         DROP  R2,R3,R4,R5                                                      
         EJECT                                                                  
**********************************************************************          
* AXTSKC - EXTRACT TASK RECORDS FOR AXTRACT                          *          
*                                                                    *          
* AL4 A(EXTRACT RECORD)                                              *          
* AL4 A(ACCFILE RECORD)                                              *          
* AL4 0=ACCFILE-EXTRACT                                              *          
* AL4 A(SXDTABD)                                                     *          
**********************************************************************          
         SPACE 1                                                                
AXTSKC   NMOD1 WORKL,*AXTSKC*,CLEAR=Y                                           
         USING WORKD,RC                                                         
         L     RA,AXTSKLIT                                                      
         USING LITERAL,RA                                                       
         ST    R1,APARM                                                         
         MVC   PARM,0(R1)          EXTRACT PARAMETER LIST                       
         LM    R2,R3,0(R1)                                                      
         L     R4,12(R1)                                                        
         USING AXTSKD,R2           R2=A(EXTRACT RECORD)                         
         USING WCORECD,R3          R3=A(ACCFILE RECORD)                         
         USING SXDTABD,R4          R4=A(SXDXTABD)                               
*                                                                               
         XC    AXTSKLEN,AXTSKLEN   RECORD LENGTH/TYPE/ENDOFREC                  
         MVC   AXTSKLEN(2),=Y(AXTSKDL)                                          
         MVC   AXTSKTYP,AXDTSKQ                                                 
         MVI   AXTSKDX,MXTRTQ                                                   
         MVI   AXTSKDX+1,MXTRTQ                                                 
         MVI   AXTSKCCD-1,MXTRTQ   2-BYTE AGENCY ALPHA                          
         MVI   AXTSKLC-1,MXTRTQ    LEDGER CODE                                  
         MVI   AXTSKCDE-1,MXTRTQ   TASK CODE                                    
         MVI   AXTSKSNM-1,MXTRTQ   SHORT NAME                                   
         MVI   AXTSKNAM-1,MXTRTQ   LONG NAME                                    
         MVI   AXTSKISC-1,MXTRTQ   IS COMMISIONABLE                             
         MVI   AXTSKISA-1,MXTRTQ   IS ACTIVE                                    
         MVI   AXTSKISR-1,MXTRTQ   IS RATE ADJUSTED                             
         MVI   AXTSKIST-1,MXTRTQ   IS FOR TIME                                  
         MVI   AXTSKTGC-1,MXTRTQ   WORKCODE GROUP                               
*                                                                               
         MVC   AXTSKCCD,SXDTAGY    2-BYTE AGY ALPHA                             
         TR    AXTSKCCD,MXTRT                                                   
         MVC   AXTSKCDE,WCOKWRK    TASK CODE                                    
         TR    AXTSKCDE,MXTRT                                                   
         MVC   AXTSKLC,WCOKUNT     LEDGER CODE                                  
         TR    AXTSKLC,MXTRT                                                    
         MVC   AXTSKNAM,SPACES     FOR COMPARISON AT END                        
*                                                                               
         LA    R5,WCORFST          R5=A(FIRST ELEMENT)                          
AXTSK02  CLI   0(R5),0             GET ELEMENT FROM RECORD                      
         JE    AXTSK06                                                          
         CLI   0(R5),WCOELQ                                                     
         JE    AXTSK08                                                          
         CLI   0(R5),NAMELQ                                                     
         JE    AXTSK10                                                          
*                                                                               
AXTSK04  XR    RF,RF               BUMP TO NEXT ELEMENT                         
         ICM   RF,1,1(R5)                                                       
         JZ    AXTSK06             EXIT IF FUNNY ELEMENT LENGTH                 
         AR    R5,RF                                                            
         J     AXTSK02                                                          
*                                                                               
AXTSK06  CLC   AXTSKNAM,SPACES     EQUATE LONG NAME TO SHORT IF N/P             
         JH    EXIT                                                             
         MVC   AXTSKNAM(L'AXTSKSNM),AXTSKSNM                                    
         J     EXIT                                                             
*                                                                               
         USING WCOELD,R5                                                        
AXTSK08  MVC   AXTSKSNM,WCODESC    SHORT NAME                                   
         TR    AXTSKSNM,MXTRT                                                   
*                                                                               
         MVI   AXTSKIST,C'N'       IS FOR TIME                                  
         TM    WCOSTAT,WCOSHCOE                                                 
         JZ    *+8                                                              
         MVI   AXTSKIST,C'Y'                                                    
*                                                                               
         MVI   AXTSKISC,C'N'       IS COMMISSIONABLE                            
         MVI   AXTSKISA,C'N'       IS ACTIVE                                    
         MVI   AXTSKISR,C'N'       IS RATE ADJUSTED                             
*                                                                               
*&&UK                                                                           
         MVI   AXTSKISR,C'N'                                                    
         MVI   AXTSKISC,C'Y'                                                    
         TM    WCOSTAT2,WCOSLPST                                                
         JO    *+8                                                              
         MVI   AXTSKISA,C'Y'                                                    
*&&                                                                             
*&&US                                                                           
         TM    WCOSTAT,WCOSNONC    US HAS DIFFERENT VALUE SETTINGS              
         JO    *+8                                                              
         MVI   AXTSKISC,C'Y'                                                    
         TM    WCOSTAT2,WCOSINA                                                 
         JO    *+8                                                              
         MVI   AXTSKISA,C'Y'                                                    
         TM    WCOSTAT2,WCOSART                                                 
         JZ    *+8                                                              
         MVI   AXTSKISR,C'Y'                                                    
*&&                                                                             
         MVC   AXTSKTGC,WCOGRP     WORKCODE GROUP                               
         TR    AXTSKTGC,MXTRT                                                   
         J     AXTSK04                                                          
         DROP  R5                                                               
*                                                                               
         USING NAMELD,R5                                                        
AXTSK10  XR    RF,RF               TASK LONG NAME                               
         IC    RF,NAMLN                                                         
         AHI   RF,-3                                                            
         JM    AXTSK04                                                          
         MVC   AXTSKNAM(0),NAMEREC                                              
         EX    RF,*-6                                                           
         TR    AXTSKNAM,MXTRT                                                   
         J     AXTSK04                                                          
*                                                                               
AXTSKLIT DC    A(LITERAL)                                                       
*                                                                               
         DROP  R2,R3,R4,R5                                                      
         EJECT                                                                  
***********************************************************************         
* AXTISC - EXTRACT TIMESHEET RECORDS FOR AXTRACT                      *         
*                                                                     *         
* AL4 A(EXTRACT RECORD)                                               *         
* AL4 A(ACCFILE RECORD)                                               *         
* AL4 0=ACCFILE-TO-EXTRACT                                            *         
* AL4 A(SXDTABD)                                                      *         
***********************************************************************         
         SPACE 1                                                                
AXTISC   NMOD1 WORKL,*AXTISC*,CLEAR=Y                                           
         USING WORKD,RC                                                         
         L     RA,AXTISLIT                                                      
         USING LITERAL,RA                                                       
         ST    R1,APARM                                                         
         MVC   PARM,0(R1)          EXTRACT PARAMETER LIST                       
         LM    R2,R3,0(R1)                                                      
         L     R4,12(R1)                                                        
         MVC   ALEVELS,16(R1)                                                   
         USING AXTISD,R2           R2=A(EXTRACT RECORD)                         
         USING TSXRECD,R3          R3=A(ACCFILE RECORD)                         
         USING SXDTABD,R4          R4=A(SXDXTABD)                               
*                                                                               
         XC    AXTISLEN,AXTISLEN   RECORD LENGTH/TYPE/ENDOFREC                  
         MVC   AXTISLEN(2),=Y(AXTISDL)                                          
         MVC   AXTISTYP,AXDTISQ                                                 
         MVI   AXTISDX,MXTRTQ                                                   
         MVI   AXTISDX+1,MXTRTQ                                                 
         MVI   AXTISCCD-1,MXTRTQ   2-BYTE AGENCY ALPHA                          
         MVI   AXTISCSC-1,MXTRTQ   CON-SYS PERSON CODE                          
         MVI   AXTISEND-1,MXTRTQ   END DATE                                     
         MVI   AXTISOFF-1,MXTRTQ   OFFICE                                       
         MVI   AXTISDPT-1,MXTRTQ   DEPT                                         
         MVI   AXTISSBD-1,MXTRTQ   SUB                                          
         MVI   AXTISTMP-1,MXTRTQ   IS FROM TEMPO                                
         MVI   AXTISSTA-1,MXTRTQ   TIME SHEET STATE                             
         MVI   AXTISSID-1,MXTRTQ   TEMPO ONLY - SUBMIT ID                       
         MVI   AXTISSDT-1,MXTRTQ   TEMPO ONLY - SUBMIT DATE/TIME                
         MVI   AXTISAPR-1,MXTRTQ   TEMPO ONLY - APPROVER                        
         MVI   AXTISADT-1,MXTRTQ   TEMPO ONLY - APPROVE DATE/TIME               
         MVI   AXTISUPR-1,MXTRTQ   TEMPO ONLY - UPLOADER                        
         MVI   AXTISUDT-1,MXTRTQ   TEMPO ONLY - UPLOAD DATE/TIME                
         MVI   AXTISPN-1,MXTRTQ    PERIOD NUMBER                                
         MVI   AXTISBIH-1,MXTRTQ   TEMPO ONLY - BILLABLE HOURS                  
         MVI   AXTISCHH-1,MXTRTQ   TEMPO ONLY - CHARGEABLE HOURS                
         MVI   AXTISNBH-1,MXTRTQ   TEMPO ONLY - NON-BILLABLE HOURS              
         MVI   AXTISNCH-1,MXTRTQ   TEMPO ONLY - NON-CLIENT HOURS                
         MVI   AXTISMDT-1,MXTRTQ   TEMPO ONLY - MODIFY DATE/TIME                
         MVI   AXTISERR-1,MXTRTQ   TEMPO ONLY - ERROR NUMBER                    
         MVI   AXTISSTD-1,MXTRTQ   START DATE                                   
*&&UK*&& MVI   AXTISDLY-1,MXTRTQ   IS DAILY TIME                                
*&&UK*&& MVI   AXTISREV-1,MXTRTQ   TEMPO ONLY - REVIEW REQUIRED                 
*                                                                               
         MVC   AXTISCCD,SXDTAGY    2-BYTE AGY ALPHA                             
         TR    AXTISCCD,MXTRT                                                   
*                                                                               
         CLI   TSXKTYP,TSXKTYPQ    TYPE                                         
         JE    AXTIS00                                                          
         CLI   TSXKTYP,AUDKTYPQ    TYPE                                         
         JE    *+6                                                              
         DC    H'0'                                                             
AXTIS00  CLI   TSXKSUB,TSXKSUBQ    SUB ID                                       
         JNE   AXTIS20                                                          
         CLC   =C'ZB',SXDTAGY      A LITTLE HARD CODE, FOR NOW                  
         JNE   AXTIS02             WILL EVENTUALLY GET DATA FROM TIMEL          
         MVC   AXTISOFF(1),TSXKODS      OFFICE                                  
         MVC   AXTISDPT(2),TSXKODS+1    DEPT                                    
         MVC   AXTISSBD(2),TSXKODS+3    SUBDEPT                                 
         J     AXTIS04                                                          
*                                                                               
AXTIS02  MVC   AXTISOFF(2),TSXKODS      OFFICE                                  
         MVC   AXTISDPT(2),TSXKODS+2    DEPT                                    
         MVC   AXTISSBD(2),TSXKODS+4    SUBDEPT                                 
*                                                                               
AXTIS04  TR    AXTISOFF,MXTRT                                                   
         TR    AXTISDPT,MXTRT                                                   
         TR    AXTISSBD,MXTRT                                                   
*                                                                               
         GOTO1 VDATCON,PLIST,(1,TSXKEND),(10,AXTISEND),0                        
*&&US                                                                           
         XC    AXTISEND(2),AXTISEND+3                                           
         XC    AXTISEND+3(2),AXTISEND                                           
         XC    AXTISEND(2),AXTISEND+3                                           
*&&                                                                             
         TR    AXTISEND,MXTRT                                                   
*                                                                               
         MVI   AXTISSID,C'0'            TEMPO ONLY - SUBMIT ID                  
         TM    TSXRSTA1,TSXTEMPO   IS FROM TEMPO?                               
         JZ    AXTIS06                                                          
         MVI   AXTISTMP,C'Y'       YES                                          
         MVC   AXTISSTA,=C'00005'  STATE IS 5 (UPLOADED)                        
         J     AXTIS07                                                          
*                                                                               
AXTIS06  MVI   AXTISTMP,C'N'       NO                                           
         MVC   AXTISSTA,=C'00006'  STATE IS 6 (TMS)                             
*                                                                               
AXTIS07  DS    0H                                                               
*&&UK                                                                           
         MVI   AXTISDLY,C'N'                                                    
         MVI   AXTISREV,C'N'                                                    
         TM    TSXRSTA1,TSXDAILY   IS TIME DAILY?                               
         JZ    AXTIS08                                                          
         MVI   AXTISDLY,C'Y'                                                    
*&&                                                                             
*                                                                               
AXTIS08  XC    PIDN,PIDN                                                        
         LA    R5,TSXRFST          R5=A(FIRST ELEMENT)                          
*                                                                               
AXTIS10  CLI   0(R5),0             GET ELEMENT FROM RECORD                      
         JE    AXTIS14                                                          
         CLI   0(R5),PIDELQ                                                     
         JE    AXTIS16                                                          
         CLI   0(R5),TIMELQ                                                     
         JE    AXTIS18                                                          
*                                                                               
AXTIS12  XR    RF,RF               BUMP TO NEXT ELEMENT                         
         IC    RF,1(R5)                                                         
         AR    R5,RF                                                            
         J     AXTIS10                                                          
*                                                                               
AXTIS14  OC    PIDN,PIDN                                                        
         JNZ   EXIT                PID IS REQUIRED                              
         L     R1,APARM                                                         
         MVI   8(R1),X'FF'         SET TO NOT WRITE BACK                        
         J     EXIT                                                             
*                                                                               
         USING PIDELD,R5                                                        
AXTIS16  MVC   PIDN,PIDNO          SAVE PIDNO                                   
         XR    R0,R0                                                            
         ICM   R0,3,PIDNO                                                       
         CVD   R0,DUB                                                           
         OI    DUB+L'DUB-1,X'0F'                                                
         UNPK  AXTISCSC,DUB                                                     
         J     AXTIS12                                                          
*                                                                               
         USING TIMELD,R5                                                        
AXTIS18  GOTO1 VDATCON,PLIST,(1,TIMXPEDT),(10,AXTISEND),0  END DATE             
*&&US                                                                           
         XC    AXTISEND(2),AXTISEND+3                                           
         XC    AXTISEND+3(2),AXTISEND                                           
         XC    AXTISEND(2),AXTISEND+3                                           
*&&                                                                             
         TR    AXTISEND,MXTRT                                                   
*                                                                               
         MVC   AXTISOFF,TIMXOFFC            OFFICE                              
         TR    AXTISOFF,MXTRT                                                   
         MVC   AXTISDPT,TIMXDEPT            DEPT                                
         TR    AXTISDPT,MXTRT                                                   
         MVC   AXTISSBD,TIMXSDPT            SUBDEPT                             
         TR    AXTISSBD,MXTRT                                                   
*                                                                               
         EDIT  TIMXPED#,(3,AXTISPN),FILL=0  PERIOD NUMBER                       
*                                                                               
         OC    TIMXPSDT,TIMXPSDT            START DATE?                         
         JZ    AXTIS12                                                          
*                                                                               
         GOTO1 VDATCON,PLIST,(1,TIMXPSDT),(10,AXTISSTD),0  START DATE           
*&&US                                                                           
         XC    AXTISSTD(2),AXTISSTD+3                                           
         XC    AXTISSTD+3(2),AXTISSTD                                           
         XC    AXTISSTD(2),AXTISSTD+3                                           
*&&                                                                             
         TR    AXTISSTD,MXTRT                                                   
         J     AXTIS12                                                          
*                                                                               
         USING AUDRECD,R3          R3=A(ACCFILE RECORD)                         
AXTIS20  CLI   AUDKSUB,AUDKSUBQ                                                 
         JE    *+6                                                              
         DC    H'0'                                                             
AXTIS22  MVC   PIDN,AUDKPIDB       SAVE PIDNO                                   
*&&UK*&& MVI   AXTISSUB,C'Y'       SET TIMESHEET IS SUBMITTED OR ABOVE          
         CLI   AUDRSTAT,0          IS TIMESHEET IN PROGRESS                     
         BE    AXTIS22A            YES - THEN REMOVE SUBMITTED STAT             
         TM    AUDRSTAT,TIMSREJE   IS TIMESHEET REJECTED                        
         BZ    AXTIS23             NO - MUST BE SUBMITTED                       
AXTIS22A DS    0H                                                               
*&&UK*&& MVI   AXTISSUB,C'N'       SET TIMESHEET IS NOT SUBMITTED               
AXTIS23  XR    R0,R0                                                            
         ICM   R0,3,PIDN                                                        
         CVD   R0,DUB                                                           
         OI    DUB+L'DUB-1,X'0F'                                                
         UNPK  AXTISCSC,DUB                                                     
         SR    R1,R1                                                            
         ICM   R1,7,AUDKPEDT                                                    
         LNR   R1,R1                                                            
         STCM  R1,7,TMPDTE                                                      
         GOTO1 VDATCON,PLIST,(1,TMPDTE),(10,AXTISEND),0  END DATE               
*&&US                                                                           
         XC    AXTISEND(2),AXTISEND+3                                           
         XC    AXTISEND+3(2),AXTISEND                                           
         XC    AXTISEND(2),AXTISEND+3                                           
*&&                                                                             
         TR    AXTISEND,MXTRT                                                   
         LA    R5,AUDRFST          R5=A(FIRST ELEMENT)                          
*                                                                               
AXTIS24  CLI   0(R5),0             GET ELEMENT FROM RECORD                      
         JNE   AXTIS26                                                          
         L     R1,APARM                                                         
         MVI   8(R1),X'FF'         SET TO NOT WRITE BACK                        
         J     EXIT                                                             
AXTIS26  CLI   0(R5),TIMELQ                                                     
         JNE   AXTIS28                                                          
         CLI   TIMETYP,TIMEARIN                                                 
         JNE   AXTIS28                                                          
         CLC   =C'1R',TIMAPUNT                                                  
         JE    AXTIS30                                                          
*                                                                               
AXTIS28  XR    RF,RF               BUMP TO NEXT ELEMENT                         
         IC    RF,1(R5)                                                         
         AR    R5,RF                                                            
         J     AXTIS24                                                          
*                                                                               
AXTIS30  TM    TIMASTAT,TIMASTMP   WAS THIS TIME ADDED IN TEMPO                 
         B     AXTIS32                                                          
*        BZ    AXTIS32             NO                                           
         L     R1,APARM            YES - DON'T EXTRACT TIS RECORD               
         MVI   8(R1),X'FF'         SET TO NOT WRITE BACK                        
         J     EXIT                                                             
AXTIS32  SR    RE,RE                                                            
         L     RF,ALEVELS                                                       
         IC    RE,0(RF)                                                         
         SHI   RE,1                                                             
         MVC   AXTISOFF(0),TIMAPACT     OFFICE                                  
         EX    RE,*-6                                                           
         LA    RE,TIMAPACT+1(RE)                                                
         TR    AXTISOFF,MXTRT                                                   
         MVC   AXTISDPT(2),0(RE)                                                
         TR    AXTISDPT,MXTRT                                                   
         MVC   AXTISSBD(2),2(RE)                                                
         TR    AXTISSBD,MXTRT                                                   
         MVI   AXTISSID,C'0'            TEMPO ONLY - SUBMIT ID                  
         MVI   AXTISTMP,C'N'       NO                                           
         MVC   AXTISSTA,=C'00006'  STATE IS 6 (TMS OR BRANDOCEAN)               
*&&UK*&& MVI   AXTISDLY,C'N'                                                    
*&&UK*&& MVI   AXTISREV,C'N'                                                    
         CLI   TIMLN,TIMALNQ                                                    
         JNH   EXIT                                                             
         EDIT  TIMAPER#,(3,AXTISPN),FILL=0  PERIOD NUMBER                       
         OC    TIMAPSDT,TIMAPSDT            START DATE?                         
         JZ    EXIT                                                             
*                                                                               
         GOTO1 VDATCON,PLIST,(1,TIMAPSDT),(10,AXTISSTD),0  START DATE           
*&&US                                                                           
         XC    AXTISSTD(2),AXTISSTD+3                                           
         XC    AXTISSTD+3(2),AXTISSTD                                           
         XC    AXTISSTD(2),AXTISSTD+3                                           
*&&                                                                             
         TR    AXTISSTD,MXTRT                                                   
         J     EXIT                                                             
*                                                                               
AXTISLIT DC    A(LITERAL)                                                       
*                                                                               
         DROP  R2,R3,R4,R5                                                      
         EJECT                                                                  
***********************************************************************         
* AXTALC - EXTRACT TIMESHEET ALLOCATION LINE INFO FROM AXTRACT        *         
*                                                                     *         
* AL4 A(EXTRACT RECORD)                                               *         
* AL4 A(ACCFILE RECORD)                                               *         
* AL1 1=BUILD ALL RECORDS, 2=PASS BACK NEXT                           *         
*     X'FF' RETURNED IF NO MORE RECORDS TO PROCESS                    *         
* AL3 0                                                               *         
* AL4 A(SXDTABD)                                                      *         
* AL4 A(LEVELS) - CAN GET LENGTHS OF LEVELS FROM HERE                 *         
***********************************************************************         
         SPACE 1                                                                
AXTALC   NMOD1 WORKL,*AXTALC*,CLEAR=Y,R9                                        
         USING WORKD,RC                                                         
         L     RA,AXTALLIT                                                      
         USING LITERAL,RA                                                       
         ST    R1,APARM                                                         
         MVC   PARM,0(R1)          EXTRACT PARAMETER LIST                       
         MVC   ALEVELS,16(R1)                                                   
         LM    R2,R3,0(R1)                                                      
         L     R4,12(R1)                                                        
         MVC   FLAG,12(R1)                                                      
*                                                                               
         USING AXTALD,R2           R2=A(EXTRACT RECORD) (LONGEST)               
         USING TIMRECD,R3          R3=A(ACCFILE RECORD)                         
         USING SXDTABD,R4          R4=A(SXDXTAB)                                
BLKTAL   USING AXTALD,BLNKTAL                                                   
BLKTAT   USING AXTATD,BLNKTAT                                                   
BLKTAD   USING AXTADD,BLNKTAD                                                   
BLKBOL   USING AXBOLD,BLNKBOL                                                   
*                                                                               
         CLI   8(R1),1                                                          
         JL    AXTALKIL            BUILD A K RECORD                             
         JE    AXTALSER            BUILD RECORDS FROM ADD RECORD                
         J     AXTALNXT            OUTPUT NEXT RECORD                           
*                                                                               
AXTALLIT DC    A(LITERAL)                                                       
         EJECT                                                                  
***********************************************************************         
* BUILD K RECORD HERE                                                 *         
***********************************************************************         
         SPACE 1                                                                
AXTALKIL BAS   RE,AXTALBLK                                                      
*                                                                               
         LA    R0,AXTALD           TO                                           
         LA    RE,BLNKTAL          FROM                                         
         LH    RF,=Y(AXTALDL)                                                   
         LR    R1,RF                                                            
         MVCL  R0,RE                                                            
         J     EXIT                COPY BACK KILL RECORD                        
         SPACE 1                                                                
***********************************************************************         
* GET NEXT RECORD AND PASS IT BACK                                    *         
***********************************************************************         
         SPACE 1                                                                
AXTALNXT L     RE,ANXTTAL                                                       
         CLI   FLAG,0              ARE WE DEALING WITH DELETE BUFFER            
         BE    AXNTAL02            NO                                           
         L     RE,ANXTTALD         YES                                          
AXNTAL02 OC    0(8,RE),0(RE)       TAL RECORD TO PASS BACK?                     
         BZ    AXNTAL10            NO                                           
*                                                                               
         LA    R0,AXTALD                                                        
         LH    RF,=Y(AXTALDL)      LENGTH OF A TAL RECORD                       
         LR    R1,RF                                                            
         MVCL  R0,RE               COPY BACK TAL RECORD                         
* CONVERT PACKED NUMBERS TO DISPLAY ONES                                        
         LA    RE,AXTALHBI+L'AXTALHBI-8   BILLABLE                              
         EDIT  (P8,(RE)),(10,WORK),FILL=0,ZERO=NOBLANK,FLOAT=-                  
         MVC   AXTALHBI,WORK                                                    
         LA    RE,AXTALHCH+L'AXTALHCH-8   CHARGABLE                             
         EDIT  (P8,(RE)),(10,WORK),FILL=0,ZERO=NOBLANK,FLOAT=-                  
         MVC   AXTALHCH,WORK                                                    
         LA    RE,AXTALHNB+L'AXTALHNB-8   NON-BILLABLE                          
         EDIT  (P8,(RE)),(10,WORK),FILL=0,ZERO=NOBLANK,FLOAT=-                  
         MVC   AXTALHNB,WORK                                                    
         LA    RE,AXTALHNC+L'AXTALHNC-8   NON-CLIENT                            
         EDIT  (P8,(RE)),(10,WORK),FILL=0,ZERO=NOBLANK,FLOAT=-                  
         MVC   AXTALHNC,WORK                                                    
*                                                                               
         CLI   FLAG,0              ARE WE DEALING WITH DELETE BUFFER            
         BNE   AXNTAL04            NO                                           
         L     RE,ANXTTAL                                                       
         AH    RE,=Y(AXTALDL)                                                   
         ST    RE,ANXTTAL          UPDATE BUFFER POINTER                        
         B     AXNTAL06                                                         
*                                                                               
AXNTAL04 L     RE,ANXTTALD                                                      
         AH    RE,=Y(AXTALDL)                                                   
         ST    RE,ANXTTALD         UPDATE BUFFER POINTER                        
*                                                                               
AXNTAL06 L     R1,APARM                                                         
         MVI   8(R1),X'00'         SET RETURNING TAL RECORD                     
         J     EXIT                                                             
*                                                                               
AXNTAL10 L     RE,ANXTTAT          A(NEXT TAT RECORD)                           
         OC    0(8,RE),0(RE)       RECORD TO PASS BACK?                         
         JZ    AXNTAL20                                                         
*                                                                               
         LA    R0,AXTALD                                                        
         LH    RF,=Y(AXTATDL)      LENGTH OF A TAT RECORD                       
         LR    R1,RF                                                            
         MVCL  R0,RE               COPY BACK TAT RECORD                         
*                                                                               
         L     RE,ANXTTAT                                                       
         AH    RE,=Y(AXTATDL)                                                   
         ST    RE,ANXTTAT          UPDATE BUFFER2 POINTER                       
*                                                                               
         L     R1,APARM                                                         
         MVI   8(R1),X'EE'         SET RETURNING TAT RECORD                     
         J     EXIT                                                             
*                                                                               
AXNTAL20 L     RE,ANXTBOL          A(NEXT BOL RECORD)                           
         OC    0(8,RE),0(RE)       RECORD TO PASS BACK?                         
         JZ    AXNTAL30                                                         
         CLI   SXDTVER,6           VERSION 6?                                   
         JL    AXNTAL30                                                         
*                                                                               
         LA    R0,AXTALD                                                        
         LH    RF,=Y(AXBOLDL)      LENGTH OF A BOL RECORD                       
         LR    R1,RF                                                            
         MVCL  R0,RE               COPY BACK BOL RECORD                         
*                                                                               
         L     RE,ANXTBOL                                                       
         AH    RE,=Y(AXBOLDL)                                                   
         ST    RE,ANXTBOL          UPDATE BUFFER2 POINTER                       
*                                                                               
         L     R1,APARM                                                         
         MVI   8(R1),X'CC'         SET RETURNING BOL RECORD                     
         J     EXIT                                                             
*                                                                               
AXNTAL30 L     RE,ANXTTAD          A(NEXT TAD RECORD)                           
         OC    0(8,RE),0(RE)       RECORD TO PASS BACK?                         
         JNZ   *+12                                                             
         MVI   8(R1),X'FF'         SET NO MORE RECORDS TO RETURN                
         J     EXIT                                                             
         CLI   SXDTVER,6           VERSION 6?                                   
         BNL   AXNTAL32                                                         
         MVI   8(R1),X'FF'         SET NO MORE RECORDS TO RETURN                
         J     EXIT                                                             
*                                                                               
AXNTAL32 LA    R0,AXTALD                                                        
         LH    RF,=Y(AXTADDL)      LENGTH OF A TAD RECORD                       
         LR    R1,RF                                                            
         MVCL  R0,RE               COPY BACK TAD RECORD                         
         USING AXTADD,R2           R2=A(EXTRACT RECORD) (LONGEST)               
*                                                                               
         LA    RE,AXTADHRS+L'AXTADHRS-8                                         
         EDIT  (P8,(RE)),(10,WORK),FILL=0,ZERO=NOBLANK,FLOAT=-                  
         MVC   AXTADHRS,WORK                                                    
*                                                                               
         L     RE,ANXTTAD                                                       
         AH    RE,=Y(AXTADDL)                                                   
         ST    RE,ANXTTAD         UPDATE BUFFER2 POINTER                        
*                                                                               
         L     R1,APARM                                                         
         MVI   8(R1),X'DD'         SET RETURNING TAD RECORD                     
         J     EXIT                                                             
         DROP  R2                                                               
         SPACE 1                                                                
***********************************************************************         
* BUILD SERIES OF RECORDS FOR THIS RECORD                             *         
***********************************************************************         
         SPACE 1                                                                
AXTALSER CLI   SXDTVER,5           VERSION 5?                                   
         BNE   AXTAL02                                                          
         CLI   4(R1),1             HAVE WE ALREADY SET UP FIRST TIME            
         JNE   AXTAL02             NO                                           
         L     R2,ANXXTAL          YES                                          
         L     R5,ANXXTAT          SET TAX RECORD                               
         L     R8,ANXXTAD          SET ADDRESS OF NEXT DAY TSALLOC NTRY         
         L     R6,ANXXBOL          SET BRANDO STATUS ELEMENT                    
         CLI   FLAG,0                                                           
         JE    AXTAL08                                                          
         L     R2,ANXXTALD                                                      
         J     AXTAL08                                                          
AXTAL02  BAS   RE,AXTALBLK         SET UP BLANK RECORDS (KEY DETAIL)            
*                                                                               
         MVI   AXTAX,C'N'                                                       
*                                                                               
         L     R2,AXTALCC          WHERE RECORDS TO BE BUILT (TAL)              
         ST    R2,ANXTTAL                                                       
         ST    R2,ANXXTAL          FOR NEXT TIME IN (TAL)                       
         CLI   FLAG,0              ARE WE DEALING WITH DELETE BUFFER            
         BE    AXTAL04             NO                                           
         L     R2,AXTALCC2         YES - BUFFER FOR DELETE                      
         ST    R2,ANXTTALD                                                      
         ST    R2,ANXXTALD         FOR NEXT TIME IN (TAL)                       
AXTAL04  XC    0(8,R2),0(R2)                                                    
         USING AXTALD,R2                                                        
         L     R5,AXTATCC          WHERE RECORDS TO BE BUILT (TAT)              
         ST    R5,ANXTTAT          FOR NEXT TIME IN (TAT)                       
         ST    R5,ANXXTAT          FOR NEXT TIME IN (TAT)                       
         XC    0(8,R5),0(R5)                                                    
         USING AXTATD,R5                                                        
         L     R8,AXTADCC          WHERE RECORDS TO BE BUILT (TAD)              
         ST    R8,ANXTTAD          FOR NEXT TIME IN (TAD)                       
         ST    R8,ANXXTAD          FOR NEXT TIME IN (TAD)                       
         XC    0(8,R8),0(R8)                                                    
         USING AXTADD,R8                                                        
         L     R6,AXBOLCC          WHERE RECORDS TO BE BUILT (BOL)              
         ST    R6,ANXTBOL          FOR NEXT TIME IN (BOL)                       
         ST    R6,ANXXBOL          FOR NEXT TIME IN (BOL)                       
         XC    0(8,R6),0(R6)                                                    
         USING AXBOLD,R6                                                        
*                                                                               
         USING TIMELD,R7                                                        
AXTAL08  LA    R7,TIMRFST          FIRST ONE RECORD DATA                        
AXTAL10  CLI   TIMEL,0                                                          
         JE    AXTAL20                                                          
         CLI   TIMEL,TIMELQ                                                     
         JNE   AXTAL18                                                          
         CLI   TIMETYP,TIMEINP     FIRST ELEMENT IN A CLUSTER                   
         JNE   AXTAL18                                                          
*                                                                               
         CLI   SXDTVER,1           VERSION 1?                                   
         JH    AXTAL38             NO                                           
         TM    TIMSTAT,TIMTEMPO    IGNORE TEMPO LINES FOR VERSION 1             
         JZ    AXTAL38                                                          
*                                                                               
AXTAL18  XR    R1,R1                                                            
         IC    R1,1(R7)                                                         
         AR    R7,R1                                                            
         J     AXTAL10                                                          
*                                                                               
AXTAL20  CLI   FLAG,0                                                           
         JE    AXTAL21                                                          
         ST    R2,ANXXTALD                                                      
         J     EXIT                                                             
AXTAL21  ST    R2,ANXXTAL                                                       
         ST    R8,ANXXTAD                                                       
         J     EXIT                                                             
*                                                                               
AXTAL38  LA    RE,BLNKTAL          COPY IN BLANK TAL RECORD                     
         LA    R0,AXTALD                                                        
         LH    RF,=Y(AXTALDL)                                                   
         LR    R1,RF                                                            
         MVCL  R0,RE                                                            
*                                                                               
         LH    RF,=Y(AXTALDL)                                                   
         AR    RF,R2                                                            
         XC    0(8,RF),0(RF)       CLEAR NEXT SLOT                              
*                                                                               
         LA    RE,BLNKTAT          COPY IN BLANK TAT RECORD                     
         LA    R0,AXTATD                                                        
         LH    RF,=Y(AXTATDL)                                                   
         LR    R1,RF                                                            
         MVCL  R0,RE                                                            
*                                                                               
         LH    RF,=Y(AXTATDL)                                                   
         AR    RF,R5                                                            
         XC    0(8,RF),0(RF)       CLEAR NEXT SLOT                              
*                                                                               
         LA    RE,BLNKTAD          COPY IN BLANK TAD RECORD                     
         LA    R0,AXTADD                                                        
         LH    RF,=Y(AXTADDL)                                                   
         LR    R1,RF                                                            
         MVCL  R0,RE                                                            
*                                                                               
         LH    RF,=Y(AXTADDL)                                                   
         AR    RF,R8                                                            
         XC    0(8,RF),0(RF)       CLEAR NEXT SLOT                              
*                                                                               
         LA    RE,BLNKBOL          COPY IN BLANK BOL RECORD                     
         LA    R0,AXBOLD                                                        
         LH    RF,=Y(AXBOLDL)                                                   
         LR    R1,RF                                                            
         MVCL  R0,RE                                                            
*                                                                               
         LH    RF,=Y(AXBOLDL)                                                   
         AR    RF,R6                                                            
         XC    0(8,RF),0(RF)       CLEAR NEXT SLOT                              
*                                                                               
         MVI   FLAG2,0                                                          
         TM    TIMSTAT,TIMSMCS     BRANDOCEAN LINES NUMBERS DIFFERENT           
         JO    AXTAL50                                                          
         TM    TIMSTAT,TIMTEMPO    TEMPO NEEDS ITS OWN LINE NUMBERS             
         JNO   AXTAL40                                                          
         CLI   AXTALACT,C'D'       IS THIS A DELETED UPDATE?                    
         JNE   *+8                                                              
         MVI   AXTALACT,C'R'       PER SONJOY 3/03-MAKE IT 'R'                  
         J     AXTAL50                                                          
AXTAL40  EDIT  TIMLINE#,(5,AXTALLNO),ZERO=NOBLANK,FILL=0                        
         MVC   AXTATLNO,AXTALLNO                                                
         MVC   AXBOLLNO,AXTALLNO                                                
*&&US*&& MVI   FLAG2,TMS                                                        
*                                                                               
AXTAL50  MVC   AXTALTSK,TIMTSK     TASK                                         
         TR    AXTALTSK,MXTRT                                                   
         MVC   AXTATTSK,AXTALTSK   TASK                                         
*                                                                               
         MVI   AXBOLIBO,C'N'                                                    
         TM    TIMSTAT,TIMSMCS     SET IS FROM BRANDO BIT                       
         JNO   *+8                                                              
         MVI   AXBOLIBO,C'Y'                                                    
*                                                                               
*                                                                               
         MVC   AXTADPED,AXTALPED                                                
         CLC   =C'SJ',TIMACC       TEST IF PROD ALLOCATION                      
         JNE   AXTAL60                                                          
         LA    RE,TIMACC+2         POINT TO BEGINNING OF SJ ACT                 
         MVC   AXTALCLI,SPACES                                                  
         L     R1,ALEVELS             CLIENT                                    
         SR    RF,RF                                                            
         IC    RF,0(R1)                                                         
         BCTR  RF,0                                                             
         MVC   AXTALCLI(0),0(RE)                                                
         EX    RF,*-6                                                           
         TR    AXTALCLI,MXTRT                                                   
*                                                                               
         MVC   AXTALPRD,SPACES                                                  
         LA    RE,1(RE,RF)         BUMP TO NEXT POSITION IN TIMACC              
         SR    RF,RF               PRODUCT                                      
         IC    RF,1(R1)                                                         
         BCTR  RF,0                                                             
         MVC   AXTALPRD(0),0(RE)                                                
         EX    RF,*-6                                                           
         TR    AXTALPRD,MXTRT                                                   
*                                                                               
         MVC   AXTALJOB,SPACES                                                  
         LA    RE,1(RE,RF)         JOB                                          
         SR    RF,RF                                                            
         IC    RF,2(R1)                                                         
         BCTR  RF,0                                                             
         MVC   AXTALJOB(0),0(RE)                                                
         EX    RF,*-6                                                           
         TR    AXTALJOB,MXTRT                                                   
         J     AXTAL70                                                          
*                                                                               
AXTAL60  MVC   AXTALNCA,TIMACC+2   NON-CLIENT ACCOUNT CODE                      
         TR    AXTALNCA,MXTRT                                                   
*                                                                               
AXTAL70  EDIT  TIMTTYP,(3,AXTALTIM),FILL=0,ZERO=NOBLANK                         
*                                                                               
*        ZAP   DUB,=P'0'                                                        
*        OI    DUB+L'DUB-1,X'0F'                                                
*        MVO   DUB,TIMMOA(1)       MOA YEAR                                     
*        UNPK  AXTALMAY,DUB                                                     
*        MVO   DUB,TIMMOA+1(1)     MOA MONTH                                    
*        UNPK  AXTALMAM,DUB                                                     
*                                                                               
         MVC   WORK(L'TIMMOA),TIMMOA                                            
         MVI   WORK+L'TIMMOA,X'01'                                              
         GOTO1 VDATCON,PLIST,(1,WORK),(X'20',WORK+6)                            
         MVI   AXTALMAY,C'0'                                                    
         MVC   AXTALMAY+1(2),WORK+6                                             
         MVI   AXTALMAM,C'0'                                                    
         MVC   AXTALMAM+1(2),WORK+8                                             
*                                                                               
         MVI   AXTALWRI,C'N'                                                    
         TM    TIMIND,TIMIWO       IS WRITE-OFF                                 
         JZ    *+8                                                              
         MVI   AXTALWRI,C'Y'                                                    
*                                                                               
         MVI   AXTALADJ,C'N'                                                    
         TM    TIMIND,TIMIADJ      IS ADJUSTED                                  
         JZ    *+8                                                              
         MVI   AXTALADJ,C'Y'                                                    
*                                                                               
         MVI   AXTALIFT,C'N'                                                    
         TM    TIMSTAT,TIMTEMPO    IS FROM TEMPO                                
         JZ    *+8                                                              
         MVI   AXTALIFT,C'Y'                                                    
*                                                                               
*                                  HOURS ARE EXTRACTED BY TIME TYPE             
         ZAP   AXTALHBI+L'AXTALHBI-8(8),PZERO                                   
         ZAP   AXTALHCH+L'AXTALHCH-8(8),PZERO                                   
         ZAP   AXTALHNB+L'AXTALHNB-8(8),PZERO                                   
         ZAP   AXTALHNC+L'AXTALHNC-8(8),PZERO                                   
         LA    RE,AXTALHBI+L'AXTALHBI-8   BILLABLE                              
         CLI   TIMTTYP,TIMTCB                                                   
         JE    AXTAL80                                                          
         LA    RE,AXTALHCH+L'AXTALHCH-8   CHARGEABLE                            
         CLI   TIMTTYP,TIMTCR                                                   
         JE    AXTAL80                                                          
         LA    RE,AXTALHNB+L'AXTALHNB-8   NON-BILLABLE                          
         CLI   TIMTTYP,TIMTCN                                                   
         JE    AXTAL80                                                          
         LA    RE,AXTALHNC+L'AXTALHNC-8   MUST BE NON-CLIENT                    
*                                                                               
*XTAL80  EDIT  TIMHRS,(10,0(RE)),FILL=0,ZERO=NOBLANK,FLOAT=-                    
AXTAL80  AP    0(8,RE),TIMHRS                                                   
*                                                                               
         MVC   SAVSEQ,TIMSEQ       SAVE THIS SEQUENCE NUMBER                    
         ST    R7,SAVER7                                                        
*                                                                               
         CLI   TIMTTYP,0           ONLY SEND RATE FOR B AND R TIME              
         JE    AXTAL90                                                          
         CLI   TIMTTYP,TIMTCR      ONLY SEND RATE FOR B AND R TIME              
         JH    AXTAL90                                                          
         CLI   TIMLN,TIMBLNQ       ENSURE ELEMENT IS CORRECT LENGTH             
         JL    AXTAL90             NO                                           
*                                                                               
         OC    TIMRATE,TIMRATE     IS THERE A RATE PRESENT                      
         BZ    AXTAL82                                                          
         EDIT  TIMRATE,(10,AXTALRAT),FILL=0,ZERO=NOBLANK,FLOAT=-                
*                                                                               
AXTAL82  LA    RE,AXTALIAC         GRAB INCOME OR INCOME SUSPENSE ACCT          
         CLC   =C'SK',TIMINC                                                    
         JNE   *+8                                                              
         LA    RE,AXTALISA                                                      
         MVC   0(L'AXTALIAC,RE),TIMINC+2                                        
         TR    0(L'AXTALIAC,RE),MXTRT                                           
*                                                                               
AXTAL90  XR    R1,R1               LOOK FOR MORE TIMELS W. CURR SEQ NO.         
         IC    R1,1(R7)                                                         
         AR    R7,R1                                                            
*                                                                               
         CLI   0(R7),0                                                          
         JE    AXTAL140                                                         
         CLI   0(R7),TIMELQ                                                     
         JNE   AXTAL140                                                         
         CLC   TIMSEQ,SAVSEQ                                                    
         JNE   AXTAL90                                                          
*                                                                               
         CLI   TIMETYP,TIMETAX                                                  
         JE    AXTAL100                                                         
         CLI   TIMETYP,TIMENAR                                                  
         JE    AXTAL110                                                         
         CLI   TIMETYP,TIMEXTRA                                                 
         JE    AXTAL120                                                         
         CLI   TIMETYP,TIMETIME                                                 
         JE    AXTAL132                                                         
         J     AXTAL90                                                          
*                                                                               
AXTAL100 MVI   AXTAX,C'Y'                                                       
         MVC   AXTATLOC,TIMTLOC                                                 
         TR    AXTATLOC,MXTRT                                                   
         OC    TIMTBAS,TIMTBAS                                                  
         JNZ   *+10                                                             
         ZAP   TIMTBAS,=P'0'                                                    
         UNPK  AXTATBAS,TIMTBAS                                                 
         OI    AXTATBAS+L'AXTATBAS-1,X'F0'                                      
         J     AXTAL90                                                          
*                                                                               
AXTAL110 XR    RF,RF               COMMENT?                                     
         IC    RF,TIMLN                                                         
         SH    RF,=Y(TIMNLNQ+1)                                                 
         BM    AXTAL90             FUNNY ELEMENT/ZERO LEN COMMENT               
         CH    RF,=Y(L'AXTALCMT-1)                                              
         JNH   *+8                                                              
         LH    RF,=Y(L'AXTALCMT-1)                                              
         MVC   AXTALCMT(0),TIMNARR                                              
         EX    RF,*-6                                                           
         TR    AXTALCMT,MXTRT                                                   
         J     AXTAL90                                                          
*                                                                               
*                                  EXTRA DETAILS                                
AXTAL120 OC    TIMXTLN#,TIMXTLN#   IS THIS A REAL EXTRA?                        
         JZ    AXTAL130                                                         
*&&US*&& TM    FLAG2,TMS                                                        
*&&US*&& BO    AXTAL130                                                         
         EDIT  TIMXTLN#,(5,AXTALLNO),ZERO=NOBLANK,FILL=0,FLOAT=-                
         TR    AXTALLNO,MXTRT                                                   
         MVC   AXTATLNO,AXTALLNO                                                
         MVC   AXBOLLNO,AXTALLNO                                                
*                                                                               
AXTAL130 OC    TIMXTPDT,TIMXTPDT   SPLIT PERIOD?                                
         JZ    AXTAL90                                                          
*                                                                               
         XC    DUB,DUB                                                          
         MVC   WORK(L'AXTALPED),AXTALPED                                        
*&&US*&& XC    WORK(2),WORK+3                                                   
*&&US*&& XC    WORK+3(2),WORK                                                   
*&&US*&& XC    WORK(2),WORK+3                                                   
         GOTO1 VDATCON,PLIST,(4,WORK),(3,DUB),0                                 
         GOTO1 (RF),(R1),(1,TIMXTPDT),(3,DUB+3),0                               
         CLC   DUB(3),DUB+3                                                     
         JL    AXTAL90             DOESNT FALL IN THIS PERIOD                   
*                                                                               
         GOTO1 (RF),(R1),(1,TIMXTPDT),(10,AXTALPED),0                           
*&&US*&& XC    AXTALPED(2),AXTALPED+3                                           
*&&US*&& XC    AXTALPED+3(2),AXTALPED                                           
*&&US*&& XC    AXTALPED(2),AXTALPED+3                                           
         TR    AXTALPED,MXTRT                                                   
         J     AXTAL90                                                          
*                                                                               
AXTAL132 OC    TIMEIDNO,TIMEIDNO   IS THIS A REAL EXTRA?                        
         JZ    AXTAL134                                                         
*&&US*&& TM    FLAG2,TMS                                                        
*&&US*&& BO    AXTAL134                                                         
         EDIT  TIMEIDNO,(5,AXTALLNO),ZERO=NOBLANK,FILL=0,FLOAT=-                
         TR    AXTALLNO,MXTRT                                                   
         MVC   AXTATLNO,AXTALLNO                                                
         MVC   AXBOLLNO,AXTALLNO                                                
*                                                                               
AXTAL134 MVI   AXTALIFT,C'N'                                                    
         OC    TIMETPDT,TIMETPDT   SPLIT PERIOD?                                
         JZ    AXTAL136                                                         
*                                                                               
         XC    DUB,DUB                                                          
         MVC   WORK(L'AXTALPED),AXTALPED                                        
*&&US*&& XC    WORK(2),WORK+3                                                   
*&&US*&& XC    WORK+3(2),WORK                                                   
*&&US*&& XC    WORK(2),WORK+3                                                   
         GOTO1 VDATCON,PLIST,(4,WORK),(3,DUB),0                                 
         GOTO1 (RF),(R1),(1,TIMETPDT),(3,DUB+3),0                               
         CLC   DUB(3),DUB+3                                                     
         JL    AXTAL136            DOESNT FALL IN THIS PERIOD                   
*                                                                               
         GOTO1 (RF),(R1),(1,TIMETPDT),(10,AXTALPED),0                           
*&&US*&& XC    AXTALPED(2),AXTALPED+3                                           
*&&US*&& XC    AXTALPED+3(2),AXTALPED                                           
*&&US*&& XC    AXTALPED(2),AXTALPED+3                                           
         TR    AXTALPED,MXTRT                                                   
         MVC   AXTATPED,AXTALPED                                                
AXTAL136 LA    RE,TIMEHRS1                                                      
         XR    R0,R0                                                            
         XR    R1,R1                                                            
         IC    R1,TIMLN                                                         
         AR    R1,R7                                                            
AXTAL137 OC    0(L'TIMEHRS,RE),0(RE)                                            
         BZ    AXTAL138                                                         
*&&UK                                                                           
         TM    TIMSTAT,TIMSMCS     SET IS FROM BRANDO BIT                       
         JNO   *+8                                                              
         MVI   AXTADIBO,C'Y'                                                    
*&&                                                                             
         MVC   AXTADPED,AXTALPED                                                
         MVC   AXTADLNO,AXTALLNO                                                
         ZAP   AXTADHRS+L'AXTADHRS-8(8),PZERO                                   
         AP    AXTADHRS+L'AXTADHRS-8(8),0(4,RE)                                 
         CVD   R0,DUB                                                           
         OI    DUB+L'DUB-1,X'0F'                                                
         UNPK  AXTADDAY,DUB                                                     
         AH    R8,=Y(AXTADDL)                                                   
         ST    R8,ANXXTAD                                                       
         XC    0(8,R8),0(R8)                                                    
         MVC   AXTADD(AXTADDL),BLNKTAD                                          
         LH    RF,=Y(AXTADDL)                                                   
         AR    RF,R8                                                            
         XC    0(8,RF),0(RF)       CLEAR NEXT SLOT                              
AXTAL138 LA    RE,L'TIMEDAY(RE)                                                 
         AHI   R0,1                                                             
         CR    RE,R1                                                            
         BNL   AXTAL90                                                          
         B     AXTAL137                                                         
*                                                                               
AXTAL140 L     R7,SAVER7           RESTORE POINTER TO FIRST IN SEQUENCE         
         AH    R2,=Y(AXTALDL)                                                   
         XC    0(8,R2),0(R2)                                                    
         CLI   FLAG,0                                                           
         BE    AXTAL142                                                         
         ST    R2,ANXXTALD                                                      
         J     *+8                                                              
AXTAL142 ST    R2,ANXXTAL                                                       
         CLI   AXTAX,C'Y'          WAS THERE A TAX LINE TO ADD?                 
         JE    AXTAL150            YES                                          
         XC    0(8,R5),0(R5)                                                    
         MVI   AXTAX,C'N'          RESET TAX                                    
         B     AXTAL160            LOOK FOR ANOTHER CLUSTER                     
*                                                                               
AXTAL150 AH    R5,=Y(AXTATDL)                                                   
         ST    R5,ANXXTAT                                                       
         MVI   AXTAX,C'N'          RESET TAX                                    
*                                                                               
AXTAL160 XC    0(8,R8),0(R8)       CLEAR                                        
         AH    R6,=Y(AXBOLDL)                                                   
         ST    R6,ANXXBOL                                                       
         XC    0(8,R6),0(R6)       CLEAR                                        
         B     AXTAL18             LOOK FOR ANOTHER CLUSTER                     
         DROP  R5,R6,R8                                                         
         EJECT                                                                  
***********************************************************************         
* BUILD  BLANK RECORD AND INPUT KEY FIELDS FOR BOTH TYPES             *         
***********************************************************************         
         SPACE 1                                                                
AXTALBLK NTR1  ,                                                                
         LA    R0,BLNKTAL                                                       
         LA    RE,AXTALD                                                        
         LH    RF,=Y(AXTALDL)      LENGTH OF A TAL RECORD                       
         LR    R1,RF                                                            
         MVCL  R0,RE               MOVE HEADER TO BUILD AREA                    
*                                                                               
         LA    R0,BLNKTAT                                                       
         LA    RE,AXTALD                                                        
         LH    RF,=Y(AXTATDL)      LENGTH OF A TAT RECORD                       
         LR    R1,RF                                                            
         MVCL  R0,RE               MOVE HEADER TO BUILD AREA                    
*                                                                               
         LA    R0,BLNKTAD                                                       
         LA    RE,AXTALD                                                        
         LH    RF,=Y(AXTADDL)      LENGTH OF A TAD RECORD                       
         LR    R1,RF                                                            
         MVCL  R0,RE               MOVE HEADER TO BUILD AREA                    
*                                                                               
         LA    R0,BLNKBOL                                                       
         LA    RE,AXTALD                                                        
         LH    RF,=Y(AXBOLDL)      LENGTH OF A BOL RECORD                       
         LR    R1,RF                                                            
         MVCL  R0,RE               MOVE HEADER TO BUILD AREA                    
*                                                                               
         ZAP   DUB,=P'0'                                                        
         XR    RF,RF                                                            
         LA    R7,TIMRFST          FIRST ONE RECORD DATA                        
AXBTAL02 CLI   0(R7),0                                                          
         JE    AXBTAL06                                                         
         CLI   0(R7),PIDELQ                                                     
         JE    AXBTAL04                                                         
         IC    RF,1(R7)                                                         
         AR    R7,RF                                                            
         J     AXBTAL02                                                         
*                                                                               
         USING PIDELD,R7                                                        
AXBTAL04 XR    R0,R0               PACK PIDNO INTO DUB                          
         ICM   R0,3,PIDNO                                                       
         CVD   R0,DUB                                                           
         OI    DUB+L'DUB-1,X'0F'                                                
*                                                                               
AXBTAL06 LA    R2,BLNKTAL                                                       
         XC    AXTALLEN,AXTALLEN   RECORD LENGTH/TYPE/ENDOFREC                  
         MVC   AXTALLEN(2),=Y(AXTALDL)                                          
         MVC   AXTALTYP,AXDTALQ                                                 
         MVI   AXTALDX,MXTRTQ                                                   
         MVI   AXTALDX+1,MXTRTQ                                                 
         MVI   AXTALCCD-1,MXTRTQ   2-BYTE AGY ALPHA                             
         MVI   AXTALCSC-1,MXTRTQ   CONSYSPERSON CODE                            
         MVI   AXTALPED-1,MXTRTQ   PERIOD END DATE                              
         MVI   AXTALLNO-1,MXTRTQ   LINE NUMBER                                  
         MVI   AXTALCLI-1,MXTRTQ   CLIENT                                       
         MVI   AXTALPRD-1,MXTRTQ   PRODUCT                                      
         MVI   AXTALJOB-1,MXTRTQ   JOB                                          
         MVI   AXTALNCA-1,MXTRTQ   NON-CLIENT ACCOUNT CODE                      
         MVI   AXTALTSK-1,MXTRTQ   TASK                                         
         MVI   AXTALTIM-1,MXTRTQ   TIME TYPE                                    
         MVI   AXTALRAT-1,MXTRTQ   RATE                                         
         MVI   AXTALIAC-1,MXTRTQ   INCOME ACCOUNT CODE                          
         MVI   AXTALISA-1,MXTRTQ   INCOME SUSPENSE ACCOUNT CODE                 
         MVI   AXTALPOF-1,MXTRTQ   PRODUCTION OFFICE CODE                       
         MVI   AXTALHBI-1,MXTRTQ   BILLABLE HOURS                               
         MVI   AXTALHCH-1,MXTRTQ   CHARGEABLE HOURS                             
         MVI   AXTALHNB-1,MXTRTQ   NON-BILLABLE HOURS                           
         MVI   AXTALHNC-1,MXTRTQ   NON-CLIENT HOURS                             
         MVI   AXTALIFT-1,MXTRTQ   IS FROM TEMPO                                
         MVI   AXTALINV-1,MXTRTQ   IS INVALID                                   
         MVI   AXTALINV,C'N'                                                    
         MVI   AXTALCAC-1,MXTRTQ   COSTING ACCOUNT CODE (OR SPACES)             
         MVI   AXTALERR-1,MXTRTQ   ERROR NUMBER                                 
         MVI   AXTALMAY-1,MXTRTQ   MOA YEAR                                     
         MVI   AXTALMAM-1,MXTRTQ   MOA MONTH                                    
         MVI   AXTALWRI-1,MXTRTQ   IS WRITE-OFF                                 
         MVI   AXTALADJ-1,MXTRTQ   IS ADJUSTED                                  
         MVI   AXTALCMT-1,MXTRTQ   COMMENT                                      
*                                                                               
         MVC   AXTALCCD,SXDTAGY    2-BYTE AGY ALPHA                             
         TR    AXTALCCD,MXTRT                                                   
         MVC   AXTALPOF,TIMKOFF    PRODUCTION OFFICE CODE                       
         TR    AXTALPOF,MXTRT                                                   
*                                                                               
         UNPK  AXTALCSC,DUB        FILL DATA SPACE WITH CSC                     
         TR    AXTALCSC,MXTRT                                                   
*                                                                               
         GOTO1 VDATCON,PLIST,(1,TIMKPEDT),(10,AXTALPED),0                       
*&&US                                                                           
         XC    AXTALPED(2),AXTALPED+3                                           
         XC    AXTALPED+3(2),AXTALPED                                           
         XC    AXTALPED(2),AXTALPED+3                                           
*&&                                                                             
         TR    AXTALPED,MXTRT                                                   
         MVC   WORK(L'AXTALPED),AXTALPED SAVE FOR OTHER RECORDS                 
*                                                                               
         MVC   AXTALCAC,SPACES                                                  
         CLC   =C'1C',TIMKULC                                                   
         BNE   *+10                                                             
         MVC   AXTALCAC,TIMKCACT                                                
         TR    AXTALCAC,MXTRT                                                   
*                                                                               
         LA    R2,BLNKTAT                                                       
         USING AXTATD,R2                                                        
         XC    AXTATLEN,AXTATLEN   RECORD LENGTH/TYPE/ENDOFREC                  
         MVC   AXTATLEN(2),=Y(AXTATDL)                                          
         MVC   AXTATTYP,AXDTATQ                                                 
         MVI   AXTATDX,MXTRTQ                                                   
         MVI   AXTATDX+1,MXTRTQ                                                 
         MVI   AXTATCCD-1,MXTRTQ   2-BYTE AGY ALPHA                             
         MVI   AXTATCSC-1,MXTRTQ   CONSYSPERSON CODE                            
         MVI   AXTATPED-1,MXTRTQ   PERIOD END DATE                              
         MVI   AXTATLNO-1,MXTRTQ   LINE NUMBER                                  
         MVI   AXTATLOC-1,MXTRTQ   LOCALITY CODE                                
         MVI   AXTATBAS-1,MXTRTQ   BASIS AMOUNT                                 
         MVI   AXTATTSK-1,MXTRTQ   TASK                                         
*                                                                               
         MVC   AXTATCCD,SXDTAGY    2-BYTE AGY ALPHA                             
         TR    AXTATCCD,MXTRT                                                   
*                                                                               
         UNPK  AXTATCSC,DUB        FILL DATA SPACE WITH CSC                     
         TR    AXTATCSC,MXTRT                                                   
*                                                                               
         MVC   AXTATPED,WORK       SAVE FROM AXTALPED                           
         TR    AXTATPED,MXTRT                                                   
*                                                                               
         LA    R2,BLNKTAD                                                       
         USING AXTADD,R2                                                        
         XC    AXTADLEN,AXTADLEN   RECORD LENGTH/TYPE/ENDOFREC                  
         MVC   AXTADLEN(2),=Y(AXTADDL)                                          
         MVC   AXTADTYP,AXDTADQ                                                 
         MVI   AXTADDX,MXTRTQ                                                   
         MVI   AXTADDX+1,MXTRTQ                                                 
         MVI   AXTADCCD-1,MXTRTQ   2-BYTE AGY ALPHA                             
         MVI   AXTADCSC-1,MXTRTQ   CONSYSPERSON CODE                            
         MVI   AXTADPED-1,MXTRTQ   PERIOD END DATE                              
         MVI   AXTADLNO-1,MXTRTQ   LINE NUMBER                                  
         MVI   AXTADDAY-1,MXTRTQ   DAY                                          
         MVI   AXTADHRS-1,MXTRTQ   HOURS                                        
*&&UK*&& MVI   AXTADIBO-1,MXTRTQ   IS BRANDOCEAN TIME                           
*                                                                               
         MVC   AXTADCCD,SXDTAGY    2-BYTE AGY ALPHA                             
         TR    AXTADCCD,MXTRT                                                   
*                                                                               
         UNPK  AXTADCSC,DUB        FILL DATA SPACE WITH CSC                     
         TR    AXTADCSC,MXTRT                                                   
*                                                                               
         MVC   AXTADPED,WORK       SAVE FROM AXTALPED                           
         TR    AXTADPED,MXTRT                                                   
*                                                                               
         LA    R2,BLNKBOL                                                       
         USING AXBOLD,R2                                                        
         XC    AXBOLLEN,AXBOLLEN   RECORD LENGTH/TYPE/ENDOFREC                  
         MVC   AXBOLLEN(2),=Y(AXBOLDL)                                          
         MVC   AXBOLTYP,AXDBOLQ                                                 
         MVI   AXBOLDX,MXTRTQ                                                   
         MVI   AXBOLDX+1,MXTRTQ                                                 
         MVI   AXBOLCCD-1,MXTRTQ   2-BYTE AGY ALPHA                             
         MVI   AXBOLCSC-1,MXTRTQ   CONSYSPERSON CODE                            
         MVI   AXBOLPED-1,MXTRTQ   PERIOD END DATE                              
         MVI   AXBOLLNO-1,MXTRTQ   LINE NUMBER                                  
         MVI   AXBOLIBO-1,MXTRTQ   IS BRANDOCEAN TIME                           
*                                                                               
         MVC   AXBOLCCD,SXDTAGY    2-BYTE AGY ALPHA                             
         TR    AXBOLCCD,MXTRT                                                   
*                                                                               
         UNPK  AXBOLCSC,DUB        FILL DATA SPACE WITH CSC                     
         TR    AXBOLCSC,MXTRT                                                   
*                                                                               
         MVC   AXBOLPED,WORK       SAVE FROM AXTALPED                           
         TR    AXBOLPED,MXTRT                                                   
         XIT1  ,                                                                
*                                                                               
BLNKLEN  EQU   500                 LENGTH OF BLNKTAL & BLNKTAT                  
         DC    C'BLNKTAL '                                                      
BLNKTAL  DC    (BLNKLEN)X'00'                                                   
         DC    C'BLNKTAT '                                                      
BLNKTAT  DC    (BLNKLEN)X'00'                                                   
         DC    C'BLNKTAD '                                                      
BLNKTAD  DC    (BLNKLEN)X'00'                                                   
         DC    C'BLNKBOL '                                                      
BLNKBOL  DC    (BLNKLEN)X'00'                                                   
*                                                                               
AXTALCC  DC    A(BUFFER1)          TAL INFORMATION SAVE AREA                    
AXTALCC2 DC    A(BUFFER3)          TAL INFORMATION SAVE AREA FOR DELETE         
AXTATCC  DC    A(BUFFER2)          TAT INFORMATION SAVE AREA                    
AXTADCC  DC    A(BUFFER4)          TAD INFORMATION SAVE AREA                    
AXBOLCC  DC    A(BUFFER5)          BOL INFORMATION SAVE AREA                    
*                                                                               
         DROP  R2,R3,R4,R7,BLKTAL,BLKTAT,BLKTAD                                 
         EJECT                                                                  
***********************************************************************         
* AXTGRC - EXTRACT WORKCODE GROUP RECORDS FOR AXTRACT                 *         
*                                                                     *         
* AL4 A(EXTRACT RECORD)                                               *         
* AL4 A(ACCFILE RECORD)                                               *         
* AL4 0=ACCFILE-TO-EXTRACT                                            *         
* AL4 A(SXDTABD)                                                      *         
***********************************************************************         
         SPACE 1                                                                
AXTGRC   NMOD1 WORKL,*AXTGRC*,CLEAR=Y                                           
         USING WORKD,RC                                                         
         L     RA,AXTGRLIT                                                      
         USING LITERAL,RA                                                       
         ST    R1,APARM                                                         
         MVC   PARM,0(R1)          EXTRACT PARAMETER LIST                       
         LM    R2,R3,0(R1)                                                      
         L     R4,12(R1)                                                        
         USING AXTGRD,R2           R2=A(EXTRACT RECORD)                         
         USING WGRRECD,R3          R3=A(ACCFILE RECORD)                         
         USING SXDTABD,R4          R4=A(SXDXTABD)                               
*                                                                               
         XC    AXTGRLEN,AXTGRLEN   RECORD LENGTH/TYPE/ENDOFREC                  
         MVC   AXTGRLEN(2),=Y(AXTGRDL)                                          
         MVC   AXTGRTYP,AXDTGRQ                                                 
         MVI   AXTGRDX,MXTRTQ                                                   
         MVI   AXTGRDX+1,MXTRTQ                                                 
         MVI   AXTGRCCD-1,MXTRTQ   2-BYTE AGENCY ALPHA                          
         MVI   AXTGRUL-1,MXTRTQ   UNIT CODE                                     
         MVI   AXTGRCDE-1,MXTRTQ   TASK CODE                                    
         MVI   AXTGRNAM-1,MXTRTQ   LONG NAME                                    
*                                                                               
         MVC   AXTGRCCD,SXDTAGY    2-BYTE AGY ALPHA                             
         TR    AXTGRCCD,MXTRT                                                   
         MVC   AXTGRUL,WGRKUNT     UNIT/LEDGER CODE                             
         TR    AXTGRUL,MXTRT                                                    
         MVC   AXTGRCDE,WGRKCODE   WORKCODE GROUP                               
         TR    AXTGRCDE,MXTRT                                                   
*                                                                               
         LA    R5,WGRRFST          R5=A(FIRST ELEMENT)                          
AXTGR02  CLI   0(R5),0             GET ELEMENT FROM RECORD                      
         JE    EXIT                                                             
         CLI   0(R5),NAMELQ                                                     
         JE    AXTGR06                                                          
*                                                                               
AXTGR04  XR    RF,RF               BUMP TO NEXT ELEMENT                         
         ICM   RF,1,1(R5)                                                       
         JZ    EXIT                EXIT IF FUNNY ELEMENT LENGTH                 
         AR    R5,RF                                                            
         J     AXTGR02                                                          
*                                                                               
         USING NAMELD,R5                                                        
AXTGR06  XR    RF,RF               TASK LONG NAME                               
         IC    RF,NAMLN                                                         
         SH    RF,=H'3'                                                         
         JM    AXTGR04                                                          
         MVC   AXTGRNAM(0),NAMEREC                                              
         EX    RF,*-6                                                           
         TR    AXTGRNAM,MXTRT                                                   
         J     EXIT                                                             
*                                                                               
AXTGRLIT DC    A(LITERAL)                                                       
*                                                                               
         DROP  R2,R3,R4,R5                                                      
         EJECT                                                                  
***********************************************************************         
* AXPOFC - EXTRACT PRODUCTION OFFICE RECORDS FOR AXTRACT              *         
*                                                                     *         
* AL4 A(EXTRACT RECORD)                                               *         
* AL4 A(ACCFILE RECORD)                                               *         
* AL4 0=ACCFILE-TO-EXTRACT                                            *         
* AL4 A(SXDTABD)                                                      *         
***********************************************************************         
         SPACE 1                                                                
AXPOFC   NMOD1 WORKL,*AXPOFC*,CLEAR=Y                                           
         USING WORKD,RC                                                         
         L     RA,AXPOFLIT                                                      
         USING LITERAL,RA                                                       
         ST    R1,APARM                                                         
         MVC   PARM,0(R1)          EXTRACT PARAMETER LIST                       
         LM    R2,R3,0(R1)                                                      
         L     R4,12(R1)                                                        
         USING AXPOFD,R2           R2=A(EXTRACT RECORD)                         
         USING OGRRECD,R3          R3=A(ACCFILE RECORD)                         
         USING SXDTABD,R4          R4=A(SXDXTAB)                                
*                                                                               
         XC    AXPOFLEN,AXPOFLEN   RECORD LENGTH/TYPE/ENDOFREC                  
         MVC   AXPOFLEN(2),=Y(AXPOFDL)                                          
         MVC   AXPOFTYP,AXDPOFQ                                                 
         MVI   AXPOFDX,MXTRTQ                                                   
         MVI   AXPOFDX+1,MXTRTQ                                                 
         MVI   AXPOFCCD-1,MXTRTQ   2-BYTE AGY ALPHA                             
         MVI   AXPOFCDE-1,MXTRTQ   OFFICE CODE                                  
         MVI   AXPOFGC-1,MXTRTQ    OFFICE GROUP CODE                            
         MVI   AXPOFNAM-1,MXTRTQ   PRODUCTION OFFICE NAME                       
*                                                                               
         MVC   AXPOFCCD,SXDTAGY    AGENCY 2-BYTE ALPHA                          
         TR    AXPOFCCD,MXTRT                                                   
         MVC   AXPOFCDE,OGRKOFC    OFFICE CODE                                  
         TR    AXPOFCDE,MXTRT                                                   
*                                                                               
         XR    RF,RF               BUMP TO NEXT ELEMENT                         
         LA    R5,OGRRFST          R5=A(FIRST ELEMENT)                          
AXPOF02  CLI   0(R5),0             GET ELEMENT FROM RECORD                      
         JE    EXIT                                                             
         CLI   0(R5),NAMELQ        NAME ELEMENT                                 
         JE    AXPOF06                                                          
         CLI   0(R5),PGRELQ        GROUP ELEMENT                                
         JE    AXPOF08                                                          
*                                                                               
AXPOF04  XR    RF,RF               BUMP TO NEXT ELEMENT                         
         IC    RF,1(R5)                                                         
         AR    R5,RF                                                            
         J     AXPOF02                                                          
*                                                                               
         USING NAMELD,R5                                                        
AXPOF06  MVC   AXPOFNAM,SPACES                                                  
         SR    RF,RF                                                            
         IC    RF,NAMLN                                                         
         SH    RF,=H'3'                                                         
         JM    AXPOF04                                                          
         MVC   AXPOFNAM(0),NAMEREC                                              
         EX    RF,*-6                                                           
         TR    AXPOFNAM,MXTRT                                                   
         J     AXPOF04                                                          
*                                                                               
         USING PGRELD,R5                                                        
AXPOF08  MVC   AXPOFGC,PGRCODE     OFFICE GROUP                                 
         TR    AXPOFGC,MXTRT                                                    
         J     AXPOF04                                                          
*                                                                               
AXPOFLIT DC    A(LITERAL)                                                       
*                                                                               
         DROP  R2,R3,R4,R5                                                      
         EJECT                                                                  
***********************************************************************         
* AXEDTC - EXTRACT EDIT HOURS RECORDS FOR AXTRACT                     *         
*                                                                     *         
* AL4 A(EXTRACT RECORD)                                               *         
* AL4 A(ACCFILE RECORD)                                               *         
* AL1 0=BUILD 'K' RECORD, 1=BUILD ALL RECORDS, 2=PASS BACK NEXT       *         
*     X'FF' RETURNED IF NO MORE RECORDS TO PROCESS                    *         
* AL3 0                                                               *         
* AL4 A(SXDTABD)                                                      *         
***********************************************************************         
         SPACE 1                                                                
AXEDTC   NMOD1 WORKL,*AXEDTC*,CLEAR=Y                                           
         USING WORKD,RC                                                         
         L     RA,AXEDTLIT                                                      
         USING LITERAL,RA                                                       
         ST    R1,APARM                                                         
         MVC   PARM,0(R1)          EXTRACT PARAMETER LIST                       
         LM    R2,R3,0(R1)                                                      
         L     R4,12(R1)                                                        
         USING AXEDTD,R2           R2=A(EXTRACT RECORD)                         
         USING EDTRECD,R3          R3=A(ACCFILE RECORD)                         
         USING SXDTABD,R4          R4=A(SXDXTAB)                                
*                                                                               
         CLI   8(R1),1                                                          
         JL    AXEDTKIL            BUILD K RECORD                               
         JE    AXEDTSER            BUILD SERIES OF RECORDS                      
         J     AXEDTNXT            OUTPUT NEXT RECORD                           
*                                                                               
AXEDTLIT DC    A(LITERAL)                                                       
         EJECT                                                                  
***********************************************************************         
* BUILD 'K' RECORD AND EXIT                                           *         
***********************************************************************         
         SPACE 1                                                                
AXEDTKIL BRAS  RE,AXEDTBLK                                                      
         J     EXIT                                                             
         SPACE 1                                                                
***********************************************************************         
* GET NEXT RECORD AND PASS IT BACK                                    *         
***********************************************************************         
         SPACE 1                                                                
AXEDTNXT L     RE,ANEXT                                                         
         OC    0(8,RE),0(RE)       RECORD TO PASS BACK?                         
         JNZ   *+12                                                             
         MVI   8(R1),X'FF'         NO MORE RECORDS                              
         J     EXIT                                                             
*                                                                               
         L     RE,ANEXT                                                         
         LA    R0,AXEDTD                                                        
         LH    RF,=Y(AXEDTDL)                                                   
         LR    R1,RF                                                            
         MVCL  R0,RE               COPY IN NEXT RECORD                          
*                                                                               
         L     RE,ANEXT                                                         
         AH    RE,=Y(AXEDTDL)                                                   
         ST    RE,ANEXT            UPDATE POINTER                               
         J     EXIT                                                             
         SPACE 1                                                                
***********************************************************************         
* BUILD SERIES OF RECORDS FOR THIS RECORD                             *         
***********************************************************************         
         SPACE 1                                                                
AXEDTSER BRAS  RE,AXEDTBLK         SET UP BLANK RECORD (KEY DETAIL)             
*                                                                               
         LH    RF,=Y(AXEDTDL)                                                   
         STH   RF,RECLEN                                                        
         L     R2,AXEDTCC          WHERE RECORDS TO BE BUILT                    
         ST    R2,ANEXT            FOR NEXT TIME IN                             
         XC    0(8,R2),0(R2)                                                    
*                                                                               
         LA    R6,EDTRFST          FIRST ONE RECORD DATA                        
AXEDT02  CLI   0(R6),0                                                          
         JE    EXIT                                                             
         CLI   0(R6),SHRELQ                                                     
         JE    AXEDT06                                                          
         CLI   0(R6),DEDELQ                                                     
         JE    AXEDT06                                                          
*                                                                               
AXEDT04  XR    R1,R1                                                            
         IC    R1,1(R6)                                                         
         AR    R6,R1                                                            
         J     AXEDT02                                                          
*                                                                               
AXEDT06  L     RE,AXREC            BLANK RECORD HERE                            
         LR    R0,R2                                                            
         LH    RF,RECLEN           LENGTH OF A BLANK RECORD                     
         LR    R1,RF                                                            
         MVCL  R0,RE                                                            
*                                                                               
         LH    RF,RECLEN                                                        
         AR    RF,R2                                                            
         XC    0(8,RF),0(RF)       BLANK OFF START OF NEXT                      
*                                                                               
         CLI   0(R6),DEDELQ                                                     
         JE    AXEDT08                                                          
         USING SHRELD,R6                                                        
         EDIT  SHRHOURS,(10,AXEDTHRS),FILL=0,ZERO=NOBLANK                       
         GOTO1 VDATCON,PLIST,(1,SHRSTART),(10,AXEDTSDT),0                       
*&&US                                                                           
         XC    AXEDTSDT(2),AXEDTSDT+3                                           
         XC    AXEDTSDT+3(2),AXEDTSDT                                           
         XC    AXEDTSDT(2),AXEDTSDT+3                                           
*&&                                                                             
         TR    AXEDTSDT,MXTRT                                                   
         GOTO1 VDATCON,PLIST,(1,SHREND),(10,AXEDTEDT),0                         
*&&US                                                                           
         XC    AXEDTEDT(2),AXEDTEDT+3                                           
         XC    AXEDTEDT+3(2),AXEDTEDT                                           
         XC    AXEDTEDT(2),AXEDTEDT+3                                           
*&&                                                                             
         TR    AXEDTEDT,MXTRT                                                   
         MVI   AXEDTVAR,C'N'                                                    
*                                                                               
         AH    R2,RECLEN           NEXT IN BUFFER                               
         J     AXEDT04                                                          
*                                                                               
         USING DEDELD,R6                                                        
AXEDT08  EDIT  DEDHRS,(10,AXEDTHRS),FILL=0,ZERO=NOBLANK                         
         MVC   TMPDTE(L'EDTRSTDT),EDTRSTDT  SAVE AS DEFAULT FOR DAYS            
         MVC   TMPDTE+L'EDTRSTDT(L'EDTRENDT),EDTRENDT                           
*                                                                               
         CLI   DEDLN,DEDLN1Q       IS IT SHORT ELEMENT                          
         BE    AXEDT10             YES - NO DATE                                
         OC    DEDDATE,DEDDATE     HAVE WE A VALID DATE                         
         BZ    AXEDT10             NO - NOTHING                                 
         MVC   TMPDTE(L'DEDDATE),DEDDATE   OVERRIDE WITH ACTUAL                 
         MVC   TMPDTE+L'DEDDATE(L'DEDDATE),DEDDATE                              
*                                                                               
AXEDT10  GOTO1 VDATCON,PLIST,(1,TMPDTE),(10,AXEDTSDT),0                         
*&&US                                                                           
         XC    AXEDTSDT(2),AXEDTSDT+3                                           
         XC    AXEDTSDT+3(2),AXEDTSDT                                           
         XC    AXEDTSDT(2),AXEDTSDT+3                                           
*&&                                                                             
         TR    AXEDTSDT,MXTRT                                                   
         GOTO1 VDATCON,PLIST,(1,TMPDTE+L'DEDDATE),(10,AXEDTEDT),0               
*&&US                                                                           
         XC    AXEDTEDT(2),AXEDTEDT+3                                           
         XC    AXEDTEDT+3(2),AXEDTEDT                                           
         XC    AXEDTEDT(2),AXEDTEDT+3                                           
*&&                                                                             
         TR    AXEDTEDT,MXTRT                                                   
*                                                                               
         MVC   AXEDTWKD,=C'000'    INITIALIZE FIELD TO CHAR ZEROES              
         MVC   AXEDTWKD+L'AXEDTWKD-L'DEDIND(L'DEDIND),DEDIND                    
         TR    AXEDTWKD,MXTRT                                                   
         MVI   AXEDTVAR,C'N'                                                    
         TM    DEDSTAT,DEDSEXCP                                                 
         BZ    AXEDT12                                                          
         MVI   AXEDTVAR,C'Y'                                                    
AXEDT12  AH    R2,RECLEN           NEXT IN BUFFER                               
         J     AXEDT04                                                          
         EJECT                                                                  
***********************************************************************         
* BUILD BLANK RECORD AND INPUT KEY FIELDS                             *         
***********************************************************************         
         SPACE 1                                                                
AXEDTBLK NTR1  ,                                                                
         XC    AXEDTLEN,AXEDTLEN   RECORD LENGTH/TYPE/ENDOFREC                  
         MVC   AXEDTLEN(2),=Y(AXEDTDL)                                          
         MVC   AXEDTTYP,AXDEDTQ                                                 
         MVI   AXEDTDX,MXTRTQ                                                   
         MVI   AXEDTDX+1,MXTRTQ                                                 
         MVI   AXEDTCCD-1,MXTRTQ   2-BYTE AGY ALPHA                             
         MVI   AXEDTOFC-1,MXTRTQ   OFFICE CODE                                  
         MVI   AXEDTDEC-1,MXTRTQ   DEPARTMENT CODE                              
         MVI   AXEDTSDC-1,MXTRTQ   SUB DEPT CODE                                
         MVI   AXEDTPER-1,MXTRTQ   PERSON CODE                                  
         MVI   AXEDTFDT-1,MXTRTQ   FIRST DATE                                   
         MVI   AXEDTLDT-1,MXTRTQ   LAST DATE                                    
         MVI   AXEDTSDT-1,MXTRTQ   START DATE                                   
         MVI   AXEDTEDT-1,MXTRTQ   END DATE                                     
         MVI   AXEDTHRS-1,MXTRTQ   HOURS                                        
         MVI   AXEDTWKD-1,MXTRTQ   WEEKDAY                                      
         MVI   AXEDTVAR-1,MXTRTQ   VARIANCE                                     
         MVC   AXEDTCCD,SXDTAGY    AGENCY 2-BYTE ALPHA                          
         TR    AXEDTCCD,MXTRT                                                   
         MVC   AXEDTOFC,EDTKOFC    OFFICE CODE                                  
         TR    AXEDTOFC,MXTRT                                                   
         MVC   AXEDTDEC(L'EDTKDPT),EDTKDPT    DEPARTMENT CODE                   
         TR    AXEDTDEC,MXTRT                                                   
         MVC   AXEDTSDC(L'EDTKSBD),EDTKSBD    SUB DEPARTMENT CODE               
         TR    AXEDTSDC,MXTRT                                                   
         MVC   AXEDTPER,EDTKPER    PERSON CODE                                  
         TR    AXEDTPER,MXTRT                                                   
         GOTO1 VDATCON,PLIST,(1,EDTRSTDT),(10,AXEDTFDT),0                       
*&&US                                                                           
         XC    AXEDTFDT(2),AXEDTFDT+3                                           
         XC    AXEDTFDT+3(2),AXEDTFDT                                           
         XC    AXEDTFDT(2),AXEDTFDT+3                                           
*&&                                                                             
         TR    AXEDTFDT,MXTRT                                                   
*                                                                               
         GOTO1 VDATCON,PLIST,(1,EDTRENDT),(10,AXEDTLDT),0                       
*&&US                                                                           
         XC    AXEDTLDT(2),AXEDTLDT+3                                           
         XC    AXEDTLDT+3(2),AXEDTLDT                                           
         XC    AXEDTLDT(2),AXEDTLDT+3                                           
*&&                                                                             
         TR    AXEDTLDT,MXTRT                                                   
         XIT1  ,                                                                
*                                                                               
AXEDTCC  DC    A(BUFFER1)          EDIT HOURS BUILD AREA                        
*                                                                               
         DROP  R2,R3,R4,R6                                                      
         EJECT                                                                  
***********************************************************************         
* AXCAPC - EXTRACT COST ALLOCATION PROFILE RECORDS FOR AXTRACT        *         
*                                                                     *         
* AL4 A(EXTRACT RECORD)                                               *         
* AL4 A(ACCFILE RECORD)                                               *         
* AL4 0=ACCFILE-TO-EXTRACT                                            *         
* AL4 A(SXDTABD)                                                      *         
***********************************************************************         
         SPACE 1                                                                
AXCAPC   NMOD1 WORKL,*AXCAPC*,CLEAR=Y                                           
         USING WORKD,RC                                                         
         L     RA,AXCAPLIT                                                      
         USING LITERAL,RA                                                       
         ST    R1,APARM                                                         
         MVC   PARM,0(R1)          EXTRACT PARAMETER LIST                       
         LM    R2,R3,0(R1)                                                      
         L     R4,12(R1)                                                        
         USING AXCAPD,R2           R2=A(EXTRACT RECORD)                         
         USING CAPRECD,R3          R3=A(ACCFILE RECORD)                         
         USING SXDTABD,R4          R4=A(SXDXTAB)                                
*                                                                               
         XC    AXCAPLEN,AXCAPLEN   RECORD LENGTH/TYPE/ENDOFREC                  
         MVC   AXCAPLEN(2),=Y(AXCAPDL)                                          
         MVC   AXCAPTYP,AXDCAPQ                                                 
         MVI   AXCAPDX,MXTRTQ                                                   
         MVI   AXCAPDX+1,MXTRTQ                                                 
         MVI   AXCAPCCD-1,MXTRTQ   2-BYTE AGY ALPHA                             
         MVI   AXCAPOFC-1,MXTRTQ   OFFICE CODE                                  
         MVI   AXCAPDEP-1,MXTRTQ   DEPARTMENT                                   
         MVI   AXCAPSDC-1,MXTRTQ   SUB-DEPARTMENT                               
         MVI   AXCAPPER-1,MXTRTQ   PERSON                                       
         MVI   AXCAPPSB-1,MXTRTQ   POST SALES FOR BILLABLE                      
         MVI   AXCAPPSC-1,MXTRTQ   POST SALES FOR CHARGEABLE                    
         MVI   AXCAPPBI-1,MXTRTQ   POST BILLABLE TO INCOME                      
         MVI   AXCAPUDI-1,MXTRTQ   USE DEFAULT INCOME                           
*                                                                               
         MVC   AXCAPCCD,SXDTAGY    AGENCY 2-BYTE ALPHA                          
         TR    AXCAPCCD,MXTRT                                                   
         MVC   AXCAPOFC,CAPKOFC    OFFICE CODE                                  
         TR    AXCAPOFC,MXTRT                                                   
         MVC   AXCAPDEP,CAPKDPT    DEPARTMENT                                   
         TR    AXCAPDEP,MXTRT                                                   
         MVC   AXCAPSDC,CAPKSDT    SUB DEPT CODE                                
         TR    AXCAPSDC,MXTRT                                                   
         MVC   AXCAPPER,CAPKPER    PERSON                                       
         TR    AXCAPPER,MXTRT                                                   
*                                                                               
         MVI   AXCAPPSB,C'N'       POST SALES FOR BILLABLE                      
         MVI   AXCAPPSC,C'N'       POST SALES FOR CHARGEABLE                    
         MVI   AXCAPPBI,C'N'       POST BILLABLE TO INCOME                      
         MVI   AXCAPUDI,C'N'       USE DEFAULT INCOME                           
*                                                                               
         LA    R6,CAPRFST          FIRST RECORD DATA                            
AXCAP02  CLI   0(R6),0                                                          
         JE    EXIT                                                             
         CLI   0(R6),OPDELQ                                                     
         JE    AXCAP06                                                          
*                                                                               
AXCAP04  XR    R1,R1                                                            
         IC    R1,1(R6)                                                         
         AR    R6,R1                                                            
         J     AXCAP02                                                          
*                                                                               
         USING OPDELD,R6                                                        
AXCAP06  CLI   OPDNUM,X'1A'        RTB - POST SALES FOR BILLABLE                
         JNE   *+8                                                              
         MVI   AXCAPPSB,C'Y'                                                    
*                                                                               
         CLI   OPDNUM,X'19'        RTR - POST SALES FOR CHARGEABLE              
         JNE   *+8                                                              
         MVI   AXCAPPSC,C'Y'                                                    
*                                                                               
         CLI   OPDNUM,X'0B'        BTL - POST BILLABLE TO INCOME                
         JNE   AXCAP08                                                          
         MVI   AXCAPPBI,C'N'                                                    
         CLI   OPDDATA,C'I'                                                     
         JNE   *+8                                                              
         MVI   AXCAPPBI,C'Y'                                                    
*                                                                               
AXCAP08  CLI   OPDNUM,X'0F'        DLK - USE DEFAULT INCOME                     
         JNE   AXCAP04                                                          
         MVC   AXCAPUDI,OPDDATA                                                 
         TR    AXCAPUDI,MXTRT                                                   
         J     AXCAP04                                                          
*                                                                               
AXCAPLIT DC    A(LITERAL)                                                       
*                                                                               
         DROP  R2,R3,R4,R6                                                      
         EJECT                                                                  
***********************************************************************         
* AXPHIC - EXTRACT PAYROLL HISTORY RECORDS FOR AXTRACT                *         
*                                                                     *         
* AL4 A(EXTRACT RECORD)                                               *         
* AL4 A(ACCFILE RECORD)                                               *         
* AL1 1=BUILD ALL RECORDS, 2=PASS BACK NEXT                           *         
*     X'FF' RETURNED IF NO MORE RECORDS TO PROCESS                    *         
* AL3 0                                                               *         
* AL4 A(SXDTABD)                                                      *         
***********************************************************************         
         SPACE 1                                                                
AXPHIC   NMOD1 WORKL,*AXPHIC*,CLEAR=Y                                           
         USING WORKD,RC                                                         
         L     RA,AXPHILIT                                                      
         USING LITERAL,RA                                                       
         ST    R1,APARM                                                         
         MVC   PARM,0(R1)          EXTRACT PARAMETER LIST                       
         LM    R2,R3,0(R1)                                                      
         L     R4,12(R1)                                                        
         USING AXPHID,R2           R2=A(EXTRACT RECORD) (LONGEST)               
         USING PHIRECD,R3          R3=A(ACCFILE RECORD)                         
         USING SXDTABD,R4          R4=A(SXDXTAB)                                
*                                                                               
         CLI   8(R1),1                                                          
         JH    AXPHINXT            OUTPUT NEXT RECORD                           
         JE    AXPHISER            BUILD RECORDS FROM ADD RECORD                
         DC    H'0'                                                             
*                                                                               
AXPHILIT DC    A(LITERAL)                                                       
         EJECT                                                                  
***********************************************************************         
* GET NEXT RECORD AND PASS IT BACK                                    *         
***********************************************************************         
         SPACE 1                                                                
AXPHINXT L     RE,ANEXT            A(NEXT HISTORY RECORD)                       
         OC    0(8,RE),0(RE)       RECORD TO PASS BACK?                         
         JNZ   *+12                YES                                          
         MVI   8(R1),X'FF'                                                      
         J     EXIT                                                             
*                                                                               
         L     RE,ANEXT                                                         
         LA    R0,AXPHID                                                        
         LH    RF,=Y(AXPHIDL)                                                   
         LR    R1,RF                                                            
         MVCL  R0,RE               COPY IN NEXT RECORD                          
*                                                                               
         L     RE,ANEXT                                                         
         AH    RE,=Y(AXPHIDL)                                                   
         ST    RE,ANEXT            UPDATE POINTER                               
         J     EXIT                                                             
         SPACE 1                                                                
***********************************************************************         
* BUILD SERIES OF RECORDS FOR THIS RECORD                             *         
***********************************************************************         
         SPACE 1                                                                
AXPHISER BAS   RE,AXPHIBLK         SET UP BLANK RECORDS (KEY DETAIL)            
*                                                                               
         LH    RF,=Y(AXPHIDL)                                                   
         STH   RF,RECLEN                                                        
         L     R2,AXPHICC          WHERE RECORDS TO BE BUILT                    
         ST    R2,ANEXT            FOR NEXT TIME IN (PHI)                       
         XC    0(8,R2),0(R2)                                                    
*                                                                               
         USING PDEELD,R7                                                        
         LA    R7,PHIRFST          FIRST ONE RECORD DATA                        
AXPHI02  CLI   PDEEL,0             END OF RECORD?                               
         JE    EXIT                YES - FINISHED                               
         CLI   PDEEL,PDEELQ        PDEEL?                                       
         JNE   *+12                                                             
         TM    PDESTAT2,PDESHRTE   HOURLY RATE?                                 
         JO    AXPHI04                                                          
*                                                                               
         XR    R1,R1                                                            
         IC    R1,PDELN                                                         
         AR    R7,R1                                                            
         J     AXPHI02                                                          
*                                                                               
AXPHI04  LR    R0,R2               COPY IN EMPTY RECORD DETAILS                 
         L     RE,AXREC                                                         
         LH    RF,RECLEN                                                        
         LR    R1,RF                                                            
         MVCL  R0,RE                                                            
*                                                                               
         LH    RF,RECLEN                                                        
         AR    RF,R2                                                            
         XC    0(8,RF),0(RF)                                                    
*                                                                               
         ZAP   AMNT,=P'0'          RESET AMOUNT                                 
AXPHI06  MVC   DATE,PDEDTE         SET DATE FOR THIS RECORD                     
         AP    AMNT,PDEAMT         ADD AMOUNT AND ADJUSTMENT                    
         AP    AMNT,PDEADJ                                                      
*                                                                               
AXPHI08  XR    R1,R1               NEXT ELEMENT                                 
         IC    R1,PDELN                                                         
         AR    R7,R1                                                            
*                                                                               
         CLI   PDEEL,0             END OF RECORD?                               
         JE    AXPHI12                                                          
         CLI   PDEEL,PDEELQ        ANOTHER PDEEL?                               
         JNE   AXPHI08                                                          
         TM    PDESTAT2,PDESHRTE   HOURLY RATE?                                 
         JZ    AXPHI08                                                          
         CLC   DATE,PDEDTE         SAME DATE?                                   
         JE    AXPHI06             YES                                          
*                                                                               
AXPHI12  GOTO1 VDATCON,PLIST,(1,DATE),(10,AXPHIDTE),0                           
*&&US                                                                           
         XC    AXPHIDTE(2),AXPHIDTE+3                                           
         XC    AXPHIDTE+3(2),AXPHIDTE                                           
         XC    AXPHIDTE(2),AXPHIDTE+3                                           
*&&                                                                             
         EDIT  (P8,AMNT),(10,AXPHIAMT),ZERO=NOBLANK,FILL=0                      
*                                                                               
         AH    R2,RECLEN           NEXT SPACE FOR PHI RECORD                    
         B     AXPHI02             CONTINUE                                     
         SPACE 1                                                                
***********************************************************************         
* BUILD  BLANK RECORD AND INPUT KEY FIELDS                            *         
***********************************************************************         
         SPACE 1                                                                
AXPHIBLK NTR1  ,                                                                
         XC    AXPHILEN,AXPHILEN   RECORD LENGTH/TYPE/ENDOFREC                  
         MVC   AXPHILEN(2),=Y(AXPHIDL)                                          
         MVC   AXPHITYP,AXDPHIQ                                                 
         MVI   AXPHIDX,MXTRTQ                                                   
         MVI   AXPHIDX+1,MXTRTQ                                                 
         MVI   AXPHICCD-1,MXTRTQ   2-BYTE AGY ALPHA                             
         MVI   AXPHIOFC-1,MXTRTQ   OFFICE CODE                                  
         MVI   AXPHIDEP-1,MXTRTQ   DEPARTMENT CODE                              
         MVI   AXPHISDC-1,MXTRTQ   SUB DEPT CODE                                
         MVI   AXPHICSP-1,MXTRTQ   PERSON CODE                                  
         MVI   AXPHIDTE-1,MXTRTQ   DATE                                         
         MVI   AXPHISUB-1,MXTRTQ   SEQUENCE NUMBER                              
*                                                                               
         MVC   AXPHICCD,SXDTAGY    2-BYTE AGY ALPHA                             
         TR    AXPHICCD,MXTRT                                                   
         MVC   AXPHIOFC,PHIKOFC    OFFICE CODE                                  
         TR    AXPHIOFC,MXTRT                                                   
         MVC   AXPHIDEP,PHIKDPT    DEPT CODE                                    
         TR    AXPHIDEP,MXTRT                                                   
         MVC   AXPHISDC,PHIKSBD    SUB DEPT CODE                                
         TR    AXPHISDC,MXTRT                                                   
         MVC   AXPHICSP,PHIKPER    PERSON CODE                                  
         TR    AXPHICSP,MXTRT                                                   
         XR    RF,RF               SEQUENCE NUMBER                              
         IC    RF,PHIKSUB                                                       
         EDIT  (RF),(5,AXPHISUB),ZERO=NOBLANK,FILL=0                            
         TR    AXPHISUB,MXTRT                                                   
         XIT1  ,                                                                
*                                                                               
AXPHICC  DC    A(BUFFER1)          INFORMATION SAVE AREA                        
*                                                                               
         DROP  R2,R3,R4,R7                                                      
         EJECT                                                                  
***********************************************************************         
* AXDTMC - EXTRACT DAILY TIME INFORMATION FOR AXTRACT                 *         
*                                                                     *         
* AL4 A(EXTRACT RECORD)                                               *         
* AL4 A(ACCFILE RECORD)                                               *         
* AL4 0=ACCFILE-TO-EXTRACT                                            *         
* AL4 A(SXDTABD) - CAN GET 2 BYTE ALPHA FROM THIS...                  *         
* AL4 A(LEVELS) - CAN GET LENGTHS OF LEVELS FROM HERE                 *         
***********************************************************************         
         SPACE 1                                                                
AXDTMC   NMOD1 WORKL,*AXDTMC*,CLEAR=Y                                           
         USING WORKD,RC                                                         
         L     RA,AXDTMLIT                                                      
         USING LITERAL,RA                                                       
         ST    R1,APARM                                                         
         MVC   PARM,0(R1)          EXTRACT PARAMETER LIST                       
         MVC   ALEVELS,16(R1)                                                   
         LM    R2,R3,0(R1)                                                      
         L     R4,12(R1)                                                        
         USING AXDTMD,R2           R2=A(EXTRACT RECORD)                         
         USING ACTKEY,R3           R3=A(ACCFILE RECORD)                         
         USING SXDTABD,R4          R5=A(SXDTABD)                                
*                                                                               
         CLI   8(R1),1                                                          
         JH    AXDTMNXT            OUTPUT NEXT RECORD                           
         JE    AXDTMSER            BUILD RECORDS FROM ADD RECORD                
         DC    H'0'                                                             
*                                                                               
AXDTMLIT DC    A(LITERAL)                                                       
*                                                                               
         EJECT                                                                  
***********************************************************************         
* GET NEXT RECORD AND PASS IT BACK                                    *         
***********************************************************************         
         SPACE 1                                                                
AXDTMNXT L     RE,ANEXT            A(NEXT HISTORY RECORD)                       
         OC    0(8,RE),0(RE)       RECORD TO PASS BACK?                         
         JNZ   *+12                YES                                          
         MVI   8(R1),X'FF'                                                      
         J     EXIT                                                             
*                                                                               
         L     RE,ANEXT                                                         
         LA    R0,AXDTMD                                                        
         LH    RF,=Y(AXDTMDL)                                                   
         LR    R1,RF                                                            
         MVCL  R0,RE               COPY IN NEXT RECORD                          
*                                                                               
         L     RE,ANEXT                                                         
         AH    RE,=Y(AXDTMDL)                                                   
         ST    RE,ANEXT            UPDATE POINTER                               
         J     EXIT                                                             
         SPACE 1                                                                
***********************************************************************         
* BUILD SERIES OF RECORDS FOR THIS RECORD                             *         
***********************************************************************         
         SPACE 1                                                                
AXDTMSER BAS   RE,AXDTMBLK         SET UP BLANK RECORDS (KEY DETAIL)            
*                                                                               
         LH    RF,=Y(AXDTMDL)                                                   
         STH   RF,RECLEN                                                        
         L     R2,AXDTMCC          WHERE RECORDS TO BE BUILT                    
         ST    R2,ANEXT            FOR NEXT TIME IN (DTM)                       
         XC    0(8,R2),0(R2)                                                    
*                                                                               
         LA    R5,ACTRFST          R5=A(FIRST ELEMENT)                          
         USING GDAELD,R5                                                        
AXDTM02  CLI   0(R5),0             GET ELEMENT FROM RECORD                      
         JE    EXIT                                                             
         CLI   0(R5),GDAELQ        GENERAL DATE ELEMENT                         
         JNE   AXDTM04                                                          
         CLI   GDATYPE,GDATDTIM                                                 
         BE    AXDTM06                                                          
*                                                                               
AXDTM04  XR    RF,RF               BUMP TO NEXT ELEMENT                         
         ICM   RF,1,1(R5)                                                       
         JZ    EXIT                EXIT IF FUNNY ELEMENT LENGTH                 
         AR    R5,RF                                                            
         J     AXDTM02                                                          
*                                                                               
AXDTM06  LR    R0,R2               COPY IN EMPTY RECORD DETAILS                 
         L     RE,AXREC                                                         
         LH    RF,RECLEN                                                        
         LR    R1,RF                                                            
         MVCL  R0,RE                                                            
*                                                                               
         LH    RF,RECLEN                                                        
         AR    RF,R2                                                            
         XC    0(8,RF),0(RF)                                                    
*                                                                               
         OC    GDADATE,GDADATE                                                  
         BZ    AXDTM08                                                          
         GOTO1 VDATCON,PLIST,(1,GDADATE),(10,AXDTMSDT)                          
*&&US                                                                           
         XC    AXDTMSDT(2),AXDTMSDT+3                                           
         XC    AXDTMSDT+3(2),AXDTMSDT                                           
         XC    AXDTMSDT(2),AXDTMSDT+3                                           
*&&                                                                             
         TR    AXDTMSDT,MXTRT      TRANSLATE DATE                               
AXDTM08  OC    GDADATE2,GDADATE2                                                
         BZ    AXDTM10                                                          
         GOTO1 VDATCON,PLIST,(1,GDADATE2),(10,AXDTMEDT)                         
*&&US                                                                           
         XC    AXDTMEDT(2),AXDTMEDT+3                                           
         XC    AXDTMEDT+3(2),AXDTMEDT                                           
         XC    AXDTMEDT(2),AXDTMEDT+3                                           
*&&                                                                             
         TR    AXDTMEDT,MXTRT      TRANSLATE DATE                               
AXDTM10  AH    R2,RECLEN           NEXT SPACE FOR DTM RECORD                    
         B     AXDTM04             CONTINUE                                     
*                                                                               
         SPACE 1                                                                
***********************************************************************         
* BUILD  BLANK RECORD AND INPUT KEY FIELDS                            *         
***********************************************************************         
         SPACE 1                                                                
AXDTMBLK NTR1  ,                                                                
         XC    AXDTMLEN,AXDTMLEN   RECORD LENGTH/TYPE/ENDOFREC                  
         MVC   AXDTMLEN(2),=Y(AXDTMDL)                                          
         MVC   AXDTMTYP,AXDDTMQ                                                 
         MVI   AXDTMDX,MXTRTQ                                                   
         MVI   AXDTMDX+1,MXTRTQ                                                 
         MVI   AXDTMCCD-1,MXTRTQ   2-BYTE AGY ALPHA                             
         MVI   AXDTMOFC-1,MXTRTQ   OFFICE CODE                                  
         MVI   AXDTMDEC-1,MXTRTQ   DEPARTMENT CODE                              
         MVI   AXDTMSDC-1,MXTRTQ   SUB DEPARTMENT CODE                          
         MVI   AXDTMPC-1,MXTRTQ    PERSON CODE                                  
         MVI   AXDTMSDT-1,MXTRTQ   START DATE                                   
         MVI   AXDTMEDT-1,MXTRTQ   END DATE                                     
*                                                                               
         MVC   AXDTMCCD,SXDTAGY    2-BYTE AGY ALPHA                             
         TR    AXDTMCCD,MXTRT                                                   
*                                                                               
         MVC   AXDTMOFC,SPACES     CLIENT                                       
         L     R6,ALEVELS                                                       
         XR    RF,RF                                                            
         IC    RF,0(R6)                                                         
         BCTR  RF,0                                                             
         MVC   AXDTMOFC(0),ACTKACT                                              
         EX    RF,*-6                                                           
         TR    AXDTMOFC,MXTRT                                                   
*                                                                               
         LA    RF,ACTKACT+1(RF)    A(DEPARTMENT CODE)                           
         MVC   AXDTMDEC,SPACES     DEPARTMENT CODE                              
         XR    RE,RE                                                            
         IC    RE,1(R6)                                                         
         BCTR  RE,0                                                             
         MVC   AXDTMDEC(0),0(RF)                                                
         EX    RE,*-6                                                           
         TR    AXDTMDEC,MXTRT                                                   
*                                                                               
         LA    RF,1(RE,RF)                                                      
         MVC   AXDTMSDC,SPACES     SUB-DEPARTMENT CODE                          
         XR    RE,RE                                                            
         IC    RE,2(R6)                                                         
         BCTR  RE,0                                                             
         MVC   AXDTMSDC(0),0(RF)                                                
         EX    RE,*-6                                                           
         TR    AXDTMSDC,MXTRT                                                   
*                                                                               
         LA    RF,1(RE,RF)                                                      
         MVC   AXDTMPC,SPACES      PERSON CODE                                  
         XR    RE,RE                                                            
         IC    RE,3(R6)                                                         
         BCTR  RE,0                                                             
         MVC   AXDTMPC(0),0(RF)                                                 
         EX    RE,*-6                                                           
         TR    AXDTMPC,MXTRT                                                    
         XIT1  ,                                                                
*                                                                               
AXDTMCC  DC    A(BUFFER1)          INFORMATION SAVE AREA                        
*                                                                               
         DROP  R2,R3,R4,R5                                                      
         EJECT                                                                  
***********************************************************************         
* AXBOSC - EXTRACT BRANDOCEAN STATUS RECORDS FOR AXTRACT              *         
*                                                                     *         
* AL4 A(EXTRACT RECORD)                                               *         
* AL4 A(ACCFILE RECORD)                                               *         
* AL4 0=ACCFILE-TO-EXTRACT                                            *         
* AL4 A(SXDTABD)                                                      *         
***********************************************************************         
         SPACE 1                                                                
AXBOSC   NMOD1 WORKL,*AXBOSC*,CLEAR=Y                                           
         USING WORKD,RC                                                         
         L     RA,AXBOSLIT                                                      
         USING LITERAL,RA                                                       
         ST    R1,APARM                                                         
         MVC   PARM,0(R1)          EXTRACT PARAMETER LIST                       
         LM    R2,R3,0(R1)                                                      
         L     R4,12(R1)                                                        
         MVC   ALEVELS,16(R1)                                                   
         USING AXBOSD,R2           R2=A(EXTRACT RECORD)                         
         USING TIMRECD,R3          R3=A(ACCFILE RECORD)                         
         USING SXDTABD,R4          R4=A(SXDXTABD)                               
*                                                                               
         XC    AXBOSLEN,AXBOSLEN   RECORD LENGTH/TYPE/ENDOFREC                  
         MVC   AXBOSLEN(2),=Y(AXBOSDL)                                          
         MVC   AXBOSTYP,AXDBOSQ                                                 
         MVI   AXBOSDX,MXTRTQ                                                   
         MVI   AXBOSDX+1,MXTRTQ                                                 
         MVI   AXBOSCCD-1,MXTRTQ   2-BYTE AGENCY ALPHA                          
         MVI   AXBOSCSC-1,MXTRTQ   CON-SYS PERSON CODE                          
         MVI   AXBOSPED-1,MXTRTQ   END DATE                                     
         MVI   AXBOSSTA-1,MXTRTQ   STATUS                                       
*                                                                               
         MVC   AXBOSCCD,SXDTAGY    2-BYTE AGY ALPHA                             
         TR    AXBOSCCD,MXTRT                                                   
*                                                                               
         GOTO1 VDATCON,PLIST,(1,TIMKPEDT),(10,AXBOSPED),0                       
*&&US                                                                           
         XC    AXBOSPED(2),AXBOSPED+3                                           
         XC    AXBOSPED+3(2),AXBOSPED                                           
         XC    AXBOSPED(2),AXBOSPED+3                                           
*&&                                                                             
         TR    AXBOSPED,MXTRT                                                   
*                                                                               
         USING TIMELD,R7                                                        
         LA    R7,TIMRFST          FIRST ONE RECORD DATA                        
AXBOS02  CLI   TIMEL,0                                                          
         JE    AXBOSX                                                           
         CLI   TIMEL,TIMELQ        8B - TIME ELEMENT                            
         JE    AXBOS06                                                          
         CLI   TIMEL,PIDELQ        D8 - PID ELEMENT                             
         JE    AXBOS08                                                          
*                                                                               
AXBOS04  XR    R1,R1                                                            
         IC    R1,1(R7)                                                         
         AR    R7,R1                                                            
         J     AXBOS02                                                          
*                                                                               
AXBOS06  CLI   TIMETYP,TIMEINP     FIRST ELEMENT IN A CLUSTER                   
         JNE   AXBOS04                                                          
         MVC   AXBOSSTA,=C'00064'   TMS T/S ARE ALWAYS FULLY APPROVED           
         TM    TIMSTAT,TIMSMCS      BRANDO LINES USE STATUS FROM KEY            
         JNO   AXBOS04                                                          
         EDIT  TIMRSTAT,(5,AXBOSSTA),ZERO=NOBLANK,FILL=0                        
         J     AXBOS04                                                          
*                                                                               
         USING PIDELD,R7                                                        
AXBOS08  XR    R0,R0               PACK PIDNO INTO DUB                          
         ICM   R0,3,PIDNO                                                       
         CVD   R0,DUB                                                           
         OI    DUB+L'DUB-1,X'0F'                                                
         UNPK  AXBOSCSC,DUB        FILL DATA SPACE WITH CSC                     
         TR    AXBOSCSC,MXTRT                                                   
         J     AXBOS04                                                          
*                                                                               
AXBOSX   XIT1  ,                                                                
*                                                                               
AXBOSLIT DC    A(LITERAL)                                                       
*                                                                               
         DROP  R2,R3,R4,R7                                                      
         EJECT                                                                  
***********************************************************************         
* LITERALS AND CONSTANTS REQUIRED FOR ALL ROUTINES                    *         
***********************************************************************         
         SPACE 1                                                                
LITERAL  DS    0D                                                               
VDATCON  DC    V(DATCON)                                                        
ANEXT    DC    F'0'                                                             
ANXTTAL  DC    F'0'                                                             
ANXXTAL  DC    F'0'                                                             
ANXTTALD DC    F'0'                                                             
ANXXTALD DC    F'0'                                                             
ANXTTAT  DC    F'0'                                                             
ANXXTAT  DC    F'0'                                                             
ANXTTAD  DC    F'0'                                                             
ANXXTAD  DC    F'0'                                                             
ANXTBOL  DC    F'0'                                                             
ANXXBOL  DC    F'0'                                                             
*                                                                               
SPACES   DC    CL60' '                                                          
PZERO    DC    P'0'                                                             
FLAG     DC    XL1'00'                                                          
FLAG2    DC    XL1'00'                                                          
TMS      EQU   X'80'                                                            
*                                                                               
Y2000    EQU   X'A000'                                                          
*                                                                               
*AXRECID                                                                        
         PRINT OFF                                                              
       ++INCLUDE AXRECID                                                        
         PRINT ON                                                               
*                                                                               
         LTORG                                                                  
         EJECT                                                                  
*MXTRT                                                                          
         PRINT OFF                                                              
       ++INCLUDE MXTRT                                                          
         PRINT ON                                                               
*                                                                               
         DC    CL8'BUFFER01'                                                    
BUFFER1  DC    14000XL8'00'        HOLDS RECORDS FOR WRITING TO FILE            
BUFFER1L EQU   *-BUFFER1                                                        
         DC    8X'FF'                                                           
*                                                                               
         DC    CL8'BUFFER02'                                                    
BUFFER2  DC    14000XL8'00'        HOLDS RECORDS FOR WRITING TO FILE            
BUFFER2L EQU   *-BUFFER2                                                        
         DC    8X'FF'                                                           
*                                                                               
         DC    CL8'BUFFER03'                                                    
BUFFER3  DC    14000XL8'00'        HOLDS RECORDS FOR WRITING TO FILE            
BUFFER3L EQU   *-BUFFER3                                                        
         DC    8X'FF'                                                           
*                                                                               
         DC    CL8'BUFFER04'                                                    
BUFFER4  DC    14000XL8'00'        HOLDS RECORDS FOR WRITING TO FILE            
BUFFER4L EQU   *-BUFFER4                                                        
*                                                                               
         DC    CL8'BUFFER05'                                                    
BUFFER5  DC    14000XL8'00'        HOLDS RECORDS FOR WRITING TO FILE            
BUFFER5L EQU   *-BUFFER5                                                        
         DC    8X'FF'                                                           
         EJECT                                                                  
***********************************************************************         
* WORKING STORAGE REQUIREMENTS                                        *         
***********************************************************************         
         SPACE 1                                                                
WORKD    DSECT                                                                  
DUB      DS    D                                                                
APARM    DS    A                   A(PARAMETER LIST)                            
*                                                                               
PARM     DS    0XL24               PARAMETER LIST                               
AXREC    DS    A                   A(EXTRACT RECORD)                            
AFREC    DS    A                   A(ACCFILE RECORD)                            
         DS    A                                                                
         DS    A                                                                
         DS    A                                                                
         DS    A                                                                
*                                                                               
WORK     DS    XL20                                                             
PLIST    DS    6F                                                               
ALEVELS  DS    A                                                                
ASJLVLS  DS    A                                                                
SAVER7   DS    A                                                                
HALF     DS    H                                                                
RECLEN   DS    H                                                                
LASTLEN  DS    H                                                                
LASTLEN1 DS    H                                                                
COSTG    DS    X                                                                
JOBSTAT  DS    XL1                                                              
PIDN     DS    XL2                                                              
SAVSEQ   DS    XL1                                                              
AXTAX    DS    XL1                                                              
DATE     DS    XL(L'PDEDTE)                                                     
AMNT     DS    PL8                                                              
TMPDTE   DS    PL3                                                              
TMPDTE2  DS    PL3                                                              
*                                                                               
WORKL    EQU   *-WORKD                                                          
         EJECT                                                                  
***********************************************************************         
* OTHER INCLUDED DSECTS                                               *         
***********************************************************************         
         SPACE 1                                                                
*AXRECD                                                                         
         PRINT OFF                                                              
       ++INCLUDE AXRECD                                                         
         PRINT ON                                                               
*DXDSECTS                                                                       
         PRINT OFF                                                              
       ++INCLUDE DXDSECTS                                                       
         PRINT ON                                                               
*ACGENFILE                                                                      
         PRINT OFF                                                              
       ++INCLUDE ACGENFILE                                                      
         PRINT ON                                                               
*ACOPTEQUS                                                                      
         PRINT OFF                                                              
       ++INCLUDE ACOPTEQUS                                                      
         PRINT ON                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'057AXROUTS   04/30/10'                                      
         END                                                                    
