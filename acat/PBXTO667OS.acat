*          DATA SET PBXTO667OS AT LEVEL 003 AS OF 05/01/02                      
*CATALP XERTO667                                                                
         PRINT GEN                                                              
         SPACE 1                                                                
XERTO667 CSECT                                                                  
         NBASE 0,**XTOW**,=V(REGSAVE),RA                                        
         GOTO1 INIT0,PARAC,INAREA,=C'RE00'                                      
INIT0    GOTO1 FIRSTLIN,PARAO,W,=C'PE00'                                        
FIRSTLIN MVI   W,X'06'             RCR                                          
         SPACE 1                                                                
NEXTLINE BAS   R9,READ                                                          
         SPACE 1                                                                
*                  LOOK FOR TABS                                                
         SPACE 1                                                                
TRYTABS  SR    R1,R1                                                            
         ZIC   R7,LINELEN                                                       
         LA    R4,IN                                                            
         SPACE 1                                                                
USCORELP EX    R7,TRYUSCOR                                                      
         BZ    TABPREP                                                          
         SPACE 1                                                                
         MVI   0(R1),X'00'         NULL MY BUSC                                 
         LA    R3,1(R1)                                                         
         SR    R3,R4                                                            
         SR    R7,R3                                                            
         LA    R4,1(R1)            PTR PAST BUSC                                
         EX    R7,TRYUSCOR                                                      
         BZ    TABPREP                                                          
         SPACE 1                                                                
         LA    R3,1(R1)            PTR PAST EUSC                                
         SR    R1,R4                                                            
         EX    R1,REQSPCES                                                      
         SR    R7,R1                                                            
         BZ    TABPREP                                                          
         SPACE 1                                                                
         LA    R4,0(R3)                                                         
         B     USCORELP                                                         
         SPACE 1                                                                
TRYUSCOR TRT   0(0,R4),USCORES                                                  
         SPACE 1                                                                
REQSPCES TR    0(0,R4),REQSPC                                                   
         SPACE 1                                                                
TABTEST  TRT   IN(0),TABCHECK                                                   
         SPACE 1                                                                
TABOUT   MVC   REBUILD(0),1(R1)    EXCLUDE TAB FROM SAVE                        
         SPACE 1                                                                
SPACEIN  MVC   0(0,R1),SPACES      BLANK TAB AND ONWARD                         
         SPACE 1                                                                
TABBACK  MVC   0(0,R1),REBUILD                                                  
         EJECT                                                                  
TABPREP  MVC   USCOFFST,=H'0'                                                   
         ZIC   R7,LINELEN                                                       
         SPACE 1                                                                
OTHERTAB EX    R7,TABTEST                                                       
         BZ    TRYEXTRA                                                         
         SPACE 1                                                                
         LA    R3,18                                                            
         LA    R4,IN-1                                                          
         LR    R5,R1                                                            
         SR    R5,R4               TAB DISP + 1                                 
         SH    R5,USCOFFST                                                      
         LA    R6,TABRACK                                                       
         LR    R8,R1                                                            
         SH    R8,=H'3'                                                         
         CR    R8,R4                                                            
         BNH   TABLOOP1                                                         
         TRT   0(4,R8),FUNCTION                                                 
         BZ    TABLOOP1                                                         
         SPACE 1                                                                
         LA    R1,3(R8)                                                         
         LH    R8,USCOFFST                                                      
         AH    R8,=H'2'                                                         
         STH   R8,USCOFFST                                                      
         SH    R5,=H'2'                                                         
         SPACE 1                                                                
TABLOOP1 CLC   0(2,R6),=H'0'                                                    
         BE    TABERROR                                                         
         CH    R5,0(R6)                                                         
         BL    TABMOVE             NEXT TAB FOUND                               
         LA    R6,2(R6)              NO - SET FOR NEXT TRY                      
         BCT   R3,TABLOOP1                                                      
         B     TABERROR                                                         
         SPACE 1                                                                
TABMOVE  LA    R2,IN-1(R7)         PTR TO LINE END - 1                          
         SR    R2,R1               L'TEXT' AFTER TAB - 1                        
         EX    R2,TABOUT           SAVE WHAT'S AFTER TAB                        
         LA    R2,1(R2)                                                         
         EX    R2,SPACEIN          BLANK TAB AND ONWARD                         
         BCTR  R2,0                                                             
         LA    R1,IN-1                                                          
         AH    R1,0(R6)                                                         
         AH    R1,USCOFFST                                                      
         EX    R2,TABBACK          RESTORE FROM SAVE                            
         LA    R7,0(R1,R2)                                                      
         LA    R4,IN                                                            
         SR    R7,R4                                                            
         STC   R7,LINELEN                                                       
         B     OTHERTAB                                                         
         SPACE 1                                                                
TABERROR MVI   0(R1),C' '                                                       
         EX    R7,TABTEST                                                       
         BNZ   *-8                                                              
         B     TRYEXTRA                                                         
         SPACE 1                                                                
         EJECT                                                                  
TRYEXTRA CLI   XER6FLAG,C'Y'                                                    
         BE    EXTRA6                                                           
         SPACE 1                                                                
         CLI   IN,X'FC'            CENTER, 850                                  
         BNE   PUTITOUT              NO                                         
         SPACE 1                                                                
         ZIC   R7,LINELEN            YES                                        
         SH    R7,=H'4'            SKIP 4-BYTE PREFIX                           
         LA    R3,IN+4                                                          
EXTRAEND LA    R4,REBUILD                                                       
         EX    R7,USAVE1                                                        
         MVC   IN(132),SPACES                                                   
         LH    R3,LINESIZE                                                      
         SR    R3,R7                                                            
         SRA   R3,1                DIVIDE BY 2                                  
         BP    *+6                                                              
         DC    H'0'                                                             
         LA    R5,0(R3,R7)                                                      
         STC   R5,LINELEN                                                       
         LA    R3,IN(R3)                                                        
         EX    R7,USTORE                                                        
         B     PUTITOUT                                                         
         SPACE 1                                                                
USAVE1   MVC   0(0,R4),0(R3)                                                    
         SPACE 1                                                                
USTORE   MVC   0(0,R3),REBUILD                                                  
         SPACE 1                                                                
NEXTSYM  TRT   0(0,R3),SPECTEST                                                 
         SPACE 1                                                                
TRYSYMB  TRT   IN(0),SPECTEST                                                   
         SPACE 1                                                                
EXTRA6   CLI   IN,X'FC'            CMD, 860, WP                                 
         BNE   TRYSW6                NO                                         
         SPACE 1                                                                
         CLI   IN+1,X'7B'          860 CENTER                                   
         BNE   TRYSW6                NO                                         
         SPACE 1                                                                
         ZIC   R7,LINELEN            YES                                        
         SH    R7,=H'3'                                                         
         LA    R3,IN+3                                                          
         B     EXTRAEND                                                         
         EJECT                                                                  
TRYSW6   ZIC   R7,LINELEN                                                       
         EX    R7,SWCODE                                                        
         BZ    PUTITOUT                                                         
         SPACE 1                                                                
         CLI   1(R1),C' '          860 SWITCH                                   
         BNE   PUTITOUT              NO                                         
         SPACE 1                                                                
         LA    R3,3(R1)                                                         
         LA    R4,REBUILD                                                       
         LA    R2,IN(R7)                                                        
         SR    R2,R3                                                            
         EX    R2,USAVE1                                                        
         MVI   0(R1),X'2A'         WP SWITCH                                    
         LA    R1,1(R2,R1)                                                      
         XC    0(2,R1),0(R1)                                                    
         SH    R3,=H'2'                                                         
         EX    R2,USTORE                                                        
         SH    R7,=H'2'                                                         
         STC   R7,LINELEN                                                       
         B     TRYSW6+6                                                         
         SPACE 1                                                                
SWCODE   TRT   IN(0),SWITCH                                                     
         EJECT                                                                  
PUTITOUT ZIC   R7,LINELEN                                                       
         SR    R1,R1                                                            
         EX    R7,TRYSYMB                                                       
         BZ    COMLNOUT              NO                                         
         SPACE 1                                                                
         LA    R4,REBUILD                                                       
SYMBOLLP LA    R3,1(R1)                                                         
         LA    R5,IN(R7)                                                        
         SR    R5,R3                                                            
         CLI   TYPENOW,C'R'          YES - ROTATED                              
         BNE   *+12                          NO                                 
         MVI   0(R1),C' '                    YES - BLANK IT                     
         B     MORESYM                                                          
         EX    R5,USAVE1                                                        
         BCTR  R3,0                                                             
         MVC   0(3,R3),=X'2F0061'                                               
         STC   R2,1(R3)                                                         
         LA    R3,3(R3)                                                         
         EX    R5,USTORE                                                        
         LA    R7,2(R7)                                                         
         STC   R7,LINELEN                                                       
MORESYM  EX    R5,NEXTSYM            NO - LOOK FOR ANOTHER                      
         BNZ   SYMBOLLP                                                         
         SPACE 1                                                                
COMLNOUT LA    R4,IN                                                            
         LA    R5,0(R4,R7)         PTR TO LINE-END CHAR                         
         BCTR  R7,0                                                             
         LTR   R7,R7                                                            
         BM    NOMORE                                                           
         CH    R7,=H'78'                                                        
         BNH   ONECARD                                                          
         MVC   W+1(79),0(R4)                                                    
         LA    R4,79(R4)                                                        
         BAS   R9,PUTOUT                                                        
         SH    R7,=H'79'                                                        
         BM    NOMORE+4                                                         
ONECARD  EX    R7,INTOW                                                         
NOMORE   BAS   R9,PUTOUT                                                        
         MVC   W(1),0(R5)          LINE-END CHAR                                
         B     NEXTLINE                                                         
         SPACE 1                                                                
INTOW    MVC   W+1(0),0(R4)                                                     
         SPACE 1                                                                
PUTOUT   GOTO1 =V(CARDS),PARAO                                                  
         XC    W,W                                                              
         BR    R9                                                               
         EJECT                                                                  
*                  DEBLOCK INPUT TEXT STREAM                                    
         SPACE 1                                                                
READ     LA    R7,IN               DEST                                         
         MVC   IN,SPACES                                                        
         MVC   IN1,SPACES                                                       
READMORE CLI   LEFTOVER,C'Y'       MORE IN 80-CHAR SEGMENT                      
         BE    MORETODO              YES                                        
         SPACE 1                                                                
         BAS   R8,GETSEG                                                        
         BNZ   SETLINE             LINE END FOUND                               
         SPACE 1                                                                
WRAPLINE LA    R6,2                CHUNK COUNTER                                
         EX    R4,MOVELINE         SET START OF NEW LINE                        
         LA    R7,1(R4,R7)         SET DEST PAST IT                             
         BAS   R8,GETSEG                                                        
         BNZ   WRAPDONE                                                         
         BCT   R6,WRAPLINE+4                                                    
         DC    H'0'                  NO                                         
         SPACE 1                                                                
WRAPDONE LA    R2,1(R1)            SAVE PTR BEYOND LINE END                     
         SR    R1,R3                                                            
         B     LINESET                                                          
         SPACE 1                                                                
MORETODO L     R3,MOREADDR                                                      
         LA    R3,0(R3)                                                         
         ZIC   R4,MOREADDR                                                      
         LA    R5,0(R4,R3)         END OF LEFTOVER                              
         SR    R1,R1                                                            
         EX    R4,SEEKEND                                                       
         BZ    WRAPLINE            NO LINE END FOUND                            
         SPACE 1                                                                
SETLINE  LA    R2,1(R1)            SAVE PTR PAST LINE END                       
         LR    R6,R1                                                            
         SR    R6,R3                                                            
         BP    *+10                                                             
         SR    R1,R1                                                            
         B     LINESET                                                          
         SPACE 1                                                                
         BCTR  R6,0                                                             
         EX    R6,CHKSPACE                                                      
         BE    ONLYEND                                                          
         SPACE 1                                                                
         LA    R1,1(R6)                                                         
         B     LINESET                                                          
         EJECT                                                                  
ONLYEND  LA    R3,1(R6,R3)                                                      
         SR    R1,R1                                                            
         SPACE 1                                                                
LINESET  EX    R1,MOVELINE                                                      
         LA    R3,IN                                                            
         LA    R1,0(R1,R7)                                                      
         LR    R6,R1               PTR TO LINE END                              
         SR    R1,R3                                                            
         STC   R1,LINELEN          SAVE L(LINE) - 1                             
         SPACE 1                                                                
         CLI   XER6FLAG,C'Y'                                                    
         BNE   TRANSLAT            850, NOT 860                                 
         SPACE 1                                                                
         CLI   FMTRDFLG,C'Y'                                                    
         BE    TRANSLAT+4            DON'T TR FOR 860                           
         SPACE 1                                                                
         CLI   0(R6),X'90'         860 FMT START                                
         BE    TRANSLAT+4            YES - DON'T TR                             
         SPACE 1                                                                
TRANSLAT EX    R1,TRFMT5                                                        
         SR    R5,R2                                                            
         BNM   *+12                                                             
         MVI   LEFTOVER,C' '                                                    
         B     ENDTEST                                                          
         SPACE 1                                                                
RESETMOR ST    R2,MOREADDR         A(REST)                                      
         STC   R5,MOREADDR         L(REST) - 1                                  
         MVI   LEFTOVER,C'Y'                                                    
         B     ENDTEST                                                          
         SPACE 1                                                                
CHKSPACE CLC   0(0,R3),XERSPACE                                                 
         SPACE 1                                                                
TRFMT5   TR    0(0,R3),XWPTOWP                                                  
         SPACE 1                                                                
MOVELINE MVC   0(0,R7),0(R3)                                                    
         EJECT                                                                  
ENDTEST  CLI   FMTRDFLG,C'Y'       READ FOR FORMAT BLOCK                        
         BER   R9                    YES                                        
         SPACE 1                                                                
         CLI   IN,X'2B'            FMT                                          
         BE    FORMAT                                                           
         SPACE 1                                                                
         CLI   IN,X'90'            XIP FMT (860)                                
         BE    FORMAT6                                                          
         SPACE 1                                                                
         CLC   IN(2),=C'/*'                                                     
         BNE   *+12                                                             
         BAS   R8,GETSEG                                                        
         B     *-4                                                              
         SPACE 1                                                                
         ZIC   R7,LINELEN                                                       
         LTR   R7,R7                                                            
         BZR   R9                                                               
         SPACE 1                                                                
BULLET   EX    R7,TRYSPECL                                                      
         BZ    ENDTEST1                                                         
         SPACE 1                                                                
         CLC   0(3,R1),=X'099638'  BULLET                                       
         BNE   ENDTEST1                                                         
         SPACE 1                                                                
         MVC   0(3,R1),=X'40AF40'                                               
         SPACE 1                                                                
ENDTEST1 LA    R6,IN(R7)           PTR TO LINE END                              
         CLI   0(R6),X'2B'         FMT AS TEXT LINE END                         
         BE    FMTSAVE               YES                                        
         CLI   0(R6),X'90'         860 FMT AS LINE END                          
         BE    FMTSAVE6                                                         
         BR    R9                                                               
         SPACE 1                                                                
TRYSPECL TRT   IN(0),SPECTEST                                                   
         EJECT                                                                  
*                   GET NEXT 80-CHAR TEXT SEGMENT                               
         SPACE 1                                                                
GETSEG   LA    R3,INAREA                                                        
         LA    R4,79                                                            
         LA    R5,INAREA+79        END OF INPUT AREA                            
         MVI   LEFTOVER,C' '                                                    
         MVC   SAVEIN3,SAVEIN2                                                  
         MVC   SAVEIN2,SAVEIN1                                                  
         MVC   SAVEIN1,INAREA                                                   
         GOTO1 =V(CARDS),PARAC                                                  
         CLC   INAREA(2),=C'/*'                                                 
         BE    ENDOFPGM                                                         
         SPACE 1                                                                
         NOP   *+16                                                             
         CLI   INAREA,X'90'        XWP FORMAT                                   
         BNE   BADDOC                                                           
         OI    *-11,X'F0'                                                       
         SPACE 1                                                                
         TRT   INAREA,NULLCHK                                                   
         BZ    FINDEND                                                          
         SPACE 1                                                                
         BCTR  R1,0                                                             
         CLI   0(R1),X'26'         ETB                                          
         BNE   *+6                                                              
         BCTR  R1,0                                                             
         LA    R4,0(R1)                                                         
         SR    R4,R3               GET NEW LENGTH                               
         LA    R5,INAREA(R4)       SET NEW END                                  
         LA    R6,1(R6)                                                         
         SPACE 1                                                                
FINDEND  EX    R4,SEEKEND                                                       
         BR    R8                                                               
         SPACE 1                                                                
SEEKEND  TRT   0(0,R3),LINEEND                                                  
         SPACE 1                                                                
BADDOC   MVC   P(39),=C'** NO FORMAT BLOCK AT DOCUMENT START **'                
         GOTO1 =V(PRINT),PARA,P-1,=C'BL01'                                      
         GOTO1 =V(CARDS),PARAC                                                  
         CLC   INAREA(2),=C'/*'                                                 
         BNE   *-16                RUN IT OUT                                   
ENDOFPGM XBASE                                                                  
         EJECT                                                                  
FMTSAVE  ZIC   R7,LINELEN                                                       
         BCTR  R7,0                                                             
         LTR   R7,R7                                                            
         BM    FORMAT                                                           
         LA    R3,IN                                                            
         EX    R7,USAVE1           SAVE LINE FRAGMENT IN REBUILD                
         STC   R7,SAVELEN                                                       
         MVI   FMTSVFLG,C'Y'                                                    
         SPACE 1                                                                
FORMAT   MVI   FMTRDFLG,C'Y'                                                    
         ST    R9,RETADDR                                                       
         BAS   R9,READ                                                          
         LA    R3,IN+1             LEFT MARGIN START                            
         BAS   R8,CHARHEX                                                       
         CH    R1,=H'2'                                                         
         BNL   *+8                                                              
         LH    R1,=H'2'                                                         
         STH   R1,LEFTMAR                                                       
         LA    R3,2(R3)            RIGHT MARGIN START                           
         BAS   R8,CHARHEX                                                       
         SH    R1,LEFTMAR                                                       
         LA    R1,1(R1)                                                         
         STH   R1,LINESIZE                                                      
         BAS   R8,LINECHNG                                                      
         BAS   R9,READ                                                          
         LA    R3,IN                                                            
         CLI   0(R3),C'1'          TAB LINE                                     
         BNE   ENDOFFMT              NO                                         
         XC    TABRACK,TABRACK                                                  
         LA    R3,1(R3)              YES                                        
         LA    R7,18               L(TABRACK) IN H'S                            
         LA    R4,TABRACK                                                       
         SPACE 1                                                                
FMTTABLP CLI   0(R3),X'1E'         CRE - NO MORE TABS                           
         BE    ENDOFFMT              YES - GET OUT                              
         SPACE 1                                                                
         BAS   R8,CHARHEX                                                       
         SH    R1,LEFTMAR                                                       
         BNP   GETNEXT                                                          
         SPACE 1                                                                
         LA    R1,1(R1)                                                         
         STH   R1,0(R4)            STORE IN TABRACK                             
         LA    R4,2(R4)                                                         
GETNEXT  LA    R3,2(R3)                                                         
         BCT   R7,FMTTABLP         MORE ROOM IN RACK                            
         EJECT                                                                  
ENDOFFMT BAS   R9,READ                                                          
         CLI   IN,C'3'             SPACING LINE                                 
         BE    INTCHANG                                                         
         CLI   IN,X'2B'            FMT AT END OF BLOCK                          
         BNE   ENDOFFMT                                                         
FMT56X   MVI   FMTRDFLG,C' '                                                    
         L     R9,RETADDR                                                       
         CLI   FMTSVFLG,C'Y'                                                    
         BNE   READ                                                             
         SPACE 1                                                                
         ZIC   R7,SAVELEN                                                       
         LA    R3,IN                                                            
         EX    R7,USTORE           RETURN LINE FRAGMENT                         
         LA    R7,IN+1(R7)                                                      
         MVI   FMTSVFLG,C' '                                                    
         B     READMORE                                                         
         SPACE 1                                                                
*                   850 CONVERT TO HALF-WORD                                    
         SPACE 1                                                                
CHARHEX  PACK  WORK(2),0(2,R3)                                                  
         LH    R1,WORK                                                          
         SRA   R1,4                                                             
         TM    1(R3),X'F0'         LOW-ORDER NUMERIC                            
         BO    *+8                   YES                                        
         AH    R1,=H'9'            BUMP BY 10 - 1                               
         BR    R8                                                               
         SPACE 1                                                                
*                   860 CONVERT TO HALF-WORD                                    
         SPACE 1                                                                
HEX6BITS NC    0(2,R3),=X'3F3F'    RESET X'40' BITS                             
         ZIC   R1,0(R3)                                                         
         SLA   R1,6                POSITION HI-ORDER 6 BITS                     
         MVI   0(R3),X'00'         ERASE FROM HALF-WORD                         
         AH    R1,0(R3)                                                         
         BR    R8                                                               
         EJECT                                                                  
*                   860 FORMAT BLOCK DECODING                                   
         SPACE 1                                                                
FMTSAVE6 ZIC   R7,LINELEN                                                       
         BCTR  R7,0                                                             
         LTR   R7,R7                                                            
         BM    FORMAT6                                                          
         LA    R3,IN                                                            
         EX    R7,USAVE1           SAVE LINE FRAGMENT IN REBUILD                
         STC   R7,SAVELEN                                                       
         MVI   FMTSVFLG,C'Y'                                                    
         SPACE 1                                                                
FORMAT6  MVI   FMTRDFLG,C'Y'                                                    
         ST    R9,RETADDR                                                       
         BAS   R9,READ             GET LINE 0, 850 OR 860                       
         NOP   DOFMT6                                                           
XER6SW   EQU   *-3                                                              
         CLC   IN(2),=X'3060'      860 LINE 0                                   
         BE    SETFMT6               YES                                        
         SPACE 1                                                                
         MVI   XER6FLAG,C' '         NO                                         
         LA    R3,IN                                                            
         ZIC   R7,LINELEN                                                       
         EX    R7,TRFMT5                                                        
         B     ENDOFFMT            IGNORE 1ST 850 FMT BLOCK                     
         SPACE 1                                                                
SETFMT6  OI    XER6SW,X'F0'                                                     
         MVI   LINEEND+146,X'00'                                                
         MVC   LINEEND+148(2),=X'9495'                                          
         MVI   LINEEND+159,X'00'                                                
         MVC   XWPTOWP+128(128),XIPCTLS                                         
         B     ENDFMT6             IGNORE 1ST 860 FMT BLOCK                     
         SPACE 1                                                                
DOFMT6   BAS   R9,READ             SKIP LINE 0, GET MARGINS LINE                
         CLI   IN,X'31'                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
         LA    R3,IN+1             PAST X'31' LINE 1 ID                         
         BAS   R8,HEX6BITS                                                      
         CH    R1,=H'2'                                                         
         BNL   *+8                                                              
         LH    R1,=H'2'                                                         
         STH   R1,LEFTMAR                                                       
         LA    R3,2(R3)            RIGHT MARGIN START                           
         BAS   R8,HEX6BITS                                                      
         SH    R1,LEFTMAR                                                       
         LA    R1,1(R1)                                                         
         STH   R1,LINESIZE                                                      
         BAS   R8,LINECHNG                                                      
         EJECT                                                                  
         BAS   R9,READ             GET TABS LINE                                
         CLI   IN,X'32'            TABS LINE 2 ID                               
         BNE   TRYLINE3+4            NO                                         
         SPACE 1                                                                
         XC    TABRACK,TABRACK                                                  
         LA    R0,18               TABRACK LIMIT                                
         LA    R1,TABRACK                                                       
         SR    R2,R2               BASE TAB VALUE                               
         LA    R3,IN+1                                                          
         TM    0(R3),X'41'         TAB FOR COL 2                                
         BO    SKIPSIT               YES                                        
         SPACE 1                                                                
TAB6LP   TM    0(R3),X'40'         TABS BYTE                                    
         BO    GETVALS               YES                                        
         NI    0(R3),X'0F'           NO  - GET SKIPPED COLS COUNT               
         ZIC   R4,0(R3)                                                         
         LA    R4,1(R4)                                                         
         MH    R4,=H'3'                                                         
         AR    R2,R4                                                            
         B     ENDLPTST                                                         
         SPACE 1                                                                
GETVALS  TM    0(R3),X'10'         1ST COL REG TAB                              
         BNO   *+10                                                             
         LR    R4,R2                                                            
         BAS   R8,SETVAL                                                        
         SPACE 1                                                                
         TM    0(R3),X'04'         2ND COL                                      
         BNO   *+12                                                             
         LA    R4,1(R2)                                                         
         BAS   R8,SETVAL                                                        
         SPACE 1                                                                
         TM    0(R3),X'01'         3RD COL                                      
         BNO   *+12                                                             
         LA    R4,2(R2)                                                         
         BAS   R8,SETVAL                                                        
         SPACE 1                                                                
SKIPSIT  LA    R2,3(R2)            BUMP BASE VALUE                              
         B     ENDLPTST                                                         
         SPACE 1                                                                
SETVAL   SH    R4,LEFTMAR                                                       
         BNPR  R8                                                               
         STH   R4,0(R1)                                                         
         LA    R1,2(R1)                                                         
         BCT   R0,*+8                                                           
         B     TRYLINE3            TABRACK FILLED                               
         BR    R8                                                               
         EJECT                                                                  
ENDLPTST LA    R3,1(R3)                                                         
         CLI   0(R3),X'FD'         860 FIELD SEP                                
         BNE   TAB6LP                                                           
         SPACE 1                                                                
TRYLINE3 BAS   R9,READ                                                          
         CLI   IN,X'33'            LINE-SPACING LINE                            
         BNE   ENDFMT6               NO                                         
         SPACE 1                                                                
         NI    IN+1,X'3F'            YES                                        
         ZIC   R1,IN+1                                                          
         SRA   R1,2                DIVIDE BY 4                                  
         STC   R1,IN+1                                                          
         OI    IN+1,X'F0'          850 FORM - 1/2 LNE COUNT                     
         SPACE 1                                                                
*                   COMMON LINE-SPACING ROUTINE                                 
         SPACE 1                                                                
INTCHANG CLC   IN+1(1),INTERNOW    SAME AS PRIOR LINE SPACING                   
         BE    ENDINTCH              YES - FORGET IT                            
         SPACE 1                                                                
         MVC   W+1(17),=C',MOD_INT 1.0_,END'                                    
         CLI   IN+1,C'2'           SINGLE SPACE                                 
         BE    INTERCHG              YES                                        
         CLI   IN+1,C'3'           1-1/2 LINE SPACING                           
         BNE   TRYTWO                NO                                         
         CLI   TYPENOW,C'R'                                                     
         BE    TRYTWO+8                                                         
         MVI   W+12,C'5'             YES                                        
         B     INTERCHG                                                         
         SPACE 1                                                                
TRYTWO   CLI   IN+1,C'4'           DOUBLE SPACE                                 
         BNE   *+12                  NO                                         
         MVI   W+10,C'2'             YES                                        
         B     INTERCHG                                                         
         SPACE 1                                                                
         MVI   W+10,C'3'           ASSUME TRIPLE SPACE                          
INTERCHG MVC   INTERNOW,IN+1                                                    
         BAS   R9,PUTOUT                                                        
         MVI   W,X'06'             RCR                                          
ENDINTCH CLI   XER6FLAG,C'Y'                                                    
         BNE   ENDOFFMT            850                                          
         SPACE 1                                                                
ENDFMT6  BAS   R9,READ                                                          
         CLI   IN,X'90'            860 FMT                                      
         BNE   ENDFMT6                                                          
         SPACE 1                                                                
         B     FMT56X                                                           
         EJECT                                                                  
*                   COMMON MARGIN/ROTATE/BODY ROUTINE                           
         SPACE 1                                                                
LINECHNG CLC   LEFTMAR,LEFTMNOW                                                 
         BE    *+8                                                              
         MVI   MARCHFLG,C'Y'                                                    
         SPACE 1                                                                
         CH    R1,=H'90'           LINESIZE GR TH 90                            
         BH    ROTATED               YES                                        
         SPACE 1                                                                
         CLI   TYPENOW,C'B'        BODY TYPE NOW                                
         BNE   MARCHG                NO - SET IT                                
         CLI   MARCHFLG,C'Y'                                                    
         BNER  R8                                                               
         SPACE 1                                                                
         MVC   W+1(17),=C',MOD_ MAR   _,END'                                    
         B     SETLEFTM                                                         
         SPACE 1                                                                
MARCHG   MVC   W+1(29),=C',MOD_MAR   _TYP 86 193_OUT 86'                        
         MVC   W+30(5),=C'_,END'                                                
         MVI   TYPENOW,C'B'                                                     
         SPACE 1                                                                
SETLEFTM LH    R4,LEFTMAR                                                       
         CVD   R4,DUB                                                           
         UNPK  W+10(2),DUB                                                      
         OI    W+11,X'F0'                                                       
         MVC   LEFTMNOW,LEFTMAR                                                 
         BAS   R9,PUTOUT                                                        
         MVI   W,X'06'             RCR                                          
         MVI   MARCHFLG,C' '                                                    
         BR    R8                                                               
         SPACE 1                                                                
ROTATED  CLI   TYPENOW,C'R'        ROTATED NOW                                  
         BNE   RMARCHG               NO - SET IT                                
         CLI   MARCHFLG,C'Y'                                                    
         BNER  R8                                                               
         SPACE 1                                                                
RMARCHG  MVC   W+1(19),=C',MOD_MAR   _TYP 224'                                  
         MVC   W+20(5),=C'_,END'                                                
         MVI   TYPENOW,C'R'                                                     
         B     SETLEFTM                                                         
         EJECT                                                                  
*                   WORK SPACE                                                  
         SPACE 2                                                                
DUB      DS    D                                                                
WORK     DS    D                                                                
PARA     DS    D                                                                
PARAC    DS    D                                                                
PARAO    DS    D                                                                
MOREADDR DS    F                                                                
RETADDR  DS    F                                                                
SAVEIN3  DS    CL80                                                             
SAVEIN2  DS    CL80                                                             
SAVEIN1  DS    CL80                                                             
INAREA   DS    CL80                                                             
IN       DS    CL80                                                             
IN1      DS    CL80                                                             
REBUILD  DS    CL160                                                            
W        DS    CL80                                                             
SPACES   DC    CL132' '                                                         
P        DC    CL132' '                                                         
XERSPACE DC    132X'00'                                                         
TABRACK  DS    XL36                                                             
LINESIZE DC    H'73'                                                            
USCOFFST DC    H'0'                                                             
LEFTMAR  DC    H'12'                                                            
LEFTMNOW DC    H'12'                                                            
LEFTOVER DC    C'N'                                                             
LINELEN  DC    X'73'                                                            
FMTRDFLG DC    C' '                                                             
FMTSVFLG DC    C' '                                                             
TYPENOW  DC    C'B'                                                             
INTERNOW DC    C'2'                                                             
SAVELEN  DS    X                                                                
MARCHFLG DC    C' '                                                             
XER6FLAG DC    C'Y'                                                             
         SPACE 1                                                                
         DS    0D                                                               
NULLCHK  DS    0CL256                                                           
         DC    X'FF'          NULL - 00                                         
         DC    255X'00'                                                         
         SPACE 1                                                                
FUNCTION DS    0CL256                                                           
         DC    35X'00'                                                          
         DC    X'23'          END OF UNDERSCORE                                 
         DC    220X'00'                                                         
         SPACE 1                                                                
SWITCH   DS    0CL256                                                           
         DC    143X'00'                                                         
         DC    X'8F'          860 REF - START OF SWITCH CODE                    
         DC    112X'00'                                                         
         EJECT                                                                  
REQSPC   DS    0CL256                                                           
         DC    X'000102030405060708090A0B0C0D0E0F'                              
         DC    X'101112131415161718191A1B1C1D1E1F'                              
         DC    X'202122232425262728292A2B2C2D2E2F'                              
         DC    X'303132333435363738393A3B3C3D3E3F'                              
         DC    X'414142434445464748494A4B4C4D4E4F'                              
         DC    X'505152535455565758595A5B5C5D5E5F'                              
         DC    X'606162636465666768696A6B6C6D6E6F'                              
         DC    X'707172737475767778797A7B7C7D7E7F'                              
         DC    X'808182838485868788898A8B8C8D8E8F'                              
         DC    X'909192939495969798999A9B9C9D9E9F'                              
         DC    X'A0A1A2A3A4A5A6A7A8A9AAABACADAEAF'                              
         DC    X'B0B1B2B3B4B5B6B7B8B9BABBBCBDBEBF'                              
         DC    X'C0C1C2C3C4C5C6C7C8C9CACBCCCDCECF'                              
         DC    X'D0D1D2D3D4D5D6D7D8D9DADBDCDDDEDF'                              
         DC    X'E0E1E2E3E4E5E6E7E8E9EAEBECEDEEEF'                              
         DC    X'F0F1F2F3F4F5F6F7F8F9FAFBFCFDFEFF'                              
         SPACE 1                                                                
LINEEND  DS    0CL256                                                           
         DC    144X'00'                                                         
         DC    X'90'     FMT - WP 2B                                            
         DC    X'00'                                                            
         DC    X'92'     FF       0C (850)                                      
         DC    X'00'                                                            
         DC    X'00'     FF       0C (860)                                      
         DC    X'00'     RPE      3A (860)                                      
         DC    X'96'     CRE      1E                                            
         DC    X'97'     RCR      06                                            
         DC    X'98'     RET      06                                            
         DC    6X'00'                                                           
         DC    X'9F'     RPE      3A (850)                                      
         DC    93X'00'                                                          
         DC    X'FD'     FS          (860)                                      
         DC    2X'00'                                                           
         SPACE 1                                                                
TABCHECK DS    0CL256                                                           
         DC    5X'00'                                                           
         DC    X'05'          HT  - 05  XEROX SIT TAB                           
         DC    51X'00'                                                          
         DC    X'39'          IT  - 39  XEROX TAB                               
         DC    198X'00'                                                         
         SPACE 1                                                                
SPECTEST DS    0CL256                                                           
         DC    9X'00'                                                           
         DC    C'2'           SPR - 09                                          
         DC    46X'00'                                                          
         DC    C'1'           SBS - 38                                          
         DC    199X'00'                                                         
         SPACE 1                                                                
USCORES  DS    0CL256                                                           
         DC    34X'00'                                                          
         DC    X'22'          BUS - 22                                          
         DC    X'23'          WUS - 23                                          
         DC    220X'00'                                                         
         EJECT                                                                  
*                   850-XWP TRANSLATE TO IBM-EBCDIC/WP                          
         SPACE 1                                                                
XWPTOWP  DS    0CL256                                                           
         DC    X'000000000000002F0805250B0C0D0E0F'  0                           
         DC    X'100000000000000000193F001C1D1E1F'  1                           
         DC    X'405A7F7B5B6C507D4D5D5C4E6B604B61'  2                           
         DC    X'F0F1F2F3F4F5F6F7F8F97A5E4C7E6E6F'  3                           
         DC    X'7CC1C2C3C4C5C6C7C8C9D1D2D3D4D5D6'  4                           
         DC    X'D7D8D9E2E3E4E5E6E7E8E9ADE0BD5F6D'  5                           
         DC    X'79818283848586878889919293949596'  6                           
         DC    X'979899A2A3A4A5A6A7A8A9C04FD0A107'  7                           
         DC    X'4000000022232700000A0000002F2F00'  8                           
         DC    X'2B400CFC40331E0606390538091A363A'  9                           
         DC    13X'00'                                                          
         DC    X'4B'                             AD U/C .                       
         DC    X'6B'                             AE U/C ,                       
         DC    17X'00'                                                          
         DC    X'4A'                                CENT SIGN                   
         DC    31X'00'                              C-D                         
         DC    X'C0E000D06A0000400000606E006000E1'  E                           
         DC    16X'00'                              F                           
         SPACE 1                                                                
*                   860-XIP TRANSLATE FROM X'80' ONWARD                         
         SPACE 1                                                                
XIPCTLS  DS    0CL128                                                           
         DC    X'40404040222340404040402F4027408F'  8                           
         DC    X'2BFC40400C3A1E060639052538094040'  9                           
         DC    X'41CA'   A0 = RSPC   A1 = SHY                                   
         DC    11X'40'                                                          
         DC    X'60'     AD = -                                                 
         DC    13X'40'                                                          
         DC    X'E0406A' BB = 1/4  BD = 1/2                                     
         DC    66X'40'                                                          
         EJECT                                                                  
         LTORG                                                                  
         SPACE 1                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'003PBXTO667OS05/01/02'                                      
         END                                                                    
