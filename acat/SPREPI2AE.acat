*          DATA SET SPREPI2AE  AT LEVEL 006 AS OF 10/02/89                      
*CATALP ICSADDEL                                                                
         TITLE 'THIS ROUTINE ADDS OR REPLACES ELEMENTS'                         
ICSADDEL CSECT                                                                  
*                   PARAMETER 1 = A(RECORD AREA)                                
*                                 BYTE 0 - ON RETURN X'FF'                      
*                                          = RECORD ALTERED                     
*                   PARAMETER 2 = A(ELEMENT (S) TO BE ADDED)                    
*                             3 = BYTE 0=ELEMENT LENGTH                         
*                             BYTE 1=POOL OR AFFID REFERENCE NUMBER             
*                                 BYTE 3=NON-ZERO ON ERROR RETURN               
*                        4=   A(MG ELEM ADDR-MISSED SPOT ON RETURN)             
*                           OR:                                                 
*                             BYTES 0,1= 2-BYTE PACKED YMD ORDERED              
*                                        AFFID DATE                             
*        BYTE 7 FOR POOL MAY HAVE SPOT REFERENCE NUMBER                         
R10      EQU   10                                                               
R11      EQU   11                                                               
R12      EQU   12                                                               
R13      EQU   13                                                               
R14      EQU   14                                                               
R15      EQU   15                                                               
         NMOD1 2,**ADDEL**                                                      
         USING WORK,R12                                                         
         USING LIST,R1                                                          
         USING BUYRECD,R10                                                      
         MVI   AREC,0              CLEAR REC ALTERED SW                         
         L     R3,AREC             A(REC)                                       
         L     R10,AREC            DSECT                                        
         MVI   AERR+3,0            ZERO ERROR IND                               
         XC    ELCTR(10),ELCTR     ZERO WORK AREA                               
         MVC   LEN,BUYLEN                                                       
         LH    R8,LEN                                                           
         LA    R5,0(R8,R3)         REC END                                      
         SR    R7,R7                                                            
         IC    R7,AERR        ELEMENT LEN TO BE ADDED                           
         AR    R8,R7            ADD TO TOTAL LENGTH                             
         CH    R8,=H'1975'         MAX REC SIZE                                 
         BH    ERR1           BH TO ERROR RETURN                                
         BCTR  R5,R0          FOR BXLE LOOP                                     
         SR    R4,R4                                                            
         IC    R4,25(R3)      BUY DESC LEN                                      
         LA    R3,24(R4,R3)   1ST ELEM ADDR                                     
         L     R7,AELEM       A(ELEM TO BE ADDED)                               
         L     R9,AREC                                                          
         MVC   ELNO+1(1),AERR+1    REFERENCE NUMBER FOR POOL                    
         CLI   0(R7),0             ALLOCATION?                                  
         BE    DTLOOP                                                           
         CLI   0(R7),6        ADDEL CODE=6?                                     
         BL    A995           IF LOW IT'S A NON-DATE ELEM                       
         CLI   0(R7),13       ADDEL CODE=13?                                    
         BH    A995                                                             
DTLOOP   CLI   0(R3),16       AFFID ELEM?                                       
         BE    NEXEL                                                            
         CLI   0(R3),13       DATE ELEM?                                        
         BH    A997                                                             
         CLI   0(R3),6             DATE ELEM?                                   
         BL    NEXEL                                                            
         CLC   2(2,R3),2(R7)  RECELEM V. ADDEL DATES                            
         BH    A997                                                             
         BE    TESPOL                                                           
NEXEL    IC    R4,1(R3)       RECELEM LEN                                       
         BXLE  R3,R4,DTLOOP  LOOP THRU ELEMENTS                                 
         B     A997                                                             
TESPOL   CLI   BUYKEY+3,X'FF'      POOL?                                        
         BE    POLROUT                                                          
A152     IC    R4,7(R3)            NO, SPOTS                                    
         L     R8,SPTCTR      SPOT CTR                                          
         TM    6(R3),X'80'   NEG SPOT?ELD                                       
         BZ    A153          IF NOT, CONTINUE                                   
         LNR   R4,R4         MAKE SPOTS NEGATIVE                                
A153     AR    R8,R4         ADD SPOTS                                          
         SR    R4,R4                                                            
         ST    R8,SPTCTR                                                        
         IC    R4,1(R3)            LEN                                          
         BXLE  R3,R4,*+8                                                        
         B     A997                                                             
*                                                                               
         CLI   0(R3),15                                                         
         BH    A997                                                             
*                                                                               
         CLC   2(2,R3),2(R7)       REC DATE V. ADDEL DATE                       
         BE    A152                                                             
         B     A997                                                             
*        THIS ROUTINE HANDLES POOL RECORDS                                      
POLROUT  CLI   0(R7),12       ADDEL=POOL OTO?                                   
         BE    TESNPOL                                                          
         CLI   0(R7),13       ADDEL=POOL FLIP?                                  
         BE    TESNPOL                                                          
         CLI   0(R7),0        ADDEL=ALLOCATION(RE) (0 TRANS CODE)?              
         BE    A998                                                             
         CLI   0(R7),11      ORIG POOL 2X PER WEEK - MORE WEEKS?                
         BE    A997                  IF SO ADD ELEM                             
         DC    X'00'          PROG ERR                                          
*              12 OR 13 TRANS CODE                                              
TESNPOL  TM    6(R7),X'80'    NEG ADDEL?                                        
         BZ    NEXEL          IF POSITIVE POOL OTO OR FLIP FIND HI ELEM         
         TM    6(R3),X'80'         NEG SPOT?                                    
         BO    NEXEL               DON'T COUNT AS REF NUMBER                    
         LH    R8,ELCTR       ELEM CTR                                          
         LA    R8,1(R8)                                                         
         STH   R8,ELCTR                                                         
         CH    R8,ELNO        COMPARE ELEM CTR + POOL REF NO                    
         BNE   NEXEL                                                            
         TM    6(R7),X'02'         MG MASTER?                                   
         BZ    *+20                                                             
         CLI   4(R3),0            PAID?                                         
         BNE   BPERR                                                            
         OI    6(R3),X'02'         SET MG MAS IND                               
         ST    R3,AMGELEM          SAVE ADDR FOR MG SLAVE PRDS                  
         TM    6(R3),X'40'         MINUSED ALREADY?                             
         BO    RETURN                                                           
         OI    6(R3),X'40'    MARK MINUSED                                      
         IC    R4,1(R3)       RECELEM LEN                                       
         MVC   AERR(1),1(R3)   ELEM LEN FOR ADDEL                               
         MVI   AERR+1,0            ELIM. SPOT REF NO.                           
         MVC   1(1,R7),1(R3)  MOVE RECELEM LEN TO ADDEL                         
         CLI   1(R3),10     UNALLOCATED (LEN = 10)?                             
         BNE   *+18                                                             
         MVI   6(R3),X'04'         HIATUS IND                                   
         XC    7(3,R3),7(R3)       ELIM RATE IF ANY                             
         B     RETURN                                                           
         LA    R9,10(R7)      1ST PROD OF ADDEL                                 
         LA    R8,10(R3)      1ST PROD OF RECELEM                               
         LA    R6,0(R4,R3)    END OF RECELEM                                    
A1003    MVC   0(2,R9),0(R8)  MOVE RECELEM TO ADDEL                             
         XC    2(2,R9),2(R9)  ZERO BILLING FIELD                                
         LA    R9,4(R9)       NEXT ADDEL PROD FIELD                             
         LA    R8,4(R8)       NEXT RECEL PROD FIELD                             
         CR    R8,R6                                                            
         BL    A1003                                                            
         MVC   6(1,R7),6(R3)       OLD STATUS BYTE TO NEW                       
         NI    6(R7),X'39'         SAVE    20 10 08 01                          
         IC    R4,1(R3)            OLD ELEM LEN                                 
         TM    6(R3),X'20'         OLD RATE?                                    
         BZ    *+10                                                             
         MVC   7(3,R7),7(R3)       OLD RATE TO NEW                              
*                                                                               
         OI    6(R7),X'80'         NEW SPOT- MINUS IND                          
         BXLE  R3,R4,*+8                                                        
         B     INSERTR             INSERT MINUS ELEM                            
*                                                                               
         CLI   0(R3),16            AFFID?                                       
         BE    REPROUT             REPLACE AFFID                                
         B     INSERTR             INSERT MINUS ELEM                            
*                                                                               
*        THIS ROUTINE HANDLES NON-DATE ELEMENTS                                 
A995     CLI   0(R7),16       AFFID ADDEL ELEM?                                 
         BE    AFFID                                                            
         CLC   0(1,R3),0(R7)  REC ELEM V. ADDEL CODE                            
         BE    A889           SAME CODE - TEST FOR REPLACE OR ADD               
         BH    INSERTR        INSERT IF NO OLD ELEM                             
NEX60EL  IC    R4,1(R3)       RECELEM LEN                                       
         BXLE  R3,R4,A995     LOOP THRU ELEMENTS                                
         B     INSERTR        IF NO OLD THEN ADD AT END                         
*                                                                               
*                                                                               
*              AFFID ELEM ROUTINE                                               
*                                                                               
AFFID    CLI   0(R3),6             DATE ELEM?                                   
         BL    AFF10                                                            
*                                                                               
         CLI   0(R3),16            AFFID?                                       
         BNL   AFF10                                                            
*                                                                               
*        MUST BE DATE ELEM                                                      
         CLC   2(2,R3),AMGELEM     ELEM DATE V. AFFID DATE                      
         BH    ERR4                BH NOT FOUND                                 
         BE    AFF20                                                            
*                                                                               
AFF10    IC    R4,1(R3)            ELEM LEN                                     
         BXLE  R3,R4,AFFID         NEXT ELEM                                    
         B     ERR4                NOT FOUND                                    
AFF20    CLI   BUYKEY+3,X'FF'      POOL?                                        
         BE    POLAFFID                                                         
AFF25    IC    R4,7(R3)            SPOT NO                                      
         L     R8,SPTCTR                                                        
         TM    6(R3),X'80'         NEG ELEM?                                    
         BZ    *+6                                                              
         LNR   R4,R4                                                            
         AR    R8,R4               ADD NO, SPOTS FOR DAY                        
         SR    R4,R4                                                            
         ST    R8,SPTCTR           SAVE NO. SPOTS                               
AFF30    IC    R4,1(R3)            ELEM LEN                                     
         BXLE  R3,R4,*+8           NEXT ELEM                                    
         B     AFF40               LAST ELEM                                    
*                                                                               
         CLI   0(R3),6                                                          
         BL    ERR4                JUST IN CASE                                 
         CLI   0(R3),16            AFFID ELEM                                   
         BH    AFF40                                                            
         BE    AFF50                                                            
*                                                                               
*        DATE ELEM                                                              
         CLC   2(2,R3),AMGELEM     SAME DATE?                                   
         BE    AFF25                                                            
*                                                                               
*        TEST FOR NET SPOTS FOR DAY                                             
AFF40    L     R8,SPTCTR           SPOT CTR                                     
         SH    R8,ELNO             AFFID REF NO.                                
         BM    ERR5                NOT ENUF SPOTS IN DAY                        
         CLI   1(R7),0             DELETE AFFID? (LEN=0)                        
         BE    RETURN                                                           
         B     INSERTR             INSERT AFFID ELEM                            
*                                                                               
*                                                                               
*        AFFID ELEM ENCOUNTERED                                                 
AFF50    LH    R8,ELCTR            ELEM CTR                                     
         LA    R8,1(R8)                                                         
         STH   R8,ELCTR                                                         
*                                                                               
         CLC   ELNO,ELCTR          AFFID REF NO. V. ELEM CTR                    
         BH    AFF30                                                            
*                                  = AFFID ELEM                                 
         L     R8,SPTCTR                                                        
         SH    R8,ELNO             TEST IF ENUF SPOTS ON DAY                    
         BM    ERR5                                                             
         B     REPROUT             REPLACE AFFID ELEM                           
*                                                                               
*                                                                               
*        POOL AFFID ROUTINE   - DATE ELEM FOUND                                 
*                                                                               
POLAFFID LH    R8,ELCTR                                                         
         LA    R8,1(R8)                                                         
         STH   R8,ELCTR                                                         
*                                                                               
*                                                                               
         CLC   ELNO,ELCTR          AFFID REF NO. V. ELEM CTR                    
         BH    AFF80                                                            
*                                                                               
*        ELEM REFERENCED FOUND                                                  
*                                                                               
         TM    6(R3),X'40'         SPOT MINUSED?                                
         BO    ERR3                                                             
*                                                                               
         CLI   1(R3),10            UNALLOCATED?                                 
         BE    ERR3                                                             
*                                                                               
         IC    R4,1(R3)            ELEM LEN                                     
         BXLE  R3,R4,*+8                                                        
         B     *+12                                                             
*                                                                               
         CLI   0(R3),16            AFFID ELEM?                                  
         BE    REPROUT             REPLACE AFFID ELEM                           
         CLI   1(R7),0             DELETE AFFID? (NO LEN)                       
         BE    RETURN                                                           
         B     INSERTR             INSERT AFFID ELEM                            
*                                                                               
*                                                                               
AFF80    IC    R4,1(R3)            ELEM LEN                                     
         BXLE  R3,R4,*+8           NEXT ELEM                                    
         B     ERR4                NOT FOUND                                    
*                                                                               
*        CHECK FOR REFERENCED ELEM                                              
         CLI   0(R3),11            JUST IN CASE                                 
         BL    ERR4                                                             
*                                                                               
         CLI   0(R3),16            AFFID ELEM?                                  
         BNL   AFF80                                                            
*                                                                               
*        DATE ELEM                                                              
         CLC   2(2,R3),AMGELEM     SAME DATE?                                   
         BNE   ERR4                                                             
*                                                                               
         TM    6(R3),X'80'         MINUS ELEM?                                  
         BO    AFF80                                                            
         B     POLAFFID                                                         
*                                                                               
*        THIS ROUTINE HANDLES END OF DATE ELEMS                                 
A997     CLI   0(R7),0        ALLOCATION?                                       
         BE    ERR4           NOT FOUND                                         
         TM    6(R7),X'80'    MINUS?                                            
         BZ    INSERTR        IF POSITIVE, INSERT                               
         CLI   0(R7),8       NEG FLIP?                                          
         BE    A842          IF SO, TEST FOR NEG SPOTS FOR DAY                  
         CLI   0(R7),7        NONPOOL MINUS OTO?                                
         BNE   A888           NOT FOUND                                         
A842     L     R8,SPTCTR     NO OF SPOTS FOR DAY                                
         SR    R5,R5                                                            
         IC    R5,7(R7)       NO. SPOTS                                         
         SR    R8,R5          TEST IF NET NO. SPOTS LESS THAN ZERO              
         BM    ERR5                                                             
         B     INSERTR        INSERT ELEM                                       
A888     CLI   AERR+1,0            REF NO. NOT FOUND?                           
         BNE   ERR4           NOT FOUND                                         
         B     INSERTR        INSERT ELEM                                       
*        THIS ROUTINE HANDLES (RE)ALLOCATIONS                                   
A998     TM    6(R3),X'80'         NEG SPOT?                                    
         BO    NEXEL                                                            
         LH    R14,ELCTR           ELEM CTR                                     
         LA    R14,1(R14)                                                       
         STH   R14,ELCTR                                                        
         CH    R14,ELNO       ELEM CTR V. REF. NO                               
         BL    NEXEL                                                            
         CLC   7(3,R3),=3X'00'    ADJUSTED?                                     
         BC    0,ERR8      ADJUSTED COST NO LONGER SACRED                       
         TM    6(R7),X'40'         DELETE SPOT?                                 
         BO    DELMG                                                            
         CLI   4(R3),0        PAID                                              
         BNE   ERR6           NO RE(ALLOCATION) IF PAID                         
         CLI   1(R3),10     UNALLOCATED? (LEN=10)                               
         BE    A999                                                             
         LA    R9,10(R3)      1ST PROD                                          
         IC    R4,1(R3)       ELEM LEN                                          
         LA    R8,0(R4,R3)    ELEM END                                          
A991     CLI   2(R9),0        BILLED?                                           
         BNE   ERR6           IF BILLED, NO (RE)ALLOCATION                      
         LA    R9,4(R9)       NEXT PROD                                         
         CR    R9,R8          ELEM END?                                         
         BL    A991                                                             
A999     TM    6(R3),X'40'         OLD SPOT MINUSED?                            
         BO    ERR6                                                             
         MVI   AERR+1,0       SPOT REF NUMBER FOUND                             
         MVC   0(1,R7),0(R3)  MOVE OLD TRANS CODE TO NEW                        
         CLI   1(R7),10       NEW UNALLOCATED?                                  
         BNE   NEWALLO                                                          
         TM    6(R7),X'20'    NEW RATE? (UNALLOCATED)                           
         BZ    REPROUT                                                          
*        NEW ELEM UNALLO + NEW RATE                                             
         CLC   BDCOST,7(R7)        NEW RATE=BD COST?                            
         BNE   *+18                                                             
*        ELIM. RATE OVERRIDE IN OLD                                             
         XC    7(3,R3),7(R3)                                                    
         NI    6(R3),X'DF'                                                      
         B     RETURN                                                           
*                                                                               
         MVC   7(3,R3),7(R7)  MOVE NEW RATE TO OLD                              
         OI    6(R3),X'20'    RATE BIT                                          
         NI    6(R3),X'FB'    TURN OFF HIATUS BIT IF ON                         
         B     RETURN                                                           
*                                                                               
*        NEW SPOT IS ALLOCATED                                                  
NEWALLO  TM    6(R7),X'20'         NEW RATE OVERRIDE?                           
         BZ    A999C                                                            
*        NEW RATE OVERRIDE                                                      
         CLC   BDCOST,7(R7)        NEW RATE V BD COST                           
         BNE   REPROUT                                                          
*                                                                               
*        ELIM NEW RATE OVERRIDE                                                 
         XC    7(3,R7),7(R7)                                                    
         NI    6(R7),X'DF'                                                      
         B     REPROUT                                                          
*                                                                               
*        NO NEW RATE OVERRIDE                                                   
A999C    TM    6(R3),X'20'         OLD RATE?                                    
         BZ    REPROUT                                                          
         CLC   BDCOST,7(R3)        OLD COST + NEW BUY DESC COST                 
         BE    REPROUT                                                          
         MVC   7(3,R7),7(R3)       MOVE OLD RATE TO NEW                         
         OI    6(R7),X'20'         NEW RATE BIT                                 
         B     REPROUT                                                          
*        DELETE MG MAS IND AND ELIM MINUS SPOT                                  
DELMG    NI    6(R3),X'FD'         ELIM MG MAS IND                              
DELSPOT  LR    R9,R3               SAVE OLD ELEM ADDR                           
         IC    R4,1(R3)            OLD ELEM LEN                                 
         LA    R3,0(R4,R3)         NEXT ELEM                                    
         TM    6(R3),X'80'         NEXT ELEM MINUS?                             
         BZ    RETURN                                                           
         CLI   0(R3),12            ELEM CODE                                    
         BNE   RETURN                                                           
         CLI   4(R3),0             PAID?                                        
         BNE   RETURN                                                           
         CLI   1(R3),10            UNALLO MINUS?                                
         BE    DELMG100                                                         
         IC    R4,1(R3)            ELEM LEN                                     
         LA    R14,4                                                            
         LA    R15,0(R4,R3)        ELEM END                                     
         BCTR  R15,R0                                                           
         LA    R4,10(R3)           1ST PRD                                      
         CLI   2(R4),0             BILLED?                                      
         BNE   RETURN                                                           
         BXLE  R4,R14,*-8          TEST IF MINUS SPOT BILLED?                   
DELMG100 NI    6(R9),X'BF'         ELIM MINUSED IND                             
         MVI   1(R7),0             DELETE IND                                   
         B     REPROUT                                                          
*        TEST FOR REPLACE OR ADD OF NON-DATE ELEMENTS                           
A889     CLI   0(R7),2        ADDEL=ORIG DEMO?                                  
         BE    REPROUT            REPLACE ORIG DEMO ELEM                        
         CLI   0(R7),3             =SWEEP DEMO?                                 
         BE    BYTES23                                                          
         CLI   0(R7),X'66'         =COMMENT?                                    
         BE    BYTE2                            CHECK NUMBER                    
         CLI   0(R7),5              MASTER-SLAVE?                               
         BE    REPROUT                                                          
         B     REPROUT              UNANTICIPATED ELEM - REPLACE                
BYTES23  CLC   2(2,R3),2(R7)        SAME BOOK?                                  
A992     BE    REPROUT              IF SO, REPLACE                              
         BH    INSERTR        IF HIGH ADD ELEM                                  
         B     NEX60EL        GET NEXT ELEM IN REC                              
*        COMMENT + MASTER-SLAVE ELEMENTS                                        
BYTE2    CLC   2(1,R3),2(R7)  SAME CODE?                                        
         B     A992           USE OTHER BRANCHES                                
ERR1     MVC   AERR,=X'C10101'     NO ROOM                                      
         B     ERRRET                                                           
ERR3     MVC   AERR,=X'C10103'     ALREADY -                                    
         B     ERRRET                                                           
ERR4     MVC   AERR,=X'C10104'     NOT FOUND                                    
         B     ERRRET                                                           
ERR5     MVC   AERR,=X'C10105'     NET = 0                                      
         B     ERRRET                                                           
ERR8     MVC   AERR(3),=X'F50007'      ADJ COST                                 
         B     ERRRET                                                           
*        BILLED-PAY ERROR CHECK                                                 
ERR6     CLC   1(1,R7),1(R3)       SAME LEN?                                    
         BE    *+14                                                             
BPERR    MVC   AERR,=X'C10106'     BILL/PAY ERROR                               
         B     ERRRET                                                           
*        TEST IF SAME PRD-LEN IN NEW AND OLD ELEMENTS                           
         SR    R4,R4                                                            
         IC    R4,1(R3)            ELEM LEN                                     
         LA    R2,10(R3)           1ST OLD PROD                                 
         LA    R5,0(R4,R3)         ELEM END                                     
         BCTR  R5,R0                                                            
         LA    R8,10(R7)           1ST NEW PROD                                 
         LA    R4,4                ENTRY LEN                                    
         CLI   1(R3),10            UNALLO?                                      
         BE    RETURN                                                           
         CLC   0(2,R2),0(R8)       SAME PRD-LEN?                                
         BNE   BPERR                                                            
         LA    R8,4(R8)                                                         
         BXLE  R2,R4,*-14                                                       
*        SAME PRD-LENGTHS                                                       
         MVC   DOUB(1),6(R3)       OLD STATUS BYTE                              
         MVC   DOUB+1(1),6(R7)     NEW STATUS BYTE                              
         NC    DOUB(2),=X'1818'    ELIM OTHER BITS                              
         CLC   DOUB(1),DOUB+1      SAME FREE RIDER STATUS?                      
         BNE   BPERR                                                            
         TM    6(R3),X'20'         OLD RATE OVERRIDE?                           
         BO    *+16                                                             
         TM    6(R7),X'20'         NEW RATE?                                    
         BO    BPERR               IF SO ERROR                                  
         B     RETURN                                                           
*        OLD RATE OVERRIDE                                                      
         TM    6(R7),X'20'         NEW RATE                                     
         BZ    *+18                                                             
*        BOTH OLD AND NEW HAVE RATE OVERRIDES                                   
         CLC   7(3,R3),7(R7)       SAME RATES?                                  
         BNE   BPERR                                                            
         B     RETURN                                                           
*        NEW DOES NOT HAVE RATE OVERRIDE-OLD DOES                               
         CLC   BDCOST,7(R3)        OLD SAME AS BUY DESC? (COULD CHANGE)         
         BNE   BPERR                                                            
         B     RETURN                                                           
ERRRET   MVI   AERR+3,X'FF'   ERROR INDICATOR                                   
RETURN   XIT1                                                                   
*        THIS ROUTINE INSERTS NEW ELEMENTS                                      
INSERTR  L     R5,AREC        A(REC)                                            
         LH    R14,LEN        REC LEN                                           
         LA    R5,0(R14,R5)   END OF RECORD                                     
         SR    R8,R8                                                            
         IC    R8,AERR       ELEM LEN                                           
REPENTRY LR    R9,R8          ADDEL LEN                                         
         BCTR  R9,R0                                                            
         LR    R4,R5          REC END                                           
         SR    R4,R8          SUB ADDEL LEN                                     
A1006    EX    R9,MOV4TO5     MOVE BOTTOM OF RECORD DOWN                        
         CR    R3,R4          INSERTION POINT V. LAST MOVE                      
         BH    A1005                                                            
         SR    R4,R8          SUB LEN                                           
         SR    R5,R8                                                            
         B     A1006                                                            
MOV7TO3  MVC   0(0,R3),0(R7)                                                    
MOV4TO5  MVC   0(0,R5),0(R4)                                                    
COM7TO3  CLC   0(0,R3),0(R7)                                                    
A1005    EX    R9,MOV7TO3     INSERT ADDEL                                      
*        COMPUTE NEW RECORD LENGTH                                              
NEWLEN   LH    R14,LEN        REC LEN                                           
         AR    R14,R8         ADD ADDEL LEN                                     
         STH   R14,LEN                                                          
         MVC   BUYLEN,LEN                                                       
         MVI   AREC,X'FF'          SET RECORD ALTERED                           
         B     RETURN                                                           
*        THIS ROUTINE REPLACES ELEMENTS                                         
REPROUT  SR    R8,R8                                                            
         LH    R14,LEN        REC LEN                                           
         L     R5,AREC        A(REC)                                            
         LA    R5,0(R14,R5)   END OF RECORD                                     
         IC    R8,AERR       ELEM LEN                                           
         LR    R9,R8                                                            
         BCTR  R9,R0                                                            
         SR    R4,R4                                                            
         IC    R4,1(R3)       REPLACE ELEM LEN                                  
         SR    R8,R4          NEW  - OLD ELEM LEN                               
         BZ    A1008                                                            
         BM    A1007                                                            
*        NEW ELEM IS LARGER THAN OLD                                            
         LR    R9,R4          OLD ELEM LEN                                      
         BCTR  R9,R0                                                            
         EX    R9,MOV7TO3     MOVE AS MUCH AS OLD AS POSSIBLE                   
         LA    R3,0(R4,R3)    BUMP TO INSERT POINT                              
         LA    R7,0(R4,R7)    BUMP ADDEL POINTER                                
         B     REPENTRY       INSERT REMAINDER                                  
*                                                                               
*        NEW + OLD ELEM SAME LENGTH                                             
A1008    EX    R9,COM7TO3          COMPARE ELEMS                                
         BE    RETURN              EQ - DONE                                    
         MVI   AREC,X'FF'          SET RECORD ALTERED                           
         EX    R9,MOV7TO3     REPLACE OLD WITH NEW                              
         B     RETURN                                                           
*                                                                               
*        NEW ELEM LESS THAN OLD                                                 
A1007    CLI   0(R7),5        MASTER-SLAVE?                                     
         BE    A1198                                                            
         CLI   1(R7),0             NEW ELEM LEN = 0? (DELETE)                   
         BE    A1199                                                            
         CLI   0(R7),X'66'    COMMENT?                                          
         BNE   A1098                                                            
A1198    CLI   1(R7),3        LEN OF ADD ELEM=3? (MEANS ELIMINATE)              
         BNE   A1098                                                            
A1199    SR    R9,R9                                                            
         IC    R9,1(R3)            LEN OF REPLACED ELEMENT                      
         LR    R4,R9                                                            
         LNR   R8,R9                                                            
         BCTR  R9,R0                                                            
         B     CONDENSE                                                         
A1098    LPR   R4,R8                                                            
         EX    R9,MOV7TO3                                                       
         LR    R9,R4          DIFF                                              
         BCTR  R9,R0                                                            
         SR    R6,R6                                                            
         IC    R6,1(R7)       NEW ELEM LEN                                      
         LA    R3,0(R6,R3)    WASTE SPACE                                       
CONDENSE LA    R7,0(R4,R3)    NEXT GOOD ELEM                                    
         EX    R9,MOV7TO3     MOVE ELEM UP OVER WASTE SPACE                     
         LA    R3,0(R4,R3)                                                      
         CR    R7,R5          END OF REC?                                       
         BL    CONDENSE                                                         
         B     NEWLEN         COMPUTE NEW LEN (SMALLER, R8 IS NEG)              
LIST     DSECT                                                                  
AREC     DS    F         A(RECORD)                                              
AELEM    DS    F         A(ELEMENT TO BE ADDED)                                 
AERR     DS    F         BYTE 0=ELEM LEN                                        
AMGELEM  DS    F                   A(MG ELEM ON RETURN - FOR PRODUCTS)          
*                        OR:  BYTES 0,1= AFFID ORD DATE                         
WORK     DSECT                                                                  
ELCTR    DS    H                                                                
SPTCTR   DS    F                                                                
ELNO     DS    H                                                                
LEN      DS    H                                                                
DOUB     DS    D                                                                
BUYRECD  DSECT                                                                  
       ++INCLUDE SPGENBUY                                                       
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'006SPREPI2AE 10/02/89'                                      
         END                                                                    
