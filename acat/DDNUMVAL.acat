*          DATA SET DDNUMVAL   AT LEVEL 003 AS OF 10/17/03                      
*CATALP NUMVAL                                                                  
         TITLE 'NUMVAL - VALIDATE NUMERIC FIELD'                                
         PRINT NOGEN                                                            
NUMVAL   CSECT                                                                  
         NMOD1 WRKX-WRKD,**NUMVAL                                               
         USING WRKD,RC             RC=A(W/S)                                    
         LR    R2,R1               R2=A(PARM)                                   
         LM    R4,R5,0(R2)         R4=A(INPUT),R5=L'INPUT                       
         MVC   DECFAC,0(R2)        GET NUMBER OF DEC PLACES                     
*                                                                               
NUMV1    CLI   4(R2),0             00=LENGTH IN BYTES 1-3                       
         BE    NUMV1X                                                           
         CLI   4(R2),3             USER SPECIFIED TERM. CHARACTER?              
         BNE   *+8                                                              
         STC   R5,TERM             YES: SAVE THE TERMINATION CHARACTER          
         SR    R5,R5                                                            
NUMV1A   CLI   4(R2),1             01=LENGTH IN FIELD HEADER                    
         BNE   NUMV1B                                                           
         LR    RE,R4                                                            
         SH    RE,=H'8'                                                         
         IC    R5,5(RE)                                                         
         B     NUMV1X                                                           
NUMV1B   CLI   4(R2),2             02=LENGTH TERM BY BLANK/NULL CHR             
         BE    *+12                                                             
         CLI   4(R2),3             03=LENGTH TERM BY USER-DEFINED CHAR          
         BNE   NUMV1C                                                           
         LA    R0,12                                                            
         LR    RF,R4                                                            
NUMV1B1  CLI   4(R2),3                                                          
         BNE   NUMV1B1D                                                         
         CLC   TERM,0(RF)                                                       
         BE    NUMV1B2                                                          
         B     NUMV1B1G                                                         
NUMV1B1D CLI   0(RF),C' '                                                       
         BE    NUMV1B2                                                          
         CLI   0(RF),X'00'                                                      
         BE    NUMV1B2                                                          
NUMV1B1G LA    RF,1(RF)                                                         
         BCT   R0,NUMV1B1                                                       
         B     NUMV1X                                                           
NUMV1B2  SR    RF,R4                                                            
         LR    R5,RF                                                            
         ST    R5,8(R2)                                                         
         B     NUMV1X                                                           
NUMV1C   EQU   *                                                                
NUMV1X   LTR   R5,R5               R5=L'DATA FIELD                              
         BP    NUMV2                                                            
         B     NUMERR              YOU PASSED ME A BAD LENGTH                   
*                                                                               
NUMV2    CTRY  ,                                                                
         STCM  R0,15,AGCOPT        GET AGCOPT/AGCTRY/LANG/CTRY                  
         LA    R1,LANGTAB                                                       
         USING LANGTABD,R1                                                      
NUMV2A   CLI   LANGLANG,LANGEOT    USE DEFAULTS IF END OF TABLE                 
         BE    NUMV2B                                                           
         CLC   LANGLANG,LANG       MATCH ON LANGUAGE CODE                       
         BE    NUMV2B                                                           
         LA    R1,LANGTABL(R1)                                                  
         B     NUMV2A                                                           
NUMV2B   MVC   MAGSEP,LANGMAG      EXTRACT LANGUAGE SPECIFIC VALUES             
         MVC   DECSEP,LANGDEC                                                   
         MVC   CURSYM,LANGSYM                                                   
         DROP  R1                                                               
         EJECT                                                                  
NUM1     MVI   0(R2),0             RETURN OK VALUE IN P1(1)                     
         MVI   POINT,0                                                          
         MVI   SIGN,0                                                           
         SR    R6,R6               R6=NUMBER OF DECIMAL PLACES                  
         SR    R7,R7               R7=OUTPUT COUNT                              
         LA    R8,WORK                                                          
         CLI   0(R4),C' '          IGNORE LEADING SPACES                        
         BNE   *+16                                                             
         LA    R4,1(R4)                                                         
         BCT   R5,*-12                                                          
         B     NUMERR                                                           
*                                                                               
NUM2     CLI   0(R4),C'0'          CHECK FOR VALID INTEGER                      
         BL    NUM4                                                             
         CLI   0(R4),C'9'                                                       
         BH    NUMERR                                                           
         MVC   0(1,R8),0(R4)                                                    
         LA    R8,1(R8)                                                         
         LA    R7,1(R7)                                                         
         CLI   POINT,0                                                          
         BE    *+8                                                              
         LA    R6,1(R6)                                                         
         B     NUM8                                                             
*                                                                               
NUM4     CLC   0(1,R4),DECSEP      CHECK FOR DECIMAL POINT SEPERATOR            
         BNE   NUM6                                                             
         CLI   POINT,0                                                          
         MVI   POINT,1                                                          
         BNE   NUMERR                                                           
         B     NUM8                                                             
*                                                                               
NUM6     CLI   0(R4),C'+'          CHECK FOR SIGN CHR                           
         BE    *+12                                                             
         CLI   0(R4),C'-'                                                       
         BNE   NUMERR                                                           
         CLI   SIGN,0                                                           
         BNE   NUMERR                                                           
         MVC   SIGN,0(R4)                                                       
         LTR   R7,R7               SIGN MUST BE LEADING OR TRAILING             
         BZ    NUM8                                                             
         CH    R5,=H'1'                                                         
         BNE   NUMERR                                                           
*                                                                               
NUM8     LA    R4,1(R4)            BUMP TO NEXT CHR IN STRING                   
         BCT   R5,NUM2                                                          
         STC   R6,POINT                                                         
         CLC   POINT,DECFAC                                                     
         BH    NUMERR                                                           
         LTR   R7,R7                                                            
         BZ    NUMERR                                                           
         IC    R5,DECFAC                                                        
         SR    R5,R6                                                            
         AR    R7,R5                                                            
         BCTR  R7,0                                                             
         MVC   0(5,R8),=C'00000'                                                
         EX    R7,*+8                                                           
         B     *+10                                                             
         PACK  DUB,WORK(0)                                                      
         CVB   R1,DUB                                                           
         CLI   SIGN,C'-'                                                        
         BNE   *+6                                                              
         LNR   R1,R1                                                            
         ST    R1,4(R2)                                                         
         B     *+8                                                              
*                                                                               
NUMERR   MVI   0(R2),X'FF'         RETURN INVALID INPUT STRING                  
         XMOD1 1                                                                
         EJECT                                                                  
         LTORG                                                                  
*                                                                               
LANGTAB  DC    AL1(LANGENG),C',.',C'$ ',C'BONFREZERNIL'                         
         DC    AL1(LANGEUK),C',.',C'$ ',C'BONFREZERNIL'                         
         DC    AL1(LANGEUS),C',.',C'$ ',C'BONFREZERNIL'                         
         DC    AL1(LANGGER),C'.,',C'DM',C'BONKOSNULNUL'                         
         DC    AL1(LANGFRE),C'.,',C'FF',C'PRIGRAZERZER'                         
         DC    AL1(LANGSPA),C'.,',C'PT',C'BONGRACERNAD'                         
         DC    AL1(LANGITA),C'.,',C'LI',C'BONGRAZERNIE'                         
         DC    AL1(LANGDUT),C'.,',C'FL',C'GRANIENULNUL'                         
LANGTABX DC    AL1(LANGEOT),C'.,',C'? ',C'BONFREZERNIL'                         
         EJECT                                                                  
LANGTABD DSECT                                                                  
LANGLANG DS    XL1                 LANGUAGE CODE                                
LANGEOT  EQU   255                 END OF TABLE INDICATOR                       
LANGMAG  DS    CL1                 MAGNITUDE CHARACTER                          
LANGDEC  DS    CL1                 DECIMAL POINT CHARACTER                      
LANGSYM  DS    CL2                 CURRENCY SYMBOL                              
LANGSPCN EQU   4                                                                
LANGSPC  DS    (LANGSPCN)CL3       SPECIAL WORDS FOR ZERO                       
LANGTABL EQU   *-LANGTABD                                                       
         SPACE 2                                                                
WRKD     DSECT                                                                  
DUB      DS    D                                                                
WORK     DS    CL20                                                             
DECFAC   DS    X                                                                
SIGN     DS    C                                                                
POINT    DS    X                                                                
TERM     DS    C                   USER-DEFINED TERMINATION CHARACTER           
*                                                                               
AGCOPT   DS    XL1                 AGENCY COUNTRY OPTIONS                       
AGCTRY   DS    XL1                 AGENCY COUNTRY                               
CTRY     DS    XL1                 ACTUAL COUNTRY                               
LANG     DS    XL1                 LANGUAGE                                     
MAGSEP   DS    CL1                 MAGNITUDE CHARACTER                          
DECSEP   DS    CL1                 DECIMAL POINT CHARACTER                      
CURSYM   DS    CL2                 CURRENCY SYMBOL                              
WRKX     EQU   *                                                                
         SPACE 2                                                                
* DDLANGEQUS                                                                    
         PRINT OFF                                                              
       ++INCLUDE DDLANGEQUS                                                     
         PRINT ON                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'003DDNUMVAL  10/17/03'                                      
         END                                                                    
