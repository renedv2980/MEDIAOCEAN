*          DATA SET NENETSPB   AT LEVEL 038 AS OF 05/01/02                      
*CATALP NETSPB                                                                  
         TITLE 'NETSPB - NETWORK SPLIT BILLING MODULE'                          
NETSPB   CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 MYDX-MYD,**NSPB**                                                
         USING MYD,RC                                                           
         MVC   USERRD,4(RD)                                                     
         L     R9,0(R1)                                                         
         USING SPLTBLKD,R9                                                      
         L     RA,SPLANETB                                                      
         USING NETBLOCK,RA                                                      
*                                                                               
         OC    NBDM,NBDM           IS NETBLOCK DATAMGR PRESENT                  
         BNZ   NSB5                                                             
         MVC   NBDM,SPLDMGR        USER MUST PASS DATAMGR                       
         OC    NBDM,NBDM                                                        
         BNZ   *+6                                                              
         DC    H'0'                                                             
NSB5     ZAP   PACKAMT,=P'0'                                                    
         ZAP   PACKPCT,=P'0'                                                    
         ZAP   HUNDS,=P'0'                                                      
         ZAP   PAKTOT,=P'0'                                                     
         ZAP   PAKAMT,=P'0'                                                     
         ZAP   PAKWRK,=P'0'                                                     
         ZAP   PCTTOT,=P'0'                                                     
*                                                                               
         XC    KEY,KEY             SET UP KEY FOR SPB RECORD                    
         LA    R4,KEY                                                           
         USING SPBRECD,R4                                                       
         MVI   SPBKTYP,SPBKTYPQ                                                 
         MVI   SPBKSUB,SPBKSUBQ                                                 
         MVC   SPBKPRD,SPLPRD                                                   
*                                                                               
         OC    SPLANETB,SPLANETB   IS NETBLOCK PRESENT                          
         BZ    NSB7                                                             
         MVC   SPBKAGMD,NBACTAM    YES/                                         
         MVC   SPBKCLT,NBACTCLI                                                 
         MVC   SPBKEST,NBACTEST                                                 
         B     NSB10                                                            
NSB7     CLI   SPLKAM,0            NO/USE PASSED KEY FIELDS                     
         BE    XIT                                                              
         MVC   SPBKAGMD,SPLKAM                                                  
         OC    SPLKCLT,SPLKCLT                                                  
         BZ    XIT                                                              
         MVC   SPBKCLT,SPLKCLT                                                  
         CLI   SPLKEST,0                                                        
         BE    XIT                                                              
         MVC   SPBKEST,SPLKEST                                                  
*                                                                               
NSB10    LA    R6,SPLIO            DO WE ALREADY HAVE SPB REC                   
         CLC   KEY(13),0(R6)                                                    
         BE    NSB11B                                                           
         GOTO1 HIGH                NO/GET IT                                    
         CLC   KEY(13),KEYSAVE                                                  
         BE    NSB11                                                            
*                                                                               
         MVC   SPLOPRD,SPLPRD      SPB REC NOT FOUND                            
         L     R1,SPLAMT           RETURN PRODUCT AND AMT                       
         CVD   R1,SPLODOLS                                                      
         XC    SPLEPCT,SPLEPCT                                                  
         BAS   RE,GOHOOK                                                        
         B     XIT                                                              
*                                                                               
NSB11    GOTO1 GETREC                                                           
*                                                                               
NSB11B   LA    R1,SPLPLIST         PRODUCT LIST                                 
         BAS   RE,PRDLIST                                                       
         CLI   SPLPRDO,C'Y'        ARE WE FINISHED                              
         BNE   NSB12                                                            
         BAS   RE,GOHOOK           YES/RETURN PROD CODES                        
         B     XIT                 AND EXIT                                     
*                                                                               
         EJECT                                                                  
NSB12    LA    R6,SPLIO                                                         
         MVI   ELCODE,3                                                         
         BAS   RE,GETEL                                                         
         BNE   XIT                                                              
         USING NETYPE,R6                                                        
         MVC   TYPE,NETPID                                                      
         TM    TYPE,X'04'          IF IT'S DOLLARS                              
         BO    NSB30                                                            
*                                                                               
         MVI   ELCODE,4            IT'S PERCENT                                 
         USING NECEL,R6                                                         
         BAS   RE,NEXTEL                                                        
         BE    *+6                                                              
         DC    H'0'                                                             
NSB16    XC    SPLEPCT,SPLEPCT                                                  
         XC    SPLEDOL,SPLEDOL                                                  
         XC    SPLETYP,SPLETYP                                                  
         MVC   SPLOPRD,NECPRD                                                   
         MVC   SPLEPCT,NECPCT                                                   
         MVC   SPLETYP,TYPE                                                     
         L     R1,SPLAMT           PENNIES                                      
         CVD   R1,DUB                                                           
         ZAP   PACKAMT,DUB                                                      
         MVC   FULL,NECPCT         PERCENT TO 4 DECIMALS                        
         L     R1,FULL                                                          
         CVD   R1,DUB                                                           
         ZAP   PACKPCT,DUB+4(4)                                                 
         MP    PACKAMT,PACKPCT                                                  
         DP    PACKAMT,=P'1000000'                                              
         TM    SPLPRECI,X'04'        IS IT TO NEAREST DOLLAR                    
         BO    NSB18                                                            
         AP    PAKTOT,PACKAMT(8)      NO/PENNIES - ADD TO TOTAL                 
         ZAP   SPLODOLS,PACKAMT(8)                 SET TO OUT AREA              
         AP    HUNDS,PACKAMT+8(4)   SAVE HUNDREDS                               
         CP    HUNDS,=P'999999'                                                 
         BNH   NSB20                                                            
         AP    SPLODOLS,=P'1'      ADD A PENNY                                  
         AP    PAKTOT,=P'1'                                                     
         SP    HUNDS,=P'1000000'                                                
         B     NSB20                                                            
NSB18    MVC   DUB,PACKAMT             TO NEAREST DOLLAR                        
         CVB   R1,DUB                                                           
         BAS   RE,ROUND                                                         
         LA    R3,100                                                           
         MR    R0,R3                                                            
         CVD   R1,SPLODOLS                                                      
         AP    PAKTOT,SPLODOLS                                                  
NSB20    BAS   RE,NEXTEL           IS THIS LAST PRODUCT                         
         BNE   NSB22                                                            
         CLI   SPLIPRD,0           RETURN ONLY SPECIFIC PROD                    
         BE    NSB21                                                            
         CLC   SPLIPRD,SPLOPRD                                                  
         BNE   NSB16                                                            
         B     NSB28                                                            
NSB21    BAS   RE,GOHOOK           HOOK BACK TO APPLICATION                     
         B     NSB16               AND DO NEXT PRODUCT                          
*                                                                               
*                                  LAST PRODUCT                                 
NSB22    CLI   SPLIPRD,0           RETURN ONLY SPECIFIC PROD                    
         BE    NSB24                                                            
         CLC   SPLIPRD,SPLOPRD                                                  
         BE    NSB24                                                            
         XC    SPLOPRD,SPLOPRD     IF NO MATCH,RETURN ZEROS                     
         XC    SPLODOLS,SPLODOLS                                                
         B     NSB28                                                            
NSB24    L     R1,SPLAMT           DOES PRD TOTAL=SPLAMT                        
         CVD   R1,DUB                                                           
         CP    DUB,PAKTOT                                                       
         BE    NSB28                                                            
         SP    DUB,PAKTOT          NO/GET DIFFERENCE                            
         AP    SPLODOLS,DUB        ADD DIFFERENCE TO LAST PROD                  
NSB28    BAS   RE,GOHOOK                                                        
         B     XIT                                                              
*                                                                               
         EJECT                                                                  
NSB30    DS    0H                  DOLLARS                                      
         LA    R6,SPLIO                                                         
         USING NECEL,R6                                                         
         MVI   ELCODE,4                                                         
         BAS   RE,GETEL                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
NSB32    MVC   FULL,NECPCT           GET TOTAL DOLLARS INTO PAKAMT              
         L     R1,FULL                                                          
         CVD   R1,DUB                                                           
         AP    PAKAMT,DUB                                                       
         BAS   RE,NEXTEL                                                        
         BE    NSB32                                                            
*                                                                               
         LA    R6,SPLIO                                                         
         BAS   RE,GETEL                                                         
NSB33    XC    SPLEPCT,SPLEPCT                                                  
         XC    SPLEDOL,SPLEDOL                                                  
         XC    SPLETYP,SPLETYP                                                  
         MVC   SPLOPRD,NECPRD                                                   
*        MVC   SPLEPCT,NECPCT                                                   
         MVC   SPLEDOL,NECPCT                                                   
         MVC   SPLETYP,TYPE                                                     
         MVC   FULL,NECPCT                                                      
         L     R1,FULL                                                          
         CVD   R1,DUB                                                           
         ZAP   PAKWRK,DUB                                                       
         MP    PAKWRK,=P'1000000'  PERCENT TO 4 DECIMALS IN PACKPCT             
         DP    PAKWRK,PAKAMT                                                    
         ZAP   PACKPCT,PAKWRK(8)                                                
         AP    PCTTOT,PAKWRK(8)    ADD TO PERCENT TOTAL                         
         MVC   DUB,PAKWRK                                                       
         CVB   R1,DUB                                                           
         ST    R1,SPLEPCT          RETURN PRECENT IN SPLEPCT                    
         L     R1,SPLAMT                                                        
         CVD   R1,DUB                                                           
         ZAP   PACKAMT,DUB                                                      
         MP    PACKAMT,PACKPCT                                                  
         DP    PACKAMT,=P'1000000'                                              
         TM    SPLPRECI,X'04'         IS IT TO DOLLARS                          
         BO    NSB33B                                                           
         AP    PAKTOT,PACKAMT(8)      NO/PENNIES - ADD TO TOTAL                 
         ZAP   SPLODOLS,PACKAMT(8)              SET TO OUT AREA                 
         AP    HUNDS,PACKAMT+8(4)   SAVE HUNDREDS                               
         CP    HUNDS,=P'999999'                                                 
         BNH   NSB34                                                            
         AP    SPLODOLS,=P'1'      ADD A PENNY                                  
         AP    PAKTOT,=P'1'                                                     
         SP    HUNDS,=P'1000000'                                                
         B     NSB34                                                            
NSB33B   MVC   DUB,PACKAMT            TO NEAREST DOLLAR                         
         CVB   R1,DUB                                                           
         BAS   RE,ROUND                                                         
         LA    R3,100                                                           
         MR    R0,R3                                                            
         CVD   R1,SPLODOLS                                                      
         AP    PAKTOT,SPLODOLS                                                  
NSB34    BAS   RE,NEXTEL           IS THIS LAST PRODUCT                         
         BNE   NSB38                                                            
         CLI   SPLIPRD,0           RETURN ONLY SPECIFIC PROD                    
         BE    NSB36                                                            
         CLC   SPLIPRD,SPLOPRD                                                  
         BNE   NSB33                                                            
         B     NSB42                                                            
NSB36    BAS   RE,GOHOOK           HOOK BACK TO APPLICATION                     
         B     NSB33               AND DO NEXT PRODUCT                          
*                                                                               
*                                  LAST PRODUCT                                 
NSB38    CLI   SPLIPRD,0           RETURN ONLY SPECIFIC PROD                    
         BE    NSB40                                                            
         CLC   SPLIPRD,SPLOPRD                                                  
         BE    NSB40                                                            
         XC    SPLOPRD,SPLOPRD     IF NO MATCH,RETURN ZEROS                     
         XC    SPLODOLS,SPLODOLS                                                
         B     NSB42                                                            
NSB40    L     R1,SPLAMT           DOES PRD TOTAL=SPLAMT                        
         CVD   R1,DUB                                                           
         CP    DUB,PAKTOT                                                       
         BE    NSB42                                                            
         SP    DUB,PAKTOT          NO/GET DIFFERENCE                            
         AP    SPLODOLS,DUB        ADD DIFFERENCE TO LAST PROD                  
NSB42    BAS   RE,GOHOOK                                                        
         B     XIT                                                              
*                                                                               
*                                                                               
ROUND    DS    0H                  DIVIDE BY 100 AND ROUND                      
         LR    R2,R1                                                            
         SR    R3,R3                                                            
         SRDA  R2,31                                                            
         D     R2,=F'100'                                                       
         LTR   R3,R3                                                            
         BM    *+8                                                              
         AH    R3,=H'1'                                                         
         SRA   R3,1                                                             
         LR    R1,R3                                                            
         BR    RE                                                               
         EJECT                                                                  
*                                                                               
* READS ELEMENTS AND RETURNS LIST OF PRODUCTS IN SPLPLIST                       
*                                                                               
PRDLIST  NTR1                                                                   
         LA    R6,SPLIO                                                         
         USING NECEL,R6                                                         
         LA    R2,SPLPLIST         AREA FOR PROD LIST                           
         XC    0(200,R2),0(R2)                                                  
         LA    R3,65               MAX PRODS=65                                 
         MVI   ELCODE,4                                                         
         BAS   RE,GETEL                                                         
         BNE   XIT                                                              
PL5      MVC   0(3,R2),NECPRD                                                   
         LA    R2,3(R2)                                                         
         BAS   RE,NEXTEL                                                        
         BNE   PL10                                                             
         BCT   R3,PL5                                                           
*PL10     BAS   RE,GOHOOK                                                       
PL10     B     XIT                                                              
         DROP  R6                                                               
         EJECT                                                                  
*              DATAMGR INTERFACE                                                
         SPACE 3                                                                
HIGH     NTR1                                                                   
         MVC   KEYSAVE,KEY                                                      
         MVC   COMMAND,=CL8'DMRDHI'          HANDLE COMMANDS                    
         B     DIRALL                                                           
         SPACE 1                                                                
SEQ      NTR1                                                                   
         MVC   KEYSAVE,KEY                                                      
         MVC   COMMAND,=CL8'DMRSEQ'                                             
         B     DIRALL                                                           
         SPACE 1                                                                
READ     NTR1                                                                   
         MVC   COMMAND,=CL8'DMREAD'                                             
         SPACE 1                                                                
DIRALL   MVC   FILE(8),=C'SPTDIR  '         DIRECTORIES                         
         LA    R2,WORK                                                          
         GOTO1 NBDM,DMCB,COMMAND,FILE,KEY,(R2),0                                
         BAS   RE,DMCHECK                                                       
         MVC   KEY,0(R2)                                                        
         B     YES                                                              
         SPACE 1                                                                
GETREC   NTR1                                                                   
         LA    R3,KEY+14                                                        
         MVC   FILE(8),=C'SPTFILE '     FILE                                    
         SPACE 1                                                                
GETREC2  LA    R2,SPLIO                                                         
         GOTO1 NBDM,DMCB,(X'00',=C'GETREC'),FILE,(R3),(R2),DMWORK               
         BAS   RE,DMCHECK                                                       
         B     XIT                                                              
         SPACE 1                                                                
DMCHECK  CLI   8(R1),0                                                          
         BER   RE                                                               
         TM    8(R1),X'90'                                                      
         BM    NO                                                               
         DC    H'0'                                                             
         SPACE 1                                                                
         GETEL (R6),24,ELCODE                                                   
         SPACE 1                                                                
NO       SR    R1,R1                                                            
         B     *+8                                                              
         SPACE 1                                                                
YES      LA    R1,1                                                             
         LTR   R1,R1                                                            
         SPACE 1                                                                
XIT      XIT1                                                                   
         EJECT                                                                  
         SPACE 1                                                                
GOHOOK   NTR1                                                                   
         L     RF,SPLAHOOK                                                      
         L     RE,USERRD           USERS RD                                     
         LM    R0,RC,20(RE)        RESTORE USERS R0-RC                          
         BASR  RE,RF               RF CONTAINED A(HOOK ROUTINE)                 
         XIT1                                                                   
         SPACE 1                                                                
RELO     DS    A                                                                
         EJECT                                                                  
         LTORG                                                                  
         EJECT                                                                  
*              DSECTS ETC                                                       
         SPACE 3                                                                
*                                                                               
MYD      DSECT                                                                  
DUB      DS    D                                                                
DMCB     DS    6F                                                               
USERRD   DS    F                                                                
FULL     DS    F                                                                
DATADSP  DS    H                                                                
WORK     DS    CL64                                                             
KEY      DS    CL27                                                             
KEYSAVE  DS    CL27                                                             
ELCODE   DS    CL1                                                              
TYPE     DS    CL1                                                              
SEQFLG   DS    CL1                                                              
COMMAND  DS    CL8                                                              
FILE     DS    CL8                                                              
DMWORK   DS    CL96                                                             
*                                                                               
PACKAMT  DS    CL12                PACKED FIELDS                                
PACKPCT  DS    CL4                                                              
PAKTOT   DS    CL8                                                              
HUNDS    DS    CL4                                                              
PAKAMT   DS    CL8                                                              
PAKWRK   DS    CL16                                                             
PCTTOT   DS    CL8                                                              
*                                                                               
MYDX     DS    CL1                                                              
         SPACE 3                                                                
         EJECT                                                                  
       ++INCLUDE SPLTBLKD                                                       
         EJECT                                                                  
       ++INCLUDE SPGENSPBL                                                      
         EJECT                                                                  
         PRINT OFF                                                              
       ++INCLUDE NETBLOCKD                                                      
         PRINT ON                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'038NENETSPB  05/01/02'                                      
         END                                                                    
