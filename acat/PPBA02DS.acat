*          DATA SET PPBA02DS   AT LEVEL 119 AS OF 05/01/02                      
*CATALP PPBA02                                                                  
         TITLE 'PPBA02 - PRINTPAK/ACCOUNTING BILL POSTING'                      
*                                                                               
* QOPT1  Y=TEST                                                                 
* QOPT2  Y=LIST NON-POSTED                                                      
* QOPT3  Y=LIST PREVIOUS POSTINGS                                               
* QOPT4  D=DUMP WORKER FILE                                                     
* QOPT5  R=RERUN, OK TO RE-CREATE WORKER FILE                                   
* QOPT6  (QOPT5+1) R=RE-POST BILLS                                              
*                                                                               
         PRINT NOGEN                                                            
PPBA02   CSECT                                                                  
         NMOD1 0,PPBA02                                                         
         SPACE 2                                                                
         LA    R9,2048(RB)                                                      
         LA    R9,2048(R9)                                                      
         USING PPBA02+4096,R9      **2ND BASE REG**                             
         L     RA,0(R1)                                                         
         USING PPWORKD,RA                                                       
         L     RC,PPFILEC                                                       
         USING PPFILED,RC                                                       
         LA    R8,SPACEND                                                       
         USING PPBAWRKD,R8                                                      
         L     RF,UTL                                                           
         MVC   SAVSYS,4(RF)                                                     
         MVC   4(1,RF),ACCTSE      SET ACCT SYS NUM                             
         SPACE 2                                                                
         CLI   MODE,RUNFRST                                                     
         BE    RUNF                                                             
         CLI   MODE,REQFRST                                                     
         BE    REQF                                                             
         CLI   MODE,PROCBIL                                                     
         BE    PRBL                                                             
         CLI   MODE,FBUYCLI                                                     
         BE    FBILC                                                            
         CLI   MODE,FBILPRO                                                     
         BE    FBILP                                                            
         CLI   MODE,LBILPRO                                                     
         BE    LBILP                                                            
         CLI   MODE,LBUYCLI                                                     
         BE    LBILC                                                            
         CLI   MODE,LBUYREQ                                                     
         BE    REQL                                                             
         CLI   MODE,RUNLAST                                                     
         BE    RUNL                                                             
*                                                                               
EXIT     DS    0H                                                               
         L     RF,UTL                                                           
         MVC   4(1,RF),SAVSYS      RESTORE SYSTEM NUM                           
         XIT1                                                                   
         EJECT                                                                  
         SPACE 3                                                                
         SPACE 3                                                                
RUNF     DS    0H                                                               
         MVI   POSTSW,C'Y'         CLEAR POSTED SWITCH                          
         LA    R4,RAMTS                                                         
         BAS   RE,ZAPSET                                                        
         LA    R5,POSTWORK                                                      
         USING MAPOSTD,R5                                                       
         ZAP   MAPRECNT,=P'0'                                                   
         ZAP   MAPCASH,=P'0'                                                    
         DROP  R5                                                               
         B     EXIT                                                             
*                                                                               
REQF     LA    R4,RQAMTS                                                        
         BAS   RE,ZAPSET                                                        
         LA    R4,CAMTS                                                         
         BAS   RE,ZAPSET                                                        
         LA    R4,PAMTS                                                         
         BAS   RE,ZAPSET                                                        
*                                                                               
         CLC   QEST,=C'ALL'        SET EST=ALL                                  
         BNE   *+10                TO SPACES (MY READING EST HDRS               
         MVC   QEST,SPACES         DISTURBS PPG BILL READ)                      
*                                                                               
         MVC   4(1,RF),SAVSYS                                                   
         XC    B1XPROF,B1XPROF      B1X PROFILE                                 
         XC    WORK,WORK                                                        
         MVC   WORK(4),=C'PB1X'                                                 
         NI    WORK,X'BF'          MAKE SYS LOWER CASE FOR PAGE A               
         MVC   WORK+4(2),QAGENCY                                                
         MVC   WORK+6(1),QMEDIA                                                 
         GOTO1 GETPROF,DMCB,WORK,B1XPROF,DATAMGR                                
*                                                                               
         SPACE 2                                                                
***********************************************************************         
*   RETAIL ACCOUNT FIX DATASET READ                                             
***********************************************************************         
         SPACE 2                                                                
         OC    AFIXTAB,AFIXTAB                                                  
         BNZ   EXIT                                                             
         L     R5,=A(FIXTAB)                                                    
         RELOC (R3)                                                             
         AR    R5,R3                                                            
         ST    R5,AFIXTAB                                                       
*                                                                               
         OPEN  (FIXDS,(INPUT))                                                  
FIX04    DS    0H                                                               
         GET   FIXDS,FIXREC                                                     
         PACK  DUB,FIXREC+7(4)                                                  
         CVB   R1,DUB                                                           
         A     R1,AFIXTAB                                                       
         CLI   0(R1),0                                                          
         BE    FIX06                                                            
         MVC   P(17),=C'DUPLICATE INVOICE'                                      
         MVC   P+18(30),FIXREC                                                  
         GOTO1 REPORT                                                           
         B     FIX04                                                            
FIX06    DS    0H                                                               
         MVC   0(1,R1),FIXREC+6                                                 
         B     FIX04                                                            
*                                                                               
FIXEOF   DS    0H                                                               
         CLOSE FIXDS                                                            
         B     EXIT                                                             
*                                                                               
FIXDS    DCB   DDNAME=FIXDS,                                           X        
               DSORG=PS,                                               X        
               RECFM=FB,                                               X        
               MACRF=GM,                                               X        
               EODAD=FIXEOF                                                     
         SPACE 3                                                                
PPBA02   CSECT                                                                  
***********************************************************************         
***********************************************************************         
*                                                                               
         B     EXIT                                                             
*                                                                               
*        FIRST FOR CLI                                                          
         SPACE 2                                                                
FBILC    DS    0H                                                               
         BC    0,FBILR10                                                        
         OI    *-3,X'F0'           FIRST TIME ONLY                              
*                                                                               
         L     RF,UTL                                                           
         MVC   4(1,RF),SAVSYS      RESTORE SYSTEM NUM                           
         XC    PROGPROB,PROGPROB   'B' PROFILE                                  
         XC    WORK,WORK                                                        
         MVC   WORK(4),=C'PBAB'                                                 
         NI    WORK,X'BF'          MAKE SYS LOWER CASE                          
         MVC   WORK+4(2),QAGENCY                                                
         MVC   WORK+6(1),QMEDIA                                                 
         MVC   WORK+7(3),PCLTKCLT                                               
         CLI   PCLTOFF,C' '                                                     
         BNH   *+14                                                             
         MVI   WORK+10,C'*'                                                     
         MVC   WORK+11(1),PCLTOFF                                               
         GOTO1 GETPROF,DMCB,WORK,PROGPROB,DATAMGR                               
*                                                                               
*                                  OPEN ACCFILE                                 
         CLI   PROGPROB+2,0        TEST ACC SYS OVERRIDE                        
         BE    *+10                                                             
         MVC   ACCTSE,PROGPROB+2                                                
         L     RF,UTL                                                           
         MVC   4(1,RF),ACCTSE                                                   
FBILR2   DS    0H                                                               
         L     R4,PPFILEC          USE AS WORK AREA FOR OPEN                    
         LA    R4,500(R4)                                                       
         GOTO1 DATAMGR,DMCB,=C'OPEN',=C'ACCFIL ',FILELIST,(R4)                  
*                                                                               
         XC    WORK,WORK                                                        
*                                                                               
         MVC   DUB+00(2),RCDATE+6                                               
         MVC   DUB+02(2),RCDATE+0                                               
         MVC   DUB+04(2),RCDATE+3                                               
         GOTO1 DATCON,DMCB,DUB,(2,BTODAYP)                                      
         GOTO1 (RF),(R1),,(5,TODAYD)                                            
         MVC   TODAY,DUB                                                        
*                                                                               
         LA    R4,WORK                                                          
         USING UKRECD,R4                                                        
*                                                                               
         MVC   UKUSRID,RCORIGID                                                 
         OC    PROGPROB+0(2),PROGPROB   TEST ID OVERRIDE                        
         BZ    *+10                                                             
         MVC   UKUSRID,PROGPROB+0                                               
         MVC   UKSYSPRG,=C'PBA'                                                 
*                                                                               
         L     RF,LOGOC                                                         
         USING LOGOD,RF                                                         
         CLI   LOGOJOB+7,C' '      LAST CHAR OF JOB                             
         BE    *+10                                                             
         MVC   UKSUBPRG,LOGOJOB+7  USE AS SUB PRG                               
         DROP  RF                                                               
*                                                                               
         PACK  FULL(2),TODAY+4(3)                                               
         MVC   UKDAY,FULL                                                       
         MVI   UKCLASS,C'P'                                                     
         DROP  R4                                                               
*                                                                               
         MVC   WID,WORK                                                         
         LA    R0,MAIN                                                          
         LA    R1,MAOUT                                                         
         L     R2,GETPROF                                                       
         L     R3,DATAMGR                                                       
         STM   R0,R3,MAPARS                                                     
         MVC   PAGE,=H'1'                                                       
         MVI   FORCEHED,C'Y'                                                    
         RELOC (R3)                                                             
         L     RF,=V(MALINK)                                                    
         AR    RF,R3                                                            
         ST    RF,AMALINK                                                       
         L     RF,=V(MAPOST)                                                    
         AR    RF,R3                                                            
         ST    RF,AMAPOST                                                       
*                                                                               
*                                  SET PERMANENT FIELDS IN MAPOSTD              
         LA    R5,POSTWORK                                                      
         USING MAPOSTD,R5                                                       
         ZAP   MAPRECNT,=P'0'                                                   
         ZAP   MAPCASH,=P'0'                                                    
         L     RF,=A(WRKRBUFF)                                                  
         AR    RF,R3                                                            
         ST    RF,MAPFILE                                                       
         LA    RF,SVPOST           A(SAVE POST AREA)                            
         ST    RF,MAPSVPST                                                      
*                                                                               
         MVC   MAPWRKR,WORKER                                                   
         MVC   MAPRECUP,RECUP                                                   
         MVC   MAPID,WID                                                        
*                                  SEE IF ID ALREADY ON FILE                    
         GOTO1 WORKER,DMCB,=C'INDEX',MAPFILE,WID                                
*                                                                               
         TM    DMCB+8,X'90'        REC NOT FOUND OR EOF                         
         BNZ   FBILR4                                                           
         CLI   QOPT5,C'R'          RERUN OK                                     
         BE    FBILR4                                                           
*                                                                               
FBILR3   DS    0H                                                               
         MVC   P(37),=C'**RE-RUN ATTEMPT - REQUEST BYPASSED**'                  
         MVI   MODE,LBUYREQ                                                     
         GOTO1 REPORT                                                           
         MVI   RERUNSW,C'Y'                                                     
         B     EXIT                                                             
*                                                                               
         DROP  R5                                                               
FBILR4   DS    0H                                                               
         MVI   RERUNSW,C'N'                                                     
         LA    R4,RQAMTS                                                        
         BAS   RE,ZAPSET                                                        
FBILR10  DS    0H                                                               
         CLI   PROGPROF+8,0        CORRECT CD PASS-ALONG OPTION                 
         BNE   *+8                                                              
         MVI   PROGPROF+8,C'Y'      (DEFAULTS TO YES)                           
*                                                                               
         CLI   RERUNSW,C'Y'                                                     
         BE    FBILR3                                                           
         MVC   TESTOPT,QOPT1                                                    
         MVC   LISTOPT,QOPT2                                                    
         MVC   PREVOPT,QOPT3                                                    
         MVC   DMPOPT,QOPT4                                                     
         MVI   RCSUBPRG,0                                                       
         CLI   PREVOPT,C'Y'                                                     
         BNE   FBILR10B                                                         
         ZIC   RF,RCSUBPRG                                                      
         LA    RF,1(RF)                                                         
         STC   RF,RCSUBPRG                                                      
FBILR10B DS    0H                                                               
         CLI   QEND,C' '                                                        
         BNE   *+10                                                             
         MVC   QEND,QSTART                                                      
         GOTO1 DATCON,DMCB,QSTART,(3,BQSTART)                                   
         GOTO1 (RF),(R1),QEND,(3,BQEND)                                         
         XC    BIGNDTE,BIGNDTE     'IGNORE' DATE                                
         CLI   QPROG+52,C' '                                                    
         BE    FBILR11                                                          
         GOTO1 DATCON,DMCB,QPROG+52,(2,BIGNDTE)                                 
*                                                                               
FBILR11  DS    0H                                                               
         B     FBILR20                                                          
*                                                                               
FILELIST DC    CL8'NACCFIL '                                                    
         DC    CL8'NCTFILE'                                                     
         DC    CL10'X'                                                          
         SPACE 3                                                                
FBILR20  DS    0H                                                               
         MVI   FORCEHED,C'Y'                                                    
*                                                                               
         XC    PROGPROA,PROGPROA   'A' PROFILE                                  
         XC    WORK,WORK                                                        
         MVC   WORK(4),=C'PBAA'                                                 
         NI    WORK,X'BF'          MAKE SYS LOWER CASE FOR PAGE A               
         MVC   WORK+4(2),QAGENCY                                                
         MVC   WORK+6(1),QMEDIA                                                 
         MVC   WORK+7(3),PCLTKCLT                                               
         CLI   PCLTOFF,C' '                                                     
         BNH   *+14                                                             
         MVI   WORK+10,C'*'                                                     
         MVC   WORK+11(1),PCLTOFF                                               
         GOTO1 GETPROF,DMCB,WORK,PROGPROA,DATAMGR                               
*                                                                               
         MVC   MAIN,SPACES                                                      
         MVC   MAUTL,UTL           PASS UTL AND SE'S ALONG TO MALINK            
         MVC   MASAVSE,SAVSYS                                                   
         MVC   MAACCSE,ACCTSE                                                   
         MVI   MASYS,C'P'                                                       
         MVC   MAAGY(3),QAGENCY                                                 
         MVC   MACLT,PCLTKCLT                                                   
         MVC   MAOFF,PCLTOFF                                                    
         OI    MAOFF,C' '                                                       
         MVC   MAPROA,PROGPROA                                                  
*                                                                               
         MVI   CMNOCDSW,C'N'       CLEAR COMM BILL AND NO CD SWITCH             
         MVI   DORMCDSW,C'N'       SET SPECIAL DOREMUS SWITCH                   
         CLC   =C'DM',QAGENCY      TO FIX LESS CD BILLS                         
         BNE   FBILC2D                                                          
         CLI   PROGPROF+8,C'Y'     TEST CD PASSALONG                            
         BNE   FBILC2B             NO- SET SPECIAL                              
         CLI   PROGPROA+3,C'Y'     OR CD REBATE                                 
         BNE   FBILC2D             NO - NO SPECIAL                              
*                                                                               
FBILC2B  DS    0H                                                               
         MVI   DORMCDSW,C'Y'                                                    
*                                                                               
FBILC2D  DS    0H                  CHECK CLIENT STATUS                          
         GOTO1 AMALINK,MAPARS                                                   
         TM    MASTAT,X'80'                                                     
         BZ    FBILC4                                                           
*                                  NO POSTINGS FOR AGY                          
         MVC   P(80),QRECORD                                                    
         MVC   PSECOND(34),=C'**AGENCY NOT SET UP FOR POSTINGS**'               
         BAS   RE,PRNT                                                          
FBILC3   DS    0H                                                               
         MVI   MODE,LBUYREQ                                                     
         B     EXIT                                                             
FBILC4   DS    0H                                                               
         TM    MASTAT,X'40'                                                     
         BZ    FBILC6                                                           
*                                  NO POSTINGS FOR CLIENT                       
         CLI   LISTOPT,C'Y'        TEST LISTING ALL                             
         BE    FBILC6              YES                                          
         CLC   QCLIENT,=C'ALL'     ELSE BYPASS CLIENT                           
         BE    FBILC5                                                           
         CLI   QCLIENT,C'*'                                                     
         BE    FBILC5                                                           
*                                  UNLESS 1 CLT REQ                             
         MVC   P(80),QRECORD                                                    
         MVC   PSECOND(31),=C'**NO POSTINGS FOR THIS CLIENT**'                  
         BAS   RE,PRNT                                                          
         B     FBILC3                                                           
FBILC5   DS    0H                                                               
         MVI   MODE,LBUYCLI        SKIP INACTIVE CLIENT                         
         B     EXIT                                                             
FBILC6   DS    0H                                                               
         LA    R4,CAMTS                                                         
         BAS   RE,ZAPSET                                                        
         B     EXIT                                                             
         SPACE 3                                                                
*        FIRST FOR PRODUCT                                                      
         SPACE 2                                                                
FBILP    DS    0H                                                               
         LA    R4,PAMTS                                                         
         BAS   RE,ZAPSET                                                        
         LA    RE,SVPOST           CLEAR SAVE POST AREA                         
         LA    RF,L'SVPOST                                                      
         XCEF                                                                   
         B     EXIT                                                             
         EJECT                                                                  
*        LAST FOR PRODUCT                                                       
         SPACE 2                                                                
LBILP    DS    0H                                                               
*                                                                               
         LA    R4,PAMTS                                                         
         BAS   RE,CHKSET                                                        
         CLI   FULL,1                                                           
         BNE   EXIT                                                             
         LA    R3,PAMTS                                                         
         LA    R4,CAMTS                                                         
         BAS   RE,PPROLL                                                        
*                                                                               
         BAS   RE,PRNT                                                          
         MVC   P+10(20),=C'** PRODUCT TOTALS **'                                
         LA    R3,PAMTS                                                         
         BAS   RE,TOTPRNT                                                       
         LA    R4,PAMTS                                                         
         BAS   RE,ZAPSET                                                        
         B     EXIT                                                             
         SPACE 3                                                                
*        LAST  FOR CLIENT                                                       
         SPACE 2                                                                
LBILC    DS    0H                                                               
         LA    R4,CAMTS                                                         
         BAS   RE,CHKSET                                                        
         CLI   FULL,1                                                           
         BNE   EXIT                                                             
         LA    R3,CAMTS                                                         
         LA    R4,RQAMTS                                                        
         BAS   RE,PPROLL                                                        
*                                                                               
         BAS   RE,PRNT                                                          
         MVC   P+10(19),=C'** CLIENT TOTALS **'                                 
         LA    R3,CAMTS                                                         
         BAS   RE,TOTPRNT                                                       
*                                                                               
         LA    R4,CAMTS                                                         
         BAS   RE,ZAPSET                                                        
         B     EXIT                                                             
         SPACE 3                                                                
*        LAST FOR REQ                                                           
REQL     DS    0H                                                               
         CLC   QCLIENT,=C'ALL'                                                  
         BE    *+12                                                             
         CLI   QCLIENT,C'*'                                                     
         BNE   *+10                                                             
         XC    PCLTREC(100),PCLTREC                                             
         LA    R4,RQAMTS                                                        
         BAS   RE,CHKSET                                                        
         CLI   FULL,1                                                           
         BNE   EXIT                                                             
         LA    R3,RQAMTS                                                        
         LA    R4,RAMTS                                                         
         BAS   RE,PPROLL                                                        
*                                                                               
         MVI   FORCEHED,C'Y'                                                    
         MVC   P+10(20),=C'** REQUEST TOTALS **'                                
         LA    R3,RQAMTS                                                        
         BAS   RE,TOTPRNT                                                       
*                                                                               
         LA    R4,RQAMTS                                                        
         BAS   RE,ZAPSET                                                        
         B     EXIT                                                             
         SPACE 3                                                                
*        LAST FOR RUN                                                           
         SPACE 2                                                                
RUNL     DS    0H                                                               
*                                                                               
         XC    PCLTREC(100),PCLTREC                                             
         MVI   PAGYKMED,C' '                                                    
         MVC   PAGYMED,SPACES                                                   
         MVI   FORCEHED,C'Y'                                                    
         MVC   P+10(19),=C'** REPORT TOTALS **'                                 
         LA    R3,RAMTS                                                         
         BAS   RE,TOTPRNT                                                       
*                                                                               
         LA    R5,POSTWORK                                                      
         USING MAPOSTD,R5                                                       
         CLI   POSTSW,C'Y'         TEST ANY POSTINGS                            
         BNE   RUNL10                                                           
*                                  CLOSE POSTING FILE                           
         GOTO1 AMAPOST,DMCB,(X'FF',POSTWORK)                                    
         CLI   DMPOPT,C'D'          DUMP RECORDS                                
         BNE   RUNL10                                                           
*                                                                               
         MVI   FORCEHED,C'Y'                                                    
         MVC   P(10),=C'WORKER ID='                                             
         GOTO1 HEXOUT,DMCB,WID,P+11,8,=C'N'                                     
         MVC   P+29(8),WID                                                      
         TR    P+29(8),TRTAB                                                    
         BAS   RE,PRNT                                                          
         BAS   RE,PRNT                                                          
         GOTO1 WORKER,DMCB,=C'INDEX',MAPFILE,WID                                
         TM    DMCB+8,X'FF'                                                     
         BZ    *+6                                                              
         DC    H'0'                                                             
RUNL4    DS    0H                                                               
         GOTO1 WORKER,DMCB,=C'READ',MAPFILE,WID,MAOUT                           
         TM    DMCB+8,X'80'                                                     
         BNZ   RUNL10                                                           
         BAS   RE,PRNT                                                          
         LA    R6,MAOUT                                                         
         LH    R2,0(R6)            LENGTH                                       
         LA    R3,0(R6,R2)         EOR                                          
*                                                                               
RUNL6    DS    0H                                                               
         LR    R4,R3                                                            
         SR    R4,R6                                                            
         BNP   RUNL4                                                            
         CH    R4,=H'32'                                                        
         BNH   *+8                                                              
         LA    R4,32                                                            
         XC    WORK,WORK                                                        
         GOTO1 HEXOUT,DMCB,(R6),WORK,(R4),=C'N'                                 
*                                                                               
         MVC   P+01(8),WORK+00                                                  
         MVC   P+10(8),WORK+08                                                  
         MVC   P+19(8),WORK+16                                                  
         MVC   P+28(8),WORK+24                                                  
         MVC   P+40(8),WORK+32                                                  
         MVC   P+49(8),WORK+40                                                  
         MVC   P+58(8),WORK+48                                                  
         MVC   P+67(8),WORK+56                                                  
*                                                                               
         MVC   WORK(32),0(R6)                                                   
         TR    WORK(32),TRTAB                                                   
         BCTR  R4,R0                                                            
         EX    R4,*+8                                                           
         B     *+10                                                             
         MVC   P+79(0),WORK                                                     
         LA    R4,1(R4)                                                         
         BAS   RE,PRNT                                                          
         LA    R6,0(R6,R4)                                                      
         B     RUNL6                                                            
*                                                                               
RUNL10   DS    0H                                                               
*                                                                               
         L     R3,LOGOC                                                         
         USING LOGOD,R3                                                         
         SPACE 1                                                                
         MVI   LOGOTYPE,C'E'                                                    
         GOTO1 LOGO,DMCB,(R3)                                                   
         SPACE 1                                                                
         MVI   LOGOEND,C'X'                                                     
         MVI   LOGOTYPE,C'S'                                                    
         MVC   LOGONAME,=CL33'******** INTERNAL CONTROL *******'                
         MVC   LOGOADD,=CL33'******** DO NOT SEND OUT ******'                   
         MVC   LOGOADD2,SPACES                                                  
         MVC   LOGOADD3,SPACES                                                  
         MVC   LOGO1,=C'CONTROL'                                                
         GOTO1 LOGO,DMCB,(R3)                                                   
         MVI   FORCEHED,C'Y'                                                    
         MVC   PAGE,=H'1'                                                       
         LA    R5,POSTWORK                                                      
         USING MAPOSTD,R5                                                       
         EDIT  (P6,MAPCASH),(12,P+40),2,MINUS=YES                               
         MVC   P+25(13),=C'TOTAL POSTING'                                       
         MVI   RCSUBPRG,10                                                      
         GOTO1 REPORT                                                           
         B     EXIT                                                             
         SPACE 3                                                                
TRTAB    DC    X'4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B'     00-0F                    
         DC    X'4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B'     10-1F                    
         DC    X'4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B'     20-2F                    
         DC    X'4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B'     30-3F                    
         DC    X'4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B'     40-4F                    
         DC    X'4B4B4B4B4B4B4B4B4B4B4B5B5C5D4B4B'     50-5F                    
         DC    X'60614B4B4B4B4B4B4B4B4B6B6C6D4B6F'     60-6F                    
         DC    X'4B4B4B4B4B4B4B4B4B4B4B7B4B7D7E4B'     70-7F                    
         DC    X'4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B'     80-8F                    
         DC    X'4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B'     90-9F                    
         DC    X'4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B'     A0-AF                    
         DC    X'4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B'     B0-BF                    
         DC    X'4BC1C2C3C4C5C6C7C8C94B4B4B4B4B4B'     C0-CF                    
         DC    X'4BD1D2D3D4D5D6D7D8D94B4B4B4B4B4B'     D0-DF                    
         DC    X'4B4BE2E3E4E5E6E7E8E94B4B4B4B4B4B'     E0-EF                    
         DC    X'F0F1F2F3F4F5F6F7F8F94B4B4B4B4B4B'     F0-FF                    
*                                                                               
         EJECT                                                                  
*        PROCESS BILL                                                           
         SPACE 2                                                                
PRBL     DS    0H                                                               
*                                                                               
*                                  **NO-OP**                                    
*        CLC   PBILLDAT,TODAY      SKIP TODAY'S BILLS                           
*        BNL   PB99                                                             
*                                                                               
*                                                                               
         CLI   PBILLMOD,C'S'       BYPASS EST BILLS IN SERIES BILLING           
         BE    PB99                                                             
         CLI   PBILLMOD,C'P'       BYPASS EST BILLS IN PRD BILLING              
         BE    PB99                                                             
         CLI   PROGPROF+4,C'I'                                                  
         BE    PB3                                                              
         CLC   PBILLDAT,QSTART     FILTER ON BILL RUN DATE                      
         BL    PB99                                                             
         CLC   PBILLDAT,QEND                                                    
         BH    PB99                                                             
         B     PB3B                                                             
*                                                                               
PB3      DS    0H                                                               
         CLC   PBILINVD,BQSTART    FILTER ON INVOICE DATE                       
         BL    PB99                                                             
         CLC   PBILINVD,BQEND                                                   
         BH    PB99                                                             
*                                                                               
PB3B     DS    0H                                                               
         MVI   MAAORSW,C'N'        RESET AOR SWITCH HERE                        
         CLI   QOPT5+1,C'R'        TEST TO RE-POST ALL                          
         BE    *+14                YES                                          
         CLC   PBILPOST,BIGNDTE    ELSE, IF POSTED ON IGNORE DATE               
         BNE   *+10                                                             
         XC    PBILPOST,PBILPOST    TREAT AS UN-POSTRED                         
         OC    PBILPOST,PBILPOST   TEST PREVIOUSLY POSTED                       
         BNZ   PB6                  YES                                         
         CLI   PREVOPT,C'Y'        ONLY PREVIOUSLY POSTED                       
         BE    PB99                                                             
*                                                                               
         CLI   DORMCDSW,C'Y'       FOR SPECIAL DOREMUS                          
         BNE   PB3F                                                             
         CLI   PBILSEP,C'C'        IGNORE SEPARATE CD BILLS                     
         BE    PB99                                                             
*                                                                               
PB3F     DS    0H                                                               
         L     RF,UTL              RESTORE SYSTEM NUMBER                        
         MVC   4(1,RF),SAVSYS                                                   
         MVC   KEY(25),PBILLREC    RE-READ DIRECTORY TO GET DA                  
         GOTO1 READ                                                             
         MVC   SAVDA,KEY+27                                                     
         L     RF,UTL              RESET TO ACCT SYS ID                         
         MVC   4(1,RF),ACCTSE                                                   
*                                  GET EST HEADER                               
         MVC   WORK(12),PBILLREC                                                
         MVI   WORK+3,7                                                         
         CLC   PESTREC(12),WORK        TEST HAVE EST HDR                        
         BE    PB4                 YES                                          
         MVC   WORK(64),KEY        SAVE KEY FOR SEQ                             
         XC    KEY,KEY                                                          
         MVC   KEY(12),PBILLREC                                                 
         MVI   KEY+3,7                                                          
         L     RF,UTL              RESTORE SYSTEM NUMBER                        
         MVC   4(1,RF),SAVSYS                                                   
         GOTO1 READ                                                             
         GOTO1 GETEST                                                           
         MVC   KEY,WORK            RESTORE                                      
         GOTO1 HIGH                                                             
         L     RF,UTL              RESET TO ACCT SYS ID                         
         MVC   4(1,RF),ACCTSE                                                   
*                                                                               
PB4      DS    0H                  GET JOB AND OPEN RATES                       
         MVI   SVORBSW,C' '        CLEAR REBATE/SUSPENSE SWITCH                 
         MVC   SVJOB,SPACES                                                     
         XC    SVOPREB(8),SVOPREB                                               
         LA    R2,PBILLEL                                                       
         MVI   ELCODE,X'09'                                                     
         BAS   RE,NEXTEL                                                        
         BNE   PB4D                                                             
         USING PBILOTH,R2                                                       
         MVC   SVJOB,PBILLJOB     JOB                                           
         OC    SVJOB,SPACES                                                     
         CLI   SVJOB,X'FF'         CHANGE FF (JOB=NONE)                         
         BNE   *+10                                                             
         MVC   SVJOB,SPACES        TO SPACES                                    
*                                                                               
         CLI   PBILLTYP,C'R'       TEST A REBATE BILL                           
         BNE   PB4C                NO                                           
         MVI   SVORBSW,C'R'        SET REBATE SWITCH                            
         ZAP   DUB,PBILLCNR        YES- PBILLCNR IS REBATE AMOUNT               
         CVB   R0,DUB                                                           
         ST    R0,SVOPREB                                                       
         B     PB4D                                                             
*                                                                               
PB4C     DS    0H                                                               
         MVI   MAFINAN,C' '                                                     
         TM    PBILLIND,X'01'      TEST FINANCIAL BILL                          
         BZ    PB4D                NO- NO SUSPENSE                              
         MVI   MAFINAN,C'Y'                                                     
*                                                                               
         MVI   SVORBSW,C'S'        SET SUSPENSE SWITCH                          
         ZAP   DUB,PBILLCNR       CONTRACT RATE ACTUAL                          
         CLI   DORMCDSW,C'Y'       **TEST DOREMUS CD SPECIAL**                  
         BNE   PB4C8                                                            
         CLI   PBILCDSW,C'S'       UNLESS CD ON SEP BILL                        
         BE    PB4C8                                                            
         TM    PBILBASA,X'04'      TEST LESS CD                                 
         BZ    PB4C8                                                            
         AP    DUB,PBILLGRS        YES - RE-ADD CD                              
         SP    DUB,PBILLBIL                                                     
*                                                                               
PB4C8    DS    0H                                                               
         CVB   R0,DUB                                                           
         ST    R0,SVCNACT                                                       
         ZAP   DUB,PBILLRCV       TRUE ACTUAL (OPEN)                            
         SP    DUB,PBILLCNR       LESS CONTRACT RATE 'ACTUAL'                   
         CVB   R0,DUB                                                           
         ST    R0,SVOPREB         = OPEN RATE REBATE                            
*                                                                               
PB4D     DS    0H                                                               
***********************************************************************         
*  RETAIL ACCOUNT FIX                                                           
***********************************************************************         
         SPACE 1                                                                
         SR    R5,R5                                                            
         ICM   R5,3,PBILKBNO                                                    
         A     R5,AFIXTAB                                                       
         MVC   PBRACCT+10(1),0(R5)                                              
         CLI   0(R5),0                                                          
         BNE   PB4F                                                             
         MVC   P(23),=C'INV NOT IN TABLE - NNNN'                                
         EDIT  (B2,PBILKBNO),(4,P+19)                                           
         GOTO1 REPORT                                                           
PB4F     DS    0H                                                               
***********************************************************************         
***********************************************************************         
         MVC   MAJOB,SVJOB                                                      
         MVC   MAPRD,PBILKPRD                                                   
         MVC   MAOTHER(3),PESTGRPS                                              
         MVC   MABLTYP,PBILLMOD    BILL TYPE 4,5,6,7                            
         MVC   MARETYP,PBRETAIL    SET RETAIL TYPE                              
         MVC   MARET,PBRACCT       AND ACCOUNT                                  
*                                                                               
PB5      DS    0H                  TEST AOR STATUS                              
         MVI   MAAORSW,C'N'                                                     
         OC    PBILPOST,PBILPOST   SKIP IF ALREADY POSTED                       
         BNZ   PB6                                                              
         TM    PBILCMSW,X'20'      DO LOOKUP FOR ALL TRUE AORS                  
         BNZ   PB5A04                                                           
         CLI   PROGPROA+7,1        FOR AOR SPECIAL OPTION 1                     
         BNE   PB5M                                                             
         TM    PBILCMSW,X'10'      DO FOR CLIENT AOR BILLS ALSO                 
         BZ    PB5M                                                             
*                                  READ FOR AOR DATA                            
PB5A04   DS    0H                                                               
         XC    WORK,WORK                                                        
         LA    R7,WORK                                                          
         USING AORKEY,R7                                                        
         MVC   AORKAGY(10),PBILKAGY   A/M/RCOD/CLT/PRD                          
         MVI   AORKRCD,X'14'                                                    
         MVC   AORKEST(2),PBILKEST                                              
         CLC   WORK(25),SVAORKEY   TEST ALREADY HAVE RECORD                     
         BE    PB5D                                                             
*                                                                               
         MVC   HOLDKEY,KEY                                                      
         MVC   KEY,WORK                                                         
         LA    R7,KEY                                                           
         L     RF,UTL              RESTORE SYSTEM CODE                          
         MVC   4(1,RF),SAVSYS                                                   
         GOTO1 HIGH                                                             
         CLC   KEY(12),KEYSAVE                                                  
         BE    PB5A20                                                           
         MVC   KEYSAVE+10(2),=X'FFFF' SET TO GET DEFAULT                        
         CLC   KEY(12),KEYSAVE        DID WE ALREADY GET IT                     
         BE    PB5A20                                                           
         MVC   KEY,KEYSAVE                                                      
         GOTO1 HIGH                                                             
         CLC   KEY(12),KEYSAVE                                                  
         BNE   PB5A21                                                           
*                                                                               
PB5A20   DS    0H                                                               
         B     PB5A22                                                           
*                                                                               
PB5A21   DS    0H                                                               
         MVI   MAAORSW,C'E'        AOR ERROR                                    
         MVC   KEY,HOLDKEY         RESTORE SEQ                                  
         GOTO1 HIGH                                                             
         L     RF,UTL              RESTORE ACC SYS CODE                         
         MVC   4(1,RF),ACCTSE                                                   
         B     PB5F                                                             
*                                                                               
PB5A22   DS    0H                                                               
         LA    R7,PBUYREC                                                       
         ST    R7,AREC                                                          
         GOTO1 GETPRT                                                           
         MVC   SVAORKEY,KEY                                                     
*                                                                               
         LA    R2,AORELS                                                        
         CLI   0(R2),X'03'                                                      
         BE    PB5B                                                             
         MVI   ELCODE,X'03'                                                     
         BAS   RE,NEXTEL                                                        
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
PB5B     DS    0H                                                               
         USING AORELEM,R2                                                       
         CLC   AORRCVBL,SPACES     ACCT MUST BE THERE                           
         BNH   PB5A21                                                           
         MVC   MAAORRCV,AORRCVBL                                                
         MVC   MAAORCOM,AORCOMM                                                 
*                                                                               
         MVC   KEY,HOLDKEY         RESTORE SEQ                                  
         GOTO1 HIGH                                                             
         L     RF,UTL              RESTORE ACC SYS CODE                         
         MVC   4(1,RF),ACCTSE                                                   
*                                                                               
PB5D     DS    0H                                                               
         MVI   MAAORSW,C'Y'        TRUE AOR BILL                                
         TM    PBILCMSW,X'20'                                                   
         BNZ   PB5F2                                                            
         MVI   MAAORSW,C'C'        ELSE CLIENT AOR BILL                         
*                                                                               
PB5F     DS    0H                                                               
         TM    PBILCMSW,X'20'      FOR TRURE AORS, LOOK FOR ORIGINAL            
         BZ    PB5M                                                             
*                                                                               
PB5F2    DS    0H                                                               
         LA    R5,SVPOST           LOOK AT SAVED POST AREA                      
         USING MAPOSTD,R5          MED/CLT/PRD WILL BE OK                       
         CLI   MAPAOR,C'Y'         PREVIOUS CANNOT ALSO BE AOR                  
         BE    PB5G                                                             
         SR    RF,RF                                                            
         ICM   RF,3,PBILKEST                                                    
         CVD   RF,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  WORK(3),DUB                                                      
         CLC   MAPEST,WORK         EST MUST AGREE                               
         BNE   PB5G                                                             
         GOTO1 DATCON,DMCB,(3,PBILINVD),WORK                                    
         CLC   MAPDATE,WORK        AND DATE                                     
         BNE   PB5G                                                             
         GOTO1 DATCON,DMCB,(3,PBILKMOS),WORK                                    
         CLC   MAPSERV,WORK        MEDIA MONTH OF SERVICE                       
         BNE   PB5G                                                             
         B     PB5H                                                             
         DROP  R5                                                               
*                                                                               
PB5G     DS    0H                                                               
         DC    H'0'                MISSING AOR ORIGINAL                         
*                                                                               
PB5H     DS    0H                                                               
         ZAP   DUB,PBILLRCV        FOR AOR BILLS, AC= ACTUAL                    
         CVB   R0,DUB              (BUT NOTE- MAPAC WILL BE DIFFERENT)          
         ST    R0,BAC                                                           
         ST    R0,BACT                                                          
         ST    R0,BARAMT                                                        
         XC    BNT,BNT             NO NET,GROSS, OR CD                          
         XC    BGRS,BGRS                                                        
         XC    BCD,BCD                                                          
*                                                                               
         DROP  R7                                                               
         DROP  R2                                                               
*                                                                               
PB5M     DS    0H                                                               
         GOTO1 AMALINK,MAPARS      GET ACCT DATA                                
         CLI   MASTAT,0            TEST OK TO POST                              
         BE    PB6                 YES                                          
         CLI   LISTOPT,C'Y'        NO - TEST LISTING NON-POSTS                  
         BNE   PB98                NO                                           
*                                                                               
PB6      DS    0H                  SET BILL AMOUNTS                             
         CLI   MAAORSW,C'Y'        IF AOR, SKIP FURTHER CALCS                   
         BE    PB30                                                             
         ZAP   DUB,PBILLGRS                                                     
         CVB   R0,DUB                                                           
         ST    R0,BGRS                                                          
         ZAP   DUB,PBILLBIL                                                     
         CVB   R1,DUB                                                           
         SR    R0,R1                                                            
         ST    R0,BCD              CD                                           
         ZAP   DUB,PBILLRCV                                                     
         CLI   DORMCDSW,C'Y'       **TEST DOREMUS CD SPECIAL**                  
         BNE   PB601                                                            
         CLI   PBILCDSW,C'S'       UNLESS CD ON SEP BILL                        
         BE    PB601                                                            
         TM    PBILBASA,X'04'      TEST LESS CD                                 
         BZ    PB601                                                            
         AP    DUB,PBILLGRS        YES - RE-ADD CD                              
         SP    DUB,PBILLBIL                                                     
*                                                                               
PB601    DS    0H                                                               
         CVB   R0,DUB                                                           
         ST    R0,BACT             ACTUAL                                       
*                                                                               
         CLI   SVORBSW,C'R'        FOR OPEN REBATE                              
         BNE   PB6A                                                             
         L     R0,SVOPREB          USE REBATE AMOUNT                            
         ST    R0,BARAMT           AS AR AMT                                    
         XC    BNT,BNT             CLEAR NET                                    
         XC    BAC,BAC             CLEAR AC                                     
         B     PB6D                                                             
*                                                                               
PB6A     DS    0H                                                               
         CLI   DORMCDSW,C'Y'       **IF DOREMUS SPECIAL**                       
         BE    *+12                SKIP LESS CD TEST                            
         TM    PBILBASA,X'04'      IF BASE NOT 'LESS CD'                        
         BNZ   PB6B                                                             
         SP    DUB,PBILLGRS        SUBTRACT CD OUT NOW                          
         AP    DUB,PBILLBIL                                                     
         CVB   R0,DUB                                                           
PB6B     DS    0H                                                               
         ST    R0,BARAMT           AR AMOUNT                                    
*                                                                               
         CLI   SVORBSW,C'S'        FOR OPEN SUSPENSE                            
         BNE   PB6C                                                             
         L     R0,SVCNACT          USE CONTRACT RATE ACTUAL                     
         CVD   R0,DUB              FOR AC CALC                                  
         CLI   DORMCDSW,C'Y'       **IF DOREMUS SPECIAL**                       
         BE    *+12                SKIP LESS CD TEST                            
         TM    PBILBASA,X'04'      IF BASE NOT 'LESS CD'                        
         BNZ   PB6C                                                             
         SP    DUB,PBILLGRS        SUBTRACT CD OUT NOW                          
         AP    DUB,PBILLBIL                                                     
         CVB   R0,DUB                                                           
*                                                                               
PB6C     DS    0H                                                               
         CLI   PBILSEP,C'C'        IF SEPARATE CD INV                           
         BE    *+10                AC = CD                                      
         SP    DUB,PBILLNET                                                     
         CVB   R0,DUB                                                           
         ST    R0,BAC                                                           
         ZAP   DUB,PBILLNET        NET LESS CD                                  
         CVB   R0,DUB                                                           
         ST    R0,BNT                                                           
*                                                                               
*******  CLI   PBILCMSW,C'C'       TEST COMMISSION ONLY BILL                    
*******  BNE   PB6D                                                             
         TM    PBILCMSW,X'02'      TEST COMMISSION ONLY BILL                    
         BNO   PB6D                                                             
         XC    BNT,BNT             NO NET                                       
         MVC   BAC,BARAMT          COMM = AR AMNOUT                             
         CLI   PROGPROA+6,C'N'     TEST TO SUPPRESS COMM CD                     
         BNE   PB6D                                                             
         MVI   CMNOCDSW,C'Y'       SET COMM BILL/NO CD SW                       
         L     R0,BACT                                                          
         TM    PBILBASA,X'04'      TEST CD SUBTRACTED                           
         BZ    *+8                 NO, OK                                       
         A     R0,BCD              YES, ADD IT BACK IN                          
         ST    R0,BAC              FORCE COMMISSION                             
         ST    R0,BACT             ACTUAL,                                      
         ST    R0,BARAMT           AND AR AMT ALL EQUAL                         
*                                                                               
PB6D     DS    0H                                                               
         MVI   RETAIL,C'N'                                                      
         CLI   PBRETAIL,0                                                       
         BE    PB30                                                             
*                                  RETAIL BILLS                                 
         CLI   PBRETAIL,X'81'       SKIP ALL CORP CONTROL                       
         BE    PB99                                                             
         OC    BAMTS,BAMTS         SKIP ZERO BILLS                              
         BZ    PB99                                                             
*                                                                               
*                             BUILD PRINT LINE                                  
PB30     DS    0H                                                               
         MVI   ARASW,C'Y'          SET ARASW- USE NORMAL A/R                    
         CLC   =C'DM',QAGENCY       SPECIAL FOR DOREMUS                         
         BE    PB30C                                                            
         CLC   =C'SJ',QAGENCY       AND SJR                                     
         BNE   PB31                                                             
PB30C    DS    0H                                                               
         CLI   PROGPROF+8,C'Y'     IF NO CD PASSALONG                           
         BNE   PB30D                                                            
         CLI   PROGPROA+3,C'Y'     OR IF CD REBATE                              
         BNE   PB31                                                             
*                                                                               
PB30D    DS    0H                                                               
         MVI   ARASW,C'N'          SET TO USE ACTUAL, NOT A/R                   
*                                  (GROSS, NOT GLCD)                            
PB31     DS    0H                                                               
         LA    R7,P                                                             
         USING BLINED,R7                                                        
*                                                                               
         MVC   BLPRD,PBILKPRD      PRD                                          
*                                                                               
         MVC   HALF,PBILKEST       EST                                          
         LH    R0,HALF                                                          
         CVD   R0,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  BLEST,DUB                                                        
*                                                                               
         GOTO1 DATCON,DMCB,(3,PBILKMOS),(6,BLPER)                               
*                                                                               
         GOTO1 (RF),(R1),(3,PBILINVD),(5,BLINVD)                                
*                                                                               
         MVC   DINVNO(2),PBILLDAT+2                                             
         CLI   B1XPROF+4,0         TEST ANY SPECIAL BASE YEAR                   
         BE    PR32D                                                            
         PACK  DUB,PBILLDAT(2)     YEAR OF BILL                                 
         CVB   R0,DUB                                                           
         ZIC   RF,B1XPROF+4        INVOICE BASE YEAR                            
         SR    R0,RF               DIFFERENCE                                   
         BNP   PR32D                                                            
         MH    R0,=H'12'                                                        
         PACK  DUB,PBILLDAT+2(2)   MONTH                                        
         CVB   RF,DUB                                                           
         AR    R0,RF                                                            
*                                                                               
PR32C    DS    0H                                                               
         CVD   R0,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  DINVNO(2),DUB       SET NEW INVOICE 'MONTH'                      
         B     PR32K                                                            
*                                                                               
PR32D    DS    0H                                                               
         CLI   B1XPROF+5,0         SEE IF BUMPING INV MONTH                     
         BE    PR32K                                                            
         PACK  DUB,PBILLDAT+2(2)   MONTH                                        
         CVB   RF,DUB                                                           
         ZIC   R0,B1XPROF+5                                                     
         AR    R0,RF                                                            
         CH    R0,=H'12'                                                        
         BNH   PR32C                                                            
         SH    R0,=H'12'                                                        
         B     PR32C                                                            
*                                                                               
PR32K    DS    0H                                                               
         MVC   HALF,PBILKBNO                                                    
         LH    R0,HALF                                                          
         CVD   R0,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  DINVNO+2(4),DUB                                                  
*                                                                               
         MVC   BLINVNO(2),DINVNO                                                
         MVI   BLINVNO+2,C'-'                                                   
         MVC   BLINVNO+3(4),DINVNO+2                                            
*                                                                               
         L     R0,BACT             ACTUAL                                       
         LA    R6,BLACTUAL                                                      
         BAS   RE,PBEDT                                                         
*                                                                               
         L     R0,BNT              NET LESS CD                                  
         LA    R6,BLNET                                                         
         BAS   RE,PBEDT                                                         
*                                                                               
         L     R0,BAC              AGY COMM                                     
         LA    R6,BLAC                                                          
         BAS   RE,PBEDT                                                         
*                                                                               
         L     R0,BCD              CASH DISC                                    
         LA    R6,BLCD                                                          
         BAS   RE,PBEDT                                                         
*                                                                               
         L     R0,BARAMT                                                        
         CLI   ARASW,C'Y'          TEST WHAT TO PRINT AS A/R                    
         BE    *+8                                                              
         L     R0,BACT                                                          
         LA    R6,BLARAMT                                                       
         BAS   RE,PBEDT                                                         
*                                  SET SPECIAL BILL TYPE                        
         CLI   SVORBSW,C'R'        OPEN RATE REBATE                             
         BNE   *+12                                                             
         MVI   BLINVNO+7,C'R'                                                   
         B     PB33                                                             
*                                                                               
         CLI   MAAORSW,C'Y'        AOR BILL                                     
         BNE   *+12                                                             
         MVI   BLINVNO+7,C'A'                                                   
         B     PB33                                                             
*                                                                               
PB33     DS    0H                                                               
         LA    R6,MAOUT                                                         
         USING MALINKD,R6                                                       
         OC    PBILPOST,PBILPOST        TEST PREVIOUSLY POSTED                  
         BZ    PB34                     NO                                      
*                                       YES-SHOW DATE                           
         GOTO1 DATCON,DMCB,(2,PBILPOST),(5,BLPOST)                              
         B     PB34B8                                                           
*                                       NOT PREV POSTED                         
PB34     DS    0H                                                               
         CLI   MAAORSW,C'E'        AOR ERROR                                    
         BNE   *+14                                                             
         MVC   BLPOST(20),=C'**MISSING AOR INFO**'                              
         B     PB34B8                                                           
*                                                                               
         L     RF,MALRPTR          POINTER TO ACTUAL RECEIVABLE ACCT            
         CLC   0(15,RF),SPACES       CAN I POST NOW                             
         BNE   PB34A2                YES                                        
*                                                                               
         MVC   BLPOST(2),=C'**'                                                 
         MVC   BLPOST+2(L'BLPOST-5),MALKYERR   SET ERROR KEY                    
         LA    RF,BLPOST+L'BLPOST-3                                             
         MVI   0(RF),C'='                                                       
         LA    RF,1(RF)                                                         
         ST    RF,DMCB+4                                                        
         GOTO1 HEXOUT,DMCB,MALNOPST,,1,=C'N'                                    
         B     PB34B8                                                           
*                                                                               
PB34A2   DS    0H                                                               
         CLI   PROGPROA+0,C'Y'     TEST PRODUCTION POSTING                      
         BNE   PB34A4                                                           
         MVC   BLPOST(6),3(RF)     SHOW AS 6/6                                  
         MVC   BLPOST+132(6),3+6(RF)                                            
         B     PB34B                                                            
*                                                                               
PB34A4   DS    0H                                                               
         MVC   BLPOST(7),3(RF)                                                  
         MVC   BLPOST+132(5),3+7(RF)      IF LONGER THAN 7                      
         CLI   RETAIL,C'Y'         AND RETAIL                                   
         BNE   PB34B                                                            
         CLI   RETPROF+12,C'0'     (OPT TO TREAT AS NOT RETAIL)                 
         BE    PB34B                                                            
         MVC   BLPOST(7),SPACES                                                 
         MVC   BLPOST(4),3(RF)     RETAIL AS 4/8                                
         MVC   BLPOST+132(8),3+4(RF)                                            
         MVI   BLPOST-2,C'R'                                                    
*                                                                               
PB34B    DS    0H                                                               
         CLI   MAAORSW,C'Y'        TEST AOR                                     
         BNE   PB34B4                                                           
         MVI   BLPOST-2,C'A'       FLAG AS AOR                                  
         CLC   =C'SR',1(RF)        OK IF RECEIVALBE                             
         BE    PB34B4                                                           
         MVC   BLPOST(8),1(RF)     ELSE SHOW FULL ACCOUNT                       
         MVC   BLPOST+132(6),9(RF)                                              
*                                                                               
PB34B4   DS    0H                                                               
         MVC   BLPOST+9(7),INCCNTR+3                                            
         MVC   BLPOST+18(7),COSTCLT+3                                           
*                                                                               
*                                  DO POSTING                                   
*                                                                               
PB34B8   DS    0H                  SET MAPOSTD FIELDS                           
         LA    R5,POSTWORK                                                      
         USING MAPOSTD,R5                                                       
         CLI   DORMCDSW,C'Y'       **IF DOREMUS SPECIAL**                       
         BE    PB34BD              SKIP LESS CD TEST                            
         MVI   MAPCDSW,C'Y'        SET DUE IS LESS CD.                          
         TM    PBILBASA,X'04'                                                   
         BNZ   *+8                                                              
*                                                                               
PB34BD   DS    0H                                                               
         MVI   MAPCDSW,C'N'                                                     
*                                                                               
         MVC   MAPAC,BAC                                                        
         CLI   MAAORSW,C'Y'        FOR AOR BILLS                                
         BNE   PB34C                                                            
         ZAP   DUB,PBILLACM        'AC' IS FROM ORIGINAL BILL                   
         CVB   R0,DUB                                                           
         LCR   R0,R0               COMPLEMENT                                   
         ST    R0,MAPAC                                                         
*                                                                               
PB34C    DS    0H                                                               
         MVC   MAPCD,BCD                                                        
         MVC   MAPARAMT,BARAMT                                                  
         MVC   MAPNET,BNT                                                       
         MVC   MAPGRS,BGRS                                                      
         MVC   MAPACT,BACT                                                      
         MVC   MAPINV,DINVNO                                                    
         MVC   MAPOFF,PCLTOFF                                                   
         CLI   PROGPROF+9,C'A'     TEST OVERRIDE OFF. NO.                       
         BL    *+10                (A-Z,1-9)                                    
         MVC   MAPOFF,PROGPROF+9                                                
         GOTO1 DATCON,DMCB,(3,PBILINVD),MAPDATE                                 
         MVC   MAPMOS,MAPDATE      MOS = BILL MONTH                             
*                                                                               
         CLI   PROGPROF+2,0        TEST ANY ADJUSTMENT                          
         BE    PB35                NO                                           
*                                                                               
         L     R4,=F'-30'          IF PROFILE 15 OR LESS                        
         CLI   PROGPROF+2,15       BILLS BEFORE THAT DATE                       
         BH    PB34D               GO TO PREVIOUS MONTH                         
         CLC   PBILINVD+2(1),PROGPROF+2                                         
         BH    PB35                                                             
         B     PB34F                                                            
*                                                                               
PB34D    DS    0H                                                               
         LA    R4,30                      IF PROFILE 16 OR MORE                 
         CLC   PBILINVD+2(1),PROGPROF+2   BILLS AFTER THAT DATE                 
         BNH   PB35                       GO TO NEXT MONTH                      
*                                                                               
PB34F    DS    0H                                                               
         MVC   WORK(4),MAPDATE      ADJUST MONTH                                
         MVC   WORK+4(2),=C'15'                                                 
         GOTO1 ADDAY,DMCB,WORK,WORK,(R4)                                        
         MVC   MAPMOS,WORK                                                      
*                                                                               
PB35     DS    0H                                                               
         GOTO1 DATCON,DMCB,(3,PBILKMOS),WORK                                    
         MVC   MAPSERV,WORK        MEDIA MONTH OF SERVICE                       
*                                                                               
         CLI   PROGPROA+4,C'Y'      TEST MOS CHANGE                             
         BNE   PB35D                                                            
         CLC   MAPSERV,MAPMOS      FOR FUTURE MONTHS                            
         BNH   PB35D                                                            
         MVC   MAPMOS,MAPSERV                                                   
*                                                                               
PB35D    DS    0H                                                               
         MVC   MAPMED,QMEDIA                                                    
         MVC   MAPPROF,PROGPROF                                                 
         MVC   MAPPROA,PROGPROA                                                 
         MVC   MAPEST,BLEST                                                     
         MVC   MAPCLT,PBILKCLT                                                  
         MVC   MAPCLTN,PCLTNAME                                                 
         MVC   MAPPRD,BLPRD                                                     
         MVC   MAPPRDN,PPRDNAME                                                 
         MVC   MAPBPER,BLPER                                                    
         MVI   MAPRTAIL,0          RETAIL BILLS NO LONGER SPECIAL               
         GOTO1 DATCON,DMCB,(3,PBILDUED),(2,MAPDUED)                             
*                                  SET JOB AND OPEN RATES                       
         MVC   MAPJOB,SVJOB                                                     
         MVC   MAPOPREB,SVOPREB                                                 
         MVC   MAPORBSW,SVORBSW                                                 
         MVC   MAPAOR,MAAORSW                                                   
         MVC   MAPNARR,SPACES                                                   
*                                                                               
         CLI   MAPPROA+5,C'Y'      TEST DO SET JOB AS NARRATION                 
         BNE   PB35J                                                            
         CLI   MAPJOB,C' '                                                      
         BNH   PB35J                                                            
         OC    PBILPOST,PBILPOST   SKIP FOR ALREADY POSTEDS                     
         BNZ   PB35J                                                            
*                                  GET JOB RECORD                               
         LA    R7,4095(RC)         **R7 FOR REST OF FILE AREA**                 
         LA    R7,1(R7)                                                         
         USING PPFILED+4096,R7                                                  
         XC    WORK,WORK                                                        
         MVC   WORK(10),PPRDREC                                                 
         MVI   WORK+3,X'15'                                                     
         MVC   WORK+10(6),MAPJOB                                                
         CLC   PJOBREC(25),WORK    TEST HAVE EST HDR                            
         BE    PB35H               YES                                          
         MVC   PJOBREC(25),WORK    HOLD JOB REC KEY                             
         MVC   WORK(64),KEY        SAVE KEY FOR SEQ                             
         MVC   KEY(25),PJOBREC                                                  
         L     RF,UTL              RESTORE SYSTEM NUMBER                        
         MVC   4(1,RF),SAVSYS                                                   
         GOTO1 HIGH                                                             
         CLC   KEY(25),KEYSAVE                                                  
         BE    *+6                                                              
         DC    H'0'                JOB NOT FOUND                                
         GOTO1 GETJOB                                                           
         MVC   KEY,WORK            RESTORE                                      
         GOTO1 HIGH                                                             
         L     RF,UTL              RESET TO ACCT SYS ID                         
         MVC   4(1,RF),ACCTSE                                                   
*                                                                               
PB35H    DS    0H                                                               
         MVC   MAPNARR(6),MAPJOB                                                
         CLI   PJOBCAP1,C' '                                                    
         BNH   PB35M                                                            
         MVC   MAPNARR+7(25),PJOBCAP1    1ST CAPTION LINE                       
         LA    RF,MAPNARR+32                                                    
         CLI   0(RF),C' '                                                       
         BH    *+8                                                              
         BCT   RF,*-8                                                           
         MVC   2(25,RF),PJOBCAP2   FLOAT 2ND LINE                               
*                                                                               
         B     PB35M                                                            
         DROP  R7                                                               
*                                                                               
PB35J    DS    0H                                                               
         CLI   PROGPROF+11,C'Y'    TEST TO SET EST NAME AS NARRATION            
         BNE   PB35M                                                            
         MVC   MAPNARR(20),PESTNAME                                             
*                                                                               
PB35M    DS    0H                                                               
         DROP  R5                                                               
*                                                                               
PB36     DS    0H                  DO POSTING                                   
         OC    PBILPOST,PBILPOST   SKIP PREVIOUSLY POSTED                       
         BNZ   PB40                                                             
         CLI   TESTOPT,C'Y'        SKIP IF TESTING                              
         BE    PB40                                                             
         CLI   MAAORSW,C'E'        OR IF AOR ERROR                              
         BE    PB40                                                             
         L     RF,MALRPTR          OR IF ANY ACCOUNTS NOT OK                    
         CLC   0(15,RF),SPACES                                                  
         BE    PB40                                                             
         GOTO1 AMAPOST,DMCB,POSTWORK,MAOUT                                      
         MVI   POSTSW,C'Y'         SET HAVE POSTINGS                            
*                                                                               
PB40     DS    0H                                                               
         OC    PBILPOST,PBILPOST   SKIP PREVIOUSLY POSTED                       
         BZ    PB40B                                                            
         CLI   PREVOPT,C'Y'        UNLESS ASKED FOR                             
         BE    PB40B                                                            
         MVC   P,SPACES                                                         
         B     PB98                                                             
PB40B    DS    0H                                                               
         BAS   RE,PRNT                                                          
         LA    R4,PPREV            PREV TOTS                                    
         OC    PBILPOST,PBILPOST                                                
         BNZ   PB42                                                             
         LA    R4,PPOST            CURRENT TOTS                                 
         L     RF,MALRPTR          POINTER TO ACTUAL RCVBL ACCT                 
         CLC   0(15,RF),SPACES                                                  
         BNE   PB42                                                             
         LA    R4,PNOT             NOT-POSTED TOTS                              
*                                                                               
PB42     DS    0H                                                               
         CLI   ARASW,C'Y'          TEST WHAT PRINTED AS A/R                     
         BE    *+10                                                             
         MVC   BARAMT,BACT         ACTUAL AS A/R                                
         LA    R3,BAMTS            ROLL TO TOTALS                               
         BAS   RE,BPROLL                                                        
*                                                                               
         CLI   TESTOPT,C'Y'                                                     
         BE    PB98                                                             
         L     RF,MALRPTR          POINTER TO ACTUAL RCVBL ACCT                 
         CLC   0(15,RF),SPACES                                                  
         BE    PB98                                                             
*                                                                               
         LA    RF,PBILLREC                                                      
         ST    RF,AREC                                                          
         L     RF,UTL              RESTORE SYSTEM CODE                          
         MVC   4(1,RF),SAVSYS                                                   
         MVC   KEY+27(4),SAVDA     RE-READ BILL RECORD                          
         GOTO1 GETPRT                                                           
         MVC   PBILPOST,BTODAYP    MARK BILL RECORD                             
         GOTO1 PUTPRT                                                           
*                                                                               
PB98     DS    0H                                                               
         LA    RF,SVPOST           SAVE PREVIOUS MAPOST                         
         LA    RE,POSTWORK                                                      
         LA    R1,L'SVPOST                                                      
         MOVE  ((RF),(R1)),(RE)                                                 
         B     PB99                                                             
*                                                                               
PB99     DS    0H                                                               
         B     EXIT                                                             
         DROP  R6                                                               
         SPACE 3                                                                
PBEDT    DS    0H                                                               
         EDIT  (R0),(14,0(R6)),2,COMMAS=YES,CR=YES                              
         BR    RE                                                               
         SPACE 2                                                                
BPROLL   DS    0H          ROLL BINARY TO PACKED                                
         LA    R0,6                                                             
BPROLL5  L     R1,0(R3)                                                         
         CVD   R1,DUB                                                           
         AP    0(8,R4),DUB                                                      
         LA    R3,4(R3)                                                         
         LA    R4,8(R4)                                                         
         BCT   R0,BPROLL5                                                       
         BR    RE                                                               
**                                                                              
PPROLL   DS    0H        ROLL PACKED TO PACKED                                  
         LA    R0,18                                                            
PPROLL5  AP    0(8,R4),0(8,R3)                                                  
         LA    R3,8(R3)                                                         
         LA    R4,8(R4)                                                         
         BCT   R0,PPROLL5                                                       
         BR    RE                                                               
         EJECT                                                                  
*                                                                               
ZAPSET   DS    0H                                                               
         LA    R0,TOTL*3/8                                                      
ZAPSET5  ZAP   0(8,R4),=P'0'                                                    
         LA    R4,8(R4)                                                         
         BCT   R0,ZAPSET5                                                       
         BR    RE                                                               
         SPACE 2                                                                
CHKSET   DS    0H                                                               
         MVI   FULL,0                                                           
         LA    R0,TOTL*3/8                                                      
CHKSET5  CP    0(8,R4),=P'0'                                                    
         BE    *+8                                                              
         MVI   FULL,1                SET NONE ZERO ENTRY FOUND                  
         LA    R4,8(R4)                                                         
         BCT   R0,CHKSET5                                                       
         BR    RE                                                               
TOTPRNT  NTR1                                                                   
         SPACE 2                                                                
         ST    R3,ATOTS                                                         
         LA    RF,3                                                             
         CLI   PREVOPT,C'Y'                                                     
         BNE   *+8                                                              
         LA    RF,4(RF)                                                         
         CLI   LISTOPT,C'Y'                                                     
         BNE   *+8                                                              
         LA    RF,3(RF)                                                         
         STC   RF,LNEED                                                         
*                                                                               
         BAS   RE,PRNT                                                          
         L     R4,ATOTS                                                         
         CLI   MODE,RUNLAST                                                     
         BE    TP3                                                              
         CLI   PREVOPT,C'Y'                                                     
         BE    TP4                                                              
TP3      DS    0H                                                               
         MVC   P+13(15),=C'POSTED THIS RUN'                                     
         BAS   RE,TPFMT                                                         
         LA    R4,TOTL(R4)                                                      
         CLI   MODE,RUNLAST                                                     
         BNE   TP6                                                              
         MVC   P+13(17),=C'PREVIOUSLY POSTED'                                   
         B     TP5                                                              
TP4      DS    0H                                                               
         MVC   P+13(12),=C'TOTAL POSTED'                                        
         LA    R4,TOTL(R4)                                                      
TP5      DS    0H                                                               
         BAS   RE,TPFMT                                                         
TP6      DS    0H                                                               
         CLI   MODE,RUNLAST                                                     
         BE    TP7                                                              
         CLI   LISTOPT,C'Y'                                                     
         BNE   TP10                                                             
TP7      DS    0H                                                               
         LA    R4,TOTL(R4)                                                      
         MVC   P+13(10),=C'NOT POSTED'                                          
         BAS   RE,TPFMT                                                         
*                                                                               
TP10     DS    0H                                                               
         BAS   RE,PRNT                                                          
         B     EXIT                                                             
         SPACE 3                                                                
TPFMT    NTR1                                                                   
         SPACE 2                                                                
         LA    R7,P                                                             
         USING BLINED,R7                                                        
         LA    R5,32(R4)                                                        
         LA    R6,BLACTUAL                                                      
         BAS   RE,TPEDT                                                         
*                                                                               
         LA    R5,40(R4)           NET LESS CD                                  
         LA    R6,BLNET                                                         
         BAS   RE,TPEDT                                                         
*                                                                               
         LA    R5,16(R4)                                                        
         LA    R6,BLAC                                                          
         BAS   RE,TPEDT                                                         
*                                                                               
         LA    R5,24(R4)                                                        
         LA    R6,BLCD                                                          
         BAS   RE,TPEDT                                                         
*                                                                               
         LA    R5,0(R4)                                                         
         LA    R6,BLARAMT                                                       
         BAS   RE,TPEDT                                                         
*                                                                               
         BAS   RE,PRNT                                                          
         B     EXIT                                                             
         SPACE 3                                                                
TPEDT    DS    0H                                                               
         EDIT  (P8,0(R5)),(16,WORK),2,COMMAS=YES,CR=YES                         
         LR    RF,R6                                                            
         SH    RF,=H'3'                                                         
         CLI   WORK,C' '                                                        
         BNE   TPEDT6                                                           
         CLI   WORK+1,C' '                                                      
         BE    TPEDT2                                                           
         CLI   1(RF),C' '                                                       
         BNE   TPEDT6                                                           
TPEDT2   DS    0H                                                               
         CLI   WORK+3,C' '                                                      
         BE    TPEDT4                                                           
         CLI   2(RF),C' '                                                       
         BNE   TPEDT6                                                           
TPEDT4   DS    0H                                                               
         OC    1(16,RF),WORK                                                    
         BR    RE                                                               
TPEDT6   DS    0H                                                               
         MVC   133(16,RF),WORK                                                  
         BR    RE                                                               
         EJECT                                                                  
*                             PRINTING                                          
         SPACE 2                                                                
PRNT     NTR1                                                                   
         SPACE 2                                                                
         CLI   FORCEHED,C'Y'                                                    
         BE    PRNT1                                                            
         ZIC   RE,LNEED                                                         
         ZIC   RF,LINE                                                          
         AR    RE,RF                                                            
         STC   RE,BYTE                                                          
         CLC   BYTE,MAXLINES                                                    
         BL    PRNT2                                                            
*                                  NEW PAGE                                     
         MVI   FORCEHED,C'Y'                                                    
PRNT1    DS    0H                                                               
         LA    R4,HEAD4                                                         
         CLI   TESTOPT,C'Y'                                                     
         BNE   *+14                                                             
         MVC   0(15,R4),=C'** TEST MODE **'                                     
         LA    R4,132(R4)                                                       
         CLI   LISTOPT,C'Y'                                                     
         BNE   *+14                                                             
         MVC   0(29,R4),=C'** UNPOSTED BILLS INCLUDED **'                       
         LA    R4,132(R4)                                                       
         CLI   PREVOPT,C'Y'                                                     
         BNE   *+14                                                             
         MVC   0(32,R4),=C'** PREVIOUS POSTINGS INCLUDED **'                    
         LA    R4,132(R4)                                                       
*                                                                               
         MVC   WORK(4),QPROG+29                                                 
         CLI   WORK,C' '                                                        
         BE    PRNT1B                                                           
         MVC   WORK+4(2),=C'01'                                                 
         GOTO1 DATCON,DMCB,WORK,(6,HEAD5+77)                                    
         MVC   HEAD5+47(29),=C'ACCOUNTING MONTH OF SERVICE ='                   
PRNT1B   DS    0H                                                               
*                                                                               
PRNT2    DS    0H                                                               
         GOTO1 REPORT                                                           
         B     EXIT                                                             
         SPACE 3                                                                
NEXTEL   DS    0H                                                               
         ZIC   R0,1(R2)                                                         
         AR    R2,R0                                                            
         CLI   0(R2),0                                                          
         BE    NEXTEL2                                                          
         CLC   ELCODE,0(R2)                                                     
         BER   RE                                                               
         B     NEXTEL                                                           
NEXTEL2  DS    0H                                                               
         LTR   RE,RE                                                            
         BR    RE                                                               
         EJECT                                                                  
         LTORG                                                                  
*                                                                               
         DS    0D                                                               
MAOUT    DS    CL2000                                                           
         SPACE 2                                                                
WRKRBUFF CSECT                                                                  
         DS    4096X                                                            
FIXTAB   DC    10000X'00'                                                       
         SPACE 3                                                                
BLINED   DSECT                                                                  
BLINE    DS    0CL132                                                           
BLPRD    DS    CL3                                                              
         DS    CL1                                                              
BLEST    DS    CL3                                                              
         DS    CL1                                                              
BLPER    DS    CL6                                                              
         DS    CL1                                                              
BLINVD   DS    CL8                                                              
         DS    CL1                                                              
BLINVNO  DS    CL7                                                              
BLACTUAL DS    CL14                                                             
BLNET    DS    CL14                                                             
BLAC     DS    CL14                                                             
BLARAMT  DS    CL14                                                             
BLCD     DS    CL14                                                             
         DS    CL2                                                              
BLPOST   DS    CL29                                                             
         SPACE 2                                                                
         EJECT                                                                  
PPBAWRKD DSECT                                                                  
TODAY    DS    CL6                                                              
BTODAYP  DS    XL2                                                              
TODAYD   DS    CL8                                                              
BIGNDTE  DS    XL2                                                              
LNEED    DS    X                                                                
SAVSYS   DS    X                                                                
ELCODE   DS    X                                                                
RERUNSW  DS    X                                                                
WID      DS    CL16                                                             
TESTOPT  DS    C                                                                
LISTOPT  DS    C                                                                
PREVOPT  DS    C                                                                
DMPOPT   DS    C                                                                
RETAIL   DS    C                                                                
DORMCDSW DS    C                   SPECIAL DOREMUS SW                           
CMNOCDSW DS    C                                                                
ATOTS    DS    A                                                                
AMALINK  DS    A                                                                
AMAPOST  DS    V                                                                
PROGPROA DS    XL16                                                             
PROGPROB DS    XL16                                                             
SVJOB    DS    CL6                                                              
SVOPREB  DS    F                                                                
SVCNACT  DS    F                                                                
SVAORKEY DS    CL25                                                             
HOLDKEY  DS    CL25                                                             
SVORBSW  DS    C                                                                
SAVDA    DS    XL4                                                              
ARASW    DS    C                                                                
B1XPROF  DS    XL16                                                             
DINVNO   DS    CL6                                                              
POSTSW   DS    X                                                                
*                                                                               
AMTS     DS    0F                                                               
*                                                                               
BAMTS    DS    0CL24                                                            
BARAMT   DS    F                                                                
BGRS     DS    F                                                                
BAC      DS    F                                                                
BCD      DS    F                                                                
BACT     DS    F                                                                
BNT      DS    F                                                                
*                                                                               
PAMTS    DS    0CL144                                                           
PPOST    DS    CL48                                                             
PPREV    DS    CL48                                                             
PNOT     DS    CL48                                                             
*                                                                               
CAMTS    DS    0CL144                                                           
CPOST    DS    CL48                                                             
CPREV    DS    CL48                                                             
CNOT     DS    CL48                                                             
*                                                                               
RQAMTS   DS    0CL144                                                           
RQPOST   DS    CL48                                                             
RQPREV   DS    CL48                                                             
RQNOT    DS    CL48                                                             
*                                                                               
RAMTS    DS    0CL144                                                           
RPOST    DS    CL48                                                             
RPREV    DS    CL48                                                             
RNOT     DS    CL48                                                             
AMTSX    EQU   *                                                                
TOTL     EQU   48                                                               
*                                                                               
*                                                                               
MAIN     DS    0CL132                                                           
MASYS    DS    CL1                                                              
MAAGY    DS    CL2                                                              
MAMED    DS    CL1                                                              
MACLT    DS    CL3                                                              
MAPRD    DS    CL3                                                              
MAJOB    DS    CL6                                                              
MAOFF    DS    CL1                                                              
MARETYP  DS    XL1                                                              
MARET    DS    CL12                                                             
MAOTHER  DS    CL20                                                             
MABLTYP  DS    CL1                                                              
MAAORRCV DS    CL14                AOR RCVBL                                    
MAAORCOM DS    CL14                AOR COMMISSION  ACCT                         
MAAORSW  DS    CL1                                                              
MAPROA   DS    XL16                                                             
MAFINAN  DS    CL1                                                              
MASAVSE  DS    CL1                                                              
MAACCSE  DS    CL1                                                              
         DS    CL1                                                              
MAUTL    DS    F                                                                
         DS    CL28                                                             
         DS    0X                                                               
*                                                                               
         DS    0F                                                               
MAPARS   DS    0CL24                                                            
         DS    F                                                                
MASTAT   DS    X                                                                
         DS    XL3                                                              
         DS    4F                                                               
*                                                                               
POSTWORK DS    XL360                                                            
SVPOST   DS    XL360                                                            
*                                                                               
AFIXTAB  DS    A                                                                
FIXREC   DS    CL132                                                            
*                                                                               
         SPACE 2                                                                
*                                                                               
       ++INCLUDE MALINKD                                                        
         SPACE 2                                                                
*                                                                               
       ++INCLUDE MAPOSTD                                                        
         SPACE 2                                                                
*                                                                               
       ++INCLUDE DMWRKRK                                                        
*                                                                               
       ++INCLUDE PAORREC                                                        
         PRINT OFF                                                              
       ++INCLUDE PPREPWORK                                                      
       ++INCLUDE PPNEWFILE                                                      
       ++INCLUDE PPMODEQU                                                       
       ++INCLUDE DDLOGOD                                                        
         PRINT ON                                                               
*                                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'119PPBA02DS  05/01/02'                                      
         END                                                                    
