*          DATA SET PPGDMGRS   AT LEVEL 021 AS OF 05/01/02                      
*CATALP PPGDMGR                                                                 
         TITLE 'PPGDMGR - PPG DATAMGR MODULE'                                   
PPGDMGR  CSECT                                                                  
         NMOD1 10,PPGDMGR                                                       
*                                                                               
         L     RA,=V(PPWORKC)                                                   
         USING PPWORKD,RA                                                       
         L     R8,PPFILEC                                                       
         LA    R9,4095(R8)                                                      
         LA    R9,1(R9)                                                         
         USING PPFILED,R8,R9                                                    
         USING WKD,RC                                                           
*                                                                               
*                                  HOB OF RF HAS CODE                           
         LR    R2,RF                                                            
         SRL   R2,24                                                            
         SLL   R2,2                                                             
         B     BRTAB(R2)                                                        
*                                                                               
BRTAB    DS    0F                                                               
         DC    F'0'                                                             
         B     PDREAD                                                           
         B     PDHIGH                                                           
         B     PDSEQ                                                            
         B     PDWRT                                                            
         B     PDREADU                                                          
         B     PDHIGHU                                                          
         B     PDSEQU                                                           
         B     PDWRTU                                                           
*                                                                               
         B     PDGAGY                                                           
         B     PDGCLI                                                           
         B     PDGPROD                                                          
         B     PDGDIV                                                           
         B     PDGREG                                                           
         B     PDGDIST                                                          
         B     PDGEST                                                           
         B     PDGBILL                                                          
         B     PDGCONT                                                          
         B     PDGLIST                                                          
         B     PDGBUY                                                           
         B     PDGJOB                                                           
         B     PDGREP                                                           
         B     PDGBKT                                                           
         B     PDGREC                                                           
         B     PDGNAME                                                          
         B     PDGLTL                                                           
*                                                                               
         B     PDPUTP                                                           
         B     PDPUTU                                                           
         B     PDADDP                                                           
         B     PDADDU                                                           
*                                                                               
         B     PDSEQKSV                                                         
*                                                                               
         EJECT                                                                  
*                                                                               
PDREAD   LA    RF,DMREAD                                                        
         B     PRTLINK                                                          
*                                                                               
PDHIGH   LA    RF,DMRDHI                                                        
         MVC   KEYSAVE,KEY                                                      
         B     PRTLINK                                                          
*                                                                               
PDSEQ    LA    RF,DMRSEQ                                                        
         B     PRTLINK                                                          
*                                                                               
PDSEQKSV LA    RF,DMRSEQ                                                        
         MVC   KEYSAVE,KEY                                                      
         B     PRTLINK                                                          
*                                                                               
PDWRT    LA    RF,DMWRT                                                         
*                                                                               
PRTLINK  LA    R1,PRTDIR                                                        
         B     DIRLINK                                                          
*                                                                               
PDREADU  LA    RF,DMREAD                                                        
         B     PUBLINK                                                          
*                                                                               
PDHIGHU  LA    RF,DMRDHI                                                        
         MVC   KEYSAVE,KEY                                                      
         B     PUBLINK                                                          
*                                                                               
PDSEQU   LA    RF,DMRSEQ                                                        
         B     PUBLINK                                                          
*                                                                               
PDWRTU   LA    RF,DMWRT                                                         
*                                                                               
PUBLINK  LA    R1,PUBDIR                                                        
*                                                                               
*                                                                               
DIRLINK  DS    0H                                                               
         ST    R1,DMCB+4                                                        
         MVC   TRACEKEY,KEY                                                     
DIRLNK2  DS    0H                                                               
         ST    RF,DMCB                                                          
         MVC   DMCB(1),DMINBTS                                                  
         GOTO1 ,DMCB,,,KEY,KEY,0                                                
         CLI   RCWRITE,C'Y'                                                     
         BE    DIRLNK2B                                                         
         L     RF,DMCB                                                          
         CLC   DMWRT,0(RF)                                                      
         BE    DIRLNK4             SUPPRESS WRITES                              
DIRLNK2B DS    0H                                                               
         GOTO1 DATAMGR                                                          
*                                  SCREEN OUT FF DELETES                        
         CLI   KEY+25,X'FF'                                                     
         BE    DIRLNK2C                                                         
         TM    KEY+25,X'40'        AND CLOSEOUTS                                
         BZ    DIRLNK4                                                          
         CLI   FCRDCLOS,C'Y'                                                    
         BE    DIRLNK4                                                          
DIRLNK2C DS    0H                                                               
         L     RF,DMCB                                                          
         CLC   DMRSEQ,0(RF)                                                     
         BE    DIRLNK2                                                          
         CLC   DMWRT,0(RF)                                                      
         BE    DIRLNK4                                                          
         CLC   DMRDHI,0(RF)                                                     
         BNE   DIRLNK3                                                          
         LA    RF,DMRSEQ                                                        
         B     DIRLNK2                                                          
DIRLNK3  DS    0H                                                               
         OI    DMCB+8,X'10'        SET RBC NOT FOUND ON READ                    
DIRLNK4  DS    0H                                                               
         B     DMCHECK                                                          
*                                                                               
PDGAGY   LA    RF,PAGYKEY                                                       
         B     PRTFL                                                            
*                                                                               
PDGCLI   LA    RF,PCLTKEY                                                       
         B     PRTFL                                                            
*                                                                               
PDGPROD  LA    RF,PPRDKEY                                                       
         B     PRTFL                                                            
*                                                                               
PDGDIV   LA    RF,PDIVKEY                                                       
         MVC   PDIVKEY(25),KEYSAVE                                              
         MVC   PDIVNAME,=CL20'MASTER DIVISION'                                  
         CLC   KEYSAVE+7(3),=C'999'                                             
         BE    NORDEXT                                                          
         B     PRTFL                                                            
*                                                                               
PDGREG   LA    RF,PREGKEY                                                       
         MVC   PREGKEY(25),KEYSAVE                                              
         MVC   PREGNAME,=CL20'MASTER REGION'                                    
         CLC   KEYSAVE+10(3),=C'999'                                            
         BE    NORDEXT                                                          
         B     PRTFL                                                            
*                                                                               
PDGDIST  LA    RF,PDSTKEY                                                       
         MVC   PDSTKEY(25),KEYSAVE                                              
         MVC   PDSTNAME,=CL20'MASTER DISTRICT'                                  
         CLC   KEYSAVE+13(3),=C'999'                                            
         BE    NORDEXT                                                          
         B     PRTFL                                                            
*                                                                               
*                                                                               
PDGEST   LA    RF,PESTKEY                                                       
         XC    0(250,RF),0(RF)                                                  
         B     PRTFL                                                            
*                                                                               
PDGBILL  LA    RF,PBILLKEY                                                      
         B     PRTFL                                                            
*                                                                               
PDGCONT  LA    RF,PCONKEY                                                       
         B     PRTFL                                                            
*                                                                               
PDGLIST  L     RF,ALISREC                                                       
         B     PRTFL                                                            
*                                                                               
PDGBUY   LA    RF,PBUYKEY                                                       
         B     PRTFL                                                            
*                                                                               
*                                                                               
PDGJOB   LA    RF,PJOBKEY                                                       
         B     PRTFL                                                            
*                                                                               
PDGREP   LA    RF,PREPKEY                                                       
         B     PRTFL                                                            
*                                                                               
PDGBKT   L     RF,ADBKT                                                         
         B     PRTFL                                                            
*                                                                               
PDGREC   L     RF,AREC                                                          
*                                                                               
PRTFL    LA    R1,PRTFILE                                                       
         B     FILINK                                                           
*                                                                               
PDGNAME  LA    RF,PUBKEY                                                        
         B     PUBFL                                                            
*                                                                               
PDGLTL   L     RF,ALTLREC                                                       
*                                                                               
PUBFL    LA    R1,PUBFILE                                                       
*                                                                               
FILINK   DS    0H                                                               
         ST    RF,DMCB+12                                                       
         MVC   DMCB(1),DMINBTS                                                  
         ST    R1,LASTFILE                                                      
         ST    R1,DMCB+4                                                        
         GOTO1 DATAMGR,DMCB,(DMINBTS,GETREC),,KEY+27,,(0,DMWORK)                
         L     RF,DMCB+12                                                       
         MVC   LASTDA,KEY+27                                                    
         MVC   LASTLEN,25(RF)                                                   
         B     DMCHECK                                                          
*                                                                               
PDPUTP   LA    RF,PRTFILE                                                       
         LA    R1,FCUPPRTF                                                      
         B     PUTLK                                                            
*                                                                               
PDPUTU   LA    RF,PUBFILE                                                       
         LA    R1,FCUPPUBF                                                      
*                                                                               
PUTLK    DS    0H                                                               
         ST    RF,DMCB+4                                                        
         LA    R2,LASTDA                                                        
         L     R3,AREC                                                          
         CLC   25(2,R3),LASTLEN                                                 
         BNH   PUTLK2                                                           
         CLI   0(R1),C'Y'                                                       
         BNE   DMERR2                                                           
*                                                                               
PUTLK2   DS    0H                                                               
         GOTO1 ,DMCB,(DMINBTS,PUTREC),,(R2),(R3),(0,DMWORK)                     
         CLI   RCWRITE,C'Y'                                                     
         BNE   DMCHECK                                                          
         GOTO1 DATAMGR                                                          
         B     DMCHECK                                                          
*                                                                               
PDADDP   LA    RF,PRTFILE                                                       
         LA    R1,FCUPPRTD                                                      
         B     ADDLK                                                            
*                                                                               
PDADDU   LA    RF,PUBFILE                                                       
         LA    R1,FCUPPUBD                                                      
*                                                                               
ADDLK    DS    0H                                                               
         ST    RF,DMCB+4                                                        
         CLC   0(2,R1),=C'YY'      TEST OPEN FOR UPDATE                         
         BNE   DMERR2                                                           
         L     RE,AREC                                                          
         MVC   KEY(25),0(RE)                                                    
         GOTO1 ,DMCB,(DMINBTS,ADDREC),,KEY,AREC,(0,DMWORK)                      
         CLI   RCWRITE,C'Y'                                                     
         BNE   DMCHECK                                                          
         GOTO1 DATAMGR                                                          
         B     DMCHECK                                                          
         SPACE 3                                                                
DMCHECK  DS    0H                                                               
         CLI   RCTRACE,C'Y'                                                     
         BNE   *+8                                                              
         BAS   RE,DMTRACE                                                       
         MVC   BYTE,DMOUTBTS                                                    
         NC    BYTE,DMCB+8                                                      
         BZ    DMEND                                                            
*                                                                               
         LA    R3,ERRM1                                                         
         B     DMERR4                                                           
*                                                                               
DMERR2   DS    0H                                                               
         LA    R3,ERRM2                                                         
*                                                                               
DMERR4   DS    0H                                                               
         GOTO1 LOGIO,PDDMCB,1,(27,(R3))                                         
         UNPK  ERRM3+19(R4),RCRQTOT                                             
         OI    ERRM3+22,X'F0'                                                   
         GOTO1 (RF),(R1),,(27,ERRM3)                                            
         LA    R1,DMCB                                                          
         BAS   RE,DMTRACE                                                       
*                                                                               
         L     RF,4(RD)                                                         
         CLC   52(4,RF),VFILCON    TEST CALLED BY PPFILCON                      
         BNE   DMEND               NO-NORMAL EXIT -LET CALLER TEST ERRS         
*                                                                               
         L     RD,4(RD)            IF CALLED BY PPFILCON                        
         LM    RE,RC,12(RD)        UNWIND WITHOUT EXIT                          
         L     RF,ANXTREQ          AND GO ON TO NEXT REQ                        
         BR    RF                                                               
         SPACE 3                                                                
DMTRACE  NTR1                                                                   
         L     RF,PPWORK2C                                                      
         USING PPWORK2D,RF                                                      
         LA    R6,P                                                             
         CLI   RC2DSECT,C'Y'                                                    
         BNE   *+8                                                              
         LA    R6,P1                                                            
*                                                                               
         DROP  RF                                                               
*                                                                               
         MVC   0(132,R6),SPACES                                                 
         MVC   132(132,R6),SPACES                                               
         LM    R2,R5,0(R1)                                                      
         MVC   0(6,R6),0(R2)                                                    
         MVC   8(8,R6),0(R3)                                                    
         MVC   TRACEDM8,8(R1)                                                   
         LA    R2,4                                                             
         CLC   3(3,R3),=C'DIR'                                                  
         BNE   DMTRACE2                                                         
         LA    R4,TRACEKEY                                                      
         LA    R2,25                                                            
*                                                                               
DMTRACE2 DS    0H                                                               
         GOTO1 HEXOUT,PDDMCB,(R4),20(R6),(R2),=C'N'                             
         GOTO1 (RF),(R1),(R5),75(R6),25                                         
         GOTO1 (RF),(R1),TRACEDM8,130(R6),1                                     
*                                                                               
         MVI   SKIPSPEC,C'Y'                                                    
         GOTO1 REPORT                                                           
         MVC   DMCB+8(1),TRACEDM8                                               
         XIT1                                                                   
         SPACE 3                                                                
NORDEXT  DS    0H                                                               
         MVI   DMCB+8,0            NO READ - NO ERROR                           
         B     EXIT                                                             
*                                                                               
DMEND    DS    0H                                                               
*                                                                               
EXIT     DS    0H                                                               
         XIT1                                                                   
         SPACE 3                                                                
ERRM1    DC    CL27'*** DATA MANAGER ERROR ***'                                 
ERRM2    DC    CL27'*** ILLEGAL FILE UPDATE ***'                                
ERRM3    DC    CL27'*** REQUEST NUMBER NNNN ***'                                
*                                                                               
*                                                                               
         SPACE 3                                                                
         LTORG                                                                  
         SPACE 3                                                                
WKD      DSECT                                                                  
TRACEKEY DS    CL25                                                             
TRACEDM8 DS    CL1                                                              
PDDMCB   DS    6F                                                               
         SPACE 3                                                                
         PRINT OFF                                                              
       ++INCLUDE PPREPWORK                                                      
       ++INCLUDE PPREPWORK2                                                     
       ++INCLUDE PPNEWFILE                                                      
       ++INCLUDE PPMODEQU                                                       
*                                                                               
         PRINT ON                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'021PPGDMGRS  05/01/02'                                      
         END                                                                    
