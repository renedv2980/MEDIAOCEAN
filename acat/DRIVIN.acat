*          DATA SET DRIVIN     AT LEVEL 026 AS OF 06/28/12                      
*CATALP DRIVIN                                                                  
         TITLE 'DRIVIN - INPUT HANDLER FOR DRIVER'                              
DRIVIN   CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 DINDX-DIND,**DRIN**                                              
         USING DIND,RC                                                          
         L     RA,0(R1)                                                         
         USING GLOBALD,RA                                                       
         CLI   GLANYERR,C'Y'       DON'T PROCESS IF PREVIOUS ERRORS             
         JE    XIT                                                              
         EJECT                                                                  
*              BROWSE DRIVE TABLE - REC STATEMENT                               
         SPACE 1                                                                
         MVI   GLRECNO,0                                                        
         LA    R2,GLAINTD-4        R2=A(INTERNAL RECORD DETAILS)                
         L     R3,GLADTAB          R3=A(DRIVE TABLE)                            
*                                  R4=A(INPUT FIELD IN RECORD)                  
         SPACE 1                                                                
DIN2     CLI   0(R3),X'10'         LOOK FOR REC                                 
         JE    DIN10                                                            
         CLI   0(R3),X'20'         AND IN                                       
         JE    DIN20                                                            
         CLI   0(R3),0                                                          
         JNE   DINEND                                                           
         CLI   RECYN,C'N'                                                       
         JE    *+8                                                              
         BRAS  RE,RECOUT           OUTPUT LAST RECORD                           
         J     XIT                                                              
         SPACE 1                                                                
         USING DRRECD,R3                                                        
DIN10    CLI   GLRECNO,0           UNLESS THIS IS THE FIRST                     
         JE    DIN12                                                            
         CLI   RECYN,C'N'          OR THIS REC REJECTED                         
         JE    DIN12                                                            
         BRAS  RE,RECOUT           OUTPUT RECORDS                               
         SPACE 1                                                                
DIN12    AI    GLRECNO,1                                                        
         LA    R2,4(R2)                                                         
         MVI   RECYN,C'N'                                                       
         MVC   GLRECLAB,DRRECLAB   SET ANY RECORD LABEL                         
*                                  CHECK RECORD LEVEL IFS                       
         GOTO1 =V(DRIVTEST),DMCB,(RA),DRRECIFS                                  
         JNE   DINEND                                                           
         MVI   GLHOOK,GLINREC      TELL APPLIC WE'RE ABOUT TO PROCESS A         
         XC    GLAROUT,GLAROUT     RECORD FOR INPUT                             
         MVI   GLAROUT,1                                                        
         XC    GLARGS,GLARGS                                                    
         MVC   GLARGS(1),GLRECNO   PASS RECORD NUM IN ARGS                      
         ST    R3,GLADTENT                                                      
         BRAS  RE,GOHOOK                                                        
         CLI   GLHOOK,GLDONT       APPLIC MAY REJECT THIS RECORD                
         JE    DINEND                                                           
         MVI   RECYN,C'Y'                                                       
         LH    RF,BGRECLEN                                                      
         L     RE,GLAIO                                                         
         XCEF                                                                   
         L     R4,GLAIO                                                         
         MVC   0(1,R4),GLRECNO     EACH RECORD STARTS WITH NO                   
         LA    R4,2(R4)                                                         
         J     DINEND                                                           
         EJECT                                                                  
*              DEAL WITH IN STATEMENTS IN DRIVE TABLE                           
         SPACE 3                                                                
*                                  R4=A(INPUT FIELD)                            
DIN20    CLI   RECYN,C'N'                                                       
         JE    DINEND                                                           
         BRAS  RE,PRECLEAR                                                      
         USING DRIND,R3                                                         
         GOTO1 =V(DRIVTEST),DMCB,(RA),DRINIFS                                   
         JNE   DIN20X                                                           
         CLI   DRINELEN,96         IS THERE A LITERAL?                          
         JE    DIN20B                                                           
         LLC   R1,DRINELEN         YES SO USE IT                                
         AHI   R1,-97                                                           
         LLC   R0,DRINLEN          MAKE SURE FIELD IS LARGE ENOUGH              
         BCTR  R0,0                                                             
         CRJL  R1,R0,*+8                                                        
         LR    R1,R0                                                            
         EXRL  R1,*+10                                                          
         J     DIN20X                                                           
         MVC   0(0,R4),DRINLIT                                                  
         SPACE 1                                                                
DIN20B   MVI   GLHOOK,GLROUT       BUILD HOOK FOR INPUT ROUTINE                 
         MVC   GLLABEL,DRINROUT                                                 
         MVC   GLAROUT,DRINRADD                                                 
         MVC   GLARGS,DRINARGS                                                  
         ST    R4,GLAIFLD                                                       
         ST    R3,GLADTENT                                                      
         BRAS  RE,GOHOOK                                                        
         J     DIN20X                                                           
         SPACE 1                                                                
DIN20X   BRAS  RE,NUMCHECK         CHECK FOR ACTIVE NUMERIC                     
         LLC   R1,DRINLEN                                                       
         AR    R4,R1                                                            
         SPACE 1                                                                
DINEND   LLC   R1,1(R3)            BUMP TO NEXT DRIVE TABLE ENTRY               
         AR    R3,R1                                                            
         J     DIN2                                                             
         EJECT                                                                  
*              PRECLEAR FIELD                                                   
         SPACE 3                                                                
*                                  R3=A(IN ELEMENT)                             
*                                  R4=A(INPUT FIELD)                            
         SPACE 1                                                                
         USING DRIND,R3                                                         
PRECLEAR LLC   RF,DRINLEN          PRE CLEAR FIELD                              
         BCTR  RF,0                                                             
         EXRL  RF,EXXCR4R4                                                      
         SPACE 1                                                                
         CLI   DRINTYPE,C'P'       IF TYPE IS PACKED                            
         JE    PRECL2                                                           
         CLI   DRINTYPE,C'M'          OR MASKED                                 
         BNER  RE                                                               
         SPACE 1                                                                
PRECL2   LLC   RF,DRINLEN                                                       
         AR    RF,R4                                                            
         BCTR  RF,0                                                             
         MVI   0(RF),X'0C'         ENSURE PACKED ZERO IN RECORD                 
         BR    RE                                                               
         EJECT                                                                  
*              CHECK FOR ACTIVE NUMERIC FIELDS                                  
         SPACE 3                                                                
*                                  R3=A(DRIVE TABLE IN ELEMENT)                 
*                                  R4=A(DATA)                                   
         SPACE 1                                                                
NUMCHECK NTR1                                                                   
         CLI   DRINTYPE+1,C'+'                                                  
         JNE   XIT                                                              
         CLI   DRINTYPE,C'C'       THESE NOT REALLY ADDITIVE                    
         JE    XIT                                                              
         CLI   DRINTYPE,C'X'                                                    
         JE    XIT                                                              
         CLI   DRINTYPE,C'W'                                                    
         JE    XIT                                                              
         CLC   DRINROUT(5),=C'COUNT'   (NOT A REAL ADDITIVE COL)                
         JE    XIT                                                              
         LLC   R0,DRINREP                                                       
         SPACE 1                                                                
NUCH2    LLC   R1,DRINFLEN                                                      
         BCTR  R1,0                R1=L'DATA-1                                  
         LR    R5,R4               R5=A(THIS DATA)                              
         CLI   DRINTYPE,C'B'                                                    
         JE    NUMCB                                                            
         CLI   DRINTYPE,C'P'                                                    
         JE    NUMCP                                                            
         CLI   DRINTYPE,C'E'                                                    
         JE    NUMCE                                                            
         LA    R5,1(R5)            GET PAST IRRELEVANT BYTE                     
         BCTR  R1,0                                                             
         J     NUMCP               SCALED AND MASKED                            
         SPACE 1                                                                
NUMCB    EXRL  R1,NUMCBTST         BINARY                                       
         JZ    NUMCHNO                                                          
         J     NUMCHYES                                                         
         SPACE 1                                                                
NUMCP    EXRL  R1,NUMCBTST         (PROTECT AGAINST BINARY ZEROS)               
         JNZ   NUMCP2                                                           
         SLL   R1,4                                                             
         EXRL  R1,NUMPZAP          SUPPOSED TO BE PACKED SO ZAP                 
         J     NUMCHNO             AND TREAT AS ZERO                            
         SPACE 1                                                                
NUMCP2   SLL   R1,4                PACKED SCALED AND MASKED                     
         EXRL  R1,NUMCPTST                                                      
         JE    NUMCHNO                                                          
         J     NUMCHYES                                                         
         SPACE 1                                                                
NUMCE    EXRL  R1,NUMCEADJ         EBCDIC - ADJUST BLANKS                       
         EXRL  R1,NUMCETST                  CHECK FOR ZEROS                     
         JE    NUMCHNO                                                          
         J     NUMCHYES                                                         
         SPACE 1                                                                
NUMCHNO  CLI   ANYACTIV,0                                                       
         JNE   NUMCHX                                                           
         MVI   ANYACTIV,1                                                       
         J     NUMCHX                                                           
         SPACE 1                                                                
NUMCHYES MVI   ANYACTIV,2                                                       
         SPACE 1                                                                
NUMCHX   LLC   R1,DRINFLEN                                                      
         AR    R4,R1                                                            
         JCT   R0,NUCH2                                                         
         J     XIT                                                              
         SPACE 1                                                                
NUMCBTST OC    0(0,R5),0(R5)                                                    
NUMPZAP  ZAP   0(0,R5),=P'0'                                                    
NUMCPTST CP    0(0,R5),=P'0'                                                    
NUMCEADJ OC    0(0,R5),=16C'0'                                                  
NUMCETST CLC   0(0,R5),=16C'0'                                                  
ANYACTIV DC    X'00'                                                            
         EJECT                                                                  
*              ROUTINE TO WRITE OUT THE RECORDS                                 
         SPACE 3                                                                
RECOUT   NTR1                                                                   
         TM    GLINDS,GLPALDET     OPTION TO GET INACTIVE RECORDS               
         JO    RECOUT1                                                          
         CLI   ANYACTIV,1          IGNORE ZERO ACTIVITY RECORDS                 
         JNE   RECOUT1                                                          
         MVI   ANYACTIV,0                                                       
         J     XIT                                                              
         SPACE 1                                                                
RECOUT1  MVI   ANYACTIV,0                                                       
         L     R2,0(R2)                                                         
         USING GLINTD,R2                                                        
         MVI   GLHOOK,0                                                         
         LA    R0,RECSAVE          SAVE THE RECORD                              
         LH    R1,GLRECLEN                                                      
         L     RE,GLAIO                                                         
         LR    RF,R1                                                            
         MVCL  R0,RE                                                            
*                                                                               
         LA    R3,GLTOTLEV+GLNUMLEV-1                                           
         LA    R4,GLLCBS+GLNUMLEV-1                                             
         LA    R0,GLNUMLEV                                                      
         SPACE 1                                                                
RECOUT2  L     R1,GLAIO            NOTE THE LEVEL NUMBER                        
         STC   R0,1(R1)                                                         
         STC   R0,TRIMLEV                                                       
         CLI   0(R3),X'10'         TEST DETAILED TOTAL                          
         JL    *+12                                                             
         BRAS  RE,RECDET           YES                                          
         J     RECNEXT                                                          
         CLI   0(R3),1             SEE IF RECORD NEEDED AT THIS LEVEL           
         JL    RECNEXT             NO                                           
         JH    *+12                                                             
         BRAS  RE,RECTOT           TOTAL RECORD AT THIS LEVEL                   
         J     RECNEXT                                                          
         TM    GLINDS,GLINODET     DETAIL REC - CHECK THAT IT'S NEEDED          
         JO    *+8                 NO                                           
         BRAS  RE,RECPUT           YES - PUT DETAIL RECORD                      
         MVI   TRIMSW,0                                                         
         SPACE 1                                                                
RECNEXT  CIJE  R0,0,XIT                                                         
         BCTR  R3,0                                                             
         BCTR  R4,0                                                             
         BCTR  R0,0                                                             
         J     RECOUT2                                                          
         SPACE 1                                                                
RECTOT   NTR1                                                                   
         CLI   GLMAXTLV,0          OPTIONS TO LIMIT TOTAL RECORDS               
         JE    *+14                                                             
         CLC   TRIMLEV,GLMAXTLV                                                 
         JL    XIT                                                              
         CLI   GLMINTLV,0                                                       
         JE    RECTOT2                                                          
         CLC   TRIMLEV,GLMINTLV                                                 
         JH    XIT                                                              
         SPACE 1                                                                
RECTOT2  LLC   R1,0(R4)            R1=L'CONTROL BREAK                           
         L     RE,GLAIO            RE=A(PIECE OF KEY TO CLEAR)                  
         LA    RE,2(RE,R1)                                                      
         LH    RF,GLKEYLEN         RF=L'PIECE TO BE CLEARED                     
         SR    RF,R1                                                            
         BCTR  RF,0                                                             
         EXRL  RF,EXXCRERE                                                      
         CLI   TRIMSW,1            ONLY NEEDED ONCE                             
         JE    *+8                                                              
         BRAS  RE,ANYTRIM                                                       
         BRAS  RE,RECPUT                                                        
         J     XIT                                                              
         SPACE 1                                                                
         SPACE 1                                                                
RECDET   NTR1                                                                   
******** CLI   GLHOOK,GLDONT       TEST RECORD ALREADY REJECTED                 
******** JE    RDX                                                              
         OI    GLINDS2,GLPUTDT     INDICATE PUTTING DETAILED TOTALS NOW         
         CLI   GLMINTLV,0                                                       
         JE    *+14                                                             
         CLC   TRIMLEV,GLMINTLV                                                 
         JH    RDX                                                              
         LLC   RF,GLDETLEV         CHECK CURRENT LEVEL LOW ENOUGH               
         BCTR  RF,0                FOR DETAILED TOTALS                          
         LLC   RE,TRIMLEV                                                       
         SR    RF,RE                                                            
         BNP   RDX                                                              
         LLC   R5,0(R3)            CHECK WE CAN SHOW THE DETAILS ASKED          
         SLL   R5,28               FOR                                          
         SRL   R5,28                                                            
         CRJL  RF,R5,RDX                                                        
         J     RD04                                                             
*                                                                               
RD02     TM    GLTOTIND,GLTITOT    CHECK TOT AT LEVEL ABOVE DETAIL LEV          
         JZ    RD04                                                             
         LLC   RE,1(R1)            YES - THEN AUTO GENERATE A REGULAR           
         BCTR  RE,0                      TOTAL RECORD                           
         CLI   GLMAXTLV,0                                                       
         JE    *+12                                                             
         CLM   RE,1,GLMAXTLV       OPTIONS TO LIMIT REGULAR TOTAL RECS          
         JL    RD04                                                             
         CLI   GLMINTLV,0                                                       
         JE    *+12                                                             
         CLM   RE,1,GLMINTLV                                                    
         JH    RD04                                                             
         STC   RE,1(R1)                                                         
         LLC   RE,GLDETDSP                                                      
         LA    RE,2(RE,R1)                                                      
         LLC   RF,GLDETLEN                                                      
         BCTR  RF,0                                                             
         EXRL  RF,EXXCRERE                                                      
         L     RF,GLAIO                                                         
         ST    R1,GLAIO                                                         
         BRAS  RE,RECPUT                                                        
         ST    RF,GLAIO                                                         
*                                                                               
RD04     CLI   GLMAXTLV,0          TEST OPTION TO LIMIT TOTALS                  
         JE    RD05                                                             
         CLC   TRIMLEV,GLMAXTLV    YES-TEST DETAIL TOTS MAY BE LIMITED          
         JNL   RD05                                                             
         LLC   RF,GLDETLEV         YES-                                         
         SR    RF,R5               RF=LAST LEVEL TO RECEIVE FF'S                
         CLM   RF,1,GLMAXTLV       TEST MAX TOTAL LEV WILL RECEIVE FF'S         
         JNL   RDX                 YES-DO NOT GENERATE ANY MORE TOTALS          
*                                                                               
RD05     LA    R0,RECBUILD         BUILD RECORD                                 
         LH    R1,GLRECLEN                                                      
         LA    RE,RECSAVE                                                       
         LR    RF,R1                                                            
         MVCL  R0,RE                                                            
         LA    R1,RECBUILD         R1=A(RECORD)                                 
         MVC   0(1,R1),GLRECNO                                                  
         MVC   1(1,R1),GLDETLEV                                                 
*                                                                               
         LLC   RF,GLDETLEV         MOVE FF'S TO LEVELS BELOW THIS ONE           
         SR    RF,R5               WHICH ARE NOT DETAILED                       
         LA    RF,GLLCBS-1(RF)                                                  
         LLC   RE,0(RF)                                                         
         LLC   RF,0(R4)                                                         
         SR    RE,RF                                                            
         BCTR  RE,0                                                             
         LA    R6,2(RF,R1)                                                      
         MVI   0(R6),X'FF'                                                      
         CIJL  RE,1,RD06                                                        
         BCTR  RE,0                                                             
         EXRL  RE,EXMVC                                                         
*                                                                               
RD06     L     RF,GLAIO            PUT DETAILED TOTAL RECORD                    
         ST    R1,GLAIO                                                         
         BRAS  RE,RECPUT                                                        
         ST    RF,GLAIO                                                         
*                                                                               
RD08     JCT   R5,RD02             AUTO GENERATE DETAILED TOTALS AT             
*                                  ALL LOWER LEVELS                             
         TM    GLTOTIND,GLTITOT                                                 
         JZ    RDX                                                              
         L     R1,GLAIO            FINAL REGULAR TOTAL RECORD                   
         MVC   1(1,R1),TRIMLEV                                                  
         BRAS  RE,RECTOT                                                        
*                                                                               
RDX      NI    GLINDS2,255-GLPUTDT                                              
         J     XIT                                                              
*                                                                               
EXMVC    MVC   1(0,R6),0(R6)                                                    
         EJECT                                                                  
*              ROUTINE TO PUT A RECORD TO SORTER                                
         SPACE 3                                                                
RECPUT   NTR1                                                                   
         MVI   GLHOOK,GLPUTSRT     TELL APPLIC WE'RE WRITING                    
         XC    GLAROUT,GLAROUT                                                  
         MVI   GLAROUT,1                                                        
         BRAS  RE,GOHOOK                                                        
         CLI   GLHOOK,GLDONT       APPLIC CAN REJECT                            
         JE    XIT                                                              
         MVI   GLHOOK,GLPUTSRT     TELL SYSTEM DRIVER AS WELL                   
         XC    GLAROUT,GLAROUT                                                  
         MVI   GLAROUT,2                                                        
         BRAS  RE,GOHOOK                                                        
         CLI   GLHOOK,GLDONT       WHO CAN ALSO REJECT                          
         JE    XIT                                                              
         CLI   FIRSTSW,C'Y'        TEST FIRST TIME                              
         JNE   RECPUT2                                                          
         MVI   FIRSTSW,C'N'                                                     
         SR    RE,RE                                                            
         ICM   RE,3,GLMAXREC       YES-TEST MAXIMUM INPUT RECORD COUNT          
         JZ    RECPUT2                                                          
         CVD   RE,DUB              YES-SET THE MAX COUNT                        
         ZAP   MAXREC,DUB                                                       
         SPACE 1                                                                
RECPUT2  CP    MAXREC,=P'0'        TEST MAX INPUT RECORD COUNT                  
         JE    *+14                                                             
         CP    COUNT,MAXREC        YES-HAVE WE GOT THERE YET ?                  
         JNL   XIT                                                              
         AP    COUNT,=P'1'                                                      
*                                                                               
         CLI   TRACOPT,C'Y'        SELECTIVE - R2=A(ELEMENT)                    
         JNE   *+8                                                              
         BRAS  RE,TRASOME                                                       
*                                                                               
         L     R4,GLAIO                                                         
         CLI   GLABCSW,0           ABC DOES NOT OUTPUT TO SORT                  
         JNE   XIT                                                              
         GOTO1 =V(DRIVPUT),DMCB,(RA),(R4)                                       
         J     XIT                                                              
         EJECT                                                                  
*              ROUTINE TO TRIM DOWN TOTAL RECORDS                               
         SPACE 3                                                                
ANYTRIM  NTR1                                                                   
         MVI   TRIMSW,1                                                         
         L     R3,GLAFIN                                                        
         USING DRIND,R3                                                         
         SPACE 1                                                                
AT2      CLI   0(R3),0             LOOKING FOR INS                              
         JE    XIT                                                              
         CLI   0(R3),X'10'                                                      
         JE    XIT                                                              
         CLI   0(R3),X'20'                                                      
         JNE   ATEND                                                            
         TM    GLINDS3,GLPNACTO    OPTION TO PRINT NON-ADDITIVE COLS            
         JO    ATEND               ON TOTALS                                    
         CLI   DRINTYPE+1,C'+'     DON'T CREAM ADDITIVE FIELDS                  
         JE    ATEND                                                            
         CLC   DRINLEV,TRIMLEV     OR KEY DATA AT LOW LEVEL                     
         JNH   ATEND                                                            
         L     R4,GLAIO            POISITION R4 TO DATA FIELD                   
         AH    R4,DRINDISP                                                      
         LLC   R1,DRINLEN                                                       
         BCTR  R1,0                                                             
         EXRL  R1,EXXCR4R4                                                      
         SPACE 1                                                                
ATEND    LLC   R1,1(R3)                                                         
         AR    R3,R1                                                            
         J     AT2                                                              
         SPACE 1                                                                
YES      SR    R1,R1                                                            
         J     *+8                                                              
         SPACE 1                                                                
NO       LA    R1,1                                                             
         LTR   R1,R1                                                            
         SPACE 1                                                                
XIT      XIT1                                                                   
         EJECT                                                                  
*              HOOK TO APPLICATION OR SYSTEM DRIVER                             
         SPACE 3                                                                
GOHOOK   NTR1                                                                   
         CLI   GLAROUT,0                                                        
         JE    XIT                                                              
         CLI   GLAROUT,2           FIRST BYTE INDICATES WHER                    
*                                  1=APPLICATION 2=SYSDRIVER                    
         JE    GOH2                                                             
         JH    GOH3                                                             
         ICM   RF,15,GLAHOOK       PICK UP APPLICATION HOOK                     
         JZ    XIT                                                              
         L     RE,USERRD           USERS RD                                     
         LM    R0,RC,20(RE)        RESTORE USERS R0-RC                          
         BASR  RE,RF               RF CONTAINED A(HOOK ROUTINE)                 
         J     XIT                                                              
         SPACE 1                                                                
GOH2     ICM   RF,15,GLASYSDR                                                   
         JZ    XIT                                                              
         MVC   WORK,0(RF)                                                       
         GOTO1 (RF),DMCB,(RA)                                                   
         J     XIT                                                              
         SPACE 1                                                                
GOH3     GOTO1 =V(DRIVROUT),DMCB,(RA)                                           
         J     XIT                                                              
         SPACE 1                                                                
         EJECT                                                                  
*              TRACE AND PRINTING FACILITIES                                    
         SPACE 3                                                                
TRASOME  NTR1                                                                   
         MVI   SPACES,C' '                                                      
         MVC   SPACES+1(132),SPACES                                             
         L     R2,GLAIO            R2=A(RECORD)                                 
         LH    R4,BGRECLEN                                                      
         SPACE 1                                                                
TR2      LR    R3,R4               R3=AMOUNT LEFT TO PRINT                      
         CHI   R3,132                                                           
         JNH   *+8                                                              
         LHI   R3,132              INSURE NOT MORE THAN L'PRINT LINE            
         BCTR  R3,0                -1 FOR EX MVC                                
         SPACE 1                                                                
         MVC   P,SPACES                                                         
         MVC   P2,SPACES                                                        
         MVC   P3,SPACES                                                        
         EXRL  R3,TRAM1                                                         
         BRAS  RE,PRINTOUT                                                      
         LA    R3,1(R3)                                                         
         GOTO1 HEXOUT,DMCB,(R2),P2,(R3),=C'SEP'                                 
         BCTR  R3,0                                                             
         EXRL  R3,TRAM2                                                         
         BRAS  RE,PRINTOUT                                                      
         LA    RF,P2+1(R3)                                                      
         EXRL  R3,TRAM3                                                         
         BRAS  RE,PRINTOUT                                                      
         BRAS  RE,PRINTOUT                                                      
         SPACE 1                                                                
         LA    R3,1(R3)                                                         
         AR    R2,R3               BUMP TO NEXT POSITION IN RECORD              
         SR    R4,R3               R4=AMOUNT LEFT TO PRINT                      
         BP    TR2                                                              
         J     XIT                                                              
         SPACE 1                                                                
TRAM1    MVC   P(0),0(R2)                                                       
TRAM2    MVC   P(0),P2                                                          
TRAM3    MVC   P(0),0(RF)                                                       
         SPACE 1                                                                
PRINTOUT NTR1                                                                   
         OC    P,SPACES                                                         
         GOTO1 PRINT,DMCB,PFILL,=C'BL01'                                        
         MVC   P,SPACES                                                         
         J     XIT                                                              
         EJECT                                                                  
EXXCR4R4 XC    0(0,R4),0(R4)                                                    
EXXCRERE XC    0(0,RE),0(RE)                                                    
         EJECT                                                                  
         DS    CL256               FORCE DATA AWAY FROM CODE                    
         LTORG                                                                  
         SPACE 1                                                                
TRIMSW   DC    X'00'                                                            
TRIMLEV  DC    X'00'                                                            
FIRSTSW  DC    C'Y'                                                             
MAXREC   DC    PL3'0'                                                           
COUNT    DC    PL8'0'                                                           
*              DSECT FOR INPUT MODULE                                           
         SPACE 3                                                                
DIND     DSECT                                                                  
         SPACE 1                                                                
PFILL    DS    CL1                                                              
P        DS    CL133                                                            
P2       DS    CL132                                                            
P3       DS    CL132                                                            
SPACES   DS    CL133                                                            
RECYN    DS    CL1                                                              
         SPACE 1                                                                
DMWORK   DS    12D                                                              
KEY      DS    CL36                                                             
         SPACE 1                                                                
RECSAVE  DS    XL2000                                                           
RECBUILD DS    XL2000                                                           
         SPACE 1                                                                
DINDX    EQU   *                                                                
         PRINT OFF                                                              
       ++INCLUDE DRGLOBAL                                                       
       ++INCLUDE DRLOCAL                                                        
       ++INCLUDE DRINTRECD2                                                     
       ++INCLUDE DRIVETABLE                                                     
         PRINT ON                                                               
         SPACE 1                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'026DRIVIN    06/28/12'                                      
         END                                                                    
