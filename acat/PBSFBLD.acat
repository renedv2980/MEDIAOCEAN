*          DATA SET PBSFBLD    AT LEVEL 011 AS OF 05/01/02                      
*CATALP SFBLD                                                                   
         TITLE 'CLIENT SORT FILE BUILD'                                         
SFBLD    CSECT                                                                  
         PRINT GEN                                                              
*                                                                               
* SAVE REGISTERS - SETUP LINKAGE                                                
*                                                                               
         USING *,15                                                             
         SAVE  (14,12),,*                                                       
         DROP  15                                                               
START    DC    0Y(0)                                                            
         BASR  12,0                                                             
         USING *,12                                                             
         ST    13,SAVEAREA+4                                                    
         LA    13,SAVEAREA                                                      
*                                                                               
* EQUATE REGISTERS                                                              
*                                                                               
R0       EQU   0                                                                
R1       EQU   1                                                                
R2       EQU   2                                                                
R3       EQU   3                                                                
R4       EQU   4                                                                
R5       EQU   5                                                                
R6       EQU   6                                                                
R7       EQU   7                                                                
R8       EQU   8                                                                
R9       EQU   9                                                                
RA       EQU   10                                                               
RB       EQU   11                                                               
RC       EQU   12                                                               
RD       EQU   13                                                               
RE       EQU   14                                                               
RF       EQU   15                                                               
         EJECT                                                                  
*                                                                               
* PAN BOOKS ARE PLACED INTO A PDS FOR INPUT TO THIS PROGRAM.                    
*                                                                               
* PROGRAM BUILDS A 244-BYTE RECORD FOR OUTPUT:                                  
*                                                                               
* BYTES   1 - 80  = FILTER LINE ('*' IN CC 1)                                   
* BYTES  81 - 120 = NAME LINE                                                   
* BYTES 121 - 160 = FIRST LINE OF ADDRESS (CC 1-4 AGENCY CODE)                  
* BYTES 161 - 200 = SECOND LINE OF ADDRESS                                      
* BYTES 201 - 240 = THIRD LINE OF ADDRESS                                       
* BYTES 241 - 244 = AGENCY CODE (SORT KEY)                                      
*                                                                               
         OPEN  (INFIL,(INPUT),OUTFIL,(OUTPUT))                                  
*                                                                               
READ1    DC    0Y(0)                                                            
         LA    R6,OBUFF                                                         
         GET   INFIL,IBUFF                                                      
         CLI   IBUFF,C'*'                                                       
         BNE   READ1                                                            
         MVC   0(80,R6),IBUFF                                                   
READ2    DC    0Y(0)                                                            
         GET   INFIL,IBUFF                                                      
         CLC   IBUFF(10),SPACES                                                 
         BE    WRITEIT                                                          
         MVC   80(40,R6),IBUFF                                                  
READ3    DC    0Y(0)                                                            
         GET   INFIL,IBUFF                                                      
         CLC   IBUFF(10),SPACES                                                 
         BE    WRITEIT                                                          
         MVC   240(4,R6),IBUFF                                                  
         MVC   120(40,R6),IBUFF                                                 
READ4    DC    0Y(0)                                                            
         GET   INFIL,IBUFF                                                      
         CLC   IBUFF(10),SPACES                                                 
         BE    WRITEIT                                                          
         MVC   160(40,R6),IBUFF                                                 
READ5    DC    0Y(0)                                                            
         GET   INFIL,IBUFF                                                      
         CLC   IBUFF(10),SPACES                                                 
         BE    WRITEIT                                                          
         MVC   200(40,R6),IBUFF                                                 
*                                                                               
WRITEIT  DC    0Y(0)                                                            
         PUT   OUTFIL,OBUFF                                                     
         MVI   OBUFF,X'40'                                                      
         MVC   OBUFF+1(243),OBUFF                                               
         B     READ1                                                            
*                                                                               
* DONE                                                                          
*                                                                               
EOJRT    DC    0Y(0)                                                            
         CLOSE (INFIL,,OUTFIL)                                                  
         L     13,SAVEAREA+4                                                    
         RETURN (14,12),T,RC=0                                                  
*                                                                               
IBUFF    DS    CL80                                                             
*                                                                               
OBUFF    DC    244C' '                                                          
*                                                                               
SAVEAREA DS    18F                                                              
*                                                                               
SPACES   DC    10C' '                                                           
         LTORG                                                                  
         DC    0F'0'                                                            
INFIL    DCB   DSORG=PS,                                               C        
               MACRF=(GM),                                             C        
               DDNAME=INPUT,                                           C        
               EODAD=EOJRT                                                      
*                                                                               
**                                                                              
*                                                                               
         DC    0F'0'                                                            
OUTFIL   DCB   DSORG=PS,                                               C        
               MACRF=(PM),                                             C        
               DDNAME=OUTPUT,                                          C        
               LRECL=244,                                              C        
               RECFM=FB                                                         
*                                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'011PBSFBLD   05/01/02'                                      
         END   SFBLD                                                            
