*          DATA SET FAGETEMAIL AT LEVEL 002 AS OF 02/11/16                      
*CATALP FAGEMAIL                                                                
         TITLE 'Get E-mail for notification'                                    
***********************************************************************         
* FACPAK builds list of emails for notification based on control                
*        file records. There are in core so we don't have to do an IO           
*        while running FACPAK code                                              
*        PARMS P1 (R5) = A(UTL)                                                 
*              P2 (R6) = A(EMAIL address)                                       
*              P3 (R7) = AL1 System Overlay #                                   
*                      = AL3 Length of email field (Max 40)                     
*                                                                               
***********************************************************************         
         PRINT NOGEN                                                            
         USING UTLD,R5                                                          
         USING WORKD,RC                                                         
GETEMAIL CSECT                                                                  
         NMOD1 WORKLNQ,*GEMAIL*                                                 
         SAM31                                                                  
         MVC   PARMS,0(R1)                                                      
         LM    R5,R7,0(R1)                                                      
         NILH  GR7,X'0000'         Clear high half of R7                        
                                                                                
         ICM   R2,15,=V(NOTFYTAB)                                               
         BZ    GETENO                                                           
                                                                                
         LTR   R7,R7               Length                                       
         BZ    GETENO                                                           
         CHI   R7,L'DEFAULT                                                     
         BH    GETENO                                                           
         MVI   0(R6),C' '          Clear down                                   
         SHI   R7,2                                                             
         EX    R7,SPACEFIL                                                      
         AHI   R7,1                Add back one                                 
                                                                                
         MVC   FULL,0(R2)          SAVE # OF ENTRIES                            
         AHI   R2,4                BUMP PAST # OF ENTRIES                       
         MVC   DUB+0(1),P3         DUB WILL HAVE SYSTEM/PROGRAM                 
         MVC   DUB+1(1),TPRG                                                    
         OC    TSVCREQ,TSVCREQ                                                  
         BZ    *+14                                                             
         MVC   DUB(2),TSVCREQ                                                   
         MVI   DUB,1               SET SYSTEM TO SERVICE                        
                                                                                
         BRAS  R9,FINDEM           FIND EMAIL BASED ON SYSTEM/PROGRAM           
         BE    GETEMXIT            HAVE IT, EMAIL IT                            
         MVI   DUB+1,0             CLEAR PROGRAM                                
         BRAS  R9,FINDEM           FIND EMAIL BASED ON SYSTEM                   
         BE    GETEMXIT                                                         
         LA    R1,DEFAULT          Default E-mail                               
         CHI   R7,L'DEFAULT-1                                                   
         BH    *+8                                                              
         LA    R7,L'DEFAULT-1                                                   
         EX    R7,MOVEMAIL                                                      
                                                                                
GETEMXIT XIT1  ,                                                                
                                                                                
GETENO   LTR   RE,RE                                                            
         B     GETEMXIT                                                         
         DROP  R5                                                               
         EJECT ,                                                                
*---------------------------------------------------------------------          
* BINARY SEARCH ON SYSTEM PROGRAM. GET EMAIL FOR NOTIFY                         
* RETURN EQUAL IF FOUND AND NOT EQUAL IF NOT FOUND                              
*---------------------------------------------------------------------          
FINDEM   LA    R0,1                START ENTRY OR LOW INDEX                     
         ICM   R1,15,FULL          END ENTRY OR HIGH INDEX                      
         BZ    FINDEXIT            NO ITEMS IN TABLE                            
                                                                                
FINDEM10 LR    R3,R1               BINARY SEARCH                                
         SR    R3,R0               HIGH INDEX - LOW INDEX                       
         AHI   R3,1                TRUE # OF ENTRIES                            
         SRL   R3,1                # ENTRIES DIVIDE BY 2                        
         AR    R3,R0               ADD LOW INDEX (BASE)                         
         LR    RE,R3                                                            
         BCTR  RE,0                ADJUST FOR BASE ZERO                         
         MHI   RE,NTFYLNQ          ENTRY LENGTH                                 
         AR    RE,R2               R2=A(START OF TABLE)                         
                                                                                
         USING NTFYD,RE                                                         
         CLC   DUB(2),NTFYSP       MATCH SYSTEM & PROGRAM NUMBER                
         BL    FINDEM20                                                         
         BH    FINDEM30                                                         
         LLC   RF,NTFYXLEN         GET EX LENGTH                                
         CR    RF,R7                                                            
         SR    R1,R1                                                            
         ICM   R1,3,NTFYDISP       DISPLACEMENT TO EMAIL ADDRESS                
         A     R1,=V(NOTFYTAB)     A(BASE OF TABLE)                             
         EX    RF,MOVEMAIL                                                      
         SR    RE,RE               SET CC TO EQUAL FOR FOUND                    
         B     FINDEXIT            OUT OF HERE                                  
         DROP  RE                                                               
*                                  LOW                                          
FINDEM20 SHI   R3,1                LESS THE ONE WE JUST CHECKED                 
         BNP   FINDEXIT                                                         
         LR    R1,R3               CHANGE HIGH INDEX TO BE LOWER                
         CR    R0,R1               CHECK LOW INDEX > HIGH INDEX                 
         BNH   FINDEM10                                                         
         B     FINDEXIT                                                         
*                                  HIGH                                         
FINDEM30 AHI   R3,1                ADD ONE FOR THE ONE WE JUST CHECKED          
         LR    R0,R3               CHANGE LOW  INDEX TO BE HIGHER               
         CR    R0,R1               CHECK LOW INDEX > HIGH INDEX                 
         BNH   FINDEM10                                                         
                                                                                
FINDEXIT LTR   RE,RE               SET CONDITION CODE                           
         BR    R9                                                               
                                                                                
         LTORG                                                                  
                                                                                
SPACEFIL MVC   1(0,R6),0(R6)       Space file                                   
MOVEMAIL MVC   0(0,R6),0(R1)                                                    
DEFAULT  DS    0CL40                                                            
*&&UK*&& DC    CL40'RICHARD.MORSE@DDS.CO.UK'                                    
*&&US*&& DC    CL40'US-MF_FAC_NOTIFY,RCRIN,AATK'                                
         EJECT ,                                                                
         LTORG                                                                  
***********************************************************************         
* Working storage                                                     *         
***********************************************************************         
WORKD    DSECT                                                                  
YES      EQU   C'Y'                                                             
NO       EQU   C'N'                                                             
         DS    0D                                                               
PARMS    DS    0XL12                                                            
P1       DS    A                   A(UTL)                                       
P2       DS    A                   A(Return field)                              
P3       DS    A                   Length of return field                       
                                                                                
DUB      DS    D                                                                
FULL     DS    F                                                                
WORKLNQ  EQU   *-WORKD                                                          
         EJECT                                                                  
*************************************************************                   
*        Other DSECTS                                       *                   
*************************************************************                   
*FANTFYD                                                                        
       ++INCLUDE FANTFYD                                                        
* FAUTL                                                                         
         PRINT OFF                                                              
       ++INCLUDE FAUTL                                                          
         PRINT ON                                                               
* FASSB                                                                         
*        PRINT OFF                                                              
*      ++INCLUDE FASSB                                                          
*        PRINT ON                                                               
* FASSBOFF                                                                      
*        ORG   SSBD                                                             
*        PRINT OFF                                                              
*      ++INCLUDE FASSBOFF                                                       
*        PRINT ON                                                               
* FATCB                                                                         
*        PRINT OFF                                                              
*      ++INCLUDE FATCB                                                          
*        PRINT ON                                                               
* DMDSHDR                                                                       
*        PRINT OFF                                                              
*      ++INCLUDE DMDSHDR                                                        
*        PRINT ON                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'002FAGETEMAIL02/11/16'                                      
         END                                                                    
