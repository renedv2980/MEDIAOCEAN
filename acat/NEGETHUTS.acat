*          DATA SET NEGETHUTS  AT LEVEL 096 AS OF 05/01/02                      
*CATALP GETHUT                                                                  
         TITLE 'GETHUT -  MODULE TO HANDLE HUT READING FOR NETPAK'              
         PRINT NOGEN                                                            
*                                  (PHASE IS T00A33)                            
GETHUT   CSECT                                                                  
         NMOD1 HUTDX-HUTD,*GETHUT*,R8                                           
         USING HUTD,RC                                                          
         MVC   USERRD,4(RD)        SAVE USERS RD                                
         MVI   HUTWHERE,0                                                       
         L     R9,0(R1)                                                         
         USING GHBLOCK,R9                                                       
         SPACE 1                                                                
         L     R2,GHCOMFCS         DEVELOP VTYPES                               
         USING COMFACSD,R2                                                      
         MVC   CALLOV,CCALLOV                                                   
         MVC   DATAMGR,CDATAMGR                                                 
         MVC   DATCON,CDATCON                                                   
         MVC   GETDAY,CGETDAY                                                   
         MVC   ADDAY,CADDAY                                                     
         SPACE 1                                                                
         XC    DMCB(12),DMCB       DUMMY CALLOV FOR DEMAND                      
         MVI   DMCB+4,C'R'                                                      
         MVC   DMCB+5(3),=X'000ADD'                                             
         GOTO1 CALLOV,DMCB                                                      
         MVC   DEMAND,DMCB                                                      
         XC    DMCB(12),DMCB       AND DEMOUT                                   
         MVI   DMCB+4,C'R'                                                      
         MVC   DMCB+5(3),=X'000ADF'                                             
         GOTO1 CALLOV,DMCB                                                      
         MVC   DEMOUT,DMCB                                                      
         MVC   NETWEEK,GHNETWK                                                  
         OC    NETWEEK,NETWEEK     TEST IF V(NETWEEK) PASSED                    
         BNZ   GET1                YES                                          
         XC    DMCB(12),DMCB       DUMMY CALLOV FOR CORE-RES                    
         MVI   DMCB+4,C'R'                                                      
         MVC   DMCB+5(3),=X'000A17'                                             
         GOTO1 CALLOV,DMCB                                                      
         MVC   NETWEEK,DMCB                                                     
         SPACE 1                                                                
GET1     BAS   RE,FIGYEAR          DEVELOP A YEAR AND N'YEARS                   
         BAS   RE,FIGWEEK          DEVELOP WEEK LIST AND N'WEEKS                
         CLI   GHERROR,0                                                        
         BNE   XIT                                                              
         SPACE 1                                                                
         XC    GHOUTPUT,GHOUTPUT                                                
         MVC   ADHUT,GHADHUT       DID USER PROVIDE A X'90' ELEMENT?            
         OC    ADHUT,ADHUT                                                      
         BZ    GET2                                                             
         BAS   RE,AVE90            DEAL WITH IT NOW                             
         MVC   GHHUT,HUT                                                        
         B     XIT                                                              
         EJECT                                                                  
*              CONTROL THE I/O - MULTIPLE YEARS - ADJUSTMENT                    
         SPACE 2                                                                
GET2     BAS   RE,FIGYEAR          DEVELOP YEAR PARAMETERS                      
         XC    GHPCT,GHPCT         CLEAR HUT ADJUSTMENT FOUND                   
         BAS   RE,HOLIHUT          CHECK FOR HOLIDAY OVERRIDES                  
         BAS   RE,SETDT            SET STANDARD DAY AND TIME LIST               
         ZIC   R2,STARTYR                                                       
         ZIC   R0,NYEARS                                                        
         XC    TOTHUT,TOTHUT                                                    
         XC    TOTHITS,TOTHITS                                                  
         MVC   GHTHSSCM,GHSCHEME                                                
         MVC   GHTHSYR,STARTYR     SEE IF ITS THE SAME AS LAST                  
         MVC   GHTHSNYR,NYEARS                                                  
         MVC   GHTHSWK,WEEKLIST                                                 
         MVC   GHTHSNWK,NWEEKS                                                  
         MVC   GHTHSTM,TIMELIST                                                 
         MVC   GHTHSNTM,NTIMES                                                  
         MVC   GHTHSDAY,DAY                                                     
         CLC   GHTHIS,GHLAST                                                    
         MVC   GHLAST,GHTHIS                                                    
         BNE   GET4                                                             
         LH    R1,GHLSTHUT         YES - SO SAVE ALL THE WORK                   
         B     GET6                                                             
         SPACE 1                                                                
GET4     STC   R2,THISYEAR         FOR EACH YEAR                                
         BAS   RE,GET10            GO AND HANDLE RECORDS                        
         LA    R2,1(R2)                                                         
         BCT   R0,GET4                                                          
         CLI   GHPREVYR,C'N'       BYPASS PREV YEAR LOGIC                       
         BE    GET5                                                             
         CLI   NYEARS,1            IF REQUEST WAS FOR 1 YEAR                    
         BNE   GET5                                                             
         OC    TOTHUT,TOTHUT       AND WE HAVE NOT FOUND ANY HUT YET            
         BNZ   GET5                                                             
         ZIC   R1,STARTYR          THEN TRY ONCE FOR PREVIOUS YEAR              
         BCTR  R1,0                                                             
         STC   R1,THISYEAR                                                      
         BAS   RE,GET10                                                         
         SPACE 1                                                                
GET5     L     R0,TOTHUT           AVERAGE                                      
         SRDA  R0,31                                                            
         XC    GHLSTHUT,GHLSTHUT   CLEAR LAST HUT                               
         OC    TOTHITS,TOTHITS                                                  
         BZ    XIT                                                              
         D     R0,TOTHITS                                                       
         AH    R1,=H'1'                                                         
         SRA   R1,1                                                             
         SPACE 1                                                                
GET6     STH   R1,GHHUT            AND PASS BACK HUT                            
         STH   R1,GHLSTHUT                                                      
         OC    GHPCT,GHPCT                                                      
         BZ    XIT                                                              
         SPACE 1                                                                
         LH    R0,GHPCT            APPLY ADJUSTMENT FACTOR                      
         LH    R1,GHHUT                  (2 DEC PLACES + OR -)                  
         MR    R0,R0                                                            
         D     R0,=F'5000'                                                      
         LTR   R1,R1                                                            
         BM    *+8                                                              
         AH    R1,=H'1'                                                         
         SRA   R1,1                                                             
         AH    R1,GHHUT                                                         
         STH   R1,GHHUT                                                         
         B     XIT                                                              
         EJECT                                                                  
*              HANDLE I/O FOR 1 YEAR                                            
         SPACE 3                                                                
GET10    NTR1                                                                   
         LA    R4,KEY                                                           
         XC    KEY,KEY             SET UP KEY FOR AGENCY SCHEME                 
         CLI   GHSCHEME,0                                                       
         BE    GET12                                                            
         CLI   GHSCHEME,X'FE'                                                   
         BE    GET28                                                            
         CLI   GHSCHEME,X'FF'                                                   
         BE    GET30                                                            
         USING NAHRECD,R4                                                       
         MVC   NAHKTYPE,=X'0D50'                                                
         MVC   NAHKAM,GHAGYMED                                                  
         MVC   NAHKSCHM,GHSCHEME                                                
         MVC   NAHKDAY,REPDAY                                                   
         MVC   NAHKYEAR,THISYEAR                                                
         MVI   FILE,C'S'                                                        
         LA    R2,NAHKTIME                                                      
         B     GET14                                                            
         SPACE 1                                                                
         USING CTHREC,R4                                                        
GET12    CLI   THISYEAR,84         CANT SUPPORT CONTROL AFTER 84                
         BH    GET30                                                            
         CLI   GH52,C'Y'           WILL NEED DEMOS FOR 52 WEEKS                 
         BE    GET30                                                            
         SPACE 1                                                                
GET13    MVI   CTHTYP,C'H'                                                      
         MVC   CTHDAY,DAY                                                       
         MVC   CTHYEAR,THISYEAR                                                 
         MVI   FILE,C'C'                                                        
         LA    R2,CTHTIME                                                       
         SPACE 1                                                                
GET14    LA    R3,TIMELIST                                                      
         ZIC   R0,NTIMES                                                        
         MVC   KEYSAVE,KEY                                                      
         SPACE 1                                                                
GET16    MVC   0(2,R2),0(R3)                                                    
         MVC   THISTIME,0(R3)                                                   
         BAS   RE,GET20                                                         
         LA    R3,2(R3)                                                         
         BCT   R0,GET16                                                         
         B     XIT                                                              
         EJECT                                                                  
*              HANDLE THE I/O FOR 1 1/2 HOUR                                    
         SPACE 3                                                                
GET20    NTR1                                                                   
         BAS   RE,HIGH                                                          
         LA    R6,IO                                                            
         CLC   KEY(13),KEYSAVE                                                  
         BNE   GET24                                                            
         CLI   FILE,C'S'                                                        
         BE    GET22                                                            
         CLC   KEY(25),KEYSAVE                                                  
         BNE   GET24                                                            
         SPACE 1                                                                
GET22    MVI   ELCODE,X'90'                                                     
         BAS   RE,GETEL                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
         ST    R6,ADHUT                                                         
         BAS   RE,AVE90                                                         
         L     R1,TOTHITS                                                       
         OC    HUT,HUT             ADD TO HITS IF ANY HUT                       
         BZ    GET24                                                            
         LA    R1,1(R1)                                                         
         ST    R1,TOTHITS                                                       
         LH    R1,HUT                                                           
         A     R1,TOTHUT                                                        
         ST    R1,TOTHUT                                                        
         B     XIT                                                              
         SPACE 1                                                                
GET24    MVC   KEY,KEYSAVE         RESTORE KEY ON A MISS                        
         CLI   GHDEFDEM,C'Y'       OPTION TO USE DEMO FILE                      
         BNE   XIT                 IF AGENCY SCHEME NOT FOUND                   
         MVI   THISWEEK,0                                                       
         MVI   HUTWHERE,X'FE'                                                   
         BAS   RE,GET38                                                         
         MVI   HUTWHERE,0                                                       
         B     XIT                                                              
         EJECT                                                                  
*              HANDLE DEMAND INTERFACE                                          
         SPACE 3                                                                
GET28    CLI   THISYEAR,80         NO DEMO RECORDS BEFORE 80                    
         BL    GET13                                                            
         MVI   THISWEEK,0          HANDLING YEAR RECORDS                        
         BAS   RE,GET34                                                         
         B     XIT                                                              
         SPACE 1                                                                
GET30    LA    R2,WEEKLIST         SET UP FOR MULTIPLE WEEKS                    
         ZIC   R0,NWEEKS                                                        
         SPACE 1                                                                
GET32    MVC   THISWEEK,0(R2)                                                   
         BAS   RE,GET34                                                         
         LA    R2,1(R2)                                                         
         BCT   R0,GET32                                                         
         B     XIT                                                              
         SPACE 1                                                                
GET34    NTR1                                                                   
         LA    R2,TIMELIST         SET UP FOR MULTIPLE TIMES                    
         ZIC   R0,NTIMES                                                        
         CLI   GH52,C'Y'                                                        
         BE    GET36                                                            
         CLI   THISWEEK,48                                                      
         BH    XIT                                                              
         SPACE 1                                                                
GET36    MVC   THISTIME,0(R2)                                                   
         BAS   RE,GET38                                                         
         LA    R2,2(R2)                                                         
         BCT   R0,GET36                                                         
         B     XIT                                                              
         EJECT                                                                  
GET38    NTR1                                                                   
         LA    R7,HUTDB            SET UP DBLOCK                                
         USING DBLOCK,R7                                                        
         XC    HUTDB,HUTDB                                                      
         MVC   DBFILE,=C'NTI'                                                   
         MVI   DBSELSRC,C'N'                                                    
         MVC   DBSELBK(1),THISYEAR                                              
         MVC   DBSELBK+1(1),THISWEEK                                            
         CLI   GHSURVY,C'H'        DOESN'T APPLY FOR HISPANIC                   
         BE    GET38A                                                           
         MVC   DBBTYPE,GHBKTYPE                                                 
         CLI   DBBTYPE,C'D'                                                     
         BNE   *+8                                                              
         MVI   DBBTYPE,0           DEFAULT FOR DIARY IS X'00'                   
         CLI   DBBTYPE,C'O'        (EXCEPT FOR RESEARCH PROGRAMS -              
         BNE   *+8                  WHERE GHBKTYPE IS SET TO 'O'                
         MVI   DBBTYPE,C'D'         IN ORDER TO FORCE DBBTYPE TO 'D')           
GET38A   MVI   DBSELMED,C'N'                                                    
         MVC   DBSELAGY,=C'SJ'     (USE 'SJ' AS DEFAULT AGENCY)                 
         OC    GHAGY,GHAGY                                                      
         BZ    *+10                                                             
         MVC   DBSELAGY,GHAGY      AGENCY                                       
         MVC   DBSELSTA(5),=C'HUT T'                                            
         CLI   GHSURVY,C'H'        HISPANIC?                                    
         BNE   *+8                                                              
         MVI   DBSELSTA+4,C'H'                                                  
         MVC   DBSELDAY,DAY                                                     
         MVC   DUB,THISTIME                                                     
         LH    R1,DUB              START TIME                                   
         SR    R0,R0                                                            
         D     R0,=F'100'                                                       
         MH    R1,=H'100'                                                       
         LA    R1,30(R1)                                                        
         LTR   R0,R0                                                            
         BZ    *+8                                                              
         LA    R1,70(R1)                                                        
         STH   R1,DUB+2            AND NEXT HALF HOUR                           
         MVC   DBSELTIM,DUB                                                     
         LA    R1,IO                                                            
         ST    R1,DBAREC                                                        
         MVC   DBCOMFCS,GHCOMFCS                                                
         MVI   DBFUNCT,DBGETDEM                                                 
         SPACE 1                                                                
         XC    THISHUT,THISHUT                                                  
         XC    HUTHITS,HUTHITS                                                  
         ZIC   RE,DBSELBK+1                                                     
         CLI   DBSELBK,94                                                       
         BNE   WKOK                                                             
         LA    RE,HF94LIST(RE)                                                  
         MVC   DBSELBK+1(1),0(RE)                                               
         B     WKOK                                                             
*                                                                               
HF94LIST DC    AL1(0,1,2,3,4,5,6,7,8,9,10,11,12)                                
         DC    AL1(13,14,15,16,18,19,20,21,22,23,24,25)                         
         DC    AL1(27,28,29,39,31,32,33,34,36,37,38,39)                         
         DC    AL1(40,41,42,43,44,45,46,47,48,17,26,35,52,52)                   
*                                                                               
WKOK     DS    0H                                                               
         GOTO1 DEMAND,DMCB,DBLOCK,GET40                                         
         CLI   GHSCHEME,X'FE'                                                   
         BE    XIT                                                              
         CLI   HUTWHERE,X'FE'                                                   
         BE    XIT                                                              
         LH    R0,THISHUT          AVERAGE WHAT WE GOT                          
         SRDA  R0,31                                                            
         LTR   R1,R1                                                            
         BZ    XIT                                                              
         LH    RF,HUTHITS                                                       
         DR    R0,RF                                                            
         AH    R1,=H'1'                                                         
         SRA   R1,1                                                             
         STH   R1,THISHUT                                                       
         A     R1,TOTHUT                                                        
         ST    R1,TOTHUT                                                        
         L     R1,TOTHITS                                                       
         OC    THISHUT,THISHUT     ADD TO HITS IF ANY HUT                       
         BZ    *+8                                                              
         LA    R1,1(R1)                                                         
         ST    R1,TOTHITS                                                       
         BAS   RE,GOHOOK                                                        
         B     XIT                                                              
         EJECT                                                                  
GET40    NTR1                                                                   
*                                  THIS IS THE HOOK FOR DEMAND                  
         LH    R1,GHIOCNT                                                       
         LA    R1,1(R1)                                                         
         STH   R1,GHIOCNT                                                       
         CLI   GHSCHEME,X'FE'                                                   
         BE    GET42                                                            
         CLI   HUTWHERE,X'FE'                                                   
         BE    GET42                                                            
         SPACE 1                                                                
         MVC   DUB(4),=X'00D901FF' CALL FOR HUT                                 
         GOTO1 DEMOUT,DMCB,(C'L',DUB),DBLOCK,DUB+4                              
         L     R1,DUB+4                                                         
         AH    R1,THISHUT                                                       
         STH   R1,THISHUT                                                       
         LH    R1,HUTHITS                                                       
         OC    DUB+4(4),DUB+4      ADD TO HITS IF ANY HUT                       
         BZ    *+8                                                              
         LA    R1,1(R1)                                                         
         STH   R1,HUTHITS                                                       
         XIT1                                                                   
         SPACE 1                                                                
GET42    LA    R6,IO                                                            
         MVI   ELCODE,X'90'                                                     
         MVC   DATADISP,=H'23'                                                  
         BAS   RE,GETEL                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
         ST    R6,ADHUT                                                         
         CLI   DBSELBK,94                                                       
         BNE   GET42A                                                           
         CLI   DBSELBK+1,0                                                      
         BNE   *+8                                                              
         BAS   RE,CNV94                                                         
GET42A   BAS   RE,AVE90                                                         
         L     R1,TOTHITS                                                       
         OC    HUT,HUT             ADD TO HITS IF ANY HUT                       
         BZ    *+8                                                              
         LA    R1,1(R1)                                                         
         ST    R1,TOTHITS                                                       
         LH    R1,HUT                                                           
         A     R1,TOTHUT                                                        
         ST    R1,TOTHUT                                                        
         MVI   THISWEEK,0                                                       
         B     XIT                                                              
         SPACE 1                                                                
         EJECT                                                                  
*              FIGURE OUT WEEK LIST AND N'WEEKS FROM DATA GIVEN                 
         SPACE 3                                                                
FIGWEEK  NTR1                                                                   
         XC    WEEKLIST,WEEKLIST                                                
         OC    GHBOOKS,GHBOOKS     DID USER PROVIDE BOOKS ?                     
         BZ    FIG2                                                             
         LA    R1,FIGLIST          FIND START IN FIGLIST                        
         SPACE 1                                                                
FIGB     CLC   0(1,R1),GHBOOKS+1                                                
         BE    FIGD                                                             
         LA    R1,1(R1)                                                         
         B     FIGB                                                             
         SPACE 1                                                                
FIGD     LA    R2,WEEKLIST                                                      
         SR    R3,R3                                                            
         SPACE 1                                                                
FIGG     MVC   0(1,R2),0(R1)                                                    
         LA    R3,1(R3)                                                         
         CLC   0(1,R2),GHBOOKS+3                                                
         BE    FIGK                                                             
         OC    GHBOOKS+2(2),GHBOOKS+2                                           
         BZ    FIGK                                                             
         LA    R1,1(R1)                                                         
         LA    R2,1(R2)                                                         
         B     FIGG                                                             
         SPACE 1                                                                
FIGK     STC   R3,NWEEKS                                                        
         B     XIT                                                              
         SPACE 1                                                                
FIGLIST  DC    AL1(1,2,3,4,5,6,7,8,9,10,11,12)                                  
         DC    AL1(13,14,15,16,49,17,18,19,20,21,22,23,24,50)                   
         DC    AL1(25,26,27,28,29,30,31,32,51,33,34,35,36)                      
         DC    AL1(37,38,39,40,41,42,43,44,45,46,47,48,52,53)                   
         DC    AL1(1,2,3,4,5,6,7,8,9,10,11,12)                                  
         DC    AL1(13,14,15,16,49,17,18,19,20,21,22,23,24,50)                   
         DC    AL1(25,26,27,28,29,30,31,32,51,33,34,35,36)                      
         DC    AL1(37,38,39,40,41,42,43,44,45,46,47,48,52,53)                   
         SPACE 2                                                                
HF94TAB  DC    AL1(1,2,3,4,5,6,7,8,9,10,11,12)                                  
         DC    AL1(13,14,15,16,49,17,18,19,20,21,22,23,24,50)                   
         DC    AL1(25,26,27,28,29,30,31,32,51,33,34,35,36)                      
         DC    AL1(37,38,39,40,41,42,43,44,45,46,47,48,52,53)                   
         DC    X'FF'                                                            
         SPACE 1                                                                
FIG2     OC    GHDATE,GHDATE       OTHERWISE MUST BE A DATE                     
         BNZ   FIG4                                                             
         MVI   GHERROR,NODATE                                                   
         B     XIT                                                              
         SPACE 1                                                                
FIG4     GOTO1 DATCON,DMCB,(2,GHDATE),(0,DUB)                                   
         GOTO1 NETWEEK,DMCB,DUB,GETDAY,ADDAY                                    
         CLC   DUB(2),=C'94'       CONVERT NEW WEEKS TO OLD BECUASE             
         BNE   FIG6                HUTS IN OLD FORMAT FOR USER RECS             
         ZIC   RE,8(R1)                                                         
         LA    RE,FIGLIST-1(RE)                                                 
         MVC   8(1,R1),0(RE)                                                    
FIG6     MVC   BWEEK,0(R1)                                                      
         MVC   WEEKLIST(1),0(R1)                                                
         CLI   GH52,C'Y'                                                        
         BNE   *+10                                                             
         MVC   WEEKLIST(1),8(R1)                                                
         CLI   WEEKLIST,53                                                      
         BNE   *+8                                                              
         MVI   WEEKLIST,51                                                      
         MVI   NWEEKS,1                                                         
         CLI   GHAVE,C'W'          NOW TEST AVERAGING SCHEME                    
         BE    XIT                 WEEK IS DONE                                 
         SPACE 1                                                                
         BAS   RE,GETMQ            DEVELOP MONTH AND QUARTER                    
         ZIC   R1,BMONTH           MONTHLY                                      
         BCTR  R1,0                                                             
         MH    R1,=H'7'                                                         
         CLI   GHYEAR,96                                                        
         BH    M6SET                                                            
         CLI   BYEAR,96                                                         
         BH    M6SET                                                            
         CLI   GHYEAR,96                                                        
         BE    *+8                                                              
         CLI   BYEAR,96                                                         
         BNE   *+12                                                             
M6SET    LA    R1,M6THLIST(R1)                                                  
         B     GOTMON                                                           
         CLI   GHYEAR,95                                                        
         BE    *+8                                                              
         CLI   BYEAR,95                                                         
         BE    *+12                                                             
         LA    R1,MNTHLIST(R1)                                                  
         B     GOTMON                                                           
         LA    R1,M5THLIST(R1)                                                  
GOTMON   MVC   NWEEKS,0(R1)                                                     
         MVC   WEEKLIST(06),1(R1)                                               
         CLI   GHAVE,C'M'                                                       
         BE    XIT                                                              
         CLI   GHAVE,0                                                          
         BE    XIT                                                              
         SPACE 1                                                                
         ZIC   R1,BQUARTER         QUARTERLY                                    
         BCTR  R1,0                                                             
         MH    R1,=H'17'                                                        
         SPACE 1                                                                
         ZIC   RF,STARTYR          DETEMINE END YEAR                            
         ZIC   RE,NYEARS                                                        
         AR    RF,RE                                                            
         LA    RE,QRTLISTA         PRIOR 1989 QUARTERS                          
         CH    RF,=H'89'                                                        
         BL    *+8                                                              
         LA    RE,QURTLIST         1989 AND SUBS QUARTERS                       
         CH    RF,=H'95'                                                        
         BL    *+8                                                              
         LA    RE,Q5RTLIST         1995 QUARTERS                                
         CH    RF,=H'96'                                                        
         BL    *+8                                                              
         LA    RE,Q6RTLIST         1996 QUARTERS                                
         SPACE 1                                                                
         AR    R1,RE               POINT TO PROPER QUARTER TABLES               
         MVC   NWEEKS,0(R1)                                                     
         MVC   WEEKLIST(16),1(R1)                                               
         CLI   GHAVE,C'Q'                                                       
         BE    XIT                                                              
         SPACE 1                                                                
         MVI   GHERROR,INVAVE                                                   
         B     XIT                                                              
         SPACE 1                                                                
MNTHLIST DS    0H                                                               
         DC    AL1(4,01,02,03,04,00,00) JAN                                     
         DC    AL1(4,05,06,07,08,00,00) FEB                                     
         DC    AL1(4,09,10,11,12,00,00) MAR                                     
         DC    AL1(5,13,14,15,16,49,00) APR                                     
         DC    AL1(4,17,18,19,20,00,00) MAY                                     
         DC    AL1(5,21,22,23,24,50,00) JUN                                     
         DC    AL1(4,25,26,27,28,00,00) JUL                                     
         DC    AL1(5,29,30,31,32,51,00) AUG                                     
         DC    AL1(4,33,34,35,36,00,00) SEP                                     
         DC    AL1(4,37,38,39,40,00,00) OCT                                     
         DC    AL1(4,41,42,43,44,00,00) NOV                                     
         DC    AL1(5,45,46,47,48,52,00) DEC                                     
M5THLIST DS    0H                                                               
         DC    AL1(4,02,03,04,05,00,00) JAN                                     
         DC    AL1(4,06,07,08,09,00,00) FEB                                     
         DC    AL1(4,10,11,12,13,00,00) MAR                                     
         DC    AL1(5,14,15,16,17,18,00) APR                                     
         DC    AL1(4,19,20,21,22,00,00) MAY                                     
         DC    AL1(4,23,24,25,26,00,00) JUN                                     
         DC    AL1(5,27,28,29,30,31,00) JUL                                     
         DC    AL1(5,32,33,34,35,36,00) AUG                                     
         DC    AL1(4,37,38,39,40,00,00) SEP                                     
         DC    AL1(4,41,42,43,44,00,00) OCT                                     
         DC    AL1(4,45,46,47,48,00,00) NOV                                     
         DC    AL1(5,49,50,51,52,53,00) DEC                                     
M6THLIST DS    0H                                                               
         DC    AL1(4,01,02,03,04,00,00) JAN                                     
         DC    AL1(4,05,06,07,08,00,00) FEB                                     
         DC    AL1(5,09,10,11,12,13,00) MAR                                     
         DC    AL1(4,14,15,16,17,00,00) APR                                     
         DC    AL1(4,18,19,20,21,00,00) MAY                                     
         DC    AL1(5,22,23,24,25,26,00) JUN                                     
         DC    AL1(4,27,28,29,30,00,00) JUL                                     
         DC    AL1(5,31,32,33,34,35,00) AUG                                     
         DC    AL1(4,36,37,38,39,00,00) SEP                                     
         DC    AL1(4,40,41,42,43,00,00) OCT                                     
         DC    AL1(4,44,45,46,47,00,00) NOV                                     
         DC    AL1(5,48,59,50,51,52,00) DEC                                     
         SPACE 1                                                                
QURTLIST DS    0H                  START WITH 89                                
Q1       DC    AL1(12,01,02,03,04,05,06,07,08,09,10,11,12,00,00,00,00)          
Q2       DC    AL1(14,13,14,15,16,17,18,19,20,21,22,23,24,49,50,00,00)          
Q3       DC    AL1(11,25,26,27,28,29,30,31,32,33,34,51,00,00,00,00,00)          
Q4       DC    AL1(15,35,36,37,38,39,40,41,42,43,44,45,46,47,48,52,00)          
         SPACE 1                                                                
QRTLISTA DS    0H                  PRIOR TO 89                                  
Q1A      DC    AL1(12,01,02,03,04,05,06,07,08,09,10,11,12,00,00,00,00)          
Q2A      DC    AL1(14,13,14,15,16,17,18,19,20,21,22,23,24,49,50,00,00)          
Q3A      DC    AL1(13,25,26,27,28,29,30,31,32,33,34,35,36,51,00,00,00)          
Q4A      DC    AL1(13,37,38,39,40,41,42,43,44,45,46,47,48,52,00,00,00)          
         SPACE 1                                                                
Q5RTLIST DS    0H                  1995                                         
Q15      DC    AL1(12,02,03,04,05,06,07,08,09,10,11,12,13,00,00,00,00)          
Q25      DC    AL1(13,14,15,16,17,18,19,20,21,22,23,24,25,26,00,00,00)          
Q35      DC    AL1(13,27,28,29,30,31,32,33,34,35,36,37,38,39,00,00,00)          
Q45      DC    AL1(15,39,40,41,42,43,44,45,46,47,48,49,50,51,52,53,00)          
         SPACE 1                                                                
Q6RTLIST DS    0H                  1996                                         
Q16      DC    AL1(13,01,02,03,04,05,06,07,08,09,10,11,12,13,00,00,00)          
Q26      DC    AL1(13,14,15,16,17,18,19,20,21,22,23,24,25,26,00,00,00)          
Q36      DC    AL1(11,27,28,29,30,31,32,33,34,35,36,37,00,00,00,00,00)          
Q46      DC    AL1(15,38,39,40,41,42,43,44,45,46,47,48,49,50,51,52,00)          
         EJECT                                                                  
*              DEVELOP YEAR DATA                                                
         SPACE 3                                                                
FIGYEAR  NTR1                                                                   
         CLI   GHYEAR,0                                                         
         BE    F42                                                              
         ZIC   R1,GHYEAR           USER PROVIDED END YEAR                       
         ZIC   R0,GHNYEARS                   AND NUMBER OF YEARS                
         LTR   R0,R0                                                            
         BNZ   *+8                                                              
         LA    R0,1                                                             
         LA    R1,1(R1)                                                         
         SR    R1,R0                                                            
         B     F44                                                              
         SPACE 1                                                                
F42      ZIC   R1,GHBOOKS          OTHERWISE TAKE YEARS                         
         ZIC   R0,GHBOOKS+2        FROM BOOK EXPRESSION                         
         AH    R0,=H'1'                                                         
         SR    R0,R1                                                            
         SPACE 1                                                                
F44      STC   R1,STARTYR                                                       
         STC   R0,NYEARS                                                        
         B     XIT                                                              
         EJECT                                                                  
*              ROUTINE TO GET MONTH AND QUARTER FROM DATE OR WEEK               
         SPACE 3                                                                
*              INPUTS              BWEEK GHDATE GHFLAVOR                        
*              OUTPUTS             BMONTH (1-12) BQUARTER (1-4)                 
         SPACE 1                                                                
GETMQ    NTR1                                                                   
         CLI   GHFLAVOR,C'B'       NTI CAN BE FIGURED FROM WEEK NO.             
         BE    GETMQ2                                                           
         CLI   GHFLAVOR,C'C'                                                    
         BE    GETMQ2                                                           
         ZIC   R1,BWEEK            MONTH=(WEEK-1)/4 +1                          
         BCTR  R1,0                                                             
         SRL   R1,2                                                             
         LA    R1,1(R1)                                                         
         STC   R1,BMONTH                                                        
         B     GETMQEND                                                         
         SPACE 1                                                                
*                                  OTHER FLAVORS WORK FROM DATE                 
GETMQ2   GOTO1 DATCON,DMCB,(2,GHDATE),(3,BDATE)                                 
         CLI   GHFLAVOR,C'C'                                                    
         BE    GETMQEND                                                         
         GOTO1 DATCON,DMCB,(2,GHDATE),(0,WORK)                                  
         GOTO1 GETDAY,DMCB,WORK,WORK+6                                          
         CLI   DMCB,7              FOR BROADCAST, GET TO NEXT SUNDAY            
         BE    GETMQ4                                                           
         ZIC   R0,DMCB                                                          
         LA    R1,7                                                             
         SR    R1,0                                                             
         ST    R1,DMCB+8                                                        
         GOTO1 ADDAY,DMCB,WORK,WORK+6                                           
         MVC   WORK(6),WORK+6                                                   
         SPACE 1                                                                
GETMQ4   PACK  DUB,WORK+2(2)                                                    
         CVB   R1,DUB                                                           
         STC   R1,BMONTH                                                        
         SPACE 1                                                                
GETMQEND ZIC   R1,BMONTH           QUARTER=(MONTH-1)/3 +1                       
         BCTR  R1,0                                                             
         SR    R0,R0                                                            
         D     R0,=F'3'                                                         
         LA    R1,1(R1)                                                         
         STC   R1,BQUARTER                                                      
         CLI   BWEEK,35            BUT WEEKS 35 AND 36                          
         BNE   *+8                                                              
         MVI   BQUARTER,4          ARE IN THE FOURTH QUARTER                    
         CLI   BWEEK,36                                                         
         BNE   *+8                                                              
         MVI   BQUARTER,4                                                       
         B     XIT                                                              
         EJECT                                                                  
*              HANDLE HOLIDAY HUT PERCENT ADJUSTMENT                            
         SPACE 3                                                                
HOLIHUT  NTR1                                                                   
         MVI   FILE,C'S'                                                        
         OC    GHDATE,GHDATE       ONLY VALID IF USER HAS PROVIDED              
         BZ    XIT                 A DATE                                       
         CLI   GHAGYMED,0          AND AN AGENCY                                
         BE    XIT                                                              
         CLI   GHDAYPRT,0          AND A DAYPART                                
         BE    XIT                                                              
         SPACE 1                                                                
         LA    R4,KEY                                                           
         USING NHHRECD,R4                                                       
         XC    KEY,KEY                                                          
         MVC   NHHKTYPE,=X'0D52'                                                
         MVC   NHHKAM,GHAGYMED                                                  
         MVC   NHHKDATE,GHDATE                                                  
         CLI   GHSCHEME,0          IF SCHEME SELECTED                           
         BE    HH2                                                              
         CLI   GHSCHEME,X'FD'                                                   
         BH    HH2                                                              
         MVC   NHHKSCHM,GHSCHEME   LOOK FOR RECORD FOR SCHEME                   
         BAS   RE,HIGH                                                          
         CLC   KEY(13),KEYSAVE                                                  
         BE    HH4                                                              
         MVC   KEY,KEYSAVE                                                      
         MVI   NHHKSCHM,0                                                       
         SPACE 1                                                                
HH2      BAS   RE,HIGH             OR GENERALLY FOR AGENCY                      
         CLC   KEY(13),KEYSAVE                                                  
         BNE   XIT                                                              
         SPACE 1                                                                
HH4      LA    R6,IO                                                            
         MVI   ELCODE,X'52'                                                     
         BAS   RE,GETEL                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
         DROP  R4                                                               
         USING NHHEL,R6                                                         
         MVC   GHPCT,NHHALL        GOT A HIT - RETURN ALL ADJUSTMENT            
         LA    R2,NHHDAY                                                        
         LA    R3,DPLIST                                                        
         SPACE 1                                                                
HH6      CLI   0(R3),X'FF'         LOOK FOR DAYPART MATCH                       
         BE    XIT                 NONE SO TAKE ALL                             
         CLC   0(1,R3),GHDAYPRT                                                 
         BE    HH8                                                              
         LA    R2,2(R2)                                                         
         LA    R3,1(R3)                                                         
         B     HH6                                                              
         SPACE 1                                                                
HH8      OC    0(2,R2),0(R2)       IF ANY DAYPART OVERRIDE                      
         BZ    XIT                                                              
         MVC   GHPCT,0(R2)         USE THAT                                     
         B     XIT                                                              
         SPACE 1                                                                
DPLIST   DC    C'DFPLKSY'                                                       
         DC    X'FF'                                                            
         EJECT                                                                  
*              SET STANDARD DAY AND TIME LIST                                   
         SPACE 3                                                                
SETDT    NTR1                                                                   
         MVC   DAY,GHDAY           USER CAN PASS SPOT                           
         CLI   DAY,0                                                            
         BNE   SD1                                                              
         ZIC   R1,GHREPDAY              OR REP DAY                              
         SLL   R1,1                X LENGTH OF DAYLIST ENTRY                    
         LA    R1,DAYLIST(R1)      CONVERT TO SPOT                              
         MVC   DAY,0(R1)                                                        
         MVC   REPDAY,GHREPDAY     AND SAVE REP DAY VALUE                       
         B     SD2                                                              
         SPACE 1                                                                
SD1      LA    R0,9                COUNTER                                      
         LA    R1,DAYLIST                                                       
         CLC   DAY,0(R1)           TEST SPOT DAY                                
         BE    *+12                                                             
         LA    R1,2(R1)            NEXT ENTRY                                   
         BCT   R0,*-14                                                          
         MVC   REPDAY,1(R1)        SET REP DAY VALUE                            
         B     SD2                                                              
         SPACE 1                                                                
* DAYLIST ENTRIES CONSIST OF SPOT DAY BITS AND REP DAY VALUE                    
*                                                                               
DAYLIST  DC    X'7C0040012002100308040405020601077F087F09'                      
         SPACE 1                                                                
SD2      MVC   TIMES,GHMILTIM      USER CAN PASS MILITARY TIMES                 
         CLC   TIMES(2),=H'3000'   CHECK FOR BAD START TIME                     
         BL    *+6                                                              
         DC    H'0'                                                             
         CLC   TIMES+2(2),=H'3000'                                              
         BL    *+6                                                              
         DC    H'0'                                                             
         OC    TIMES,TIMES                                                      
         BNZ   SD6                                                              
         LA    R2,GHQUARTS         OR 1/4 HOUR RANGE                            
         LA    R3,TIMES                                                         
         BAS   RE,SD4                                                           
         LA    R2,GHQUARTS+1                                                    
         LA    R3,TIMES+2                                                       
         BAS   RE,SD4                                                           
         B     SD6                                                              
         SPACE 1                                                                
SD4      NTR1                                                                   
         ZIC   R1,0(R2)            OR CONVERT 1/4 HR. TO MILITARY               
         SR    R0,R0                                                            
         D     R0,=F'4'            (HRS IN R1, 1/4'S IN R0)                     
         LA    R1,6(R1)                                                         
         CH    R1,=H'24'                                                        
         BL    *+8                                                              
         SH    R1,=H'24'                                                        
         MH    R1,=H'100'                                                       
         LTR   R0,R0                                                            
         BZ    *+8                                                              
         LA    R1,30(R1)                                                        
         STH   R1,0(R3)                                                         
         B     XIT                                                              
         SPACE 1                                                                
SD6      MVC   TIMELIST(96),PRETIME                                             
         MVI   NTIMES,48           PRESET FOR HUTLIST                           
         CLC   TIMES(4),LISTTIME                                                
         BE    XIT                                                              
         LA    R2,TIMELIST         NOW SET UP LIST                              
         SR    R3,R3                                                            
         LH    R1,TIMES            INSURE START ON HALF HOUR                    
         SR    R0,R0                                                            
         D     R0,=F'100'                                                       
         LTR   R0,R0                                                            
         BZ    *+8                                                              
         LA    R0,30                                                            
         MH    R1,=H'100'                                                       
         AR    R1,R0                                                            
         STH   R1,TIMES                                                         
         LH    R1,TIMES+2          AND END                                      
         SR    R0,R0                                                            
         D     R0,=F'100'                                                       
         LTR   R0,R0                                                            
         BZ    *+8                                                              
         LA    R0,30                                                            
         MH    R1,=H'100'                                                       
         AR    R1,R0                                                            
         STH   R1,TIMES+2                                                       
         SPACE 1                                                                
SD7      OC    TIMES+2(2),TIMES+2  NO END TIME SPECIFIED                        
         BZ    *+14                YES-ONLY ONE ENTRY                           
         CLC   TIMES(2),TIMES+2    TEST FOR SINGLE HALF HOUR                    
         BNE   SD8                 NO-MORE THAN 1 ENTRY                         
         MVC   0(2,R2),TIMES       YES-PROGRAM L.T. 30 MINS.                    
         MVI   NTIMES,1                                                         
         B     XIT                                                              
         SPACE 1                                                                
SD8      MVC   0(2,R2),TIMES                                                    
         LA    R2,2(R2)            POINT TO NEXT POSITION IN LIST               
         LA    R3,1(R3)            INCREMENT LIST ENTRY COUNT                   
         CH    R3,=Y(MAXTIMES)     TEST FOR TABLE OVERFLOW                      
         BNH   *+6                 NO                                           
         DC    H'0'                YES-BLOW UP TO PROTECT ONLINE SYSTEM         
         STC   R3,NTIMES                                                        
         LH    R1,TIMES            GET TO NEXT 1/2 HOUR                         
         SR    R0,R0                                                            
         D     R0,=F'100'                                                       
         MH    R1,=H'100'                                                       
         AH    R0,=H'30'                                                        
         CH    R0,=H'60'                                                        
         BNE   SD10                                                             
         LA    R0,100                                                           
         SPACE 1                                                                
SD10     AR    R1,R0                                                            
         CH    R1,=H'2430'                                                      
         BNE   *+8                                                              
         LA    R1,30                                                            
         STH   R1,TIMES                                                         
         CH    R1,TIMES+2          CHECK FOR MATCH                              
         BE    XIT                                                              
         LA    R1,2400(R1)                                                      
         CH    R1,TIMES+2          TRY TIME+2400 AS WELL                        
         BE    XIT                                                              
         B     SD8                                                              
         EJECT                                                                  
*                                                                               
         SPACE 3                                                                
* CONVERT 90 ELEMENT TO OLD STYLE                                               
CNV94    NTR1                                                                   
         L     R6,ADHUT                                                         
         XC    MY90,MY90                                                        
         ZIC   R1,1(R6)                                                         
         BCTR  R1,0                                                             
         MVC   MY90(2),0(R6)                                                    
         LA    R6,2(R6)                                                         
         LA    RE,MY90                                                          
         LA    RF,HF94TAB                                                       
CNV94A   ZIC   R2,0(RF)                                                         
         SLL   R2,1                                                             
         LA    RE,0(RE,R2)                                                      
         MVC   0(2,RE),0(R6)                                                    
         LA    RE,MY90                                                          
         LA    RF,1(RF)                                                         
         LA    R6,2(R6)                                                         
         CLI   0(RF),X'FF'                                                      
         BNE   CNV94A                                                           
         L     R6,ADHUT                                                         
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R6),MY90                                                     
         B     XIT                                                              
         EJECT                                                                  
*              GET HUT FROM X'90' ELEMENT                                       
         SPACE 3                                                                
AVE90    NTR1                                                                   
         L     R6,ADHUT                                                         
         USING CTHUTD,R6                                                        
         XC    MY90,MY90           CATER FOR DIFFERENT LENGTH ELEMENT           
         ZIC   R1,1(R6)                                                         
         BCTR  R1,0                                                             
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   MY90(0),0(R6)                                                    
         LA    R6,MY90                                                          
         SR    R1,R1               TOTAL HUT IN R1                              
         LA    R2,WEEKLIST                                                      
         SR    R3,R3               HITS IN R3                                   
         ZIC   R0,NWEEKS                                                        
         SPACE 1                                                                
AVE92    MVC   THISWEEK,0(R2)      PRESENT WEEK NO FROM LIST                    
         CLI   THISWEEK,0                                                       
         BE    AVE94                                                            
         CLI   GH52,C'Y'                                                        
         BE    AVE93                                                            
         CLI   THISWEEK,48                                                      
         BH    AVE94                                                            
         SPACE 1                                                                
AVE93    ZIC   R5,THISWEEK                                                      
         BCTR  R5,0                                                             
         SLL   R5,1                ADDRESS HUT                                  
         LA    R5,CTHUTLST(R5)                                                  
*                                                                               
         AH    R1,0(R5)                                                         
         OC    0(2,R5),0(R5)       COUNT HITS                                   
         BZ    AVE93B                                                           
         LA    R3,1(R3)                                                         
         MVC   THISHUT,0(R5)                                                    
         BAS   RE,GOHOOK           USER MAY MANT A PEEP                         
         SPACE 1                                                                
         B     AVE94                                                            
         SPACE 1                                                                
AVE93B   CLI   GHZERO,X'FF'        SHOULD PARTIAL DATA BE RETURNED              
         BE    AVE94                                                            
         CLI   GHSCHEME,X'FE'      IF DATA IS MISSING ON DEMO FILE              
         BNE   AVE94                                                            
         CLI   THISWEEK,48         AND ITS A REAL WEEK                          
         BH    AVE94                                                            
         XC    HUT,HUT             PASS BACK ZERO HUT                           
         B     XIT                 DO NOT RISK PARTIAL DATA                     
         SPACE 1                                                                
AVE94    LA    R2,1(R2)                                                         
         BCT   R0,AVE92                                                         
         SPACE 1                                                                
         XC    HUT,HUT                                                          
         LTR   R3,R3               NO HITS                                      
         BZ    XIT                                                              
         SR    R0,R0               AVERAGE PER WEEK                             
         SLDA  R0,1                                                             
         DR    R0,R3                                                            
         AH    R1,=H'1'                                                         
         SRA   R1,1                                                             
         STH   R1,HUT                                                           
         B     XIT                                                              
         EJECT                                                                  
*              ACCESS TO DATAMGR                                                
         SPACE 3                                                                
HIGH     NTR1                                                                   
         CLI   FILE,C'C'                                                        
         BE    HIGH2                                                            
         MVC   KEYSAVE(13),KEY                                                  
         MVC   DATADISP,=H'24'                                                  
         GOTO1 DATAMGR,DMCB,=C'DMRDHI',=C'SPTDIR',KEY,KEY,0                     
         BAS   RE,CHECK                                                         
         CLC   KEY(13),KEYSAVE                                                  
         BNE   XIT                                                              
         GOTO1 (RF),DMCB,=C'GETREC',=C'SPTFILE',KEY+14,IO,DMBLOCK               
         BAS   RE,CHECK                                                         
         B     XIT                                                              
         SPACE 1                                                                
HIGH2    MVC   DATADISP,=H'28'                                                  
         MVC   KEYSAVE(25),KEY                                                  
         GOTO1 DATAMGR,DMCB,=C'DMRDHI',=C'CTFILE',KEY,IO,0                      
         MVC   KEY,IO              SET KEY FROM RETURNED RECORD                 
         BAS   RE,CHECK                                                         
         B     XIT                                                              
         SPACE 1                                                                
CHECK    CLI   DMCB+8,0                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
         LH    R1,GHIOCNT                                                       
         LA    R1,1(R1)                                                         
         STH   R1,GHIOCNT                                                       
         BR    RE                                                               
         SPACE 1                                                                
         GETEL (R6),DATADISP,ELCODE                                             
         SPACE 1                                                                
XIT      XIT1                                                                   
         SPACE 1                                                                
GOHOOK   NTR1                                                                   
         L     RF,GHHOOK                                                        
         LTR   RF,RF               OPTIONAL USER HOOK                           
         BZ    XIT                                                              
         MVC   GHHOOKYR,THISYEAR   YES SO SUPPLY VALUES                         
         MVC   GHHOOKWK,THISWEEK                                                
         MVC   GHHOOKTM,THISTIME                                                
         MVC   GHHOOKHT,THISHUT                                                 
         L     RE,USERRD                                                        
         LM    R0,RC,20(RE)        RESTORE USERS R0-RC                          
         BASR  RE,RF                                                            
         XIT1                                                                   
         EJECT                                                                  
*              LITERAL POOL AND CONSTANTS                                       
         SPACE 3                                                                
LISTTIME DC    H'30'                                                            
         DC    H'2400'                                                          
         SPACE 1                                                                
PRETIME  DC    H'0030'                                                          
         DC    H'0100'                                                          
         DC    H'0130'                                                          
         DC    H'0200'                                                          
         DC    H'0230'                                                          
         DC    H'0300'                                                          
         DC    H'0330'                                                          
         DC    H'0400'                                                          
         DC    H'0430'                                                          
         DC    H'0500'                                                          
         DC    H'0530'                                                          
         DC    H'0600'                                                          
         DC    H'0630'                                                          
         DC    H'0700'                                                          
         DC    H'0730'                                                          
         DC    H'0800'                                                          
         DC    H'0830'                                                          
         DC    H'0900'                                                          
         DC    H'0930'                                                          
         DC    H'1000'                                                          
         DC    H'1030'                                                          
         DC    H'1100'                                                          
         DC    H'1130'                                                          
         DC    H'1200'                                                          
         DC    H'1230'                                                          
         DC    H'1300'                                                          
         DC    H'1330'                                                          
         DC    H'1400'                                                          
         DC    H'1430'                                                          
         DC    H'1500'                                                          
         DC    H'1530'                                                          
         DC    H'1600'                                                          
         DC    H'1630'                                                          
         DC    H'1700'                                                          
         DC    H'1730'                                                          
         DC    H'1800'                                                          
         DC    H'1830'                                                          
         DC    H'1900'                                                          
         DC    H'1930'                                                          
         DC    H'2000'                                                          
         DC    H'2030'                                                          
         DC    H'2100'                                                          
         DC    H'2130'                                                          
         DC    H'2200'                                                          
         DC    H'2230'                                                          
         DC    H'2300'                                                          
         DC    H'2330'                                                          
         DC    H'2400'                                                          
         LTORG                                                                  
         EJECT                                                                  
*              DSECT TO COVER GETHUT STORAGE                                    
         SPACE 3                                                                
HUTD     DSECT                                                                  
ADDAY    DS    A                                                                
CALLOV   DS    A                                                                
DATAMGR  DS    A                                                                
NETWEEK  DS    A                                                                
DATCON   DS    A                                                                
DEMAND   DS    A                                                                
DEMOUT   DS    A                                                                
GETDAY   DS    A                                                                
ADHUT    DS    A                                                                
USERRD   DS    A                                                                
DMCB     DS    CL24                                                             
DUB      DS    D                                                                
TIMES    DS    CL4                                                              
NTIMES   DS    CL1                                                              
WORK     DS    CL32                                                             
DAY      DS    CL1                                                              
REPDAY   DS    X                                                                
TIMELIST DS    CL96                                                             
         DS    CL8                 OVERFLOW PADDING FOR TIMELIST                
WEEKLIST DS    CL53                                                             
NWEEKS   DS    CL1                                                              
STARTYR  DS    CL1                                                              
NYEARS   DS    CL1                                                              
THISYEAR DS    CL1                                                              
THISWEEK DS    CL1                                                              
THISTIME DS    CL2                                                              
THISHUT  DS    H                                                                
TOTHUT   DS    F                                                                
TOTHITS  DS    F                                                                
DMBLOCK  DS    CL96                                                             
MY90     DS    CL120                                                            
ELCODE   DS    CL1                                                              
DATADISP DS    H                                                                
HUT      DS    H                                                                
HUTHITS  DS    H                                                                
FILE     DS    CL8                                                              
KEY      DS    CL25                                                             
KEYSAVE  DS    CL25                                                             
         DS    0D                                                               
HUTDB    DS    CL256               DBLOCK FOR DEMAND/DEMOUT                     
GHTHIS   DS    0CL9                OPTIMIZATION CONTROL                         
GHTHSSCM DS    CL1                                                              
GHTHSYR  DS    CL1                                                              
GHTHSNYR DS    CL1                                                              
GHTHSWK  DS    CL1                                                              
GHTHSNWK DS    CL1                                                              
GHTHSTM  DS    CL2                                                              
GHTHSNTM DS    CL1                                                              
GHTHSDAY DS    CL1                                                              
HUTWHERE DS    CL1                                                              
BDATE    DS    0XL3                                                             
BYEAR    DS    XL1                                                              
BMONTH   DS    XL1                                                              
BDAY     DS    XL1                                                              
BQUARTER DS    XL1                                                              
BWEEK    DS    XL1                                                              
IO       DS    0C                                                               
         DS    2000C                                                            
HUTDX    EQU   *                                                                
         SPACE 2                                                                
* EQUATES                                                                       
*                                                                               
MAXTIMES EQU   L'TIMELIST/2        MAXIMUM TIMELIST ENTRIES                     
         EJECT                                                                  
       ++INCLUDE NEGETHUTD                                                      
         PRINT OFF                                                              
HUTDBDS  DSECT                                                                  
       ++INCLUDE DEDBLOCK                                                       
       ++INCLUDE DDCOMFACS                                                      
       ++INCLUDE FAFACTS                                                        
       ++INCLUDE NEGENHUT                                                       
       ++INCLUDE CTGENFILE                                                      
         PRINT ON                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'096NEGETHUTS 05/01/02'                                      
         END                                                                    
