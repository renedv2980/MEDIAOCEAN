*          DATA SET DMLDSIZE   AT LEVEL 002 AS OF 07/26/12                      
*CATALP DMLDSIZE                                                                
         TITLE 'DMLDSIZE - LOAD/DUMP DA FILE REC SIZE COUNT/PRINT'              
*        PARAMS VIA R1                                                          
*        XL2   COUNT/PRINT  X'0000'=COUNT  X'FF..'=PRINT                        
*        XL2   LENGTH                                                           
*        AL4   A(FILE NAME)                                                     
*                                                                               
         PRINT NOGEN                                                            
LDSIZE   CSECT                                                                  
         NMOD1 WRKX-WRKD,**SIZE**                                               
         USING WRKD,RC                                                          
         CLI   0(R1),X'FF'                                                      
         BE    PRINT                                                            
                                                                                
COUNT    LA    R3,LENTAB+6         SET FOR BXLE                                 
         LH    R4,LENTAB                                                        
         L     R5,LENTAB+2                                                      
*                                                                               
C1       CLC   2(2,R1),0(R3)       TEST REC LEN TO TABLE LEN                    
         BNH   C2                  ADD TO COUNTER IF FITS                       
         BXLE  R3,R4,C1                                                         
         SR    R3,R4               END OF TABLE ADD INTO LAST COUNTER           
C2       AP    2(6,R3),=P'1'                                                    
*                                                                               
         CLC   2(2,R1),MINLEN      UPDATE MINIMUM RECORD LENGTH                 
         BNL   *+10                                                             
         MVC   MINLEN,2(R1)                                                     
         CLC   2(2,R1),MAXLEN      UPDATE MAXIMUM RECORD LENGTH                 
         BNH   *+10                                                             
         MVC   MAXLEN,2(R1)                                                     
*                                                                               
C3       ICM   RF,15,=V(RECTYV)    TEST IF RECORD TYPE DATA DEFINED             
         BZ    XIT                                                              
         SR    RE,RE                                                            
         IC    RE,4(RF)            GET AGY/CPY CODE                             
         SLL   RE,4                INDEX INTO AGY/CPY COUNTERS                  
         LA    RE,AGYSZE(RE)                                                    
         LH    R0,2(R1)            GET RECORD LENGTH                            
         CVD   R0,DUB                                                           
         AP    0(8,RE),=P'1'       BUMP RECORD COUNT                            
         AP    8(8,RE),DUB         BUMP BYTE COUNT                              
*                                                                               
XIT      XMOD1 1                                                                
         EJECT                                                                  
PRINT    L     RA,=V(CPRINT)       PRINT HEADING                                
         USING DPRINT,RA                                                        
         MVC   P,SPACES                                                         
         GOTO1 =V(PRINTER)                                                      
         L     RF,4(R1)            GET DAFILE NAME                              
         MVC   HDL+8(8),0(RF)                                                   
         MVC   P(L'HDL),HDL                                                     
         GOTO1 =V(PRINTER)                                                      
         MVC   P(L'UDL),UDL                                                     
         GOTO1 =V(PRINTER)                                                      
*                                                                               
         LA    R3,LENTAB+6         SET FOR BXLE                                 
         LH    R4,LENTAB                                                        
         L     R5,LENTAB+2                                                      
         SR    R7,R7               USE AS FROM LENTAB                           
         ZAP   TOTCOUNT,=P'0'      TOTAL COUNTER                                
*                                                                               
PR1      AP    TOTCOUNT,2(6,R3)    BUMP TOTAL                                   
         CLC   0(2,R3),=AL2(32767)                                              
         BNE   *+14                                                             
         CP    2(6,R3),=P'0'       EXIT IF LAST GIANT ENTRY IS EMPTY            
         BE    PR2                                                              
         LA    R7,1(R7)            SET 'FROM' LENTAB                            
         BAS   RF,FORMAT           FORMAT FROM-TO                               
         UNPK  PCOUNT,2(6,R3)                                                   
         OI    PCOUNT+L'PCOUNT-1,X'F0'                                          
         CLI   PCOUNT,C'0'                                                      
         BNE   *+8                                                              
         MVI   PCOUNT,C' '                                                      
*                                                                               
         MVC   P(L'PRT),PRT                                                     
         GOTO1 =V(PRINTER)                                                      
         LH    R7,0(R3)            SET NEXT FROM                                
         BXLE  R3,R4,PR1                                                        
                                                                                
PR2      MVC   P(L'UDL),UDL        PRINT TOTAL AT END OF TABLE                  
         GOTO1 =V(PRINTER)                                                      
*                                                                               
         UNPK  PCOUNT,TOTCOUNT                                                  
         OI    PCOUNT+L'PCOUNT-1,X'F0'                                          
         CLI   PCOUNT,C'0'                                                      
         BNE   *+8                                                              
         MVI   PCOUNT,C' '                                                      
*                                                                               
         MVC   P(L'PRT),PRT                                                     
         MVC   P(15),=CL15'0000 TO 0000'                                        
         SR    R0,R0                                                            
         ICM   R0,3,MINLEN                                                      
         CVD   R0,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  P(4),DUB                                                         
         SR    R0,R0                                                            
         ICM   R0,3,MAXLEN                                                      
         CVD   R0,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  P+7(5),DUB                                                       
         CLI   P+7,C'0'            TEST IF GIANT MAXIMUM                        
         BNE   *+8                                                              
         MVI   P+7,C' '                                                         
         GOTO1 =V(PRINTER)                                                      
         MVC   P(L'UDL),UDL                                                     
         GOTO1 =V(PRINTER)                                                      
                                                                                
PR3      LA    R3,LENTAB+6         CLEAR COUNTERS                               
         LH    R4,LENTAB                                                        
         L     R5,LENTAB+2                                                      
         ZAP   2(6,R3),=P'0'                                                    
         BXLE  R3,R4,*-6                                                        
         MVC   MINLEN,=AL2(32767)                                               
         MVC   MAXLEN,=AL2(0)                                                   
                                                                                
PR4      ICM   RF,15,=V(RECTYV)    TEST IF RECORD TYPE DATA DEFINED             
         BZ    XIT                                                              
         MVC   P,SPACES                                                         
         GOTO1 =V(PRINTER)                                                      
         MVC   P(L'HDLAGY),HDLAGY                                               
         GOTO1 =V(PRINTER)                                                      
         MVC   P(L'UDLAGY),UDLAGY                                               
         GOTO1 =V(PRINTER)                                                      
         LA    R3,AGYSZE           R3=A(COUNTERS FOR AGENCY COMPANY)            
         LA    R4,0                R4=AGY/CPY HEX CODE                          
                                                                                
PR5      CP    0(8,R3),=P'0'       TEST INACTIVE                                
         BE    PR8                                                              
         MVC   DESC,SPACES                                                      
         STC   R4,BYTE                                                          
         ICM   RF,15,=V(HEXOUT)    OUTPUT HEX AGY/CPY CODE                      
         BZ    PR6                                                              
         GOTO1 (RF),DMCB,BYTE,DESC,1,=C'MIX'                                    
*                                                                               
PR6      UNPK  PCOUNT,4(4,R3)      NUMBER OF RECORDS FOR AGENCY                 
         OI    PCOUNT+L'PCOUNT-1,X'F0'                                          
         CLI   PCOUNT,C'0'                                                      
         BNE   *+8                                                              
         MVI   PCOUNT,C' '                                                      
*                                                                               
PR7      ZAP   BIGGY,8(8,R3)       NUMBER OF BYTES FOR AGENCY                   
         DP    BIGGY,=PL4'1024'                                                 
         UNPK  PKB,BIGGY(12)                                                    
         OI    PKB+L'PKB-1,X'F0'                                                
         CLI   PKB,C'0'                                                         
         BNE   *+8                                                              
         MVI   PKB,C' '                                                         
*                                                                               
         MVC   P(L'PRT),PRT                                                     
         GOTO1 =V(PRINTER)                                                      
*                                                                               
PR8      LA    R3,16(R3)           BUMP TO NEXT TABLE ENTRY                     
         LA    R4,1(R4)            BUMP AGY/CPY NUMBER                          
         CHI   R4,255                                                           
         BL    PR5                                                              
*                                                                               
PR9      LA    R3,AGYSZE           CLEAR COUNTERS                               
         LA    R4,255              R4=AGY/CPY HEX CODE                          
PR9A     ZAP   0(8,R3),=P'0'                                                    
         ZAP   8(8,R3),=P'0'                                                    
         LA    R3,16(R3)                                                        
         BCT   R4,PR9A                                                          
         B     XIT                                                              
                                                                                
FORMAT   LR    R0,R7               SET FROM NUMBER                              
         CVD   R0,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  FROM(4),DUB                                                      
         MVC   SEP,=C' TO '                                                     
         LH    R0,0(R3)            SET TO NUMBER                                
         CVD   R0,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  TO(4),DUB                                                        
         CH    R0,=AL2(32767)      TEST IF GIANT ENTRY                          
         BNER  RF                                                               
         MVC   TO(4),=C'....'                                                   
         BR    RF                                                               
         EJECT                                                                  
         CNOP  2,4                                                              
LENTAB   DC    H'8'                TABLE OF REC LENGTHS AND COUNTERS            
         DC    A(LENTABX-1)                                                     
         DC    AL2(0064),PL6'0'                                                 
         DC    AL2(0128),PL6'0'                                                 
         DC    AL2(0256),PL6'0'                                                 
         DC    AL2(0512),PL6'0'                                                 
         DC    AL2(0768),PL6'0'                                                 
         DC    AL2(1024),PL6'0'                                                 
         DC    AL2(1536),PL6'0'                                                 
         DC    AL2(2048),PL6'0'                                                 
         DC    AL2(4096),PL6'0'                                                 
         DC    AL2(6144),PL6'0'                                                 
         DC    AL2(32767),PL6'0'   LAST ENTRY FOR GIANT RECORDS                 
LENTABX  DS    0C                                                               
                                                                                
MINLEN   DC    AL2(32767)                                                       
MAXLEN   DC    AL2(0)                                                           
                                                                                
HDL      DC    C'REC LEN XXXXXXX  COUNT    '                                    
UDL      DC    C'---------------  ---------'                                    
                                                                                
HDLAGY   DC    C'REC LEN AGY/CPY  COUNT      KILOBYTES'                         
UDLAGY   DC    C'---------------  ---------  ---------'                         
                                                                                
PRT      DS    0CL37                                                            
DESC     DS    0CL16                                                            
FROM     DC    CL4'    '                                                        
SEP      DC    CL4'    '                                                        
TO       DC    CL4'    '                                                        
         DC    CL4' '                                                           
PCOUNT   DC    CL10' '                                                          
         DC    CL1' '                                                           
PKB      DC    CL10' '                                                          
                                                                                
         LTORG                                                                  
                                                                                
         DS    0D                                                               
         DC    CL8'*AGYSZE*'                                                    
AGYSZE   DC    512PL8'0'                                                        
                                                                                
WRKD     DSECT                                                                  
DUB      DS    D                                                                
FULL     DS    F                                                                
HALF     DS    H                                                                
BYTE     DS    X                                                                
BYTE1    DS    X                                                                
DMCB     DS    6F                                                               
TOTCOUNT DS    PL6                                                              
BIGGY    DS    PL16                                                             
WRKX     EQU   *                                                                
                                                                                
*DDDPRINT                                                                       
       ++INCLUDE DDDPRINT                                                       
                                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'002DMLDSIZE  07/26/12'                                      
         END                                                                    
