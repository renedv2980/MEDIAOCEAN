*          DATA SET DDLOCKSPC  AT LEVEL 016 AS OF 09/17/20                      
*CATALP LOCKSPC                                                                 
***********************************************************************         
* P1     XL1   ALLOCATION FLAGS                                       *         
*              X'80' LONG ALLOCATE (DEFAULT 100 SECS OR SET IN P3+3)  *         
*              X'40' I/O ALLOCATE                                     *         
*              X'20' ENQUIRE ON RESOURCE                              *         
*              X'10' FREE RESOURCE                                    *         
*              X'08' ALWAYS RETURN                                    *         
*              X'04' RETURN ONLY IF LONG ALLOCATE                     *         
*              X'02' RECOVERY ALLOCATE (LOCK USED DURING ABEND)       *         
*              X'01' FORCE FREE (EVEN IF LOCKED BY OTHER)             *         
* P1+1   XL1   LOCK FUCTION                                           *         
*              00 NORMAL LOCK/UNLOCK                                  *         
*              01 OPEN SYSTEM CALL                                    *         
*              02 CLOSE SYSTEM CALL                                   *         
*              03 ADD FACPAK SYSTEM                                   *         
*              04 RETURN UNIQUE REF IN P2                             *         
*              05 REFRESH SYSTEM DTFS (USED BY =XTNT)                 *         
*              06 OPEN FOR MAINTENANCE                                *         
*              07 CLOSE MAINTENANCE                                   *         
*              08 SET JOB ENTRY TO UPDATIVE (FROM UPDATE IN DMGR)     *         
*              09 SET JOB ENTRY TO READ-ONLY                          *         
*              10 OPEN SYSTEM AS READ-ONLY                            *         
*              11 SPECIAL JOBVAL CALL FOR LOCKER                      *         
*              12 RE INITIALISE LOCKSPC                               *         
* P1+2   XL2   RESOURCE NUMBER                                        *         
*              X'80' TABS DATASPACE AND X'40' PGMS DATASPACE          *         
*                                                                     *         
* P2+0   XL1   PASS FLAGS TO CONTROL OFFLINE CONSOLE OUTPUT           *         
*              X'80' CONSOLE FLAG SET                                 *         
*              X'40' NO CONSOLE PASS BACK MESSAGE IN P4               *         
*              X'20' CONSOLE BUT DONT ASK FOP OPERATOR REPLY          *         
* P2+0   XL1   RETURN FLAGS (IF P1=08 OR 04)                          *         
*              X'80' OPEN FAILED DUE TO MAINTENANCE                   *         
*              X'80' RESOURCE IS UNAVAILABLE (LONG)                   *         
*              X'40' RESOURCE IS UNAVAILABLE (SHORT)                  *         
* P2+1   AL3   A(RETURN 64 BYTE RESOURCE HEADER OR 512 BYTE JOB TAB)  *         
*                                                                     *         
* P3     XL2   SPARE                                                  *         
* P3+2   XL1   EXT FILE NUMBER FOR MAINT                              *         
* P3+3   AL1   ACTION CODE FOR MAINT (LONG ALLOCATE VAL SEC *10)      *         
*                                                                     *         
* P4     AL4   ADDRESS RETURNED ON SOME ACTIONS                       *         
***********************************************************************         
         TITLE 'DDLOCKSPC - LOCK A DATASPACE RESOURCE '                         
         PRINT NOGEN                                                            
LOCKSPC  CSECT                                                                  
         NMOD1 WORKX-WORKD,**LKSP**,RA,R9,CLEAR=YES                             
         USING WORKD,RC                                                         
*                                                                               
MAIN     SAM24                                                                  
         LR    R3,R1               R3=A(PARAMETER LIST)                         
         USING PARMD,R3                                                         
         TIME  BIN                 SET TIME NOW                                 
         ST    R0,TIMENOW                                                       
         MVC   DSRSRC,P1+2                                                      
         NC    DSRSRC,=X'0FFF'     DSRSRC IS PURE RESOURCE NUMBER               
         MVC   SAVEP2,P2                                                        
*                                                                               
MAIN0D   TM    P1+2,X'C0'          TEST DMGR RESOURCE                           
         BNZ   MAIN0T                                                           
         MVI   DSTYPE,C'D'         FLAG DMGR DATASPACE                          
         TM    INITFLGS,DMGRINIT+FRSTINIT                                       
         BO    MAIN1                                                            
         BRAS  RE,INIT                                                          
         B     MAIN1                                                            
MAIN0T   TM    P1+2,X'80'          TEST TABS RESOURCE                           
         BZ    MAIN0P                                                           
         MVI   DSTYPE,C'T'         FLAG TABS DATASPACE                          
         TM    INITFLGS,TABSINIT+FRSTINIT                                       
         BO    MAIN1                                                            
         BRAS  RE,INIT                                                          
         B     MAIN1                                                            
MAIN0P   MVI   DSTYPE,C'P'         FLAG PGMS DATASPACE                          
         TM    INITFLGS,PGMSINIT+FRSTINIT                                       
         BO    MAIN1                                                            
         BRAS  RE,INIT                                                          
*                                                                               
MAIN1    XC    TLKNTRY,TLKNTRY     INIT LOCSPACE TRACE ENTRY                    
         MVI   TLKTYPE,C'L'        SET LOCKSPACE TRACE                          
         ST    R0,TLKTIME          SET TIME                                     
         MVC   TLKLOCK,MVSLOCK     SET CPU/ASID                                 
         L     R8,4(RD)                                                         
         L     R1,64(R8)           CALLERS RB                                   
         MVC   TLKCBASE,24(R1)     CALLERS BASE MODULE NAME                     
         L     RE,12(R8)           CALLERS RE                                   
         SR    RE,R1                                                            
         BNP   *+8                                                              
         ST    RE,TLKCDISP         CALLERS RE-RB                                
         CLC   TLKCBASE,=C'LOCK'                                                
         BNE   *+14                                                             
         L     R1,60(R8)           GET LOCKER'S RA                              
         MVC   TLKMISC(4),0(R1)    SET FILENUM/DISKADDR                         
*                                                                               
MAIN2    L     R8,MYSSB            R8=A(SSB)                                    
         USING SSBD,R8                                                          
         MVC   MVSADV(1),SSBSYSID  SET ADV/TSK                                  
         OC    MVSADV(1),SSBSYSIX                                               
         ICM   R1,15,SSBTKADR                                                   
         BZ    *+14                                                             
         MVC   MVSADV+1(1),TCBTASK-TCBD(R1)                                     
         B     *+8                                                              
         MVI   MVSADV+1,0          ZERO IF NO TASK                              
         MVC   TLKTASK,MVSADV                                                   
*                                                                               
MAIN3    OC    SSBALET,SSBALET     EXIT IF NO DSPACE                            
         BNZ   *+14                                                             
         OC    SSBTBLET,SSBTBLET                                                
         BZ    EXIT                                                             
         MVC   MYALET,SSBALET      COPY FROM SSB                                
*                                                                               
MAIN4    CLI   P1+1,0              TEST LOCK/FREE NORMAL CALL                   
         BNE   MAIN5                                                            
         OC    P1+2(2),P1+2        ENQ ONLY IF RESOURCE ZERO                    
         BZ    ENQUIRY                                                          
*                                                                               
         TM    P1+2,X'80'          RESOURCE 8000+                               
         BZ    MAIN4A                                                           
         MVC   MYALET,SSBTBLET     USE TABS DATASPACE                           
*                                                                               
MAIN4A   TM    P1+2,X'40'          RESOURCE 4000+                               
         BZ    MAIN4B                                                           
         MVC   MYALET,SSBPGMTA     USE PGMS DATASPACE SSB                       
*                                                                               
MAIN4B   CLI   ONLIFLG,C'Y'                                                     
         BE    ONLK                                                             
         B     OFLK                                                             
*                                                                               
MAIN5    LLC   RF,P1+1             GET FUNCTION/ROUTINE NUMBER                  
         CHI   RF,ROUTMAX                                                       
         JH    *+2                 DIE=INV ROUTINE NUM                          
         SLL   RF,2                                                             
         A     RF,=A(ROUTS)        INDEX INTO ROUTINE TABLE                     
         L     RF,0(RF)                                                         
         BR    RF                                                               
*                                                                               
EXIT     XIT1                                                                   
         EJECT                                                                  
***********************************************************************         
* ONLINE LOCK                                                         *         
***********************************************************************         
ONLK     SAC   0                                                                
         TIME  BIN                 SET TIME NOW                                 
         ST    R0,TIMENOW                                                       
         SAC   512                 SET ACCESS REG MODE                          
         LAM   AR0,ARF,ARZERO                                                   
         LAM   AR2,AR2,MYALET                                                   
         SR    R2,R2                                                            
*                                                                               
ONLK1    LH    RF,DSRSRC           RF=RESOURCE NUMBER                           
         SLL   RF,6                INDEX TO RESOURCE HEADER                     
         AR    R2,RF                                                            
         USING DMSPACED,R2                                                      
*                                                                               
         OC    DSPNAME,DSPNAME     MUST BE ALLOCATED                            
         JZ    *+2                 DIE=NOT ALLOCATED                            
         MVC   TLKRSRC,DSPNAME                                                  
         TM    P1,X'10'            TEST FOR FREE CALL                           
         BO    FREEIT                                                           
         TM    P1,X'20'            IF ENQUIRY JUST RETURN                       
         BO    RETURN                                                           
         MVI   TLKACTN,C'L'        LOCK RESOURCE                                
*                                                                               
ONLK2    SAC   0                                                                
         PUSH ACONTROL                                                          
         ACONTROL COMPAT(NOCASE)                                                
         SYSEVENT DONTSWAP                                                      
         POP  ACONTROL                                                          
         SAC   512                                                              
*                                                                               
         L     R1,MVSLOCK          R1=MY LOCK WORD                              
         SR    R0,R0                                                            
         CS    R0,R1,DSPLOCK       LOCK IF FREE                                 
         BNE   ONLK3               NO                                           
         CLI   TLKWAIT,0           TEST IF WE HAD TO WAIT                       
         BE    LOCKIT              NO                                           
         L     RE,TIMENOW          YES COMPUTE WAIT TIME                        
         SL    RE,TLKDELT                                                       
         ST    RE,TLKDELT          SET TIME WAITED                              
         B     LOCKIT                                                           
*                                                                               
ONLK3    STM   R0,R1,DUB           SAVE R0,R1 FOR DSPLOCK2 TEST                 
         SAC   0                                                                
         PUSH ACONTROL                                                          
         ACONTROL COMPAT(NOCASE)                                                
         SYSEVENT OKSWAP                                                        
         POP  ACONTROL                                                          
         SAC   512                                                              
         LM    R0,R1,DUB           RESTORE R0                                   
*                                                                               
         MVI   BYTE,C'1'           CHECK LOCKWORD IS GOOD                       
         L     RF,DSPLOCK2                                                      
         LTR   RF,RF               IF COPY IS ZERO ITS NOT BUILT YET            
         BZ    ONLK3A                                                           
         CR    R0,RF               DOES LOCKWORD MATCH ITS COPY                 
         BE    ONLK3A              YES - OK                                     
         CLC   DSPLOCK,DSPLOCK2    DOUBLE CHECK                                 
         BNE   ONLK8               NOT GOOD SO BADLOCK IT                       
*                                                                               
ONLK3A   CR    R0,R1               IS IT LOCKED BY ME                           
         BNE   ONLK5               NO                                           
         CLC   DSPADV,MVSADV       TEST SAME TASK                               
         BNE   ONLK5               NO                                           
*                                                                               
ONLK4    SR    R1,R1               I ALREADY OWN SO BUMP THE COUNT              
         IC    R1,DSPLOCKS                                                      
         LA    R1,1(R1)                                                         
         STC   R1,DSPLOCKS                                                      
         STC   R1,TLKRCNT                                                       
         MVI   P2,255              FLAG FOR RETURN                              
         BAS   RE,TRCIT                                                         
         CHI   R1,10                                                            
         BNH   RETURN                                                           
         DC    H'0'                DIE=TOO MANY LOCKS                           
*                                                                               
ONLK5    ICM   R1,15,DSPWCNT       BUMP NUM OF WAITS                            
         LA    R1,1(R1)                                                         
         STCM  R1,15,DSPWCNT                                                    
         TM    P1,X'08'            NOT FREE - TEST RETURN                       
         BO    RETURN                                                           
         TM    DSPFLAG,DSPRCVR     RECOVERY IN PROCESS                          
         BO    ONLK10                                                           
         TM    DSPFLAG,DSPLONG     LONG WAIT EXPECTED                           
         BZ    ONLK6                                                            
         TM    P1,X'04'            TEST RETURN ON LONG WAIT                     
         BO    RETURN                                                           
*                                                                               
ONLK6    L     R1,DSPTIME          GET TIME OF LOCK                             
         LTR   R1,R1               IF ZERO THEN ...                             
         BZ    ONLK10              GETTING OR RELEASING SO WAIT                 
         L     RF,TIMENOW                                                       
         SR    RF,R1               CALCULATE DELTA T                            
         BNM   *+12                                                             
         MVC   DSPTIME,TIMENOW     SET BAD TIME TO TIME NOW                     
         SR    RF,RF                                                            
         TM    DSPFLAG,DSPLONG     LONG WAIT EXPECTED                           
         BO    ONLK7                                                            
         CH    RF,=H'500'          LESS THAN 05 SECONDS = WAIT                  
         BL    ONLK10                                                           
         MVI   BYTE,C'2'                                                        
         CH    RF,=H'3000'         MORE THAN 30 SECONDS = BADLOCK               
         BNL   ONLK7A                                                           
         CLC   DSPNAME(4),=C'SERV' FIVE SECOND ONLY FOR SERVICE                 
         BE    ONLK7A                                                           
         CLI   SSBMTIND,0          IF MULTI TASKING REJECT IT                   
         BNE   ONLKXOUT                                                         
*                                                                               
         B     ONLK10              OTHERWISE WAIT FOR FULL EXPIRY               
*                                                                               
ONLK7    CH    RF,=H'1000'         LESS THAN 10 SECONDS = WAIT                  
         BL    ONLK10                                                           
         CH    RF,=H'10000'        MORE THAT 100 SECONDS= REJECT                
         BL    ONLKXOUT                                                         
*                                                                               
         SR    R0,R0                                                            
         IC    R0,DSPLONGT         GET LONG TIME ESTIMATE                       
         MH    R0,=H'1000'         TIMES 10 TIMES 100 FOR MS                    
         CR    RF,R0                                                            
         BL    ONLKXOUT            IF LESS THAN ESTIMATE THEN WAIT              
*                                                                               
         MVI   BYTE,C'3'                                                        
ONLK7A   ST    RF,BADDELT          SAVE DELTA TIME FOR LOCK                     
*                                                                               
ONLK8    MVC   BADLOCK,0(R2)       SAVE THE BAD LOCK INFO                       
         MVC   TLKERR,BYTE         SET BAD LOCK REASON CODE                     
         MVC   TLKOTIME,DSPTIME                                                 
         MVC   TLKOLOCK,DSPLOCK                                                 
         MVC   TLKOTASK,DSPADV                                                  
         MVC   BADTRACE,TLKNTRY    SAVE THE BAD LOCK TRACE ENTRY                
         SR    R0,R0                                                            
         ST    R0,DSPTIME          CLEAR TIME                                   
         XC    DSPJOB,DSPJOB       CLEAR JOBNO                                  
         XC    DSPTYPE,DSPTYPE     CLEAR TYPE                                   
         XC    DSPFLAG,DSPFLAG     CLEAR FLAGS                                  
         XC    DSPLONGT,DSPLONGT   CLEAR TIME ESTIMATE                          
         XC    DSPMVS,DSPMVS       CLEAR JOB NAME                               
         XC    DSPLOCKS,DSPLOCKS   REMOVE COUNT                                 
         OI    DSPECB,X'40'        FLAG ECB FREE                                
         ST    R0,DSPLOCK2         CLEAR LOCKWORD COPY                          
         L     R0,DSPLOCK                                                       
         SR    R1,R1                                                            
         CS    R0,R1,DSPLOCK       CLEAR LOCKWORD                               
         BE    ONLK9                                                            
*                                                                               
         MVC   MSG(2),=H'69'                                                    
         MVC   MSG+2(64),0(R2)     LOCK ENTRY IN DATASPACE NOW                  
         MVI   MSG+66,C'+'                                                      
         STCM  R0,15,MSG+67        DSPLOCK VALUE AT LAST CS INSTR               
         SAC   0                                                                
         BRAS  RE,WTOHRD                                                        
         LARL  R4,AUTOMSG3         DSPLOCK,ONLINE,NOT CLEAR                     
         BRAS  RE,WTO                                                           
         SAC   512                                                              
*                                                                               
ONLK9    SAC   0                                                                
         MVC   MSG(2),=H'69'                                                    
         MVC   MSG+2(64),BADLOCK   SAVED LOCK ENTRY                             
         MVI   MSG+66,C'+'                                                      
         STCM  R0,15,MSG+67        DSPLOCK VALUE AT LAST CS INSTR               
         BRAS  RE,WTOHRD                                                        
*                                                                               
         LARL  RF,BADLMSG          50+FAC***+ DSPACE BADLOCK#.                  
         MVC   MSG(52),0(RF)                                                    
         L     RF,=V(SSB)                                                       
         MVC   MSG+6(3),SSBSYSNA-SSBD(RF)                                       
         MVC   MSG+26(1),TLKERR                                                 
ONLK9A   CLI   TLKERR,C'2'         TEST TIME OUT                                
         BNE   ONLK9B                                                           
         L     RF,BADDELT          GET DELTA TIME FOR LOCK                      
         CVD   RF,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  MSG+28(5),DUB                                                    
         MVC   MSG+34(4),TLKRSRC                                                
         GOTO1 =V(HEXOUT),PARM,TLKOLOCK,MSG+39,4,0                              
ONLK9B   LA    R4,MSG                                                           
         BRAS  RE,WTO                                                           
         LARL  R4,AUTOMSG4         BADLOCK - ONLINE                             
         BRAS  RE,WTO                                                           
         SAC   512                                                              
         B     ONLK                GO TRY TO GET THE LOCK AGAIN                 
*                                                                               
ONLK10   BRAS  RE,ENQHOLD          DO ENQHOLD ON OWNER                          
*                                                                               
ONLK10A  CLI   SSBMTIND,0          ONLY POSSIBLE IF MULTI TASKING               
         BE    ONLK11                                                           
         ICM   RF,15,=V(ADWAIT)    WAIT UNTIL ITS FREE                          
         LR    R1,R2               SET ECB TO OFFSET                            
         LA    R1,DSPECB-DMSPACED(R1)                                           
*                                                                               
         CLI   DSTYPE,C'D'                                                      
         BNE   *+12                                                             
         O     R1,=X'FE000000'     FLAG AS DSPACE ECB                           
         B     ONLK10C                                                          
*                                                                               
         CLI   DSTYPE,C'T'                                                      
         BNE   *+12                                                             
         O     R1,=X'FD000000'     FLAG AS TABS ECB                             
         B     ONLK10C                                                          
*                                                                               
         CLI   DSTYPE,C'P'                                                      
         BNE   *+12                                                             
         O     R1,=X'FC000000'     FLAG AS PGMS ECB                             
         B     ONLK10C                                                          
*                                                                               
ONLK10C  SAC   0                   RETURN TO NORMAL AMODE                       
         BASR  RE,RF               GO FIND SOMETHING TO DO (ADWAIT)             
         B     ONLK12                                                           
*                                                                               
ONLK11   SAC   0                   NO MULTI TASKING                             
         LHI   R1,1000             SET TO 10000 MS (10 SECS)                    
         MHI   R1,384                                                           
         ST    R1,FULL                                                          
         STIMER WAIT,TUINTVL=FULL                                               
*                                                                               
ONLK12   SR    R1,R1               BUMP NUMBER OF TIMES WE WAITED               
         IC    R1,TLKWAIT                                                       
         LA    R1,1(R1)                                                         
         STC   R1,TLKWAIT                                                       
         CHI   R1,1                                                             
         BNE   ONLK                                                             
         MVC   TLKDELT,TLKTIME     SET TIME OF FIRST ATTEMPT                    
         B     ONLK                                                             
         EJECT                                                                  
***********************************************************************         
* LONG LOCK - EXIT TO USER WITH MSG                                   *         
* ALSO SEND CONSOLE MESSAGE TO WARN OF LOCKOUT                        *         
***********************************************************************         
ONLKXOUT LARL  RF,LOCKMSG          50+FAC***+ DSPACE LOCKOUT .......            
         MVC   MSG(52),0(RF)                                                    
         L     RF,=V(SSB)                                                       
         MVC   MSG+6(3),SSBSYSNA-SSBD(RF)                                       
         MVC   MSG+26(7),DSPNAME                                                
*                                                                               
         OC    DSPJOB,DSPJOB       IF NO JOB HEXOUT LOCKWORD                    
         BZ    XOUT86                                                           
         TM    DSPTYPE,DSPJOBQ     TEST OFFLINE JOB                             
         BO    XOUT85                                                           
         L     R1,SSBAFID-SSBD(RF)                                              
         MVC   BYTE1,DSPJOB                                                     
         NI    BYTE1,X'0F'                                                      
*                                                                               
XOUT81   CLC   BYTE1,4(R1)         FIND ADV SYSTEM                              
         BE    XOUT82                                                           
         LA    R1,8(R1)                                                         
         CLI   5(R1),X'FF'         CHECK EOT                                    
         BNE   XOUT81                                                           
         B     XOUT85                                                           
*                                                                               
XOUT82   MVC   MSG+43+0(4),0(R1)                                                
         MVC   MSG+43+5(2),=C'/#'                                               
         MVC   MSG+43+7(1),DSPJOB+1                                             
         MVI   MSG+43+4,C' '                                                    
         SR    R1,R1                                                            
         ICM   R1,1,DSPJOB                                                      
         SRL   R1,4                                                             
         LTR   R1,R1                                                            
         BZ    XOUT90                                                           
         LA    R1,X'C0'(R1)                                                     
         STC   R1,MSG+43+4                                                      
         B     XOUT90                                                           
*                                                                               
XOUT85   MVI   MSG+43,C'J'         JOB NO                                       
         SR    R1,R1                                                            
         ICM   R1,3,DSPJOB                                                      
         N     R1,=X'0000FFFF'                                                  
         EDIT  (R1),(6,MSG+44),FILL=0                                           
         B     XOUT90                                                           
*                                                                               
XOUT86   GOTO1 =V(HEXOUT),PARM,DSPLOCK,MSG+43,4,0                               
*                                                                               
XOUT90   SAC   0                                                                
         LA    R4,MSG              WRITE CONSOLE MESSAGE                        
         BRAS  RE,WTO                                                           
*                                                                               
         LA    R0,6                UPDATES UNAVAILABLE - TRY AGAIN              
         GOTO1 =V(GETTXT),DUB,(X'FF',(R0))                                      
*                                                                               
         DC    H'0',C'$ABEND'      DIE=$ABEND                                   
         EJECT                                                                  
***********************************************************************         
* OFFLINE LOCK                                                        *         
***********************************************************************         
OFLK     SAC   0                                                                
         TIME  BIN                 SET TIME NOW                                 
         ST    R0,TIMENOW                                                       
         SAC   512                 SET ACCESS REG MODE                          
         LAM   AR0,ARF,ARZERO                                                   
         LAM   AR2,AR2,MYALET                                                   
         SR    R2,R2                                                            
*                                                                               
         LH    RF,DSRSRC           RF=RESOURCE NUMBER                           
         SLL   RF,6                INDEX TO RESOURCE HEADER                     
         AR    R2,RF                                                            
         USING DMSPACED,R2                                                      
*                                                                               
         TM    P1,X'10'            TEST FOR FREE CALL                           
         BO    FREEIT                                                           
         TM    P1,X'20'            IF ENQUIRY JUST RETURN                       
         BO    RETURN                                                           
*                                                                               
         SAC   0                                                                
         PUSH ACONTROL                                                          
         ACONTROL COMPAT(NOCASE)                                                
         SYSEVENT DONTSWAP                                                      
         POP  ACONTROL                                                          
         SAC   512                                                              
*                                                                               
         L     R1,MVSLOCK          R1=MY LOCK WORD                              
         SR    R0,R0                                                            
         CS    R0,R1,DSPLOCK       LOCK IF FREE                                 
         BE    LOCKIT              YES                                          
         CR    R0,R1               IS IT LOCKED BY ME                           
         BNE   OFLK015                                                          
         CLI   P1,0                MUST BE NORMAL LOCK                          
         JNE   *+2                 DIE=NOT NORMAL LOCK                          
*                                                                               
         CLI   DSPLOCKS,10         LESS THAN 10 LOCKS                           
         JNL   *+2                 DIE=LESS THEN 10                             
         SR    R1,R1                                                            
         IC    R1,DSPLOCKS         BUMP MULTIPLE LOCKS                          
         LA    R1,1(R1)                                                         
         STC   R1,DSPLOCKS                                                      
         MVI   P2,255              FLAG FOR RETURN                              
         B     RETURN                                                           
*                                                                               
OFLK015  SAC   0                                                                
         PUSH ACONTROL                                                          
         ACONTROL COMPAT(NOCASE)                                                
         SYSEVENT OKSWAP                                                        
         POP  ACONTROL                                                          
         SAC   512                                                              
*                                                                               
         ICM   R1,15,DSPWCNTO      BUMP NUM OF WAITS OFFLINE                    
         LA    R1,1(R1)                                                         
         STCM  R1,15,DSPWCNTO                                                   
*                                                                               
         TM    P1,X'08'            NOT FREE - TEST RETURN                       
         BO    RETURN                                                           
         TM    DSPFLAG,DSPLONG     LONG WAIT EXPECTED                           
         BZ    OFLK020                                                          
         TM    P1,X'04'            TEST RETURN ON LONG WAIT                     
         BO    RETURN                                                           
*                                                                               
OFLK020  MVC   FULL,DSPTIME        CALCULATE DELTA T                            
         OC    FULL,FULL                                                        
         BZ    OFLK030             ZERO TIME SO WAIT FOR IT                     
         ICM   RF,15,TIMENOW                                                    
         S     RF,FULL                                                          
         BM    OFLK030             WAIT IF NEGATIVE TOO                         
         CH    RF,=H'10000'        UP TO 100 SECONDS THEN WAIT                  
         BL    OFLK030                                                          
*                                                                               
         TM    DSPFLAG,DSPLONG     LONG WAIT EXPECTED                           
         BZ    OFLK025                                                          
         SR    R0,R0                                                            
         IC    R0,DSPLONGT         GET LONG TIME ESTIMATE                       
         MH    R0,=H'1000'         TIMES 10 TIMES 100 FOR MS                    
         CR    RF,R0                                                            
         BL    OFLK030             IF LESS THAN ESTIMATE THEN WAIT              
*                                                                               
OFLK025  MVC   BADLOCK,0(R2)       SAVE BAD LOCK INFO                           
         XR    R1,R1                                                            
         L     R0,DSPLOCK          SAVE LOCK IN R0                              
         ST    R1,DSPTIME          CLEAR TIME                                   
         XC    DSPJOB,DSPJOB       CLEAR JOBNO                                  
         XC    DSPTYPE,DSPTYPE     CLEAR JOBTYPE                                
         XC    DSPFLAG,DSPFLAG     CLEAR FLAGS                                  
         XC    DSPLONGT,DSPLONGT   CLEAR TIME ESTIMATE                          
         XC    DSPMVS,DSPMVS       CLEAR JOB NAME                               
         XC    DSPLOCKS,DSPLOCKS   REMOVE COUNT                                 
         OI    DSPECB,X'40'        FLAG ECB FREE                                
         ST    R1,DSPLOCK2         CLEAR LOCKWORD COPY                          
         CS    R0,R1,DSPLOCK                                                    
         BNE   BADFREE                                                          
         SAC   0                                                                
         MVC   MSG(2),=H'69'                                                    
         MVC   MSG+2(64),BADLOCK   SAVED LOCK ENTRY                             
         MVI   MSG+66,C'+'                                                      
         STCM  R0,15,MSG+67        DSPLOCK VALUE AT LAST CS INSTR               
         BRAS  RE,WTOHRD                                                        
         LARL  R4,AUTOMSG5         BADLOCK - OFFLINE                            
         BRAS  RE,WTO                                                           
         SAC   512                                                              
         B     OFLK                GO BACK AND TRY AGAIN                        
*                                                                               
BADFREE  MVC   MSG(2),=H'69'                                                    
         MVC   MSG+2(64),0(R2)     LOCK ENTRY IN DATASPACE                      
         MVI   MSG+66,C'+'                                                      
         STCM  R0,15,MSG+67        DSPLOCK VALUE AT LAST CS INSTR               
         SAC   0                                                                
         BRAS  RE,WTOHRD                                                        
         LARL  R4,AUTOMSG6         DSPLOCK,OFFLINE,NOT CLEAR                    
         BRAS  RE,WTO                                                           
         SAC   512                                                              
         B     OFLK                GO BACK AND TRY AGAIN                        
*                                                                               
OFLK030  BRAS  RE,ENQHOLD          DO ENQHOLD ON OWNER                          
*                                                                               
OFLK0301 SAC   0                                                                
         TM    SSOFLAG2,SSO2HIGH   TEST HIGH PRIORITY JOB                       
         BO    OFLK032                                                          
         LHI   R1,1000             SET TO 10000 MS (10 SECS)                    
         MHI   R1,384                                                           
         ST    R1,FULL                                                          
         STIMER WAIT,TUINTVL=FULL                                               
         B     OFLK                                                             
*                                                                               
OFLK032  L     R1,OFFLKN           OFFLKN IS INITIALLY ZERO                     
         AHI   R1,1                START AT 1 MS                                
         CHI   R1,3840             UP 1 MS EACH TIME UNTIL 3840 MS              
         BNL   *+8                                                              
         ST    R1,OFFLKN                                                        
         STIMER WAIT,TUINTVL=OFFLKN                                             
         B     OFLK                                                             
*NOP*    LHI   R1,10               SET TO 100 MS (1/10 SEC)                     
*NOP*    MHI   R1,384                                                           
*NOP*    ST    R1,FULL                                                          
*NOP*    STIMER WAIT,TUINTVL=FULL                                               
*NOP*    B     OFLK                                                             
         EJECT                                                                  
***********************************************************************         
* FREE THIS RESOURCE                                                  *         
***********************************************************************         
FREEIT   MVI   TLKACTN,C'U'        UNLOCK RESOURCE                              
         MVC   TLKOTIME,DSPTIME                                                 
         TM    P1,X'01'            UNCONDITIONAL FORCE FREE                     
         BZ    FREEIT1                                                          
         MVI   TLKERR,C'6'         FORCE LOCK FREE                              
         MVC   TLKOLOCK,DSPLOCK                                                 
         MVC   TLKOTASK,DSPADV                                                  
         B     FREEIT3                                                          
*                                                                               
FREEIT1  L     RF,DSPLOCK          TEST IF RESOURCE LOCKED                      
         LTR   RF,RF                                                            
         BZ    RETURN                                                           
         C     RF,MVSLOCK          IF SO I MUST BE THE OWNER                    
         BE    FREEIT2                                                          
         MVI   TLKERR,C'7'         CANT UNLOCK IF DIDNT LOCK FIRST              
         ST    RF,TLKOLOCK                                                      
         MVC   TLKOTASK,DSPADV                                                  
         BAS   RE,TRCIT                                                         
         B     RETURN                                                           
*                                                                               
FREEIT2  SR    R1,R1               DECR NUMBER OF LOCKS                         
         ICM   R1,1,DSPLOCKS                                                    
         BNZ   *+12                                                             
         MVI   TLKERR,C'8'         COUNT SHOULD NOT BE ZERO                     
         B     FREEIT3                                                          
         BCTR  R1,0                                                             
         STC   R1,DSPLOCKS                                                      
         STC   R1,TLKRCNT                                                       
         LTR   R1,R1               DON'T UNLOCK UNTIL COUNT=0                   
         BNZ   FREEITX                                                          
*                                                                               
FREEIT3  SR    R1,R1               FORCE FREE SET DSPLOCKS TO ZERO              
         STC   R1,DSPLOCKS                                                      
         ICM   RF,15,SSBTKADR      AM I FREEING ONE IN TCBDSPC                  
         BZ    FREEIT5                                                          
         LA    R0,3                                                             
         LA    RF,TCBDSPC-TCBD(RF)                                              
FREEIT4  CLC   P1+2(2),0(RF)       IF I FIND IT CLEAR IT                        
         BNE   *+10                                                             
         XC    0(2,RF),0(RF)                                                    
         LA    RF,2(RF)                                                         
         BCT   R0,FREEIT4                                                       
*                                                                               
FREEIT5  ICM   RF,15,TIMENOW       SET RF TO LOCK TIME                          
         SL    RF,DSPTIME                                                       
         ST    RF,TLKDELT          TRACE HOW LONG WE OWNED IT                   
         L     R0,DSPLOCK          SAVE LOCK IN R0                              
         XR    R1,R1                                                            
         ST    R1,DSPTIME          CLEAR TIME                                   
         XC    DSPJOB,DSPJOB       CLEAR JOBNO                                  
         XC    DSPTYPE,DSPTYPE     CLEAR JOBTYPE                                
         MVC   FLAG,DSPFLAG        SAVE BEFORE CLEARING                         
         XC    DSPFLAG,DSPFLAG     CLEAR FLAGS                                  
         XC    DSPLONGT,DSPLONGT   CLEAR TIME ESTIMATE                          
         XC    DSPMVS,DSPMVS       CLEAR JOB NAME                               
         OI    DSPECB,X'40'        FLAG ECB FREE                                
         ST    R1,DSPLOCK2         CLEAR LOCKWORD COPY                          
         CS    R0,R1,DSPLOCK                                                    
         BNE   BADFREE2                                                         
         SAC   0                                                                
*                                                                               
         PUSH ACONTROL             DO OKSWAP                                    
         ACONTROL COMPAT(NOCASE)                                                
         SYSEVENT OKSWAP                                                        
         POP  ACONTROL                                                          
*                                                                               
         TM    FLAG,DSPABPEN       ANY ABENDS PENDING                           
         BZ    FREEITX                                                          
*                                                                               
         ICM   R1,15,=V(SYSFAC)    SEARCH TCB FOR ANY ABENDS PENDING            
         BZ    FREEITX                                                          
         ICM   R1,15,VTCB-SYSFACD(R1)                                           
         BZ    FREEITX                                                          
*                                                                               
         LH    RE,0(R1)                                                         
         L     RF,2(R1)                                                         
         LA    R1,6(R1)                                                         
FREEIT6  TM    TCBINDS1-TCBD(R1),TCBABPEN     ABEND PENDING ?                   
         BNO   FREEIT7                                                          
         CLC   TCBSEN-TCBD(1,R1),DSRSRC+1     ON THIS SYSTEM ?                  
         BNE   FREEIT7                                                          
*                                                                               
         NI    TCBINDS1-TCBD(R1),255-TCBABPEN                                   
         LA    R0,STARTECB         USE THIS DUMMY ECB TO KICK IT OFF            
         ST    R0,TCBSVECB-TCBD(R1)                                             
*                                                                               
FREEIT7  BXLE  R1,RE,FREEIT6       NEXT TASK                                    
*                                                                               
FREEITX  SAC   0                                                                
         MVI   P2,0                ENSURE FLAGS ALL OFF                         
         BAS   RE,TRCIT                                                         
         B     EXIT                                                             
*                                                                               
BADFREE2 MVC   MSG(2),=H'69'                                                    
         MVC   MSG+2(64),0(R2)     LOCK ENTRY IN DATASPACE NOW                  
         MVI   MSG+66,C'+'                                                      
         STCM  R0,15,MSG+67        DSPLOCK VALUE AT LAST CS INSTR               
         SAC   0                                                                
         BRAS  RE,WTOHRD                                                        
         LARL  R4,AUTOMSG7         DSPLOCK,NOT CLEAR                            
         BRAS  RE,WTO                                                           
         SAC   512                                                              
         B     FREEITX             GO BACK AND TRY AGAIN                        
*                                                                               
         DS    0F                                                               
STARTECB DC    X'40000000'                                                      
         EJECT                                                                  
***********************************************************************         
* CS HAS ALLOCATED RESOURCE SO FILL THE HEADER                        *         
***********************************************************************         
LOCKIT   ST    R1,DSPLOCK2         COPY THE LOCK WORK                           
         LR    RF,R1               SAVE LOCK WORD IN RF                         
         ICM   R1,15,SSBTKADR      SET RESOURCE NUM IN TCB                      
         BZ    LOCKIT3                                                          
         LA    R0,3                                                             
         LA    R1,TCBDSPC-TCBD(R1)                                              
LOCKIT1  OC    0(2,R1),0(R1)       TEST AVAILABLE                               
         BNZ   LOCKIT2                                                          
         MVC   0(2,R1),P1+2        SET RESOURCE OWNED                           
         B     LOCKIT3                                                          
*                                                                               
LOCKIT2  CLC   0(2,R1),P1+2        DO I ALREADY OWN IT                          
         BE    LOCKIT3                                                          
         LA    R1,2(R1)            TRY NEXT SLOT                                
         BCT   R0,LOCKIT1                                                       
         L     R0,DSPLOCK                                                       
         SR    R1,R1                                                            
         CS    R0,R1,DSPLOCK       MUST RELEASE THIS ONE                        
         JE    *+2                 DIE=MAX RESOURCES                            
*                                                                               
         MVC   MSG(2),=H'69'                                                    
         MVC   MSG+2(64),0(R2)     LOCK ENTRY IN DATASPACE                      
         MVI   MSG+66,C'+'                                                      
         STCM  R0,15,MSG+67        DSPLOCK VALUE AT LAST CS INSTR               
         SAC   0                                                                
         BRAS  RE,WTOHRD                                                        
         LARL  R4,AUTOMSG8         DSPLOCK,NOT CLEAR                            
         BRAS  RE,WTO                                                           
         SAC   512                                                              
         DC    H'0'                DIE=MAX RESOURCES                            
*                                                                               
LOCKIT3  MVC   DSPMVS,MVSNAME      MVS JOBNAME                                  
         CLI   ONLIFLG,C'Y'        TEST ONLINE                                  
         BE    LOCKIT4                                                          
         C     RF,DSPLOCK          COMPARE LOCK WORD                            
         BE    LOCKIT3X            YES ITS THE SAME                             
         MVC   MSG(2),=H'69'                                                    
         MVC   MSG+2(64),0(R2)     LOCK ENTRY IN DATASPACE                      
         MVI   MSG+66,C'+'                                                      
         STCM  RF,15,MSG+67        MY LOCK                                      
         SAC   0                                                                
         BRAS  RE,WTOHRD                                                        
         LARL  R4,AUTOMSG1         DSPLOCK,OFFLINE,NOT MATCH                    
         BRAS  RE,WTO                                                           
         SAC   512                                                              
LOCKIT3X DS    0H                                                               
*                                                                               
         MVC   DSPJOB,MVSJNUM      USE JOBNO OFFLINE                            
         OI    DSPTYPE,DSPJOBQ     FLAG IT SO WE KNOW                           
         ICM   R1,15,DSPLCNTO      BUMP NUM OF LOCKS OFFLINE                    
         LA    R1,1(R1)                                                         
         STCM  R1,15,DSPLCNTO                                                   
         B     LOCKIT5                                                          
*                                                                               
LOCKIT4  MVC   DSPADV,MVSADV       ADV/TSK                                      
         MVI   DSPTYPE,0           MAKE SURE WE DON'T HAVE JOB FLAG ON          
*                                                                               
         C     RF,DSPLOCK          COMPARE LOCK WORD                            
         BE    LOCKIT4X            YES ITS THE SAME                             
*                                                                               
         MVC   MSG(2),=H'69'                                                    
         MVC   MSG+2(64),0(R2)     LOCK ENTRY IN DATASPACE                      
         MVI   MSG+66,C'+'                                                      
         STCM  RF,15,MSG+67        MY LOCK                                      
         SAC   0                                                                
         BRAS  RE,WTOHRD                                                        
         LARL  R4,AUTOMSG2         DSPLOCK,ONLINE,NOT MATCH                     
         BRAS  RE,WTO                                                           
         SAC   512                                                              
LOCKIT4X DS    0H                                                               
*                                                                               
         ICM   R1,15,DSPLCNT       BUMP NUM OF LOCKS                            
         LA    R1,1(R1)                                                         
         STCM  R1,15,DSPLCNT                                                    
*                                                                               
LOCKIT5  NI    DSPECB,255-X'40'    CLEAR FREE BIT IN ECB                        
         TM    P1,X'80'            TEST FOR LONG LOCK                           
         BZ    LOCKIT6                                                          
         OI    DSPFLAG,DSPLONG                                                  
         MVC   DSPLONGT,P3+3       SAVE SECONDS * 10 ESTIMATED                  
*                                                                               
LOCKIT6  TM    P1,X'40'            TEST FOR I/O WAIT LOCK                       
         BZ    *+8                                                              
         OI    DSPFLAG,DSPIOW                                                   
         TM    P1,X'02'            TEST FOR RECOVERY IN PROCESS LOCK            
         BZ    *+8                                                              
         OI    DSPFLAG,DSPRCVR                                                  
         MVI   DSPLOCKS,1          SET TO 1 LOCK                                
         L     R0,TIMENOW                                                       
         ST    R0,DSPTIME          SET TIME OF LOCK                             
*                                                                               
         CLC   P4,=C'ALET'         IF P4=C'ALET'                                
         BNE   *+10                                                             
         MVC   P4,MYALET           RETURN ALET IN P4                            
*                                                                               
         MVI   P2,255              FLAG FOR RETURN                              
         BAS   RE,TRCIT                                                         
*                                                                               
         B     RETURN                                                           
         EJECT                                                                  
***********************************************************************         
* RETURN TO USER WITH RESOURCE HEADER                                 *         
***********************************************************************         
RETURN   OC    P2+1(3),P2+1        ANY RETURN AREA SUPPLIED                     
         BNZ   RET010                                                           
         LA    RF,RETBLK           NO - RETURN THIS BLOCK                       
         STCM  RF,7,P2+1                                                        
*                                                                               
RET010   SR    RF,RF               RETURN RESOURCE HEADER                       
         ICM   RF,7,P2+1                                                        
         MVC   0(64,RF),0(R2)                                                   
         CLI   P2,255              TEST NORMAL ALLOCATE CALL                    
         BNE   RET015                                                           
         MVI   P2,0                                                             
         B     RET020                                                           
*                                                                               
RET015   TM    P1,X'20'            JUST AN ENQUIRY                              
         BO    RET020                                                           
         TM    DSPFLAG,DSPLONG     RETURN LONG WAIT                             
         BZ    *+12                                                             
         OI    P2,X'80'            LONG WAIT FLAG                               
         B     RET020                                                           
         OI    P2,X'40'            ELSE SHORT WAIT FLAG                         
*                                                                               
RET020   SAC   0                   TURN OFF ACCESS                              
         B     EXIT                                                             
         DROP  R2                                                               
         EJECT                                                                  
***********************************************************************         
* WAITING FOR RESOURCE. REQUEST PRIORITY BUMP OF HOLDER               *         
* CHECK JOB IS ACTIVE THEN DO SYSEVENT ENQHOLD TO BUMP IT'S PRIORITY  *         
***********************************************************************         
                                                                                
ENQHOLD  NTR1                                                                   
*                                                                               
*NOP     J     EXIT                THIS ROUTINE IS OPTIONAL                     
*                                                                               
         MVC   LOCKASID,DSPLOCK-DSPHDR(R2)                                      
         XC    LOCKASID(2),LOCKASID                                             
         MVC   LOCKNAME,DSPMVS-DSPHDR(R2)                                       
*                                                                               
         SAC   0                                                                
*                                                                               
*NOP     PGFIX R,A=(RC),EA=(RF),ECB=0    *NOP USE PGSER                         
*                                                                               
         MODESET MF=(E,SUP)        CHANGE TO SUPERVISOR STATE                   
         LR    RF,RC                                                            
         AHI   RF,4095                                                          
         PGSER R,FIX,A=(RC),ECB=0,EA=(RF)                                       
         MODESET MF=(E,PROB)       RETURN TO PROBLEM STATE                      
*                                                                               
         SAC   0                                                                
         L     R4,LOCKASID         GET THE ASID                                 
         LOCASCB ASID=(R4)                                                      
         LR    R5,R1                                                            
         LTR   RF,RF                                                            
         JNZ   ENQALLX             ERROR FROM MACRO                             
*                                                                               
         USING ASCB,R5                                                          
         CLC   ASCBASCB,=C'ASCB'                                                
         JNE   ENQALLX             NOT AN ASCB                                  
*                                                                               
         L     R4,ASCBASSB                                                      
         SAM31                                                                  
         ICM   R4,15,ASSBJSAB-ASSB(R4) R4 = A(JSAB)                             
         JZ    ENQALLX             NO JSAB                                      
*                                                                               
         ICM   R1,15,ASCBJBNI      ANY JOBNAME                                  
         JZ    ENQH005                                                          
         CLC   LOCKNAME,0(R1)      MUST MATCH                                   
         JE    ENQH010                                                          
         JNE   ENQALLX                                                          
*                                                                               
ENQH005  ICM   R1,15,ASCBJBNS      ANY TASKNAME                                 
         JZ    ENQALLX                                                          
         CLC   LOCKNAME,0(R1)      MUST MATCH                                   
         JNE   ENQALLX                                                          
         DROP  R5                                                               
*                                                                               
ENQH010  SAM24                                                                  
         SAC   512                                                              
         XC    PARMS(88),PARMS                                                  
         LA    R1,PARMS                                                         
         USING IRAENQHR_PARMLIST,R1                                             
         MVC   IRAENQHR_FUNCTION,=F'1'                                          
         MVC   IRAENQHR_ASID,LOCKASID+2                                         
         MVC   IRAENQHR_SUBSYS,=C'STC '                                         
         MVC   IRAENQHR_SUBSYSNAME,LOCKNAME                                     
*                                                                               
         OC    IRAENQHR_SUBSYSNAME,IRAENQHR_SUBSYSNAME                          
         JZ    ENQALLX             FINAL CHECK BEFORE CALL                      
         OC    IRAENQHR_ASID,IRAENQHR_ASID                                      
         JZ    ENQALLX                                                          
*                                                                               
         SAC   0                                                                
         PUSH ACONTROL                                                          
         ACONTROL COMPAT(NOCASE)                                                
         SYSEVENT ENQHOLD,TYPE=3,ENTRY=SVC                                      
         LTR   RF,RF                                                            
         JNZ   *+2                                                              
         POP  ACONTROL                                                          
*                                                                               
ENQALLX  EQU   *                                                                
*                                                                               
*NOP     PGFREE R,A=(RC),EA=(RF),ECB=0   *NOP USE PGSER                         
*                                                                               
         MODESET MF=(E,SUP)        CHANGE TO SUPERVISOR STATE                   
         LR    RF,RC                                                            
         AHI   RF,4095                                                          
         PGSER R,FREE,A=(RC),ECB=0,EA=(RF)                                      
         MODESET MF=(E,PROB)       RETURN TO PROBLEM STATE                      
*                                                                               
         SAM24                                                                  
         SAC   512                                                              
         J     EXIT                                                             
*                                                                               
SUP      MODESET MODE=SUP,MF=L PARMLIST FOR SUPERVISOR STATE                    
PROB     MODESET MODE=PROB,MF=L PARMLIST FOR PROBLEM STATE                      
         DROP  R1                                                               
         EJECT                                                                  
***********************************************************************         
* ENQUIRY CALL (ENQUIRE ON RESOURCE ZERO)                             *         
***********************************************************************         
ENQUIRY  OC    P2+1(3),P2+1        ANY RETURN AREA SUPPLIED                     
         BNZ   ENQ010                                                           
         LA    RF,RETBLK           NO - RETURN THIS BLOCK                       
         STCM  RF,7,P2+1                                                        
ENQ010   SR    RF,RF                                                            
         ICM   RF,7,P2+1           BUILD DUMMY HEADER                           
         USING DMSPACED,RF                                                      
         XC    DSPHDR,DSPHDR                                                    
         MVC   DSPLOCK,MVSLOCK     CPU/ASID                                     
         MVC   DSPMVS,MVSNAME      MVS JOBNAME                                  
*                                                                               
         CLI   ONLIFLG,C'Y'        TEST ONLINE                                  
         BE    ENQ020                                                           
         MVC   DSPJOB,MVSJNUM      USE JOBNO OFFLINE                            
         OI    DSPTYPE,DSPJOBQ     FLAG IT SO WE KNOW                           
         B     ENQ030                                                           
*                                                                               
ENQ020   MVC   DSPADV,MVSADV       ADV/TSK                                      
*                                                                               
ENQ030   B     EXIT                                                             
         DROP  RF                                                               
         EJECT                                                                  
***********************************************************************         
* OPEN SYSTEM FOR MAINTENANCE ACTION                                  *         
***********************************************************************         
MAINTOPN MVI   BYTE1,C'I'          INIT CALL TO CHKDSN                          
         NI    P2,X'7F'            TURN-OFF ERROR INCASE RETRY CALL             
         BAS   RE,CHKDSN                                                        
         CLI   BYTE1,C'L'          DID IT RETURN LOCAL                          
         BE    OPEN990                                                          
*                                                                               
         SAC   512                 SET ACCESS REG MODE                          
         LAM   AR0,ARF,ARZERO                                                   
         LAM   AR2,AR2,MYALET                                                   
         SR    R2,R2                                                            
*                                                                               
         SR    RF,RF               RF=RESOURCE NUMBER                           
         ICM   RF,3,P1+2                                                        
         JZ    *+2                 DIE=INV RESOURCE NUM                         
         N     RF,=X'00000FFF'                                                  
         SLL   RF,6                INDEX TO RESOURCE HEADER                     
         AR    R2,RF                                                            
         USING DMSPACED,R2                                                      
*                                                                               
         L     R2,DSPECB           GET A(SYSTEM)                                
         N     R2,=X'00FFFFFF'     IGNORE POST BITS                             
         ST    R2,ASYSTEM                                                       
         USING DMSYSHDR,R2                                                      
         TM    DSYSSTAT,DSYQOPEN   TEST SYSTEM OPEN                             
         BNZ   MAINT010                                                         
         OI    DSYSSTAT,DSYQEXCL   SET EXCLUSIVE OWNERSHIP                      
         B     OPEN005                                                          
*                                                                               
MAINT010 L     R2,DSYAJOBS         LOCATE JOB TABLE                             
         USING DSJOBHDR,R2         AND COPY JOBTAB TO RETBLK                    
         MVC   RETBLK+000(255),0(R2)                                            
         MVC   RETBLK+256(255),256(R2)                                          
         MVC   RETBLK+512(255),512(R2)                                          
         MVC   RETBLK+768(255),768(R2)                                          
         OI    P2,X'80'            FLAG MAINT OPEN FAILED                       
         LA    R1,RETBLK                                                        
         STCM  R1,7,P2+1                                                        
         MVI   MSGNUM,1                                                         
         B     WARNING                                                          
         B     OPEN990                                                          
         EJECT                                                                  
***********************************************************************         
* SET SYSTEM TO ACTIVE - UPDATE HAS BEEN DONE                         *         
***********************************************************************         
SETACT   MVI   BYTE1,C'A'          SET ACTIVE CALL TO CHKDSN                    
*NOP     BAS   RE,CHKDSN           CHECK FILES ARE CONSISTENT                   
*                                                                               
SETACT1  SAC   512                 SET ACCESS REG MODE                          
         LAM   AR0,ARF,ARZERO                                                   
         LAM   AR2,AR2,MYALET                                                   
         SR    R2,R2                                                            
*                                                                               
         SR    RF,RF               RF=RESOURCE NUMBER                           
         ICM   RF,3,P1+2                                                        
         JZ    *+2                 DIE=INV RESOURCE NUM                         
         N     RF,=X'00000FFF'                                                  
         SLL   RF,6                INDEX TO RESOURCE HEADER                     
         AR    R2,RF                                                            
         USING DMSPACED,R2                                                      
*                                                                               
         L     R2,DSPECB           GET A(SYSTEM)                                
         N     R2,=X'00FFFFFF'     IGNORE POST BITS                             
         ST    R2,ASYSTEM                                                       
         USING DMSYSHDR,R2                                                      
*                                                                               
         LTR   R2,R2               SYSTEM MUST EXIST                            
         JZ    *+2                 DIE=SYS NOT DEFINED                          
*                                                                               
SETA005  L     R2,ASYSTEM                                                       
         L     R2,DSYAJOBS         LOCATE JOB TABLE                             
         USING DSJOBHDR,R2                                                      
         LR    RF,R2               SAVE R2 IN RF                                
*                                                                               
         LA    R1,DSJBHMXQ         MAX ENTRIES                                  
         LA    RE,RETBLK                                                        
         XC    0(8,RE),0(RE)                                                    
*                                                                               
SETA006  CLC   DSJOBNAM,MVSNAME    IGNORE MY OWN JOB OR PREVIOUS                
         BE    SETA007                                                          
         CLC   =C'RCVR',MVSNAME    RCVRXXX JOBS CAN IGNORE                      
         JNE   *+12                                                             
         TM    DSJOBFLG,DSJOBABN   JOBS THAT HAVE ABND FLAG                     
         JO    SETA007             LET RCVR RECOVERY EXCLUSIVE JOB              
*                                                                               
         CLI   LOCKERF,C'Y'        LOCKER BEING USED                            
         BE    SETA006A                                                         
         CLI   ONLIFLG,C'Y'        ARE WE ONLINE                                
         BE    *+12                                                             
         TM    SSOFLAG2,SSO2CLU    OFFLINE CAN USE LOCICAL LOCKS                
         BO    SETA006A                                                         
*                                                                               
         TM    DSJOBFLG,DSJOBUPQ+DSJOBSHQ+DSJOBMAQ                              
         BZ    SETA007             TEST FOR UPDATE SHR OR MAINT                 
         B     SETA006X                                                         
*                                                                               
SETA006A TM    DSJOBFLG,DSJOBUPQ+DSJOBMAQ                                       
         BZ    SETA007             TEST FOR UPDATE OR MAINT                     
         B     SETA006X                                                         
*                                                                               
SETA006X MVC   0(8,RE),0(R2)       SET IT IN RETURN BLOCK                       
         LA    RE,16(RE)                                                        
         XC    0(8,RE),0(RE)                                                    
*                                                                               
SETA007  LA    R2,16(,R2)          NEXT ENTRY                                   
         BCT   R1,SETA006                                                       
         LA    RE,RETBLK                                                        
         MVI   MSGNUM,3                                                         
         OC    0(8,RE),0(RE)       ANYTHING ACTIVE NOW                          
         BNZ   WARNING                                                          
*                                                                               
         LR    R2,RF               NO ACTIVE FOUND                              
*                                                                               
         LA    R1,DSJBHMXQ         MAX ENTRIES                                  
SETA010  CLC   MVSALL,DSJOBNAM                                                  
         BE    SETA020                                                          
         LA    R2,16(,R2)          NEXT ENTRY                                   
         BCT   R1,SETA010                                                       
         B     SETA990             BUT THERE ARE EXCEPTIONS                     
*                                                                               
SETA020  CLI   LOCKERF,C'Y'        LOCKER OR NOT                                
         BE    SETA021                                                          
         CLI   ONLIFLG,C'Y'        ARE WE ONLINE                                
         BE    *+12                                                             
         TM    SSOFLAG2,SSO2CLU    OFFLINE CAN USE LOCICAL LOCKS                
         BO    SETA021                                                          
         MVI   DSJOBFLG,DSJOBUPQ   FLAG UPDATIVE NOW                            
         B     SETA022                                                          
*                                                                               
SETA021  MVI   DSJOBFLG,DSJOBSHQ   FLAG SHARED NOW                              
*                                                                               
SETA022  L     R1,MYSSB                                                         
         CLI   ONLIFLG,C'Y'        ONLINE SET RECOVERY ON                       
         BE    SETA030                                                          
         TM    SSOFLAG1-SSOOFF(R1),SSOFRCVR                                     
         BZ    SETA990            SSOFRCVR MEANS OFFLINE RECOVERY               
*                                                                               
SETA030  OI    DSJOBFLG,DSJOBRCV   FLAG RECOVERY REQUIRED                       
*                                                                               
SETA990  SAC   0                   RETURN TO NORMAL MODE                        
         B     EXIT                                                             
         EJECT                                                                  
***********************************************************************         
* SET SYSTEM TO READ-ONLY                                             *         
***********************************************************************         
SETREAD  SAC   512                 SET ACCESS REG MODE                          
         LAM   AR0,ARF,ARZERO                                                   
         LAM   AR2,AR2,MYALET                                                   
         SR    R2,R2                                                            
*                                                                               
         SR    RF,RF               RF=RESOURCE NUMBER                           
         ICM   RF,3,P1+2                                                        
         JZ    *+2                 DIE=INV RESOURCE NUM                         
         N     RF,=X'00000FFF'                                                  
         SLL   RF,6                INDEX TO RESOURCE HEADER                     
         AR    R2,RF                                                            
         USING DMSPACED,R2                                                      
*                                                                               
         L     R2,DSPECB           GET A(SYSTEM)                                
         N     R2,=X'00FFFFFF'     IGNORE POST BITS                             
         ST    R2,ASYSTEM                                                       
         USING DMSYSHDR,R2                                                      
         LTR   R2,R2               SYSTEM DOESN'T EXIST                         
         BZ    SETR990                                                          
*                                                                               
SETR005  L     R2,ASYSTEM                                                       
         L     R2,DSYAJOBS         LOCATE JOB TABLE                             
         USING DSJOBHDR,R2                                                      
*                                                                               
         LA    R1,DSJBHMXQ         MAX ENTRIES                                  
SETR010  CLC   MVSALL,DSJOBNAM                                                  
         BE    SETR020                                                          
         LA    R2,16(,R2)          NEXT ENTRY                                   
         BCT   R1,SETR010                                                       
         B     SETR990             MY JOB IS NOT HERE                           
*                                                                               
SETR020  MVI   DSJOBFLG,DSJOBROQ   FLAG JOB AS READ-ONLY                        
*                                                                               
SETR990  SAC   0                   RETURN TO NORMAL MODE                        
         B     EXIT                                                             
         EJECT                                                                  
***********************************************************************         
* FIND AND RETURN JOB ENTRY - USED BY LOCKER                          *         
***********************************************************************         
JOBVAL   XC    RETBLK(16),RETBLK                                                
         MVI   P2,255                                                           
*                                                                               
JOBVAL1  SAC   512                 SET ACCESS REG MODE                          
         LAM   AR0,ARF,ARZERO                                                   
         LAM   AR2,AR2,MYALET                                                   
         SR    R2,R2                                                            
*                                                                               
         SR    RF,RF               RF=RESOURCE NUMBER                           
         ICM   RF,3,P1+2                                                        
         JZ    *+2                 DIE=INV RESOURCE NUM                         
         N     RF,=X'00000FFF'                                                  
         SLL   RF,6                INDEX TO RESOURCE HEADER                     
         AR    R2,RF                                                            
         USING DMSPACED,R2                                                      
*                                                                               
         L     R2,DSPECB           GET A(SYSTEM)                                
         N     R2,=X'00FFFFFF'     IGNORE POST BITS                             
         ST    R2,ASYSTEM                                                       
         USING DMSYSHDR,R2                                                      
*                                                                               
         LTR   R2,R2               SYSTEM DOESN'T EXIST                         
         BZ    JOBV90                                                           
*                                                                               
JOBV05   L     R2,ASYSTEM                                                       
         L     R2,DSYAJOBS         LOCATE JOB TABLE                             
         USING DSJOBHDR,R2                                                      
*                                                                               
         LA    R1,DSJBHMXQ         MAX ENTRIES                                  
JOBV10   CLC   MVSALL,DSJOBNAM                                                  
         BE    JOBV20                                                           
         LA    R2,16(,R2)          NEXT ENTRY                                   
         BCT   R1,JOBV10                                                        
         B     JOBV90              MY JOB IS NOT HERE                           
*                                                                               
JOBV20   MVC   RETBLK(16),0(R2)    RETURN JOB ENTRY                             
*                                                                               
JOBV90   LA    RF,RETBLK           RETURN TO CALLER                             
         ST    RF,P2                                                            
         SAC   0                                                                
         B     EXIT                                                             
         EJECT                                                                  
***********************************************************************         
* RE-INITIALISE LOCKSPC                                               *         
***********************************************************************         
REINIT   MVI   INITFLGS,0          ALL INITS RESET                              
         B     EXIT                                                             
         EJECT                                                                  
***********************************************************************         
* OPEN SYSTEM AND ADD ENTRY TO JOBTABLE                               *         
***********************************************************************         
OPENSYS  MVI   BYTE1,C'I'          INIT CALL TO CHKDSN                          
*                                                                               
* FOLLOWING MADE US-ONLY. JUST BECAUSE FLASH=X IS SPECIFIED, DOESN'T            
* MEAN THAT ALL SYSTEMS OPENED BY THE TASK ARE FLASH COPIES. IT IS              
* PERFECTLY REASONABLE TO HAVE A MEDIA FILE OPENED AS A READ-ONLY               
* FLASH COPY BUT ALSO OPEN THE CONTROL SYSTEM FOR UPDATE (E.G. ESS)             
*&&US                                                                           
         CLI   ONLIFLG,C'Y'        IF OFFLINE AND FLASH=Y                       
         BE    OPENSYS1                                                         
         ICM   RF,15,MYSSB         DON'T OPEN GLOBAL SYS                        
         BZ    OPENSYS1                                                         
         TM    SSOFLAG2-SSOOFF(RF),SSO2FLSH                                     
         BO    OPEN990                                                          
*&&                                                                             
*                                                                               
OPENSYS1 BAS   RE,CHKDSN                                                        
         CLI   BYTE1,C'L'          DID IT RETURN LOCAL                          
         BE    OPEN990                                                          
*&&UK                                                                           
         CLI   MYDSPACE,C'T'       TEST DATASPACE                               
         JNE   OPENSYS2                                                         
         CLC   P1+2(2),=X'0014'    AND MEDZ                                     
         JNE   OPENSYS2                                                         
         CLI   READONLY,C'Y'       IGNORE IF ALL FILES R/O                      
         JE    OPEN990                                                          
*&&                                                                             
OPENSYS2 SAC   512                 SET ACCESS REG MODE                          
         LAM   AR0,ARF,ARZERO                                                   
         LAM   AR2,AR2,MYALET                                                   
         SR    R2,R2                                                            
*                                                                               
         SR    RF,RF               RF=RESOURCE NUMBER                           
         ICM   RF,3,P1+2                                                        
         JZ    *+2                 DIE=INV RESOURCE NUM                         
         N     RF,=X'00000FFF'                                                  
         SLL   RF,6                INDEX TO RESOURCE HEADER                     
         AR    R2,RF                                                            
         USING DMSPACED,R2                                                      
*                                                                               
         L     R2,DSPECB           GET A(SYSTEM)                                
         N     R2,=X'00FFFFFF'     IGNORE POST BITS                             
         ST    R2,ASYSTEM                                                       
         USING DMSYSHDR,R2                                                      
         MVC   SYSNAME,DSYSID+5    SAVE SYSTEM NAME                             
         MVC   SYSNUM,DSYSENUM     SAVE SYSTEM NUMBER                           
*                                                                               
         LTR   R2,R2                                                            
         BNZ   OPEN001                                                          
         SAC   0                                                                
         WTO   '*** SYSTEM IS NOT DEFINED IN DSPACE ***'                        
         B     OPEN990                                                          
*                                                                               
OPEN001  TM    DSYSSTAT,DSYQEXCL   TEST EXCLUSIVE ACCESS                        
         BZ    OPEN005                                                          
*                                                                               
         LR    RF,R2               SAVE R2 IN RF                                
         L     R2,ASYSTEM                                                       
         L     R2,DSYAJOBS         LOCATE JOB TABLE                             
         CLC   DSJOBNAM-DSJOBHDR(8,R2),MVSNAME    IGNORE MY OWN JOB             
         BNE   *+10                                                             
         LR    R2,RF               RESTORE R2 AND CONTINUE                      
         B     OPEN005                                                          
*                                                                               
         XC    RETBLK(32),RETBLK                                                
         MVC   RETBLK(16),0(R2)    COPY IN JOB (MUST BE ONLY ONE)               
         MVI   MSGNUM,2                                                         
         B     WARNING                                                          
*                                                                               
OPEN005  LR    RF,R2               SAVE R2 IN RF                                
*                                                                               
         L     R2,ASYSTEM                                                       
         L     R2,DSYAJOBS         LOCATE JOB TABLE                             
         USING DSJOBHDR,R2                                                      
*                                                                               
         LA    R1,DSJBHMXQ         MAX ENTRIES                                  
         LA    RE,RETBLK                                                        
         XC    0(8,RE),0(RE)                                                    
*                                                                               
OPEN006  CLC   DSJOBNAM,MVSNAME    IGNORE MY OWN JOB                            
         BE    OPEN007                                                          
         CLC   =C'RCVR',MVSNAME    RCVRXXX JOBS CAN IGNORE                      
         JNE   *+12                                                             
         TM    DSJOBFLG,DSJOBABN   JOBS THAT HAVE ABND FLAG                     
         JO    OPEN007                                                          
*                                                                               
         CLI   P1+1,10             R/O CHECK MAINT ONLY                         
         BNE   OPEN006A                                                         
         TM    DSJOBFLG,DSJOBMAQ                                                
         BZ    OPEN007             TEST FOR MAINT                               
         B     OPEN006B                                                         
*                                                                               
OPEN006A TM    DSJOBFLG,DSJOBUPQ+DSJOBMAQ                                       
         BZ    OPEN007             TEST FOR UPDATE OR MAINT                     
*                                                                               
OPEN006B MVC   0(8,RE),0(R2)       SET IT IN RETURN BLOCK                       
         LA    RE,16(RE)                                                        
         XC    0(8,RE),0(RE)                                                    
*                                                                               
OPEN007  LA    R2,16(,R2)          NEXT ENTRY                                   
         BCT   R1,OPEN006                                                       
         LA    RE,RETBLK                                                        
         MVI   MSGNUM,4                                                         
         OC    0(8,RE),0(RE)       ANYTHING ACTIVE NOW                          
         BNZ   WARNING                                                          
*                                                                               
OPEN008  L     R2,ASYSTEM                                                       
         USING DMSYSHDR,R2                                                      
         OI    DSYSSTAT,DSYQOPEN   SET SYSTEM TO OPEN                           
         L     R2,DSYAJOBS         LOCATE JOB TABLE                             
         USING DSJOBHDR,R2                                                      
         LR    RF,R2               SAVE R2 IN RF                                
*                                                                               
         LA    R1,DSJBHMXQ         MAX ENTRIES                                  
OPEN009  CLC   MVSALL,DSJOBNAM     OVERWRITE ANY OLD JOB ENTRY                  
         BE    OPEN011                                                          
         LA    R2,16(,R2)          NEXT ENTRY                                   
         BCT   R1,OPEN009                                                       
         LR    R2,RF               NO OLD ENTRY FOUND                           
*                                                                               
         LA    R1,DSJBHMXQ         MAX ENTRIES                                  
OPEN010  OC    DSJOBNAM,DSJOBNAM                                                
         BNZ   OPEN020                                                          
OPEN011  MVC   DSJOBNAM,MVSNAME    FILL IN MY DETAILS                           
         MVC   DSJOBNUM,MVSJNUM                                                 
         MVC   DSJOBADV,MVSADV                                                  
         L     R0,TIMENOW                                                       
         SRL   R0,8                                                             
         STH   R0,DSJTIME                                                       
         MVC   DSJOASID,MVSASID                                                 
*                                                                               
         OI    DSJOBFLG,DSJOBROQ   FLAG R/O ACCESS                              
*                                                                               
         CLI   P1+1,6              IS THIS A MAINT OPEN                         
         BNE   *+8                                                              
         MVI   DSJOBFLG,DSJOBMAQ   FLAG MAINT ACCESS                            
         ST    R2,AJOBNTRY         SAVE A(OUR JOBTAB ENTRY)                     
         B     OPEN030                                                          
*                                                                               
OPEN020  LA    R2,16(,R2)          NEXT ENTRY                                   
         BCT   R1,OPEN010                                                       
         ABEND 251,DUMP            NO ROOM IN JOB TABLE                         
*                                                                               
OPEN030  OC    P3+2(2),P3+2        ANY FILE ACTIONS SET                         
         BZ    OPEN990                                                          
         L     R2,ASYSTEM                                                       
         USING DMSYSHDR,R2                                                      
         SR    R0,R0               SET R0 TO NUMBER OF FILES                    
         ICM   R0,3,DSYFILES                                                    
         BZ    OPEN990                                                          
         LA    R2,DSYDATA          SET R2 TO FILETAB                            
OPEN035  CLC   P3+2(1),0(R2)       SEARCH FOR FILE                              
         BE    OPEN040                                                          
         LA    R2,32(,R2)          NEXT                                         
         BCT   R0,OPEN035                                                       
         B     OPEN990                                                          
*                                                                               
OPEN040  MVC   1(1,R2),P3+3        SET ACTION CODE                              
*                                                                               
OPEN990  SAC   0                   RETURN TO NORMAL MODE                        
         B     EXIT                                                             
         EJECT                                                                  
***********************************************************************         
* CLOSE SYSTEM AFTER MAINTENANCE ACTION                               *         
***********************************************************************         
MAINTCLS SAC   512                 SET ACCESS REG MODE                          
         LAM   AR0,ARF,ARZERO                                                   
         LAM   AR2,AR2,MYALET                                                   
         SR    R2,R2                                                            
*                                                                               
         LH    RF,DSRSRC           RF=RESOURCE NUMBER                           
         SLL   RF,6                INDEX TO RESOURCE HEADER                     
         AR    R2,RF                                                            
         USING DMSPACED,R2                                                      
*                                                                               
         L     R2,DSPECB           GET A(SYSTEM)                                
         ST    R2,ASYSTEM                                                       
         USING DMSYSHDR,R2                                                      
         SR    R0,R0               SET R0 TO NUMBER OF FILES                    
         ICM   R0,3,DSYFILES                                                    
         BZ    CLOSESYS                                                         
*                                                                               
         LA    R2,DSYDATA          SET R2 TO FILETAB                            
MAINTC05 MVI   1(R2),0             CLEAR ACTION BYTE                            
         LA    R2,32(,R2)          NEXT                                         
         BCT   R0,MAINTC05                                                      
         B     CLOSESYS                                                         
         EJECT                                                                  
***********************************************************************         
* CLOSE SYSTEM AND REMOVE ENTRY FROM JOBTABLE                         *         
***********************************************************************         
CLOSESYS SAC   512                 SET ACCESS REG MODE                          
         LAM   AR0,ARF,ARZERO                                                   
         LAM   AR2,AR2,MYALET                                                   
         SR    R2,R2                                                            
*                                                                               
         LH    RF,DSRSRC           RF=RESOURCE NUMBER                           
         SLL   RF,6                INDEX TO RESOURCE HEADER                     
         AR    R2,RF                                                            
         USING DMSPACED,R2                                                      
*                                                                               
         L     R2,DSPECB           GET A(SYSTEM)                                
         N     R2,=X'00FFFFFF'     IGNORE POST BITS                             
         ST    R2,FULL                                                          
         USING DMSYSHDR,R2                                                      
         MVC   SYSNAME,DSYSID+5    SAVE SYSTEM NAME                             
         MVC   SYSNUM,DSYSENUM     SAVE SYSTEM NUMBER                           
         L     R2,DSYAJOBS         LOCATE JOB TABLE                             
         USING DSJOBHDR,R2                                                      
*                                                                               
         LA    R1,DSJBHMXQ         MAX ENTRIES                                  
         MVI   FLAG1,0                                                          
CLOSE010 CLC   MVSALL,DSJOBNAM     FIND THIS JOB                                
         BNE   CLOSE020                                                         
         XC    DSJOBNAM(16),DSJOBNAM                                            
*                                                                               
         OI    FLAG1,X'40'         FLAG 40 IN FLAG1 IF WE CLEARED ONE           
*                                                                               
CLOSE020 OC    DSJOBNAM,DSJOBNAM   FLAG 80 IN FLAG1 IF ANY JOBS                 
         BZ    *+8                                                              
         OI    FLAG1,X'80'                                                      
         LA    R2,16(,R2)          NEXT ENTRY                                   
         BCT   R1,CLOSE010                                                      
*                                                                               
         L     R2,FULL             BACK TO SYSTEM AREA                          
         USING DMSYSHDR,R2                                                      
         SR    R0,R0                                                            
         ICM   R0,3,DSYFILES       BUMP AND REPORT ON FILES                     
         LA    R2,DSYDATA                                                       
CLOSE025 MVI   REPTFLAG,X'02'      SET CLOSE FLAG                               
         BRAS  RE,REPTDSN                                                       
         LA    R2,32(,R2)                                                       
         BCT   R0,CLOSE025                                                      
*                                                                               
         MVC   MSG+02(23),=C'SYSNAME >CLOSED        '                           
         MVC   MSG+02(7),SYSNAME                                                
         MVC   MSG(2),=H'23'                                                    
         SAC   0                                                                
         CLI   ONLIFLG,C'Y'                                                     
         BE    CLOSE026                                                         
         L     RF,MYSSB                                                         
         TM    SSOFLAG3-SSOOFF(RF),SSO3NOFM  MESSAGE NOT WANTED                 
         BO    CLOSE027                                                         
CLOSE026 WTO   TEXT=MSG,MCSFLAG=HRDCPY                                          
CLOSE027 SAC   512                                                              
         LAM   ARE,AR1,ARZERO                                                   
*                                                                               
         TM    FLAG1,X'80'         ANY JOBS AT ALL                              
         BO    CLOSE990                                                         
         B     CLEAR                                                            
         EJECT                                                                  
***********************************************************************         
* CHECK THE DSN'S OF FILES TO BE OPENED                               *         
***********************************************************************         
CHKDSN   NTR1                                                                   
         L     R5,=V(SYSFLES)      FIND SYSTEM IN SYSFLES LIST                  
         USING SYSFLSTD,R5                                                      
CHK010   CLC   SYSFSYS#,P1+3                                                    
         BE    CHK020                                                           
         LH    RF,SYSF#FLS         RF=NUM OF FILES                              
         SLL   RF,3                                                             
         LA    R5,SYSFLIST(RF)                                                  
         CLI   SYSFSYS#,X'FF'      TEST EOT                                     
         BNE   CHK010                                                           
         DC    H'0'                DIE=SYS NOT DEFINED                          
*                                                                               
CHK020   ST    R5,ASYSFLES         SAVE THIS                                    
*NOP     TM    SYSFSTAT,SYSFDSP    SYSTEM IS DATASPACE PROTECTED                
*NOP     BZ    CHKXXX                                                           
*                                                                               
         SR    R0,R0               SET R0 TO NUM OF FILES                       
         ICM   R0,3,SYSF#FLS                                                    
         LA    R5,SYSFLIST         R5=A(FIRST FILE)                             
*                                                                               
         SAC   512                 SET ACCESS REG MODE                          
         LAM   AR0,ARF,ARZERO                                                   
         LAM   AR2,AR2,MYALET                                                   
         SR    R2,R2                                                            
*                                                                               
         LH    RF,DSRSRC           RF=RESOURCE NUMBER                           
         SLL   RF,6                INDEX TO RESOURCE HEADER                     
         AR    R2,RF                                                            
         USING DMSPACED,R2                                                      
*                                                                               
         L     R2,DSPECB           GET A(SYSTEM)                                
         ST    R2,ASYSTEM                                                       
         USING DMSYSHDR,R2                                                      
         MVC   SYSNAME,DSYSID+5    SAVE SYSTEM NAME                             
         MVC   SYSNUM,DSYSENUM     SAVE SYSTEM NUMBER                           
         CLM   R0,3,DSYFILES                                                    
         JH    *+2                 DIE=TOO MANY FILES                           
*                                                                               
         LA    R2,DSYDATA          SET R2 TO FILETAB                            
         USING DSFILHDR,R2                                                      
         MVI   READONLY,C'Y'                                                    
*                                                                               
CHK050   CLC   SYSFILE#,DSFILNUM   CHECK FILES AGAINST DMFILES                  
         JNE   *+2                 DIE=NO MATCH FILE                            
         SR    RF,RF                                                            
         ICM   RF,7,SYSFADTF       GET A(DTF)                                   
*                                                                               
         TM    ISFOPEN-ISDTF(RF),ISFORO+ISFONOP                                 
         BNZ   CHK070              IGNORE READ-ONLY OR NOP                      
         TM    SYSFIND2,SF_RO+SFALIAS                                           
         BNZ   CHK070              IGNORE READ-ONLY OR ALIAS                    
         MVI   READONLY,C'N'                                                    
*                                                                               
         MVC   DUB,ISFDD-ISDTF(RF) NAME DD NAME FOR ERROR MESSAGE               
         LA    RF,ISFFID-ISDTF(RF)                                              
         SAC   0                                                                
         BAS   RE,GETDSN           GET THE DSN FROM DYNALLOC                    
         LAM   ARF,ARF,ARZERO      DYNALLOC SOMETIMES SETS ARF                  
         SAC   512                                                              
         OC    0(20,RF),0(RF)      TEST FOR NO NAME                             
         BZ    CHK070                                                           
*                                                                               
*NOP     CLI   BYTE1,C'A'          IS THIS THE ACTIVATE CALL                    
*NOP     BE    CHK055                                                           
         BAS   RE,CHKLOC           NO CHECK FOR LOCAL                           
         CLI   BYTE1,C'L'                                                       
         BE    CHK070                                                           
*                                                                               
CHK055   BAS   RE,PUTDSN           PUT IT INTO DHADSN TABLE                     
*                                                                               
         OC    DSDSNX,DSDSNX       NO DSN ALLOCATED                             
         BZ    *+14                                                             
         CLC   HALF,DSDSNX         DSN IS SAME AS BEFORE                        
         BNE   CHK056                                                           
*                                                                               
         MVI   REPTFLAG,X'01'      SET CHECK/OPEN FLAG                          
         BRAS  RE,REPTDSN          SHOW US SOME DETAILS                         
         B     CHK060                                                           
*                                                                               
         PUSH  USING                                                            
CHK056   MVC   HALF,DSDSNX         SAVE ACTUAL DSNX                             
         SAC   0                                                                
         LA    R4,MSG                                                           
         MVC   MSG+2(23),=C'FILE NAME MISMATCH FOR '                            
         MVC   MSG+25(8),DUB                                                    
         MVC   MSG(2),=H'32'                                                    
         WTO   TEXT=(R4)                                                        
         SAC   512                                                              
         SR    R2,R2                                                            
         USING DMDSHDR,R2          LOCATE DHADSN                                
         L     R2,DHADSN                                                        
         LH    R1,HALF                                                          
         BCTR  R1,0                                                             
         SLL   R1,5                                                             
         AR    R2,R1               R2=A(DSN)                                    
         LA    R4,MSG                                                           
         MVC   MSG+2(10),=C'EXPECTING '                                         
         MVC   MSG+12(30),0(R2)                                                 
         MVC   MSG(2),=H'40'                                                    
         SAC   0                                                                
         WTO   TEXT=(R4)                                                        
         ABEND 252                 FILE NAMES MUST MATCH                        
         POP   USING                                                            
*                                                                               
CHK060   MVC   DSDSNX,HALF         SET FILE NAME INDEX                          
*                                                                               
CHK070   LA    R2,32(,R2)          NEXT FILE                                    
         LA    R5,SYSFLNQ(,R5)     NEXT DTF                                     
         BCT   R0,CHK050                                                        
*                                                                               
         SAC   0                                                                
         CLI   BYTE1,C'L'                                                       
         BE    CHK090                                                           
         CLI   ONLIFLG,C'Y'                                                     
         BE    *+12                                                             
         TM    SSOFLAG3,SSO3NOFM   MESSAGE NOT WANTED                           
         BO    CHKXXX                                                           
         LA    R4,MSG                                                           
         MVC   MSG+02(32),=C'SYSNAME >OPEN CHK DSNS  DSPACE=X'                  
         MVC   MSG+02(7),SYSNAME                                                
         MVC   MSG+33(1),SSODSPAC                                               
         CLI   ONLIFLG,C'N'                                                     
         BE    *+10                                                             
         MVC   MSG+33(1),SSBDSPAC                                               
         MVC   MSG(2),=H'32'                                                    
         WTO   TEXT=(R4),MCSFLAG=HRDCPY                                         
         B     CHKXXX                                                           
*                                                                               
CHK090   WTO   'DSN CHECK - LOCAL FILESET',MCSFLAG=HRDCPY                       
*                                                                               
         L     R5,ASYSFLES                                                      
         LA    R5,SYSFLIST         POINT TO FIRST FILE                          
         SR    RF,RF                                                            
         ICM   RF,7,SYSFADTF       GET A(DTF)                                   
         TM    ISFOPEN-ISDTF(RF),ISFORO+ISFONOP                                 
         BZ    CHKXXX              IF READ-ONLY OR NOP SET NOT GLOBAL           
         NI    DTFFLAG-DTFPHD(RF),255-DTFGLOB                                   
*                                                                               
CHKXXX   SAC   0                                                                
         B     EXIT                                                             
         EJECT                                                                  
***********************************************************************         
* CHECK FOR LOCAL FILE SET                                            *         
***********************************************************************         
CHKLOC   CLC   0(4,RF),=C'COPY'    COPY = LOCAL                                 
         BE    CHKLOC9                                                          
         CLC   0(2,RF),=C'FL'                                                   
         BNE   CHKLOC0                                                          
         CLC   2(2,RF),=C'1.'      FL1. = LOCAL                                 
         BE    CHKLOC9                                                          
         CLC   2(2,RF),=C'2.'      FL2. = LOCAL                                 
         BE    CHKLOC9                                                          
*                                                                               
CHKLOC0  LA    R1,31(RF)                                                        
CHKLOC1  CLI   0(R1),C'.'                                                       
         BE    CHKLOC2                                                          
         BCTR  R1,0                                                             
         CR    R1,RF                                                            
         BER   RE                                                               
         B     CHKLOC1                                                          
*                                                                               
CHKLOC2  CLC   0(6,R1),=C'.LOCAL'                                               
         BE    CHKLOC9                                                          
         CLC   0(8,R1),=C'.OFFLINE'                                             
         BE    CHKLOC9                                                          
*                                                                               
         CLI   BYTE1,C'L'          ANY LOCAL FILES SET                          
         BNER  RE                                                               
         SAC   0                                                                
         LARL  R4,FILEMIX          MIX OF LOCAL AND GLOBAL                      
         BRAS  RE,WTO                                                           
         ABEND 253                                                              
*                                                                               
CHKLOC9  MVI   BYTE1,C'L'          RETURN LOCAL                                 
         SR    RF,RF                                                            
         ICM   RF,7,SYSFADTF       GET A(DTF)                                   
         NI    DTFFLAG-DTFPHD(RF),255-DTFGLOB                                   
         DROP  R5                                                               
                                                                                
         L     R1,ASYSFLES         POINT TO START OF LIST HEADER                
         USING SYSFLSTD,R1                                                      
         NI    SYSFSTAT,255-SYSFDSP TURN OFF DSPACE PROT                        
         BR    RE                                                               
         DROP  R1                                                               
         EJECT                                                                  
***********************************************************************         
* CALL DYNALLOC FOR DSN                                               *         
***********************************************************************         
GETDSN   ST    RE,SAVERE                                                        
         ST    R0,SAVER0                                                        
         LA    R1,DYNBLK2          SET UP DYNALLOC PARMS                        
         ST    R1,DYNBLK1                                                       
         OI    DYNBLK1,X'80'                                                    
         LA    R1,DYNBLK4                                                       
         ST    R1,DYNBLK3                                                       
         LA    R1,DYNBLK5                                                       
         ST    R1,DYNBLK3+4                                                     
         OI    DYNBLK3+4,X'80'                                                  
         MVC   DYNBLK2,=X'1407000000000000000000000000000018000000'             
         LA    R1,DYNBLK3                                                       
         ST    R1,DYNBLK2+8                                                     
         MVC   DYNBLK4(6),=X'000100010008'                                      
         MVC   DYNBLK4+6(8),0(RF)                                               
         MVC   DYNBLK5,SPACES                                                   
         MVC   DYNBLK5(6),=X'000500010020'                                      
         LA    R1,DYNBLK1                                                       
         DYNALLOC                                                               
         LTR   RF,RF                                                            
         BZ    GETDSNX                                                          
         XC    DYNBLK5,DYNBLK5                                                  
GETDSNX  LA    RF,DYNBLK5+6                                                     
         L     RE,SAVERE                                                        
         L     R0,SAVER0                                                        
         BR    RE                                                               
         EJECT                                                                  
***********************************************************************         
* PUT DSN TO DATA SPACE                                               *         
***********************************************************************         
PUTDSN   NTR1                                                                   
         LA    RE,1                START FROM 1                                 
         SR    R2,R2                                                            
         USING DMDSHDR,R2          LOCATE DHADSN                                
         L     R2,DHADSN                                                        
PUTD010  OC    0(32,R2),0(R2)      FIND AN EMPTY ENTRY                          
         BZ    PUTD020                                                          
         CLC   0(32,R2),0(RF)      OR A MATCHING ONE                            
         BE    PUTD020                                                          
         LA    RE,1(RE)            BUMP TO NEXT                                 
         LA    R2,32(,R2)                                                       
         CH    RE,=Y(12*4096/32)   12=NUM PAGES ALLOCATED                       
         BL    PUTD010                                                          
         DC    H'0'                DIE=TOO MANY DSNS                            
***********************************************************************         
* NOT ENOUGH ROOM FOR FILES - NEED TO INCREASE PAGES ALLOCATED        *         
* TO DHADSN IN DMDMGRDSP AND CHANGE THE ABOVE =Y(12*4096/32)          *         
***********************************************************************         
PUTD020  MVC   0(32,R2),0(RF)                                                   
         STH   RE,HALF             RETURN INDEX IN HALF                         
         B     EXIT                                                             
         EJECT                                                                  
***********************************************************************         
* REPORT ON DSN DETAILS                                               *         
***********************************************************************         
REPTDSN  NTR1                                                                   
         USING DSFILHDR,R2                                                      
         CLC   SYSNUM,=X'000A'     TEST IF CONTROL SYSTEM                       
         BE    REPT010                                                          
         MVC   BYTE,DSFILNUM       NO - IGNORE CONTROL FILE COPYS               
         NI    BYTE,X'F0'                                                       
         CLI   BYTE,X'A0'                                                       
         BE    EXIT                                                             
*                                                                               
REPT010  TM    REPTFLAG,X'02'      TEST IF CLOSING SYSTEM                       
         BZ    REPT030                                                          
         SR    R4,R4                                                            
         ICM   R4,3,SYSNUM         INDEX INTO 16-BYTE SYSTEM TABLE              
         BZ    REPT030                                                          
         SLL   R4,4                                                             
         ICM   RE,15,=V(SYSTABL)                                                
         BZ    REPT030                                                          
         AR    R4,RE                                                            
         SR    RE,RE                                                            
         ICM   RE,7,DMSYAFNO-SYSTABD(R4) RE=A(FILE NUM INDEX TABLE)             
         L     R4,DMSYASYF-SYSTABD(R4)   R4=A(SYSFLES LIST ENTRY)               
*                                                                               
REPT015  LLC   RF,DSFILNUM         RF=FILE NUMBER                               
         AR    RE,RF                                                            
         IC    RF,0(RE)            RF=ENTRY NUMBER IN SYSFLES LIST              
         AHI   RF,-1                                                            
         SLL   RF,3                                                             
         LA    RE,4(RF,R4)         INDEX INTO 8-BYTE SYSFLES FILE ENTRY         
         USING SYSFLSTD,RE                                                      
         CLC   DSFILNUM,SYSFILE#   CHECK FILE NUMBERS MATCH                     
         BNE   REPT030                                                          
         SR    RF,RF               RF=A(DTF)                                    
         ICM   RF,7,SYSFADTF                                                    
         TM    DTFOPEN-DTF(RF),DTF_RO+DTF_NOP                                   
         BNZ   EXIT                IGNORE READ-ONLY OR NOP FILES                
         TM    DTFOPEN-DTF(RF),DTF_POPN                                         
         BZ    EXIT                IGNORE IF FILE NOT PREVIOUSLY OPEN           
         DROP  RE                                                               
*                                                                               
REPT030  LA    R4,MSG              OUTPUT DDNAME/DSEOF1/DSEOF2/DSN              
         MVC   MSG+2(8),DSFILDD                                                 
         MVI   MSG+10,C' '                                                      
         GOTO1 =V(HEXOUT),PARM,DSEOF1,MSG+11,4,0                                
         MVI   MSG+19,C' '                                                      
         GOTO1 =V(HEXOUT),PARM,DSEOF2,MSG+20,2,0                                
         MVC   MSG+24(6),=C'  DSN='                                             
*                                                                               
         MVC   FULL1(2),DSDSNX     SAVE ACTUAL DSNX                             
         SR    R2,R2                                                            
         USING DMDSHDR,R2          LOCATE DHADSN                                
         L     R2,DHADSN                                                        
         LH    R1,FULL1                                                         
         BCTR  R1,0                                                             
         SLL   R1,5                                                             
         AR    R2,R1               R2=A(DSN)                                    
         MVC   MSG+30(30),0(R2)                                                 
         MVC   MSG(2),=H'0058'                                                  
         SAC   0                                                                
         CLI   MSG+30,0            NO MESSAGE IF DSN NEVER SET                  
         BE    REPT050                                                          
         CLI   ONLIFLG,C'Y'                                                     
         BE    REPT040                                                          
         L     RF,MYSSB                                                         
         TM    SSOFLAG3-SSOOFF(RF),SSO3NOFM  MESSAGE NOT WANTED                 
         BO    REPT050                                                          
REPT040  WTO   TEXT=(R4),MCSFLAG=HRDCPY                                         
REPT050  SAC   512                                                              
         B     EXIT                                                             
         EJECT                                                                  
***********************************************************************         
* OPEN HAS FAILED DUE TO EXCLUSIVE ACCESS                             *         
* SEND WARNING TO CONSOLE AND SYSLOG                                  *         
***********************************************************************         
WARNING  LA    R4,MSG                                                           
         MVI   MSG+2,C' '            SET "+MYJOB   + SESYS   "                  
         MVC   MSG+3(77),MSG+2                                                  
         MVC   MSG+2(10),=C'+        +'                                         
         MVC   MSG+3(7),MVSNAME                                                 
         L     R2,ASYSTEM                                                       
         MVC   MSG+13(7),5(R2)                                                  
         SAC   0                                                                
         CLI   MSGNUM,1                                                         
         BE    WARN010                                                          
         CLI   MSGNUM,2                                                         
         BE    WARN050                                                          
         CLI   MSGNUM,3                                                         
         BE    WARN015                                                          
         CLI   MSGNUM,4                                                         
         BE    WARN015                                                          
*                                                                               
WARN010  MVC   MSG+21(28),=C'CANNOT BE OWNED EXCLUSIVELY '                      
         MVC   MSG(2),=H'50'                                                    
         WTO   TEXT=(R4)                                                        
         B     WARN020                                                          
*                                                                               
WARN015  MVC   MSG+21(28),=C'CANNOT CONCURRENTLY UPDATE  '                      
         MVC   MSG(2),=H'50'                                                    
         WTO   TEXT=(R4)                                                        
*                                                                               
WARN020  LA    R1,RETBLK                                                        
         LA    R0,DSJBHMXQ                                                      
WARN021  OC    0(8,R1),0(R1)                                                    
         BZ    WARN025                                                          
         MVC   MSG+43(8),0(R1)                                                  
         MVC   MSG+21(22),=C'CURRENTLY ATTACHED TO '                            
         MVC   MSG(2),=H'52'                                                    
         STM   RE,R1,DUB                                                        
         WTO   TEXT=(R4)                                                        
         LM    RE,R1,DUB                                                        
WARN025  LA    R1,16(R1)                                                        
         BCT   R0,WARN021                                                       
         B     WARN100                                                          
*                                                                               
WARN050  MVC   MSG+21(21),=C'OWNED EXCLUSIVELY BY '                             
         MVC   MSG+42(8),RETBLK                                                 
         MVC   MSG(2),=H'52'                                                    
         WTO   TEXT=(R4)                                                        
         B     WARN100                                                          
*                                                                               
WARN100  CLI   ONLIFLG,C'Y'        DON'T WTOR ONLINE                            
         BE    WARN105                                                          
         TM    SAVEP2,X'A0'        TEST NOT TO ASK OPERATOR                     
         BNO   WARN102                                                          
         TM    P1,X'08'            TEST TO ALWAYS RETURN                        
         BZ    WARN102                                                          
         LA    RF,MSG              RETURN MESSAGE                               
         ST    RF,P4                                                            
         B     WARN105                                                          
*                                                                               
WARN102  XC    ECBAD,ECBAD                                                      
         WTOR  'RETRY OR CANCEL',REPLY,1,ECBAD,ROUTCDE=(1)                      
         WAIT  ECB=ECBAD                                                        
         CLI   REPLY,C'R'                                                       
         BE    WARN110                                                          
         ABEND 254                 CONCURRENT FILE UPDATING                     
*                                                                               
WARN105  OI    P2,X'80'            ONLINE SET 80 RETURN                         
         SAC   0                                                                
         B     EXIT                                                             
*                                                                               
WARN110  CLI   MSGNUM,1            TRY AGAIN                                    
         BE    MAINTOPN                                                         
         CLI   MSGNUM,2                                                         
         BE    OPENSYS                                                          
         CLI   MSGNUM,3                                                         
         BE    SETACT                                                           
         CLI   MSGNUM,4                                                         
         BE    OPENSYS                                                          
         DC    H'0'                DIE=INV MESSAGE NUM                          
*                                                                               
ECBAD    DC    F'0'                WTOR FIELDS                                  
REPLY    DC    CL4' '                                                           
         EJECT                                                                  
***********************************************************************         
* ONLY GET HERE IF NO OTHER JOBS ARE RUNNING WITH THIS SYSTEM         *         
* CLEAR SYSTEM DOWN AND SET FORMALLY CLOSED                           *         
* THIS CODE IS ALSO IN DMDMGRRCV                                      *         
***********************************************************************         
CLEAR    ICM   R2,15,FULL          FULL=A(SYSTEM HEADER)                        
         BZ    CLOSE990                                                         
         USING DMSYSHDR,R2                                                      
         MVI   DSYSSTAT,0          SET SYSTEM TO CLOSED                         
         SR    R1,R1               CLEAR FILES FIRST                            
         ICM   R1,3,DSYFILES                                                    
         LA    R2,DSYDATA          R2=A(FILES)                                  
         USING DSFILHDR,R2                                                      
*                                                                               
CLR010   XC    DSEOF1,DSEOF1       REMOVE EOF1 & 2                              
         XC    DSEOF2,DSEOF2                                                    
         XC    DSFILACT,DSFILACT   AND ACTIONS                                  
*&&US*&& XC    DSFILFLG,DSFILFLG   AND FLAGS (SEEMS WRONG TO DO THIS)           
         LA    R2,32(,R2)                                                       
         BCT   R1,CLR010                                                        
*                                                                               
         SR    R4,R4                                                            
         ICM   R4,3,SYSNUM         INDEX INTO 16-BYTE SYSTEM TABLE              
         JZ    *+2                 DIE=ZERO SYSTEM NUMBER                       
         SLL   R4,4                                                             
         ICM   RE,15,=V(SYSTABL)                                                
         JZ    *+2                 DIE=MISSING SYSTABL                          
         AR    R4,RE                                                            
         USING SYSTABD,R4                                                       
         L     R4,DMSYASYF         R4=A(SYSFLES LIST ENTRY)                     
         USING SYSFLSTD,R4                                                      
*                                                                               
         SR    R0,R0               R0=NUM OF FILES                              
         ICM   R0,3,SYSF#FLS                                                    
         LA    R4,SYSFLIST         POINT TO FIRST FILE                          
CLR012   TM    SYSFIND1,SFRCV      RECOVERY?                                    
         BO    CLR014              YES                                          
         LA    R4,SYSFLNQ(R4)                                                   
         BCT   R0,CLR012                                                        
         DC    H'0'                DIE=CANT FIND RECOVERY FILE                  
*                                                                               
CLR014   SR    RE,RE                                                            
         ICM   RE,7,SYSFADTF       A(RECOVERY DTF)                              
         USING DTFPHD,RE                                                        
         TM    DTFFLAG,DTFSHMEM    SHARED MEMORY?                               
         BZ    CLR016              NO                                           
         DROP  R4,RE                                                            
*                                                                               
         SAC   0                   GET A(SHRMEM RECOVERY BUFFER)                
         LLH   R0,SYSNUM           BUILD OVERRIDE KEY                           
         GOTO1 =V(DMSHMUSS),DMCB,#ATTACH,#RCVB,0,(R0)                           
         SAM31                     VALUE IN R2 IS 31 BIT ADDRESS                
         L     R2,DMCB3                                                         
         ST    R2,ARECBUF          SAVE A(SHRMEM RECOVERY BUFFER)               
         B     CLR017                                                           
*                                                                               
CLR016   L     R2,FULL             FULL=A(SYSTEM HEADER)                        
         USING DMSYSHDR,R2                                                      
         ICM   RF,15,DSYABUFF      GET RECOVERY HEADER ADDR                     
         BZ    CLR020                                                           
         LR    R2,RF                                                            
*                                                                               
         USING VRCVHDR,R2                                                       
CLR017   SR    R1,R1                                                            
         ICM   R1,3,VRCVNXT                PICK UP LEN OF DATA                  
         XC    VRCVHDR(VRCXBLKQ),VRCVHDR   CLEAR HEADER FOR SURE                
         LR    R3,R1                                                            
         SR    RE,RE                                                            
         SR    RF,RF                                                            
         MVCL  R2,RE               CLEAR WHOLE BUFFER                           
         DROP  R2                                                               
*                                                                               
CLR020   LAM   AR0,ARF,ARZERO      RESTORE ARS AFTER FRESHIT                    
         LAM   AR2,AR2,MYALET                                                   
         SAC   512                 ALWAYS BACK TO AR MODE NOW                   
*                                                                               
         L     R2,FULL             FULL=A(SYSTEM HEADER)                        
         USING DMSYSHDR,R2                                                      
         ICM   RF,15,DSYALOCK      CLEAR LOCKTAB                                
         LR    R2,RF                                                            
         LH    R3,0(,R2)           R3=MAX NUMBER OF LOCKS                       
         SLL   R3,3                *8                                           
         XC    2(2,R2),2(R2)       CLEAR NUMBER OF LOCKS                        
         LA    R2,16(RF)                                                        
         SR    RE,RE                                                            
         SR    RF,RF                                                            
         MVCL  R2,RE               CLEAR LOCK TABLE                             
*                                                                               
         SR    R2,R2                                                            
         SR    RF,RF               CLEAR ALL BUT ECB IN HEADER                  
         ICM   RF,3,P1+2                                                        
         BZ    CLOSE990                                                         
         N     RF,=X'00000FFF'                                                  
         SLL   RF,6                                                             
         AR    R2,RF                                                            
         USING DMSPACED,R2                                                      
         XC    DSPHDR(L'DSPHDR-4),DSPHDR                                        
         MVI   DSPECB,0                                                         
*                                                                               
CLOSE990 SAC   0                   RETURN TO NORMAL MODE                        
         B     EXIT                                                             
         EJECT                                                                  
***********************************************************************         
* START NEW FACPAK SYSTEM OR RUNNER                                   *         
***********************************************************************         
STARTSYS LAM   AR0,ARF,ARZERO                                                   
         LAM   AR2,AR2,MYALET                                                   
         SAC   512                                                              
         SR    R2,R2                                                            
         L     R2,DHAJOBS-DMDSHDR(,R2)  R2=A(ADV TABLE)                         
*                                                                               
         OC    MVSADV,MVSADV       IS THIS AN ADV                               
         BZ    STRT2G              NO HANDLE AS JOB                             
*                                                                               
         LA    R0,127              ADV TABLE IS 127 * 32                        
STRT2F   CLC   0(8,R2),MVSNAME     LOOK FOR THIS JOB                            
         JNE   *+14                                                             
         CLC   8(2,R2),MVSJNUM     WITH SAME NUMBER                             
         JE    STRT2CHK                                                         
         LA    R2,32(,R2)          NEXT ENTRY                                   
         BCT   R0,STRT2F                                                        
*                                                                               
STRT2G   SR    R2,R2                                                            
         L     R2,DHAJOBS-DMDSHDR(,R2) A(ADV TABLE)                             
         LA    R0,128              ADV TABLE IS 128 * 32                        
STRT2G1  OC    0(8,R2),0(R2)       LOOK FOR FREE ENTRY                          
         BE    STRT2Z                                                           
         LA    R2,32(,R2)          NEXT ENTRY                                   
         BCT   R0,STRT2G1                                                       
         DC    H'0'                DIE=ADV TABLE FULL                           
*                                                                               
STRT2Z   SAC   512                                                              
*NOP     XC    0(32,R2),0(R2)                                                   
         MVC   0(8,R2),MVSNAME     FILL IN DETAILS                              
         MVC   8(2,R2),MVSJNUM                                                  
         MVC   10(1,R2),MVSADV                                                  
*                                                                               
         MVC   12(2,R2),MVSASID    SAVE ASID AT +12                             
         OC    MVSADV,MVSADV       IS THIS AN ADV                               
         BZ    STRT2Z1             NO HANDLE AS JOB                             
*                                                                               
         L     R1,=V(DSOPSECB)     SAVE ECB  AT +16                             
         ST    R1,16(,R2)                                                       
         L     R1,=V(DSPACECB)     SAVE ECB  AT +20                             
         ST    R1,20(,R2)                                                       
         B     START990                                                         
*                                                                               
STRT2Z1  ICM   R1,15,P3            OFFLINE JOBS (RUNNERS) USE P3,P4             
         BZ    *+8                 TO PASS OPSECB AND DSPACE ECB                
         ST    R1,16(,R2)          DSOPSECB                                     
         ICM   R1,15,P4                                                         
         BZ    *+8                                                              
         ST    R1,20(,R2)          DSPACE ECB                                   
         B     START990                                                         
*                                                                               
STRT2CHK LH    R4,DHAJOBS-DMDSHDR(,R2) CHECK THIS ADDRESS SPACE                 
         STH   R4,HALF                                                          
         CLC   HALF,MVSASID        OK IF ITS ME                                 
         BE    STRT2Z                                                           
         SAC   0                                                                
         LOCASCB ASID=(R4)                                                      
         LR    R5,R1                                                            
         USING ASCB,R5                                                          
         LTR   RF,RF                                                            
         BNZ   STRT2Z              OK REPLACE IT                                
         CLC   ASCBASCB,=C'ASCB'                                                
         BNE   STRT2Z              OK REPLACE IT                                
         L     R4,ASCBASSB                                                      
         SAM31                     SWITCH TO 31-BIT MODE                        
         L     R4,ASSBJSAB-ASSB(R4) R4=A(JSAB)                                  
         USING JSAB,R4                                                          
         CLC   MVSNAME,JSABJBNM                                                 
         SAM24                     SWITCH BACK TO 24-BIT MODE                   
         BNE   STRT2Z              OK REPLACE IT                                
*                                                                               
         MVI   WTORLREP,1                                                       
         LA    R1,BYTE                                                          
         STCM  R1,7,WTORAREP                                                    
         LA    R1,ECB                                                           
         ST    R1,WTORAECB                                                      
         LA    R1,52                                                            
         STH   R1,WTORLMSG                                                      
         LARL  RF,ASIDMSG          52+FACPAK+ ALREADY RUNNING IN ASID=.         
         MVC   WTORMSG(50),2(RF)                                                
         MVC   WTORMSG+1(7),MVSNAME                                             
         GOTO1 =V(HEXOUT),PARM,HALF,WTORMSG+33,2,0                              
         WTOR  MF=(E,WTORHDR)                                                   
         WAIT  ECB=ECB                                                          
         CLI   BYTE,C'Y'                                                        
         BNE   STRT2Z                                                           
         OI    P2,X'80'            FLAG ABORT START                             
*                                                                               
START990 ST    R2,P4               SAVE IN P4                                   
*                                                                               
         XR    R2,R2               PICK UP A COMM                               
         L     R2,DHACOMM-DMDSHDR(,R2)                                          
         LA    R0,256                                                           
         AH    R2,=H'4096'         SKIP HEADER PAGE                             
         USING DSCOMM,R2                                                        
START991 OC    DSCDEST,DSCDEST     IS THERE A COMMAND                           
         BZ    START999                                                         
         TM    DSCFLAG,DSCJNUMQ    IS THIS COMMAND FOR A JOB NUM                
         BO    START992                                                         
         CLC   MVSADV(1),DSCDEST   IS IT FOR ME                                 
         BNE   START999                                                         
         XC    0(32,R2),0(R2)      STARTING UP SO CLEAR ANY OLD COMMS           
         B     START999                                                         
*                                                                               
START992 CLC   MVSJNUM,DSCDEST     TEST JOBNUM. IS IT FOR ME                    
         BNE   START999                                                         
         XC    0(32,R2),0(R2)      STARTING UP SO CLEAR ANY OLD COMMS           
*                                                                               
START999 LA    R2,32(,R2)          POINT TO NEXT COMM ENTRY                     
         BCT   R0,START991         NEXT COMMAND                                 
         SAC   0                                                                
         B     EXIT                                                             
         EJECT                                                                  
***********************************************************************         
* STOP NEW FACPAK SYSTEM OR RUNNER                                    *         
***********************************************************************         
STOPSYS  LAM   AR0,ARF,ARZERO                                                   
         LAM   AR2,AR2,MYALET                                                   
         SAC   512                                                              
         SR    R2,R2                                                            
         L     R2,DHAJOBS-DMDSHDR(,R2) R2=A(ADV TABLE)                          
*                                                                               
         LA    R0,127              ADV TABLE IS 127 * 32                        
STOP01   CLC   12(2,R2),MVSASID    LOOK FOR THIS JOB                            
         BE    STOP02                                                           
         LA    R2,32(,R2)          NEXT ENTRY                                   
         BCT   R0,STOP01                                                        
         B     STOP03                                                           
*                                                                               
STOP02   MVC   FULL,DSJLOCAL-DSJOBD(R2)                                         
         XC    0(32,R2),0(R2)      CLEAR ENTRY                                  
         ICM   R2,15,FULL          ANY LOCAL STATE ENTRY                        
         JZ    STOP03                                                           
*                                                                               
DEL010   LA    R3,DSLLEN           REMOVE LOCAL ENTRY                           
         XR    R0,R0                                                            
         XR    R1,R1                                                            
         MVCL  R2,R0                                                            
*                                                                               
STOP03   SAC   0                                                                
         B     EXIT                                                             
         EJECT                                                                  
***********************************************************************         
* REFRESH SYSTEM DTFS - DIRECTLY CALLABLE ACTION FRESHSYS             *         
* ALSO CALLED BY CLEAR ROUTINE TO GET RECOVERY DTF ADDRESS            *         
***********************************************************************         
FRESHSYS BAS   RE,FRESHIT          CALL REFRESH IT SUBROUTINE                   
         B     EXIT                                                             
*                                                                               
FRESHIT  NTR1                                                                   
         LAM   AR0,ARF,ARZERO                                                   
         LAM   AR2,AR2,MYALET                                                   
         SAC   512                                                              
         SR    R2,R2                                                            
         LH    RF,DSRSRC           RF=RESOURCE NUMBER                           
         SLL   RF,6                INDEX TO RESOURCE HEADER                     
         AR    R2,RF                                                            
         USING DMSPACED,R2                                                      
*                                                                               
         L     R2,DSPECB           GET A(SYSTEM)                                
         N     R2,=X'00FFFFFF'     IGNORE POST BITS                             
         USING DMSYSHDR,R2                                                      
         LTR   R2,R2                                                            
         BZ    FRESH990                                                         
         TM    DSYSSTAT,DSYQOPEN                                                
         BZ    FRESH990                                                         
         MVC   SYSNAME,DSYSID+5    SAVE SYSTEM NAME                             
         MVC   SYSNUM,DSYSENUM     SAVE SYSTEM NUMBER                           
         SAC   0                                                                
*                                                                               
         ICM   R4,15,=V(SELIST)    TEST IF HAVE SELIST                          
         BZ    FRESH2                                                           
         LH    R0,0(R4)            SET BXLE                                     
         L     R1,2(R4)                                                         
         LA    R4,6(R4)                                                         
         USING SELISTD,R4                                                       
FRESH1   CLC   SESYS,SYSNUM+1      MATCH ON SYSTEM NUMBER                       
         BE    FRESH2                                                           
         BXLE  R4,R0,FRESH1        NEXT                                         
         DC    H'0'                DIE=SYS NOT DEFINED                          
         DROP  R4                                                               
*                                                                               
FRESH2   XC    DMCB,DMCB                                                        
         MVC   DMCB+11(1),SYSNUM+1                                              
         GOTO1 =V(DATAMGR),DMCB,=C'DMREAD',=C'SYSFLES'                          
         ICM   R1,15,12(R1)                                                     
         JZ    *+2                 DIE=SYS NOT DEFINED                          
         LA    R1,0(R1)            R1=SE FILES                                  
         USING SYSFLSTD,R1                                                      
*                                                                               
         ST    R1,ASYSFLES                                                      
         CLC   SYSFSYS#,SYSNUM+1                                                
         JNE   *+2                 DIE=NO MATCH SYSTEM                          
         LAM   AR0,ARF,ARZERO                                                   
         LAM   AR2,AR2,MYALET                                                   
         SAC   512                                                              
         SR    R4,R4                                                            
         ICM   R4,3,DSYFILES                                                    
         LA    R2,DSYDATA                                                       
         USING DSFILHDR,R2                                                      
*                                                                               
FRESH99  SR    RE,RE               RE=NUMBER OF FILES                           
         ICM   RE,3,SYSF#FLS                                                    
         LA    RF,SYSFLIST         RF=A(FILE LIST)                              
         DROP  R1                                                               
         USING SYSFLSTD,RF                                                      
*                                                                               
FRESH100 CLC   SYSFILE#,DSFILNUM   FIND THIS FILES ENTRY                        
         BE    FRESH110                                                         
         LA    RF,SYSFLNQ(RF)                                                   
         BCT   RE,FRESH100                                                      
         DC    H'0'                DIE=NO MATCH FILE                            
*                                                                               
FRESH110 L     RF,SYSFADTF-1       GET THE DTF                                  
         USING DTFPHD,RF                                                        
         TM    DTFFLAG,DTFGLOB     MUST BE A GLOBAL FILE                        
         BZ    FRESH150                                                         
         TM    DTFOPEN,DTF_RO      MUST NOT BE READ ONLY                        
         BO    FRESH150                                                         
         TM    DTFTYPE,DTFTIS                                                   
         BO    FRESH120                                                         
         OC    DSEOF1,DSEOF1                                                    
         BZ    FRESH140                                                         
         MVC   DNEXT,DSEOF1                                                     
         MVC   DCOUNT,DSEOF2                                                    
         B     FRESH140                                                         
*                                                                               
         USING ISDTF,RF                                                         
FRESH120 OC    DSEOF1,DSEOF1                                                    
         BZ    FRESH130                                                         
         MVC   ISOVLAST,DSEOF1                                                  
         MVC   ISPDLAST,DSEOF2                                                  
*                                                                               
FRESH130 MVC   DSEOF1,ISOVLAST                                                  
         MVC   DSEOF2,ISPDLAST                                                  
         LA    R2,32(,R2)                                                       
         BCT   R4,FRESH99                                                       
         B     FRESH990                                                         
*                                                                               
         USING DTFPHD,RF                                                        
FRESH140 MVC   DSEOF1,DNEXT                                                     
         MVC   DSEOF2,DCOUNT                                                    
         LA    R2,32(,R2)                                                       
FRESH150 BCT   R4,FRESH99                                                       
*                                                                               
FRESH990 SAC   0                                                                
         B     EXIT                                                             
         EJECT                                                                  
***********************************************************************         
* GET UNIQUE REFERENCE ID                                             *         
***********************************************************************         
GETREF   CLI   ONLIFLG,C'Y'        ARE WE ONLINE                                
         BE    GETREF1                                                          
         XC    P2(4),P2                                                         
         MVC   P2(2),MVSJNUM       OFFLINE ITS JOB NUM                          
         OI    P2,X'80'            FLAG IT AS OFFLINE JOB                       
         B     EXIT                                                             
*                                                                               
GETREF1  XC    P2(4),P2                                                         
         MVC   P2(2),MVSADV        ONLINE ITS ADV/TSK                           
         B     EXIT                                                             
         EJECT                                                                  
***********************************************************************         
* ROUTINES TO WRITE AND READ FROM CONSOLE. R4=A(TEXT) FOR WTO AND WTOR*         
***********************************************************************         
WTO      NTR1                                                                   
         WTO   TEXT=(R4),DESC=2                                                 
         LAM   ARE,AR1,ARZERO                                                   
         XIT1                                                                   
*                                                                               
WTOR     NTR1                                                                   
         WTOR  TEXT=((R4),REPLY,1,ECBAD),ROUTCDE=(1)                            
         LAM   ARE,AR1,ARZERO                                                   
         XC    ECBAD,ECBAD                                                      
         WAIT  ECB=ECBAD                                                        
         J     EXIT                                                             
*                                                                               
WTOHRD   NTR1                                                                   
         WTO   TEXT=MSG,MCSFLAG=HRDCPY                                          
         LAM   ARE,AR1,ARZERO                                                   
         J     EXIT                                                             
         EJECT                                                                  
***********************************************************************         
* MOVE TRACE ENTRY TO TRACE BUFFER                                    *         
***********************************************************************         
TRCIT    CLI   TRTACTV,C'Y'        EXIT IF TRACE NOT ACTIVE                     
         BNER  RE                                                               
         CLC   TLKRSRC,=C'SERV'    EXIT IF NOT SE1 SYSTEM                       
         BNER  RE                                                               
*                                                                               
TRCIT1   NTR1                                                                   
         STAR  CLEAR=ARZERO,ARS=OFF                                             
         PROT  OFF                                                              
*                                                                               
         XR    R4,R4               TABLE IS HERE                                
         LAM   AR4,AR4,SSBTBLET                                                 
         SAC   512                                                              
         ICM   R4,15,TABSLOX-FATABSD(R4)                                        
         BZ    TRCIT10             NO LOCK TABLE SET UP                         
         ICM   RF,15,4(R4)         RF=A(END OF TABLE)                           
         BZ    TRCIT10                                                          
         CLI   9(R4),C'X'          TEST IF NO LONGER TRACING                    
         BE    TRCIT10                                                          
*                                                                               
TRCIT2   ICM   RE,15,0(R4)         RE=A(NEXT ENTRY)                             
         BNZ   *+8                                                              
         LA    RE,TLKLEN(R4)       IF NOT SET USE FIRST ONE                     
         LA    RF,TLKLEN(RE)       POINT RF TO NEXT ONE                         
         C     RF,4(,R4)           SEE IF STILL BEFORE END                      
         BL    *+8                 YES                                          
         LA    RF,TLKLEN(R4)       POINT BACK TO TOP                            
         CS    RE,RF,0(R4)                                                      
         BNE   TRCIT2                                                           
*                                                                               
TRCIT3   LR    R4,RE               ADDRESS OF OUR ENTRY                         
         MVC   0(TLKLEN,R4),TLKNTRY                                             
         XR    R4,R4                                                            
         ICM   R4,15,TABSLOX-FATABSD(R4)                                        
         CLI   TLKERR,C' '         TEST IF ERROR ENTRY                          
         BNH   *+8                                                              
         MVI   9(R4),C'X'          STOP TRACING IN DSPACE NOW!!                 
         LAM   AR4,AR4,=F'0'                                                    
*                                                                               
TRCIT10  CLI   TLKERR,0            TRACE ERRORS LOCALLY                         
         BH    TRCIT11                                                          
         CLI   TLKWAIT,0           TRACE WAITS LOCALLY                          
         BH    TRCIT11                                                          
         B     TRCITX                                                           
*                                                                               
TRCIT11  L     RE,TRTLOCKA         RE=A(NEXT ENTRY IN TABLE)                    
         C     RE,TRTLOCKF         TEST START OF TABLE                          
         BE    TRCIT13                                                          
         C     RE,TRTLOCKL         TEST END OF TABLE                            
         BL    TRCIT14                                                          
*                                                                               
TRCIT12  CLI   TRTDISK,C'Y'        TABLE IS FULL                                
         BNE   TRCIT13                                                          
         L     R1,TRTLOCKF                                                      
         L     RF,=V(LOGGER)                                                    
         BASR  RE,RF               WRITE TO ADRFILE                             
*                                                                               
TRCIT13  LA    RE,TRTLOCK          POINT TO START OF TABLE                      
         MVI   TRTERRS,C' '        SET NO ERRORS IN TABLE                       
         XC    0(16,RE),0(RE)                                                   
         MVC   0(5,RE),=C'*LKSP'   SET TEXT IN FIRST ENTRY                      
         LA    RE,16(RE)                                                        
*                                                                               
TRCIT14  MVC   0(TLKLEN,RE),TLKNTRY MOVE TRACE ENTRY TO NEXT SLOT               
         LA    RE,TLKLEN(RE)                                                    
         ST    RE,TRTLOCKA         BUMP SLOT ADDRESS                            
         CLI   TLKERR,0                                                         
         BNH   *+8                                                              
         MVI   TRTERRS,C'Y'        SET TABLE CONTAINS ERROR ENTRIES             
*                                                                               
TRCITX   REAR  ARS=ON                                                           
         PROT  ON                                                               
         XIT1                                                                   
         EJECT                                                                  
***********************************************************************         
* CONSTANTS & LTORG                                                   *         
***********************************************************************         
SPACES   DC    CL40' '                                                          
ARZERO   DC    16F'0'                                                           
*                                                                               
AJOBNTRY DC    A(0)                A(OUR JOBTAB ENTRY)                          
MYSSB    DC    A(0)                A(SSB)                                       
*                                                                               
MVSINFO  DS    0CL32                                                            
MVSLOCK  DC    CL4'    '           LOCK WORD                                    
*                                                                               
MVSALL   DC    0CL10               MVS JOBNAME / NUMBER                         
MVSNAME  DC    CL8'        '       MVS JOB NAME                                 
MVSJNUM  DC    XL2'0000'           NUMERIC PART OF JOBID                        
*                                                                               
MVSCPU   DC    CL4'0000'           CPU ID                                       
MVSASID  DC    XL2'00'             ADDRESS SPACE ID                             
MVSJOB   DC    CL8'JOB00000'       JOB ID                                       
MYASCB   DC    A(0)                                                             
*                                                                               
ONLIFLG  DC    C'*'                ONLINE=Y OFFLINE=N                           
LOCKERF  DC    C'*'                DO WE HAVE LOCKER                            
*                                                                               
#ATTACH  DC    CL8'ATTACH'                                                      
#RCVB    DC    CL8'RCVB  '                                                      
*                                                                               
INITFLGS DC    X'00'               INITIALIZATION FLAGS                         
FRSTINIT EQU   X'01'               INIT FIRST TIME IN                           
DMGRINIT EQU   X'20'               DMGR DATASPACE ACCESS                        
PGMSINIT EQU   X'40'               PGMS DATASPACE ACCESS                        
TABSINIT EQU   X'80'               TABS DATASPACE ACCESS                        
*                                                                               
MYDSPACE DS    X                                                                
         LTORG                                                                  
         DS    0D                                                               
*DDLOCKTLK                                                                      
       ++INCLUDE DDLOCKTLK                                                      
*                                                                               
         DS    0D                                                               
BADTRACE DC    CL48' '                                                          
         DC    C'BADLOCK='                                                      
BADLOCK  DC    CL64' '                                                          
BADDELT  DC    F'0'                                                             
*                                                                               
         DS    0D                                                               
TRTLOCKF DC    A(TRTLOCK)          A(FIRST ENTRY IN TABLE)                      
TRTLOCKL DC    A(TRTLOCKX)         A(END OF TABLE)                              
TRTLOCKA DC    A(TRTLOCK)          A(NEXT ENTRY IN TABLE)                       
         DC    H'100',H'0'         MAX NUMBER OF ENTRIES                        
TRTACTV  DC    C'N'                TRACE ACTIVE=Y                               
TRTDISK  DC    C'N'                TRACE BLOCKS TO DISK=Y                       
TRTERRS  DC    C' '                TABLE CONTAINS ERRORS=Y                      
         DC    C' '                                                             
         DC    C'TRTLOCK='                                                      
TRTLOCK  DC    XL16'00'            LOCK TRACE TABLE HEADER                      
         DC    133XL48'00'         LOCK TRACE TABLE ENTRIES                     
TRTLOCKX DC    X'FFFFFFFF'                                                      
         EJECT                                                                  
***********************************************************************         
* MESSAGES SENT TO CONSOLE AND EMAIL                                  *         
***********************************************************************         
FILEMIX  DC    Y(L'FILEMIXT)                                                    
FILEMIXT DC    C'MIX OF LOCAL AND GLOBAL FILES '                                
*                                                                               
DSNLOCL  DC    Y(L'DSNLOCLT)                                                    
DSNLOCLT DC    C'DSN CHECK - LOCAL FILESET'                                     
*                                                                               
NOSYSDS  DC    Y(L'NOSYSDST)                                                    
NOSYSDST DC    C'*** SYSTEM IS NOT DEFINED IN DSPACE ***'                       
*                                                                               
DMGRMSG  DC    Y(L'DMGRMSGT)                                                    
DMGRMSGT DC    C'***CHECK DMGR DATASPACE IS INITIALISED***'                     
*                                                                               
TABSMSG  DC    Y(L'TABSMSGT)                                                    
TABSMSGT DC    C'***CHECK TABS DATASPACE IS INITIALISED***'                     
*                                                                               
PGMSMSG  DC    Y(L'PGMSMSGT)                                                    
PGMSMSGT DC    C'***CHECK PGMS DATASPACE IS INITIALISED***'                     
*                                                                               
BADLMSG  DC    Y(L'BADLMSGT)                                                    
BADLMSGT DC    C'+FAC***+ DSPACE BADLOCK#.                         '            
*                                                                               
LOCKMSG  DC    Y(L'LOCKMSGT)                                                    
LOCKMSGT DC    C'+FAC***+ DSPACE LOCKOUT ....... OWNED BY .......  '            
*                                                                               
ASIDMSG  DC    Y(L'ASIDMSGT)                                                    
ASIDMSGT DC    C'+FACPAK+ ALREADY RUNNING IN ASID=XXXX. ABORT Y/N  '            
*                                                                               
RETRYMSG DC     AL2(L'RTRYMSGT)                                                 
RTRYMSGT DC    C'RETRY OR CANCEL'                                               
*                                                                               
AUTOMSG1 DC    Y(AUTOMS1X-*-2)                                                  
*&&US*&& DC    C'AUTONOTE*US-MF_FAC_NOTIFY:'                                    
*&&UK*&& DC    C'AUTONOTE*EU-FAC-TEAM:'                                         
         DC    C'DSPLOCK,OFFLINE,NOT MATCH'                                     
AUTOMS1X EQU   *                                                                
*                                                                               
AUTOMSG2 DC    Y(AUTOMS2X-*-2)                                                  
*&&US*&& DC    C'AUTONOTE*US-MF_FAC_NOTIFY:'                                    
*&&UK*&& DC    C'AUTONOTE*EU-FAC-TEAM:'                                         
         DC    C'DSPLOCK,ONLINE,NOT MATCH'                                      
AUTOMS2X EQU   *                                                                
*                                                                               
AUTOMSG3 DC    Y(AUTOMS3X-*-2)                                                  
*&&US*&& DC    C'AUTONOTE*US-MF_FAC_NOTIFY:'                                    
*&&UK*&& DC    C'AUTONOTE*EU-FAC-TEAM:'                                         
         DC    C'DSPLOCK,ONLINE,NOT CLEAR'                                      
AUTOMS3X EQU   *                                                                
*                                                                               
AUTOMSG4 DC    Y(AUTOMS4X-*-2)                                                  
*&&US*&& DC    C'AUTONOTE*US-MF_FAC_NOTIFY:'                                    
*&&UK*&& DC    C'AUTONOTE*EU-FAC-TEAM:'                                         
         DC    C'BADLOCK - ONLINE'                                              
AUTOMS4X EQU   *                                                                
*                                                                               
AUTOMSG5 DC    Y(AUTOMS5X-*-2)                                                  
*&&US*&& DC    C'AUTONOTE*US-MF_FAC_NOTIFY:'                                    
*&&UK*&& DC    C'AUTONOTE*EU-FAC-TEAM:'                                         
         DC    C'BADLOCK - OFFLINE'                                             
AUTOMS5X EQU   *                                                                
*                                                                               
AUTOMSG6 DC    Y(AUTOMS6X-*-2)                                                  
*&&US*&& DC    C'AUTONOTE*US-MF_FAC_NOTIFY:'                                    
*&&UK*&& DC    C'AUTONOTE*EU-FAC-TEAM:'                                         
         DC    C'DSPLOCK,OFFLINE,NOT CLEAR'                                     
AUTOMS6X EQU   *                                                                
*                                                                               
AUTOMSG7 DC    Y(AUTOMS7X-*-2)                                                  
*&&US*&& DC    C'AUTONOTE*US-MF_FAC_NOTIFY:'                                    
*&&UK*&& DC    C'AUTONOTE*EU-FAC-TEAM:'                                         
         DC    C'DSPLOCK,NOT CLEAR'                                             
AUTOMS7X EQU   *                                                                
*                                                                               
AUTOMSG8 DC    Y(AUTOMS8X-*-2)                                                  
*&&US*&& DC    C'AUTONOTE*US-MF_FAC_NOTIFY:'                                    
*&&UK*&& DC    C'AUTONOTE*EU-FAC-TEAM:'                                         
         DC    C'DSPLOCK,NOT CLEAR'                                             
AUTOMS8X EQU   *                                                                
         EJECT                                                                  
***********************************************************************         
* ROUTINE ADDRESS TABLE                                               *         
***********************************************************************         
ROUTS    DC    A(0)                                                             
         DC    A(OPENSYS)          01 TEST OPEN SYSTEM                          
         DC    A(CLOSESYS)         02 TEST CLOSE SYSTEM                         
         DC    A(STARTSYS)         03 START NEW FACPAK                          
         DC    A(GETREF)           04 GET UNIQUE ID AND RETURN IT               
         DC    A(FRESHSYS)         05 REFRESH SYSTEM DTFS                       
         DC    A(MAINTOPN)         06 OPEN SYSTEM FOR MAINTENANCE               
         DC    A(MAINTCLS)         07 CLOSE SYSTEM MAINTENANCE                  
         DC    A(SETACT)           08 SET JOB TO ACTIVE                         
         DC    A(SETREAD)          09 SET JOB TO READ-ONLY                      
         DC    A(OPENSYS)          10 OPEN SYSTEM AS READ-ONLY                  
         DC    A(JOBVAL)           11 VALIDATE JOB NAME FOR LOCKER              
         DC    A(REINIT)           12 RESET LOCKSPC INIT FLAGS                  
         DC    A(STOPSYS)          13 STPO FACPAK OR RUNNER                     
ROUTSX   EQU   *                                                                
*                                                                               
ROUTMAX  EQU   ((ROUTSX-ROUTS)/4)-1                                             
         EJECT                                                                  
***********************************************************************         
* FIRST TIME SET UP MVS DETAILS AS CONSTANTS                          *         
***********************************************************************         
INIT     NTR1  BASE=*                                                           
         LAM   AR0,ARF,ARZERO                                                   
         TM    INITFLGS,FRSTINIT   TEST FIRST CALL TO INITIALIZE                
         BO    INIT020                                                          
         OI    INITFLGS,FRSTINIT                                                
*                                                                               
         MVI   LOCKERF,C'N'        IS LOCKER AVAILABLE                          
         ICM   R1,15,=V(LOCKER)                                                 
         BZ    INIT010                                                          
         CLI   0(R1),X'90'         CHECK ITS NOT 07FE                           
         BNE   INIT010                                                          
         MVI   LOCKERF,C'Y'        YES LOCKER IS THERE                          
*                                                                               
INIT010  LA    R2,FULL                                                          
         EXTRACT (2),'S',FIELDS=(ASID)                                          
         LA    R1,FULL                                                          
         L     R2,0(R1)                                                         
         LOCASCB ASID=(R2)         GET ASCB ADDRESS INTO R1                     
         ST    R1,MYASCB                                                        
         L     R2,ASCBASSB-ASCB(R1) R5=A(ASSB)                                  
*                                                                               
         SAM31                     SWITCH TO 31-BIT MODE                        
*                                                                               
         L     R1,ASSBJSAB-ASSB(R2) R2=A(JSAB)                                  
         USING JSAB,R1                                                          
         MVC   MVSJOB,JSABJBID     GET JOBID (E.G. JOB12345)                    
         PACK  DUB,MVSJOB+3(5)                                                  
         CVB   R1,DUB                                                           
         STCM  R1,3,MVSJNUM        CONVERT TO JOBNO                             
*                                                                               
         SAM24                     SWITCH BACK TO 24 BT MODE                    
         DROP  R1                                                               
*                                                                               
         LA    R2,FULL             GET JOB NAME INTO MVSNAME                    
         EXTRACT (2),FIELDS=TIOT                                                
         L     R2,FULL                                                          
         MVC   MVSNAME,0(R2)                                                    
*                                                                               
         L     R1,X'10'(,R0)       GET CPU ID INTO CPU                          
         L     R1,X'C4'(R1)        R1=A(SMCA) FROM CVT                          
         MVC   MVSCPU,16(R1)       CPU IS C'XXXX' FROM SMCASID                  
*                                                                               
         LA    R2,FULL             GET ASID INTO MVSASID                        
         EXTRACT (2),FIELDS=ASID                                                
         MVC   MVSASID,FULL+2                                                   
         MVC   MVSLOCK+0(2),MVSCPU+2 MVSLOCK=CPUID+ASID                         
         MVC   MVSLOCK+2(2),MVSASID                                             
*                                                                               
INIT020  ICM   R8,15,=V(SSB)       MUST HAVE AN SSB                             
         JZ    *+2                 DIE=SSB NOT DEFINED                          
         MVC   SSBLOCK,MVSLOCK     SET LOCKWORD IN SSB                          
*                                                                               
         ST    R8,MYSSB            SAVE ADDRESS                                 
         MVI   ONLIFLG,C'Y'                                                     
         OC    SSBCNTL,SSBCNTL     TEST FOR ONLINE/OFFLINE                      
         JZ    INIT021                                                          
*                                                                               
         MVC   MYDSPACE,SSBDSPAC                                                
         OI    INITFLGS,DMGRINIT+TABSINIT+PGMSINIT                              
         B     INITX               FASTART HAS ALREADY DONE THIS                
*                                                                               
INIT021  MVI   ONLIFLG,C'N'        SET OFFLINE FLAG                             
         CLI   SSOXTND,X'FF'       MUST BE EXTENDED SSB                         
         JNE   *+2                 DIE=SSB NOT EXTENDED                         
         CLI   SSODSPAC,C' '       TEST IF DATA SPACE SET IN SSB                
         BH    INIT029                                                          
         MVI   SSODSPAC,C'A'       NO - DEFAULT IS PRODUCTION                   
                                                                                
         LA    R2,FULL                                                          
         EXTRACT (2),FIELDS=TIOT   FIND DDNAME=TSTPQDD0 & SET DSPACE=T          
         L     R2,FULL                                                          
         AHI   R2,24                                                            
         XR    RF,RF                                                            
INIT025  CLI   0(R2),0             TEST FOR END OF TIOT TABLE                   
         BE    INIT029                                                          
         CLC   =C'REPPQDD0',4(R2)  TEST DDNAME=REPPQDD0                         
         BNE   *+12                                                             
         MVI   SSODSPAC,C'R'       SET DSPACE=R                                 
         B     INIT029                                                          
         CLC   =C'TSTPQDD0',4(R2)  TEST DDNAME=TSTPQDD0                         
         BNE   *+12                                                             
         MVI   SSODSPAC,C'T'       SET DSPACE=T                                 
         B     INIT029                                                          
         CLC   =C'CSCPQDD0',4(R2)  TEST DDNAME=CSCPQDD0                         
         BNE   *+12                                                             
         MVI   SSODSPAC,C'C'       SET DSPACE=C                                 
         B     INIT029                                                          
         CLC   =C'FQAPQDD0',4(R2)  TEST DDNAME=FQAPQDD0                         
         BNE   *+12                                                             
         MVI   SSODSPAC,C'Q'       SET DSPACE=Q                                 
         B     INIT029                                                          
         CLC   =C'BARPQDD0',4(R2)  TEST DDNAME=BARPQDD0                         
         BNE   *+12                                                             
         MVI   SSODSPAC,C'B'       SET DSPACE=B                                 
         B     INIT029                                                          
         IC    RF,0(R2)            GET LENGTH OF THIS ENTRY                     
         BXH   R2,RF,INIT025                                                    
*                                                                               
INIT029  MVC   MYDSPACE,SSODSPAC                                                
*                                                                               
INIT030  TM    P1+2,X'C0'          TEST DMGR DATASPACE RESOURCE                 
         BNZ   INIT100                                                          
         OC    SSOALET,SSOALET     DO WE HAVE THE DATASPACE                     
         BNZ   INIT060                                                          
*                                                                               
INIT040  LA    R0,21               GO GET DATASPACE                             
         LNR   R0,R0                                                            
         LA    R1,WORK                                                          
         XC    WORK(28),WORK                                                    
         MVC   WORK+0(4),=C'GETA'                                               
*                                                                               
         MVC   WORK+4(12),=C'DMGCDATAMGRX'                                      
         CLI   SSODSPAC,C'C'                                                    
         BE    INIT050                                                          
         MVC   WORK+4(12),=C'DMGTDATAMGRX'                                      
         CLI   SSODSPAC,C'T'                                                    
         BE    INIT050                                                          
         MVC   WORK+4(12),=C'DMGQDATAMGRX'                                      
         CLI   SSODSPAC,C'Q'                                                    
         BE    INIT050                                                          
         MVC   WORK+4(12),=C'DMGBDATAMGRX'                                      
         CLI   SSODSPAC,C'B'                                                    
         BE    INIT050                                                          
         MVC   WORK+4(12),=C'DMGADATAMGRX'                                      
         CLI   SSODSPAC,C'A'                                                    
         BE    INIT050                                                          
         CLI   SSODSPAC,C'P'                                                    
         BE    INIT050                                                          
         MVC   WORK+4(12),=C'DMGRDATAMGRX'                                      
         CLI   SSODSPAC,C'R'                                                    
         BE    INIT050                                                          
         ABEND 250,DUMP            WHICH DATASPACE IS BEING ASKED FOR?          
*                                                                               
INIT050  SVC   247                                                              
*                                                                               
INIT054  LTR   RF,RF               TEST FOR ERRORS                              
         BNZ   INIT056                                                          
         OC    WORK+24(4),WORK+24                                               
         BNZ   INIT058                                                          
*                                                                               
INIT056  LARL  R4,DMGRMSG          CHECK DMGR DSPACE IS INITIALISED             
         BRAS  RE,WTO                                                           
         LARL  R4,RETRYMSG                                                      
         BRAS  RE,WTOR                                                          
         CLI   REPLY,C'R'                                                       
         BE    INIT040                                                          
*                                                                               
*&&US*&& ABEND 911                                                              
*&&UK*&& ABEND 999                                                              
*                                                                               
INIT058  MVC   SSOALET,WORK+24     SET SSB VALUES                               
INIT060  OI    INITFLGS,DMGRINIT                                                
         B     INITX                                                            
*                                                                               
INIT100  LAM   AR0,ARF,ARZERO                                                   
         TM    P1+2,X'80'          TEST TABS DATASPACE RESOURCE                 
         BZ    INIT200                                                          
INIT110  OC    SSOTBLET,SSOTBLET   DO WE HAVE THE TABS DATASPACE                
         BNZ   INIT160                                                          
*                                                                               
         LA    R0,21               GO GET DATASPACE                             
         LNR   R0,R0                                                            
         LA    R1,WORK                                                          
         XC    WORK(28),WORK                                                    
         MVC   WORK+0(4),=C'GETA'                                               
*&&US                                                                           
         MVC   WORK+4(12),=C'TABCXXXXXXXX'                                      
         CLI   SSODSPAC,C'C'                                                    
         BE    INIT150                                                          
         MVC   WORK+4(12),=C'TABTXXXXXXXX'                                      
         CLI   SSODSPAC,C'T'                                                    
         BE    INIT150                                                          
         MVC   WORK+4(12),=C'TABQXXXXXXXX'                                      
         CLI   SSODSPAC,C'Q'                                                    
         BE    INIT150                                                          
         MVC   WORK+4(12),=C'TABPXXXXXXXX'                                      
*&&                                                                             
*&&UK                                                                           
         MVC   WORK+4(12),=C'TABCXXXXXXXX'                                      
         CLI   SSODSPAC,C'C'                                                    
         BE    INIT150                                                          
         MVC   WORK+4(12),=C'TABTXXXXXXXX'                                      
         CLI   SSODSPAC,C'T'                                                    
         BE    INIT150                                                          
         MVC   WORK+4(12),=C'TABQXXXXXXXX'                                      
         CLI   SSODSPAC,C'Q'                                                    
         BE    INIT150                                                          
         MVC   WORK+4(12),=C'TABBXXXXXXXX'                                      
         CLI   SSODSPAC,C'B'                                                    
         BE    INIT150                                                          
         MVC   WORK+4(12),=C'TABAXXXXXXXX'                                      
*&&                                                                             
INIT150  SVC   247                                                              
*                                                                               
INIT154  LTR   RF,RF               TEST FOR ERRORS                              
         BNZ   INIT156                                                          
         OC    WORK+24(4),WORK+24                                               
         BNZ   INIT158             NO DATASPACE AVAILABLE                       
*                                                                               
INIT156  LARL  R4,TABSMSG          CHECK TABS DSPACE IS INITIALISED             
         BRAS  RE,WTO                                                           
         LARL  R4,RETRYMSG                                                      
         BRAS  RE,WTOR                                                          
         CLI   REPLY,C'R'                                                       
         BE    INIT110                                                          
*                                                                               
*&&US*&& ABEND 911                                                              
*&&UK*&& ABEND 999                                                              
*                                                                               
INIT158  MVC   SSOTBLET,WORK+24    SET SSB VALUES                               
INIT160  OI    INITFLGS,TABSINIT                                                
         B     INITX                                                            
*                                                                               
INIT200  LAM   AR0,ARF,ARZERO                                                   
         TM    P1+2,X'40'          TEST PGMS DATASPACE RESOURCE                 
         BZ    INITX                                                            
         OC    SSOPGMTA,SSOPGMTA   DO WE HAVE THE PGMS DATASPACE                
         BNZ   INIT260                                                          
*                                                                               
         LA    R0,21               GO GET DATASPACE                             
         LNR   R0,R0                                                            
         LA    R1,WORK                                                          
         XC    WORK(28),WORK                                                    
         MVC   WORK+0(4),=C'GETA'                                               
*&&US                                                                           
         MVC   WORK+4(12),=C'PRGCXXXXXXXX'                                      
         CLI   SSODSPAC,C'C'                                                    
         BE    INIT250                                                          
         MVC   WORK+4(12),=C'PRGTXXXXXXXX'                                      
         CLI   SSODSPAC,C'T'                                                    
         BE    INIT250                                                          
         MVC   WORK+4(12),=C'PRGAXXXXXXXX'                                      
         CLI   SSODSPAC,C'A'                                                    
         BE    INIT250                                                          
         CLI   SSODSPAC,C'P'                                                    
         BE    INIT250                                                          
         MVC   WORK+4(12),=C'PRGRXXXXXXXX'                                      
*&&                                                                             
*&&UK                                                                           
         MVC   WORK+4(12),=C'PRGCXXXXXXXX'                                      
         CLI   SSODSPAC,C'C'                                                    
         BE    INIT250                                                          
         MVC   WORK+4(12),=C'PRGBXXXXXXXX'                                      
         CLI   SSODSPAC,C'B'                                                    
         BE    INIT250                                                          
         MVC   WORK+4(12),=C'PGMTXXXXXXXX'                                      
         CLI   SSODSPAC,C'T'                                                    
         BE    INIT250                                                          
         MVC   WORK+4(12),=C'PGMAXXXXXXXX'                                      
*&&                                                                             
INIT250  SVC   247                                                              
*                                                                               
INIT254  LTR   RF,RF               TEST FOR ERRORS                              
         BNZ   INIT256                                                          
         OC    WORK+24(4),WORK+24                                               
         BNZ   INIT258             NO DATASPACE AVAILABLE                       
*                                                                               
INIT256  LARL  R4,PGMSMSG          CHECK PGMS DSPACE IS INITIALISED             
         BRAS  RE,WTO                                                           
*&&US*&& ABEND 911                                                              
*&&UK*&& ABEND 999                                                              
*                                                                               
INIT258  MVC   SSOPGMTB,WORK+20    SET SSB VALUES                               
INIT260  OI    INITFLGS,PGMSINIT                                                
*                                                                               
INITX    OI    SSOFLAG1,SSOFXCPY                                                
         XIT1                                                                   
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
* WORKING STORAGE DSECT                                               *         
***********************************************************************         
WORKD    DSECT                                                                  
DUB      DS    D                                                                
FULL     DS    F                                                                
FULL1    DS    F                                                                
HALF     DS    H                                                                
BYTE     DS    X                                                                
BYTE1    DS    X                                                                
*                                                                               
OFFLKN   DS    F                                                                
*                                                                               
SAVERE   DS    F                                                                
SAVERF   DS    F                                                                
SAVER0   DS    F                                                                
SAVER1   DS    F                                                                
*                                                                               
MYALET   DS    F                                                                
ASYSFLES DS    A                                                                
ASYSTEM  DS    A                                                                
ARECBUF  DS    A                                                                
TIMENOW  DS    F                                                                
ECB      DS    F                                                                
PARM     DS    6F                                                               
*                                                                               
DMCB0    DS    0F                                                               
DMCB     DS    0XL24                                                            
DMCB1    DS    F                                                                
DMCB2    DS    F                                                                
DMCB3    DS    F                                                                
DMCB4    DS    F                                                                
DMCB5    DS    F                                                                
DMCB6    DS    F                                                                
*                                                                               
DSTYPE   DS    C                   DATASPACE TYPE                               
SAVEP2   DS    X                                                                
FLAG     DS    X                                                                
FLAG1    DS    X                                                                
DSRSRC   DS    H                   RESOURCE NUMBER                              
MVSADV   DS    XL2                 SYS/TSK                                      
SYSNUM   DS    H                                                                
SYSNAME  DS    CL7                                                              
REPTFLAG DS    X                                                                
MSGNUM   DS    X                                                                
READONLY DS    X                                                                
WORK     DS    0CL64                                                            
RETBLK   DS    XL1024                                                           
MSG      DS    CL80                                                             
*                                                                               
         DS    0F                  FOR ENQHOLD                                  
LOCKASID DS    XL4                                                              
LOCKNAME DS    CL8                                                              
PARMS    DS    XL88                                                             
*                                                                               
WTORHDR  DS    0F                                                               
WTORLREP DS    XL1                                                              
WTORAREP DS    XL3                                                              
WTORAECB DS    XL4                                                              
WTORLMSG DS    XL2                                                              
         DS    XL2                                                              
WTORMSG  DS    CL70                                                             
*                                                                               
DYNBLK1  DS    F                   DYNALLOC BLOCK                               
DYNBLK2  DS    XL20                                                             
DYNBLK3  DS    XL8                                                              
DYNBLK4  DS    XL14                                                             
DYNBLK5  DS    XL38                                                             
WORKX    EQU   *                                                                
         EJECT                                                                  
***********************************************************************         
* OTHER DSECTS                                                        *         
***********************************************************************         
PARMD    DSECT                                                                  
P1       DS    F                                                                
P2       DS    F                                                                
P3       DS    F                                                                
P4       DS    F                                                                
                                                                                
*FASSB                                                                          
       ++INCLUDE FASSB                                                          
         ORG   SSBCNTL                                                          
*FASSBOFF                                                                       
       ++INCLUDE FASSBOFF                                                       
*FATCB                                                                          
       ++INCLUDE FATCB                                                          
*FASELIST                                                                       
       ++INCLUDE FASELIST                                                       
*DMDTFPH                                                                        
       ++INCLUDE DMDTFPH                                                        
*DMDTFIS                                                                        
       ++INCLUDE DMDTFIS                                                        
*DMSYSFD                                                                        
       ++INCLUDE DMSYSFD                                                        
*FATABSD                                                                        
       ++INCLUDE FATABSD                                                        
         PRINT ON                                                               
         PUSH ACONTROL                                                          
         ACONTROL COMPAT(NOCASE)                                                
         IHAASCB LIST=YES                                                       
         IHAASSB LIST=YES                                                       
         IAZJSAB LIST=YES                                                       
         IRAEVPL DSECT=YES,IRAENQHR_PARMLIST=YES                                
         IHAPVT                                                                 
         POP  ACONTROL                                                          
*DMRCVRD                                                                        
       ++INCLUDE DMRCVRD                                                        
*DMSPACED                                                                       
       ++INCLUDE DMSPACED                                                       
*DMDSHDR                                                                        
       ++INCLUDE DMDSHDR                                                        
*DMDSYSHDR                                                                      
       ++INCLUDE DMDSYSHDR                                                      
*DMSYSTABD                                                                      
       ++INCLUDE DMSYSTABD                                                      
*FASYSFAC                                                                       
       ++INCLUDE FASYSFAC                                                       
         PRINT ON                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'016DDLOCKSPC 09/17/20'                                      
         END                                                                    
