*          DATA SET DDCARDS    AT LEVEL 004 AS OF 02/06/13                      
*PROCESS USING(WARN(15))                                                        
*CATALP CARDS                                                                   
         TITLE '** CARDS **  READ AND PUNCH CARDS '                             
CARDS    CSECT                                                                  
         ENTRY SYSIN                                                            
         ENTRY SYSPUNCH                                                         
*                                                                               
         NMOD1 0,CARDS                                                          
         LR    R9,R1               SAVE A(PARMS)                                
         LM    R2,R3,0(R1)         GET DATA ADDRESS AND PARAMS                  
         CLI   0(R3),C'R'                                                       
         BE    READ                                                             
         CLI   0(R3),C'P'                                                       
         BE    PUNCH                                                            
CARDX    XMOD1 1                                                                
         EJECT                                                                  
READ     BC    0,READ2                                                          
         OI    *-3,X'F0'                                                        
         OPEN  (SYSIN)                                                          
         CLI   2(R3),C'1'          CALLER WANTS TO KNOW IF OPEN FAILED?         
         BNE   READ2                                                            
         STC   RF,4(R9)            YES: GIVE RETURN CODE BACK TO USER           
         CLI   4(R9),4             WAS THE OPEN SUCCESSFUL?                     
         BH    READEOF             NO: PRETEND WE HIT EOF                       
*                                                                               
READ2    LA    R1,SYSIN                                                         
         LR    R0,R2                                                            
         GET   (1),(0)                                                          
         B     CARDX                                                            
         SPACE 2                                                                
*                                                                               
READEOF  MVI   0(R2),C' '                                                       
         MVC   1(79,R2),0(R2)                                                   
         MVC   0(2,R2),=C'/*'                                                   
         CLOSE (SYSIN)                                                          
         B     CARDX                                                            
         SPACE 2                                                                
PUNCH    BC    0,PUNCH2                                                         
         OI    *-3,X'F0'                                                        
         OPEN  (SYSPUNCH,(OUTPUT))                                              
*                                                                               
PUNCH1   CLI   1(R3),C'C'          TEST FOR CLOSE VIA C'PC00'                   
         BNE   PUNCH2                                                           
         CLOSE (SYSPUNCH)                                                       
         B     CARDX                                                            
*                                                                               
PUNCH2   LA    R1,SYSPUNCH                                                      
         LR    R0,R2                                                            
         PUT   (1),(0)                                                          
         B     CARDX                                                            
*                                                                               
         EJECT                                                                  
SYSIN    DCB   DDNAME=SYSIN,MACRF=(GM),DSORG=PS,RECFM=FB,              X        
               EODAD=READEOF,,LRECL=80                                          
         SPACE 2                                                                
SYSPUNCH DCB   DDNAME=SYSPUNCH,MACRF=(PM),DSORG=PS,RECFM=F,            X        
               LRECL=80                                                         
         EJECT                                                                  
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'004DDCARDS   02/06/13'                                      
         END                                                                    
