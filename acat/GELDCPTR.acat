*          DATA SET GELDCPTR   AT LEVEL 025 AS OF 11/27/18                      
*CATALP GELDCPTR                                                                
         TITLE 'LDCPTR - GENDIR/GENFIL - CREATE DIRECTORY POINTERS'             
**********************************************************************          
*   CHANGES:                                                         *          
*   DEC14/12 (HWON)--- GENERATION OF PASSIVE FOR EDI PARTNER RECORDS *          
*   MARCH/11 (AWIL)--- GENERATION OF PASSIVES FOR LISTING RECORDS    *          
*   0CT  /06 (BOB) --- GENERATION OF PASSIVES FOR 00D701 KEYS        *          
*   0CT15/02 (BU ) --- GENERATION OF PASSIVES FOR 0071   KEYS        *          
**********************************************************************          
         PRINT NOGEN                                                            
*        PARA1 A(RECORD)                                                        
*        PARA2 A(AREA TO CREATE PASSIVE POINTERS)                               
*              LAST POINTER FOLLOWED BY X'00'                                   
*                                                                               
LDCPTR   CSECT                                                                  
         NMOD1 WRKX-WRKD,GELDCPTR                                               
         USING WRKD,RC                                                          
         LM    R2,R3,0(R1)         R2=A(PRIME DATA RECORD)                      
         MVI   8(R1),X'FF'         SET END-OF-PASSIVE INDICATOR                 
*                                                                               
         USING ACCSD,R2                                                         
         XC    0(ACDLEN,R3),0(R3)                                               
         MVC   0(ACKEYL,R3),0(R2)  KEY FROM PARENT                              
         MVC   ACDSTAT-ACCSD(4,R3),ACFSTAT    STATUS FROM PARENT                
         LA    R3,ACDLEN(R3)       R3=A(NEXT PASSIVE POINTER)                   
         XC    0(ACDLEN,R3),0(R3)                                               
         SR    RF,RF                                                            
         LA    RE,ACCSD+ACFIRST    RE=A(FIRST ELEMENT)                          
*                                                                               
EDIP     USING CTEPKEY,R2                                                       
EDI00    CLI   EDIP.CTEPKTYP,CTEPKTYQ   X'00'                                   
         BNE   DSTA00                                                           
         CLI   EDIP.CTEPKSTY,CTEPKSTQ   X'39'                                   
         BNE   DSTA00                                                           
         B     LDCEDI                                                           
         DROP  EDIP                                                             
*                                                                               
DSTA     USING DSTKEYD,R2                                                       
DSTA00   CLI   DSTA.DSTKMSYS,DSTKMSYQ  DEMO STATION CALL LETTER CHANGE?         
         BNE   LDC010                                                           
         CLI   DSTA.DSTKSYS,DSTKSYSQ                                            
         BNE   LDC010                                                           
         CLI   DSTA.DSTKSTYP,DSTKSTYQ                                           
         BNE   LDC010                                                           
         B     LDCDSTA                 YES: BUILD PASSIVES                      
         DROP  DSTA                                                             
*                                                                               
LLST     USING LLSTD,R2                                                         
LDC010   CLI   LLST.LLSTKMAJ,LLSTKMAQ  (L) LISTING SYSTEM                       
         BNE   LDC015                                                           
         CLI   LLST.LLSTKMIN,LLSTKMIQ  (S) SECURITY SYSTEM                      
         BNE   LDC015                                                           
         CLI   LLST.LLSTKREC,LLSTKREQ  (L) LIST RECORD                          
         BNE   LDC015                                                           
         B     LDCLLST                 YES: BUILD PASSIVES                      
         DROP  LLST                                                             
*                                                                               
CDC      USING CDCRECD,R2                                                       
LDC015   CLI   CDC.CDCKMAJ,0           (0) ZERO                                 
         BNE   LDC020                                                           
         CLI   CDC.CDCKMIN,CDCKMIQ     (D) DEMOGRAPHICS                         
         BNE   LDC020                                                           
         CLI   CDC.CDCKREC,CDCKREQ     (C) CATEGORY RECORD                      
         BNE   LDC020                                                           
         B     LDCCDC                  YES: BUILD PASSIVES                      
         DROP  CDC                                                              
*                                                                               
LDC020   CLI   ACKEY,CFMKTYPQ      TEST ACTIVE CFM RECORD                       
         BE    LDCFM                                                            
*                                                                               
         CLC   ACKEY(3),=AL3(GCOLKRIQ) TEST STANDARD COLUMN RECORD              
         BE    LDGCOL                                                           
*                                                                               
         OC    ACKEY(2),ACKEY      TEST SYSTEM/SUBSYSTEM                        
         BNZ   EXIT                                                             
         CLI   ACKREC,MOFKTYPQ     TEST FOR MEDIA OFFICE RECORD                 
         BE    LDXMOF                                                           
         CLI   ACKREC,GXFKRECQ     TEST FOR XFILE RECORD                        
         BE    LDXFILE                                                          
         CLI   ACKREC,GXSKRECQ     TEST FOR XTRANSFER RECORD                    
         BE    LDXTRANS                                                         
         CLI   ACKREC,GPKRECQ      TEST FOR PCPAK RECORD                        
         BE    LDPCPAK                                                          
         CLI   ACKREC,GSPLRECQ     TEST FOR SPOT S/P-P/P RECORD                 
         BE    LDSPPPKY                                                         
         CLI   ACKREC,GMKRECQ      TEST FOR MESSAGE RECORD                      
         BE    LDC040                                                           
         CLI   ACKREC,ACKRECQ      TEST FOR PERSON HEADER RECORD                
         BNE   EXIT                                                             
*                                                                               
         OC    ACKPWD,ACKPWD                                                    
         BZ    EXIT                                                             
         OC    ACKUSR(ACKEND-ACKUSR),ACKUSR                                     
         BNZ   EXIT                                                             
LDC030   CLI   0(RE),0             TEST E-O-R                                   
         BE    EXIT                                                             
         CLI   0(RE),ACNAMELQ      LOCATE PERSON NAME ELEMENT                   
         BE    *+14                                                             
         IC    RF,1(RE)                                                         
         AR    RE,RF                                                            
         B     LDC030                                                           
*                                                                               
         USING ACNAMD,RE                                                        
         XC    0(ACDLEN,R3),0(R3)  GENERATE PUBLIC-ID PASSIVE                   
         MVI   ACKREC-ACCSD(R3),ACKPASQ                                         
         MVC   ACKAGY-ACCSD(L'ACKAGY,R3),ACKAGY                                 
         MVC   ACKPID-ACCSD(L'ACKPID,R3),ACNAMPID                               
         MVC   ACDSTAT-ACCSD(4,R3),ACFSTAT                                      
         B     LDCX                                                             
         DROP  R2,RE                                                            
*                                                                               
         USING GMSGD,R2                                                         
LDC040   CLI   GMKTYP,GMKTGDIC     NEW-STYLE DICTIONARY RECORD?                 
         BNE   EXIT                                                             
         XC    0(GMDLEN,R3),0(R3)  GENERATE EQUATENAME PASSIVE                  
         MVI   GQKREC-GMSGD(R3),GQKRECQ           PSV RECORD TYPE               
         MVC   GQKMNUM-GMSGD(,R3),GMKMSG          MSG#                          
         CLI   GMKSYS,15                                                        
         BNE   *+8                                                              
         OI    GQKMNUM-GMSGD(R3),DD#GEN                                         
         MVC   GQKLANG-GMSGD(,R3),GMKLANG         LANG CODE                     
         MVC   GMDELEM-GMSGD(,R3),GMFELEM         ELEMENT FLAG                  
*                                                                               
LDC044   CLI   0(RE),0             TEST E-O-RECORD                              
         BE    EXIT                                                             
         CLI   0(RE),GMQSYELC      LOCATE EQUATE NAME ELEMENT                   
         BE    LDC048                                                           
         ZIC   RF,1(RE)                                                         
         AR    RE,RF                                                            
         B     LDC044                                                           
*                                                                               
         USING GMQSYD,RE                                                        
LDC048   MVC   GQKQNAME-GMSGD(,R3),GMQSYSYM  EQUATE NAME                        
         MVC   GQKRFP-GMSGD(,R3),GMQRFP      RFP RULE #                         
         DROP  R2,RE                                                            
*                                                                               
         DS    0H                  GENERATE RFP RULE PASSIVE POINTER            
         CLI   GQKRFP-GMSGD(R3),0   IS THERE AN RFP RULE NUMBER?                
         BE    LDCRFPX               NO, DON'T CREATE RFP PASSIVE               
                                                                                
         LR    RF,R3                RF-->EQUATENAME PASSIVE                     
         LA    R3,GMDLEN(R3)                                                    
         XC    0(GMDLEN,R3),0(R3)                                               
         MVI   GRKREC-GMSGD(R3),GRKRECQ                                         
         MVC   GRKSYS-GMSGD(,R3),GMKSYS-GMSGD(R2)                               
         MVC   GRKMNUM-GMSGD(,R3),GMKMSG-GMSGD(R2)                              
         MVC   GRKLANG-GMSGD(,R3),GMKLANG-GMSGD(R2)                             
         MVC   GRKRFP-GMSGD(,R3),GQKRFP-GMSGD(RF)                               
         MVC   GRKQNAME-GMSGD(,R3),GQKQNAME-GMSGD(RF)                           
         MVC   GMDELEM-GMSGD(,R3),GMFELEM-GMSGD(R2)                             
         CLI   GMKSYS-GMSGD(R2),15                                              
         BNE   *+8                                                              
         OI    GRKMNUM-GMSGD(R3),DD#GEN                                         
LDCRFPX  EQU   *                                                                
         B     LDCX                                                             
*---------------------------------------------------------------------          
* GENERATE MEDIA OFFICE ONE BYTE OFFICE PASSIVE                                 
*---------------------------------------------------------------------          
         USING MOFRECD,R2                                                       
LDXMOF   CLI   MOFKSUB,MOFKS1Q     MAKE SURE 1 BYTE OFFICE RECORD               
         BNE   EXIT                                                             
         XC    0(MOFKLEN,R3),0(R3)                                              
         MVI   MOFKTYP-MOFRECD(R3),MOFKTYPQ             C'O'                    
         MVI   MOFKSUB-MOFRECD(R3),MOFKS2Q              X'02'                   
         MVC   MOFKAGY-MOFRECD(L'MOFKAGY,R3),MOFKAGY    AGENCY                  
         MVC   MOFK2OF-MOFRECD(L'MOFK2OF,R3),MOFFC2OF   TWO BYTE OFFICE         
         MVC   MOFKSYS-MOFRECD(L'MOFKSYS,R3),MOFKSYS    SYSTEM NUMBER           
         MVC   MOFKSTAT-MOFRECD(L'MOFKSTAT,R3),MOFFSTAT STATUS                  
         MVC   MOFKC1OF-MOFRECD(L'MOFKC1OF,R3),MOFK1OF  ONE BYTE OFFICE         
         B     LDCX                                                             
         DROP  R2                                                               
*---------------------------------------------------------------------          
* GENERATE AGENCY FILE PASSIVE                                                  
*---------------------------------------------------------------------          
         USING GXTRD,R2                                                         
LDXFILE  XC    0(GXDLEN,R3),0(R3)  GENERATE AGENCY FILE PASSIVE                 
         MVI   GXKREC-GXTRD(R3),GXAFRECQ                                        
         MVC   GXAFAGY-GXTRD(L'GXAFAGY,R3),GXFKAGY                              
         MVC   GXAFSYS-GXTRD(L'GXAFSYS,R3),GXFKSYS                              
         MVC   GXAFSUB-GXTRD(L'GXAFSUB,R3),GXFKSUB                              
         MVC   GXAFDAT-GXTRD(L'GXAFDAT,R3),GXFKDAT                              
         MVC   GXAFTIM-GXTRD(L'GXAFTIM,R3),GXFKTIM                              
         MVC   GXDSTAT-GXTRD(4,R3),GXFSTAT                                      
*                                                                               
         USING GXFTEL,RE                                                        
LDXF010  CLI   GXFTEL,0                                                         
         BE    LDCX                                                             
         CLI   GXFTEL,GXFTELQ                                                   
         BE    LDXF030                                                          
LDXF020  SR    RF,RF                                                            
         IC    RF,GXFTELL                                                       
         AR    RE,RF                                                            
         B     LDXF010                                                          
*                                                                               
LDXF030  LA    R3,ACDLEN(R3)       BUMP TO NEXT PASSIVE POSITION                
         XC    0(ACDLEN,R3),0(R3)                                               
         XC    0(GXDLEN,R3),0(R3)  GENERATE ESS FILE PASSIVE                    
         MVI   GXKREC-GXTRD(R3),GXSFRECQ                                        
         MVC   GXSFEID-GXTRD(L'GXSFEID,R3),GXFTEID                              
         MVC   GXSFAGY-GXTRD(L'GXSFAGY,R3),GXFKAGY                              
         MVC   GXSFSYS-GXTRD(L'GXSFSYS,R3),GXFKSYS                              
         MVC   GXSFSUB-GXTRD(L'GXSFSUB,R3),GXFKSUB                              
         MVC   GXSFFNUM-GXTRD(L'GXSFFNUM,R3),GXFTFNUM                           
         MVC   GXDSTAT-GXTRD(4,R3),GXFSTAT                                      
         MVC   GXDCTL-GXTRD(L'GXDCTL,R3),GXFTCTL                                
         B     LDXF020                                                          
         DROP  R2,RE                                                            
*                                                                               
         USING GXTRD,R2                                                         
LDXTRANS XC    0(GXDLEN,R3),0(R3)  GENERATE ESS AGENCY PASSIVE                  
         MVI   GXKREC-GXTRD(R3),GXSARECQ                                        
         MVC   GXSAAGY-GXTRD(L'GXSAAGY,R3),GXSKAGY                              
         MVC   GXSASYS-GXTRD(L'GXSASYS,R3),GXSKSYS                              
         MVC   GXSASUB-GXTRD(L'GXSASUB,R3),GXSKSUB                              
         MVC   GXSAEID-GXTRD(L'GXSAEID,R3),GXSKEID                              
         MVC   GXSAAPID-GXTRD(L'GXSAAPID,R3),GXSKAPID                           
         MVC   GXDSTAT-GXTRD(4,R3),GXFSTAT                                      
         B     LDCX                                                             
         DROP  R2                                                               
*                                                                               
         USING GPAKD,R2            GENERATE PCPAK VERSION PASSIVES              
LDPCPAK  OC    GPKSYSLX,GPKSYSLX   SKIP CONTROL RECORD                          
         BZ    EXIT                                                             
         CLI   GPKSEQN,0           SKIP UNLESS SEQUENCE ZERO REC                
         BNE   EXIT                                                             
*                                                                               
         USING GPKVD,RE                                                         
LDPK010  CLI   GPKVEL,0            FIND VERSION CONTROL ELEMENT                 
         BE    EXIT                                                             
         CLI   GPKVEL,GPKVELQ                                                   
         BE    LDPK020                                                          
         SR    RF,RF                                                            
         IC    RF,GPKVELL                                                       
         AR    RE,RF                                                            
         B     LDPK010                                                          
*                                                                               
LDPK020  MVI   GPVRREC-GPVRD(R3),GPVRRECQ                                       
         MVC   GPVRSYS-GPVRD(L'GPKSYSLX,R3),GPKSYSLX                            
         MVC   GPVRVER-GPVRD(,R3),GPKVVER                                       
         MVC   GPVRFILE-GPVRD(,R3),GPKFILE                                      
         MVC   GPVDSCRS-GPVRD(,R3),GPKVSCRS                                     
         B     LDCX                                                             
         DROP  R2,RE                                                            
*                                                                               
         USING GSPLRECD,R2                                                      
LDSPPPKY EQU   *                                                                
         CLI   GSPLKSTP,0          SUBTYPE = SUBSIDIARY REP?                    
         BE    EXIT                YES - NO PASSIVE NEEDED - DON'T              
*                                                                               
         LR    R4,R3               SAVE A(1ST PASSIVE) WOULD BE                 
*                                                                               
LDSPPP10 CLI   0(RE),0             EOR?                                         
         BE    EXIT                                                             
         CLI   0(RE),GSPLOFCQ      LOOK FOR OFFICE ELEMENTS                     
         BE    LDSPPP20                                                         
LDSPPP15 IC    RF,1(RE)            NEXT ELEMENT                                 
         AR    RE,RF                                                            
         B     LDSPPP10                                                         
*                                                                               
LDSPPP20 CR    R3,R4               DID WE ADD A PASSIVE KEY YET?                
         BE    LDSPPP50            NO, THIS IS OUR FIRST                        
***********************************                                             
* THIS CODE WAS PUT IN TO PROTECT US FROM DUP KEYS AS SBS HAS NY AND NY         
* RESEARCH WITH DIFFERENT SBS CITY CODES, BUT THE SAME NSI CODE                 
***********************************                                             
         LR    R5,R4               CHECK IF WE ADDED THIS CITY ALREADY          
LDSPPP25 CR    R5,R3                                                            
         BE    LDSPPP50            NO, WE HAVE NOT                              
         CLC   GSPKPOFF-GSPLKEY(02,R5),GSPLOFDD-GSPLOFCD(RE) SAME?              
         BE    LDSPPP15            YES, THEN DON'T ADD A DUPLICATE              
         LA    R5,ACDLEN(R5)       R5=A(NEXT PASSIVE POINTER)                   
         B     LDSPPP25                                                         
*                                                                               
LDSPPP50 MVI   GSPLPTYP-GSPLKEY(R3),GSPLPTYQ                                    
         MVC   GSPLPSTP-GSPLKEY(01,R3),GSPLKSTP   SUBREC TYPE                   
         MVC   GSPLPREP-GSPLKEY(02,R3),GSPLKSRP   REP CODE                      
         MVC   GSPKPOFF-GSPLKEY(02,R3),GSPLOFDD-GSPLOFCD(RE) DDS OFFICE         
         MVC   GSPKPCOD-GSPLKEY(03,R3),GSPLKSAL                                 
         LA    R3,ACDLEN(R3)       R3=A(NEXT PASSIVE POINTER)                   
         XC    0(ACDLEN,R3),0(R3)                                               
         B     LDSPPP15                                                         
         DROP  R2                                                               
*                                                                               
ACTIVE   USING CFMKEY,R2                                                        
PASSIVE  USING CFMPAS,R3                                                        
LDCFM    CLI   ACTIVE.CFMKSUBR,CFMKSCLT                                         
         BE    LDCFM02                                                          
         CLI   ACTIVE.CFMKSUBR,CFMKSBRD                                         
         BE    LDCFM04                                                          
         CLI   ACTIVE.CFMKSUBR,CFMKSMED                                         
         BH    EXIT                                                             
         OC    ACTIVE.CFMKRNOD,ACTIVE.CFMKRNOD                                  
         BZ    EXIT                                                             
         MVI   PASSIVE.CFMPTYPE,CFMPTYPQ                                        
         MVC   PASSIVE.CFMPAGY,ACTIVE.CFMKAGY                                   
         MVC   PASSIVE.CFMPRNOD,ACTIVE.CFMKRNOD                                 
         XC    PASSIVE.CFMPRNOD,=X'FFFFFFFF'                                    
         MVC   PASSIVE.CFMPPNOD,ACTIVE.CFMKPNOD                                 
         B     LDCFM06                                                          
*                                  CREATE CLIENT POINTER PASSIVE                
LDCFM02  MVI   PASSIVE.CLTPTYPE,CLTPTYPQ                                        
         MVC   PASSIVE.CLTPAGY,ACTIVE.CFMKAGY                                   
         MVC   PASSIVE.CLTPMED,ACTIVE.CFMKCMED                                  
         MVC   PASSIVE.CLTPCLT,ACTIVE.CFMKCLTE                                  
         B     LDCFM06                                                          
*                                  CREATE BRAND POINTER PASSIVE                 
LDCFM04  MVI   PASSIVE.BRDPTYPE,BRDPTYPQ                                        
         MVC   PASSIVE.BRDPAGY,ACTIVE.CFMKAGY                                   
         MVC   PASSIVE.BRDPMED,ACTIVE.CFMKBMED                                  
         MVC   PASSIVE.BRDPCLT,ACTIVE.CFMKBCLT                                  
         MVC   PASSIVE.BRDPBRD,ACTIVE.CFMKBRDE                                  
*                                                                               
LDCFM06  MVC   PASSIVE.CFMKSTAT,ACTIVE.CFMRSTAT                                 
         B     LDCX                                                             
         DROP  ACTIVE,PASSIVE                                                   
                                                                                
*======================================================================         
* CUSTOM DEMO CATEGORY NUMBER PASSIVE X'00'C'DC'                                
*======================================================================         
ACTIVE   USING CDCRECD,R2                                                       
PASSIVE  USING CDCRECD,R3                                                       
         USING CDCELD,RE                                                        
*                                                                               
LDCCDC   CLI   0(RE),CDCELQ        CUSTOM DEMO CATEGORY ELEMENT                 
         BE    LDCCDC2             . YES                                        
*                                                                               
         XR    R0,R0               NEXT ELEMENT                                 
         IC    R0,1(RE)                                                         
         AR    RE,R0                                                            
*                                                                               
         CLI   0(RE),0             EOR?                                         
         BNE   LDCCDC                                                           
         B     EXIT                EXIT, NUMBER NOT FOUND                       
*                                                                               
LDCCDC2  MVI   PASSIVE.CDCKMAJ,0        X'00'                                   
         MVI   PASSIVE.CDCKMIN,CDCKMIQ  C'D'                                    
         MVI   PASSIVE.CDCKREC,CDCKREQ  C'C'                                    
*                                                                               
         MVC   PASSIVE.CDCKNUM,CDCNUM                                           
         MVC   PASSIVE.CDCKOXC,ACTIVE.CDCKOXC                                   
*                                                                               
         MVC   PASSIVE.CDCDSTAT,ACTIVE.CDCDSTAT                                 
         MVC   PASSIVE.CDCDCTL,ACTIVE.CDCDCTL                                   
*                                                                               
         AHI   R3,ACDLEN           R3=A(NEXT PASSIVE POINTER)                   
         XC    0(ACDLEN,R3),0(R3)  CLEAR SLOT                                   
         B     EXIT                                                             
*                                                                               
         DROP  ACTIVE,PASSIVE                                                   
         DROP  RE                                                               
                                                                                
*======================================================================         
* LIST NUMBER PASSIVE - C'LSN'                                                  
*======================================================================         
ACTIVE   USING LLSTD,R2                                                         
PASSIVE  USING LNUMD,R3                                                         
         USING LLNUD,RE                                                         
*                                                                               
LDCLLST  CLI   0(RE),LLNUELQ       LIST NUMBER ELEMENT                          
         BE    LDCLLST2            . YES                                        
         XR    R0,R0               NEXT ELEMENT                                 
         IC    R0,1(RE)                                                         
         AR    RE,R0                                                            
*                                                                               
         CLI   0(RE),0             EOR?                                         
         BNE   LDCLLST                                                          
         B     EXIT                EXIT, NUMBER NOT FOUND                       
*                                                                               
LDCLLST2 MVI   PASSIVE.LNUMKMAJ,LNUMKMAQ                                        
         MVI   PASSIVE.LNUMKMIN,LNUMKMIQ                                        
         MVI   PASSIVE.LNUMKREC,LNUMKREQ                                        
*                                                                               
         MVC   PASSIVE.LNUMKNUM,LLNUNU                                          
*                                                                               
         MVC   PASSIVE.LNUMKAGY,ACTIVE.LLSTKAGY                                 
         MVC   PASSIVE.LNUMKPSN,ACTIVE.LLSTKPSN                                 
         MVC   PASSIVE.LNUMKGRP,ACTIVE.LLSTKGRP                                 
         MVC   PASSIVE.LNUMKSYS,ACTIVE.LLSTKSYS                                 
         MVC   PASSIVE.LNUMKTYP,ACTIVE.LLSTKTYP                                 
*                                                                               
         MVC   PASSIVE.LNDSTAT,ACTIVE.LLFSTAT                                   
         MVC   PASSIVE.LNDATT,ACTIVE.LLFATT                                     
         MVC   PASSIVE.LNDCTL,ACTIVE.LLFCTL                                     
         B     LDCX                CLEAR NEXT PASSIVE POINTER SLOT              
*                                                                               
         DROP  ACTIVE,PASSIVE                                                   
         DROP  RE                                                               
*======================================================================         
                                                                                
LDCDSTA  DS    0H                                                               
ACTIVE   USING DSTKEYD,R2                                                       
PASSIVE  USING DSTKEYD,R3                                                       
         CLI   0(RE),DSTSTACQ      STATION ELEMENT?                             
         BNE   LDCDSTA5                                                         
*                                                                               
         USING DSTSTAD,RE                                                       
         MVC   PASSIVE.DSTKEY,ACTIVE.DSTKEY  ACTIVE KEY                         
         MVC   PASSIVE.DSTKSTN,DSTSTAST      STATION FROM ELEMENT               
         MVC   PASSIVE.DSTKLSBK,DSTSTLBK     LAST BOOK FROM ELEMENT             
         MVC   PASSIVE.DSTKSTAT,ACTIVE.DSTSTAT  STATUS BYTES                    
         OI    PASSIVE.DSTKSTAT,X'40'        SET PASSIVE BIT IN HOB             
         AHI   R3,ACDLEN           R3=A(NEXT PASSIVE POINTER)                   
         XC    PASSIVE.DSTKEY(ACDLEN),PASSIVE.DSTKEY                            
*                                                                               
LDCDSTA5 DS    0H                                                               
         ZIC   R0,1(RE)                                                         
         AR    RE,R0                                                            
         CLI   0(RE),0             EOR?                                         
         BNE   LDCDSTA                                                          
         B     EXIT                                                             
         DROP  ACTIVE,PASSIVE                                                   
         DROP  RE                                                               
*                                                                               
LDCX     AHI   R3,ACDLEN           R3=A(NEXT PASSIVE POINTER)                   
         XC    0(ACDLEN,R3),0(R3)                                               
         B     EXIT                                                             
*                                                                               
*======================================================================         
*                                                                               
LDGCOL   DS    0H                  PRINT STANDARD COIUMN PASSIVE                
*                                                                               
ACTIVE   USING GCOLKEY,R2          ACTIVE STANDARD COLUMN KEY                   
PASSIVE  USING GCOLPKEY,R3                                                      
*                                                                               
         LA    R6,ACTIVE.GCOLFRST  POINT TO FIRST ELEMENT                       
         SR    RF,RF                                                            
*                                                                               
LDGCOLLP DS    0H                                                               
*                                                                               
         USING PCOLELEM,R6         ESTABLISH CUSTOM COLUMN DSECT                
*                                                                               
         CLI   PCOLELEM,0          DONE IF END OF RECORD REACHED                
         BE    LDGCOLDN                                                         
*                                                                               
         CLI   PCOLELEM,X'61'      FIND COLUMN DESCRIPTION ELEMENT              
         BE    LDGCOLFD                                                         
*                                                                               
LDGCOLCN DS    0H                                                               
         IC    RF,PCOLELEM+1       BUMP TO NEXT ELEMENT                         
         LA    R6,PCOLELEM(RF)                                                  
*                                                                               
         B     LDGCOLLP                                                         
*                                                                               
LDGCOLFD DS    0H                  HAVE COLUMN DESCRIPTION ELEMENT              
*                                                                               
         MVC   PASSIVE.GCOLPRID,=AL3(GCOLPRIQ) SET PASSIVE ID                   
         MVC   PASSIVE.GCOLPMED,ACTIVE.GCOLKMED  MEDIA                          
         MVI   PASSIVE.GCOLPRCD,GCOLPRCQ         RECORD ID                      
         MVC   PASSIVE.GCOLPSQN,PCOLSQN          COLUMN SEQUENCE NO.            
         XC    PASSIVE.GCOLPSQN,=X'FFFF'         COMPLEMENT SQN                 
         MVC   PASSIVE.GCOLPSTA,ACTIVE.GCOLDSTA  STATUS BYTE                    
         MVC   PASSIVE.GCOLPCTL,ACTIVE.GCOLDCTL  CONTROL BYTES                  
*                                                                               
         AHI   R3,ACDLEN           R3=A(NEXT PASSIVE POINTER)                   
         XC    0(ACDLEN,R3),0(R3)                                               
*                                                                               
         B     EXIT                                                             
*                                                                               
LDGCOLDN DS    0H                                                               
*                                                                               
         B     EXIT                ALL DONE                                     
         DROP  ACTIVE,PASSIVE                                                   
*======================================================================         
* GENERATE EDI PARTNER PASSIVE X'0039'                                          
*======================================================================         
ACT      USING CTEPRECD,R2                                                      
PAS      USING CTEQKEY,R3                                                       
*                                                                               
LDCEDI   DS    0H                  GENERATE EDI PARTNER PASSIVES                
*                                                                               
         LA    R6,ACT.CTEPDAT      POINT TO FIRST ELEMENT                       
         SR    RF,RF                                                            
*                                                                               
         CLI   0(R6),0              EOR??                                       
         BE    LDCEDIX              OK, JUST SKIP IT                            
*                                                                               
         USING CTEPRD,R6                                                        
         CLI   CTEPREL,CTEPRELQ     X'02'- DID WE FIND PRIMARY ELEMENT          
         BNE   LDCEDIX                                                          
         OC    CTEPPREF,CTEPPREF                                                
         BZ    LDCEDIX                                                          
         MVI   PAS.CTEQKTYP,CTEQKTYQ      X'00'                                 
         MVI   PAS.CTEQKSTY,CTEQKSTQ      X'3A'                                 
         MVC   PAS.CTEQKPAR,CTEPPREF                                            
         MVC   PAS.CTEQKUSC,ACT.CTEPKREP                                        
*                                                                               
         AHI   R3,ACDLEN           R3=A(NEXT PASSIVE POINTER)                   
         XC    0(ACDLEN,R3),0(R3)                                               
*                                                                               
LDCEDIX  B     EXIT                ALL DONE                                     
         DROP  PAS,ACT                                                          
*                                                                               
EXIT     MVI   0(R3),X'FF'         SET END OF TABLE                             
         XMOD1 1                                                                
         EJECT                                                                  
DD#GEN   EQU   X'40'                                                            
         SPACE 2                                                                
         LTORG                                                                  
         SPACE 2                                                                
WRKD     DSECT                                                                  
DUB      DS    D                                                                
WRKX     EQU   *                                                                
         EJECT                                                                  
* GEGENACS                                                                      
* GEGENCFM                                                                      
* GEGENMSG                                                                      
* GEGENXTR                                                                      
* GEGENSPSAL                                                                    
* GEGENOFF                                                                      
* GEGENLST                                                                      
* GEGENCDC                                                                      
* CTGENDSTA                                                                     
* GEGENPAK                                                                      
* PCOLREC                                                                       
* GEGENEDI                                                                      
**       PRINT OFF                                                              
       ++INCLUDE GEGENACS                                                       
         EJECT                                                                  
       ++INCLUDE GEGENCFM                                                       
         EJECT                                                                  
       ++INCLUDE GEGENMSG                                                       
         EJECT                                                                  
       ++INCLUDE GEGENXTR                                                       
         EJECT                                                                  
       ++INCLUDE GEGENSPSAL                                                     
         EJECT                                                                  
       ++INCLUDE GEGENOFF                                                       
         EJECT                                                                  
       ++INCLUDE GEGENLST                                                       
         EJECT                                                                  
       ++INCLUDE GEGENCDC                                                       
         EJECT                                                                  
       ++INCLUDE CTGENDSTA                                                      
         EJECT                                                                  
       ++INCLUDE GEGENPAK                                                       
         EJECT                                                                  
       ++INCLUDE PCOLREC                                                        
         EJECT                                                                  
       ++INCLUDE GEGENEDI                                                       
         PRINT ON                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'025GELDCPTR  11/27/18'                                      
         END                                                                    
