*          DATA SET PRWRICRT   AT LEVEL 026 AS OF 03/20/06                      
*CATALP PRWRICRT                                                                
         TITLE 'PRWRICRT - CONTRACT RATES - ENTRY POINTS'                       
         ENTRY ICRATE                                                           
PRWRICRT CSECT                                                                  
         TITLE 'PRWRICRT - CHANGE LOG'                                          
*                                                                               
* BOBY  02/06 - OUTDOOR COMMISSION DEFAULT NO LONGER 16.667%                    
*                                                                               
         TITLE 'PRWRICRT - CONTRACT RATES '                                     
***********************************************************************         
*                                                                     *         
*        THIS SECTION RETRIEVES CONTRACT RATE DATA                    *         
*                                                                     *         
*          IF PROCESSING BUYS THE CURRENT BUY RECORD IS USED TO       *         
*            FILTER THE CONTRACT RATES. I.E. BUY SPACE DESCRIPTION    *         
*            IS USED TO FIND APPROPRIATE CONTRACT RATE ELEMENT.       *         
*          IF PROCESSING ONLY CONTRACTS THE SPACE DESCRIPTION IS      *         
*            PASSED IN A CONTRACT RATE ELEMENT SAVED BY THE DRIVING   *         
*            ROUTINE. IN GENERAL THIS ROUTINE USES THE CURRENT LEVEL  *         
*            RATE ELEMENTS OR ENTERED SPACE DESCRIPTIONS. THUS THIS   *         
*            ROUTINE MUST READ CONTRACT RECORD FOR SPACE MATCH        *         
*          BECAUSE IT MAY BE LOOKING FOR LOWER OR HIGHER RATE ELEMENTS.         
*                                                                     *         
*NTRY    R1  ==>  PARAMETER LIST                                      *         
*                 A(A(CONTRACT RATE ELEMENT))                         *         
*                 A(RATE COLLECTION AREA)                             *         
*                                                                     *         
*        R4  ==>  PBUYREC                                             *         
*                                                                     *         
*        GLARGS      0  1   2   3   4                                 *         
*                                                                     *         
*                  C'B'   BUY        KEYWORD                          *         
*                  C'C'   CONTRACT   KEYWORD                          *         
*                     C'D'   CONTRACT RATE EFFECTIVE DATE             *         
*                     C'P'   CONTRACT PER CENT DISCOUNT               *         
*                     C'R'   CONTRACT RATE                            *         
*                         C'2'   LESS CD                              *         
*                         C'M'   USE MASK IN OUTPUT                   *         
*                         X'20'  CURRENT CONTRACT RATES               *         
*                         X'21'  LOWER   CONTRACT RATES               *         
*                         X'22'  HIGHER  CONTRACT RATES               *         
*                         X'24'  OPEN    CONTRACT RATES               *         
*                              C'R' RATE CODE KEYWORD                 *         
*                                  C'C'  RATE CODE                    *         
*                                  C'D'  RATE CODE DESCRIPTION        *         
*                                  C'R'  RATE CODE RATE               *         
*                     C'U'   CONTRACT UNITS                           *         
*                         C'P'   USE CU= INSTEAD OF DOLLARS           *         
*                             C'0'   NO DEFAULT ALLOWED               *         
*                     C'V'   CONTRACT LEVEL                           *         
*                                                                     *         
*                                                                     *         
*                                                                     *         
*                                                                     *         
*EXIT    R2  ==>  3PL8   FIELDS                                       *         
*                   1 = CONTRACT LEVEL REPORTED                       *         
*                   2 = INTERNAL COUNTER                              *         
*                   3 = CONTRACT UNITS DESCRIPTION CODE               *         
*                                                                     *         
***********************************************************************         
         SPACE 2                                                                
         PRINT NOGEN                                                            
         TITLE 'PRWRICRT - CONTRACT RATES - ICRATE'                             
         DS    0D                                                               
ICRATE   NMOD1 0,**#ICR                                                         
*                                                                               
*        INITIALIZATION                                                         
*                                                                               
         USING SPOOLD,R8           ESTABLISH SPOOL WORKAREA                     
         USING SYSD,R9             ESTABLISH PRINTWRITER WORKAREA               
         USING GLOBALD,RA          ESTABLISH DRIVER WORKAREA                    
*                                                                               
         LA    R7,PBLOCK           ESTABLISH PRINT BLOCK                        
         USING PBLOCK,R7                                                        
*                                                                               
         L     RC,GLAWORKD         RESET WORKING STORAGE POINTER                
         USING GEND,RC                                                          
*                                                                               
         USING PBUYRECD,R4         ESTABLISH BUY RECORD                         
*                                                                               
         USING PRBELEM,R6          ESTABLISH CONTRACT RATE ELEMENT              
*                                                                               
         MVC   ICRPRMS(ICRPRMSL),0(R1) SAVE PARAMETER LIST                      
         ST    R1,ICRTPRMA         SAVE PARAMETER LIST ADDRESS                  
*                                                                               
         XC    ICRTELA,ICRTELA     CLEAR ADDRESS OF ELEMENT                     
         XC    ICRTSPC,ICRTSPC     INIT SPACE WORKAREA                          
         XC    ICRTLIND,ICRTLIND   INIT LEVEL INDICATOR WORKAREA                
         XC    ICRTRCD,ICRTRCD     INIT RATE CODE DESCRIPTION                   
         ZAP   ICRTCLTR,=P'0'      INIT DATA COLLECTION AREA                    
         ZAP   ICRTCNTR,=P'0'      INIT ITERATION COUNTER                       
*                                                                               
         ZAP   0(8,R2),=P'0'       GENERATE 3 SIGNED PACKED FIELDS              
         ZAP   8(8,R2),=P'0'                                                    
         ZAP   16(8,R2),=P'0'                                                   
*                                                                               
         TM    PBQREAD,PBQRDBUY    IF READING BUYS                              
         BNO   ICRTBUYN                                                         
*                                                                               
         CLI   PBUYKRCD,PBUYKIDQ      SKIP IF NO BUY IN AOR AREA                
         BNE   ICRTX                                                            
*                                                                               
         TM    PBUYCNTL,X'80'         SKIP IF DELETED BUY                       
         BO    ICRTX                                                            
*                                                                               
         MVC   ICRTMED,PBUYKMED          USE BUY MEDIA                          
         MVC   ICRTSPC,PBDSPACE          USE BUY SPACE DESCRIPTION              
         MVC   ICRTRCD,PBDRCODE          USE RATE CODE                          
*                                                                               
         B     ICRTBUYX                                                         
*                                                                               
ICRTBUYN DS    0H                                                               
*                                                                               
         ICM   R6,15,PBQCRTA      IF WE HAVE CONTRACT RATE ELEMENT              
         BZ    ICRTX                   EXIT IF NONE FOUND                       
*                                                                               
         L     RF,AIO2                         POINT TO CONTRACT RECORD         
         MVC   ICRTMED,PCONKMED-PCONREC(RF)   AND CONTRACT MEDIA                
*                                                                               
         CLI   ELCODE,X'20'           IF DOING CURRENT LEVEL DATA               
         BNE   *+12                                                             
         ST    R6,ICRTELA                SAVE ADDRESS                           
         B     ICRTFD                    AND USE ELEMENT                        
*                                                                               
         MVC   ICRTSPC,PRBDESC       USE ITS SPACE DECRIPTION                   
         MVC   ICRTLIND,PRBLIND      USE ITS LEVEL INDICATOR                    
*                                                                               
         CLC   PRBDESC(2),=C'R='     IF DESCRIPTION STARTS WITH R=              
         BNE   *+10                                                             
         MVC   ICRTRCD,PRBDESC+2        SAVE AS RATE CODE                       
*                                                                               
ICRTBUYX DS    0H                                                               
*                                                                               
ICRT05   DS    0H                                                               
*                                                                               
         CLI   ICRTSPC,C'*'        IF SPACE DOESN'T COUNT                       
         BNE   ICRT08                 TOWARDS CONTRACT                          
*                                                                               
         CLI   GLARGS+1,C'U'          IF NOT LOOKING FOR UNITS                  
         BNE   ICRTX                     DONE                                   
*                                                                               
         TM    PBQREAD,PBQRDBUY       IF NOT READING BUYS                       
         BNO   ICRTAUZ                   GO REPORT AS 0 UNITS                   
*                                                                               
         OC    PBDCU,PBDCU            IF NO CONTRACT UNITS OVERRIDE             
         BZ    ICRTAUZ                   GO REPORT AS 0 UNITS                   
*                                                                               
         CLC   PBDCU,=X'000001'       IF EQUAL TO .0001 BYPASS                  
         BE    ICRTAUZ                   GO REPORT AS 0 UNITS                   
*                                                                               
         SR    RF,RF                  ELSE CONVERT TO PACKED FORMAT             
         ICM   RF,7,PBDCU                                                       
         CVD   RF,ICRTDUB                                                       
         SRP   ICRTDUB(8),64-1,5           ROUND TO 3 DECIMALS                  
         ZAP   0(8,R2),ICRTDUB(8)                                               
         ZAP   ICRTCLTR,ICRTDUB(8)         FOR DIFFERENCE                       
*                                                                               
         B     ICRTAUX                     UNITS FOUND                          
*                                                                               
ICRT08   DS    0H                                                               
*                                                                               
         CLI   ICRTSPC,X'FF'       IF NOT OUTDOOR SPACE DESCRIPTION             
         BE    *+10                                                             
         OC    ICRTSPC,SPACES         MAKE UPPERCASE                            
*                                                                               
         L     RF,AIO2             POINT TO CONTRACT RECORD                     
*                                                                               
         CLI   PCONNUM-PCONREC(RF),X'FF' IF CONTS RQSTED BY BUY FLAVOR          
         BNE   ICRT10                                                           
*                                        AND NO CONTRACT FOUND                  
         CLI   GLARGS+1,C'U'             AND LOOKING FOR UNITS                  
         BE    ICRTAUN                       GO TAKE CARE OF UNITS              
         B     ICRTX                     ELSE DONE                              
*                                                                               
ICRT10   DS    0H                                                               
*                                                                               
         EJECT                                                                  
***********************************************************************         
*                                                                     *         
*        FIND CONTRACT RATE ELEMENT TO USE                            *         
*        MATCH ON SPACE DESCRIPTION                                   *         
*        IF RATE CODE PRESENT THEN USE IT FOR MATCH                   *         
*                                                                     *         
***********************************************************************         
         SPACE 2                                                                
         L     R6,AIO2             SET TO SCAN CONTRACT RECORD                  
*                                                                               
*                                  ELCODE HAS ELEMENT ID                        
*                                                                               
         BAS   RE,GETEL            FIND FIRST ELEMENT OF TYPE                   
         BE    ICRT11                                                           
*                                                                               
         CLI   GLARGS+1,C'U'       IF NONE AND DOING UNITS                      
         BE    ICRTAUN                GO HANDLE THEM                            
         B     ICRTX               ELSE EXIT                                    
*                                                                               
ICRT11   DS    0H                                                               
*                                                                               
* COVER THE SITUATION WHERE NO RATE FOR SPACE HAS BEEN CREATED AND              
*       CONTRACT UNITS WERE REQUESTED-- USE RATE BASIS FROM FIRST               
*       CONTRACT ELEMENT                                                        
*                                                                               
         CLI   GLARGS+1,C'V'      LEVEL REQUESTED ??                            
         BE    *+8                                                              
         CLI   GLARGS+1,C'U'      OR UNITS REQUESTED ??                         
         BNE   *+8                                                              
         ST    R6,ICRTELA          SAVE ADDRESS OF FIRST RATE ELEMENT           
*                                                                               
ICRTLP   DS    0H                                                               
*                                                                               
         OC    ICRTRCD,ICRTRCD    IF RATE CODE PRESENT                          
         BZ    ICRTL0                                                           
*                                                                               
         CLC   =C'R=',PRBDESC            DESC MUST START WITH 'R='              
         BNE   ICRTCN                                                           
*                                                                               
         CLC   ICRTRCD,PRBDESC+2         AND RATE CODES MUST MATCH              
         BNE   ICRTCN                                                           
*                                                                               
         B     ICRTL1                                                           
*                                                                               
ICRTL0   DS    0H                                                               
*                                                                               
         CLC   ICRTSPC,PRBDESC    ELM SPACE MUST MATCH                          
         BNE   ICRTL01                                                          
*                                                                               
         CLC   ICRTSPC,SPACES      IF SPACE NOT GIVEN                           
         BH    ICRTL1                                                           
         OC    PBQCRTA,PBQCRTA     AND RATE ELEMENT GIVEN                       
         BZ    ICRTL1                                                           
         CLC   ICRTLIND,PRBLIND    THEN LVL INDICATOR MUST MATCH                
         BE    ICRTL1                                                           
*                                                                               
ICRTL01  DS    0H                                                               
*                                                                               
         CLI   PRBDESC,X'FF'         NO MATCH IF OUTDOOR FORMAT                 
         BE    ICRTCN                                                           
*                                                                               
         LH    RF,=Y(PPBYOUTC-SYSD)  DISPLACEMENT OF PPBYOUT AREA               
         AR    RF,R9                                                            
         USING PPBYOUTD,RF         ESTABLISH PPBYOUT AREA                       
*                                                                               
         CLC   PRBDESC,PBYOSPC         ELSE MATCH PPBYOUT OUTPUT                
         BNE   ICRTCN                                                           
*                                                                               
         MVC   ICRTSPC,PBYOSPC              USE PPBYOUT DESCRIPTION             
*                                                                               
ICRTL1   DS    0H                                                               
*                                                                               
         CLI   CONTPROD,0         IF CONTRACT PRODUCT                           
         BE    *+10                  BEING PROCESSED, IT MUST MATCH             
         CLC   CONTPROD,PRBOPEN      RATE ELEMENT PRODUCT                       
         BNE   ICRTCN                                                           
*                                                                               
         CLI   PRBDATE,0          IF NO EFFECTIVE DATE FOR RATE                 
         BNE   ICRTL10                                                          
*                                                                               
         L     RF,AIO2                   POINT TO CONTRACT RECORD               
         LA    RF,PCONELEM-PCONREC(RF)   1ST ELM IS DESCRIPTION ELEMENT         
*                                      DEFAULT TO CONTRACT START DATE           
         MVC   PRBDATE,PCONSDT-PCONDESC(RF)                                     
         MVI   PRBDATE+2,0               FLAG DAY SO IT WILL NOT BE             
*                                        PRINTED ON OUTPUT SIDE                 
ICRTL10 DS     0H                                                               
*                                                                               
         TM    PBQREAD,PBQRDBUY    IF NOT READING BUYS                          
         BO    ICRTL20                                                          
*                                                                               
         ICM   RF,15,PBQCRTA       POINT TO PASSED RATE ELEMENT                 
         BZ    ICRTX                  EXIT IF NONE FOUND                        
*                                  COULD BE DOING LOWER/HIGHER RATES            
*                                  BASED ON CURRENT RATES                       
*                                                                               
         LA    R1,PRBDATE-PRBELEM(RF)   RATE EFFECTIVE DATE                     
*                                                                               
         OC    0(L'PRBDATE,R1),0(R1)    IF NO EFF DATE PRESENT                  
         BNZ   *+16                                                             
         L     R1,AIO2                     POINT TO CONTRACT RECORD             
         LA    R1,PCONELEM-PCONREC(R1)   1ST ELM IS DESCRIPTION ELEMENT         
         LA    R1,PCONSDT-PCONDESC(R1)   DEFAULT TO CONTRACT START DATE         
*                                                                               
         CLC   PRBDATE,0(R1)   DONE IF                                          
         BH    ICRTDN              ELM EFF DATE AFTER PASSED ELM                
*                                                                               
         ST    R6,ICRTELA      SAVE ADDRESS OF POSSIBLE CANDIDATE               
*                                                                               
         BE    ICRTDN          USE ELEMENT IF EFF DATE MATCHES EXACTLY          
         B     ICRTCN          ELSE FIND ELEMENT THAT COULD APPLY               
*                                                                               
ICRTL20 DS     0H                                                               
*                                                                               
         CLC   PRBDATE,PBUYKDAT STOP ELM SEARCH IF                              
         BH    ICRTDN              ELM EFF DATE AFTER THAT OF BUY               
*                                                                               
         ST    R6,ICRTELA      SAVE ADDRESS OF POSSIBLE CANDIDATE               
*                                                                               
         BE    ICRTDN          USE ELEMENT IF EFF DATE MATCHES EXACTLY          
         B     ICRTCN          ELSE FIND ELEMENT THAT COULD APPLY               
*                                                                               
ICRTCN   DS    0H                                                               
*                                                                               
         BAS   RE,NEXTEL                                                        
         BE    ICRTLP              ANOTHER ELEMENT FOUND                        
*                                                                               
ICRTDN   DS    0H                                                               
*                                                                               
         OC    ICRTELA,ICRTELA     WAS THERE A CANDIDATE                        
         BNZ   ICRTFD              YES                                          
*                                                                               
         CLI   ICRTMED,C'N'        FINISHED IF NOT NEWSPAPER                    
         BNE   ICRTX                                                            
*                                                                               
         CP    ICRTCNTR,=P'0'      IF SECOND TIME AROUND                        
         BH    ICRTX                  DONE                                      
*                                                                               
         AP    ICRTCNTR,=P'1'      BUMP ITERATION COUNTER                       
         MVC   ICRTSPC,SPACES      AND  SET TO LOOK FOR SPACES                  
*                                                                               
         B     ICRT05              LOOP THRU                      L04           
*                                                                               
ICRTFD   DS    0H                                                               
*                                                                               
         EJECT                                                                  
***********************************************************************         
*                                                                     *         
*        PROCESS THIS ELEMENT                                         *         
*                                                                     *         
***********************************************************************         
         SPACE 2                                                                
         L     R6,ICRTELA          POINT TO FOUND CONTRACT RATE ELM             
*                                                                               
*        LEVEL                                                                  
*                                                                               
ICRTLV   DS    0H                                                               
*                                                                               
         CLI   GLARGS+1,C'V'   IF PROCESSING LEVEL                              
         BNE   ICRTLVN                                                          
*                                                                               
         ZAP   0(8,R2),PRBLEVEL           RETURN LEVEL                          
         ZAP   8(8,R2),=P'1'              INTERNAL COUNT                        
         ZAP   16(8,R2),=P'0'             INITALIZE LEVEL DESCRIPTION           
***                                                                             
** MUST ALIGN TO CONFORM TO UNITS SINCE THIS WILL USED AS PART OF               
**         CALCULATION( UNITS-LEVEL)                                            
**                                                                              
         LA    RF,ICRTLVT          POINT TO LEVEL DATA TABLE                    
*                                                                               
ICRTTLP DS     0H                                                               
*                                                                               
         CLI   0(RF),X'FF'            EOT                                       
         BE    ICRTX               GIVE UP                                      
*                                                                               
         CLC   0(1,RF),PRBLIND     MATCH INDICATOR TO TABLE                     
         BE    ICRTTDN                                                          
*                                                                               
ICRTTCN DS     0H                                                               
*                                                                               
         LA    RF,5(RF)            BUMP TO NEXT TABLE ENTRY                     
         B     ICRTTLP                                                          
*                                                                               
ICRTTDN DS     0H                                                               
*                                                                               
         MP    0(8,R2),2(3,RF)     RETURN LEVEL TIMES FACTOR                    
         ZAP   16(8,R2),1(1,RF)       LEVEL INDICATOR                           
         ZAP   8(8,R2),=P'1'          INTERNAL COUNT                            
         TM    PRBIND,X'01'        IF A FLAT RATE                               
         BNO   *+10                                                             
         AP    8(8,R2),=P'100000'     SET INDICATOR                             
         B     ICRTX                                                            
*                                                                               
*        LEVEL TABLE                                                            
*              BYTE 1 = LEVEL TYPE ID                                           
*              BYTE 2 = INTERNAL LEVEL TYPE ID                                  
*              BYTE 3 = CONVERSION FACTOR                                       
*                                                                               
ICRTLVT DC     C'$',PL1'1',PL3'1000'   DOLLARS                                  
         DC    C'U',PL1'2',PL3'1000'   UNITS                                    
         DC    C'I',PL1'3',PL3'1000'     INCHES                                 
         DC    C'L',PL1'4',PL3'1000'     LINES                                  
         DC    C'S',PL1'5',PL3'1000'   SPECIAL                                  
         DC    C'O',PL1'6',PL3'1000'   OPEN                                     
         DC    C'X',PL1'7',PL3'1000'   TIMES                                    
         DC    C'P',PL1'8',PL3'1000'   PAGES                                    
         DC    X'FFFF'                                                          
*                                                                               
ICRTLVN DS     0H                                                               
*                                                                               
         EJECT                                                                  
*                                                                               
*        CONTRACT RATES                                                         
*                                                                               
ICRTRT   DS    0H                                                               
*                                                                               
         CLI   GLARGS+1,C'R'    IF PROCESSING RATE                              
         BNE   ICRTRTN                                                          
*                                                                               
         ZAP   DUB,PRBRATE         GET RATE                                     
         ZAP   ICRTCLTR,PRBRATE    ACCUMULATE                                   
         ZAP   0(8,R2),DUB         RETURN RATE                                  
         ZAP   8(8,R2),=P'1'              COUNT                                 
         ZAP   16(8,R2),=P'0'             INIT TYPE AS GROSS                    
*                                                                               
         TM    PRBIND,X'10'        TEST FOR NET RATE                            
         BNO   *+14                                                             
         ZAP   16(8,R2),=P'2'         FOR NET                                   
         B     ICRTRT1                                                          
*                                                                               
         TM    PRBIND,X'04'        TEST FOR COMMISSION RATE                     
         BNO   *+14                                                             
         ZAP   16(8,R2),=P'1'      C  FOR COMMISS                               
         B     ICRTRT1                                                          
*                                                                               
         TM    PRBIND,X'02'        TEST FOR S RATE                              
         BNO   ICRTRT1                                                          
*                                                                               
         ZAP   16(8,R2),=P'3'      S  FOR S RATE                                
*                                                                               
ICRTRT1 DS     0H                                                               
*                                                                               
         TM    PRBIND,X'20'      IF UNIT RATE                                   
         BNO   ICRTRT2                                                          
*                                                                               
         CLI   ICRTMED,C'N'      AND NEWSPAPER                                  
         BNE   ICRTRT2                                                          
*                                                                               
         AP    16(8,R2),=P'1000'   SET UNIT RATE INDICATOR                      
*                                                                               
         TM    PRBIND,X'08'            IF INCH RATE                             
         BO    *+12                                                             
         CLI   PRBLIND,C'I'                                                     
         BNE   *+10                                                             
         AP    16(8,R2),=P'100'         SET INDICATOR                           
*                                                                               
         B     ICRTRT3                                                          
*                                                                               
ICRTRT2 DS     0H                                                               
*                                  ELSE                                         
         CLI   PRBLIND,C'L'               MUST BE LINE RATE                     
         BE    ICRTRT3                                                          
*                                                                               
         CLI   PRBLIND,C'I'               OR INCH RATE                          
         BNE   ICRTRTX                                                          
*                                                                               
*        ALL RATES ASSUMED TO BE 2 DECIMALS ON EXIT                             
*                                                                               
ICRTRT3 DS     0H                                                               
*                                                                               
         TM    PRBIND,X'40'      IF TOTAL RATE THEN TWO DECIMALS                
         BO    *+10                   ALREADY                                   
         AP    16(8,R2),=P'10'     ELSE PREPARE FOR ROUNDING                    
*                                   (ALL OTHER RATES ARE 3 DECIMALS)            
ICRTRTX DS     0H                                                               
*                                                                               
         CLI   GLARGS+2,C'2'       SKIP UNLESS CD TO BE SUBTRACTED              
         BNE   ICRTCDX                                                          
*                                                                               
*        NOW FIND CASH DISCOUNT RATE                                            
*                                                                               
*        FIRST CHECK CONTRACT FOR PCATELEM                                      
*                                                                               
         ZAP   ICLVCD,=P'0'      INIT CASH DISCOUNT                             
         ZAP   ICLVAC,=P'15000'  INIT AGENCY COMMISSION                         
*                                                                               
         MVI   ELCODE,PCATELQ      SET FOR CONTRACT ATTENTION ELEMENT           
         L     R6,AIO2             POINT TO CONTRACT RECORD                     
*                                                                               
         BAS   RE,GETEL            LOOK FOR ELEMENT                             
         BNE   ICRTCD5             NOT FOUND                                    
*                                                                               
         USING PCATELD,R6                                                       
*                                                                               
         OC    PCATPCT,PCATPCT                                                  
         BZ    ICRTCD5             MEANS CD NOT SPECIFIED IN CONTRACT           
*                                                                               
         CLC   PCATPCT,=X'FFFF'    MEANS 0.0 CD                                 
         BNE   *+14                                                             
         ZAP   ICLVCD,=P'99999'    SET MAX VALUE                                
         B     ICRTCD5                                                          
*                                                                               
         SR    R0,R0                                                            
         ICM   R0,3,PCATPCT        CONVERT CD TO PACKED FORMAT AND SAVE         
         CVD   R0,DUB                                                           
         ZAP   ICLVCD,DUB                                                       
*                                                                               
ICRTCD5 EQU    *                   CHECK PUB FOR AC & CD                        
*                                                                               
         MVI   ELCODE,PUBGENQ      SEARCH PUB RECORD FOR                        
         L     R6,AIO3               GENERAL INFO ELEMENT                       
         BAS   RE,GETEL                                                         
         BE    ICRTCD6                                                          
*                                                                               
         B     ICRTCD7                                                          
*                                                                               
*        FOLLOWING BYPASSED BECAUSE OUTDOOR AC NO LONGER                        
*              DEFAULTS TO 16.667%                                              
*              IT DEFAULTS TO 15% LIKE ALL OTHER MEDIA                          
*                                                                               
         L     RF,AIO3             POINT TO PUB RECORD                          
         CLI   PUBKMED-PUBREC(RF),C'O'  OUTDOOR                                 
         BNE   *+10                                                             
         ZAP   ICLVAC,=P'16667'    HAS DEFAULT AC OF 16.667%                    
*                                                                               
         B     ICRTCD7                                                          
*                                                                               
ICRTCD6 DS     0H                  PUB PROD ELEMENT FOUND                       
*                                                                               
         USING PUBGEND,R6          ESTABLISH ELEMENT                            
*                                                                               
         CP    ICLVCD,=P'0'        IF CD STILL UNKNOWN                          
         BNE   *+10                                                             
         ZAP   ICLVCD,PUBCD        USE PUB CD - 1 DECIMAL PLACE                 
*                                                                               
         ZAP   ICLVAC,PUBAC        USE PUB AC - 3 DECIMAL PLACES                
*                                                                               
ICRTCD7 DS     0H                                                               
*                                                                               
         CP    ICLVCD,=P'99999'    CHECK FOR 0.0 CD                             
         BNE   *+10                                                             
         ZAP   ICLVCD,=P'0'        USE PUB CD - 1 DECIMAL PLACE                 
*                                                                               
*        FIND CONTRACT NET VALUE                                                
*                                                                               
         MVC   WORK(32),0(R2)                                                   
*                                                                               
         ZAP   ICLVRATE,0(8,R2)    COPY CURRENT RATE                            
*                                                                               
         ZAP   DUB(8),ICLVRATE     CONTRACT RATE                                
         MP    DUB(8),ICLVAC       GET AGENCY COMMISSION                        
         SRP   DUB(8),64-5,5       ROUND TO 2 DECIMALS                          
*                                                                               
         SP    ICLVRATE,DUB(8)     NET RATE                                     
*                                                                               
*        SUBTRACT CD FROM NET RATE                                              
*                                                                               
         ZAP   DUB(8),ICLVRATE     CONTRACT NET RATE                            
         MP    DUB(8),ICLVCD       RATE * CD = CD                               
         SRP   DUB(8),64-3,5       ROUND TO 2 DECIMALS                          
*                                                                               
         SP    0(8,R2),DUB(8)      NET RATE LESS CD                             
*                                                                               
ICRTCDX DS     0H                                                               
*                                                                               
         B     ICRTX                                                            
*                                                                               
ICLVRATE DS    PL8                 RATE SAVEAREA                                
ICLVCD   DS    PL3                 CASH DISCOUNT PERCENT TO 1 DEC               
ICLVAC   DS    PL3                 AGENCY COMMISSION TO 3 DEC                   
*                                                                               
ICRTRTN DS     0H                                                               
*                                                                               
         EJECT                                                                  
*                                                                               
*        REPORTING EFFECTIVE DATE                                               
*                                                                               
ICRTED   DS    0H                                                               
*                                                                               
         USING PRBELEM,R6          ESTABLISH CONTRACT RATE ELEMENT              
*                                                                               
         CLI   GLARGS+1,C'D'       IF EFFECTIVE DATE PROCESSING                 
         BNE   ICRTEDN                                                          
*                                                                               
         MVC   0(3,R2),PRBDATE        RETURN EFFECTIVE DATE                     
         B     ICRTX                                                            
*                                                                               
ICRTEDN DS     0H                                                               
*                                                                               
         EJECT                                                                  
*                                                                               
*        REPORTING UNITS                                                        
*                                                                               
ICRTUN   DS    0H                                                               
*                                                                               
         CLI   GLARGS+1,C'U'     IF PROCESSING UNITS                            
         BNE   ICRTUNN                                                          
*                                                                               
*   DETERMINE WHAT TO ADD TO ACCUMULATOR                                        
*                                                                               
         CLI   PRBLIND,C'$'  IF CONTRACT IN TERMS OF $'S                        
         BNE   ICRTUN2                                            BUG09         
*                                                                               
         CLI   GLARGS+2,C'P' AND TO USE CU= OVERRIDE INSTEAD OF DOLLARS         
         BE    ICRTAUN             GO ADD UNITS                                 
*                                                                               
         TM    PBQREAD,PBQRDBUY    ELSE IF WE DIDN'T READ BUYS                  
         BO    ICRTUN0                                                          
*                                                                               
         ZAP   ICRTDUB(8),PRBRATE        RETURN RATE AS GROSS $'S               
*                                                                               
         CLI   PRBLIND,C'L'              LINE RATE IS TO 5 DECIMALS             
         BE    *+8                                                              
         CLI   PRBLIND,C'I'              SAME FOR INCH RATE                     
         BNE   ICRTUNA                                                          
*                                                                               
         TM    PRBIND,X'40'              UNLESS TOTAL RATE                      
         BO    ICRTUNA                                                          
*                                                                               
         SRP   ICRTDUB(8),64-2,5   ROUND DOWN TO 3 DECIMALS                     
         B     ICRTUN1                                                          
*                                                                               
ICRTUNA DS     0H                                                               
*                                                                               
         SRP   ICRTDUB(8),1,0      ROUND 2 DECIMALS TO 3                        
         B     ICRTUN1                                                          
*                                                                               
ICRTUN0 DS     0H                                                               
*                                                                               
         L      RF,PBGRS       ELSE GET BUY DOLLARS                             
         MH     RF,=H'10'       (TO ALIGN DOLLARS WITH UNITS)                   
         CVD    RF,ICRTDUB                                                      
*                                                                               
ICRTUN1 DS     0H                                                               
*                                                                               
         ZAP    0(8,R2),ICRTDUB(8) RETURN DOLLARS                               
         ZAP    ICRTCLTR,ICRTDUB(8) CUMULAT $'S FOR POSSIBLE DIFFERENCE         
*                                                                               
         CLI    GLARGS+2,C'M'        PUT IN POSSIBLE MASK                       
         BNE    *+8                                                             
         MVI    0(R2),C'$'                                                      
*                                                                               
         B      ICRTUNX                                                         
*                                                                               
ICRTUN2 DS      0H                                                              
*                                                                               
         CLI   PBMED,C'N'         SKIP IF NOT DOING NEWSPAPER                   
         BNE   ICRTUN5                                                          
*                                                                               
         TM    PBQREAD,PBQRDBUY   SKIP IF WE DIDN'T READ BUYS                   
         BNO   ICRTU29                                                          
*                                                                               
         OC    PBDCU,PBDCU        SKIP IF NO CONTRACT UNITS OVERRIDE            
         BZ    ICRTU29                                                          
*                                                                               
         CLC   PBDCU,=X'000001'       IF EQUAL TO .0001 BYPASS                  
         BE    ICRTAUZ                   GO REPORT AS 0 UNITS                   
*                                                                               
         SR    RF,RF                  ELSE CONVERT TO PACKED FORMAT             
         ICM   RF,7,PBDCU                                                       
         CVD   RF,ICRTDUB                                                       
         SRP   ICRTDUB(8),64-1,5           ROUND TO 3 DECIMALS                  
         ZAP   0(8,R2),ICRTDUB(8)                                               
         ZAP   ICRTCLTR,ICRTDUB(8)         FOR DIFFERENCE                       
*                                                                               
         B     ICRTAUX                     UNITS FOUND                          
*                                                                               
ICRTU29 DS      0H                                                              
*                                                                               
         CLI    PRBLIND,C'I'               IF RATE FOR INCHES                   
         BE     *+12                                                            
         CLI    PRBLIND,C'L'               OR LINES                             
         BNE    ICRTUN5                                                         
*                                                                               
         TM    PBQREAD,PBQRDBUY   IF WE DIDN'T READ BUYS                        
         BO    ICRTUN3                                                          
*                                                                               
         ZAP   0(8,R2),PRBLEVEL      RETURN LEVEL AS UNITS                      
         SRP   0(8,R2),3,0         ADD 3 DECIMAL PLACES                         
         B     ICRTUN4                                                          
*                                                                               
ICRTUN3 DS      0H                                                              
*                                                                               
         ZAP   0(8,R2),PBDUNITS    ELSE RETURN BUY UNITS                        
*                                                                               
         SRP   0(8,R2),1,0         ADD DECIMAL PLACE                            
         CLI   PBDUIND,X'89'       ALREADY AT TWO DECIMALS                      
         BE    *+10                                                             
         SRP   0(8,R2),2,0         ELSE ADD 2 MORE DECIMAL PLACES               
*                                                                               
ICRTUN4 DS      0H                                                              
*                                                                               
*-------                                                                        
* DETERMINE IF BUY IS FOR INCH/LINES AND CONTRACT IS FOR INCH/LINE              
*   IF NOT SIMILAR MUST CONVERT TO CORRECT UNIT                                 
*------                                                                         
         TM    PBQREAD,PBQRDBUY    IF NOT READING BUYS                          
         BNO   ICRTUNX             THEN NO INCOMPATABILITIES                    
*                                                                               
         CLI   PRBLIND,C'I'       INCH CONTRACT                                 
         BNE   ICRTUNL                                                          
*                                                                               
         CLI   PBDUIND,C'L'       IS BUY IN LINES                               
         BNE   ICRTUNX             NO - OK                                      
*                                                                               
         ZAP   WORK(8),0(8,R2)     CONVERT LINES TO INCHES                      
         DP    WORK(8),=P'14'                                                   
         ZAP   0(8,R2),WORK(6)                                                  
         B     ICRTUNX                                                          
*                                                                               
ICRTUNL DS     0H                  LINE CONTRACT                                
*                                                                               
         CLI   PBDUIND,C'L'        AND LINE BUY                                 
         BE    *+10                OKAY                                         
         MP    0(8,R2),=P'14'      ELSE CONVERT INCHES TO LINES                 
*                                                                               
         B     ICRTUNX                                                          
*                                                                               
ICRTUNX DS     0H                                                               
*                                                                               
         B     ICRTTST                                                          
*                                                                               
ICRTUN5 CLI    PRBLIND,C'U'              UNITS                                  
         BE    *+8                                                              
         CLI   PRBLIND,C'S'              SPECIAL                                
         BE    *+8                                                              
         CLI   PRBLIND,C'O'              OPEN                                   
         BE    *+8                                                              
         CLI   PRBLIND,C'X'              TIMES                                  
         BE    *+8                                                              
         CLI   PRBLIND,C'P'              PAGES                                  
         BE    ICRTAUN                                                          
*                                                                               
         CLI   PRBLIND,C'I'        IF LINES OR INCHES                           
         BE    *+8                                                              
         CLI   PRBLIND,C'L'                                                     
         BNE   ICRTTST                                                          
*                                                                               
         CLI   PBMED,C'N'          THEN MEDIA CAN'T BE NEWSPAPER                
         BE    ICRTTST                                                          
*                                                                               
*        ACCUMULATE UNITS                                                       
*                                                                               
ICRTAUN DS      0H                                                              
*                                                                               
         TM    PBQREAD,PBQRDBUY    IF NO BUYS READ, USE DEFAULT                 
         BNO   ICRTAU1                                                          
*                                                                               
         CLI   PBQADVSW,C'Y'       IF DOING ADVERTISER REPORT                   
         BNE   *+20                                                             
         SR    R0,R0                                                            
         L     RF,PBUTLA                                                        
         IC    R0,4(RF)               SAVE CURRENT FILES POINTER                
         MVC   4(1,RF),PAORSE         SET TO READ AOR FILES                     
*                                                                               
         GOTO1 =V(PPGETCU),ICRTDCB,AIO1,AIO2,DATAMGR    GET CU ENTRY            
*                                                                               
         CLI   PBQADVSW,C'Y'       IF DOING ADVERTISER REPORT                   
         BNE   *+12                                                             
         L     RF,PBUTLA                                                        
         STC   R0,4(RF)               RESTORE CURRENT FILES POINTER             
*                                                                               
         MVC   KEY,IOKEYSVE      RESTORE POINTER                                
         OI    DMINBTS,8         PASS BACK DELETED RECORDS                      
         GOTO1 HIGH                                                             
*                                                                               
         LA    R1,ICRTDCB          POINT TO RETURNED DATA                       
*                                                                               
         CLI   0(R1),X'FF'         IF ERROR                                     
         BE    ICRTAUZ                DEFAULT TO ZERO                           
*                                                                               
         CLC   4(4,R1),=X'00000001'  IF EQUAL TO .0001 BYPASS BUY               
         BE    ICRTAUX                  NOT USED TO MEET CONTRACT               
*                                                                               
         OC    4(4,R1),4(R1)   IF NOTHING IN PBDCU ASSUME ONE UNIT              
         BNZ   ICRTAU2            (CU=CONTRACT UNITS OVERRIDE)                  
*                                                                               
ICRTAU1 DS     0H                                                               
*                                                                               
         CLI   GLARGS+3,C'0'      IF NO DEFAULT ALLOWED                         
         BNE   *+12                                                             
         MVI   0(R2),X'FF'           INDICATE NO ENTRY                          
         B     *+10                                                             
         ZAP   0(8,R2),=P'1000'   ELSE DEFAULT TO 1                             
*                                                                               
         B     ICRTAUX                                                          
*                                                                               
ICRTAU2 DS     0H                                                               
*                                                                               
         SR    RF,RF              CONVERT TO PACKED FORMAT                      
         ICM   RF,7,5(R1)                                                       
         SR    RE,RE                                                            
         D     RE,=F'10'          REDUCE TO 3 DECIMALS                          
         CVD   RF,ICRTDUB                                                       
         ZAP   0(8,R2),ICRTDUB(8)                                               
         ZAP   ICRTCLTR,ICRTDUB(8)     FOR DIFFERENCE                           
         B     ICRTAUX                                                          
*                                                                               
ICRTAUZ DS     0H                                                               
*                                                                               
         CLI   GLARGS+3,C'0'      IF NO DEFAULT ALLOWED                         
         BNE   *+8                                                              
         MVI   0(R2),X'FF'          INDICATE NO ENTRY & TO PRINT BLANKS         
*                                                                               
ICRTAUX DS     0H                                                               
*                                                                               
ICRTTST  DS     0H                 CHECK OUT LIVE/TEST BUY REPORTING            
*                                                                               
         TM    GLARGS+5,X'08'      IF REPORTING TEST BUYS ONLY                  
         BNO   ICRTTST1                                                         
         CLI   PBDBFD,C'T'            AND WE HAVE A LIVE BUY                    
         BE    ICRTTST5                                                         
         ZAP   0(8,R2),=P'0'              CLEAR RESULTS                         
         B     ICRTTSTX                                                         
*                                                                               
ICRTTST1 TM    GLARGS+5,X'04'      IF REPORTING LIVE BUYS ONLY                  
         BNO   ICRTTST5                                                         
         CLI   PBDBFD,C'T'            AND WE HAVE A TEST BUY                    
         BNE   ICRTTST5                                                         
         ZAP   0(8,R2),=P'0'              CLEAR RESULTS                         
         B     ICRTTSTX                                                         
*                                                                               
ICRTTST5 TM    PBQTOTTY,TOTTYPN   DONE IF NORMAL TOTALS WANTED                  
         BO    ICRTTSTX                                                         
*  TEST TOTALS REQUESTED                                                        
         CLI   PBDBFD,C'T'        TEST BUY                                      
         BNE   ICRTTSTX                                                         
         MVC   DMCB(1),0(R2)      SAVE POSSIBLE MASK                            
         ZAP   8(8,R2),0(8,R2)                                                  
         MVC   0(1,R2),DMCB       RESTORE POSSIBLE MASK                         
         B     ICRTTSTX                                                         
*                                                                               
ICRTTSTX DS    0H                                                               
*                                                                               
         B      ICRTX                                                           
*                                                                               
ICRTUNN DS     0H                                                               
*                                                                               
*        REPORTING PERCENT DISCOUNT                                             
*                                                                               
ICRTPC   DS    0H                                                               
*                                                                               
         CLI   GLARGS+1,C'P'     PROCESS PCT                                    
         BNE   ICRTPCN                                                          
*                                                                               
         ZAP   0(8,R2),PRBPCT                                                   
         B     ICRTX                                                            
*                                                                               
ICRTPCN DS     0H                                                               
*                                                                               
ICRTX    DS    0H                                                               
*                                                                               
         L     R1,ICRPELA          RETURN CONTRACT RATE ADDRESS                 
         MVC   0(4,R1),ICRTELA                                                  
*                                                                               
         L     R1,ICRPCLTA         RETURN RATE FOR COLLECTION                   
         ZAP   0(8,R1),ICRTCLTR                                                 
*                                                                               
         XIT1                                                                   
*                                                                               
         LTORG                                                                  
*                                                                               
         DS    CL256               FOR NEW Z/OS PROCESSOR                       
ICRPRMS  DS    0D                  ICRATE PARAMETER LIST                        
ICRPELA  DS    A                   A(A(CONTRACT RATE ELEMENT))                  
ICRPCLTA DS    A                   A(RATE COLLECTION AREA)                      
ICRPRMSL EQU   *-ICRPRMS           LENGTH OF PARAMETER LIST                     
*                                                                               
ICRTDUB  DS    D                   DOUBLE WORD WORK AREA                        
ICRTDCB  DS    6A                  ROUTINE ICRTDUB                              
ICRTPRMA DS    A                   A(PARAMETER LIST)                            
ICRTELA  DS    A                   ELEMENT ADDRESS SAVEAREA                     
ICRTSPC  DS    XL(L'PBDSPACE)      SPACE DESCRIPTION WORKAREA                   
ICRTLIND DS    XL1                 LEVEL INDICATOR   WORKAREA                   
ICRTMED  DS    XL1                 MEDIA             WORKAREA                   
ICRTRCD  DS    CL3                 RATE CODE         WORKAREA                   
ICRTCLTR DS    PL8                 RATE COLLECTION AREA                         
ICRTCNTR DS    PL1                 ITERATION COUNTER                            
*                                                                               
         GETEL (R6),33,ELCODE      FIND ELEMENT ROUTINE                         
*        ========  *                                                            
         DROP  R6                                                               
*        ========  *                                                            
R6POINT  DS    F                                                                
*                                                                               
         TITLE   'PRWRICRT DSECTS / STORAGE'                                    
* PRWRIWORKD                                                                    
         PRINT   OFF                                                            
       ++INCLUDE PRWRIWORKD                                                     
         PRINT ON                                                               
* DRGLOBAL                                                                      
         PRINT   OFF                                                            
       ++INCLUDE DRGLOBAL                                                       
         PRINT   ON                                                             
* DRIVETABLE                                                                    
         PRINT   OFF                                                            
       ++INCLUDE DRIVETABLE                                                     
         PRINT   ON                                                             
* DRINTRECD2                                                                    
         PRINT   OFF                                                            
       ++INCLUDE DRINTRECD2                                                     
         PRINT   ON                                                             
* DDSPOOLD                                                                      
         PRINT   OFF                                                            
       ++INCLUDE DDSPOOLD                                                       
         PRINT   ON                                                             
* DDSPLWORKD                                                                    
         PRINT OFF                                                              
       ++INCLUDE DDSPLWORKD                                                     
         PRINT   ON                                                             
* PRGENFILE                                                                     
         PRINT OFF                                                              
       ++INCLUDE PRGENFILE                                                      
         PRINT   ON                                                             
* PRGLOBEQUS                                                                    
         PRINT OFF                                                              
       ++INCLUDE PRGLOBEQUS                                                     
         PRINT   ON                                                             
* DDCOMFACSD                                                                    
         PRINT OFF                                                              
       ++INCLUDE DDCOMFACSD                                                     
         PRINT   ON                                                             
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'026PRWRICRT  03/20/06'                                      
         END                                                                    
