*          DATA SET R00310MH   AT LEVEL 017 AS OF 05/01/02                      
*CATALP R00310                                                                  
         TITLE 'R00310 - OFFLINE TAPE/PRINTER/READER'                           
R00310   CSECT                                                                  
         ENTRY RDTAPE                                                           
         ENTRY WRTAPE                                                           
         ENTRY CLTAPE                                                           
         ENTRY WTMTAPE                                                          
         ENTRY TRNTAPE                                                          
         NMOD1 0,R00310                                                         
         LR    2,1                 SAVE PARAM LIST POINTER                      
         L     1,12(2)             SET DTF POINTER                              
         LA    1,0(1)              CLEAR HOB                                    
         XC    8(2,2),8(2)         CLEAR ERROR BYTES                            
         L     15,0(2)             GET ROUTINE ADDRESS                          
         BR    15                                                               
R00310X  XMOD  1                                                                
         EJECT                                                                  
RDTAPE   BAS   9,TESTPR            TEST FOR READER OR PRINTER                   
         BAS   9,TESTOPEN          TEST IF FILE OPEN                            
         BAS   9,GETADDR           GET ADDRESS OF WORKAREA                      
         GET   (1),(0)                                                          
         LR    3,0                                                              
         MVC   WRFULL(2),0(3)      LENGTH - 4 TO USER                           
         LH    0,WRFULL                                                         
         SH    0,=H'4'                                                          
         STH   0,10(2)                                                          
         MVC   0(4,3),SAVE         RESTORE SAVED BYTES                          
         BAS   9,BUMPCNT           BUMP RECORD COUNTER                          
         B     R00310X                                                          
         SPACE 2                                                                
WRTAPE   BAS   9,TESTPR                                                         
         BAS   9,TESTOPEN                                                       
         C     R1,=A(PRTTAPE)      TEST PRTTAPE                                 
         BNE   WRTAP2                                                           
*                                                                               
         L     R9,4(R2)            GET DATA ADDRESS                             
         MVI   SVLINE,X'09'        SET PRINT/SPACE 1                            
         CLI   0(R9),X'40'                                                      
         BE    WRTAP1                                                           
         MVI   SVLINE,X'89'        SET PRINT/SKIP TO CH1                        
         CLI   0(R9),X'C1'                                                      
         BE    WRTAP1                                                           
         MVI   SVLINE,X'11'        SET PRINT/SPACE 2                            
         CLI   0(R9),X'41'                                                      
         BE    WRTAP1                                                           
         MVI   SVLINE,X'19'        ELSE SET PRINT/SPACE 3                       
*                                                                               
WRTAP1   LTR   R5,R5               TEST FIRST TIME                              
         BNE   *+8                 NO                                           
         MVI   SVLINE,X'8B'        FIRST TIME DO SKIP TO CH1 IMMED              
         LA    R0,SVLINE                                                        
         PUT   (1),(0)                                                          
*                                                                               
         MVC   SVLINE+1(132),1(R9)   SAVE THIS PRINT LINE                       
         BAS   R9,BUMPCNT                                                       
         B     R00310X                                                          
         EJECT                                                                  
*                                                                               
WRTAP2   EQU   *                                                                
         BAS   9,GETADDR           GET ADDRESS OF WORK AREA                     
         LH    9,10(2)             LENGTH + 4 TO RECORD                         
         AH    9,=H'4'                                                          
         STH   9,WRFULL                                                         
         MVC   0(4,4),WRFULL                                                    
WRTAP4   PUT   (1),(0)                                                          
         MVC   0(4,4),SAVE         RESTORE SAVED BYTES                          
         BAS   9,BUMPCNT                                                        
         B     R00310X                                                          
WRFULL   DC    F'0'                                                             
         SPACE 2                                                                
CLTAPE   LR    R4,R1               DTF ADDRESS                                  
         SH    R4,=H'4'            COUNTER ADDRESS                              
         L     R5,0(R4)                                                         
         LTR   R5,R5                                                            
         BZ    R00310X                                                          
         CLOSE (1)                                                              
         B     R00310X                                                          
         SPACE 2                                                                
WTMTAPE  CNTRL (1),WTM                                                          
         B     R00310X                                                          
*                                                                               
TRNTAPE  TRUNC (1)                 WRITE SHORT BLOCK                            
         B     R00310X                                                          
         EJECT                                                                  
*                                  CHECK FOR READER OR PRINTER COMMAND          
TESTPR   L     8,=A(PRINTER)                                                    
         C     8,12(2)                                                          
         BC    8,PRINT                                                          
         L     8,=A(READER)                                                     
         C     8,12(2)                                                          
         BC    8,RDCARD                                                         
         L     8,=A(PUNCH)                                                      
         C     8,12(2)                                                          
         BE    WRCARD                                                           
         BR    9                                                                
*                                                                               
*                                  TEST IF FILE OPEN YET                        
TESTOPEN LR    4,1                 DTF ADDR                                     
         SH    4,=H'4'             COUNTER ADDR                                 
         L     5,0(4)                                                           
         LTR   5,5                                                              
         BCR   7,9                                                              
         OPEN  (1)                                                              
         BR    9                                                                
*                                  POINT USER I/O ADDRESS 4 BYTES BACK          
GETADDR  L     4,4(2)              WORK AREA ADDRESS                            
         SH    4,=H'4'                                                          
         MVC   SAVE,0(4)           SAVE 4 BYTES                                 
         LR    0,4                 ADDRESS TO R0                                
         BR    9                                                                
*                                                                               
*                                  BUMP RECORD COUNTER                          
BUMPCNT  LR    4,1                 DTF ADDRESS                                  
         SH    4,=H'4'                                                          
         L     5,0(4)                                                           
         LA    5,1(5)                                                           
         ST    5,0(4)                                                           
         BR    9                                                                
         EJECT                                                                  
*                                  SPECIAL RTNS FOR READER/PRINTER              
RDCARD   MVC   RDRCCW+1(3),5(2)     SET I/O ADDRESS                             
         BAS   9,EXCP                                                           
         BAS   9,BUMPCNT                                                        
         B     R00310X                                                          
*                                                                               
WRCARD   MVC   PCHCCW+1(3),5(2)                                                 
         BAS   9,EXCP                                                           
         BAS   9,BUMPCNT                                                        
         B     R00310X                                                          
         SPACE 2                                                                
*                                  SUPPORT PRINT AFTER SPACING                  
PRINT    L     5,4(2)              GET LINE ADDRESS                             
         TM    0(5),X'80'          TEST FOR SKIP TO CHANNEL                     
         BC    8,PRTSP             NO- TEST IF SPACING REQD                     
         IC    4,0(5)                                                           
         SLL   4,28                                                             
         SRL   4,28                                                             
         LA    4,SKTABL-1(4)                                                    
         MVC   CNTLCCW(1),0(4)     MOVE OP CODE                                 
         LA    4,CNTLCCW                                                        
         ST    4,PRINTER+8                                                      
         BAS   9,EXCP                                                           
         CLI   CNTLCCW,X'8B'       TEST SKIP TO CHANNEL 1                       
         BC    7,*+8                                                            
         BAS   9,BUMPCNT                                                        
         B     PRTLN                                                            
SKTABL   DC    X'8B939BA3ABB3BBC3CBD3DBE3'                                      
         SPACE 2                                                                
PRTSP    TM    0(5),X'03'          TEST IF SPACING CONTROL NEEDED               
         BC    8,PRTLN                                                          
         IC    4,0(5)                                                           
         SLL   4,28                                                             
         SRL   4,28                                                             
         LA    4,SPTABL-1(4)                                                    
         MVC   CNTLCCW(1),0(4)                                                  
         LA    4,CNTLCCW                                                        
         ST    4,PRINTER+8                                                      
         BAS   9,EXCP                                                           
         B     PRTLN                                                            
SPTABL   DC    X'0B131B'                                                        
         SPACE 2                                                                
PRTLN    L     3,4(2)                                                           
         LA    3,1(3)                                                           
         ST    3,PRTCCW                                                         
         OC    00(33,3),BLANKS     'OR' PRINT LINE WITH BLANKS                  
         OC    66(33,3),BLANKS                                                  
         OC    33(33,3),BLANKS                                                  
         OC    99(33,3),BLANKS                                                  
         MVI   PRTCCW,9                                                         
         LA    4,PRTCCW                                                         
         ST    4,PRINTER+8                                                      
         BAS   9,EXCP                                                           
         B     R00310X                                                          
BLANKS   DC    33C' '                                                           
         EJECT                                                                  
EXCP     EXCP  (1)                                                              
         WAIT  (1)                                                              
         TM    2(1),X'40'          TEST UNIT EXCEPTION FOR EOF                  
         BCR   8,9                                                              
SETEOF   MVI   9(2),X'04'          SET EOF                                      
         B     R00310X                                                          
         SPACE 2                                                                
         ENTRY PRINTER                                                          
         CNOP  4,8                                                              
         DC    F'0'                                                             
PRINTER  CCB   SYSLST,PRTCCW,X'0200'                                            
         SPACE 2                                                                
         ENTRY READER                                                           
         CNOP  4,8                                                              
         DC    F'0'                                                             
READER   CCB   SYSRDR,RDRCCW,X'0200'                                            
         SPACE 2                                                                
         ENTRY PUNCH                                                            
         CNOP  4,8                                                              
         DC    F'0'                                                             
PUNCH    CCB   SYSPCH,PCHCCW,X'0200'                                            
*                                                                               
PRTCCW   CCW   9,*,X'20',132                                                    
CNTLCCW  CCW   00,*,X'20',1                                                     
PCHCCW   CCW   X'01',*,X'20',80    EJECT & SS2                                  
RDRCCW   CCW   X'02',*,X'20',80                                                 
*                                                                               
*                                                                               
SAVE     DC    F'0'                                                             
SVLINE   DS    CL133                                                            
         LTORG                                                                  
         EJECT                                                                  
         ENTRY PRTTAPE                                                          
         CNOP  4,8                                                              
         DC    F'0'                                                             
PRTTAPE  DTFMT BLKSIZE=1330,DEVADDR=SYS010,FILABL=NO,IOAREA1=PRTAREA,  X        
               RECFORM=FIXBLK,RECSIZE=133,REWIND=UNLOAD,TYPEFLE=OUTPUT,X        
               TPMARK=NO,WORKA=YES                                              
*                                                                               
PRTAREA  DS    1330C                                                            
         EJECT                                                                  
         ENTRY REQTAPE                                                          
         CNOP  4,8                                                              
         DC    F'0'                                                             
REQTAPE  DTFMT BLKSIZE=88,DEVADDR=SYS012,FILABL=STD,IOAREA1=REQAREA,   X        
               RECFORM=VARBLK,TYPEFLE=INPUT,WORKA=YES,EOFADDR=SETEOF            
         EJECT                                                                  
         ENTRY IN1                                                              
         CNOP  4,8                                                              
         DC    F'0'                                                             
IN1      DTFMT BLKSIZE=2008,DEVADDR=SYS014,FILABL=STD,IOAREA1=IN1AREA, X        
               RECFORM=VARBLK,TYPEFLE=INPUT,WORKA=YES,EOFADDR=SETEOF            
         EJECT                                                                  
         ENTRY OUT1                                                             
         CNOP  4,8                                                              
         DC    F'0'                                                             
OUT1     DTFMT BLKSIZE=2008,DEVADDR=SYS016,FILABL=STD,IOAREA1=OUT1AREA,X        
               RECFORM=VARBLK,REWIND=UNLOAD,TYPEFLE=OUTPUT,WORKA=YES            
         EJECT                                                                  
         ENTRY OUT2                                                             
         CNOP  4,8                                                              
         DC    F'0'                                                             
OUT2     DTFMT BLKSIZE=2008,DEVADDR=SYS018,FILABL=STD,IOAREA1=OUT2AREA,X        
               RECFORM=VARBLK,REWIND=UNLOAD,TYPEFLE=OUTPUT,WORKA=YES            
         EJECT                                                                  
*********      NOTE THAT IN2 SHARES IO AREA WITH OUT2     **********            
         ENTRY IN2                                                              
         CNOP  4,8                                                              
         DC    F'0'                                                             
IN2      DTFMT BLKSIZE=2008,DEVADDR=SYS018,FILABL=STD,IOAREA1=OUT2AREA,X        
               RECFORM=VARBLK,TYPEFLE=INPUT,WORKA=YES,EOFADDR=SETEOF            
         EJECT                                                                  
REQAREA  DS    88C                                                              
IN1AREA  CSECT                                                                  
         DS    2008C                                                            
OUT1AREA CSECT                                                                  
         DS    2008C                                                            
OUT2AREA CSECT                                                                  
         DS    2008C                                                            
 END                                                                            
