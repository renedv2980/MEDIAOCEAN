*          DATA SET DDGETDAY   AT LEVEL 005 AS OF 04/23/02                      
*CATALP GETDAY                                                                  
*                                                                               
* XL1    SET X'80' TO RETURN NUMBER OF DAYS IN MONTH IN P2(1)                   
* XL1    RETURN DAY NUMBER 1 (MON) THRU 7 (SUN). 0=INVALID DATE                 
* AL3    A(INPUT DATE EBCDIC C'YYMMDD')                                         
*                                                                               
* XL1    RETURN DAYS IN MONTH IF P1(1)=X'80'                                    
* AL3    A(OUTPUT AREA - 3 BYTE ALPHA DAY OF WEEK)                              
*                                                                               
* THIS PROGRAM WILL VALIDATE AND CALCULATE THE DAY OF THE WEEK                  
* FOR ANY DATE AFTER MAR 1,1600                                                 
*                                                                               
* COMPUTE= D + I(2.6M - .2) - 2C + Y + I(C/4) + I(Y/4)                          
* WHERE -                                                                       
* D = DAY                                                                       
* M = MONTH (MAR=1,APR=2,...FEB=12)                                             
* C = HUNDREDS PART OF YEAR                                                     
* Y = YEAR, WITHOUT HUNDREDS, COUNTED FROM MAR TO FEB                           
* (I.E. IF MONTH = JAN OR FEB, Y = YEAR - 1)                                    
* I( ) = INTEGER PART OF ( )                                                    
*                                                                               
* DIVIDE THIS RESULT BY 7                                                       
* THE REMAINDER IS THE DAY OF THE WEEK, WITH SUNDAY=0                           
* OUTPUT IS CONVERTED SO MON=1,...SUN=7                                         
         TITLE 'DDGETDAY - GET DAY OF WEEK'                                     
         PRINT NOGEN                                                            
GETDAY   CSECT                                                                  
         NMOD1 DATX-DATD,DEGETDAY                                               
         USING DATD,RC                                                          
         LA    RE,*+6              SET 24-BIT MODE                              
         BSM   0,RE                                                             
         SPACE 1                                                                
         LM    R2,R3,0(R1)         R2=A(INPUT) R3=A(OUTPUT)                     
         STM   R0,R1,RSAV                                                       
         CTRY                      GET TERM LANG CODE                           
         STC   R0,LANG                                                          
         CLI   LANG,7              ADJUST IF OUT OF RANGE                       
         BNH   *+8                                                              
         MVI   LANG,0                                                           
         CLI   LANG,0                                                           
         BNE   *+10                                                             
         MVC   LANG,DEFLANG                                                     
         LM    R0,R1,RSAV                                                       
         MVC   DATE,0(R2)          EXTRACT INPUT DATE                           
         MVC   OPT1,0(R1)          SAVE 1ST BYTE OF PLIST                       
         MVI   0(R1),0             SET DATE INVALID FLAG                        
         MVC   0(3,R3),=3C' '      SPACE FILL OUTPUT                            
         SPACE 1                                                                
GD1      LA    R4,19               R4=CENTURY (DEFAULTS TO 19)                  
         CLI   DATE,C'9'                                                        
         BNH   GD1A                                                             
         LA    R4,20               SET CENTURY=20                               
         SR    R5,R5                                                            
         IC    R5,DATE                                                          
         SH    R5,=H'10'                                                        
         STC   R5,DATE                                                          
         B     GD2                                                              
GD1A     CLC   DATE(2),=C'27'      ASSUME C'00'-C'27' ARE 2000-2027             
         BH    GD2                                                              
         LA    R4,20                                                            
         SPACE 1                                                                
GD2      MVC   DUB(6),=C'000000'   VALIDATE INPUT DATE                          
         MVZ   DUB(6),DATE                                                      
         CLC   DUB(6),=C'000000'                                                
         BNE   GDEXT                                                            
         PACK  DUB,DATE+0(2)       R5=YEAR                                      
         CVB   R5,DUB                                                           
         PACK  DUB,DATE+2(2)       R6=MONTH                                     
         CVB   R6,DUB                                                           
         LTR   R6,R6                                                            
         BZ    GDEXT                                                            
         CH    R6,=H'12'                                                        
         BH    GDEXT                                                            
         PACK  DUB,DATE+4(2)       R7=DAY                                       
         CVB   R7,DUB                                                           
         SPACE 1                                                                
GD3      SR    R8,R8               VALIDATE DAY NUMBER                          
         IC    R8,HIDAYS-1(R6)                                                  
         CH    R6,=H'2'            TEST FEBRUARY                                
         BNE   GD3A                                                             
         STC   R5,DUB                                                           
         TM    DUB,X'03'           TEST LEAP YEAR                               
         BNZ   *+8                                                              
         LA    R8,29                                                            
GD3A     TM    OPT1,X'80'          TEST IF WANT DAYS IN MONTH RETURNED          
         BZ    *+8                                                              
         STC   R8,4(R1)                                                         
         CR    R7,R8               TEST DAY NUM WITH MAX FOR MONTH              
         BH    GDEXT                                                            
         LTR   R7,R7               EXIT IF ZERO DAY                             
         BZ    GDEXT                                                            
         SPACE 1                                                                
GD4      IC    R8,ADJTAB-1(R6)     GET ADJUSTMENT FOR MONTH                     
         AR    R7,R8               ADD ADJUSTMENT TO DAYS                       
         CH    R6,=H'2'            JAN OR FEB                                   
         BH    GD5                 NO                                           
         BCTR  R5,0                YES DECREMENT YEAR                           
         LTR   R5,R5                                                            
         BNM   GD5                                                              
         LA    R5,99               SET YEAR TO 99                               
         BCTR  R4,0                AND DECREMENT CENTURY                        
         SPACE 1                                                                
GD5      AR    R7,R5               ADD YEAR                                     
         SRL   R5,2                DIVIDE YEAR BY 4                             
         AR    R7,R5               ADD RESULT                                   
         SLL   R4,1                MULTIPLY CENT BY 2                           
         SR    R7,R4               SUBTRACT RESULT                              
         SRL   R4,3                (2*C)/8 = C/4                                
         AR    R7,R4               ADD RESULT                                   
         M     R6,=F'1'            KEEP SIGN ON RESULT                          
         D     R6,=F'7'            DIVIDE RESULT BY 7                           
         LTR   R6,R6                                                            
         BP    GD8                 REM>0 GO AND GET DAY                         
         BZ    GD6                                                              
         A     R6,=F'7'            REM<0 ADD 7                                  
         B     GD8                                                              
GD6      LA    R6,7                SET REM TO 7                                 
         SPACE 1                                                                
GD8      STC   R6,0(R1)            RETURN DAY NO (1 - 7)                        
         MH    R6,=H'3'            SET INDEX TO DAYTAB                          
         SR    R7,R7                                                            
         IC    R7,LANG                                                          
         MH    R7,DAYTABL          SET INDEX TO LANGUAGE ENTRY                  
         AR    R6,R7               ADD IT TO INDEX                              
         LA    R6,DAYTAB-3(R6)     GET DAY NAME                                 
         MVC   0(3,R3),0(R6)       RETURN IT                                    
         SPACE 1                                                                
GDEXT    XMOD1 1                                                                
         ORG   *-2                                                              
         BSM   0,RE                RESTORE CALLERS ADDRESSING MODE              
         EJECT                                                                  
DAYTABL  DC    H'21'                                                            
DAYTAB   DC    C'MONTUEWEDTHUFRISATSUN'  00                                     
EUKDAY   DC    C'MONTUEWEDTHUFRISATSUN'  01                                     
EUSDAY   DC    C'MONTUEWEDTHUFRISATSUN'  02                                     
GERDAY   DC    C'MONDIEMITDONFRESAMSON'  03                                     
FREDAY   DC    C'LUNMARMERJEUVENSAMDIM'  04                                     
SPADAY   DC    C'LUNMARMIEJUEVIESABDOM'  05                                     
ITADAY   DC    C'LUNMARMERGIOVENSABDOM'  06                                     
DUTDAY   DC    C'MAADINWOEDONVRIZATZON'  07                                     
         SPACE 1                                                                
HIDAYS   DC    AL1(31,28,31,30,31,30,31,31,30,31,30,31)                         
         SPACE 1                                                                
*              VALUES OF I(2.6*M - .2)                                          
ADJTAB   DC    AL1(28,31,2,5,7,10,12,15,18,20,23,25)                            
*                                                                               
DEFLANG  DS    0C                                                               
*&&UK*&& DC    AL1(1)                                                           
*&&US*&& DC    AL1(2)                                                           
         SPACE 1                                                                
         LTORG                                                                  
         SPACE 1                                                                
DATD     DSECT                                                                  
DUB      DS    D                                                                
RSAV     DS    D                                                                
DATE     DS    CL6                                                              
LANG     DS    X                                                                
OPT1     DS    X                                                                
DATX     EQU   *                                                                
         SPACE 1                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'005DDGETDAY  04/23/02'                                      
         END                                                                    
