*          DATA SET REAUDIT    AT LEVEL 111 AS OF 04/25/01                      
*CATALP REAUDIT                                                                 
*INCLUDE HELEN                                                                  
        TITLE 'REAUDIT' - REPPAK RECORD AUDIT ELEM MAINTENANCE ROUTINE'         
*                                                                               
* INPUT  PARAMETER 1:   0  ELEMENT CODE FOR AUDIT ELEM                          
*                     1-3  A(RECORD)                                            
*                                                                               
*        PARAMETER 2:   0  # OF AUDIT ENTRIES TO KEEP (MAX 10)                  
*                     1-3  A(COMFACS)                                           
*                                                                               
*        PARAMETER 3:      A(OPTIONAL 10 CHAR NOTATION) OR NULL                 
*                                                                               
*        PARAMETER 4:      A(TWA)                                               
*                                                                               
*                                                                               
* RETURN          CC:    EQUAL - ACTION SUCCESSFUL                              
*                        NOT EQUAL - ACTION NOT COMPLETED                       
*                                                                               
REAUDIT  CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 (WORKDX-WORKD),*REAUD*,CLEAR=YES,RR=R5                           
         USING WORKD,RC                                                         
*                                                                               
* INITIALIZATION                                                                
*                                                                               
         ST    R5,RELO                                                          
         ST    RD,SAVERD                                                        
         ST    R1,SVPARM                                                        
*                                                                               
         MVC   ELCODE,0(R1)                                                     
         CLI   ELCODE,0                                                         
         BNE   *+8                                                              
         MVI   ELCODE,X'FC'        DEFAULT TO FC ELEM CODE                      
         ZICM  RF,1(R1),3                                                       
         ST    RF,ARECORD                                                       
         MVC   ENTRIES,4(R1)                                                    
         CLI   ENTRIES,10                                                       
         BNH   *+6                                                              
         DC    H'0'                MAX 10 ENTRIES                               
*                                                                               
         ZICM  RF,5(R1),3          ACOMFACS                                     
         USING COMFACSD,RF                                                      
         MVC   HELLO,CHELLO                                                     
         MVC   DATCON,CDATCON                                                   
         MVC   GETFACT,CGETFACT                                                 
         DROP  RF                                                               
*                                                                               
* BUILD NEW MINI ELEM                                                           
*                                                                               
         XC    MINI,MINI                                                        
         LA    R8,MINI                                                          
         USING RAUDMINI,R8                                                      
*                                                                               
         GOTO1 DATCON,DMCB,(5,0),(2,RAUDDATE)   DATE                            
*                                                                               
         TIME  DEC                                                              
         STCM  R0,12,RAUDTIME                                                   
*                                                                               
         L     R1,SVPARM                                                        
         L     RF,12(R1)                                                        
         USING TWAD,RF                                                          
         MVC   RAUDCTID,TWAUSRID   CONNECT ID                                   
         DROP  RF                                                               
*                                                                               
         GOTO1 GETFACT,DMCB,0                                                   
         L     RF,0(R1)                                                         
         USING FACTSD,RF                                                        
         MVC   RAUDLUID,FASYM      LUID                                         
         DROP  RF                                                               
*                                                                               
         L     R1,SVPARM                                                        
         ICM   R1,15,8(R1)                                                      
         BZ    *+10                                                             
         MVC   RAUDNOTE,0(R1)                                                   
         DROP  R8                                                               
*                                                                               
* REMOVE OLD ELEM FROM RECORD                                                   
*                                                                               
         XC    ELEM,ELEM                                                        
         LA    R8,ELEM                                                          
         USING RAUDELEM,R8                                                      
*                                                                               
         GOTO1 HELLO,DMCB,(C'G',=C'REPFILE'),(ELCODE,ARECORD),0,0               
         CLI   12(R1),6            NOT FOUND                                    
         BE    AUD120                                                           
         CLI   12(R1),0            FOUND                                        
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         ZICM  R6,13(R1),3         SAVE COPY OF ELEM                            
         ZIC   R5,1(R6)                                                         
         BCTR  R5,0                                                             
         EX    R5,*+4                                                           
         MVC   ELEM(0),0(R6)                                                    
*                                                                               
         GOTO1 HELLO,DMCB,(C'D',=C'REPFILE'),(ELCODE,ARECORD),0,0               
         CLI   12(R1),0            DELETED                                      
         BE    AUD130                                                           
*                                                                               
AUD120   DS    0H                  BUILD NEW ELEMENT                            
         MVC   RAUDCODE,ELCODE                                                  
         MVI   RAUDLEN,RAUDOVLQ                                                 
*                                                                               
* DELETE OLDEST MINI ELEM (IF NECESSARY)                                        
*                                                                               
AUD130   DS    0H                                                               
         ZIC   R2,ENTRIES          # TO KEEP                                    
         ZIC   R5,RAUDLEN                                                       
         LA    R5,RAUDELEM(R5)                                                  
         LA    R3,RAUDMINI                                                      
         B     *+8                                                              
AUD140   DS    0H                                                               
         LA    R3,RAUDMLNQ(R3)                                                  
         CR    R3,R5                                                            
         BNL   AUD160                                                           
         BCT   R2,AUD140                                                        
*                                                                               
         XC    0(RAUDMLNQ,R3),0(R3) DELETE LAST MINI ELEM                       
         B     AUD165                                                           
*                                                                               
* MAKE ROOM FOR & INSERT NEW MINI ELEM                                          
*                                                                               
AUD160   DS    0H                                                               
         ZIC   R1,RAUDLEN           UPDATE ELEM LENGTH                          
         AHI   R1,RAUDMLNQ                                                      
         STC   R1,RAUDLEN                                                       
*                                                                               
AUD165   DS    0H                                                               
         LA    R3,RAUDMINI                                                      
         OC    RAUDMINI(RAUDMLNQ),RAUDMINI HAVE ROOM                            
         BZ    AUD200              YES                                          
*                                                                               
         LA    R4,RAUDMINI                                                      
         B     *+6                                                              
AUD170   DS    0H                                                               
         LR    R3,R5                                                            
         LA    R5,RAUDMLNQ(R3)                                                  
         OC    0(RAUDMLNQ,R5),0(R5)                                             
         BNZ   AUD170                                                           
AUD180   DS    0H                                                               
         MVC   0(RAUDMLNQ,R5),0(R3)                                             
         XC    0(RAUDMLNQ,R3),0(R3)                                             
         CR    R3,R4                                                            
         BNH   AUD200                                                           
         LR    R5,R3                                                            
         SHI   R3,RAUDMLNQ                                                      
         B     AUD180                                                           
*                                                                               
AUD200   DS    0H                                                               
         MVC   0(RAUDMLNQ,R3),MINI                                              
*                                                                               
         GOTO1 HELLO,DMCB,(C'P',=C'REPFILE'),(0,ARECORD),ELEM,0                 
         CLI   12(R1),0                                                         
         BE    EXITOK                                                           
         B     EXITL                                                            
*                                                                               
**********************************************************************          
* COMMON ROUTINES                                                               
**********************************************************************          
EXITH    CLI   *,0                 SET CC HIGH                                  
         B     EXIT                                                             
EXITL    CLI   *,X'FF'                                                          
         B     EXIT                                                             
EXITNO   LTR   RB,RB               SET CC NOT EQUAL                             
         B     EXIT                                                             
OK       EQU   *                                                                
EXITOK   CR    RB,RB               SET CC EQUAL                                 
         B     EXIT                                                             
EXIT     DS    0H                  JUST EXIT                                    
         XIT1                                                                   
*                                                                               
NOHOOK   BR    RE                                                               
*                                                                               
         GETEL R6,34,ELCODE                                                     
*                                                                               
SPACES   DC    80C' '                                                           
         LTORG                                                                  
***>>>   FOR TESTING PURPOSES, KEEP THE SIZE OF THIS MODULE THE SAME            
***>>>   ORG   REAUDIT+(((*-REAUDIT)/2048)+1)*2048                              
*                                                                               
WORKD    DSECT                                                                  
DUB      DS    D                                                                
DMCB     DS    6F                                                               
FULL     DS    F                                                                
RELO     DS    F                                                                
SVPARM   DS    F                                                                
SAVERD   DS    F                                                                
GETFACT  DS    A                                                                
HELLO    DS    A                                                                
DATCON   DS    A                                                                
ARECORD  DS    A                                                                
ELCODE   DS    X                                                                
ENTRIES  DS    X                                                                
BYTE     DS    X                                                                
MINI     DS    CL(RAUDMLNQ)                                                     
ELEM     DS    CL255                                                            
WORK     DS    CL64                                                             
*                                                                               
WORKDX   EQU   *                                                                
         DSECT                                                                  
       ++INCLUDE REGENAUD                                                       
       ++INCLUDE DDCOMFACS                                                      
       ++INCLUDE FAFACTS                                                        
       ++INCLUDE FATWA                                                          
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'111REAUDIT   04/25/01'                                      
         END                                                                    
