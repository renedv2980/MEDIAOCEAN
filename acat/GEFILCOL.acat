*          DATA SET GEFILCOL   AT LEVEL 007 AS OF 09/25/06                      
*CATALP GEFILCOL                                                                
FILCOL   TITLE 'GENERALISED COLUMN DISPLAY OVERLAY'                             
FILCOL   CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 0,GEFILCOL,RR=RE                                                 
         USING TWAD,RA                                                          
         USING WORKD,R9                                                         
         L     R8,AGWORK                                                        
         USING GWORKD,R8                                                        
         L     RC,AOVERWRK                                                      
         USING OVERWRKD,RC                                                      
         ST    RE,BORELO                                                        
         MVC   SVPARMS,0(R1)                                                    
         ST    R1,CALLR1                                                        
         L     R6,ASVEBLK                                                       
         USING MYSAVE,R6                                                        
*                                                                               
         SRL   RF,24                                                            
         CLM   RF,1,=AL1(ROUTSN)                                                
         BNH   *+6                 UNKNOWN ROUTINE                              
         DC    H'0'                                                             
         SLL   RF,2                                                             
         B     ROUTS(RF)                                                        
         SPACE 1                                                                
ROUTS    DS    0XL4                                                             
         B     OBJECT              OBJECT INVOKER                               
         B     INIT                INITIALIZATION CALL                          
*                                                                               
ROUTSN   EQU   (*-ROUTS)/5                                                      
         SPACE 1                                                                
***********************************************************************         
* EXITS                                                               *         
***********************************************************************         
         SPACE 1                                                                
EXITH    CLI   *,0                 SET CC HIGH                                  
         B     EXIT                                                             
EXITL    CLI   *,FF                SET CC LOW                                   
         B     EXIT                                                             
EXITOK   CR    RB,RB               SET CC EQUAL                                 
         SPACE 1                                                                
EXIT     L     R1,CALLR1           RETURN PARAMETERS                            
         MVC   0(L'SVPARMS,R1),SVPARMS                                          
         XIT1  ,                   EXIT WITH CC SET                             
         SPACE 2                                                                
EXITNV   MVC   FVMSGNO,=AL2(FVFNOTV)                                            
         B     EXITL               EXIT WITH FIELD NOT VALID SET                
EXITNOTN MVC   FVMSGNO,=AL2(FVFNOTN)                                            
         B     EXITL               EXIT WITH FIELD NOT NUMERIC SET              
         EJECT                                                                  
***********************************************************************         
* TABLE ITERATION ROUTINE  - EXPECTS RF TO HOLD A(TABLE)              *         
*                          - EXPECTS R1 TO HOLD VERB                  *         
***********************************************************************         
         SPACE 1                                                                
         USING OBJTABD,RF                                                       
ITER     CLI   OBJVERB,EOT         E.O.T.                                       
         BE    EXITOK                                                           
         CLM   R1,1,OBJVERB        R1 HOLDS EQUATED VERB                        
         BE    ITER02              MATCHED                                      
         LA    RF,OBJTABL(RF)                                                   
         B     ITER                ITERATE THIS TABLE                           
*                                                                               
ITER02   ICM   RF,15,OBJADR        ROUTINE TO HANDLE THE VERB                   
         LA    R1,SVPARMS                                                       
         A     RF,BORELO                                                        
         BR    RF                                                               
         DROP  RF                                                               
         SPACE 2                                                                
***********************************************************************         
* OBJECT CONTROLLER - INTERFACES TO ALL USER OBJECTS                  *         
*                                                                     *         
* P1 HOLDS EQUATED VERB                                               *         
***********************************************************************         
         SPACE 1                                                                
OBJECT   L     R1,SVPARMS                                                       
         LA    RF,TABLEOO          KNOWN OBJECTS                                
         B     ITER                                                             
*                                                                               
TABLEOO  DC    AL1(OKEY),AL1(0,0,0),AL4(KEY)                                    
         DC    AL1(ORECH),AL1(0,0,0),AL4(EXITH)                                 
         DC    AL1(ODATA),AL1(0,0,0),AL4(DATA)                                  
         DC    AL1(OSES),AL1(0,0,0),AL4(NTRSES)                                 
         DC    AL1(OLIST),AL1(0,0,0),AL4(LIST)                                  
         DC    AL1(OSUBACT),AL1(0,0,0),AL4(EXITH)                               
         DC    AL1(OACTH),AL1(0,0,0),AL4(EXITH)                                 
         DC    AL1(EOT)                                                         
         SPACE 2                                                                
***********************************************************************         
* INITIALIZATION                                                      *         
***********************************************************************         
         SPACE 1                                                                
INIT     B     EXITOK                                                           
         EJECT                                                                  
***********************************************************************         
* KEY OBJECT                                                          *         
*                                                                     *         
* P1 HOLDS EQUATED OBJECT                                             *         
* P2 HOLDS EQUATED VERB                                               *         
* P3 A(KEY)                                                           *         
* P4 HOLDS SUB-ACTION                                                 *         
***********************************************************************         
         SPACE 1                                                                
KEY      LM    R0,R2,SVPARMS                                                    
         USING FDRRECD,R2                                                       
         LA    RF,KEYTABL                                                       
         B     ITER                ITERATE KEY TABLE                            
*                                                                               
KEYTABL  DC    AL1(KFIRST),AL1(0,0,0),AL4(KEYFRST)                              
         DC    AL1(EOT)                                                         
         SPACE 2                                                                
***********************************************************************         
* FIRST TIME FOR KEY OBJECT                                           *         
***********************************************************************         
         SPACE 1                                                                
KEYFRST  L     R1,SVPARMS4         R1=INVOKING ACTION                           
         LA    RF,KFTABL           TABLE OF KNOWN INVOKERS                      
         B     ITER                ITERATE TABLE                                
*                                                                               
KFTABL   DC    AL1(KVAL),AL1(0,0,0),AL4(KFKVAL)      VALIDATE                   
         DC    AL1(KFVAL),AL1(0,0,0),AL4(KFKFVAL)    VALIDATE FILTER            
         DC    AL1(EOT)                                                         
         SPACE 2                                                                
***********************************************************************         
* FIRST TIME FOR VALIDATE OF A KEY OBJECT                             *         
***********************************************************************         
         SPACE 1                                                                
KFKVAL   XC    FDRKEY,FDRKEY       INITIALIZE KEY OF FIELD RECORD               
         MVI   FDRKMIN,FDRKMINQ    SET MINOR SYSTEM                             
         MVI   FDRKTYP,FDRKTYPQ    SET FIELD RECORD                             
         B     EXITOK                                                           
         SPACE 2                                                                
***********************************************************************         
* FIRST TIME FOR VALIDATE OF A KEY FILTER                             *         
***********************************************************************         
         SPACE 1                                                                
KFKFVAL  XC    FDRKEY,FDRKEY       INITIALIZE KEY OF FIELD RECORD               
         MVI   FDRKMIN,FDRKMINQ    SET MINOR SYSTEM                             
         MVI   FDRKTYP,FDRKTYPQ    SET FIELD RECORD                             
         MVC   FDRKSYS,GCOVSYS                                                  
         MVC   FDRKPRG,PSSHPRG                                                  
         MVC   FDRKREC,PSSHREC     SET SYS/PRG/REC                              
         B     EXITOK                                                           
         EJECT                                                                  
***********************************************************************         
* DATA OBJECT                                                         *         
*                                                                     *         
* P1 HOLDS EQUATED OBJECT IDENTIFIER                                  *         
* P2 HOLDS EQUATED DATA IDENTIFIER OR 0 IF GLOBAL DATA ACTION         *         
* P3 BYTE  0   HOLDS EQUATED DATA VERB IF P2 IS ZERO                  *         
* P3 BYTES 1-3 HOLDS EQUATED ACTION VERB                              *         
* P4 HOLDS A(RECORD AT CORRECT LEVEL)                                 *         
* P5 HOLDS A(FIELD TABLE ENTRY) OR ZERO IF P2 IS ZERO                 *         
*                                                                     *         
* FIELD DATA IS EXTRACTED/OUTPUT INTO FVIFLD                          *         
*                                                                     *         
***********************************************************************         
         SPACE 1                                                                
DATA     ICM   R1,15,SVPARMS2      R1=DATA IDENTIFIER                           
         BNZ   DATA02              ACTION IS ON A DATA OBJECT                   
*                                                                               
         L     R2,SVPARMS4                                                      
         USING FDRRECD,R2                                                       
         XR    R1,R1                                                            
         IC    R1,SVPARMS3         GET GLOBAL VERB                              
         LA    RF,DTATABL          TABLE OF GLOBAL VERBS                        
         B     ITER                ITERATE TABLE                                
*                                                                               
DATA02   LA    RF,KNOWTAB          TABLE OF KNOWN OBJECTS                       
         USING KNOWTABD,RF                                                      
*                                                                               
DATA04   CLC   KNOWID,=AL2(EOT)    REACH END - NOT A KNOWN DATA TYPE            
         BE    EXITH                                                            
         CLM   R1,3,KNOWID         IS THIS A KNOWN TYPE?                        
         BE    DATA06                                                           
         LA    RF,KNOWLQ(RF)                                                    
         B     DATA04                                                           
*                                                                               
DATA06   ICM   RF,15,KNOWADD       A(KNOWN OBJECT)                              
         A     RF,BORELO           RELOCATE IT                                  
*                                                                               
         LM    R1,R2,SVPARMS3      R1 HOLDS VERB                                
         USING FDRRECD,R2          R2 HOLDS A(RECORD)                           
         L     R3,AFDREL                                                        
         USING FDRELD,R3           R3=A(FDREL ON RECORD)                        
         BR    RF                                                               
         SPACE 1                                                                
*                                                                               
DTATABL  DC    AL1(DFIRST),AL1(0,0,0),AL4(DTAFRST)                              
         DC    AL1(EOT)                                                         
         SPACE 2                                                                
***********************************************************************         
* TABLE OF KNOWN RECORD OBJECTS                                       *         
***********************************************************************         
         SPACE 1                                                                
KNOWTAB  DC    AL2(00084),AL4(FNTDTA)    FIELD TAG                              
         DC    AL2(00083),AL4(FFEDTA)    FIELD INPUT                            
         DC    AL2(EOT)                                                         
*                                                                               
KNOWTABD DSECT                                                                  
KNOWID   DS    XL2                 IDENTIFIER                                   
KNOWADD  DS    AL4                 A(OBJECT)                                    
KNOWLQ   EQU   *-KNOWTABD                                                       
*                                                                               
FILCOL   CSECT                                                                  
         EJECT                                                                  
***********************************************************************         
* FIRST TIME FOR DATA OBJECT                                          *         
***********************************************************************         
         SPACE 1                                                                
DTAFRST  L     R1,SVPARMS3         VERB                                         
         LA    RF,DFTABL           TABLE OF KNOWN INVOKERS                      
         B     ITER                ITERATE TABLE                                
*                                                                               
DFTABL   DC    AL1(DDIS),AL1(0,0,0),AL4(DFDDIS)      DISPLAY                    
         DC    AL1(DVAL),AL1(0,0,0),AL4(DFDVAL)      VALIDATE                   
         DC    AL1(DFDIS),AL1(0,0,0),AL4(DFDDIS)     DISPLAY FILTER             
         DC    AL1(DFVAL),AL1(0,0,0),AL4(DFDVAL)     VALIDATE FILTER            
         DC    AL1(EOT)                                                         
         SPACE 2                                                                
***********************************************************************         
* FIRST TIME FOR DISPLAY OF A DATA OBJECT                             *         
***********************************************************************         
         SPACE 1                                                                
DFDDIS   XC    AFDREL,AFDREL                                                    
         GOTO1 VHELLO,BOPARM,(C'G',GCFILNAM),('FDRELQ',FDRRECD),0               
         CLI   12(R1),0                                                         
         BE    *+6                                                              
         DC    H'0'                FDREL IS MISSING FROM THIS RECORD            
*                                                                               
         MVC   AFDREL,12(R1)                                                    
         B     EXITOK                                                           
         SPACE 2                                                                
***********************************************************************         
* FIRST TIME FOR VALIDATE OF A DATA OBJECT                            *         
***********************************************************************         
         SPACE 1                                                                
DFDVAL   XC    AFDREL,AFDREL                                                    
         GOTO1 VHELLO,BOPARM,(C'G',GCFILNAM),('FDRELQ',FDRRECD),0               
         CLI   12(R1),0                                                         
         BE    *+6                                                              
         DC    H'0'                FDREL MISSING FROM THIS RECORD               
         MVC   AFDREL,12(R1)                                                    
         B     EXITOK                                                           
         EJECT                                                                  
***********************************************************************         
* DATA OBJECT FOR FIELD COLUMN NAMES                                  *         
*                                                                     *         
* R1 HOLDS EQUATED VERB                                               *         
* R2 HOLDS A(RECORD AT CORRECT LEVEL)                                 *         
***********************************************************************         
         SPACE 1                                                                
FNTDTA   LA    RF,FNTTBL           TABLE OF KNOWN VERBS                         
         B     ITER                ITERATE TABLE                                
*                                                                               
FNTTBL   DC    AL1(DDIS),AL1(0,0,0),AL4(DISFNT)                                 
         DC    AL1(EOT)                                                         
         SPACE 2                                                                
***********************************************************************         
* DISPLAY COLUMN LIST TAG                                             *         
***********************************************************************         
         SPACE 1                                                                
DISFNT   L     R3,ATLST                                                         
         USING TLSTD,R3                                                         
         MVC   FVIFLD(L'TLKRNAM),TLKRNAM                                        
         B     EXITOK              MOVE OUT FIELD FROM TSAR RECORD              
         DROP  R3                                                               
         EJECT                                                                  
***********************************************************************         
* DATA OBJECT FOR DIS=VALIDATION                                      *         
*                                                                     *         
* R1 HOLDS EQUATED VERB                                               *         
* R2 HOLDS A(RECORD AT CORRECT LEVEL)                                 *         
***********************************************************************         
         SPACE 1                                                                
FFEDTA   LA    RF,FFETBL           TABLE OF KNOWN VERBS                         
         B     ITER                ITERATE TABLE                                
*                                                                               
FFETBL   DC    AL1(DDIS),AL1(0,0,0),AL4(DISFFE)                                 
         DC    AL1(DVAL),AL1(0,0,0),AL4(VALFFE)                                 
         DC    AL1(EOT)                                                         
         SPACE 2                                                                
***********************************************************************         
* DISPLAY FILTER FIELD VALUES                                         *         
***********************************************************************         
         SPACE 1                                                                
DISFFE   L     R3,ATLST                                                         
         USING TLSTD,R3                                                         
         CLI   TLKDIS,TLKNSEL      BEEN ASSIGNED YET?                           
         BE    EXITOK              NO                                           
*                                                                               
         CLI   TLKDIS,TLKFIX       FIXED?                                       
         BNE   *+12                NO                                           
         MVI   FVIFLD,C'F'                                                      
         B     DFFE02                                                           
         CLI   TLKDIS,TLKVAR       VARIABLE?                                    
         BNE   *+12                NO                                           
         MVI   FVIFLD,C'V'                                                      
         B     DFFE02                                                           
         DC    H'0'               TSAR RECORD FUCKED UP                         
*                                                                               
DFFE02   XR    RF,RF                                                            
         IC    RF,TLKPOS                                                        
         CURED (RF),(3,FVIFLD+1),0,DMCB=BOPARM,ALIGN=LEFT                       
         LA    RF,FVIFLD+4                                                      
         CLI   0(RF),C' '                                                       
         BH    *+8                                                              
         BCT   RF,*-8                                                           
*                                                                               
         TM    TLTYP,TLTOPEN       PROTECTED FROM INPUT?                        
         BZ    EXITOK              YES                                          
         MVI   1(RF),C'&&'         FLAG AS OPEN FIELD.                          
         B     EXITOK                                                           
         DROP  R3                                                               
         SPACE 2                                                                
***********************************************************************         
* VALIDATE FILTER FIELD VALUES                                        *         
***********************************************************************         
         SPACE 1                                                                
VALFFE   L     R3,ATLST                                                         
         USING TLSTD,R3                                                         
*                                                                               
         XC    AMATCH,AMATCH                                                    
         LA    RF,MYFIXCLM                                                      
         LA    R0,FIXMAX                                                        
         LA    R1,1                                                             
         USING DCTABD,RF                                                        
VFFE02   CLC   TLKRNUM,DCTFLD#     MATCH ON FIELD NUMBER?                       
         BE    VFFE08                                                           
         LA    R1,1(R1)                                                         
         LA    RF,DCTABL(RF)                                                    
         BCT   R0,VFFE02                                                        
*                                                                               
         LA    RF,MYVARCLM                                                      
         LA    R0,VARMAX                                                        
         LA    R1,1                                                             
VFFE04   CLC   TLKRNUM,DCTFLD#     MATCH ON FIELD NUMBER?                       
         BE    VFFE08                                                           
         LA    R1,1(R1)                                                         
         LA    RF,DCTABL(RF)                                                    
         BCT   R0,VFFE04                                                        
*                                                                               
         LA    RF,MYVALCLM                                                      
         LA    R0,VALMAX                                                        
         LA    R1,1                                                             
VFFE06   CLC   TLKRNUM,DCTFLD#     MATCH ON FIELD NUMBER?                       
         BE    VFFE08                                                           
         LA    R1,1(R1)                                                         
         LA    RF,DCTABL(RF)                                                    
         BCT   R0,VFFE06                                                        
         DC    H'0'                DON`T KNOW ABOUT THIS FIELD                  
*                                                                               
VFFE08   ST    RF,AMATCH           SAVE ADDRESS OF MATCHED COLUMN               
*                                                                               
VFFE10   CLI   FVILEN,0            CLEARING THIS FIELD                          
         BNE   VFFE16                                                           
         ICM   RF,15,AMATCH        KNOW THIS COL IS OK IF GET THIS FAR          
         BZ    *+10                                                             
         XC    0(DCTABL,RF),0(RF)  CLEAR MATCHED COLUMN FROM LIST               
*                                                                               
         LA    RF,MYVALCLM                                                      
         LA    R0,VALMAX                                                        
VFFE12   OC    DCTFLD#,DCTFLD#     FIND FIRST BLANK IN VALID FOR DISP.          
         BZ    VFFE14                                                           
         LA    RF,DCTABL(RF)                                                    
         BCT   R0,VFFE12                                                        
         DC    H'0'                                                             
*                                                                               
VFFE14   MVC   DCTFLD#,TLKRNUM     NUMBER                                       
         MVI   DCTINDS1,0                                                       
         TM    TLTYP,TLTCOPN                                                    
         BZ    *+8                                                              
         OI    DCTINDS1,DCTICOPN                                                
         B     VFFE34                                                           
         DROP  RF                                                               
*                                                                               
         USING DCTABD,R4                                                        
VFFE16   TM    FVIIND,FVINUM       IF ONLY NUMBER INPUT, THEN                   
         BZ    VFFE18              DEFAULT IS VARIABLE & CLOSED                 
         LA    R4,MYVARCLM                                                      
         MVI   THISMAX,VARMAX                                                   
         MVI   THISOPEN,C'C'                                                    
         L     RF,BCFULL                                                        
         B     VFFE32                                                           
*                                                                               
VFFE18   XR    R4,R4               CLEAR R4                                     
         CLI   FVIFLD,C'F'         FIXED?                                       
         BNE   *+16                NO                                           
         LA    R4,MYFIXCLM                                                      
         MVI   THISMAX,FIXMAX                                                   
         B     VFFE20                                                           
*                                                                               
         CLI   FVIFLD,C'V'         VARIABLE?                                    
         BNE   EXITNV              NO                                           
         LA    R4,MYVARCLM                                                      
         MVI   THISMAX,VARMAX                                                   
*                                                                               
VFFE20   XR    R1,R1                                                            
         IC    R1,FVXLEN           ACTUAL INPUT LENGTH-1                        
         LA    RF,FVIFLD(R1)                                                    
         MVI   THISOPEN,C'C'                                                    
         CLI   0(RF),C'&&'         IS FIELD SUPPOSED TO BE OPEN?                
         BNE   VFFE24              NO                                           
         TM    TLTYP,TLTCOPN       ALLOWED TO OPEN THIS FIELD?                  
         BNZ   VFFE22              YES - THAT`S COOL                            
         MVI   FVOSYS,QSACC                                                     
         MVC   FVMSGNO,=AL2(AE$TCNOP)                                           
         LA    RE,FVIFLD                                                        
         SR    RF,RE                                                            
         STC   RF,FVERRNDX         POSITION CURSOR ON OFFENDING SYMBOL          
         B     EXITL                                                            
*                                                                               
VFFE22   MVI   THISOPEN,C'O'                                                    
         SH    R1,=H'1'                                                         
         BM    EXITNV              R1 CONTAINS LENGTH OF 'NUMBER'               
*                                                                               
VFFE24   STC   R1,NUMLEN           SAVE IT                                      
         LR    R0,R1                                                            
         LA    RF,FVIFLD+1         FIRST BYTE IS FIXED/VARIABLE                 
*                                                                               
VFFE26   CLI   0(RF),C'0'          MAKE SURE THIS IS ALL NUMERIC                
         BL    VFFE28                                                           
         CLI   0(RF),C'9'                                                       
         BH    VFFE28                                                           
         LA    RF,1(RF)                                                         
         BCT   R0,VFFE26                                                        
         B     VFFE30                                                           
*                                                                               
VFFE28   LA    RE,FVIFLD                                                        
         SR    RF,RE                                                            
         STC   RF,FVERRNDX                                                      
         B     EXITNOTN                                                         
*                                                                               
VFFE30   BCTR  R1,0                GET BINARY EQUIVALENT OF NUMBER              
         EX    R1,*+8                                                           
         B     *+10                                                             
         PACK  BODUB1,FVIFLD+1(0)                                               
         CVB   RF,BODUB1           RF=NUMBER                                    
*                                                                               
VFFE32   LTR   R4,R4               MAKE DOUBLY SURE R4 HAS BEEN SET.            
         BNZ   *+6                                                              
         DC    H'0'                                                             
*                                                                               
         XR    R1,R1               SEE IF # FITS IN THIS DISPLAY TYPE           
         IC    R1,THISMAX                                                       
         CR    RF,R1               IS IT TOO BIG?                               
         BH    EXITNV              YES                                          
*                                                                               
         ICM   RE,15,AMATCH        KNOW THIS COL IS OK IF GET THIS FAR          
         BZ    *+10                                                             
         XC    0(DCTABL,RE),0(RE)  CLEAR MATCHED COLUMN FROM LIST               
*                                                                               
         STC   RF,NUMVAL           SAVE IT                                      
         SR    R1,RF               NUMBER OF COLS AFTER THIS POINT              
         STC   R1,THISLEFT                                                      
         BCTR  RF,0                MAKE CURRENT POSN ZERO BASED                 
         MH    RF,=Y(DCTABL)                                                    
         LA    R0,DCTABD(RF)       INDEX INTO TABLE OF COLUMNS                  
         ST    R0,THISPNT          WHERE THIS COLUMN IS TO BE INSERTED          
         LA    RE,TMPCLM           TEMP SAVE AREA                               
         MH    R1,=Y(DCTABL)       LENGTH OF WHAT IS LEFT                       
         LR    RF,R1                                                            
         MVCL  RE,R0               SAVE ALL AFTER THIS POINT                    
*                                                                               
         L     RF,THISPNT          INSERT THE NUMBER AND DISPLAY INFO           
X        USING DCTABD,RF                                                        
         MVC   X.DCTFLD#,TLKRNUM   NUMBER                                       
         MVI   X.DCTINDS1,0                                                     
         CLI   THISOPEN,C'O'       OPEN?                                        
         BNE   *+8                 NO                                           
         OI    X.DCTINDS1,DCTIOPEN FLAG FIELD OPEN FOR INPUT                    
         DROP  X                                                                
*                                                                               
         LA    RE,DCTABL(RF)       TO                                           
         LA    R0,TMPCLM           FROM                                         
         XR    R1,R1                                                            
         IC    R1,THISLEFT         HOW MANY TO MOVE BACK                        
         MH    R1,=Y(DCTABL)       LENGTH TO MOVE IN R1, BUT NEED TO            
         SH    R1,=Y(DCTABL)       MOVE BACK 1 LESS THAN BEFORE                 
         BM    VFFE34              NOTHING TO MOVE BACK                         
         LR    RF,R1                                                            
         MVCL  RE,R0               MOVE BACK COLUMNS AFTER THIS ONE             
*                                                                               
VFFE34   BAS   RE,CLOSE            CLOSE UP THE GAPS                            
         OI    LSSCIND1,LSSCIBLD   REBUILD LIST                                 
         B     EXITOK                                                           
         DROP  R3,R4                                                            
         EJECT                                                                  
***********************************************************************         
* CLOSE ANY GAPS IN MYFIXCLM AND MYVARCLM AND SET MYFIXNUM & MYVARNUM *         
***********************************************************************         
         SPACE 1                                                                
CLOSE    NTR1  ,                                                                
         XC    FIXOVER(DCTABL),FIXOVER                                          
         XC    VAROVER(DCTABL),VAROVER                                          
         XC    VALOVER(DCTABL),VALOVER                                          
         LA    R0,FIXMAX           FIRST SORT OUT THE FIXED COLUMNS             
         LA    R2,MYFIXCLM         WHERE THEY ARE SAVED                         
         USING DCTABD,R2                                                        
CLOS02   OC    DCTFLD#,DCTFLD#     HAS THIS COLUMN GOT A NUMBER?                
         BZ    CLOS04              NO - CLOSE THE GAP 1 SPACE                   
         LA    R2,DCTABL(R2)                                                    
         BCT   R0,CLOS02           DO NEXT COLUMN                               
         B     CLOS10                                                           
*                                                                               
CLOS04   LR    R5,R0               R0 HOLDS THE NUMBER UNPROCESSED              
         LR    R4,R2               R2 HOLDS THE CURRENT POSITION                
*                                                                               
X        USING DCTABD,R4                                                        
CLOS06   OC    X.DCTFLD#,X.DCTFLD# MORE FIELDS BETWEEN HERE AND END?            
         BNZ   CLOS08              YES - CLOSE UP THE GAP                       
         LA    R4,DCTABL(R4)                                                    
         BCT   R5,CLOS06                                                        
         B     CLOS10              WHOLE OF REST OF COLUMNS ARE EMPTY           
*                                                                               
CLOS08   LR    R5,R0               R0 HOLDS THE NUMBER LEFT TO MOVE             
         MH    R5,=Y(DCTABL)                                                    
         LR    RF,R5                                                            
         LR    R4,R2               R4 HOLDS THE CURRENT POSITION                
         LA    RE,DCTABL(R4)       RE HOLDS THE NEXT POSITION ALONG             
         MVCL  R4,RE               CLOSE UP 1 SPACE                             
         B     CLOS02                                                           
*                                                                               
CLOS10   XR    R1,R1               CALCULATE HOW MANY FIXED COLS EXIST          
         LA    R0,FIXMAX                                                        
         LA    R2,MYFIXCLM                                                      
*                                                                               
CLOS12   OC    DCTFLD#,DCTFLD#     LOOK FOR FIRST EMPTY SLOT                    
         BZ    CLOS14                                                           
         LA    R1,1(R1)            R1 HOLDS COUNT OF FIELDS FOUND               
         LA    R2,DCTABL(R2)                                                    
         BCT   R0,CLOS12                                                        
*                                                                               
CLOS14   STH   R1,MYFIXNUM         CLOSE UP GAPS IN VARIABLE COLUMNS            
         LA    R0,VARMAX                                                        
         LA    R2,MYVARCLM                                                      
         USING DCTABD,R2                                                        
*                                                                               
CLOS16   OC    DCTFLD#,DCTFLD#     COLUMN HAS FIELD IN IT?                      
         BZ    CLOS18              NO - CLOSE UP GAP BY 1                       
         LA    R2,DCTABL(R2)                                                    
         BCT   R0,CLOS16                                                        
         B     CLOS24                                                           
*                                                                               
CLOS18   LR    R5,R0               R0 HOLDS THE NUMBER LEFT TO MOVE             
         LR    R4,R2               R2 HOLDS THE CURRENT POSITION                
*                                                                               
X        USING DCTABD,R4                                                        
CLOS20   OC    X.DCTFLD#,X.DCTFLD# SEE IF ANY MORE TO MOVE IN FROM END          
         BNZ   CLOS22              YES                                          
         LA    R4,DCTABL(R4)                                                    
         BCT   R5,CLOS20                                                        
         B     CLOS24              NO MORE TO MOVE IN                           
*                                                                               
CLOS22   LR    R5,R0               R0 HOLDS THE NUMBER LEFT TO MOVE             
         MH    R5,=Y(DCTABL)                                                    
         LR    RF,R5                                                            
         LR    R4,R2                                                            
         LA    RE,DCTABL(R4)                                                    
         MVCL  R4,RE               CLOSE UP GAP BY 1                            
         B     CLOS16                                                           
*                                                                               
CLOS24   XR    R1,R1               NOW COUNT NUMBER OF VARIABLE COLS            
         LA    R0,VARMAX                                                        
         LA    R2,MYVARCLM                                                      
*                                                                               
CLOS26   OC    DCTFLD#,DCTFLD#     FIELD IN THIS COLUMN?                        
         BZ    CLOS28                                                           
         LA    R1,1(R1)                                                         
         LA    R2,DCTABL(R2)                                                    
         BCT   R0,CLOS26                                                        
*                                                                               
CLOS28   STH   R1,MYVARNUM         SAVE NUMBER OF VARIABLE FIELDS               
         LA    R0,VALMAX                                                        
         LA    R2,MYVALCLM                                                      
         USING DCTABD,R2                                                        
*                                                                               
CLOS30   OC    DCTFLD#,DCTFLD#     COLUMN HAS FIELD IN IT?                      
         BZ    CLOS32              NO - CLOSE UP GAP BY 1                       
         LA    R2,DCTABL(R2)                                                    
         BCT   R0,CLOS30                                                        
         B     CLOS38                                                           
*                                                                               
CLOS32   LR    R5,R0               R0 HOLDS THE NUMBER LEFT TO MOVE             
         LR    R4,R2               R2 HOLDS THE CURRENT POSITION                
*                                                                               
X        USING DCTABD,R4                                                        
CLOS34   OC    X.DCTFLD#,X.DCTFLD# SEE IF ANY MORE TO MOVE IN FROM END          
         BNZ   CLOS36              YES                                          
         LA    R4,DCTABL(R4)                                                    
         BCT   R5,CLOS34                                                        
         B     CLOS38              NO MORE TO MOVE IN                           
*                                                                               
CLOS36   LR    R5,R0               R0 HOLDS THE NUMBER LEFT TO MOVE             
         MH    R5,=Y(DCTABL)                                                    
         LR    RF,R5                                                            
         LR    R4,R2                                                            
         LA    RE,DCTABL(R4)                                                    
         MVCL  R4,RE               CLOSE UP GAP BY 1                            
         B     CLOS30                                                           
*                                                                               
CLOS38   XR    R1,R1               NOW COUNT NUMBER OF VARIABLE COLS            
         LA    R0,VALMAX                                                        
         LA    R2,MYVALCLM                                                      
*                                                                               
CLOS40   OC    DCTFLD#,DCTFLD#     FIELD IN THIS COLUMN?                        
         BZ    CLOS42                                                           
         LA    R1,1(R1)                                                         
         LA    R2,DCTABL(R2)                                                    
         BCT   R0,CLOS40                                                        
*                                                                               
CLOS42   STH   R1,MYVALNUM         SAVE NUMBER OF VARIABLE FIELDS               
         XIT1                                                                   
         DROP  X                                                                
***********************************************************************         
* NTRSES OBJECT                                                       *         
* -------------                                                       *         
* P1 HOLDS EQUATED OBJECT                                             *         
* P2 HOLDS EQUATED VERB                                               *         
* P3 HOLDS A(INITIAL LIST KEY)                                        *         
* P4 HOLDS A(MEMORY COVERED BY FESD)                                  *         
***********************************************************************         
         SPACE 1                                                                
NTRSES   LM    R0,R3,SVPARMS                                                    
         USING FDRRECD,R2                                                       
         USING FESD,R3                                                          
         LA    RF,NSSTABL                                                       
         B     ITER                                                             
*                                                                               
NSSTABL  DC    AL1(SNTRIN),AL1(0,0,0),AL4(NTRIN)                                
         DC    AL1(SXITOUT),AL1(0,0,0),AL4(XITOUT)                              
         DC    AL1(EOT)                                                         
         SPACE 2                                                                
***********************************************************************         
* BUILD PARAMETER LIST FOR NTRSES TRANSFER (EXIT FROM)                *         
***********************************************************************         
         SPACE 1                                                                
XITOUT   LA    R4,FESGSSV                                                       
G        USING GSSAVE,R4                                                        
         MVC   G.LSFIXNUM,MYFIXNUM RESET DEFAULT DISPLAY INFORMATION            
         MVC   G.LSVARNUM,MYVARNUM                                              
         MVC   G.LSVALNUM,MYVALNUM                                              
         LA    R0,MYFIXCLM                                                      
         LA    RE,G.LSFIXCLM                                                    
         LH    R1,=Y(FIXCLMLQ)                                                  
         LR    RF,R1                                                            
         MVCL  RE,R0                                                            
         LA    R0,MYVARCLM                                                      
         LA    RE,G.LSVARCLM                                                    
         LH    R1,=Y(VARCLMLQ)                                                  
         LR    RF,R1                                                            
         MVCL  RE,R0                                                            
         LA    R0,MYVALCLM                                                      
         LA    RE,G.LSVALCLM                                                    
         LH    R1,=Y(VALCLMLQ)                                                  
         LR    RF,R1                                                            
         MVCL  RE,R0                                                            
         B     EXITOK                                                           
         DROP  G                                                                
         SPACE 2                                                                
***********************************************************************         
* PROCESS PARAMETER LIST FOR NTRSES TRANSFER (ENTER IN)               *         
***********************************************************************         
         SPACE 1                                                                
NTRIN    MVC   MYFIXNUM,LSFIXNUM   SAVE DEFAULT DISPLAY INFORMATION             
         MVC   MYVARNUM,LSVARNUM                                                
         MVC   MYVALNUM,LSVALNUM                                                
         LA    R0,MYFIXCLM                                                      
         LA    RE,LSFIXCLM                                                      
         LH    R1,=Y(FIXCLMLQ)                                                  
         LR    RF,R1                                                            
         MVCL  R0,RE                                                            
         LA    R0,MYVARCLM                                                      
         LA    RE,LSVARCLM                                                      
         LH    R1,=Y(VARCLMLQ)                                                  
         LR    RF,R1                                                            
         MVCL  R0,RE                                                            
         LA    R0,MYVALCLM                                                      
         LA    RE,LSVALCLM                                                      
         LH    R1,=Y(VALCLMLQ)                                                  
         LR    RF,R1                                                            
         MVCL  R0,RE                                                            
         B     EXITOK                                                           
         DROP  R2,R3                                                            
         SPACE 2                                                                
***********************************************************************         
* LIST OBJECT                                                         *         
*                                                                     *         
* P1 HOLDS EQUATED OBJECT                                             *         
* P2 HOLDS EQUATED VERB                                               *         
* P3 HOLDS CURRENT KEY BUILD AREA                                     *         
* P4 HOLDS PREVIOUS KEY                                               *         
***********************************************************************         
         SPACE 1                                                                
LIST     LM    R0,R3,SVPARMS                                                    
THIS     USING FDRRECD,R2                                                       
         LA    RF,LISTABL                                                       
         B     ITER                                                             
*                                                                               
LISTABL  DC    AL1(LGETFRST),AL1(0,0,0),AL4(BLST)                               
         DC    AL1(LGETNEXT),AL1(0,0,0),AL4(BLST)                               
         DC    AL1(LLSTFRST),AL1(0,0,0),AL4(FTFLST)                             
         DC    AL1(LTSARDIR),AL1(0,0,0),AL4(TSARDIR)                            
         DC    AL1(LTSARFIL),AL1(0,0,0),AL4(TSARFIL)                            
         DC    AL1(LSCRLAST),AL1(0,0,0),AL4(LSTSCR)                             
         DC    AL1(LINIT),AL1(0,0,0),AL4(ILST)                                  
         DC    AL1(EOT)                                                         
         SPACE 2                                                                
***********************************************************************         
* INITIALISE LIST                                                     *         
***********************************************************************         
         SPACE 1                                                                
ILST     MVI   LSSUBLEN,0                                                       
         NI    LSSTAT1,FF-(LSSENTK+LSSTSAR)                                     
         OI    LSSTAT1,LSSBALL+LSSMULIN                                         
         NI    LSSTAT2,FF-(LSSADD)                                              
         MVC   LSCOLLIN,=AL2(25)                                                
         MVC   LSLINROW,=AL2(3)                                                 
         B     EXITOK                                                           
         SPACE 2                                                                
***********************************************************************         
* FIRST TIME FOR LIST BUILD                                           *         
***********************************************************************         
         SPACE 1                                                                
K        USING FCRRECD,IOKEY                                                    
FTFLST   LA    R2,CLMTAB                                                        
DCLM02   CLI   0(R2),EOT                                                        
         BE    EXITOK              NEED TO SET A DEFAULT?                       
         XC    K.FCRKEY,K.FCRKEY                                                
         MVI   K.FCRKMIN,FCRKMINQ                                               
         MVI   K.FCRKTYP,FCRKTYPQ                                               
         MVC   K.FCRKSYS,GCOVSYS   SYSTEM                                       
         MVC   K.FCRKPRG,GCPRGNO   PROGRAM                                      
         MVC   K.FCRKREC,PSREC     RECORD                                       
         TM    0(R2),CPAGE         PAGE REQUIRED?                               
         BZ    DCLM04                                                           
         MVI   K.FCRKPAGE,FCRKPLST LIST PAGE                                    
         TM    PSLSTAT1,LSSMAIN                                                 
         BZ    *+10                                                             
         MVC   K.FCRKPAGE,PSSMPAGE                                              
*                                                                               
DCLM04   TM    0(R2),CACTION       ACTION REQUIRED?                             
         BZ    *+10                                                             
         MVC   K.FCRKACT,PSACT     ACTION                                       
         TM    0(R2),CSBREC        SUB-RECORD REQUIRED?                         
         BZ    *+10                                                             
         MVC   K.FCRKSREC,P2REC    SUB-RECORD                                   
         TM    0(R2),CAGENCY       AGY ALPHA REQUIRED?                          
         BZ    *+10                                                             
         MVC   K.FCRKAGY,CUAALF    AGENCY ALPHA ID                              
         TM    0(R2),CUSER         PERSON ID REQUIRED?                          
         BZ    *+10                                                             
         MVC   K.FCRKPER,CUUSER    PERSON ID                                    
*                                                                               
         L     R1,=AL4(XOREAD+XOGENDIR+XIO2)                                    
         GOTOX ('XIO',AGROUTS)                                                  
         BE    DCLM06              GOT THIS RECORD?                             
         LA    R2,1(R2)                                                         
         B     DCLM02                                                           
*                                                                               
CLMTAB   DC    AL1(CPAGE+CACTION+CSBREC+CAGENCY+CUSER)                          
         DC    AL1(CPAGE+CACTION+CSBREC+CAGENCY)                                
         DC    AL1(CPAGE+CACTION+CSBREC)                                        
         DC    AL1(CPAGE+CACTION+CAGENCY+CUSER)                                 
         DC    AL1(CPAGE+CACTION+CAGENCY)                                       
         DC    AL1(CPAGE+CACTION)                                               
         DC    AL1(CPAGE+CAGENCY+CUSER)                                         
         DC    AL1(CPAGE+CAGENCY)                                               
         DC    AL1(CPAGE)                                                       
         DC    AL1(EOT)                                                         
*                                                                               
CPAGE    EQU   X'80'                                                            
CACTION  EQU   X'40'                                                            
CSBREC   EQU   X'20'                                                            
CAGENCY  EQU   X'10'                                                            
CUSER    EQU   X'08'                                                            
*                                                                               
DCLM06   L     R1,=AL4(XOGET+XOGENFIL+XIO2)                                     
         GOTOX ('XIO',AGROUTS)                                                  
         BE    EXITOK                                                           
         DC    H'0'                GETREC NOT HAPPY                             
         SPACE 2                                                                
***********************************************************************         
* BUILD LIST FOR DISPLAY                                              *         
***********************************************************************         
         SPACE 1                                                                
K        USING FDRRECD,IOKEY                                                    
BLST     MVC   K.FDRKEY,THIS.FDRKEY                                             
         XC    K.FDRKCTRY(3),K.FDRKCTRY                                         
         XR    RF,RF                                                            
         ICM   RF,3,K.FDRKNUM                                                   
         LA    RF,1(RF)                                                         
         STCM  RF,3,K.FDRKNUM                                                   
*                                                                               
         ICM   R1,15,SVPARMS5                                                   
         A     R1,=A(XOGENDIR+XOHIGH)                                           
         GOTOX ('XIO',AGROUTS)                                                  
         CLC   K.FDRKEY(FDRKNUM-FDRRECD),THIS.FDRRECD                           
         BNE   EXITL               GONE PAST FDRRECS FOR THIS LIST              
*                                                                               
         MVC   THIS.FDRKEY,K.FDRKEY                                             
         OC    ASTEST,ASTEST       CONNECTED TO TEST PHASE?                     
         BZ    BLST02              NO                                           
*                                                                               
         MVC   K.FDRKEY,THIS.FDRKEY                                             
         MVC   K.FDRKCTRY,CUCTRY                                                
         XI    K.FDRKCTRY,FF                                                    
         MVI   K.FDRKSUB,FF                                                     
         MVC   K.FDRKTEST,ASTEST                                                
         ICM   R1,15,SVPARMS5                                                   
         A     R1,=A(XOGENDIR+XOREAD)                                           
         GOTOX ('XIO',AGROUTS)                                                  
         BE    BLST06              TEST RECORD FOR CONNECTED COUNTRY            
*                                                                               
BLST02   MVC   K.FDRKEY,THIS.FDRKEY                                             
         MVC   K.FDRKCTRY,CUCTRY                                                
         XI    K.FDRKCTRY,FF                                                    
         MVI   K.FDRKSUB,FF                                                     
         XC    K.FDRKTEST,K.FDRKTEST                                            
         ICM   R1,15,SVPARMS5                                                   
         A     R1,=A(XOGENDIR+XOREAD)                                           
         GOTOX ('XIO',AGROUTS)                                                  
         BE    BLST06              LIVE  RECORD FOR CONNECTED COUNTRY           
*                                                                               
         OC    ASTEST,ASTEST                                                    
         BZ    BLST04                                                           
*                                                                               
         MVC   K.FDRKEY,THIS.FDRKEY                                             
         MVI   K.FDRKCTRY,FF                                                    
         MVI   K.FDRKSUB,FF                                                     
         MVC   K.FDRKTEST,ASTEST                                                
         ICM   R1,15,SVPARMS5                                                   
         A     R1,=A(XOGENDIR+XOREAD)                                           
         GOTOX ('XIO',AGROUTS)                                                  
         BE    BLST06              TEST RECORD FOR HOST PROCESSOR               
*                                                                               
BLST04   MVC   K.FDRKEY,THIS.FDRKEY                                             
         MVI   K.FDRKCTRY,FF                                                    
         MVI   K.FDRKSUB,FF                                                     
         XC    K.FDRKTEST,K.FDRKTEST                                            
         ICM   R1,15,SVPARMS5                                                   
         A     R1,=A(XOGENDIR+XOREAD)                                           
         GOTOX ('XIO',AGROUTS)                                                  
         BE    BLST06              LIVE RECORD FOR HOST PROCESSOR               
         B     BLST                                                             
*                                                                               
BLST06   ICM   R1,15,SVPARMS5                                                   
         A     R1,=A(XOGENFIL+XOGET)                                            
         GOTOX ('XIO',AGROUTS)                                                  
         BE    *+6                                                              
         DC    H'0'                GETREC ERROR                                 
*                                                                               
         L     RF,SVPARMS5         GET A(RECORD)                                
         SRL   RF,4-2                                                           
         L     RF,AIO1-L'AIO1(RF)                                               
*                                                                               
         GOTOX VHELLO,BOPARM,(C'G',GCFILNAM),('FDRELQ',(RF)),0                  
         CLI   12(R1),0            NO FDREL?                                    
         BNE   BLST                                                             
*                                                                               
         ICM   R4,15,12(R1)                                                     
L        USING FDRELD,R4                                                        
         CLI   L.FDRLHLEN,0        LIST HEADING WIDTH IS 0?                     
         BE    BLST                NOT VALID THEN                               
         CLI   L.FDRLCLEN,0        LIST DATA WIDTH IS 0?                        
         BE    BLST                NOT VALID THEN                               
*                                                                               
         TM    L.FDRINDS1,FDR1DDS  DDS ONLY FIELD?                              
         BZ    *+12                                                             
         TM    CUSTAT,CUSDDS       TEST DDS TERMINAL IF DDS ONLY FIELD          
         BZ    BLST                NOT FOR YOU...                               
*                                                                               
         USING DCTABD,RF                                                        
         LA    RF,MYFIXCLM         IS IT A FIXED COLUMN?                        
         XR    R0,R0                                                            
         ICM   R0,3,MYFIXNUM       NUMBER OF FIXED COLUMNS                      
         BZ    BLST10              NO FIXED COLUMNS                             
*                                                                               
BLST08   CLC   L.FDRNUM,DCTFLD#    TRY TO MATCH FIELD NUMBERS                   
         BE    BLSTX               MATCH                                        
         LA    RF,DCTABL(RF)                                                    
         BCT   R0,BLST08                                                        
*                                                                               
BLST10   LA    RF,MYVARCLM         VARIABLE COLUMN?                             
         XR    R0,R0                                                            
         ICM   R0,3,MYVARNUM       NUMBER OF VARIABLE COLUMNS                   
         BZ    BLST14              NO VARIABLE COLUMNS                          
*                                                                               
BLST12   CLC   L.FDRNUM,DCTFLD#    TRY TO MATCH FIELD NUMBERS                   
         BE    BLSTX               MATCH                                        
         LA    RF,DCTABL(RF)                                                    
         BCT   R0,BLST12                                                        
*                                                                               
BLST14   LA    RF,MYVALCLM         VALID FOR DISPLAY?                           
         XR    R0,R0                                                            
         ICM   R0,3,MYVALNUM       NUMBER OF VALID COLUMNS                      
         BZ    BLST18              NO VALID COLUMNS                             
*                                                                               
BLST16   CLC   L.FDRNUM,DCTFLD#    TRY TO MATCH FIELD NUMBERS                   
         BE    BLSTX               MATCHED                                      
         LA    RF,DCTABL(RF)                                                    
         BCT   R0,BLST16                                                        
*                                                                               
BLST18   B     BLST                NOT MATCHED ON ANY OF DISPLAY TYPES          
*                                                                               
BLSTX    MVC   THIS.FDRRECD(FDRKLEN),IOKEY                                      
         B     EXITOK                                                           
         DROP  L,RF,THIS,K                                                      
         SPACE 2                                                                
***********************************************************************         
* LAST FOR SCREEN                                                     *         
***********************************************************************         
         SPACE 1                                                                
LSTSCR   CLI   BCPFKEY,PFK06       UPDATE PREFERENCES PFKEY                     
         BNE   EXITOK                                                           
*                                                                               
K        USING FCRRECD,IOKEY                                                    
         LA    R2,CLMTAB1                                                       
LSTS02   CLI   0(R2),EOT                                                        
         BNE   *+12                                                             
         MVI   ADDFLG,FF                                                        
         B     LSTS08              NEED TO SET A DEFAULT?                       
*                                                                               
         XC    K.FCRKEY,K.FCRKEY                                                
         MVI   K.FCRKMIN,FCRKMINQ                                               
         MVI   K.FCRKTYP,FCRKTYPQ                                               
         MVC   K.FCRKSYS,GCOVSYS   SYSTEM                                       
         MVC   K.FCRKPRG,GCPRGNO   PROGRAM                                      
         MVC   K.FCRKREC,PSREC     RECORD                                       
         TM    0(R2),CPAGE         PAGE REQUIRED?                               
         BZ    LSTS04                                                           
         MVI   K.FCRKPAGE,FCRKPLST LIST PAGE                                    
         TM    PSLSTAT1,LSSMAIN                                                 
         BZ    *+10                                                             
         MVC   K.FCRKPAGE,PSSMPAGE                                              
*                                                                               
LSTS04   TM    0(R2),CACTION       ACTION REQUIRED?                             
         BZ    *+10                                                             
         MVC   K.FCRKACT,PSACT     ACTION                                       
         TM    0(R2),CSBREC        SUB-RECORD REQUIRED?                         
         BZ    *+10                                                             
         MVC   K.FCRKSREC,P2REC    SUB-RECORD                                   
         TM    0(R2),CAGENCY       AGY ALPHA REQUIRED?                          
         BZ    *+10                                                             
         MVC   K.FCRKAGY,CUAALF    AGENCY ALPHA ID                              
         TM    0(R2),CUSER         PERSON ID REQUIRED?                          
         BZ    *+10                                                             
         MVC   K.FCRKPER,CUUSER    PERSON ID                                    
*                                                                               
         L     R1,=AL4(XOREAD+XOGENDIR+XIO2)                                    
         GOTOX ('XIO',AGROUTS)                                                  
         BE    LSTS06              GOT THIS RECORD?                             
         LA    R2,1(R2)                                                         
         B     LSTS02                                                           
*                                                                               
CLMTAB1  DC    AL1(CPAGE+CACTION+CSBREC+CAGENCY+CUSER)                          
         DC    AL1(CPAGE+CACTION+CAGENCY+CUSER)                                 
         DC    AL1(CPAGE+CAGENCY+CUSER)                                         
         DC    AL1(EOT)                                                         
*                                                                               
LSTS06   L     R1,=AL4(XOREAD+XOLOCK+XOGENFIL+XIO2)                             
         GOTOX ('XIO',AGROUTS)                                                  
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
LSTS08   L     R3,AIO2                                                          
         USING FCRRECD,R3                                                       
         XC    FCRKEY,FCRKEY       CHECK SUB-RECORD FOR USER ID                 
         MVI   FCRKMIN,FCRKMINQ                                                 
         MVI   FCRKTYP,FCRKTYPQ                                                 
         MVC   FCRKSYS,GCOVSYS     SYSTEM                                       
         MVC   FCRKPRG,GCPRGNO     PROGRAM                                      
         MVC   FCRKREC,PSREC       RECORD                                       
         MVI   K.FCRKPAGE,FCRKPLST LIST PAGE                                    
         TM    PSLSTAT1,LSSMAIN    MAINTENANCE LIST?                            
         BZ    *+10                                                             
         MVC   FCRKPAGE,PSSMPAGE   SET MAINT PAGE                               
*??      MVC   FCRKACT,PSACT       ACTION                                       
*??      MVC   FCRKSREC,P2REC      SUB-RECORD                                   
         MVC   FCRKAGY,CUAALF      AGENCY ALPHA ID                              
         MVC   FCRKPER,CUUSER      PERSON ID                                    
*                                                                               
LSTS10   LA    RF,FCRFIRST+1                                                    
         STCM  RF,3,FCRFLEN        SET EMPTY RECORD                             
         LA    RF,FCRRECD+FCRFIRST                                              
         MVI   0(RF),0             MAKE FIRST BYTE A 0                          
*                                                                               
         PUSH  USING                                                            
         USING FCRELD,BOELEM                                                    
         LA    R4,MYFIXCLM         FIXED COLUMNS                                
         USING DCTABD,R4                                                        
         LA    R5,1                COLUMN NUMBER                                
*                                                                               
LSSC06   OC    DCTFLD#,DCTFLD#     LOOK FOR FIRST EMPTY SLOT                    
         BZ    LSSC08                                                           
         XC    FCRELD(FCRLNQ),FCRELD                                            
         MVI   FCREL,FCRELQ                                                     
         MVI   FCRLN,FCRLNQ                                                     
         MVI   FCRTYPE,FCRTFIX                                                  
         STC   R5,FCRCNUM                                                       
         MVC   FCRFNUM,DCTFLD#                                                  
         MVC   FCRCIND,DCTINDS1                                                 
         GOTO1 AADDEL,BOPARM,FCRRECD,0                                          
         LA    R4,DCTABL(R4)                                                    
         LA    R5,1(R5)                                                         
         BCT   R0,LSSC06                                                        
*                                                                               
LSSC08   LA    R0,VARMAX           VARIABLE COLUMNS                             
         LA    R4,MYVARCLM                                                      
         LA    R5,1                                                             
*                                                                               
LSSC10   OC    DCTFLD#,DCTFLD#     LOOK FOR FIRST EMPTY SLOT                    
         BZ    LSSC12              FINISHED ADDING THESE                        
         XC    FCRELD(FCRLNQ),FCRELD                                            
         MVI   FCREL,FCRELQ                                                     
         MVI   FCRLN,FCRLNQ                                                     
         MVI   FCRTYPE,FCRTVAR                                                  
         STC   R5,FCRCNUM                                                       
         MVC   FCRFNUM,DCTFLD#                                                  
         MVC   FCRCIND,DCTINDS1                                                 
         GOTO1 AADDEL,BOPARM,FCRRECD,0                                          
         LA    R4,DCTABL(R4)                                                    
         LA    R5,1(R5)                                                         
         BCT   R0,LSSC10                                                        
*                                                                               
LSSC12   LA    R0,VALMAX           VALID FOR DISPLAY                            
         LA    R4,MYVALCLM                                                      
         LA    R5,1                                                             
*                                                                               
LSSC14   OC    DCTFLD#,DCTFLD#     LOOK FOR FIRST EMPTY SLOT                    
         BZ    LSSC16                                                           
         XC    FCRELD(FCRLNQ),FCRELD                                            
         MVI   FCREL,FCRELQ                                                     
         MVI   FCRLN,FCRLNQ                                                     
         MVI   FCRTYPE,FCRTVOK                                                  
         STC   R5,FCRCNUM                                                       
         MVC   FCRFNUM,DCTFLD#                                                  
         MVC   FCRCIND,DCTINDS1                                                 
         GOTO1 AADDEL,BOPARM,FCRRECD,0                                          
         LA    R4,DCTABL(R4)                                                    
         LA    R5,1(R5)                                                         
         BCT   R0,LSSC14                                                        
*                                                                               
LSSC16   CLI   ADDFLG,FF           ADDING OR CHANGING THIS RECORD?              
         BNE   LSSC18                                                           
         GOTOX ('PUTRAC',AGROUTS),BOPARM,('RACTADD+RACTCHA',FCRRECD)            
         B     LSSC20                                                           
LSSC18   GOTOX ('PUTRAC',AGROUTS),BOPARM,('RACTCHA',FCRRECD)                    
*                                                                               
LSSC20   L     R1,=AL4(XOPUTREC+XOGENFIL+XIO2)                                  
         CLI   ADDFLG,FF                                                        
         BNE   *+8                                                              
         L     R1,=AL4(XOADDREC+XOGENFIL+XIO2)                                  
         GOTOX ('XIO',AGROUTS)                                                  
         MVC   FVMSGNO,=AL2(5)                                                  
         CLI   ADDFLG,FF                                                        
         BNE   *+10                                                             
         MVC   FVMSGNO,=AL2(6)     THIS IS AWFUL...                             
         MVI   FVOMTYP,GTMINF                                                   
         B     EXITOK                                                           
         DROP  R3,R4                                                            
         SPACE 2                                                                
***********************************************************************         
* GET TSAR RECORD INFORMATION FROM DIRECTORY RECORD                   *         
***********************************************************************         
         SPACE 1                                                                
         USING TLSTD,R3                                                         
TSARDIR  LH    RF,=Y(TLENQ)                                                     
         STCM  RF,3,TLRLEN                                                      
         B     EXITOK                                                           
         DROP  R3                                                               
         EJECT                                                                  
***********************************************************************         
* GET TSAR RECORD INFORMATION FROM FILE RECORD                        *         
***********************************************************************         
         SPACE 1                                                                
         USING TLSTD,R3                                                         
         USING FDRRECD,R2                                                       
TSARFIL  LA    R4,FDRFIRST(R2)     A(FIRST ELEMENT)                             
         USING FDRELD,R4                                                        
*                                                                               
         XR    R1,R1                                                            
TSAF02   CLI   FDREL,0             LOOK FOR FIELD ELEMENT ON FIELD REC          
         BNE   *+6                                                              
         DC    H'0'                YOU'RE FUCKED IF THIS EVER HAPPENS           
         CLI   FDREL,FDRELQ                                                     
         BE    TSAF04                                                           
         IC    R1,FDRLN                                                         
         LA    R4,0(R1,R4)                                                      
         B     TSAF02                                                           
*                                                                               
TSAF04   MVC   TLKRNUM,FDRNUM      SET FIELD NUMBER                             
         MVI   TLTYP,0                                                          
         MVC   TLKRNAM,BCSPACES                                                 
         OC    FDRLHED1,FDRLHED1   FIRST HEADLINE?                              
         BZ    TSAF06                                                           
*                                                                               
         MVC   TLKRNAM(L'FDRLHED1),FDRLHED1                                     
         MVI   TLKRNAM,DD#ESCL     SET HEADLINE 1 LEFT ALIGNED                  
*                                                                               
         LA    RF,D_LEN            LENGTH OF DISPLAY FIELD                      
         CLI   FDRLHED2,DD#ESUL2   SECOND HEADLINE IS UNDERLINE?                
         BNL   *+8                 NO                                           
         SRL   RF,1                LENGTH OF DISPLAY FIELD/2                    
         CLM   RF,1,TLKRNAM+3      SHORTER THAN SPACE?                          
         BH    *+8                                                              
         STCM  RF,1,TLKRNAM+3      MAKE IT FIT                                  
*                                                                               
         ICM   RF,15,=C'SL  '      RESOLVE FIRST HEADLINE                       
         ICM   RF,2,FDRKSYS                                                     
         GOTO1 VDICTAT,BOPARM,(RF),TLKRNAM                                      
*                                                                               
TSAF06   CLI   FDRLHED2,DD#ESUL2   SECOND HEADLINE IS UNDERLINE?                
         BNL   TSAF08              YES                                          
         OC    FDRLHED2,FDRLHED2   SECOND HEADLINE?                             
         BZ    TSAF08              NO                                           
*                                                                               
         XC    BOWORK1,BOWORK1                                                  
         MVC   BOWORK1(L'FDRLHED2),FDRLHED2                                     
         ICM   RF,15,=C'SL  '      RESOLVE SECOND HEADLINE                      
         ICM   RF,2,FDRKSYS                                                     
         GOTO1 VDICTAT,BOPARM,(RF),BOWORK1                                      
*                                                                               
         LA    RF,TLKRNAM+D_LEN-1                                               
         CLI   0(RF),C' '          PUT HEADING 2 AFTER HEADING 1                
         BH    *+8                                                              
         BCT   RF,*-8                                                           
         MVC   1(1,RF),BCSLASH     PUT SLASH BETWEEN THEM                       
         LA    RF,2(RF)                                                         
         MVC   0(L'FDRLHED2,RF),FDRLHED2                                        
*                                                                               
         LA    RE,TLKRNAM+D_LEN-1                                               
         SR    RE,RF                                                            
         BCTR  RE,0                                                             
         EX    RE,*+4                                                           
         MVC   0(0,RF),BOWORK1                                                  
*                                                                               
TSAF08   MVI   TLKDIS,TLKNSEL      DEFAULT IS NOT YET SELECTED                  
         XR    R0,R0                                                            
         ICM   R0,3,MYFIXNUM       COUNT OF FIXED COLUMNS                       
         BZ    TSAF14              NO FIXED COLUMNS                             
*                                                                               
         LA    R1,1                                                             
         LA    RF,MYFIXCLM         SEE IF FIELD IS IN FIXED COLUMNS             
         USING DCTABD,RF                                                        
TSAF10   CLC   TLKRNUM,DCTFLD#     MATCH ON FIELD NUMBER?                       
         BE    TSAF12              MATCHED                                      
         LA    R1,1(R1)                                                         
         LA    RF,DCTABL(RF)                                                    
         BCT   R0,TSAF10                                                        
         B     TSAF14              NOT A FIXED COLUMN                           
*                                                                               
TSAF12   STC   R1,TLKPOS           SAVE POSITION                                
         MVI   TLKDIS,TLKFIX       SET FIXED COLUMN                             
         TM    DCTINDS1,DCTIOPEN   IS OPEN FOR EDITING?                         
         BZ    *+8                 NO                                           
         MVI   TLTYP,TLTOPEN+TLTCOPN                                            
         TM    DCTINDS1,DCTICOPN   CAN OPEN FOR EDITING?                        
         BZ    *+8                 NO                                           
         MVI   TLTYP,TLTCOPN                                                    
         B     EXITOK                                                           
*                                                                               
TSAF14   XR    R0,R0               NOT FIXED - TRY VARIABLE COLUMNS             
         ICM   R0,3,MYVARNUM       NUMBER OF VARIABLE COLUMNS                   
         BZ    TSAF20              NO VARIABLE COLUMNS                          
         LA    R1,1                                                             
         LA    RF,MYVARCLM                                                      
         USING DCTABD,RF                                                        
TSAF16   CLC   TLKRNUM,DCTFLD#     MATCH ON FIELD NUMBER?                       
         BE    TSAF18                                                           
         LA    R1,1(R1)                                                         
         LA    RF,DCTABL(RF)                                                    
         BCT   R0,TSAF16                                                        
         B     TSAF20                                                           
*                                                                               
TSAF18   STC   R1,TLKPOS           SAVE POSITION                                
         MVI   TLKDIS,TLKVAR       SET VARIABLE COLUMN                          
         TM    DCTINDS1,DCTIOPEN   IS OPEN FOR EDITING?                         
         BZ    *+8                 NO                                           
         MVI   TLTYP,TLTOPEN+TLTCOPN                                            
         TM    DCTINDS1,DCTICOPN   CAN OPEN FOR EDITING?                        
         BZ    *+8                 NO                                           
         MVI   TLTYP,TLTCOPN                                                    
         B     EXITOK                                                           
*                                                                               
TSAF20   B     EXITOK              NOT YET ASSIGNED                             
         DROP  R2,R3,RF                                                         
         EJECT                                                                  
***********************************************************************         
* LITERALS & CONSTANTS                                                *         
***********************************************************************         
         SPACE 1                                                                
         LTORG                                                                  
CNTRL    EQU   X'0A'                                                            
FF       EQU   X'FF'                                                            
FFFF     EQU   X'FFFF'                                                          
D_LEN    EQU   18                                                               
         SPACE 1                                                                
***********************************************************************         
* OVERLAY WORKING STORAGE                                             *         
***********************************************************************         
         SPACE 1                                                                
OVERWRKD DSECT                                                                  
CALLR1   DS    A                                                                
SVPARMS  DS    0XL24                                                            
SVPARMS1 DS    A                                                                
SVPARMS2 DS    A                                                                
SVPARMS3 DS    A                                                                
SVPARMS4 DS    A                                                                
SVPARMS5 DS    A                                                                
SVPARMS6 DS    A                                                                
*                                                                               
AFDREL   DS    A                                                                
*                                                                               
AMATCH   DS    A                                                                
TEMPP    DS    A                                                                
THISPNT  DS    A                                                                
NUMLEN   DS    XL1                                                              
NUMVAL   DS    XL1                                                              
THISMAX  DS    XL1                                                              
THISOPEN DS    XL1                                                              
THISLEFT DS    XL1                                                              
ADDFLG   DS    XL1                                                              
TMPCLM   DS    (VALMAX)XL(DCTABL)                                               
*                                                                               
*        GEFILWORK                                                              
         PRINT OFF                                                              
       ++INCLUDE GEFILWORK                                                      
         PRINT ON                                                               
*        ACMSGEQUS                                                              
         PRINT OFF                                                              
       ++INCLUDE ACMSGEQUS                                                      
         PRINT ON                                                               
*        DDFLDHDR                                                               
         PRINT OFF                                                              
       ++INCLUDE DDFLDHDR                                                       
         PRINT ON                                                               
*        DDDDEQUS                                                               
         PRINT OFF                                                              
       ++INCLUDE DDDDEQUS                                                       
         PRINT ON                                                               
*                                                                               
TWAD     DSECT                                                                  
         ORG   TWSAVE                                                           
MYSAVE   DS    0X                                                               
MYFIXNUM DS    H                   LOCAL DUPLICATES FROM LS STUFF               
MYFIXCLM DS    (FIXMAX)XL(DCTABL)                                               
FIXCLMLQ EQU   *-MYFIXCLM                                                       
FIXOVER  DS    XL(DCTABL)          OVERFLOW ??                                  
MYVARNUM DS    H                                                                
MYVARCLM DS    (VARMAX)XL(DCTABL)                                               
VARCLMLQ EQU   *-MYVARCLM                                                       
VAROVER  DS    XL(DCTABL)          OVERFLOW ??                                  
MYVALNUM DS    H                                                                
MYVALCLM DS    (VALMAX)XL(DCTABL)                                               
VALCLMLQ EQU   *-MYVALCLM                                                       
VALOVER  DS    XL(DCTABL)          OVERFLOW ??                                  
*                                                                               
TLSTD    DSECT                                                                  
         ORG   TLKSRT                                                           
TLKDIS   DS    XL1                                                              
TLKFIX   EQU   1                   COLUMN IS FIXED                              
TLKVAR   EQU   2                   COLUMN IS VARIABLE                           
TLKNSEL  EQU   3                   COLUMN IS NOT SELECTED                       
TLKPOS   DS    XL1                 DISPLAY POSITION                             
TLKRNAM  DS    XL(D_LEN)           LIST COLUMN HEADINGS                         
TLKRNUM  DS    XL2                 FIELD NUMBER                                 
*                                                                               
         ORG   TLUSER                                                           
TLTYP    DS    XL1                                                              
TLTOPEN  EQU   X'80'               COLUMN IS OPEN (UNPROTECTED)                 
TLTCOPN  EQU   X'40'               COLUMN IS MAY BE OPENED                      
TLENQ    EQU   *-TLSTD                                                          
*                                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'007GEFILCOL  09/25/06'                                      
         END                                                                    
