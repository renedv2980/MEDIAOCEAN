*          DATA SET ACACCLISTS AT LEVEL 011 AS OF 05/01/02                      
*CATALP ACLIST                                                                  
         TITLE 'ACACCLIST - ROUTINE TO PROCESS LIST RECORDS'                    
***********************************************************************         
* OLD STYLE                                                           *         
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *         
*              P1        BYTE 0    RETURN CODE                        *         
*                                  X'00' ERROR                        *         
*                                  C'I'  INCLUDE                      *         
*                                  C'E'  EXCLUDE                      *         
*                        BYTE 1-3  A(LIST RECORD)                     *         
*              P2        BYTE 0    X'FF' COMPARE USING ONLY THE       *         
*                                        SIGNIFICANT LENGTH OF THE    *         
*                                        LIST DATA NOT THE FULL       *         
*                                        SUBELEMENT LENGTH            *         
*                        BYTE 1-3  A(SEARCH ARGUMENT)                 *         
***********************************************************************         
* NEW STYLE                                                           *         
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *         
* INPUT        P1    1   BYTE 0    C'R'  READ IN LIST RECORD (ACCDIR) *         
*                                        RETURN F'0' ON ERROR         *         
*                                  C'S'  SEARCH FOR ENTRY             *         
*                                        RETURN CODE                  *         
*                                        X'00' ERROR                  *         
*                                        C'I'  INCLUDE                *         
*                                        C'E'  EXCLUDE                *         
*                                  C'B'  BUILD SORTED LIST IN AREA    *         
*                        BYTE 1-3  A(LIST RECORD)                     *         
*              PARAM 2   BYTE 0    IF P1(1)=S THEN IF X'FF' THEN      *         
*                                     ONLY SIGNIFICANT LENGTH OF THE  *         
*                                     LIST DATA NOT THE FULL SUBELEM  *         
*                                  IF P1(1)=R THEN AL1(COMPANY CODE)  *         
*                        BYTE 1-3  A(+/-LIST RECORD) OR A(ACCOUNT)    *         
*                                  IF P1(1)=R AND P2=A(ACCOUNT) THEN  *         
*                                     TO EXCLUDE, THE 1ST BYTE MUST   *         
*                                     HAVE THE X'40' TURNED OFF       *         
*                                  IF P1(1)=B THEN A(SORTED LIST AREA)*         
*              PARAM 3   BYTE 1-3  IF P1(1)=R THEN P3=A(DATAMGR)      *         
*                                  IF P1(1)=B THEN P3=F(L'SORT AREA)  *         
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *         
* OUTPUT                              RETURN                          *         
*                                     F(0) ERROR                      *         
*                                     F(NUMBER OF ENTRIES IN TABLE)   *         
***********************************************************************         
         EJECT                                                                  
         USING ACLSTD,RC                                                        
ACLIST   CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 ACLSTX-ACLSTD,*ACLIST*                                           
         MVC   LIST,0(R1)                                                       
         MVI   0(R1),0                                                          
         MVC   ARG,4(R1)                                                        
         MVI   ARG,0                                                            
         MVC   LENIND,4(R1)                                                     
         MVC   BLOCKLST,8(R1)                                                   
         XC    8(4,R1),8(R1)                                                    
         SPACE 1                                                                
         L     R2,LIST                                                          
         LA    R2,0(R2)            CLEAR HOB                                    
         LTR   R2,R2                                                            
         BZ    XIT                                                              
         MVC   KEYLEN,=Y(ACCRFST-ACCRECD) NEW FILE                              
         CLI   LIST,C'S'           SEARCH NEW FILE WAY                          
         BE    ACLIST1                                                          
         CLI   LIST,C'R'           READ IN LIST RECORD?                         
         BE    GETLIST                                                          
         CLI   LIST,C'B'           BUILD SORTED LIST RECORD?                    
         BE    BLDLIST                                                          
         MVC   KEYLEN,=Y(ACCORFST) OLD FILE                                     
*                                                                               
         USING LITELD,R2                                                        
ACLIST1  MVI   ELCODE,X'1E'                                                     
         BAS   RE,GETEL                                                         
         BNE   XIT                                                              
         ST    R2,ATYPE                                                         
         MVC   TYPE,LITTYPE        SAVE TYPE                                    
         CLI   LIST,C'S'           SEARCH NEW FILE WAY                          
*&&UK*&& BNE   ACLIST1A            UK INLCUDE U/L IN ARG (OLD WAY)              
*&&US*&& BNE   ACLIST2             US DOESN'T INCLUDE U/L IN ARG                
         CLI   TYPE,LITTMED        BILLING SOURCE  PASSED ?                     
         BE    *+8                 ADJUST                                       
*                                                                               
ACLIST1A CLI   TYPE,LITTACT        ACCOUNT PASSED ?                             
         BNE   ACLIST2                                                          
         L     R7,ARG                                                           
         MVC   LISTUL,0(R7)        SAVE OFF UNIT/LEDGER                         
         LA    R7,2(R7)            BUMP PAST UNIT/LEDGER                        
         ST    R7,ARG              SAVE IT                                      
         EJECT                                                                  
ACLIST2  L     R2,LIST                                                          
         MVI   ELCODE,X'1F'                                                     
         BAS   RE,GETEL                                                         
         BNE   XIT                                                              
*                                                                               
         USING LIDELD,R2                                                        
ACLIST3  CLI   LIST,C'S'           NEW SEARCH TYPE                              
         BNE   ACLIST4             NO, OLD TYPE                                 
         CLI   TYPE,LITTACT        ACCOUNT TYPE?                                
         BNE   ACLIST4             NO, SO DON'T WORRY ABOUT UNIT/LEDGER         
         CLC   LIDDLEDG,LISTUL     MATCH UNIT/LEDGER                            
         BNE   ACLIST9             LOOP FOR NEXT                                
*                                                                               
ACLIST4  ZIC   R3,LIDITLN          LENGTH OF EACH ITEM                          
         BCTR  R3,0                                                             
         CLI   LENIND,X'FF'                                                     
         BNE   ACLIST5                                                          
         LA    R3,LIDDACCS         GET LENGTH FOR PARTIAL MATCH                 
         ZIC   RE,LIDITLN                                                       
         AR    R3,RE                                                            
         BCTR  R3,0                                                             
         CLI   0(R3),C' '                                                       
         BNE   *+8                                                              
         BCT   R3,*-8                                                           
         LA    RE,LIDDACCS                                                      
         SR    R3,RE                                                            
ACLIST5  XR    R4,R4                                                            
         ZIC   R5,LIDLN                                                         
         SH    R5,=H'10'                                                        
         ZIC   R6,LIDITLN                                                       
         DR    R4,R6               R5 HAS NUMBER OF ITEMS                       
         LA    R4,LIDDACCS                                                      
         L     R7,ARG                                                           
         SPACE 1                                                                
ACLIST8  EX    R3,*+8                                                           
         B     *+10                                                             
         CLC   0(0,R7),0(R4)       SEARCH VS. DATA                              
         BE    MATCH                                                            
         ZIC   RF,LIDITLN          LENGTH OF EACH ITEM                          
         AR    R4,RF               NEXT DATA                                    
         BCT   R5,ACLIST8                                                       
ACLIST9  BAS   RE,NEXTEL                                                        
         BE    ACLIST3                                                          
*                                                                               
         USING LITELD,R2                                                        
NOMATCH  MVI   0(R1),C'E'                                                       
         L     R2,ATYPE            A(TYPE ELEMENT)                              
         CLI   LITUSE,LITUINCL     INCLUDE LIST AND I DIDN'T FIND IT            
         BE    XIT                 SO EXCLUDE ITEM                              
         MVI   0(R1),C'I'          EXCLUDE LIST AND I DIDN'T FIND IT            
         B     XIT                 SO INCLUDE ITEM                              
*                                                                               
MATCH    MVI   0(R1),C'I'                                                       
         L     R2,ATYPE                                                         
         CLI   LITUSE,LITUINCL     FOUND MATCH ON INCLUDE LIST                  
         BE    XIT                 SO INCLUDE                                   
         MVI   0(R1),C'E'          FOUND MATCH ON EXCLUDE LIST                  
*                                                                               
XIT      XMOD1                                                                  
         EJECT                                                                  
         USING LSTRECD,R2                                                       
GETLIST  LR    R6,R1               SAVE ADDRESS OF PARAMETER LIST               
         LA    R2,LISTKEY          BUILD KEY                                    
         XC    LSTKEY,LSTKEY                                                    
         MVI   LSTKTYP,LSTKTYPQ                                                 
         MVC   LSTKCPY,LISTCPY                                                  
         L     R3,ARG              ADDRESS OF LIST CODE                         
         MVC   LSTTYPE,0(R3)                                                    
         CLI   0(R3),C'+'          INCLUDE LIST                                 
         BE    GETLST05                                                         
         CLI   0(R3),C'-'          EXCLUDE LIST                                 
         BNE   GETACCT                                                          
*                                                                               
GETLST05 MVC   LSTKLST,1(R3)                                                    
         MVC   SAVEKEY,LSTKEY                                                   
         GOTO1 DATAMGR,LSTPARM,(X'88',READHI),ACCDIR,LISTKEY,LSTDIR             
         CLC   LSTDIR(LSTKEND),SAVEKEY                                          
         BNE   GETLSTNO                                                         
         LA    R2,LSTDIR                                                        
*                                                                               
GETLST10 MVC   DA,LSTKDA                                                        
         GOTO1 DATAMGR,LSTPARM,(X'88',GETREC),ACCMST,LSTKDA,LIST,LSTWRK         
         L     R2,LIST                                                          
         MVI   ELCODE,X'1E'                                                     
         BAS   RE,GETEL                                                         
         BNE   GETLSTNO                                                         
*                                                                               
         USING LITELD,R2                                                        
GETLST20 MVI   LITUSE,LITUINCL      INLCUDE LIST                                
         CLI   LSTTYPE,C'+'                                                     
         BE    *+8                                                              
         MVI   LITUSE,LITUEXCL      EXCLUDE LIST                                
         B     GETLST90                                                         
*                                                                               
GETLSTNO XC   0(4,R6),0(R6)                                                     
GETLST90 B    XIT                                                               
         EJECT                                                                  
***********************************************************************         
*  BUILD FAKE LIST RECORD OF SINGLE ACCOUNT ENTRY                     *         
***********************************************************************         
         SPACE 1                                                                
         USING LSTRECD,R2                                                       
GETACCT  L     RE,=A(IO)                                                        
         ST    RE,AIO                                                           
         MVC   LISTACC,0(R3)       SAVE ACCOUNT DATA                            
         L     R2,LIST                                                          
         XC    LSTKEY,LSTKEY                                                    
         MVC   LSTKCPY,LISTCPY                                                  
         MVI   LSTKTYP,LSTKTYPQ    X'1D'BUILD DUMMY LIST RECORD                 
         MVC   LSTKLST,SPACES                                                   
         XC    LSTKSTA(LSTRFST-LSTRLEN),LSTKSTA                                 
*                                                                               
         USING LITELD,R2                                                        
         AH    R2,KEYLEN           POINT TO FIRST ELEMENT                       
         ST    R2,ATYPE            SAVE ADDRESS                                 
         XC    0(LITLNQ,R2),0(R2)  CLEAR FOR ELEMENT                            
         MVI   LITEL,LITELQ        LIST TYPE ELEMENT X'1E'                      
         MVI   LITLN,LITLNQ        ELEMENT LENGTH                               
         MVI   LITUSE,LITUINCL     DEFAULT TO INCLUDE                           
         TM    LISTACC,X'40'       BUMP PAST UNIT/LEDGER TO TEST                
         BO    *+8                                                              
         MVI   LITUSE,LITUEXCL     EXCLUDE ACCOUNT FEATURE                      
         OI    LISTACC,X'40'                                                    
*                                                                               
         MVI   ACCLVL,0            SET LEVEL TO NONE                            
         MVI   ACCLEN,2            SET 2 FOR UNIT/LEDGER                        
         MVI   ELLEN,LIDDALVL-LIDELD   ELEMENT LENGTH                           
         MVI   TYPE,LITTLDG        LEDGER TYPE                                  
         CLC   LISTACC+2(12),SPACES                                             
         BE    GETACC20            BUILD X'1F' ELEMENT                          
*                                                                               
         MVI   ACCLVL,1            SET LEVEL TO ONE                             
         MVI   ACCLEN,12           SET DEFAULT                                  
         MVI   ELLEN,LIDDACCS-LIDELD+12      ELEMENT LENGTH                     
         MVI   TYPE,C'M'           MEDIA TYPE                                   
         CLC   LISTACC(2),SPACES   ANY UNIT/LEDGER?                             
         BE    GETACC20            NO                                           
*                                                                               
         MVI   TYPE,LITTACT        ACCOUNT TYPE                                 
         LA    R2,LISTKEY          GO READ UNIT/LEDGER                          
         USING LDGRECD,R2                                                       
         MVC   LDGKEY,SPACES       CLEAR TO SPACES                              
         MVC   LDGKCPY,LISTCPY                                                  
         MVC   LDGKUNT(2),LISTACC                                               
         MVC   SAVEKEY,LDGKEY                                                   
         GOTO1 DATAMGR,LSTPARM,(X'88',READHI),ACCDIR,LISTKEY,LSTDIR             
         CLC   SAVEKEY,LSTDIR                                                   
         BNE   GETACC20                                                         
         LA    R2,LSTDIR                                                        
         GOTO1 DATAMGR,LSTPARM,(X'88',GETREC),ACCMST,LDGKDA,IO,LSTWRK           
         L     R2,AIO                                                           
         MVI   ELCODE,ACLELQ       X'16' LEVEL LEDGER STRUCTURE                 
         BAS   RE,GETEL                                                         
         BNE   GETACC20                                                         
         LA    R0,1                LEVEL ONE                                    
         SR    R1,R1                                                            
         LA    R2,ACLLN1Q(R2)      POINT TO FIRST LEVEL                         
*                                                                               
         USING ACLVALS,R2                                                       
GETACC10 CLI   ACLVLEN,0           END OF LEVELS?                               
         BE    GETACC20            BUILD X'1F' ELEMENT                          
         IC    R1,ACLVLEN          LENGTH OF LEVEL                              
         LA    RE,LISTACC+2(R1)    POINT INTO ACCOUNT                           
         LA    RF,12                                                            
         SR    RF,R1               LENGTH OF COMPARE                            
         BZ    GETACC15                                                         
         BCTR  RF,0                                                             
         EX    RF,*+8                                                           
         BNH   GETACC15                                                         
         CLC   0(0,RE),SPACES                                                   
         AH    R2,=Y(L'ACLVALS)    BUMP TO NEXT LEVEL                           
         AH    R0,=H'01'           ADD LEVEL                                    
         B     GETACC10                                                         
*                                                                               
GETACC15 MVC   ACCLEN,ACLVLEN      SAVE LENGTH                                  
         STC   R0,ACCLVL           SAVE LEVEL NUMBER                            
         SR    R1,R1                                                            
         IC    R1,ACCLEN                                                        
         LA    R1,LIDDACCS-LIDELD(R1)   ELEMENT LENGTH                          
         STC   R1,ELLEN                                                         
*                                                                               
         USING LITELD,R2                                                        
GETACC20 L     R2,ATYPE            POINT TO LIST TYPE ELEMENT                   
         MVC   LITTYPE,TYPE        SAVE TYPE                                    
         SR    R1,R1                                                            
         IC    R1,LITLN                                                         
         AR    R2,R1                                                            
*                                                                               
         USING  LIDELD,R2                                                       
         XC     0(LIDDACCS-LIDELD+12,R2),0(R2)   LONGEST POSSIBLE ELEM          
         MVI    LIDEL,LIDELQ       X'1F' LIST DATA                              
         MVC    LIDLN,ELLEN        SAVE LENGTH                                  
         MVC    LIDITLN,ACCLEN     SAVE LENGTH OF DATA                          
         MVC    LIDDALVL,ACCLVL                                                 
         MVC    LIDDLEDG,LISTACC   MOVE IN POSSIBLE UNIT/LEDGER                 
         CLI    TYPE,C'M'          BILLING SOURCE?                              
         BNE    *+10                                                            
         MVC    LIDDLEDG,=C'ME'                                                 
         CLI    LIDLN,LIDDACCS-LIDELD                                           
         BNH    GETACC30                                                        
         IC     R1,LIDITLN                                                      
         BCTR   R1,0                                                            
         EX     R1,*+4                                                          
         MVC    LIDDACCS(0),LISTACC+2                                           
GETACC30 IC     R1,LIDLN                                                        
         AR     R2,R1                                                           
         MVI    0(R2),0            MARK END OF RECORD                           
         L      R3,LIST                                                         
         SR     R2,R3              LENGHT OF RECORD                             
*                                                                               
         USING  LSTRECD,R3                                                      
         STH    R2,LSTRLEN                                                      
         B      XIT                                                             
         DROP   R3                                                              
         EJECT                                                                  
         USING LSTRECD,R2                                                       
BLDLIST  LR    R6,R1               SAVE ADDRESS OF PARAMETER LIST               
         SR    R8,R8               N'ENTIRS IN TABLE                            
         XC    8(4,R6),8(R6)                                                    
*                                                                               
         USING LITELD,R2                                                        
         MVI   ELCODE,X'1E'        GET TYPE OF LIST                             
         BAS   RE,GETEL                                                         
         BNE   BLDLSTX                                                          
         MVC   TYPE,LITTYPE        SAVE TYPE                                    
*                                                                               
         USING LIDELD,R2                                                        
         L     R2,LIST                                                          
         MVI   ELCODE,X'1F'                                                     
         BAS   RE,GETEL                                                         
         BNE   BLDLSTX                                                          
*                                                                               
         L     R5,ARG              AREA TO BUILD LIST IN                        
         L     R1,BLOCKLST         SIZE OF AREA                                 
         SR    R0,R0               R1=MAX NUMBER OF KEYS ALLOWED                
         D     R0,=F'15'           LENGTH OF KEY                                
         CLI   TYPE,LITTLDG        LEDGER LIST?                                 
         BE    BLDLST30                                                         
*                                                                               
BLDLST05 ZIC   R0,LIDLN                                                         
         SH    R0,=Y(LIDDACCS-LIDELD)   LENGTH OF DATA                          
         MVC   LISTUL,LIDDAUNT     SAVE UNIT/LEDGER                             
         LA    R3,LIDDACCS         ACCOUNT                                      
*                                                                               
BLDLST10 ZIC   RF,LIDITLN          LENGTH OF EACH ITEM                          
         STC   RF,0(R5)            SAVE LENGTH OF ENTRY                         
         CLI   LENIND,X'FF'                                                     
         BNE   BLDLST20                                                         
         BCTR  RF,0                                                             
         AR    RF,R3               POINT TO END OF ACCOUNT-1                    
         CLI   0(RF),C' '          FIND NONE BLANK TO FIGURE LENGTH             
         BNE   *+8                                                              
         BCT   RF,*-8                                                           
         SR    RF,R3                                                            
         BNP   BLDLST28            GET NEXT WITHOUT INCREMENTING                
         LA    RF,1(RF)            ADD ONE FOR TRUE LENGTH                      
         STC   RF,0(R5)            SAVE NEW LENGTH                              
*                                                                               
BLDLST20 MVC   1(14,R5),SPACES                                                  
         ZIC   RF,0(R5)                                                         
         AH    RF,=H'02'           ADD TWO FOR UNIT LEDGER                      
         STC   RF,0(R5)            SAVE NEW LENGTH                              
         CLI   TYPE,C'M'           BILLING SOURCE TYPE?                         
         BE    *+10                                                             
         MVC   1(2,R5),LISTUL      SAVED UNIT/LEDGER                            
         IC    RF,LIDITLN          LENGTH OF EACH ITEM                          
         BCTR  RF,0                                                             
         EX    RF,*+4                                                           
         MVC   3(0,R5),0(R3)       MOVE IN DATA                                 
         LA    R5,15(R5)           BUMP TO NEXT ENTRY                           
         LA    R8,1(R8)            COUNT NUMBER OF ENTRIES ADDED                
         CR    R8,R1               IS TABLE FULL                                
         BNH   BLDLST28            YES                                          
         SR    R8,R8               SET ERROR                                    
         B     BLDLSTX                                                          
*                                                                               
BLDLST28 ZIC   RF,LIDITLN                                                       
         AR    R3,RF               BUMP TO NEXT POSSIBLE DATA                   
         SR    R0,RF                                                            
         BP    BLDLST10            BUILD NEXT ENTRY                             
         BAS   RE,NEXTEL                                                        
         BE    BLDLST05            ADD ANOTHER GROUP                            
         B     BLDLST40                                                         
*                                                                               
BLDLST30 IC    R8,LIDLN                                                         
         SH    R8,=Y(LIDDATA-LIDELD)                                            
         SRL   R8,1                NUMBER OF UNIT/LEDGERS IN LIST               
         CR    R8,R1               TABLE FULL                                   
         BNH   BLDLST32                                                         
         SR    R8,R8                                                            
         B     BLDLSTX             YES                                          
*                                                                               
BLDLST32 LR    R0,R8                                                            
         LA    R3,LIDDATA          POINT TO FIRST UNIT/LEDGER                   
BLDLST35 MVI   0(R5),2             LENGTH OF UNIT/LEDGER                        
         MVC   1(14,R5),SPACES     CLEAR                                        
         MVC   1(2,R5),0(R3)       SAVE OFF UNIT/LEDGER                         
         LA    R3,2(R3)            BUMP TO NEXT UNIT/LEDGER                     
         LA    R5,15(R5)                                                        
         BCT   R0,BLDLST35                                                      
         EJECT                                                                  
***********************************************************************         
*  SORT  TABLE HERE AND ELIMINATE POSSIBLE DUPLICATES                 *         
***********************************************************************         
         SPACE 1                                                                
BLDLST40 LR    R0,R8                                                            
         SH    R0,=H'01'           ONE LESS THEN NUM OF ENTRIES                 
         BNP   BLDLSTX                                                          
         LR    R5,R0                                                            
         MH    R5,=H'15'           POINT TO LAST ENTRY                          
         A     R5,ARG                                                           
         ST    R5,LASTNTRY         SAVE LAST ENTRY                              
*                                                                               
BLDLST42 L     R5,LASTNTRY         RESET TO LAST ENTRY                          
         SH    R5,=H'15'           BUMP BACK ONE ENTRY                          
         LR    R1,R0                                                            
*                                                                               
BLDLST45 CLC   1(14,R5),16(R5)                                                  
         BL    BLDLST60                                                         
         BH    BLDLST55                                                         
         SH    R8,=H'01'           LESS ONE ENTRY                               
         BNP   BLDLSTX             ONLY ONE ENTRY LEFT                          
         L     RE,LASTNTRY         ELIMINATE DUPLICATES                         
         XC    0(15,RE),15(R5)     SWAP WITH LAST ENTRY                         
         XC    15(15,R5),0(RE)                                                  
         XC    0(15,RE),15(R5)                                                  
         SH    RE,=H'15'           ONE LESS ENTRY                               
         ST    RE,LASTNTRY                                                      
         B     BLDLST45            LET'S TRY AGAIN                              
*                                                                               
BLDLST55 XC    0(15,R5),15(R5)     SWAP                                         
         XC    15(15,R5),0(R5)                                                  
         XC    0(15,R5),15(R5)                                                  
*                                                                               
BLDLST60 SH    R5,=H'15'                                                        
         BCT   R1,BLDLST45                                                      
         BCT   R0,BLDLST42                                                      
*                                                                               
BLDLSTX  ST    R8,8(R6)            RETURN NUMBER OF ENTRIES                     
         B     XIT                                                              
         EJECT                                                                  
         GETEL R2,KEYLEN,ELCODE                                                 
         SPACE 2                                                                
READHI   DC    CL8'DMRDHI'                                                      
GETREC   DC    CL8'GETREC'         GET RECORD                                   
ACCDIR   DC    CL8'ACCDIR'         DIRECTORY FILE FOR IO READ                   
ACCMST   DC    CL8'ACCMST'         FILE FOR IO READ                             
SPACES   DC    CL80' '             SPACES                                       
         SPACE 1                                                                
         LTORG                                                                  
IO       DS    XL2010                                                           
         EJECT                                                                  
ACLSTD   DSECT                                                                  
LIST     DS    A                   A(LIST RECORD)                               
ARG      DS    A                   A(SEARCH ARGUMENT)                           
BLOCKLST DS    A                   A(SIZE OF AREA)                              
         ORG   BLOCKLST                                                         
DATAMGR  DS    A                   A(DATAMGR)                                   
ATYPE    DS    A                   A(X'1E' ELEMENT)                             
DA       DS    A                   DISK ADDRESS OF RECORD                       
AIO      DS    A                                                                
ELCODE   DS    CL1                                                              
ELLEN    DS    XL1                                                              
KEYLEN   DS    H                                                                
TYPE     DS    CL1                 SAVE TYPE                                    
ACCLEN   DS    XL1                 LENGTH OF ACCOUNT DATA                       
ACCLVL   DS    XL1                                                              
LISTUL   DS    CL2                                                              
LENIND   DS    CL1                 USE FULL OR PARTIAL LENGTH                   
         ORG   LENIND                                                           
LISTCPY  DS    XL1                 COMPANY CODE                                 
LISTKEY  DS    CL42                                                             
LISTNTRY DS    F                                                                
LASTNTRY DS    A                                                                
LSTTYPE  DS    CL1                 +,- LIST TYPE                                
LSTPARM  DS    6F                                                               
LSTWRK   DS    12D                                                              
LSTDIR   DS    CL60                                                             
LISTACC  DS    CL14                UNIT/LEDGER/ACCT IF NOT LIST                 
SAVEKEY  DS    CL42                                                             
ACLSTX   EQU   *                                                                
         SPACE 2                                                                
*              INCLUDE ACGENFILE                                                
         PRINT OFF                                                              
       ++INCLUDE ACGENFILE                                                      
         PRINT ON                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'011ACACCLISTS05/01/02'                                      
         END                                                                    
