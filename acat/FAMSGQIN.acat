*          DATA SET FAMSGQIN   AT LEVEL 005 AS OF 11/18/15                      
*CATALP FAMSGQIN                                                                
                                                                                
         TITLE 'FACILITIES INPUT MESSAGE QUEUE HANDLER'                         
***********************************************************************         
* PARAMETER LIST FROM LINE CONTROL MODULE IS                          *         
* P1=A(SYSFAC)                                                        *         
* P2=A(MESSAGE BUFFER) PRECEDED BY 8 BYTE HEADER (SEE FATBHD)         *         
* P3=A(UTL ENTRY)                                                     *         
* RETURN WITH CC NEQ IF THE REPLY HAS BEEN PUT INTO MESSAGE BUFFER    *         
***********************************************************************         
         PRINT NOGEN                                                            
MSGQIN   CSECT                                                                  
         NMOD1 WRKL,**MSGQIN,R6                                                 
         USING WRKD,RC                                                          
         SAM31                     SET IN 31-BIT XA MODE                        
         ST    R1,SAVER1                                                        
         MVC   SRPARS,0(R1)        SAVE FIRST PART OF PARAM LIST                
*                                                                               
         L     RA,SRPAR1           RA=A(SYSFAC)                                 
         USING SYSFACD,RA                                                       
         L     R9,SRPAR3           R9=A(UTL ENTRY)                              
         USING UTLD,R9                                                          
*                                                                               
         NI    TTEST1,255-TTESTLLB RESET S/R LOADLIB FLAGS                      
         XC    SRINFO,SRINFO       CLEAR S/R INFO                               
         MVI   AUTOFLAG,0          CLEAR AUTO/GOBACK FLAG                       
         MVI   REPLY,0             SET NO REPLY IN BUFFER                       
         XC    TOKEN,TOKEN                                                      
         TM    TSTAT6,TST6SCRP     TEST SCRIPT EXECUTION IN PROCESS             
         BO    QIN4                                                             
         EJECT                                                                  
***********************************************************************         
* TEST TERMINAL AND MESSAGE TYPE                                      *         
***********************************************************************         
QIN1     TIME  TU                  R0=TRANSACTION ARRIVAL TIME                  
         ST    R0,TTIMETU                                                       
         XC    TSIN,TSIN           CLEAR UTL SIN                                
         XR    RF,RF                                                            
         ICM   RF,3,TTRCNT                                                      
         LA    RF,1(RF)                                                         
         STH   RF,TTRCNT           BUMP UTL TRANSACTION COUNTER                 
*                                                                               
QIN2     ICM   RE,15,TXPRNT        TEST IF INPUT FROM REMOTE PRINTER            
         BZ    QIN3                                                             
         MVC   TSVCREQ,$TOPRN      REGULAR VERSION IS T105                      
         TM    PRQATT2-PRQD(RE),PRQATOP                                         
         BZ    QINSE1                                                           
         MVC   TSVCREQ,$TOPRNA     ALTERNATE VERSION IS T102 (AT2=H)            
         B     QINSE1                                                           
         EJECT                                                                  
***********************************************************************         
* LOOK FOR VIRTUAL TERMINAL TOKEN AS FIRST SPECIAL STRUCTURED FIELD   *         
* REMOVE IT AND IF VALID SWITCH R9=A(VIRTUAL TERMINAL UTL)            *         
***********************************************************************         
QIN3     TM    TTYPE2,TTYPE2SF+TTYPE2SC TEST STRUCTURED FIELD MODE              
         BZ    QIN3X                                                            
         L     R4,SRPAR2                                                        
         CLC   4(4,R4),=X'88000DCF' TEST STRUCTURED FIELD TOKEN                 
         BNE   QIN3X                                                            
         MVC   TOKENIN(10),8(R1)   SAVE TOKEN INPUT DATA                        
*                                                                               
QIN3A    LR    RF,R4               RF=A(BUFFER)                                 
         AHI   RF,-(TBHL)                                                       
         LH    R1,TBHMSGL-TBHD(RF) R1=LENGTH OF DATA IN BUFFER                  
         LA    RE,5(R4)            RE=A(FIRST STRUCTURED FIELD)                 
         LA    R0,13               R0=LENGTH OF FIRST FIELD                     
         SR    R1,R0                                                            
         BNP   QIN3X               R1=L'BUFFER-L'FIELD                          
         STH   R1,6(RF)                                                         
         AR    R0,RE               R0=A(SECOND STRUCTURED FIELD)                
         LR    RF,R1                                                            
         MVCL  RE,R0               SHIFT BUFFER LEFT TO DELETE TOKEN            
*                                                                               
QIN3D    CLI   TOKENIN,C'>'        CHECK THAT TOKEN LOOKS OK                    
         BNE   QIN3E                                                            
         MVI   TOKENIN,C'0'        CONVERT TO ZERO FOR HEXIN                    
         GOTO1 VHEXIN,DUB,TOKENIN,TOKEN,10                                      
         OC    12(4,R1),12(R1)                                                  
         BZ    QIN3E               INVALID TOKEN                                
         BAS   RE,VALTOKEN                                                      
         BE    QIN3F                                                            
*                                                                               
QIN3E    OI    TVIFLAG,TVINVTOK    SET INVALID TOKEN RECEIVED                   
         B     QIN3X                                                            
*                                                                               
QIN3F    OI    TVIFLAG,TVTOW327          R9=A(VIRTUAL TERMINAL)                 
         OI    TVIFLAG-UTLD(RF),TVIVATOK RF=A(REAL TERMINAL)                    
*                                                                               
QIN3G    NI    TTEST1,255-TTESTLLB SET VIRUAL UTL DATA                          
         MVC   TTIMETU,TTIMETU-UTLD(RF)                                         
         XC    TSIN,TSIN           CLEAR VIRTUAL UTL SIN                        
         XR    R1,R1                                                            
         ICM   R1,3,TTRCNT                                                      
         LA    R1,1(R1)                                                         
         STH   R1,TTRCNT           BUMP VIRTUAL UTL TRANSACTIONS                
*                                                                               
QIN3X    EQU   *                                                                
         EJECT                                                                  
***********************************************************************         
* LOOK FOR SPECIAL DATA IN BUFFER AND QUEUE TO SPECIAL PROGRAM        *         
***********************************************************************         
QIN4     TM    TTYPE2,TTYPE2SF     TEST STRUCTURED FIELD XMT                    
         BZ    QIN4A                                                            
         MVC   TSVCREQ,$INDFILE                                                 
         B     QINSE1                                                           
*                                                                               
QIN4A    TM    TTYPE2,TTYPEMQ      TEST MQ SERIES MESSAGE                       
         BO    QINSE1                                                           
*                                                                               
QIN4B    L     R4,SRPAR2           TEST MESSAGE TYPE IN BUFFER HEADER           
         AHI   R4,-(TBHL)                                                       
         USING TBHD,R4                                                          
         TM    TBHINDS,TBHIREIN    TEST IF REINPUT MESSAGE FROM $CT             
         BO    QIN30                                                            
         TM    TBHINDS,TBHISCRP    TEST SCRIPT DEFINITION                       
         BO    QINSE0                                                           
         TM    TBHINDS,TBHIBACK    TEST GOBACK                                  
         BO    QIN9                                                             
*&&US                                                                           
         TM    TTYPE2,TTYPEECT     TEST REP ELECTRONIC CONTRACT TERM            
         BZ    *+14                                                             
         MVC   TSVCREQ,$CFI                                                     
         B     QINSE1                                                           
*&&                                                                             
         EJECT                                                                  
***********************************************************************         
* LOOK FOR VIRTUAL TERMINAL TOKEN AT (01,02) IN REAL 3270 BUFFER      *         
* REMOVE IT AND IF VALID SWITCH R9=A(VIRTUAL TERMINAL UTL)            *         
***********************************************************************         
QIN5     TM    TTYPE,TTYPE327      POINT TO REAL 3270 INPUT BUFFER              
         BZ    QIN5X                                                            
         TM    TVIFLAG,TVIVIRT                                                  
         BO    QIN5X                                                            
         NI    TVIFLAG,255-TVIVATOK-TVINVTOK                                    
         L     R1,SRPAR2                                                        
         CLC   7(3,R1),=X'1140C1'  IS FIRST FIELD AT (01,02)                    
         BNE   QIN5X                                                            
         CLI   10(R1),C'>'         DOES IT LOOK LIKE A VIRTUAL TOKEN            
         BNE   QIN5X                                                            
         MVC   TOKENIN(10),10(R1)  SAVE TOKEN INPUT DATA                        
         MVI   TOKENIN,C'0'        CONVERT TO ZERO FOR HEXIN                    
*                                                                               
QIN5A    LA    R1,10(R1)           POINT TO FIRST TEXT CHR                      
         LA    RF,20                                                            
         LR    RE,R1                                                            
QIN5B    CLI   0(RE),SBA           SEARCH FOR END OF FIELD AT (01,02)           
         BE    QIN5C                                                            
         CLI   0(RE),ETX                                                        
         BE    QIN5C                                                            
         LA    RE,1(RE)                                                         
         BCT   RF,QIN5B                                                         
         B     QIN5E               INVALID TOKEN                                
*                                                                               
QIN5C    LR    RF,RE               RF=A(FIELD DELIMITER)                        
         SR    RE,R1               RE=LEN OF DATA AT (01,02)                    
         LA    RE,3(RE)                                                         
         STC   RE,SRFLAGL          SET LENGTH OF TOKEN FIELD                    
         BAS   RE,DELFST           DELETE TOKEN                                 
*                                                                               
QIN5D    CLI   SRFLAGL,3+10        FORMAT IS >FTTTTCCCC                         
         BNE   QIN5E                                                            
         GOTO1 VHEXIN,DUB,TOKENIN,TOKEN,10                                      
         OC    12(4,R1),12(R1)                                                  
         BZ    QIN5E               INVALID TOKEN                                
         BAS   RE,VALTOKEN                                                      
         BE    QIN5F                                                            
*                                                                               
QIN5E    OI    TVIFLAG,TVINVTOK    SET INVALID TOKEN RECEIVED                   
         MVC   TSVCREQ,$TIMEOUT    TREAT SAME AS A TIME OUT                     
         L     R1,SRPAR2                                                        
         MVI   7(R1),ETX           DELETE ANY INPUT DATA FROM BUFFER            
         AHI   R1,-2                                                            
         MVC   0(2,R1),=H'8'                                                    
         B     QINSE1              PASS CONTROL TO $TIMEOUT                     
*                                                                               
QIN5F    OI    TVIFLAG,TVTOW327          R9=A(VIRTUAL TERMINAL)                 
         OI    TVIFLAG-UTLD(RF),TVIVATOK RF=A(REAL TERMINAL)                    
*                                                                               
QIN5G    NI    TTEST1,255-TTESTLLB SET VIRTUAL UTL INPUT DATA                   
         MVC   TTIMETU,TTIMETU-UTLD(RF)                                         
         XC    TSIN,TSIN           CLEAR VIRTUAL UTL SIN                        
         XR    R1,R1                                                            
         ICM   R1,3,TTRCNT                                                      
         LA    R1,1(R1)                                                         
         STH   R1,TTRCNT           BUMP VIRTUAL UTL TRANSACTIONS                
                                                                                
QIN5X    EQU   *                                                                
         EJECT                                                                  
***********************************************************************         
* TEST IF TERMINAL WITH ACTIVE SESSIONS HAS TIMED OUT                 *         
***********************************************************************         
QIN6     CLI   TSSBITS,0           TEST IF ANY ACTIVE SESSIONS                  
         BE    QIN7                                                             
         TM    TVIFLAG,TVIVIRT     SKIP VIRTUAL TERMINALS                       
         BO    QIN7                                                             
         CLI   TVICOUNT,0          SKIP REAL TERMINALS THAT OWN VIRTUAL         
         BNE   QIN7                                                             
         ICM   R1,15,TTIME         R1=TIME OF LAST TRANSACTION IN TUS           
         BZ    QIN7                                                             
         CLR   R0,R1               R0=TIME NOW IN TUS (1/38400 SEC)             
         BNL   *+8                                                              
         AL    R0,=X'C5C10000'     ADJUST FOR MIDNIGHT (24 HRS IN TUS)          
         SLR   R0,R1               R0=TIME SINCE LAST TRANSACTION               
         SR    RF,RF                                                            
         ICM   RF,1,TTIMEOUT       RF=TERMINAL TIME OUT IN 5 MIN UNITS          
         BZ    QIN7                                                             
         CHI   RF,255              MAX VALUE MEANS NO TIME OUT                  
         BE    QIN7                                                             
         M     RE,=A(5*60*38400)   RF=MAXIMUM TIME BEFORE TIME OUT              
         CLR   R0,RF                                                            
         BNH   QIN7                                                             
         TM    TSTAT,TSTATDDS      IGNORE TIME OUT FOR DDS TERMINALS            
         BNZ   QIN7                                                             
         NI    TSTAT2,255-TSTATNIT RESET ANY FIRST TIME FLAGS                   
         NI    TSTAT5,255-TST5PSWD                                              
         MVC   TSVCREQ,$TIMEOUT    SET TERMINAL HAS TIMED OUT                   
         TM    TTYPE,TTYPE327                                                   
         BZ    QINSE1                                                           
         L     R1,SRPAR2           POINT TO 3270 INPUT STREAM                   
         MVI   7(R1),ETX           DELETE ANY INPUT DATA FROM BUFFER            
         AHI   R1,-2                                                            
         MVC   0(2,R1),=H'8'                                                    
         B     QINSE1              PASS CONTROL TO $TIMEOUT                     
         EJECT                                                                  
***********************************************************************         
* NON INITIALISED 3270 TERMINAL                                       *         
* LOOK FOR TALK DIRTY STUFF AND VERSION CONTROL INFO IN BUFFER        *         
***********************************************************************         
QIN7     TM    TSTAT2,TSTATNIT     TEST TERMINAL NOT INITIALIZED                
         BZ    QIN9                                                             
         TM    TTYPE,TTYPE327                                                   
         BZ    QIN7G                                                            
*                                                                               
QIN7A    NI    TSTAT9,255-TSTNVRSN-TST9CMAD                                     
         NI    TSTATC,255-TSTCXSES                                              
*                                                                               
         L     R1,SRPAR2           POINT TO 3270 INPUT STREAM                   
         CLC   7(3,R1),DIRTYSBA                                                 
         BNE   QIN7G                                                            
         CLC   10(L'DDSVRSN,R1),DDSVRSN TEST IF USING COMPRESSION               
         BNE   QIN7E                                                            
*                                                                               
         LA    RE,6                FOLLOWING DDSVRSN# IS XXYYZZ DECIMAL         
         LA    RF,10+L'DDSVRSN(R1) WHERE EACH CORRESPONDS TO A 1BYTE            
QIN7B    CLI   0(RF),C'0'          VERSION INFO FIELD MUST BE NUMERIC           
         BL    QIN7F                                                            
         CLI   0(RF),C'9'                                                       
         BH    QIN7F               IGNORE INVALID VERSION INFO                  
         AHI   RF,1                                                             
         BCT   RE,QIN7B                                                         
*                                                                               
         PACK  DUB3,18(6,R1)                                                    
         L     RF,DUB3+4           NOW GET 3 BYTES INTO HOB OF RF               
         SRL   RF,4                LOSE SIGN                                    
         SLL   RF,8                                                             
         ST    RF,DUB3                                                          
*                                                                               
         CLC   DUB3(3),=X'030600'  FOR COMPRESSING $MAD                         
         BL    *+8                                                              
         OI    TSTAT9,TST9CMAD                                                  
*                                                                               
         CLC   DUB3(3),=X'040400'  FOR ALLOWING MORE THAN 4 SESSIONS            
         BNH   *+8                                                              
         OI    TSTATC,TSTCXSES                                                  
*                                                                               
         LA    RF,BADVTAB          LOOK FOR BAD VERSIONS                        
QIN7C    CLI   0(RF),0             TEST END OF TABLE                            
         BE    QIN7D                                                            
         CLC   DUB3(3),0(RF)       TEST FOR BAD VERSION                         
         BE    QIN7F                                                            
         AHI   RF,3                                                             
         B     QIN7C                                                            
*                                                                               
QIN7D    OI    TSTAT9,TSTNVRSN     SET NEW VERSION                              
         OI    TSTAT8,TST8BINT                                                  
         B     QIN7F                                                            
*                                                                               
QIN7E    CLC   10(L'DIRTYTX2,R1),DIRTYTX2 TEST DIRTY TEXT IN BUFFER             
         BE    QIN7F                                                            
         CLC   10(L'DIRTYTXN,R1),DIRTYTXN                                       
         BE    QIN7F                                                            
         CLC   10(L'DIRTYTXO,R1),DIRTYTXO                                       
         BNE   QIN7G                                                            
*                                                                               
QIN7F    MVI   7(R1),ETX           REMOVE DIRTY TEXT FROM BUFFER                
         AHI   R1,-2                                                            
         MVC   0(2,R1),=H'8'                                                    
         OI    TSTAT6,TST6STSS                                                  
         OI    TSTAT8,TST8STSS+TST8DRTY                                         
         MVI   TLVLDRTY,0          SET DIRTY LEVEL NUMBER                       
*                                                                               
QIN7G    EQU   *                                                                
*&&UK*&& B     QIN7G1              CAUSED SECURITY PROBLEMS IN UK               
         TM    TSTAT6,TST6STSS                                                  
         BO    QIN7H                                                            
         TM    TSTAT8,TST8STSS     IF SOFT STEREO DO THIS AT QIN30              
         BO    QIN7H                                                            
*                                                                               
QIN7G1   TM    TSTAT5,TST5PSWD     TEST PASSWORD CHECK REQUIRED                 
         BZ    QIN7H               NO                                           
         NI    TSTAT5,255-TST5PSWD RESET PASSWORD REQUIRED                      
         MVC   TSVCREQ,$SRPSWD     SET FOR $PSWD                                
         B     QIN7X                                                            
*                                                                               
QIN7H    MVC   TSVCREQ,$CT         SET TO $CT OR $RE                            
         NI    TSTAT2,255-TSTATNIT RESET STATUS BIT TO INHIBIT                  
         CLI   TSYS,0                                                           
         BE    QINSE1                                                           
         MVC   TSVCREQ,$RE                                                      
QIN7X    NI    TSTAT2,255-TSTATNIT RESET STATUS BIT TO INHIBIT                  
         B     QINSE1              WRITE OF SCREEN TO TEMPSTR                   
*                                                                               
BADVTAB  DS    0XL3                BAD VERSIONS                                 
         DC    AL1(00,00,00)                                                    
         DC    AL1(0)                                                           
         EJECT                                                                  
***********************************************************************         
* TEST IF THIS IS A SERVICE REQUEST MESSAGE                           *         
***********************************************************************         
QIN9     L     R1,SRPAR2           POINT TO START OF DATA                       
*                                                                               
QIN10    TM    TTYPE,TTYPETWX      ADDS/TWX TYPE TERMINAL                       
         BZ    QIN12                                                            
         LA    R1,2(R1)            POINT TO FIRST INPUT CHAR                    
         CLC   0(3,R1),=X'27E840'  IS IT ESC/Y/ROW 1                            
         BNE   QIN12                                                            
         LA    R1,4(R1)                                                         
         B     QIN20               GO SEE IF S/R DATA IN FIRST FIELD            
*                                                                               
QIN12    NI    TSTATC,255-TSTATFUP TURN OFF FALINK BULK UPLOAD                  
         CLC   4(3,R1),=X'00617F'  SPECIAL CURSOR VALUE (BULK UPLOAD)           
         BNE   *+8                                                              
         OI    TSTATC,TSTATFUP     FALINK BULK UPLOAD ON                        
*                                                                               
QIN12A   NI    TSTATD,255-TSTATPVP TURN OFF PRE-VALIDATE PASSWORD               
         CLC   4(3,R1),=X'00627F'  SPECIAL CURSOR VALUE FOR PVP                 
         BNE   *+8                                                              
         OI    TSTATD,TSTATPVP     PRE-VALIDATED PASSWORD ON                    
*                                                                               
QIN13    TM    TTYPE,TTYPE327      3270 TYPE TERMINAL                           
         BZ    QIN25                                                            
         TM    TBHINDS,TBHIAUTO    TEST AUTO S/R                                
         BO    QIN13A                                                           
         TM    TBHINDS,TBHIBACK    TEST GOBACK FROM S/R TO APPLIC               
         BO    QIN13E                                                           
         TM    TTYPE2,TTYPE2SC     TEST STRUCTURED CONVERSATION                 
         BO    QIN32                                                            
         B     QIN14                                                            
                                                                                
***********************************************************************         
* BUILD A MESSAGE IN BUFFER WITH AUTO S/R DATA                                  
***********************************************************************         
QIN13A   MVC   64(17,R1),0(R1)     SAVE S/R DATA                                
         XC    0(64,R1),0(R1)      CLEAR AREA                                   
         MVI   4(R1),0             SET AID TO ENTER                             
         MVC   5(2,R1),=X'5D7F'    FORCE CURSOR TO END OF SCREEN                
*                                                                               
         MVC   7(3,R1),=X'11407E'  SET SBA TO (01,63)                           
         MVC   10(17,R1),64(R1)    MOVE SAVED S/R                               
*                                                                               
         LA    RE,26(R1)           POINT TO LAST CHAR                           
         CLI   0(RE),C' '          TEST SIGNIFICANT CHAR                        
         BH    *+8                                                              
         BCT   RE,*-8              IF NOT BACK UP                               
*                                                                               
         MVI   1(RE),ETX                                                        
         LA    RE,2(RE)            POINT TO NEXT CHAR POSN                      
         SR    RE,R1               SUBTRACT STARTING ADDRESS                    
         STH   RE,TBHMSGL          SET MSG LENGTH IN BUFFER HEADER              
         MVI   AUTOFLAG,1          SET FLAG AUTO/GOBACK DATA IN BUFFER          
         B     QIN14                                                            
*                                                                               
QIN13B   MVC   07(3,R1),=X'1140C1' SET AUTO S/R IN SOFT FIELD MODE              
         MVI   10(R1),C'A'         STEREO MESSAGE TYPE CODE                     
         LR    R0,RE                                                            
         L     RE,VSSB                                                          
         ZIC   RF,SSBSYSID-SSBD(RE)                                             
         LR    RE,R0                                                            
         LA    RF,C'0'(RF)         FACPAK ID AS A NUMBER                        
         STC   RF,11(R1)                                                        
         ZIC   RF,TSESSION                                                      
         LA    RF,C'A'(RF)         SESSION ID AS A LETTER                       
         STC   RF,12(R1)                                                        
         MVC   13(5,R1),=X'F4D87DD8D8' ROW=01/UNP/COL=63/DLN=17/FLN=17          
         MVC   18(17,R1),64(R1)    MOVE SAVED S/R                               
*                                                                               
         LHI   RF,C'Q'             CALCULATE TRANSLATED EBCDIC LENGTH           
*                                  01-09=A-I,10-17=J-Q                          
         LA    RE,34(R1)           POINT TO LAST CHAR                           
QIN13C   CLI   0(RE),C' '          TEST SIGNIFICANT CHAR                        
         BH    QIN13D                                                           
         CHI   RF,C'J'                                                          
         BNE   *+12                                                             
         LHI   RF,C'I'                                                          
         B     *+8                                                              
         AHI   RF,-1                                                            
         BCT   RE,QIN13C           IF NOT BACK UP                               
*                                                                               
QIN13D   STC   RF,16(R1)           SET TRANSLATED STEREO DATA LENGTH            
         MVI   1(RE),ETX                                                        
         LA    RE,2(RE)            POINT TO NEXT CHAR POSN                      
         SR    RE,R1               SUBTRACT STARTING ADDRESS                    
         STH   RE,TBHMSGL          SET MSG LENGTH IN BUFFER HEADER              
         MVI   AUTOFLAG,1          SET FLAG AUTO/GOBACK DATA IN BUFFER          
         B     QIN14                                                            
                                                                                
***********************************************************************         
* BUILD A MESSAGE IN BUFFER TO GO BACK TO APPLICATION FROM S/R        *         
* BUFFER SET TO XL1=LEN OF ENTRY,XL2=PSEUDO SCR ADDR,CLN=DATA         *         
* UP TO 10 ENTRYS MAX LENGTH OF 100 ZERO MARKS LAST ENTRY             *         
***********************************************************************         
QIN13E   NI    TSTAT2,255-TSTATNIT ENSURE TSTATNIT BIT IS CLEAR                 
         MVC   256(256,R1),0(R1)   MOVE DATA PASSED BY S/R                      
         XC    0(256,R1),0(R1)     CLEAR AREA                                   
         MVI   4(R1),0             SET AID TO ENTER                             
         MVC   5(2,R1),=X'5D7F'    FORCE CURSOR TO END OF SCREEN                
*                                                                               
         LA    R0,10               SET MAX FIELD COUNT                          
         LA    R7,256(R1)          R7=S/R DATA                                  
         LA    R8,7(R1)            R8=FIRST FIELD                               
QIN13F   SR    R3,R3                                                            
         ICM   R3,1,0(R7)          L'DATA ELEMENT                               
         BZ    QIN13G              ZERO LEN IS END OF TEXT                      
         CLI   0(R7),X'64'                                                      
         BH    QIN13G              LEN > 100 IS BAD, JUST EXIT                  
         AHI   R3,-1                                                            
         EX    R3,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R8),0(R7)       INSERT DATA INTO BUFFER                      
         MVI   0(R8),SBA           REPLACE LEN WITH SBA                         
         SR    RE,RE                                                            
         ICM   RE,3,1(R8)          GET ABSOLUTE SCREEN ADDRESS                  
         SRDL  RE,6                CONVERT TO 3270 HARDWARE FORMAT              
         SRL   RF,2                                                             
         SLDL  RE,8                                                             
         STCM  RE,3,1(R8)                                                       
         OC    1(2,R8),=X'4040'    SET EBCDIC 3270 ADDRESS                      
         AR    R8,R3                                                            
         CLI   0(R8),C' '          SQUASH OUT NULLS & SPACES                    
         BH    *+8                                                              
         BCT   R8,*-8                                                           
         LA    R8,1(R8)            POINT TO NEXT ENTRY                          
         LA    R3,1(R3)                                                         
         AR    R7,R3                                                            
         BCT   R0,QIN13F           GO AND DO ANOTHER ONE                        
*                                                                               
QIN13G   MVI   0(R8),ETX           SET END OF TEXT                              
         LA    R8,1(R8)            POINT TO 'NEXT' CHAR POSN                    
         SR    R8,R1               SUBTRACT STARTING ADDRESS                    
         STH   R8,TBHMSGL          SET MSG LENGTH IN BUFFER HEADER              
         MVI   AUTOFLAG,1          SET FLAG AUTO/GOBACK DATA IN BUFFER          
*                                                                               
QIN14    CLC   INDREQ(L'INDREQ),10(R1) TEST FOR IND$FILE REQUEST                
         BNE   QIN14B                                                           
         MVI   4(R1),0             RESET ENTER KEY                              
         MVC   7(3,R1),=X'11407E'  SET SBA/ROW1/COL63                           
         MVC   10(L'INDREQSR,R1),INDREQSR MAKE IT LOOK LIKE A SVC REQ           
         MVC   20(3,R1),=X'11C261'                                              
*&&DO                                                                           
         LA    RE,18(R1)           POINT BEYOND =INDFILE                        
QIN14A   CLI   0(RE),SBA           TEST SBA                                     
         BE    QIN14B              YES - DONE                                   
         CLI   0(RE),ETX           TEST ETX                                     
         BE    QIN14B              YES - DONE                                   
         MVI   0(RE),X'00'         ELSE SET TO NULL                             
         LA    RE,1(RE)                                                         
         B     QIN14A                                                           
*&&                                                                             
QIN14B   CLI   4(R1),1             TEST PFK 01 THRU PFK 24                      
         BL    QIN16                                                            
         SR    RE,RE                                                            
         ICM   RE,3,5(R1)          CONVERT CURSOR ADDRESS                       
         SRDL  RE,6                                                             
         SRL   RE,2                                                             
         SRDL  RE,6                                                             
         SRL   RF,20                                                            
         CHI   RF,80               TEST IF CURSOR IN LINE 1                     
         BL    QIN15                                                            
*                                                                               
QIN14C   CLC   TSVCREQ+1(1),$BYE+1 TEST IF LEAVING S/R MODE                     
         BE    QIN16                                                            
         CLC   TSVCREQ+1(1),$SEARCH+1 TEST $SEARCH                              
         BE    QIN16                                                            
*&&US*&& CLI   TOVSYS,7            TALENT USES ALL 24 PF KEYS                   
*&&US*&& BE    *+12                                                             
         CLI   4(R1),22            PF22/23/24 ALWAYS MEAN =SS/=S-/=S+           
         BNL   QIN15                                                            
         OC    TSVCREQ,TSVCREQ     IS THIS A S/R                                
         BZ    QIN16               NO                                           
         CLI   4(R1),12            PF12/22/23/24 ARE VALID ON ANY LINE          
         BE    QIN15                                                            
         CLI   4(R1),22                                                         
         BL    QIN16                                                            
*                                                                               
QIN15    LLC   RE,4(R1)            SET PFKEY EQUATED VALUE                      
         AHI   RE,-1                                                            
         MHI   RE,17                                                            
         A     RE,APFKTAB                                                       
         MVI   04(R1),0            CHANGE AID FROM PFK TO ENTER                 
         MVC   05(02,R1),=X'407E'  CHANGE CURSOR ADDR TO S/R FIELD              
         TM    TSTAT6,TST6STSS                                                  
         BO    QIN15S                                                           
         TM    TSTAT8,TST8STSS     TEST FOR NEW STEREO MODE                     
         BO    QIN15S                                                           
         MVC   07(03,R1),=X'11407E'                                             
         MVC   10(17,R1),0(RE)     SET TEXT TO ASSOCIATED VALUE                 
         MVI   27(R1),ETX                                                       
         LR    RE,R1               MESSAGE LENGTH PRECEEDS DATA                 
         AHI   RE,-2                                                            
         MVC   0(2,RE),=H'28'      SET NEW MESSAGE LENGTH                       
         B     QIN16                                                            
*                                                                               
QIN15S   MVC   07(3,R1),=X'1140C1' PROCESS PFKEY IN SOFT FIELD MODE             
         MVI   10(R1),C'A'         STEREO MESSAGE TYPE CODE                     
         LR    R0,RE                                                            
         L     RE,VSSB                                                          
         ZIC   RF,SSBSYSID-SSBD(RE)                                             
         LR    RE,R0                                                            
         LA    RF,C'0'(RF)         FACPAK ID AS A NUMBER                        
         STC   RF,11(R1)                                                        
         ZIC   RF,TSESSION                                                      
         LA    RF,C'A'(RF)         SESSION ID AS A LETTER                       
         STC   RF,12(R1)                                                        
         MVC   13(5,R1),=X'F4D87DD8D8' ROW=01/UNP/COL=63/DLN=17/FLN=17          
         MVC   18(17,R1),0(RE)     SET TEXT TO ASSOCIATED PFKEY VALUE           
         MVI   35(R1),ETX          SET END OF DATA CODE                         
         LR    RE,R1               MESSAGE LENGTH PRECEEDS DATA                 
         AHI   RE,-2                                                            
         MVC   0(2,RE),=H'28'      SET NEW MESSAGE LENGTH                       
*                                                                               
QIN16    LA    R1,7(R1)            POINT TO FIRST INPUT FIELD                   
         CLC   0(3,R1),DIRTYSBA                                                 
         BNE   QIN16A                                                           
         CLC   3(L'DIRTYTXN,R1),DIRTYTXN TEST NEW/OLD TALK DIRTY TEXT           
         BE    *+14                                                             
         CLC   3(L'DIRTYTXO,R1),DIRTYTXO                                        
         BNE   QIN16A                                                           
         MVI   0(R1),ETX           REMOVE DIRTY TEXT FROM BUFFER                
         L     RE,SRPAR2                                                        
         AHI   RE,-2                                                            
         MVC   0(2,RE),=H'8'                                                    
         OI    TSTAT6,TST6STSS                                                  
         OI    TSTAT8,TST8STSS+TST8DRTY                                         
         MVI   TLVLDRTY,0          SET DIRTY LEVEL NUMBER                       
QIN16A   CLC   0(3,R1),=X'11407E'  IS FIRST FIELD AT (01,63)                    
         BE    QIN19               YES VALIDATE INPUT IN S/R FIELD              
*&&US                                                                           
QIN16B   TM    TTYPE2,TTYPE2BI     TEST BIAS TERMINAL                           
         BZ    QIN16C                                                           
         CLI   TSYS,0              IF NOT CONNECTED SEND TO $CT                 
         BNE   *+14                                                             
         MVC   TSVCREQ,$CT                                                      
         B     QINSE1                                                           
         BAS   RE,SETSBA           FIX SBA ADDRESSES FROM BIAS                  
         B     QIN30                                                            
QIN16C   EQU   *                                                                
*&&                                                                             
         EJECT                                                                  
***********************************************************************         
*FIRST 3270 INPUT FIELD IS AT (01,02)                                 *         
***********************************************************************         
QIN17    CLC   0(3,R1),=X'1140C1'  IS FIRST FIELD AT (01,02)                    
         BNE   QIN30               NO MUST BE ORDINARY INPUT DATA               
         LA    R1,3(R1)                                                         
         LA    RF,80                                                            
         LR    RE,R1                                                            
QIN17A   CLI   0(RE),SBA           SEARCH FOR END OF FIELD AT (01,02)           
         BE    QIN17B                                                           
         CLI   0(RE),ETX                                                        
         BE    QIN17B                                                           
         LA    RE,1(RE)                                                         
         BCT   RF,QIN17A                                                        
         B     QIN25                                                            
QIN17B   LR    RF,RE               RF=A(FIELD DELIMITER)                        
         SR    RE,R1               RE=LEN OF DATA AT (01,02)                    
         LA    RE,3(RE)                                                         
         STC   RE,SRFLAGL          SET DATA LEN                                 
         CLI   AUTOFLAG,0          TEST AUTO/GOBACK DATA IN BUFFER              
         BNE   QIN17C                                                           
         TM    TSTAT6,TST6STSS                                                  
         BO    QIN18                                                            
         TM    TSTAT8,TST8STSS     TEST DIRTY STEREO DATA IN BUFFER             
         BO    QIN18                                                            
QIN17C   OI    SRFLAG,X'01'        SET DATA INPUT AT (01,02)                    
         TM    TSTAT2,TSTATCC      TERMINAL MUST BE IN $CC MODE                 
         BZ    QIN25                                                            
QIN17D   CLC   0(3,RF),=X'11407E'  TEST FIELD AT (01,63)                        
         BNE   QIN25               NO                                           
         LR    R1,RF               YES POINT TO S/R FIELD                       
         B     QIN19                                                            
         EJECT                                                                  
***********************************************************************         
* STEREO SOFT FIELD MODE HAS 24 LINES OF 79 BYTE UNPROTECTED FIELDS   *         
* FIRST THREE BYTES OF FIRST FIELD IS THE STEREO HEADER.              *         
* 1ST CHR = A=STANDARD SCREEN / M=$MAD SCREEN / Z=$MAD WITH SWAP FLAG *         
*           LOWER CASE FOR CHANGED FIELDS ONLY                        *         
* 2ND CHR = FACPAK ID                                                 *         
* 3RD CHR = SESSION ID                                                *         
***********************************************************************         
QIN18    CLI   0(R1),C'A'          STANDARD STEREO HEADER                       
         BE    QIN18A                                                           
         CLI   0(R1),X'81'                                                      
         BE    QIN18A                                                           
         CLI   0(R1),C'N'          STANDARD STEREO HEADER WITH                  
         BE    QIN18A              UNSWAPPABLE SESSION FLAG                     
         CLI   0(R1),X'95'                                                      
         BE    QIN18A                                                           
         CLI   0(R1),C'M'          MAD STEREO HEADER                            
         BE    QIN18C                                                           
         CLI   0(R1),C'Z'          MAD STEREO HEADER                            
         BE    QIN18C              WITH UNSWAPPABLE SESSION FLAG                
         CLI   0(R1),X'82'                                                      
         BE    QIN18C                                                           
         CLI   0(R1),C'C'          OPEN STEREO SESSION HEADER                   
         BE    QIN18B                                                           
         CLI   0(R1),X'83'                                                      
         BE    QIN18B                                                           
         B     QIN18G                                                           
*                                                                               
QIN18A   CLI   3(R1),X'A7'         TEST STEREO COMMAND IN BUFFER                
         BNE   QIN18B                                                           
         LA    R1,2(R1)            BUMP PAST SET TRANSFER SESSION CMD           
         B     QIN18B                                                           
QIN18B   CLI   3(R1),X'F4'         TEST ROW 01                                  
         BNE   QIN18H                                                           
         CLI   5(R1),X'7D'         TEST COLUMN 63                               
         BNE   QIN18H                                                           
         CLI   7(R1),X'D8'         TEST MAX FIELD LENGTH 17                     
         BNE   QIN18H                                                           
         CLI   6(R1),X'A1'         TEST DATA LENGTH WITH ZERO                   
         BE    QIN18H                                                           
         B     QIN18D                                                           
*                                                                               
QIN18C   LA    R1,63(R1)           STEREO MAD FORMAT                            
         LA    RF,17               S/R FIELD IS LAST 16 BYTES                   
         LR    RE,R1                                                            
         B     QIN19A                                                           
QIN18D   CLI   6(R1),X'C1'         TEST LENGTH 01 THRU 09 CHRS                  
         BL    QIN18E                                                           
         CLI   6(R1),X'C9'                                                      
         BH    QIN18E                                                           
         LLC   RE,6(R1)                                                         
         AHI   RE,-192                                                          
         B     QIN18F                                                           
QIN18E   CLI   6(R1),X'D1'         TEST LENGTH 10 THRU 17 CHRS                  
         BL    QIN18J                                                           
         CLI   6(R1),X'D8'                                                      
         BH    QIN18J                                                           
         LLC   RE,6(R1)                                                         
         AHI   RE,-208                                                          
         LA    RE,9(RE)                                                         
*                                                                               
QIN18F   LA    R1,8(R1)            POINT TO START OF S/R DATA                   
         STC   RE,SVSTRLEN         RE=LENGTH OF S/R FIELD                       
         AR    RE,R1               POINT PAST END OF S/R DATA                   
         B     QIN19B              VALIDATE S/R DATA                            
*                                                                               
QIN18G   NI    TSTAT6,255-TST6STSS INVALID STEREO HEADER                        
         NI    TSTAT8,255-TST8STSS                                              
         B     QIN25                                                            
QIN18H   B     QIN30               STEREO DATA IS NOT A S/R                     
QIN18J   B     QIN25               INVALID STEREO S/R                           
         EJECT                                                                  
***********************************************************************         
*THERE IS 3270 INPUT DATA IN SERVICE REQUEST FIELD AT (01,63)         *         
***********************************************************************         
QIN19    LA    R1,3(R1)            POINT TO S/R FLD DATA                        
         LA    RF,18                                                            
         LR    RE,R1                                                            
QIN19A   CLI   0(RE),SBA           FIND END OF S/R DATA                         
         BE    QIN19B                                                           
         CLI   0(RE),ETX                                                        
         BE    QIN19B                                                           
         LA    RE,1(RE)                                                         
         BCT   RF,QIN19A                                                        
         B     QIN25               S/R ERROR IF DATA LEN GT MAX                 
*                                                                               
QIN19B   SR    RE,R1               R1=A(START) AND RE=A(END)                    
         BZ    QIN30               NOT A S/R IF EMPTY FIELD                     
         AHI   RE,-1                                                            
         EX    RE,*+8                                                           
         B     *+10                                                             
         TR    0(0,R1),UPPER       CONVERT TO UPPER CASE                        
*                                                                               
QIN19C   CLI   0(R1),C'>'          TEST TOKEN IN S/R FIELD                      
         BNE   QIN20                                                            
         TM    TSTAT1,TSTATDDS     OK FOR DDS TERMINALS                         
         BZ    QIN20                                                            
         MVI   0(R1),C'*'          CONVERT TO COMMENT                           
         MVC   TOKENIN,0(R1)       SAVE TOKEN INPUT DATA                        
         MVI   TOKENIN,C'0'        CONVERT TO ZERO FOR HEXIN                    
         LA    RE,1(RE)                                                         
         CHI   RE,10               >FTTTTIIII IS MINIMUM FORMAT                 
         BL    QIN30                                                            
         ST    R1,DUB3             SAVE START AND LENGTH                        
         ST    RE,DUB3+4                                                        
         GOTO1 VHEXIN,DUB,TOKENIN,TOKEN,10                                      
         OC    12(4,R1),12(R1)                                                  
         BZ    QIN30               IGNORE INVALID TOKENS                        
         BAS   RE,VALTOKEN                                                      
         BNE   QIN30                                                            
         L     R1,DUB3             RESTORE START AND LENGTH                     
         L     RE,DUB3+4                                                        
         CLI   TOKENIN+10,C'='     TEST IF S/R ALSO PRESENT                     
         BNE   QIN30                                                            
         AHI   RE,-2                                                            
         MVI   0(R1),C' '          SPACE FILL THE S/R FIELD                     
         EX    RE,*+8                                                           
         B     *+10                                                             
         MVC   1(0,R1),0(R1)                                                    
         AHI   RE,-9               RE=L'RESIDUAL-1                              
         BNP   QIN30                                                            
         EX    RE,*+8                                                           
         B     QIN21                                                            
         MVC   0(0,R1),TOKENIN+10  RESTORE S/R TO START OF S/R FIELD            
         EJECT                                                                  
***********************************************************************         
* R1=A(DATA IN S/R FIELD). MUST START WITH VALID SPECIAL CHARACTER    *         
* IF VALID TRY TO MATCH TEXT WITH A SERVICE REQUEST NAME              *         
***********************************************************************         
QIN20    CLI   0(R1),C'*'          TEST FIRST BYTE OF S/R                       
         BE    QIN30                                                            
         CLI   0(R1),C' '                                                       
         BE    QIN30                                                            
         CLI   0(R1),C'='          S/R STARTS WITH =                            
         BE    QIN21                                                            
         CLI   0(R1),C'$'          S/R STARTS WITH $                            
         BE    QIN21                                                            
         CLI   0(R1),C'+'          S/R STARTS WITH +                            
         BE    QIN21                                                            
         CLI   0(R1),C''          ALLOW ALTERNATE CURRENCY SYMBOL              
         BNE   QIN25                                                            
         MVI   0(R1),C'$'                                                       
         OI    SRFLAG,X'80'        SET PRIORITY FLAG                            
*                                                                               
QIN21    L     R3,VSELIST          TRY TO MATCH INPUT TO S/R NAME               
         LA    R3,6(R3)            POINT TO SE1                                 
         USING SELISTD,R3                                                       
         L     R3,SEPGMS           PROGRAM (S/R) NAME LIST ADDRESS              
         USING PGMLSTD,R3                                                       
         BAS   RE,SETBXLE                                                       
*                                                                               
QIN22    BAS   RE,SETLEN           SET LEN OF S/R NAME FOR EX                   
         EX    R8,QINCLC                                                        
         BNE   QIN22A                                                           
         ST    R3,SRAPGM                                                        
         STC   R8,SVPGMLEN                                                      
         LA    R8,2(R8,R1)         POINT 1 BEYOND LAST CHAR                     
         CLI   0(R8),C' '                                                       
         BE    QIN24                                                            
         CLI   0(R8),X'00'                                                      
         BE    QIN24                                                            
         CLI   0(R8),ETX                                                        
         BE    QIN24                                                            
         CLI   0(R8),LF                                                         
         BE    QIN24                                                            
         CLI   0(R8),SBA                                                        
         BE    QIN24                                                            
         CLI   0(R8),C'.'                                                       
         BE    QIN24                                                            
         CLI   0(R8),C'-'                                                       
         BE    QIN24                                                            
         CLI   0(R8),C','                                                       
         BE    QIN22B                                                           
         CLI   0(R8),C'/'          S/R IN TEST MODE                             
         BE    QIN23D                                                           
         CLI   AUTOFLAG,0          TEST AUTO/GOBACK DATA IN BUFFER              
         BNE   QIN22A                                                           
         TM    TSTAT6,TST6STSS                                                  
         BO    *+12                                                             
         TM    TSTAT8,TST8STSS     TEST DIRTY STEREO FORMAT                     
         BNO   QIN22A                                                           
         CLI   0(R8),C'?'          TEST COMPRESSED BLANKS SEQUENCE              
         BNE   *+12                                                             
         CLI   2(R8),C' '                                                       
         BE    QIN24                                                            
         IC    RE,SVPGMLEN                                                      
         LA    RE,2(RE)                                                         
         CLM   RE,1,SVSTRLEN       TEST EXACT MATCH ON S/R FOR STEREO           
         BE    QIN24                                                            
QIN22A   BXLE  R3,R4,QIN22                                                      
         B     QIN23                                                            
         EJECT                                                                  
***********************************************************************         
* S/R NAME FOLLOWED BY COMMA - TEST IF SPECIAL FEATURES FOR S/R       *         
***********************************************************************         
QIN22B   CLC   PGMNUM,$KWX+1       CHECK FOR $KWX,X                             
         BNE   QIN22C                                                           
         CLI   1(R8),C'A'                                                       
         BL    QIN26                                                            
         CLI   1(R8),C'I'                                                       
         BH    QIN26                                                            
         MVO   TSVCREQ(1),1(1,R8)  SAVE $KWX ROOT SCREEN NUMBER                 
         B     QIN26+4                                                          
QINCLC   CLC   PGMNAME(0),1(R1)                                                 
*                                                                               
QIN22C   CLC   PGMNUM,$DISP+1      CHECK FOR $DISP,AOR# OR TOR                  
         BE    QIN22CA                                                          
         CLC   PGMNUM,$LOAD+1      CHECK FOR $LOAD,AOR# OR TOR                  
         BE    QIN22CA                                                          
         B     QIN22CD                                                          
*                                                                               
QIN22CA  CLC   1(3,R8),=C'AOR'                                                  
         BNE   QIN22CC                                                          
         LA    R1,1                1 DIGIT NUMBER                               
         CLI   4(R8),C'0'                                                       
         BL    QIN26                                                            
         CLI   4(R8),C'9'                                                       
         BH    QIN26                                                            
         CLI   5(R8),C' '                                                       
         BNH   QIN22CB                                                          
         LA    R1,1(R1)            2 DIGIT NUMBER                               
         CLI   5(R8),C'0'                                                       
         BL    QIN26                                                            
         CLI   5(R8),C'9'                                                       
         BH    QIN26                                                            
*                                                                               
QIN22CB  AHI   R1,-1              PACK NUMBER INTO DUB2                         
         EX    R1,*+8                                                           
         B     *+10                                                             
         PACK  DUB2,4(0,R8)                                                     
         CVB   R1,DUB2            GET BINARY EQUIVALENT                         
         CHI   R1,SBFACMAX        MAX IS 32                                     
         BH    QIN26              TOO BIG                                       
*                                                                               
         SLL   R1,2               USE BITS 1-6 (0=X'80') FOR NUMBER             
         STCM  R1,1,TSVCREQ       SET NUMBER IN FIRST BYTE                      
         OI    TSVCREQ,X'80'      FLAG IT AS SET                                
         B     QIN26A                                                           
*                                                                               
QIN22CC  CLC   1(3,R8),=C'TOR'                                                  
         BNE   QIN22CD                                                          
         MVI   TSVCREQ,X'80'                                                    
         B     QIN26A                                                           
*                                                                               
QIN22CD  CLC   PGMNUM,$DUMP+1      CHECK FOR $DUMP,L                            
         BNE   QIN22D                                                           
         CLI   1(R8),C'L'                                                       
         BNE   QIN26                                                            
         OI    TSVCREQ,X'E0'       SAVE $DUMP ROOT SCREEN NUMBER                
         B     QIN26+4                                                          
*                                                                               
QIN22D   CLC   PGMNUM,$RE+1        CHECK FOR $RE,S=ABC FOR STEREO               
         BNE   QIN24                                                            
         CLC   1(2,R8),=C'S='                                                   
         BNE   QIN26                                                            
         L     RE,TBUFF            SAVE =RE,S=ABC DATA AT TBUFF+1024            
         LA    RE,1024(RE)                                                      
         MVC   0(4,RE),=C'=RE,'                                                 
         MVC   4(6,RE),1(R8)                                                    
         B     QIN26               MONITOR WILL PROCESS S=ABC DATA              
         EJECT                                                                  
***********************************************************************         
* CHECK FOR $AAAA/XX/ TO IDENTIFY A S/R NOT IN PROGRAM LIST           *         
***********************************************************************         
QIN23    LA    R0,7                                                             
         LA    R8,1(R1)                                                         
QIN23A   CLI   0(R8),C'/'                                                       
         BE    QIN23C                                                           
         CLI   0(R8),SBA                                                        
         BE    QIN23B                                                           
         CLI   0(R8),ETX                                                        
         BE    QIN23B                                                           
         LA    R8,1(R8)                                                         
         BCT   R0,QIN23A                                                        
*                                                                               
QIN23B   MVC   TSVCREQ,$CT         SET TO PASS TO $CT AS $PGMNAME               
         L     RE,TBUFF                                                         
         AHI   RE,-(TBHL)                                                       
         TM    TBHINDS-TBHD(RE),TBHIREAL                                        
         BZ    *+8                                                              
         NI    TSTAT1,X'FF'-TSTATASW RESET AUTOSWAP FLAG                        
         B     QIN28               $CT WILL CHECK CONNECT STATUS                
*                                                                               
QIN23C   LA    R3,$SRDUMMY         POINT TO DUMMY PGMLIST ENTRY                 
         MVC   HALF,1(R8)          COPY 'XX' LOCALLY                            
         GOTO1 VHEXIN,DUB,HALF,PGMNUM,2                                         
         OC    12(4,R1),12(R1)                                                  
         BZ    QIN25                                                            
         LA    R8,3(R8)                                                         
         CLI   0(R8),C'/'                                                       
         BNE   QIN25                                                            
*                                                                               
QIN23D   CLI   1(R8),C'Y'          CHECK TEST FOR Y OR LEVEL A                  
         BE    *+16                                                             
         CLI   1(R8),C'A'                                                       
         BNE   QIN25                                                            
         OI    TTEST1,TTESTSRA     SET S/R USES LOADIB A VERSION                
         OI    TTEST1,TTESTSRY     SET S/R USES LOADIB                          
         TM    TSTAT1,TSTATDDS     USER TRMS CANT BE IN S/R TEST MODE           
         BNZ   QIN24                                                            
         NI    TTEST1,255-TTESTLLB RESET S/R LOADLIB FLAGS                      
         B     QIN25                                                            
         EJECT                                                                  
***********************************************************************         
* INPUT IS A SERVICE REQUEST                                          *         
***********************************************************************         
QIN24    CLC   PGMNUM,TSVCREQ+1    TEST SAME S/R AS LAST TIME                   
         BNE   QIN24A                                                           
         NI    TSVCREQ,255-X'01'   CLEAR FIRST TIME FLAG                        
         B     QIN28                                                            
QIN24A   TM    PGMIND,PGMIACC      TEST IF RESTRICTED S/R                       
         BZ    QIN26                                                            
         TM    TSTAT1,TSTATDDS     OK FOR DDS TERMINALS                         
         BNZ   QIN26                                                            
*                                                                               
QIN25    MVC   TSVCREQ,$SRERR      PASS TO S/R ERROR                            
         NI    TSTAT2,255-TSTATCC                                               
         TM    SRFLAG,X'01'                                                     
         BZ    *+8                                                              
         BAS   RE,DELFST           DELETE (01,02) DATA IF INPUT                 
         CLI   TSYS,0                                                           
         BNZ   QINSE1                                                           
         MVC   TSVCREQ,$SRERR      PASS TO S/R ERROR FOR NOT CONNECT            
         B     QINSE1                                                           
*                                                                               
QIN26    MVI   TSVCREQ,X'01'                                                    
QIN26A   MVC   TSVCREQ+1(1),PGMNUM                                              
*                                                                               
QIN28    CLI   AUTOFLAG,0          TEST AUTO/GOBACK DATA IN BUFFER              
         BNE   QIN28A                                                           
         TM    TSTAT6,TST6STSS                                                  
         BO    QINSE1                                                           
         TM    TSTAT8,TST8STSS     TEST DIRTY STEREO DATA IN BUFFER             
         BO    QINSE1                                                           
QIN28A   TM    TTYPE,TTYPE327                                                   
         BO    QIN29                                                            
         CLC   TSVCREQ+1(1),$CC+1  $CC CALCULATOR ONLY FOR 3270                 
         BE    QIN25                                                            
         B     QINSE1                                                           
                                                                                
* MODIFY 3270 INPUT FIELDS FOR SEQUENTIAL MATCHING                              
*                                                                               
QIN29    L     R1,SRPAR2           POINT TO START OF BUFFER                     
         MVC   HALF,5(R1)          SAVE CURSOR ADDR                             
         TM    SRFLAG,X'01'        TEST IF ANY DATA IN (01,02)                  
         BZ    QIN29A              NO                                           
*                                                                               
         MVC   HALF,=X'407F'       YES SET CURSOR ON (01,64)                    
         CLC   TSVCREQ+1(1),$CC+1  MUST BE INPUT TO $CC                         
         BNE   *+14                                                             
         NC    HALF,=X'3F3F'                                                    
         B     QIN29B                                                           
*                                                                               
         BAS   RE,DELFST           DELETE FIRST INPUT FIELD                     
         NI    TSTAT2,255-TSTATCC                                               
*                                                                               
QIN29A   CLC   HALF,=X'407E'       TEST CURSOR ON (01,63)                       
         BNE   *+10                NO                                           
         MVC   HALF,=X'407F'       YES ADJUST FOR MATCHING                      
         NC    HALF,=X'3F3F'                                                    
*                                                                               
         L     R1,SRPAR2                                                        
         LA    R1,7(R1)            POINT TO FIRST INPUT FIELD SBA               
         CLC   INDREQSR,3(R1)      SPECIAL TEST FOR =INDFILE                    
         BE    QINSE1              IF YES, LEAVE IT ALONE                       
         CLI   AUTOFLAG,0          TEST AUTO/GOBACK DATA IN BUFFER              
         BNE   QIN29B                                                           
         TM    TSTAT6,TST6STSS                                                  
         BO    QINSE1                                                           
         TM    TSTAT8,TST8STSS     TEST FOR NEW STEREO MODE                     
         BO    QINSE1              NEW STEREO - PROCESS IN FATISTR              
*                                                                               
QIN29B   CLI   0(R1),ETX           SCAN FOR FIELD DELIMITERS                    
         BE    QINSE1                                                           
         CLI   0(R1),SBA                                                        
         BE    *+12                                                             
         LA    R1,1(R1)                                                         
         B     QIN29B                                                           
*                                                                               
         NC    1(2,R1),=X'3F3F'                                                 
         TM    TSVCREQ,X'02'       CHECK FOR CURSOR FLAG                        
         BO    *+14                                                             
         CLC   HALF,1(R1)          IS FLD ADDR GT CURSOR ADDR                   
         BNH   QIN29B1             YES TRUNCATE                                 
*                                                                               
         XC    1(2,R1),1(R1)       CLEAR DATA ADDR                              
         LA    R1,3(R1)            NO BACK TO NEXT FIELD                        
         B     QIN29B                                                           
*                                                                               
QIN29B1  MVI   0(R1),ETX           SET NEW DATA LEN                             
         L     R4,SRPAR2                                                        
         SR    R1,R4                                                            
         LA    R1,1(R1)                                                         
         AHI   R4,-(TBHL)                                                       
         STH   R1,TBHMSGL                                                       
         EJECT                                                                  
***********************************************************************         
* INPUT IS A SERVICE REQUEST OR A SCRIPT                              *         
***********************************************************************         
QINSE1   L     R3,VSELIST          POINT R3 TO SE1 AND Q INPUT                  
         LA    R3,6(R3)                                                         
         LT    RF,SRAPGM           TEST IF S/R PGMLST ENTRY KNOWN               
         BNZ   QINSE1A                                                          
         CLC   TSVCREQ,$CT         SET $CT PGMLST ENTRY                         
         BNE   QINSE1A                                                          
         L     RF,=V(SRPGMCT)                                                   
QINSE1A  STCM  RF,7,TASVC          SET A(PGMLST ENTRY FOR S/R)                  
         B     QSE                                                              
                                                                                
QINSE0   L     R3,VSELIST          POINT R3 TO SE0 AND Q INPUT                  
         L     R3,2(R3)                                                         
         LA    R3,1(R3)            POINT TO SPECIAL SELIST ENTRY                
         B     QSE                                                              
         EJECT                                                                  
***********************************************************************         
* INPUT IS NOT A SERVICE REQUEST                                      *         
***********************************************************************         
QIN30    XC    TSVCREQ,TSVCREQ     RESET SERVICE REQUEST STATUS                 
         NI    TSTAT2,255-TSTATCC                                               
         TM    SRFLAG,X'01'        DELETE FIRST FIELD IF INPUT                  
         BZ    *+8                                                              
         BAS   RE,DELFST                                                        
         XC    TASVC,TASVC                                                      
         CLI   TSYS,0              TEST TERMINAL CONNECTED                      
         BNZ   QIN32               YES                                          
         MVC   TSVCREQ,$CT         NO PASS DATA TO CONNECT                      
         B     QINSE1                                                           
*                                                                               
QIN32    XR    R3,R3               POINT TO PROGRAM LIST ENTRY                  
         ICM   R3,7,TAPRG                                                       
         USING PGMLSTD,R3          TEST IF OK TO ACCESS PROGRAM                 
         BZ    QIN34                                                            
*                                                                               
         L     RF,VSSB             TEST TAPRG HOLDS DISPLACEMENT                
         TM    SSBSTAT4-SSBD(RF),SSBPGDSP                                       
         BZ    QIN33                                                            
         XR    RF,RF                                                            
         ICM   RF,7,TARSYS                                                      
         BZ    QIN34                                                            
         ICM   RF,15,SEPGMS-SELISTD(RF)                                         
         AR    R3,RF                                                            
*                                                                               
QIN33    SR    R0,R0                                                            
         ICM   R0,B'1000',TOVSYS   OVERLAY SYSTEM                               
         ICM   R0,B'0100',TPRG     PROGRAM                                      
         ICM   R0,B'0010',TSYS     SENUM                                        
         GOTO1 =V(FAVERCHK),DUB,(RA),(R0)                                       
         CLI   DUB,X'FF'                                                        
         BE    QIN33C              PROGRAM IS UNAVAILABLE (VIA =VER)            
         TM    PGMIND,PGMINOP+PGMIACC                                           
         BZ    QIN34                                                            
         TM    PGMIND,PGMINOP                                                   
         BO    *+12                                                             
         TM    TSTAT1,TSTATDDS     RESTRICTED OK FOR DDS TERMINALS              
         BNZ   QIN34                                                            
QIN33C   MVC   TSVCREQ,$SYSNOP                                                  
         B     QINSE1              QUEUE TO SE1 IF NOP                          
*                                                                               
QIN34    XR    R3,R3                                                            
         ICM   R3,7,TASYS          POINT TO SYSTEM LIST ENTRY                   
         BZ    QIN34N              SYSNOP IF NOT DEFINED                        
         USING SELISTD,R3                                                       
         TM    SEIND,SEINOP        MUST CHECK NOP FIRST                         
         BO    QIN34N                                                           
         CLC   TSYS,SESYS                                                       
         BNE   QIN34N              SYSNOP IF SYSTEM NOT FOUND                   
         TM    SEIND,SEINOP+SEIRESA TEST NOP OR RESTRICTED ACCESS               
         BZ    QIN40                                                            
         TM    SEIND,SEINOP                                                     
         BO    QIN34N                                                           
         TM    TSTAT1,TSTATDDS     OK FOR DDS TERMINALS                         
         BNZ   QIN40                                                            
QIN34N   MVC   TSVCREQ,$SYSNOP                                                  
         B     QINSE1                                                           
         EJECT                                                                  
***********************************************************************         
* TEST IF TERMINAL IN DDLINK POLLING MODE                             *         
***********************************************************************         
QIN40    TM    TSTATC,TSTATFUP     FALINK BULK UPLOAD ON                        
         BZ    QINPOLL                                                          
         L     R4,SRPAR2           TBUFF XA                                     
         AHI   R4,-(TBHL)                                                       
         TM    TBHINDS,TBHIREAL    TEST REAL INPUT FROM FALCM                   
         BZ    QSE                                                              
*                                                                               
         ICM   RF,3,TBHMSGL        BUFFER LENGTH                                
         STH   RF,TBHDATA+2        KEEP ORIGINAL SIZE HERE                      
         LA    RE,TBHDATA+7        GO PAST VTRD, CURSOR                         
         LA    R0,2                FIND TWICE                                   
QBULK2   CLI   0(RE),SBA           SHOULD BE SBA                                
         LA    RE,1(RE)            NEXT FIELD                                   
         BNE   QBULK2                                                           
         BCT   R0,QBULK2                                                        
                                                                                
         LA    R0,20                                                            
QBULK3   CLI   0(RE),EOT           FIND END OF TRANSMISSION                     
         BE    QBULK5                                                           
         LA    RE,1(RE)                                                         
         BCT   R0,QBULK3                                                        
         B     QINPOLL             DOESN'T MAKE ANY SENSE                       
                                                                                
QBULK5   LA    RE,1(RE)                                                         
         S     RE,SRPAR2           MESSAGE LENGTH                               
         STH   RE,TBHMSGL          SCREEN TO DISPLAY                            
         B     QSE                                                              
*                                                                               
QINPOLL  TM    TSTATC,TSTCDDLP     TEST IF DDLINK SET POLLING MODE              
         BZ    QSE                                                              
         ICM   R2,15,TRUNTAB       MUST KNOW LOCATION OF RUNTAB ENTRY           
         BZ    QSE                                                              
                                                                                
MAP      L     R4,SRPAR2           GET LENGTH OF INPUT DATA                     
         AHI   R4,-(TBHL)                                                       
         TM    TBHINDS,TBHIREAL    TEST REAL INPUT FROM FALCM                   
         BZ    MAPX                                                             
         SR    RF,RF               RF=INPUT DATA LENGTH                         
         ICM   RF,3,TBHMSGL                                                     
         AHI   RF,-1                                                            
         BNP   MAPX                                                             
         STH   RF,FULL+2           FULL+2(2)=DISPLACEMENT TO LAST CHR           
         LA    RE,TBHDATA          RE=A(BUFFER POINTER)                         
                                                                                
         LR    R0,RE               R0=A(SAVED START OF BUFFER)                  
MAP1     CLC   0(2,RE),=C'R?'                                                   
         BE    MAP2                                                             
         CLC   0(2,RE),=X'996F'                                                 
         BE    MAP3                                                             
         LA    RE,1(RE)                                                         
         BCT   RF,MAP1                                                          
         L     RF,POLLNF           BUMP NOT FOUND COUNT                         
         AHI   RF,1                                                             
         ST    RF,POLLNF                                                        
         B     MAPX                                                             
MAP2     L     RF,POLLUC           BUMP UPPER CASE COUNT                        
         AHI   RF,1                                                             
         ST    RF,POLLUC                                                        
         B     MAP4                                                             
MAP3     L     RF,POLLLC           BUMP LOWER CASE COUNT                        
         AHI   RF,1                                                             
         ST    RF,POLLLC                                                        
MAP4     CLI   2(RE),X'03'         BUMP ETX COUNT                               
         BNE   MAP5                                                             
         L     RF,POLLETX                                                       
         AHI   RF,1                                                             
         ST    RF,POLLETX                                                       
MAP5     SR    RE,R0               RE=DISPLACEMENT                              
         STH   RE,FULL                                                          
         L     RE,=A(POLLDATA)                                                  
MAP6     CLC   0(4,RE),=F'0'       TEST EMPTY SLOT                              
         BNE   MAP7                                                             
         MVC   0(4,RE),FULL                                                     
         B     MAP8                                                             
MAP7     CLC   0(4,RE),FULL        TEST MATCHED SLOT                            
         BNE   MAP9                                                             
MAP8     L     RF,4(RE)            BUMP SLOT COUNTER                            
         AHI   RF,1                                                             
         ST    RF,4(RE)                                                         
         CHI   RF,1                                                             
         BNE   MAPX                                                             
         MVC   10(1,RE),TOVSYS     SAVE SYSTEM/PROGRAM                          
         MVC   11(1,RE),TPRG                                                    
         L     RF,12(RE)           RF=A(SAVE VTAM BUFFER AREA)                  
         LR    RE,R0               RE=A(INPUT BUFFER)                           
         LH    R1,FULL+2           R1=L'INPUTDATA-1                             
         CHI   R1,59                                                            
         BNH   *+8                                                              
         LHI   R1,59                                                            
         EX    R1,*+8              SAVE INPUT DATA                              
         B     MAPX                                                             
         MVC   4(0,RF),0(RE)                                                    
MAP9     CLC   0(4,RE),=4X'FF'     TEST END OF SLOTS                            
         BE    MAPX                                                             
         LA    RE,16(RE)                                                        
         B     MAP6                                                             
MAPX     EQU   *                                                                
                                                                                
         L     RE,SRPAR2           GET LENGTH OF INPUT DATA                     
         AHI   RE,-(TBHL)                                                       
         TM    4(RE),TBHIREAL      TEST REAL INPUT FROM FALCM                   
         BZ    QSE                                                              
         L     R0,POLLCNT          BUMP POLLING MODE COUNT                      
         AHI   R0,1                                                             
         ST    R0,POLLCNT                                                       
         L     RF,VSSB                                                          
         ST    R0,SSB#POLL-SSBD(RF) SAVE TO DISPLAY IN SSB                      
*                                                                               
         SR    RF,RF               RF=INPUT DATA LENGTH                         
         ICM   RF,3,TBHMSGL-TBHD(RE)                                            
         BZ    QSE                                                              
         LHI   R0,12               R0=MAX CHRS TO SCAN BACK                     
         CHI   RF,48               7+(3+17)+(3+17)+1                            
         BH    QSE                                                              
         CR    R0,RF                                                            
         BNL   *+6                                                              
         LR    R0,RF                                                            
         LA    RF,TBHL-1(RE,RF)    RF=A(LAST CHR IN INPUT BUFFER)               
*                                                                               
QIN41    CLC   0(2,RF),=C'R?'      LOOK FOR POLLING REQUEST R?                  
         BE    QIN41A                                                           
         AHI   RF,-1               GO BACK ONE BYTE IN BUFFER                   
         BCT   R0,QIN41                                                         
         B     QSE                                                              
QIN41A   TM    TSTAT8,TST8STSS                                                  
         BZ    QIN44                                                            
         CLI   2(RF),C'?'          CHECK FOR R??                                
         BNE   QSE                                                              
*                                                                               
QIN44    L     RE,VSSB             GET TABSDSP ALET                             
         USING SSBD,RE                                                          
         LAM   AR2,AR2,SSBTBLET                                                 
         USING TABSQUED,R2                                                      
         SAC   512                                                              
         CLI   TQSTATUS,TQSPEND    TEST SEND FILE PENDING                       
         BE    QIN44A                                                           
         CLI   TQSTATUS,TQSDONE    OR WORK IS COMPLETE                          
         BNE   QIN44B                                                           
QIN44A   LAM   AR2,AR2,ARZERO                                                   
         SAC   0                                                                
         L     R0,LINKCNT          BUMP DDLINK COUNT                            
         AHI   R0,1                                                             
         ST    R0,LINKCNT                                                       
         B     QSE                 LET DDLINK RESPOND TO POLL                   
*                                                                               
QIN44B   LAM   AR2,AR2,ARZERO                                                   
         SAC   0                                                                
         L     R0,MSGQCNT          BUMP MSGQIN COUNT                            
         AHI   R0,1                                                             
         ST    R0,MSGQCNT                                                       
         ST    R0,SSB#MSGQ         SAVE TO DISPLAY IN =SSB                      
         DROP  RE                                                               
*                                                                               
QIN45    TM    TSTAT8,TST8STSS     TEST STEREO SOFT FIELDS                      
         BO    QIN47                                                            
*                                                                               
QIN46    LM    R1,R2,POLL327       GET 3270 BUFFER LENGTH AND TEXT              
         L     RE,SRPAR2                                                        
         AHI   RE,-(TBHL)                                                       
         XC    4(2,RE),4(RE)                                                    
         STH   R1,6(RE)            SET REPLY LENGTH                             
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   8(0,RE),0(R2)       SET REPLY TEXT                               
         B     QIN48                                                            
*                                                                               
QIN47    LM    R1,R2,POLLSTS       GET STEREO BUFFER LENGTH AND TEXT            
         L     RE,SRPAR2                                                        
         AHI   RE,-(TBHL)                                                       
         XC    4(2,RE),4(RE)                                                    
         STH   R1,6(RE)            SET REPLY LENGTH                             
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   8(0,RE),0(R2)       SET REPLY TEXT                               
         LA    RE,13(RE)           RE=A(TYPE/FACPAK/SESSION)                    
QIN47A   L     RF,VSSB             GET FACPAK ID FROM SSB                       
         LLC   R1,SSBSYSID-SSBD(RF)                                             
         CHI   R1,10                                                            
         BL    QIN47B                                                           
         AHI   R1,-10              MAKE IT A LETTER                             
         LA    R1,C'A'(R1)                                                      
         B     *+8                                                              
QIN47B   LA    R1,C'0'(R1)         MAKE IT A NUMBER                             
         STC   R1,1(RE)                                                         
QIN47C   IC    R1,TSESSION         GET SESSION ID FROM UTL                      
         LA    R1,C'A'(R1)         MAKE IT A LETTER                             
         STC   R1,2(RE)                                                         
*                                                                               
QIN48    LA    R0,VTWRITE          CALL LCM TO WRITE THE RESPONSE               
         GOTO1 =V(LCM),DUB,(R0),(R9),0                                          
         MVI   REPLY,1                                                          
         B     EXIT                EXIT WITH REPLY INDICATOR SET                
         EJECT                                                                  
***********************************************************************         
* QUEUE INPUT TO SELIST ENTRY AT R3                                   *         
***********************************************************************         
QSE      ST    R3,SAVESE           SAVE SE ENTRY                                
         USING SELISTD,R3                                                       
         OI    TSTAT8,TST8INQ                                                   
         OC    TXNEXTIN,TXNEXTIN   IS TXNEXTIN POINTING TO ANOTHER UTL          
         BNZ   QSEERR              JUST WRITE AN ERROR AND EXIT                 
*                                                                               
         CLI   SESYS,1                                                          
         BE    QSE0                                                             
         MVI   TPRTYIN,PRTYMAX-1   SET DEFAULT PRIORITY TO MAX                  
         XR    RE,RE                                                            
         ICM   RE,7,TAPRG          SET PRIORITY FROM PGM LIST VALUE             
         BZ    QSE0                                                             
*                                                                               
         L     RF,VSSB             TEST TAPRG HOLDS DISPLACEMENT                
         TM    SSBSTAT4-SSBD(RF),SSBPGDSP                                       
         BZ    QSEA                                                             
         XR    RF,RF                                                            
         ICM   RF,7,TARSYS                                                      
         BZ    QSE0                                                             
         ICM   RF,15,SEPGMS-SELISTD(RF)                                         
         AR    RE,RF                                                            
*                                                                               
QSEA     MVC   TPRTYIN,PGMPRTY-PGMLSTD(RE)                                      
         CLI   TPRTYIN,0                                                        
         BNE   QSE0                                                             
         MVI   TPRTYIN,PRTYMAX-1   SET PRIORITY TO MAX IF NOT DEFINED           
*                                                                               
QSE0     OC    SEFIRST,SEFIRST     QUEUE IS EMPTY                               
         BNZ   QSE1                                                             
         XC    SEQLEN,SEQLEN                                                    
         ST    R9,SEFIRST          SET FIRST                                    
         B     QSE1X                                                            
*                                                                               
QSE1     CLI   SESYS,1             SERVICE REQUESTS ARE FIFO                    
         BNE   QSE2                                                             
         TM    TSTAT1,TSTATDDS     DDS TERMINALS CAN BE AN EXCEPTION            
         BZ    QSE1A                                                            
         TM    SRFLAG,X'80'        IF THEY HAVE PRIORITY FLAG SET               
         BNO   QSE1A                                                            
         MVC   TXNEXTIN,SEFIRST    GO TO THE FRONT OF THE QUEUE                 
         ST    R9,SEFIRST                                                       
         B     QSE1XX                                                           
*                                                                               
QSE1A    L     RE,SELAST           POINT TO OLD LAST & CHAIN TO CURR            
         ST    R9,TXNEXTIN-TNUM(RE)                                             
QSE1X    ST    R9,SELAST           UPDATE LAST                                  
QSE1XX   LH    RE,SEQLEN           UPDATE Q LENGTH                              
         LA    RE,1(RE)                                                         
         STH   RE,SEQLEN                                                        
         B     EXIT                                                             
*                                                                               
QSE2     L     RE,SEFIRST          POINT TO FIRST IN QUEUE                      
         CLC   TPRTYIN,TPRTYIN-UTLD(RE)                                         
         BNH   QSE4                                                             
         MVC   TXNEXTIN,SEFIRST    WE ARE THE NEW FIRST                         
         ST    R9,SEFIRST                                                       
         B     QSE8                                                             
*                                                                               
QSE4     LR    RF,RE                   RF IS THE PREV QUEUE ENTRY               
         ICM   RE,15,TXNEXTIN-UTLD(RF) RE IS THE CURR QUEUE ENTRY               
         BZ    QSE6                                                             
         CLC   TPRTYIN,TPRTYIN-UTLD(RE)                                         
         BNH   QSE4                                                             
         CR    R9,RE                 MAKE SURE NOT TO ITSELF                    
         BE    QSEERR                                                           
         ST    RE,TXNEXTIN           POINT US TO CURR                           
QSE6     CR    R9,RF                 MAKE SURE NOT TO ITSELF                    
         BE    QSEERR                                                           
         ST    R9,TXNEXTIN-UTLD(RF)  POINT PREV TO US                           
*                                                                               
QSE8     LR    RF,R9               SET PREV QUEUE ENTRY                         
         SR    RE,RE                                                            
QSE10    ICM   RE,15,TXNEXTIN-UTLD(RF)                                          
         BZ    QSE14                                                            
         CLI   TPRTYIN-UTLD(RE),PRTYMAX                                         
         BNL   QSE12                                                            
         IC    R1,TPRTYIN-UTLD(RE)                                              
         LA    R1,PRTYBUMP(R1)                                                  
         STC   R1,TPRTYIN-UTLD(RE)                                              
QSE12    LR    RF,RE                                                            
         B     QSE10                                                            
*                                                                               
QSE14    ST    RF,SELAST           UPDATE LAST                                  
         LH    RE,SEQLEN           UPDATE Q LENGTH                              
         A     RE,=F'1'                                                         
         STH   RE,SEQLEN                                                        
         B     EXIT                                                             
*                                                                               
QSEERR   L     RE,VSSB                                                          
         MVC   QERROR+4(3),SSBSYSNA-SSBD(RE)  INSERT FACPAK NAME                
         MVC   QERROR+32(8),TSYM              INSERT TERMINAL LUID              
         L     RF,VDMOD000                                                      
         MVC   DUB,VWCTYPE                                                      
         GOTO1 (RF),DUB,,QERROR,L'QERROR      WRITE ERROR TO LOG                
         NI    TSTAT8,255-TST8INQ                                               
                                                                                
EXIT     CLI   REPLY,0             EXIT WITH CC NEQ IF HAVE REPLY               
         XMOD1 1                                                                
         EJECT                                                                  
***********************************************************************         
* MISCELLANEOUS SUB ROUTINES                                          *         
***********************************************************************         
SETBXLE  LH    R4,0(R3)            SET BXLE REGS FOR TABLE AT R3                
         L     R5,2(R3)                                                         
         LA    R3,6(R3)                                                         
         BR    RE                                                               
                                                                                
SETLEN   LA    R8,6                SET TO MAX LEN-1                             
         LA    R7,6(R3)            POINT TO END                                 
SETLEN2  CLI   0(R7),C' '                                                       
         BNER  RE                                                               
         AHI   R8,-1                                                            
         BCT   R7,SETLEN2                                                       
         DC    H'0'                                                             
         BR    RE                                                               
         EJECT                                                                  
***********************************************************************         
* DELETE THE FIRST 3270 INPUT FIELD                                   *         
***********************************************************************         
DELFST   ST    RE,SAVERE           DELETE FIRST 3270 INPUT FIELD                
*                                                                               
         L     RE,SRPAR2           POINT TO BUFFER                              
         LR    RF,RE                                                            
         LA    RE,7(RE)            RE=A(FIRST 3270 INPUT FIELD)                 
         AHI   RF,-(TBHL)                                                       
         LH    R1,TBHMSGL-TBHD(RF) R1=LENGTH OF DATA IN BUFFER                  
         XR    R0,R0                                                            
         ICM   R0,1,SRFLAGL        R0=LENGTH OF FIRST FIELD                     
         BZ    DELFSTX                                                          
         SR    R1,R0                                                            
         BNP   DELFSTX             R1=L'BUFFER-L'FIELD                          
         STH   R1,6(RF)                                                         
         AR    R0,RE               R0=A(SECOND 3270 INPUT FIELD)                
         LR    RF,R1                                                            
         MVCL  RE,R0               SHIFT BUFFER LEFT TO DELETE                  
*                                                                               
DELFSTX  L     RE,SAVERE                                                        
         BR    RE                                                               
         EJECT                                                                  
***********************************************************************         
* VALIDATE VIRTUAL TERMINAL TOKEN                                     *         
***********************************************************************         
VALTOKEN ST    RE,SAVERE           VALIDATE TOKEN                               
         L     RE,VSSB             FIRST CHR IS FACPAK ID                       
         CLC   TOKSYSID,SSBSYSID-SSBD(RE)                                       
         BNE   VALTOKER                                                         
         SR    RE,RE               NEXT TWO CHRS ARE VIRTUAL TERM NUM           
         ICM   RE,3,TOKTNUM                                                     
         BZ    VALTOKER                                                         
         L     RF,VUTL                                                          
         AHI   RF,-6                                                            
         C     RE,0(RF)            COMPARE WITH MAXIMUM SO FAR                  
         BH    VALTOKER                                                         
         AHI   RF,6                                                             
         AHI   RE,-1               TNUM-1                                       
         MH    RE,0(RF)                                                         
         LA    RF,6(RE,RF)         RF=A(VIRTUAL UTL ENTRY)                      
*                                                                               
VALTOK1  CR    RF,R9               TEST IF VIRTUAL UTL IS REAL UTL              
         BE    VALTOKOK            YES                                          
*                                                                               
VALTOK2  TM    TVIFLAG-UTLD(RF),TVIVIRT                                         
         BZ    VALTOKER                                                         
         CLC   TOKCID,TVICID-UTLD(RF)                                           
         BNE   VALTOKER                                                         
*                                                                               
         ST    RF,TXTWIN              POINT REAL UTL TO VIRTUAL UTL             
         ST    R9,TXTWIN-UTLD(RF)     POINT VIRTUAL UTL TO REAL UTL             
         MVC   TBUFF-UTLD(4,RF),TBUFF POINT VIRTUAL TBUFF TO REAL TBUFF         
         LR    R9,RF                  SWITCH FROM REAL UTL TO VIRTUAL           
         L     RF,TXTWIN                                                        
         B     VALTOKOK                                                         
*                                                                               
VALTOKER CR    RB,RD              SET CC NEQ IF INVALID TOKEN                   
         B     *+6                                                              
VALTOKOK CR    RB,RB              SET CC EQL IF VALID                           
         L     RE,SAVERE                                                        
         BR    RE                                                               
         EJECT                                                                  
*&&US                                                                           
***********************************************************************         
* FIX SBA ADDRESSES IN MESSAGES FROM BIAS TERMINALS                   *         
* ON ENTRY R1 POINTS TO FIRST SBA                                     *         
***********************************************************************         
SETSBA   NTR1                                                                   
         MVC   0(3,R1),=X'11C1D1'                                               
         BAS   RE,NEXTSBA                                                       
         MVC   0(3,R1),=X'11C3F1'                                               
         BAS   RE,NEXTSBA                                                       
         MVC   0(3,R1),=X'11C6D1'                                               
         BAS   RE,NEXTSBA                                                       
         MVC   0(3,R1),=X'11C8F1'                                               
SETSBAX  DS    0H                                                               
         XIT1                                                                   
*                                                                               
NEXTSBA  LA    R1,1(R1)                                                         
         CLI   0(R1),SBA                                                        
         BER   RE                                                               
         CLI   0(R1),ETX                                                        
         BE    SETSBAX                                                          
         B     NEXTSBA                                                          
*&&                                                                             
         EJECT                                                                  
***********************************************************************         
* CONSTANTS AND LITERALS                                              *         
***********************************************************************         
PRTYMAX  EQU   15                                                               
PRTYBUMP EQU   1                                                                
*                                                                               
VHEXIN   DC    V(HEXIN)                                                         
*                                                                               
$TOPRN   DC    X'0105'             SRTOP REGULAR VERSION.....OLD/PROD           
$TOPRNA  DC    X'0102'             SRTOP ALTERNATE VERSION...NEW/TEST           
$SYSNOP  DC    X'0103'                                                          
$SRGRF   DC    X'0108'                                                          
$CT      DC    X'0110'                                                          
$LOAD    DC    X'0117'                                                          
$DISP    DC    X'0118'                                                          
$RE      DC    X'0120'                                                          
$TIMEOUT DC    X'012D'                                                          
$KWX     DC    X'0147'                                                          
$CC      DC    X'0156'                                                          
$DUMP    DC    X'015D'                                                          
$CFI     DC    X'0160'                                                          
$BYE     DC    X'01EE'                                                          
$SEARCH  DC    X'012A'                                                          
$SRPSWD  DC    X'01FD'                                                          
$SRERR   DC    X'01FE'                                                          
$INDFILE DC    X'0165'                                                          
*                                                                               
INDREQ   DC    CL12'IND$FILE GET'                                               
INDREQSR DC    CL8'=INDFILE'                                                    
*                                                                               
DIRTYSBA DC    X'115DD7'           SBA/24/40                                    
DIRTYTXN DC    C'*INITIATE STEREO*'                                             
DIRTYTXO DC    C'TALK DIRTY TO ME!'                                             
DIRTYTX2 DC    C'*INITIATE STEREO2'                                             
DDSVRSN  DC    CL8'DDSVRSN#'                                                    
*                                                                               
APFKTAB  DC    A(PFKTAB)                                                        
*                                                                               
$SRDUMMY DC    CL7'       '         PGMNAME                                     
         DC    AL1(PGMIACC+PGMIOLA) PGMIND                                      
         DC    AL1(0,0)             PGMINUM,PGMCOSYS                            
         DC    AL1(0,0)             PGMPRTY,PGMIND2                             
         DC    AL1(0,0,0,0)         PGMTSKMX,PGMTSK,PGMALNUM,PGMCTRY            
         DC    AL2(0)               PGMTEXT                                     
         DC    AL1(PGMIRTOR)        PGMIND3                                     
         DC    AL1(0),XL4'00000000' PGMIND4,PGMCNT1                             
         DC    AL1(0),AL3(0)        N/D,PGMAGYLA                                
         DC    XL14'00'             PGMVINFO                                    
         DC    XL32'00'             SPARE FOR PGMLST EXPANSION                  
*                                                                               
QERROR   DS    0CL40                                                            
*&&UK*&& DC    C'+FACPAK+ SYSTEM QUEUE ERROR FOR VTAMLUID'                      
*&&US*&& DC    C'*FACPAK* SYSTEM QUEUE ERROR FOR VTAMLUID'                      
*                                                                               
         DS    0D                                                               
         DC    C'**DDLP**'                                                      
POLLCNT  DC    F'0'                 COUNT POLLS EXPECTED COUNT                  
MSGQCNT  DC    F'0'                 COUNT POLLS PROCESSED BY FAMSGQIN           
LINKCNT  DC    F'0'                 COUNT POLLS PROCESSED BY DDLINK             
         DC    C'********'                                                      
         DC    C'POLLCNTS'                                                      
POLLNF   DC    F'0'                 NO POLL FOUNT                               
POLLUC   DC    F'0'                 UPPER CASE POLL                             
POLLLC   DC    F'0'                 LOWER CASE POLL                             
POLLETX  DC    F'0'                 ETX FOLLOWS COUNT                           
         DC    C'********'                                                      
ARZERO   DC    F'0'                                                             
POLL327  DC    AL4(TEXT327X-TEXT327),A(TEXT327)                                 
POLLSTS  DC    AL4(TEXTSTSX-TEXTSTS),A(TEXTSTS)                                 
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
*THIS TABLE IS USED TO TRANSLATE 3270 DATA IN S/R FIELD TO UPPER CASE *         
***********************************************************************         
*                0.1.2.3.4.5.6.7.8.9.A.B.C.D.E.F.                               
UPPER    DC    X'00404040404040404040404040404040' 00-0F                        
         DC    X'40404040404040404040404040404040' 10-1F                        
         DC    X'40404040404040404040404040404040' 20-2F                        
         DC    X'40404040404040404040404040404040' 30-3F                        
         DC    X'404040404040404040404A4B4C4D4E4F' 40-4F                        
         DC    X'504040404040404040405A5B5C5D5E5F' 50-5F                        
         DC    X'606140404040404040406A6B6C6D6E6F' 60-6F                        
         DC    X'404040404040404040407A7B7C7D7E7F' 70-7F                        
         DC    X'40C1C2C3C4C5C6C7C8C9404040404040' 80-8F                        
         DC    X'40D1D2D3D4D5D6D7D8D9404040404040' 90-9F                        
         DC    X'4040E2E3E4E5E6E7E8E9404040404040' A0-AF                        
         DC    X'F0F1F2F3F4F5F6F7F8F9404040404040' B0-BF                        
         DC    X'40C1C2C3C4C5C6C7C8C9404040404040' C0-CF                        
         DC    X'40D1D2D3D4D5D6D7D8D9404040404040' D0-DF                        
         DC    X'4040E2E3E4E5E6E7E8E9404040404040' E0-EF                        
         DC    X'F0F1F2F3F4F5F6F7F8F9404040404040' F0-FF                        
                                                                                
***********************************************************************         
* THIS TABLE CONTAINS THE EQUATED TEXT FOR PF KEYS 1 THRU 12          *         
***********************************************************************         
PFKTAB   DS    0CL17                                                            
PFK01    DC    CL17'=PFK,01'                                                    
PFK02    DC    CL17'=SWS'                                                       
PFK03    DC    CL17'=NEW'          WAS =S+                                      
PFK04    DC    CL17'=DQU'                                                       
PFK05    DC    CL17'=PQ'                                                        
PFK06    DC    CL17'=JOB'                                                       
PFK07    DC    CL17'=PFK,07'       WAS =S-                                      
PFK08    DC    CL17'=COPY'                                                      
PFK09    DC    CL17'=SVS'                                                       
PFK10    DC    CL17'=PQ,#1'                                                     
PFK11    DC    CL17'=PQ,#2'                                                     
PFK12    DC    CL17'=RE'                                                        
*                                                                               
PFK13    DC    CL17'=SS,A'                                                      
PFK14    DC    CL17'=SS,B'                                                      
PFK15    DC    CL17'=SS,C'                                                      
PFK16    DC    CL17'=SS,D'                                                      
PFK17    DC    CL17'=PFK,17'                                                    
PFK18    DC    CL17'=PFK,18'                                                    
PFK19    DC    CL17'=PFK,19'                                                    
PFK20    DC    CL17'=PFK,20'                                                    
PFK21    DC    CL17'=PFK,21'                                                    
PFK22    DC    CL17'=SS'                                                        
PFK23    DC    CL17'=S-'                                                        
PFK24    DC    CL17'=S+'                                                        
         EJECT                                                                  
TEXT327  EQU   *                                                                
         DC    X'11C150',X'1D40',X'13'     SBA/02,01/SF/UNP/IC                  
         DC    C'RN',X'3CC260',X'00'       C'RN'/RA/03,01/X'00'                 
         DC    X'03'                       ETX                                  
TEXT327X EQU   *                                                                
                                                                                
TEXTSTS  EQU   *                                                                
         DC    X'114040',X'1D00'           SBA/01,01/SF/X'00'                   
         DC    C'BFS'                      TYPE/FACPAK/SESSION                  
         DC    C'<FALINK> 2 6 10 10 1 1 K=*'                                    
         DC    X'3CC150',X'40'             RA/02,01/X'40'                       
         DC    X'11C150',X'1D00'           SBA/02,01/SF/X'00'                   
         DC    C'RN',X'3CC260',X'40'       C'RN'/RA/03,01/X'40'                 
         DC    X'03'                       ETX                                  
TEXTSTSX EQU   *                                                                
                                                                                
         DS    0Q                                                               
         DC    CL8'POLLDATA',CL8'POLLDATA'                                      
POLLDATA DC    XL2'00',XL2'00',XL4'00',XL4'00',A(PD01)                          
         DC    XL2'00',XL2'00',XL4'00',XL4'00',A(PD02)                          
         DC    XL2'00',XL2'00',XL4'00',XL4'00',A(PD03)                          
         DC    XL2'00',XL2'00',XL4'00',XL4'00',A(PD04)                          
         DC    XL2'00',XL2'00',XL4'00',XL4'00',A(PD05)                          
         DC    XL2'00',XL2'00',XL4'00',XL4'00',A(PD06)                          
         DC    XL2'00',XL2'00',XL4'00',XL4'00',A(PD07)                          
         DC    XL2'00',XL2'00',XL4'00',XL4'00',A(PD08)                          
         DC    XL2'00',XL2'00',XL4'00',XL4'00',A(PD09)                          
         DC    XL2'00',XL2'00',XL4'00',XL4'00',A(PD10)                          
         DC    16X'FF'                                                          
PD01     DC    CL4'*01*',XL60'00'                                               
PD02     DC    CL4'*02*',XL60'00'                                               
PD03     DC    CL4'*03*',XL60'00'                                               
PD04     DC    CL4'*04*',XL60'00'                                               
PD05     DC    CL4'*05*',XL60'00'                                               
PD06     DC    CL4'*06*',XL60'00'                                               
PD07     DC    CL4'*07*',XL60'00'                                               
PD08     DC    CL4'*08*',XL60'00'                                               
PD09     DC    CL4'*09*',XL60'00'                                               
PD10     DC    CL4'*10*',XL60'00'                                               
                                                                                
***********************************************************************         
* WORKING STORAGE DSECT                                               *         
***********************************************************************         
WRKD     DSECT                                                                  
SRPARS   DS    0CL12                                                            
SRPAR1   DS    A                   A(SYSTEM FACILITIES TABLE)                   
SRPAR2   DS    A                   A(TIA)                                       
SRPAR3   DS    A                   A(UTL ENTRY)                                 
*                                                                               
SAVER1   DS    F                                                                
SAVERE   DS    F                                                                
SAVESE   DS    A                                                                
*                                                                               
DUB      DS    D                                                                
DUB1     DS    D                                                                
DUB2     DS    D                                                                
DUB3     DS    D                                                                
FULL     DS    F                                                                
HALF     DS    H                                                                
BYTE     DS    X                                                                
REPLY    DS    X                   NON ZERO IF REPLY PUT IN TBUFF               
*                                                                               
         DS    0A                                                               
SRINFO   DS    0XL6                                                             
SRAPGM   DS    A                   A(S/R ENTRY IN PRGM LIST)                    
SRFLAG   DS    X                   FLAG FOR S/R DATA                            
SRFLAGL  DS    X                   LENGTH OF FIRST INPUT FIELD                  
*                                                                               
SVSTRLEN DS    X                   LENGTH OF STEREO S/R FIELD                   
SVPGMLEN DS    X                   LENGTH OF S/R PROGRAM NAME                   
*                                                                               
AUTOFLAG DS    X                   FLAG AUTO/GOBACK DATA IN BUFFER              
TOKENIN  DS    CL17                TOKEN INPUT DATA                             
*                                                                               
TOKEN    DS    0XL5                VIRTUAL TERMINAL TOKEN                       
TOKSYSID DS    XL1                 VIRTUAL TERMINAL FAKPAK ID                   
TOKTNUM  DS    XL2                 VIRTUAL TERMINAL NUMBER                      
TOKCID   DS    XL2                 VIRTUAL TERMINAL CONTROL ID                  
*                                                                               
WRKL     EQU   *-WRKD                                                           
                                                                                
*FAPRQ                                                                          
       ++INCLUDE FAPRQ                                                          
*FAPIGFACD                                                                      
       ++INCLUDE FAPIGFACD                                                      
*FADSECTS                                                                       
       ++INCLUDE FADSECTS                                                       
*FATBHD                                                                         
       ++INCLUDE FATBHD                                                         
*FATABSRUN                                                                      
       ++INCLUDE FATABSRUN                                                      
                                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'005FAMSGQIN  11/18/15'                                      
         END                                                                    
