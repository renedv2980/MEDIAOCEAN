*          DATA SET ACBMONVAL  AT LEVEL 015 AS OF 05/04/98                      
*CATALP BMONVAL                                                                 
BMONVAL  TITLE '- VALIDATES A MONTH OF SERVICE PASSED BY CALLER'                
         PRINT NOGEN                                                            
BMONVAL  CSECT                                                                  
ACBMON   NMOD1 WORKX-WORKD,**BMON**,CLEAR=YES                                   
         USING WORKD,RC                                                         
*                                                                               
         LR    R2,R1               R2=A(PARAMETER LIST)                         
         ST    R2,APARMS           SAVE A(CALLING PARAMETER LIST)               
         MVC   ABATMON,0(R2)       SAVE A(INPUT MONTH)                          
         MVC   ACOMFACS,4(R2)      SAVE A(COMFACS)                              
         MVC   AOUT,8(R2)          SAVE A(OUTPUT BLOCK)                         
*                                                                               
         MVC   BATMONL,0(R2)       SAVE DATA LENGTH                             
         MVC   BATTYP,4(R2)        SAVE INPUT BATCH TYPE                        
         MVC   AGYLANG,8(R2)       SAVE AGENCY LANGUAGE CODE                    
         MVC   COMPANY,12(R2)      SAVE INPUT COMPANY                           
*                                                                               
         CLI   BATMONL,0           TEST DATA LENGTH INPUT                       
         BNE   BMON02                                                           
         L     R1,ABATMON          INPUT IS A(FIELD HEADER)                     
         MVC   BATMONL,FHILD(R1)   SAVE INPUT LENGTH                            
         LA    R1,FHDAD(R1)                                                     
         ST    R1,ABATMON          SAVE A(FIELD DATA)                           
*                                                                               
         USING COMFACSD,R1                                                      
BMON02   L     R1,ACOMFACS         R1=A(COMFACS)                                
         MVC   VDATAMGR,CDATAMGR                                                
         MVC   VDATCON,CDATCON                                                  
         MVC   VPERVAL,CPERVAL                                                  
         MVC   VADDAY,CADDAY                                                    
         DROP  R1                                                               
*                                                                               
         GOTO1 VDATCON,DMCB,(5,0),(1,TODAYP)                                    
         GOTO1 (RF),(R1),,(3,TODAYB)                                            
         EJECT                                                                  
***********************************************************************         
* VALIDATE PASSED BATCH MONTH OF SERVICE                              *         
***********************************************************************         
         SPACE 1                                                                
         USING PERVALD,R2                                                       
         LA    R2,WORK                                                          
*&&US                                                                           
         CLI   BATMONL,0           CHECK DATE INPUT                             
         BE    BMON08                                                           
*&&                                                                             
BMON04   GOTO1 VPERVAL,DMCB,(BATMONL,ABATMON),(AGYLANG,WORK)                    
         MVC   MOSMSG,=AL2(INVDATE)                                             
         MVI   MOSERR,BMOEINVQ                                                  
         TM    4(R1),X'01'         TEST DATE INVALID                            
         BO    BMON26                                                           
         CLC   PVALNMNS,=H'1'      ONLY ONE MONTH ALLOWED                       
         BNE   BMON26                                                           
         MVI   MOSERR,BMOEOKQ                                                   
         SPACE 1                                                                
***********************************************************************         
* READ COMPANY RECORD AND GET LAST LOCKED MOS FROM COMPANY ELEMENT    *         
***********************************************************************         
         SPACE 1                                                                
BMON06   MVC   ACCMOSB,PVALBSTA                                                 
         MVC   ACCMOSP,PVALPSTA                                                 
         DROP  R2                                                               
BMON08   LA    R1,KEY                                                           
         USING CPYRECD,R1                                                       
         MVI   CPYKEY,C' '                                                      
         MVC   CPYKEY+1(L'CPYKEY-1),CPYKEY                                      
         MVC   CPYKCPY,COMPANY                                                  
         GOTO1 VDATAMGR,DMCB,DMREAD,ACCOUNT,KEY,IOAREA                          
         BE    *+6                                                              
         DC    H'0'                DIE IF NO COMPANY RECORD                     
         SR    R0,R0                                                            
         LA    R1,IOAREA                                                        
         AH    R1,DATADISP                                                      
*                                                                               
         USING MSLELD,RF                                                        
         MVI   MOSNEW,C'N'         NEW FORMAT - OVERRIDE FROM MSLEL             
         LR    RF,R1                                                            
BMON09   CLI   MSLEL,0                                                          
         BE    BMON10                                                           
         CLI   MSLEL,MSLELQ                                                     
         BE    *+14                                                             
         IC    R0,MSLLN                                                         
         AR    RF,R0                                                            
         B     BMON09                                                           
         CLI   MSLNUM,1                                                         
         BL    BMON10                                                           
         CLI   MSLVTYP,0                                                        
         BNE   BMON10                                                           
         MVI   MOSNEW,C'Y'                                                      
         DROP  RF                                                               
*                                                                               
         USING CPYELD,R1                                                        
BMON10   CLI   CPYEL,0                                                          
         BNE   *+6                                                              
         DC    H'0'                DIE IF NO COMPANY ELEMENT                    
         CLI   CPYEL,CPYELQ                                                     
         BE    BMON12                                                           
         IC    R0,CPYLN                                                         
         AR    R1,R0                                                            
         B     BMON10                                                           
*                                                                               
BMON12   L     RE,APARMS                                                        
         MVC   0(1,RE),CPYBSEC     SET COMPANY LEVEL SECURITY                   
         TM    CPYSTAT6,CPYSBANY   TEST ANY OPEN/LOCKED MONTH ALLOWED           
         BNZ   BMON14                                                           
         MVC   MOSLOCK+1(L'CPYMOSX),CPYMOSX                                     
         TM    CPYSTAT3,CPYSOPBM   TEST ANY OPEN MONTH ALLOWED                  
         BNZ   BMON14                                                           
*&&US                                                                           
         CLI   BATMONL,0           CHECK DATE INPUT                             
         BE    BMON14                                                           
*&&                                                                             
         SR    RE,RE                                                            
         IC    RE,TODAYB                                                        
         MH    RE,=H'12'                                                        
         SR    RF,RF                                                            
         IC    RF,TODAYB+1                                                      
         AR    RF,RE               RF=(CURRENT YEAR*12)+MONTH                   
         LA    RE,1(RF)            RE=RELATIVE NEXT MONTH                       
         STH   RE,DUB+2            DUB+2(2)=1 RELATIVE MONTH FORWARD            
         BCTR  RF,0                                                             
         BCTR  RF,0                                                             
         STH   RF,DUB+0            DUB+0(2)=2 RELATIVE MONTHS BACK              
         SR    RE,RE                                                            
         IC    RE,ACCMOSB                                                       
         MH    RE,=H'12'                                                        
         SR    RF,RF                                                            
         IC    RF,ACCMOSB+1                                                     
         AR    RF,RE               RF=INPUT MOS RELATIVE MONTH                  
         MVC   MOSMSG,=AL2(INVRNGE)                                             
         MVI   MOSERR,BMOERNGQ                                                  
         CH    RF,DUB+0            TEST MORE THAN 2 MONTHS BACK                 
         BL    BMON26                                                           
         CH    RF,DUB+2            TEST MORE THAN 1 MONTH FORWARD               
         BH    BMON26                                                           
         MVI   MOSERR,BMOEOKQ                                                   
*                                                                               
BMON14   CLI   MOSNEW,C'Y'         OVERRIDE MOS USES MSLEL NOT CPYEL            
         BE    *+14                                                             
         OC    BATTYP,BATTYP       WAS A SPECIFIC BATCH TYPE PASSED             
         BZ    BMON18                                                           
         SPACE 1                                                                
***********************************************************************         
* GET MOS LOCK ELEMENT AND SEE IF PASSED BATCH TYPE HAS A LOCKED MOS  *         
***********************************************************************         
         SPACE 1                                                                
         SR    R0,R0                                                            
         USING MSLELD,R1                                                        
BMON16   IC    R0,MSLLN            LOCATE SPECIFIC BATCH MOS LOCKS              
         AR    R1,R0                                                            
         CLI   MSLEL,0                                                          
         BE    BMON18                                                           
         CLI   MSLEL,MSLELQ        X'18' MOS LOCK ELEMENT                       
         BNE   BMON16                                                           
         SR    RE,RE                                                            
         IC    RE,MSLNUM           RE=NUMBER OF MOS LOCK ENTRIES                
         LA    RF,MSLVALS          RF=A(FIRST MOS LOCK ENTRY)                   
         USING MSLVALS,RF                                                       
         CLI   MOSNEW,C'Y'         TEST NEW FORMAT                              
         BNE   BMON16A                                                          
         OC    MOSLOCK,MOSLOCK     IF OLD FORMAT ALLOWS ANY MONTH               
         BZ    BMON16A             THEN DON'T SET AN OVERRIDE MONTH             
         MVC   MOSLOCK(L'MSLVYEAR),MSLVYEAR  OVERRIDE MOS                       
         MVC   MOSLOCK+1(L'MSLVMON),MSLVMON                                     
         LA    RF,L'MSLVALS2(RF)                                                
         B     BMON16B                                                          
BMON16A  CLC   MSLVTYP,BATTYP      COMPARE BTYP ENTRY TO PASSED BTYP            
         BE    BMON17                                                           
         LA    RF,L'MSLVALS(RF)    BUMP TO NEXT ENTRY                           
         CLI   MOSNEW,C'Y'         NEW FORMAT                                   
         BNE   BMON16B                                                          
         LA    RF,L'MSLVALS2-L'MSLVALS(RF)                                      
BMON16B  BCT   RE,BMON16A                                                       
         B     BMON18                                                           
BMON17   MVC   MOSLOCK+1(L'MSLVMON),MSLVMON                                     
         CLI   MOSNEW,C'Y'                                                      
         BNE   *+10                                                             
         MVC   MOSLOCK(L'MSLVYEAR),MSLVYEAR                                     
         TM    MSLVSEC,MSLVSDEF    TEST OVERRIDE SECURITY LEVEL                 
         BO    BMON18                                                           
         L     RE,APARMS                                                        
         MVC   0(1,RE),MSLVSEC     SET BATCH TYPE SECURITY LEVEL                
         DROP  R1                                                               
*                                                                               
BMON18   XC    MOSMSG,MOSMSG       CLEAR ERROR                                  
         CLI   MOSLOCK+1,X'40'     TEST LATEST MOS LOCK SET                     
         BNH   BMON26              NO - EXIT                                    
         CLI   MOSLOCK+1,X'F1'     TEST F1-F9 OR C1-C3                          
         BL    *+12                                                             
         NI    MOSLOCK+1,X'0F'     F1-F9 ->01-09                                
         B     BMON20                                                           
         SR    R1,R1                                                            
         IC    R1,MOSLOCK+1                                                     
         LA    R0,X'C1'-X'10'                                                   
         SR    R1,R0               C1-C3 -> 10-12                               
         STC   R1,MOSLOCK+1                                                     
BMON20   CLI   MOSNEW,C'Y'         NEW FORMAT                                   
         BE    BMON24                                                           
         CLC   TODAYP+1(1),MOSLOCK+1 TEST CURRENT MONTH > LOCKED MONTH          
         BNH   BMON22                                                           
         MVC   MOSLOCK(1),TODAYP   YES - SET CURRENT YEAR                       
         B     BMON24                                                           
*                                                                               
BMON22   MVC   WORK(1),TODAYP      SET 01/JAN/CURRENT YEAR                      
         MVI   WORK+1,X'01'                                                     
         MVI   WORK+2,X'01'                                                     
         GOTO1 VDATCON,DMCB,(1,WORK),(0,WORK+3)                                 
         GOTO1 VADDAY,DMCB,WORK+3,WORK+9,-1                                     
         GOTO1 VDATCON,DMCB,(0,WORK+9),(1,WORK)                                 
         MVC   MOSLOCK(1),WORK     SET PREVIOUS YEAR                            
*                                                                               
BMON24   MVC   MOSMSG,=AL2(MOSLOKD)                                             
         MVI   MOSERR,BMOELOKQ                                                  
         CLC   MOSLOCK,ACCMOSP                                                  
         BNL   BMON26                                                           
         MVI   MOSERR,BMOEOKQ                                                   
         DROP  RF                                                               
*                                                                               
         USING BMONVALD,R1                                                      
BMON26   L     R1,AOUT             POINT TO COMMON OUTPUT ADDRESS               
         XC    BMONVALD(BMONVALQ),BMONVALD CLEAR OUTPUT BLOCK                   
         MVC   BMOERR,MOSERR                                                    
         CLI   BMOERR,BMOEOKQ      TEST ANY ERROR                               
         BE    BMON28                                                           
         MVC   BMOMSG,MOSMSG                                                    
         CLI   BMOERR,BMOEINVQ     TEST DATE INVALID ERROR                      
         BE    EXIT                YES - CAN'T PASS BACK DATES                  
         CLI   BMOERR,BMOERNGQ     TEST DATE OUT OF RANGE                       
         BE    *+10                                                             
BMON28   MVC   BMOLCKP,MOSLOCK     NO - SET LAST LOCKED MOA                     
         MVC   BMOMOSP,ACCMOSP                                                  
         MVC   BMOMOSC+0(1),ACCMOSP+0 YEAR 'DIGIT' C'0'-C'9'                    
         OI    BMOMOSC,X'F0'                                                    
         MVC   BMOMOSC+1(1),ACCMOSP+1 MONTH PACKED                              
         TR    BMOMOSC+1(1),MONTAB    TRANSLATE PACKED MM TO CHARACTER          
*                                                                               
EXIT     XIT1  ,                                                                
         EJECT                                                                  
         LTORG                                                                  
         SPACE 2                                                                
DMREAD   DC    CL8'DMREAD'                                                      
ACCOUNT  DC    CL8'ACCOUNT'                                                     
DATADISP DC    H'49'               DISPLACEMENT TO FIRST ELEMENT                
MONTAB   DC    C'.123456789......ABC'                                           
         EJECT                                                                  
WORKD    DSECT                     ** LOCAL WORKING STORAGE **                  
*                                                                               
DUB      DS    D                                                                
DMCB     DS    6F                                                               
VDATAMGR DS    V                                                                
VDATCON  DS    V                                                                
VPERVAL  DS    V                                                                
VADDAY   DS    V                                                                
ACOMFACS DS    A                   A(COMFACS)                                   
ABATMON  DS    A                   A(INPUT MONTH)                               
AOUT     DS    A                   A(OUTPUT BLOCK)                              
APARMS   DS    A                   A(CALLING PARAMETER LIST)                    
COMPANY  DS    XL1                 COMPANY BYTE                                 
BATMONL  DS    XL1                 LENGTH OF MOS FIELD DATA                     
BATTYP   DS    XL1                 BATCH TYPE OR ZERO                           
AGYLANG  DS    XL1                 AGENCY LANGUAGE CODE                         
*                                                                               
MOSNEW   DS    CL1                 USE MSLEL FOR OVERRIDE                       
MOSERR   DS    XL1                 ERROR INDICATOR                              
MOSMSG   DS    XL2                 MESSAGE NUMBER                               
INVDATE  EQU   013                 INVALID DATE                                 
MOSLOKD  EQU   114                 MOS IS LOCKED                                
INVRNGE  EQU   111                 MOS OUT OF VALID RANGE                       
*                                                                               
ACCMOSP  DS    PL2                 PWOS MOS X'YYMM'                             
ACCMOSB  DS    XL2                 BINARY MOS X'YYMM'                           
MOSLOCK  DS    XL2                 LATEST CLOSED MOS                            
TODAYP   DS    PL3                 TODAY'S DATE (P'YYMMDD')                     
TODAYB   DS    XL3                 TODAY'S DATE (B'YYMMDD')                     
WORK     DS    XL64                                                             
*                                                                               
KEY      DS    XL(L'CPYKEY)                                                     
IOAREA   DS    XL2048                                                           
WORKX    EQU   *                                                                
         EJECT                                                                  
* ACBMONVALD                                                                    
         PRINT OFF                                                              
       ++INCLUDE ACBMONVALD                                                     
         PRINT ON                                                               
* ACGENFILE                                                                     
         PRINT OFF                                                              
       ++INCLUDE ACGENFILE                                                      
         PRINT ON                                                               
* DDCOMFACS                                                                     
         PRINT OFF                                                              
       ++INCLUDE DDCOMFACS                                                      
         PRINT ON                                                               
* DDPERVALD                                                                     
         PRINT OFF                                                              
       ++INCLUDE DDPERVALD                                                      
         PRINT ON                                                               
* DDFH                                                                          
         PRINT OFF                                                              
       ++INCLUDE DDFH                                                           
         PRINT ON                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'015ACBMONVAL 05/04/98'                                      
         END                                                                    
