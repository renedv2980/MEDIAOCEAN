*          DATA SET CXAPNC     AT LEVEL 038 AS OF 02/03/95                      
*CATALP CXAPNC                                                                  
         TITLE 'CXAPNC - CONTROL EXTRACT - T.S. APPROVER GROUP NAMES'           
**********************************************************************          
* AL4 A(EXTRACT RECORD)                                              *          
* AL4 A(CONFILE RECORD)                                              *          
**********************************************************************          
         SPACE 1                                                                
         PRINT NOGEN                                                            
CXAPNC   CSECT                                                                  
         NMOD1 WORKX-WORKD,*CXAPNC*,RR=RA                                       
         USING WORKD,RC                                                         
         ST    R1,APARM                                                         
         ST    RA,RELO                                                          
         MVC   PARM,0(R1)          EXTRACT PARAMETER LIST                       
         LM    R2,R3,0(R1)                                                      
         USING CXAPND,R2           R2=A(EXTRACT RECORD)                         
         USING SAAPREC,R3          R3=A(CONFILE RECORD)                         
*                                                                               
CXALEN   XC    CXAPNLEN,CXAPNLEN   RECORD LENGTH/TYPE/ENDOFREC                  
         MVC   CXAPNLEN(2),=AL2(CXAPNDL)                                        
         MVC   CXAPNTYP,CXAPNDQ                                                 
         MVI   CXAPNDX,MXTRTQ                                                   
         MVI   CXAPNDX+1,MXTRTQ                                                 
*                                                                               
CXAAGY   MVI   CXAPNAGY-1,MXTRTQ   AGENCY ALPHA ID                              
         MVC   CXAPNAGY,SAAPAGY                                                 
*                                                                               
CXACOD   MVI   CXAPNAGC-1,MXTRTQ   APPROVER GROUP CODE                          
         MVC   CXAPNAGC,SAAPAGR                                                 
*                                                                               
         MVI   CXAPNNAM-1,MXTRTQ                                                
         MVC   CXAPNNAM,SPACES                                                  
*                                                                               
         LA    R4,SAAPDATA         R4=A(FIRST ELEMENT)                          
*                                                                               
CXAELM   CLI   0(R4),0             GET ELEMENT FROM RECORD                      
         BE    CXANXTX                                                          
         CLI   0(R4),SAAPGELQ                                                   
         BE    CXAAPG                                                           
CXANXT   SR    RF,RF               BUMP TO NEXT ELEMENT                         
         IC    RF,1(R4)                                                         
         CH    RF,=H'1'                                                         
         BNH   EXIT                EXIT IF FUNNY ELEMENT LENGTH                 
         AR    R4,RF                                                            
         B     CXAELM                                                           
*                                                                               
         USING SAAPGD,R4                                                        
*                                  APPROVER GROUP NAME                          
CXAAPG   LA    RE,SAAPGLNQ         RE = LENGTH OF FIXED PART                    
         XR    RF,RF                                                            
         IC    RF,SAAPGLN          RF = LENGTH OF WHOLE ELEMENT                 
         SR    RF,RE               RF = LENGTH OF VARIABLE PART                 
         BCTR  RF,0                -1 FOR EXECUTE                               
         LTR   RF,RF               CHECK FOR NULL LENGTH                        
         BM    CXANXT                                                           
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   CXAPNNAM(0),SAAPGNAM                                             
         TR    CXAPNNAM,MXTRT                                                   
         B     CXANXT                                                           
*                                                                               
CXANXTX  B     EXIT                                                             
*                                                                               
DELREC   L     R1,APARM            DELETE THIS RECORD                           
         MVI   8(R1),X'80'                                                      
         SPACE 1                                                                
EXIT     XMOD1 1                                                                
         EJECT                                                                  
         DS    0D                                                               
SPACES   DC    80C' '                                                           
         DS    0D                                                               
         LTORG                                                                  
         SPACE 1                                                                
       ++INCLUDE MXTRT                                                          
       ++INCLUDE CXRECID                                                        
         SPACE 1                                                                
         DS    0D                                                               
VHEXOUT  DC    V(HEXOUT)                                                        
VCRYPT   DC    V(CRYPT)                                                         
         EJECT                                                                  
* INCLUDE CXTRACT LOCAL DSECTS                                                  
         SPACE 1                                                                
       ++INCLUDE CXTRACTD                                                       
         EJECT                                                                  
WORKD    DSECT                                                                  
PARM     DS    0XL16               PARAMETER LIST                               
AXREC    DS    A                   A(EXTRACT RECORD)                            
CFREC    DS    A                   A(CONFILE RECORD)                            
         DS    A                                                                
         DS    A                                                                
*                                                                               
DMCB     DS    6F                                                               
DUB      DS    D                                                                
APARM    DS    A                   A(PARAMETER LIST)                            
RELO     DS    A                                                                
*                                                                               
WORKX    EQU   *                                                                
         EJECT                                                                  
       ++INCLUDE CXAPND                                                         
         EJECT                                                                  
       ++INCLUDE CTGENFILE                                                      
         EJECT                                                                  
       ++INCLUDE SEACSFILE                                                      
         SPACE 1                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'038CXAPNC    02/03/95'                                      
         END                                                                    
