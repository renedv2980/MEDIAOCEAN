*          DATA SET REGENPBY   AT LEVEL 005 AS OF 06/11/12                      
*CATALP REGENPBY                                                                
         TITLE 'REGENPBY - REPPAK BUY LINE DISPLAY'                             
*              PARAMETER 1 =       A(BUYREC)                                    
*                                       BYTE 0 = X'80' INVOKED BY               
*                                                RECNT63 & 17                   
*                                                X'02' STATION REPORT           
*                                                EXCLUDES DEMO                  
*                                                X'40' SUPPRESS AUTO-           
*                                                COMMENT FOR BUYLINES           
*                        2 =       A(OUTPUT AREA)                               
*                                       BYTE 0 = MAXIMUM LINES                  
*                                       OUTPUT AREA = 81 X MAX LINES            
*                                       BYTE 0 = NUMBER LINES ON RETURN         
*                                       THIS CAN BE 0 (DELETES)                 
*                        3 =       A(ROUTINE ADDRESS BLOCK)                     
*                                       WORD 1 = A(UNDAY)                       
*                                            2 = A(UNTIME)                      
*                                            3 = A(DATCON)                      
*                                            4 = A(ADDAY)                       
*                                            5 = A(REPFACS)                     
*                                            IF 4 IS 0 THEN ALTERNATE           
*                                            WEEKS ARE NOT EXPLODED             
*                                            6 = A(DEMOCON)                     
*                        4 =       A(CONREC)                                    
*                                                                               
*                        5 =       A(CONTRACT PROGRAM PROFILES)                 
*                                                                               
*                        6 =       BYTE 1-3  A(COMFACS)                         
*                                                                               
*                                                                               
***********************************************************************         
* HISTORY:                                                                      
*                                                                               
* 18MAR/92 (SKU) --- FIX MCI BUG                                                
*                                                                               
* 12APR/95 (BU ) --- MULTIPLE MG MISSED DATES                                   
*                                                                               
* 31MAY/95 (SKU) --- MAKEGOOD CONTINUATION TO 2ND COMMENT LINE SUPPORT          
*                                                                               
* 13JUL/95 (SKU) --- FIX CONFIRMATION PRINT BUG                                 
*                                                                               
* 13OCT/95 (BU ) --- ADD KATZ DISPLAY CHANGES:  BAD INFO                        
*                                                                               
* 12FEB/95 (SKU) --- SKIP X'07' CREDIT FROM TOTAL SPOT COUNT                    
*                                                                               
* 02MAR/96 (SKU) --- FIX CONFIRMATION PRINT BUG AGAIN                           
*                                                                               
* 20AUG/98 (SKU) --- DISPLAY DIRECT CREDIT HISTORY                              
*                                                                               
* 22DEC/98 (RHV) --- HANDLE NON-DELETED CANCELLED BUYS                          
*                                                                               
* 20JAN/00 (SKU) --- PROFILE DRIVEN CREDIT HISTORY PRINTING IN                  
*                    WORKSHEET ONLY                                             
*                                                                               
* 21JUL/00 (BU ) --- TRADE PROCESSING IMPLEMENTED                               
*                                                                               
* 30OCT/00 (SKU) --- FIX CREDIT AUDIT TRAIL PRINTING                            
*                                                                               
* 04DEC/00 (SKU) --- BETTER MAKEGOOD COMMENTS                                   
* 08NOV00  (RHV) --- SPORTS BUYS                                                
* 18JAN02  (SKU) --- REPLACEMENT FOR NA SUPPORT                                 
* 25JUL02  (HQ ) --- DISPLAY DEMO CAT/VALUE                                     
* 17MAR04  (SKU) --- DISPLAY AUTO COMMENT FOR DARE/ROM BUYS                     
* 20MAY05  (HQ ) --- PARAMETER TO SUPPRESS BUY AUTO-COMMENT                     
* 18MAR10  (KUI) --- FIX PRINTING BUG                                           
* 11FEB11  (KUI) --- REMOVE X'FF' MARKERS CAUSING ARCHIVE ISSUES                
* 23MAY12  (BOB) --- ADD GRPS TO REPORT                                         
* ***  END TOMBSTONE  ***                                                       
***********************************************************************         
REGENPBY CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 WORKX-WORKD,REGENPBY,RR=R3                                       
         USING WORKD,RC                                                         
*                                                                               
         L     RA,0(R1)            DSECT FOR BUYREC                             
         ST    R1,SAVER1                                                        
         XC    SV#SPTS,SV#SPTS     INIT SPOTS ACCUMULATOR                       
         USING BUYREC,RA                                                        
         L     R9,4(R1)            OUTPUT AREA                                  
         USING PD,R9               DISPLAY LINE DSECT                           
         USING CONREC,R8                                                        
         L     R8,12(R1)                                                        
*                                                                               
         ST    R3,RELO                                                          
*                                                                               
         GOTO1 =A(INIT),RR=Y                                                    
*                                                                               
         TM    RBUYCNTL,X'C0'      VOID POINTER?                                
         BO    EXIT                                                             
         CLI   RBUYCHGI,C'C'       CANCELLED?                                   
         BE    P4                                                               
         TM    RBUYCNTL,X'80'      DELETED?                                     
         BZ    P5                                                               
         CLI   RBUYCHGI,C'X'       DELETED (NOT CANCELLED?)                     
         BE    EXIT                                                             
* MUST HAVE BEEN CANCELLED                                                      
P4       DS    0H                                                               
         TM    PROFILES+CNTDCANB,CNTDCANA  DISPLAY CANCELLED LINES?             
         BO    P5                  YES                                          
         TM    RCONMODR+1,X'C0'    ACE/GRAPHNET                                 
         BNZ   P5                  DO CANCELLED ACE/GRAPHNET BUYS LATER         
         CLC   RBUYCREA,RBUYCHGD   CREATED SAME DAY AS CANCEL?                  
         BE    EXIT                                                             
         CLC   RBUYKMOD,RCONMOD    BUY K LEVEL V K MOD NUM                      
         BL    EXIT                                                             
*                                                                               
* GET TODAY'S DATE                                                              
P5       GOTO1 VDATCON,DMCB,(5,0),(3,TODAY)                                     
*                                                                               
         LR    RE,R9                                                            
         LH    RF,MAX              MAXIMUM LINES                                
         MHI   RF,L'PRTLN          LINE COUNT                                   
*                                                                               
*         XCEF                      CLEAR OUTPUT AREA                           
         XCEF                                                                   
         CLI   RBUYKLIN,255        PLAN REC?                                    
         BNE   P6                                                               
*                                                                               
         GOTO1 =A(DISPLAN),RR=Y                                                 
         B     EXIT                                                             
*                                                                               
P6       GOTO1 =A(DISMOD),RR=Y     CHECK & PRINT MOD CODES                      
         BNE   EXIT                SKIP LINE                                    
*                                                                               
         EDIT  (1,RBUYKLIN),(3,PLIN)    LINE NUMBER                             
*                                                                               
         TM    RBUYSFG,X'40'       SPORTS BUY?                                  
         BZ    P8                  NO - NORMAL PROCESSING                       
         GOTO1 =A(SBTYP),RR=Y      SPORTS BUY TYPE DISPLAY ROUTINE              
         EDIT  (4,RBUYTCOS),PSCOST,2,FLOAT=-,COMMAS=YES                         
         B     P50                                                              
*                                                                               
P8       MVC   DMCB(2),RBUYDUR     LENGTH                                       
         NI    DMCB,X'7F'          MINUTE IND                                   
         EDIT  (2,DMCB),(3,PLEN)                                                
         TM    RBUYDUR,X'80'       MINUTES?                                     
         BZ    P9                                                               
         MVC   PLEN-1(3),PLEN                                                   
         MVI   PLEN+2,C'M'                                                      
*                                                                               
P9       MVC   PCLS,RBUYCLS        CLASS                                        
         MVC   PSEC,RBUYSEC        SECTION                                      
         MVC   PTYP,RBUYTYP        SECTION                                      
*                                                                               
* DISPLAY FLIGHT NUMBER IF NO SECTION EXISTS FOR BUY                            
*                                                                               
P12      CLI   RBUYFLT,0           DOES BUY HAVE A FLIGHT                       
         BE    P15                 NO                                           
         OC    VADDAY,VADDAY       ONLY DO IT IF ON-LINE                        
         BNZ   P15                                                              
         CLI   PSEC,0              TEST FOR ABSENCE OF SECTION                  
         BE    *+12                                                             
         CLI   PSEC,C' '                                                        
         BNE   P15                                                              
         MVI   PSEC,C'F'           FLIGHT CODE PREFIX                           
         EDIT  (B1,RBUYFLT),(2,PSEC+1),ALIGN=LEFT                               
*                                                                               
P15      MVC   PPLN,RBUYKPLN       PLAN                                         
         CLC   RBUYKPLN,=3X'FF'                                                 
         BNE   *+10                                                             
         MVC   PPLN,=3C' '                                                      
*                                                                               
*                                                                               
         CLC   RBUYKPLN,=3X'FF'    PLAN                                         
         BE    *+14                                                             
         MVC   PRAT+3(4),=C'PLAN'                                               
         B     P50                                                              
         EDIT  (4,RBUYCOS),(10,PRAT),2,FLOAT=-,COMMAS=YES                       
*                                                                               
P50      MVC   DUB(2),RBUYLEN      REC LENGTH                                   
         LH    R5,DUB                                                           
         LA    R5,RBUYREC(R5)      REC END FOR BXLE                             
*              PREPARE TO GET DAY-TIME ELEMENTS                                 
         BCTR  R5,R0                                                            
         BCTR  R5,R0                                                            
         SR    R4,R4                                                            
         LA    R3,RBUYELEM         FIRST ELEM                                   
* GET DAY SPAN                                                                  
         SR    RE,RE                                                            
         SR    RF,RF                                                            
         ZIC   RE,RBUYSTED                                                      
         SRDL  RE,4                                                             
         SRL   RF,28                                                            
         CR    RE,RF                                                            
         BNH   *+8                                                              
         LA    RF,7(RF)                                                         
         SR    RF,RE                                                            
         ST    RF,DAY1                                                          
*                                                                               
         SH    R9,=Y(L'PRTLN)                                                   
*                                                                               
         SR    R6,R6               LINE CTR FOR DAY-TIME LINES                  
         B     P125                NO DATE ELEM                                 
*              DISPLAY DAY-TIME                                                 
P100     EQU   *                                                                
         TM    RBUYSFG,X'40'       SPORTS BUY?                                  
         BO    P125                YES - SKIP TO '03' DATE ELEMS                
*                                                                               
         CLC   =X'FFFF',2(R3)      CONVERT ERROR DATA?                          
         BNE   P104                NO                                           
         GOTO1 =A(KZDAYERR),RR=RELO YES - RETRIEVE FROM ERROR ELEMENT           
         B     P106                                                             
P104     EQU   *                                                                
         GOTO1 VOUTDAY,DMCB,3(R3),2(R3),PDAY                                    
P106     EQU   *                                                                
*                                                                               
         CLC   =X'FFFF',4(R3)      CONVERT ERROR DATA?                          
         BNE   P108                NO                                           
         GOTO1 =A(KZTIMERR),RR=RELO YES - RETRIEVE FROM ERROR ELEMENT           
         B     P110                                                             
P108     EQU   *                                                                
         GOTO1 VUNTIME,DMCB,4(R3),PTIM                                          
P110     EQU   *                                                                
         B     P125                                                             
         EJECT                                                                  
*                                                                               
*                                                                               
P125     ZIC   R4,1(R3)            ELEM LEN                                     
         BXLE  R3,R4,*+8           NEXT ELEM                                    
         B     PXIT                                                             
*                                                                               
         CLI   0(R3),2             DAY TIME ELEM?                               
         BNE   P200                                                             
         LA    R9,L'PRTLN(R9)      OUTPUT                                       
         LA    R6,1(R6)            DAY-TIME CTR                                 
         CH    R6,MAX              MAXIMUM LINES?                               
         BH    P125                                                             
         B     P100                DISPLAY NEXT DAY-TIME                        
*                                                                               
*              GET DATE ELEMENTS                                                
P200     SR    R7,R7               DATE LINE CTR                                
         L     R1,SAVER1                                                        
         L     R9,4(R1)                                                         
         SH    R9,=Y(L'PRTLN)                                                   
         CLI   0(R3),3             DATE ELEM                                    
         BNE   P260                IF NONE JUST CARRY ON                        
         B     P255                                                             
* PROCESS DATE ELEMENT                                                          
P225     TM    RBUYSFG,X'40'       SPORTS BUY?                                  
         BO    P229                YES                                          
*                                                                               
         SR    RE,RE                                                            
         SR    RF,RF                                                            
         ZIC   RF,9(R3)            NO. PER WEEK                                 
* DISPLAY NPW                                                                   
         EDIT  (RF),(3,PNPW),ZERO=NOBLANK                                       
         TM    RBUYFLG2,X'02'      TRADE BUY?                                   
         BZ    *+8                 NO                                           
         MVI   PRAT-1,C'T'         SET TRADE INDICATOR AFTER NPW                
*                                                                               
         SR    R2,R2                                                            
         ZIC   R2,10(R3)           NO. OF WEEKS                                 
* DISPLAY NO. OF WEEKS IF NO CLASS                                              
         CLI   PCLS,0                                                           
         BE    *+12                                                             
         CLI   PCLS,C' '                                                        
         BNE   P226                                                             
         EDIT  (R2),(2,PCLS)                                                    
         MVI   PCLS+2,C'W'                                                      
* DISPLAY TOTAL SPOTS MINUS MISSED SPOTS                                        
***>     TM    RBUYCNTL,X'80'                                                   
P226     CLI   RBUYCHGI,C'C'       CANCELLED?                                   
         BE    P229                                                             
         SPACE 1                                                                
         LA    R1,RBUYELEM                                                      
         ZIC   R4,1(R1)                                                         
         AR    R1,R4                                                            
         CLI   0(R1),0                                                          
         BE    P226A                                                            
         CLI   0(R1),4                                                          
         BNE   *-18                                                             
         CLC   2(3,R1),=C'CR='                                                  
         BE    P226B               IGNORE THE MINUS BUY CHECK                   
         SPACE 1                                                                
P226A    TM    RBUYCOS,X'80'                                                    
         BO    P229                                                             
P226B    MR    RE,R2               TOTAL SPOTS IN RF                            
* GET MISSED SPOTS, IF ANY                                                      
         LA    R1,RBUYELEM                                                      
P227     CLI   0(R1),0             LAST?                                        
         BE    P228                                                             
         CLI   0(R1),6             MISSED ELEM?                                 
         BE    P227D                                                            
*        CLI   0(R1),7             MISSED CREDIT ISSUED                         
*        BE    P227D                                                            
P227B    ZIC   R4,1(R1)                                                         
         AR    R1,R4               NEXT ELEM                                    
         B     P227                                                             
* CHECK MISSED DATE AGAINST DATE ELEM                                           
P227D    CLC   2(3,R1),2(R3)        START                                       
         BL    P227B                                                            
         CLC   2(3,R1),5(R3)       END                                          
         BH    P227B                                                            
* MISSED ELEM APPLIES, SUBTRACT MISSED SPOTS                                    
         ZIC   R4,6(R1)            MISSED SPOTS                                 
         SR    RF,R4                                                            
         B     P227B                                                            
* DISPLAY TOTAL SPOTS FOR DATE ELEM                                             
P228     DS    0H                                                               
*                                                                               
         ICM   RE,15,SV#SPTS       BUMP SPOTS ACCUMULATOR                       
         AR    RE,RF                                                            
         STCM  RE,15,SV#SPTS       SAVE NUMBER OF SPOTS                         
*                                                                               
         EDIT  (RF),(4,PTOT-1),ZERO=NOBLANK                                     
* TEST FOR ALT WEEKS                                                            
P229     TM    8(R3),X'40'         ALT?                                         
         BZ    P235                                                             
         OC    VADDAY,VADDAY                                                    
         BZ    P235                                                             
* MUST BE OFF-LINE CONTRACT - EXPLODE ALTERNATE WEEKS                           
*                                                                               
* GET START-END IN WORK                                                         
         GOTO1 VDATCON,DMCB,(3,2(R3)),WORK                                      
*                                                                               
         GOTO1 (RF),(R1),(3,5(R3)),WORK+6                                       
P230     GOTO1 VDATCON,(R1),WORK,(4,PDAT)                                       
         MVI   PDAT+11,C'A'                                                     
         L     R2,DAY1                                                          
         LTR   R2,R2               ROTATOR?                                     
         BZ    P232                                                             
* GET END DATE OF WEEK                                                          
         GOTO1 VADDAY,(R1),WORK,WORK+12,(R2)                                    
*                                                                               
         GOTO1 VDATCON,(R1),WORK+12,(4,PDAT+6)                                  
         MVI   PDAT+5,C'-'                                                      
* GET NEXT ALTERNATE WEEK                                                       
P232     MVC   DUB(6),WORK                                                      
         GOTO1 VADDAY,(R1),DUB,WORK,14                                          
         CLC   WORK(6),WORK+6      PAST ELEM END DATE?                          
         BH    P250                                                             
*                                                                               
* DISPLAY NEXT ALTERNATE WEEK                                                   
         LA    R9,L'PRTLN(R9)      OUTPUT                                       
         LA    R7,1(R7)            LINE CTR                                     
         CH    R7,MAX                                                           
         BNH   P230                                                             
         LR    R6,R7                                                            
         B     PXIT                                                             
P235     GOTO1 VDATCON,DMCB,(3,2(R3)),(4,PDAT)                                  
         CLC   2(3,R3),5(R3)       SAME START-END?                              
         BE    P240                                                             
*                                                                               
         MVI   PDAT+5,C'-'                                                      
*              CONVERT ELEMENT END DATE TO MMMDD                                
         GOTO1 VDATCON,DMCB,(3,5(R3)),(4,PDAT+6)                                
*                                                                               
P240     TM    8(R3),X'40'         ALTERNATE WEEKS?                             
         BZ    P250                                                             
         MVI   PDAT+11,C'A'                                                     
         LA    R9,L'PRTLN(R9)                                                   
         LA    R7,1(R7)                                                         
         CH    R7,MAX                                                           
         BNH   *+10                                                             
         LR    R6,R7                                                            
         B     PXIT                                                             
         MVC   PDAT(15),=C'ALTERNATE WEEKS'                                     
*              GET NEXT ELEM                                                    
P250     ZIC   R4,1(R3)            ELEM LEN                                     
         BXLE  R3,R4,*+8           NEXT ELEM                                    
         B     P260                                                             
*                                                                               
         CLI   0(R3),3             ANOTHER DATE ELEM?                           
         BNE   P260                                                             
         TM    8(R3),X'40'         ALT WEEKS                                    
         BZ    P255                                                             
         OC    VADDAY,VADDAY                                                    
         BZ    P255                                                             
         MVI   PDAT+79,C'*'        IND ANOTHER ALT ELEM                         
P255     LA    R7,1(R7)            DATE CTR                                     
         LA    R9,L'PRTLN(R9)                                                   
         CH    R7,MAX                                                           
         BNH   P225                DISPLAY NEXT DATE ELEM                       
         LR    R6,R7                                                            
         B     PXIT                                                             
*                                                                               
P260     DS    0H                                                               
         CR    R6,R7               FIND HIGHEST LINE SO FAR                     
         BNL   *+6                                                              
         LR    R6,R7                                                            
         CH    R6,MAX                                                           
         BE    PXIT                                                             
*                                                                               
         LR    R7,R6                                                            
         MHI   R7,L'PRTLN                                                       
         L     R1,SAVER1                                                        
         L     R9,4(R1)            OUTPUT                                       
         AR    R9,R7               POSITION TO NEXT AVAILABLE LINE              
*                                                                               
         STM   R3,R5,SAVEBX        SAVE BXLE                                    
*                                                                               
         TM    MYFLAGS,X'40'       ALREADY PRINTED?                             
         BO    P270                YES                                          
         GOTO1 =A(DISPGM),RR=Y     PRINT DESCRIPTIVE LINES                      
         OI    MYFLAGS,X'40'                                                    
*                                                                               
P270     DS    0H                                                               
         TM    FLAGS,X'80'         ONLY WHEN CALLED BY 63 OR 17                 
         BZ    P275                                                             
         TM    FLAGS,X'02'         DO NOT PRINT DEM WHEN STATION                
         BO    P275                                                             
         GOTOR DISDEM                                                           
*                                                                               
P275     DS    0H                                                               
         GOTOR DISPMKGS                                                         
*                                                                               
*              PREPARE TO DISPLAY COMMENTS                                      
*                                                                               
         TM    RBUYSFG,X'40'       SPORTS BUY?                                  
         BZ    P285                NO                                           
         TM    MYFLAGS,X'80'       ALREADY PRINTED?                             
         BO    P285                YES                                          
         GOTO1 =A(SBDESC),RR=Y     PRINT DESCRIPTIVE LINES                      
         OI    MYFLAGS,X'80'                                                    
*                                                                               
P285     DS    0H                                                               
         TM    MYFLAGS,X'20'       ALREADY PRINTED?                             
         BO    P286                YES                                          
         GOTO1 =A(DISPTN),RR=Y     PRINT PATTERN                                
         OI    MYFLAGS,X'20'                                                    
P286     DS    0H                                                               
         LM    R3,R5,SAVEBX        BXLE                                         
         CR    R6,R7               FIND HIGHEST LINE SO FAR                     
         BNL   *+6                                                              
         LR    R7,R6                                                            
*                                                                               
         CLI   0(R3),4             COMMENT ELEM?                                
         BE    P290                                                             
         CLI   0(R3),0             END OF RECORD?                               
         BE    PXIT                                                             
         B     P400                CHECK FOR ORDER COMMENTS                     
         SPACE 1                                                                
P290     LR    R7,R6                                                            
         MHI   R7,L'PRTLN                                                       
         L     R1,SAVER1                                                        
         L     R9,4(R1)            OUTPUT                                       
         LA    R9,0(R7,R9)         POSITION TO NEXT AVAILABLE LINE              
*                                                                               
*                                                                               
*                                                                               
*              DISPLAY COMMENT                                                  
P300     CLC   2(3,R3),=C'CR='     CREDIT FOR MISSED                            
         BNE   P305                                                             
         STM   R3,R5,SAVEBX        SAVE BXLE                                    
P301     ZIC   R4,1(R3)                                                         
         BXLE  R3,R4,*+8           NEXT ELEM                                    
         B     P315                                                             
         CLI   0(R3),0                                                          
         BE    P315                                                             
         CLI   0(R3),5             MAKE-GOOD ELEMENT?                           
         BNE   P301                                                             
         SPACE 1                                                                
         MVC   PDAY+6(29),=C'CREDIT FOR SPOT          LINE'                     
         CLI   9(R3),2             NO. OF MISSED                                
         BL    *+10                                                             
         MVC   PDAY+6(29),=C'CREDIT FOR SPOTS         LINE'                     
         SPACE 1                                                                
         EDIT  (1,2(R3)),(2,PDAY+36),ALIGN=LEFT                                 
*                                                                               
         LA    R6,1(R6)                                                         
         LA    R9,L'PRTLN(R9)      NEXT LINE                                    
         CH    R6,MAX                                                           
         BH    PXIT                                                             
*                                                                               
         B     P315                                                             
         SPACE 1                                                                
P305     DS    0H                                                               
         CLC   =C'MG=',2(R3)       SKIP OLD STYLE MAKE-GOOD FORMULA             
         BE    P315                                                             
         CLC   =C'CREDIT:',2(R3)   SKIP CREDIT AUDIT TRAIL                      
         BNE   P325                                                             
*                                                                               
P315     LM    R3,R5,SAVEBX        BXLE                                         
* CHECK FOR COMMENT ALONG WITH MG=                                              
         LR    RE,R3                                                            
         SR    RF,RF                                                            
         ZIC   RF,1(R3)            ELEM LEN                                     
         SR    R1,R1                                                            
P320     CLI   0(RE),C' '                                                       
         BE    P322                                                             
         CLI   0(RE),C'+'          CHECK FOR CONTINUATION CHAR                  
         BE    P360                                                             
         LA    RE,1(RE)                                                         
         LA    R1,1(R1)                                                         
         CR    R1,RF                                                            
         BL    P320                                                             
         B     P350                                                             
* MOVE REST OF COMMENT                                                          
P322     DS    0H                                                               
         CLC   =C'CREDIT:',1(RE)   DISPLAY CREDIT HISTORY?                      
         BNE   P323                                                             
         TM    FLAGS,X'80'         WORKSHEET PRINT?                             
         BZ    P355                                                             
         TM    PROFILES+CNTPRCRB,CNTPRCRA                                       
         BZ    P355                                                             
*                                                                               
P323     DS    0H                                                               
         SR    RF,R1                                                            
         SH    RF,=H'2'                                                         
         BM    P350                                                             
         LA    R6,1(R6)                                                         
         LA    R9,L'PRTLN(R9)      NEXT LINE                                    
         CH    R6,MAX                                                           
         BH    PXIT                                                             
*                                                                               
         EX    RF,*+8                                                           
         B     P350                                                             
         MVC   PDAY+6(0),1(RE)                                                  
*                                                                               
P324     DS    0H                                                               
         CLC   =C'CREDIT:',2(R3)   DISPLAY CREDIT HISTORY?                      
         BNE   P325                                                             
         TM    FLAGS,X'80'         WORKSHEET PRINT?                             
         BZ    P355                                                             
         TM    PROFILES+CNTPRCRB,CNTPRCRA                                       
         BZ    P355                                                             
*                                                                               
P325     ZIC   R4,1(R3)            ELEM LEN                                     
         SH    R4,=H'3'                                                         
         EX    R4,MOVECOM          MOVE COMMENT TO DISPLAY LINE                 
*                                                                               
P350     LA    R6,1(R6)                                                         
         LA    R9,L'PRTLN(R9)                                                   
P355     DS    0H                                                               
         ZIC   R4,1(R3)            ELEM LEN                                     
         BXLE  R3,R4,*+8           NEXT ELEM                                    
         B     PXIT                                                             
*                                                                               
         CLI   0(R3),4             COMMENT?                                     
         BNE   P410                CHECK FOR ORDER COMMENT                      
         CH    R6,MAX              MAXIMUM LINES?                               
         BNH   P325                DISPLAY NEXT COMMENT                         
         B     PXIT                OR RETURN                                    
*                                                                               
P360     DS    0H                  CONTINUING MAKEGOOD IN 2ND CMMT LINE         
         LA    R6,1(R6)                                                         
         LA    R9,L'PRTLN(R9)                                                   
         ZIC   R4,1(R3)            ELEM LEN                                     
         BXLE  R3,R4,*+8           NEXT ELEM                                    
         B     PXIT                                                             
*                                                                               
         CLI   0(R3),4             COMMENT?                                     
         BNE   P410                CHECK FOR ORDER COMMENT                      
         CH    R6,MAX              MAXIMUM LINES?                               
         BH    PXIT                DISPLAY NEXT COMMENT OR RETURN               
*                                                                               
P365     DS    0H                                                               
         LR    RE,R3                                                            
         SR    RF,RF                                                            
         ZIC   RF,1(R3)            ELEM LEN                                     
         SR    R1,R1                                                            
P370     CLI   0(RE),C' '                                                       
         BE    P380                                                             
         LA    RE,1(RE)                                                         
         LA    R1,1(R1)                                                         
         CR    R1,RF                                                            
         BL    P370                                                             
         B     P390                                                             
* MOVE REST OF COMMENT                                                          
P380     SR    RF,R1                                                            
         SH    RF,=H'2'                                                         
         BM    P390                                                             
         CH    R6,MAX                                                           
         BH    PXIT                                                             
*                                                                               
         EX    RF,*+8                                                           
         B     P390                                                             
         MVC   PDAY+6(0),1(RE)                                                  
*                                                                               
         ZIC   R4,1(R3)            ELEM LEN                                     
         SH    R4,=H'3'                                                         
         EX    R4,MOVECOM          MOVE COMMENT TO DISPLAY LINE                 
*                                                                               
P390     LA    R6,1(R6)                                                         
         LA    R9,L'PRTLN(R9)                                                   
         ZIC   R4,1(R3)            ELEM LEN                                     
         BXLE  R3,R4,*+8           NEXT ELEM                                    
         B     PXIT                                                             
*                                                                               
P400     LR    R7,R6               IF NO REGULAR BUY CMTS,                      
         MHI   R7,L'PRTLN          POSITION TO NEXT AVAILABLE LINE              
         L     R1,SAVER1                                                        
         L     R9,4(R1)            OUTPUT                                       
         AR    R9,R7                                                            
*                                                                               
* DISPLAY CREDIT AUDIT TRAIL                                                    
*                                                                               
P410     DS    0H                                                               
         SR    R7,R7                                                            
*                                                                               
P411     DS    0H                                                               
         CLI   0(R3),X'16'                                                      
         BE    P413                                                             
         BH    P415                                                             
P412     ZIC   RF,1(R3)                                                         
         LTR   RF,RF                                                            
         BZ    PXIT                                                             
         AR    R3,RF                                                            
         B     P411                                                             
*                                                                               
P412A    ZIC   RF,1(R3)                                                         
         LTR   RF,RF                                                            
         BZ    PXIT                                                             
         AR    R3,RF                                                            
         CLI   0(R3),X'16'                                                      
         BE    P413                                                             
         LA    R9,L'PRTLN(R9)      OUTPUT                                       
         B     P415                                                             
*                                                                               
P413     DS    0H                                                               
         TM    FLAGS,X'80'         WORKSHEET PRINT?                             
         BZ    P413A                                                            
         TM    PROFILES+CNTPRCRB,CNTPRCRA                                       
         BZ    P415                                                             
*                                                                               
P413A    DS    0H                                                               
         CH    R6,MAX                                                           
         BNL   PXIT                                                             
*                                                                               
         LTR   R7,R7                                                            
         BZ    P414A                                                            
         MVI   0(R4),C','                                                       
         AHI   R4,1                                                             
         B     P414B                                                            
*                                                                               
P414A    DS    0H                                                               
         USING RBUYCAEL,R3                                                      
         TM    RBUYCAFL,X'40'      REPLACEMENT OFFER NA?                        
         BO    P414AA                                                           
         LA    R4,PDAY+6                                                        
         MVC   0(28,R4),=C'CREDIT WAS TAKEN FOR SPOT ON'                        
         AHI   R4,29                                                            
         AHI   R6,1                                                             
         CH    R6,MAX                                                           
         BNL   PXIT                                                             
         B     P414B                                                            
*                                                                               
P414AA   DS    0H                                                               
         LA    R4,PDAY+6                                                        
         MVC   0(31,R4),=C'CREDIT WAS TAKEN FOR SPOT NA ON'                     
         AHI   R4,32                                                            
         AHI   R6,1                                                             
         CH    R6,MAX                                                           
         BNL   PXIT                                                             
*                                                                               
P414B    DS    0H                                                               
         EDIT  (1,RBUYCASD+1),(2,0(R4)),ALIGN=LEFT                              
         AR    R4,R0                                                            
         MVI   0(R4),C'/'                                                       
         AHI   R4,1                                                             
         EDIT  (1,RBUYCASD+2),(2,0(R4)),ALIGN=LEFT                              
         AR    R4,R0                                                            
*                                                                               
         OC    RBUYCAED,RBUYCAED                                                
         BZ    P414C                                                            
         CLC   RBUYCASD,RBUYCAED                                                
         BE    P414C                                                            
         MVI   0(R4),C'-'          END DATE IF ANY                              
         AHI   R4,1                                                             
*                                                                               
         EDIT  (1,RBUYCAED+1),(2,0(R4)),ALIGN=LEFT                              
         AR    R4,R0                                                            
         MVI   0(R4),C'/'                                                       
         AHI   R4,1                                                             
         EDIT  (1,RBUYCAED+2),(2,0(R4)),ALIGN=LEFT                              
         AR    R4,R0                                                            
*                                                                               
         B     P414D                                                            
*                                                                               
P414C    DS    0H                                                               
         CLI   RBUYCANW,1                                                       
         BNH   P414E                                                            
         MVI   0(R4),C'-'                                                       
         AHI   R4,1                                                             
         EDIT  RBUYCANW,(3,0(R4)),ALIGN=LEFT                                    
         AR    R4,R0                                                            
         MVI   0(R4),C'W'                                                       
         AHI   R4,1                                                             
*                                                                               
P414D    DS    0H                                                               
         TM    RBUYCAFL,X'80'      ALTERNATING WEEKS??                          
         BZ    P414E                                                            
         MVI   0(R4),C'A'                                                       
         AHI   R4,1                                                             
*                                                                               
P414E    DS    0H                  NUMBER OF SPOTS CREDITED                     
         AHI   R4,1                                                             
         MVI   0(R4),C'('                                                       
         AHI   R4,1                                                             
         EDIT  RBUYCASP,(3,(R4)),ALIGN=LEFT                                     
         AR    R4,R0                                                            
         MVC   0(4,R4),=C'/WK)'                                                 
         LA    R9,L'PRTLN(R9)      OUTPUT                                       
         LA    R4,PDAY+6                                                        
         B     P412                                                             
         DROP  R3                                                               
*                                                                               
* DISPLAY THE MAKEGOOD LINES WHICH THIS BUY HAS BEEN MADE GOOD BY               
*                                                                               
P415     DS    0H                                                               
         CLI   0(R3),X'56'                                                      
         BE    P420                                                             
         BH    P500                                                             
         ZIC   RF,1(R3)                                                         
         LTR   RF,RF                                                            
         BZ    PXIT                                                             
         AR    R3,RF                                                            
         B     P415                                                             
*                                                                               
P420     DS    0H                                                               
         GOTO1 =A(BLDBLIST),RR=RELO                                             
*                                                                               
         LA    R4,2                                                             
*                                                                               
P423     DS    0H                                                               
         LA    R7,BUYLIST                                                       
         LA    RE,1                                                             
         TM    MYFLAGS,X'08'       ANY REPLACEMENT OFFERS?                      
         BO    P425                                                             
         LA    R4,1                                                             
         B     P430                                                             
*                                                                               
P425     DS    0H                                                               
         CHI   R4,2                                                             
         BNE   P430                                                             
         MVC   PDAY+6(28),=C'NA SPOTS REPLACED BY LINE(S)'                      
         LA    R2,PDAY+38                                                       
         B     P440                                                             
*                                                                               
P430     DS    0H                                                               
         TM    MYFLAGS,X'10'                                                    
         BZ    P500                                                             
         MVC   PDAY+6(20),=C'MADE-GOOD BY LINE(S)'                              
         LA    R2,PDAY+30                                                       
*                                                                               
P440     DS    0H                  IF CELL IS MARKED WITH AN X                  
         CHI   R4,2                                                             
         BNE   P445                                                             
         TM    0(R7),X'40'         REPLACEMENT OFFER                            
         BO    P450                                                             
         B     P448                                                             
*                                                                               
P445     DS    0H                                                               
         TM    0(R7),X'80'         REGULAR MAKEGOODS                            
         BO    P450                                                             
*                                                                               
P448     DS    0H                                                               
         AHI   RE,1                                                             
         C     RE,=F'255'                                                       
         BH    P470                                                             
         AHI   R7,1                                                             
         B     P440                                                             
*                                                                               
P450     DS    0H                                                               
         EDIT  (RE),(3,0(R2)),ALIGN=LEFT                                        
*                                                                               
P460     DS    0H                                                               
         AHI   RE,1                                                             
         C     RE,=F'255'                                                       
         BH    P470                                                             
         AHI   R7,1                                                             
         AR    R2,R0                                                            
         AHI   R2,1                                                             
*                                                                               
         LA    RF,PD+76            CHECK WE DON'T EXCEED DISPLAY LINE           
         CR    R2,RF                                                            
         BNH   P440                                                             
         LA    R9,L'PRTLN(R9)      OUTPUT                                       
         AHI   R6,1                                                             
         CH    R6,MAX                                                           
         BNL   PXIT                                                             
         B     P425                                                             
*                                                                               
P470     DS    0H                                                               
         LA    R9,L'PRTLN(R9)      OUTPUT                                       
         AHI   R6,1                                                             
         CH    R6,MAX                                                           
         BNL   PXIT                                                             
         BCT   R4,P423                                                          
*                                                                               
P500     CLI   0(R3),X'84'         ORDER COMMENT?                               
         BE    P510                                                             
*                                                                               
         TM    FLAGS,X'40'         SUPPRESS AUTO-COMMENT?                       
         BO    P504                                                             
         CLI   0(R3),X'B2'         DATES/SPOTS AUTO COMMENT?                    
         BE    P530                                                             
*                                                                               
P504     ZIC   R4,1(R3)                                                         
         AR    R3,R4                                                            
         CLI   0(R3),0             END OF RECORD??                              
         BE    PXIT                                                             
         B     P500                                                             
         SPACE 1                                                                
P510     CH    R6,MAX              MAXIMUM LINES?                               
         BNL   PXIT                                                             
         SPACE 1                                                                
         TM    RCONMODR+1,X'C0'    IF ACE OR GRAPHNET                           
         BZ    P520                                                             
         TM    SVCONF,X'40'        AND CONFIRMED                                
         BZ    P520                                                             
***>     TM    RBUYCNTL,X'80'      AND CANCELLED                                
         CLI   RBUYCHGI,C'C'       CANCELLED?                                   
         BE    PXIT                DON'T PRINT ORDER COMMENTS                   
         SPACE 1                                                                
P520     MVC   PDAY+6(4),=C'SOC-'  STATION ORDER CMT                            
         TM    2(R3),X'80'                                                      
         BZ    *+8                                                              
         MVI   PDAY+6,C'R'         REP ORDER CMT (ROC)                          
         ZIC   R4,1(R3)                                                         
         SH    R4,=H'4'                                                         
         EX    R4,*+8                                                           
         B     *+10                                                             
         MVC   PDAY+10(0),3(R3)                                                 
         CLC   3(3,R3),=C'#DS'     INDICATES DON'T SEND THIS LINE               
         BNE   *+8                                                              
         MVI   NOSEND,C'D'                                                      
         B     P350                LOOP BACK FOR OTHER ORDER CMTS               
*                                                                               
* GENERATE AUTO COMMENTING FOR DATE/SPOT CHANGES                                
*                                                                               
P530     DS    0H                                                               
         LA    RE,ACAREA                                                        
         LA    RF,L'ACAREA                                                      
         XCEF                                                                   
*                                                                               
         MVC   DUB(4),ACOMFACS                                                  
         MVC   DUB+4(2),RBUYKREP                                                
*                                                                               
         GOTO1 (RFAUTOCM,VREPFACS),DMCB,RBUYREC,ACAREA,RCONREC,DUB              
*                                                                               
         LA    RE,6                                                             
         LA    RF,ACAREA                                                        
*                                                                               
P540     DS    0H                                                               
         OC    0(60,RF),0(RF)                                                   
         BZ    PXIT                                                             
         MVC   PDAY+6(60),0(RF)                                                 
         LA    R9,L'PRTLN(R9)      OUTPUT                                       
         AHI   R6,1                                                             
         CH    R6,MAX                                                           
         BNL   PXIT                                                             
         AHI   RF,60                                                            
         BCT   RE,P540                                                          
*                                                                               
*                                                                               
*              RETURN                                                           
PXIT     L     R1,SAVER1                                                        
         CH    R6,MAX                                                           
         BNH   *+8                                                              
         LH    R6,MAX                                                           
         STC   R6,4(R1)            LINE CTR                                     
         MVI   8(R1),0                                                          
         CLI   NOSEND,C'D'                                                      
         BNE   *+8                                                              
         MVI   8(R1),C'D'          INDICATE THIS LINE IS 'DON'T SEND'           
EXIT     XMOD1 1                                                                
*                                                                               
MOVECOM  MVC   PDAY+6(0),2(R3)                                                  
*                                                                               
         EJECT                                                                  
         LTORG                                                                  
         EJECT                                                                  
         GETEL R6,DATADISP,ELCODE                                               
         EJECT                                                                  
*                                                                               
* INITIALIZE MODULE                                                             
*                                                                               
INIT     NTR1  BASE=*,LABEL=*                                                   
         MVC   FLAGS,0(R1)         SAVE FLAGS                                   
*                                                                               
         ICM   RE,15,16(R1)                                                     
         BNZ   *+6                                                              
         DC    H'0'                                                             
         MVC   PROFILES,0(RE)      SAVE CONTRACT PROFILES                       
*                                                                               
         MVC   ACOMFACS,20(R1)                                                  
*                                                                               
         MVI   MYFLAGS,0                                                        
*                                                                               
         L     R2,8(R1)            ROUTINES                                     
         MVC   VOUTDAY(24),0(R2)                                                
         MVI   MAX,0                                                            
         MVC   MAX+1(1),4(R1)      MAX LINES                                    
         MVI   4(R1),0             NO LINES SO FAR                              
         MVC   DATADISP,=H'34'                                                  
         XIT1                                                                   
         LTORG                                                                  
*                                                                               
* DISPLAY PLAN TOTAL REC LINE                                                   
*                                                                               
DISPLAN  NTR1  BASE=*,LABEL=*                                                   
         L     R1,SAVER1                                                        
         MVI   4(R1),1             1 LINE                                       
         MVC   PRAT-28(27),=C'WEEKLY RATE FOR PLAN XXX IS'                      
         MVC   PRAT-7(3),RBUYKPLN                                               
         MVC   DUB(4),RBUYCOS                                                   
         L     R4,DUB                                                           
         EDIT  (R4),(10,PRAT),2,FLOAT=-,COMMAS=YES,ALIGN=LEFT                   
         EDIT  (2,RBUYTSPT),(4,PTOT-1)                                          
         XIT                                                                    
         LTORG                                                                  
***********************************************************************         
* BUILD PRINT LINE                                                              
* CHECK FOR MODIFICATION CODE PRINT                                             
***********************************************************************         
DISMOD   NTR1  BASE=*,LABEL=*                                                   
         TM    RCONMODR+1,X'C0'    X'C0'=ACE/GRAPHNET CONTRACT                  
         BZ    P6P                                                              
*                                                                               
         SR    R4,R4                                                            
         LA    R1,RCONELEM                                                      
P6C      ZIC   R4,1(R1)            FIND X'1F' ELEMENT                           
         AR    R1,R4                                                            
         CLI   0(R1),0                                                          
         BNE   *+6                                                              
         DC    H'0'                ELEMENT SHOULD BE THERE                      
         CLI   0(R1),X'1F'                                                      
         BNE   P6C                                                              
*                                                                               
         MVC   SVCONF,6(R1)        SAVE CONFIRMED STATUS                        
         TM    6(R1),X'80'         X'80'=NOT CONFIRMED                          
         BZ    P6F                                                              
*                                                                               
         ZIC   R4,1(R1)                                                         
         AR    R1,R4               GET X'20' (SEND) ELEMENT                     
         CLI   0(R1),X'20'                                                      
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         CLI   5(R1),1             DON'T PRINT CHANGES WHEN                     
         BNE   P6D                 REP VERSION IS 1 AND                         
         CLI   14(R1),0            STATION VERSION IS 0                         
         BNE   P6D                                                              
         TM    PROFILES+CNTDCANB,CNTDCANA  DISPLAY CANCELLED LINES?             
         BZ    P7                                                               
         CLI   RBUYCHGI,C'C'       CANCELLED?                                   
         BNE   P7                                                               
*                                                                               
P6D      CLC   5(1,R1),14(R1)      COMPARE REP TO STATION VERSION               
         BH    P6E       THEN COMPARE BUY VERSION TO HIGHER OF THE TWO          
*                        & DON'T PRINT CHANGES FOR OLD VERSIONS                 
         CLC   RBUYVER,14(R1)      STATION VERSION                              
         BE    P6S                                                              
         CLI   RBUYCHGI,C'C'       CANCELLED?                                   
         BNE   P7                  OTHERWISE, DON'T PRINT MOD CODE              
         TM    PROFILES+CNTDCANB,CNTDCANA  DISPLAY CANCELLED LINES?             
         BO    P6S                                                              
         B     P7XL                DON'T PRINT LINE AT ALL                      
*                                                                               
P6E      CLC   RBUYVER,5(R1)       REP VERSION                                  
         BE    P6S                                                              
         CLI   RBUYCHGI,C'C'       CANCELLED?                                   
         BNE   P7                  OTHERWISE, DON'T PRINT MOD CODE              
         TM    PROFILES+CNTDCANB,CNTDCANA  DISPLAY CANCELLED LINES?             
         BO    P6S                                                              
         B     P7XL                DON'T PRINT LINE AT ALL                      
         SPACE 1                                                                
* CONFIRMED -  ONLY PRINT CHANGES FROM LAST MOD NUMBER, AND DON'T               
*              PRINT CHANGE CODE O (ORDER COMMENT)                              
         SPACE 1                                                                
P6F      DS    0H                                                               
*                                                                               
         CLI   RCONMOD,0           DON'T PRINT CHANGES FOR MOD 0                
         BE    P6FA                                                             
*                                                                               
         TM    PROFILES+CNTDCANB,CNTDCANA  DISPLAY CANCELLED LINES?             
         BO    P6F10                                                            
         CLI   RBUYCHGI,C'C'       CANCELLED?                                   
         BNE   P6F10               CHECK IF CANCELLED AND ADDED IN THE          
         LA    R6,RBUYREC          SAME MOD NUM                                 
         MVI   ELCODE,X'10'        EXTRA DESCRIPTION ELEMENT PRESENT?           
         BRAS  RE,GETEL                                                         
         BNE   P6F10                                                            
         USING RBUYXXEL,R6         COMPARE                                      
         CLC   RBUYXXMD,RBUYKMOD   MOD NUMBER WHEN BUY WAS ADDED AND            
         BE    P7XL                MOD NUMBER WHEN IT WAS CANCELLED             
         DROP  R6                  DON'T PRINT LINE IF SAME                     
*                                                                               
P6F10    DS    0H                                                               
         ZIC   R4,RCONMOD                                                       
         BCTR  R4,0                GET LAST MOD NUMBER                          
         ZIC   R1,RBUYKMOD                                                      
         CR    R1,R4                                                            
         BE    P6G                                                              
P6FA     CLI   RBUYCHGI,C'C'       CANCELLED?                                   
         BNE   P7                  OTHERWISE, DON'T PRINT MOD CODE              
         TM    PROFILES+CNTDCANB,CNTDCANA  DISPLAY CANCELLED LINES?             
         BZ    P7XL                DON'T PRINT LINE AT ALL                      
         SPACE 1                                                                
P6G      DS    0H                                                               
         LA    R4,PCHG                                                          
         CLI   RBUYCHGI,C'O'       ORDER COMMENT                                
         BE    P6H                                                              
         MVC   0(1,R4),RBUYCHGI                                                 
         LA    R4,1(R4)                                                         
         MVI   0(R4),C' '          MOVE BLANK TO 2ND CHARACTER                  
         SPACE 1                                                                
P6H      CLI   RBUYCHGI+1,C'O'     ORDER COMMENT                                
         BE    P7                                                               
         CLI   RBUYCHGI+1,C' '     BLANK                                        
         BE    P7                                                               
         MVC   0(1,R4),RBUYCHGI+1                                               
         OC    PCHG,=C'  '         SPACES                                       
         B     P7                                                               
         SPACE 1                                                                
* FOR NON ACE/GRAPHNET CONTRACTS                                                
         SPACE 1                                                                
P6P      DS    0H                                                               
         CLI   RBUYCHGI,C'C'       CANCELLED?                                   
         BNE   P6Q                                                              
         TM    PROFILES+CNTDCANB,CNTDCANA  DISPLAY CANCELLED LINES?             
         BO    P6S                                                              
P6Q      DS    0H                                                               
         CLI   RCONMOD,0           K MOD NUM                                    
         BE    P7                                                               
         CLI   RCONMOD,X'FF'                                                    
         BE    P7                                                               
         CLI   RBUYKMOD,X'FF'                                                   
         BE    P7                                                               
         CLI   RBUYKMOD,0          BUY K MOD NUM                                
         BE    P7                                                               
         CLC   RBUYKMOD,RCONMOD                                                 
         BL    P7                                                               
*                                                                               
P6S      MVC   PCHG,RBUYCHGI                                                    
         B     P7                                                               
*                                                                               
P7XL     DS    0H                                                               
         LTR   RB,RB               SKIP: CC<>                                   
         B     P7X                                                              
P7       DS    0H                                                               
         CR    RB,RB               OK: CC=                                      
P7X      XIT1                                                                   
         LTORG                                                                  
**********************************************************************          
* SBTYP - FETCH SPORTS BUY TYPE DESCRIPTION FROM EXTERNAL TABLE                 
**********************************************************************          
SBTYP    NTR1  BASE=*,LABEL=*                                                   
         GOTO1 (RFGETTAB,VREPFACS),DMCB,('RTSPORTS',0)                          
         BE    *+6                                                              
         DC    H'0'                          MUST BE THERE                      
         L     R5,0(R1)                                                         
         MVC   HALF,0(R5)                    TABLE ENTRY LENGTH                 
         LA    R5,4(R5)                      START OF TABLE                     
SBTYP10  DS    0H                                                               
         CLI   0(R5),X'FF'                   BUY TYPE NOT MATCHED               
         BNE   *+6                                                              
         DC    H'0'                                                             
         CLC   RBUYSTYP,67(R5)                                                  
         BE    SBTYP20                                                          
         AH    R5,HALF                                                          
         B     SBTYP10                                                          
*                                                                               
SBTYP20  DS    0H                                                               
         XC    PSTYPE,PSTYPE                                                    
         MVI   PSTYPE,C'*'                                                      
         MVC   PSTYPE+1(15),52(R5)   SPORTS BUY TYPE LABEL                      
         LA    R5,PSTYPE+16                                                     
         BCTR  R5,0                                                             
         CLI   0(R5),C' '                                                       
         BE    *-6                                                              
         MVI   1(R5),C'*'                                                       
         XIT1                                                                   
         LTORG                                                                  
**********************************************************************          
* GENERATE PGM= DISPLAY LINE                                                    
**********************************************************************          
DISPGM   NTR1  BASE=*,LABEL=*                                                   
         LR    R7,R6                                                            
         LR    R0,R6               SAVE R6                                      
         LA    R6,RBUYREC                                                       
         MVI   ELCODE,X'21'                                                     
         BRAS  RE,GETEL                                                         
         BE    DPGM10                                                           
         XIT1                                                                   
*                                                                               
DPGM10   CH    R7,MAX                                                           
         BNL   DPGM100                                                          
         MHI   R7,L'PRTLN                                                       
         L     R1,SAVER1                                                        
         L     R9,4(R1)            OUTPUT                                       
         AR    R9,R7               POSITION TO NEXT AVAILABLE LINE              
*                                                                               
         MVC   PSDESC(4),=C'PGM='  LABEL                                        
         LA    R4,PSDESC+4                                                      
         ZIC   R1,1(R6)                                                         
         AHI   R1,-3                                                            
         EX    R1,*+4                                                           
         MVC   0(0,R4),2(R6)       PGM NAME                                     
         MVI   PDAY,X'FF'          MARKER                                       
         LR    R6,R0               RESTORE R6                                   
         AHI   R6,1                LINE COUNT+1                                 
         AHI   R9,L'PRTLN                                                       
*                                                                               
DPGM100  DS    0H                                                               
         LR    R7,R6                                                            
         XIT1  REGS=(R6,R9)                                                     
         LTORG                                                                  
         EJECT                                                                  
*                                                                               
**********************************************************************          
* GENERATE DEMO LINE                                                            
**********************************************************************          
DISDEM   NTR1  BASE=*,LABEL=*                                                   
         ZAP   SVRTG,=P'0'         INIT RATING                                  
         MVI   DEMOFLGS,0                                                       
         LR    R7,R6                                                            
         LR    R0,R6               SAVE R6                                      
         GOTOR PICKDEM                                                          
         BE    DDEM10                                                           
DDEM05   DS    0H                                                               
         XIT1                                                                   
*                                                                               
DDEM10   DS    0H                                                               
         CH    R0,MAX                                                           
         BNL   DDEM80                                                           
         MHI   R0,L'PRTLN                                                       
         L     R1,SAVER1                                                        
         L     R9,4(R1)            OUTPUT                                       
         AR    R9,R0               POSITION TO NEXT AVAILABLE LINE              
*                                                                               
DDEM30   DS    0H                                                               
         MVI   PDAY,X'FF'                                                       
*        LA    R5,PSDESC           SET A(PRINTOUT)                              
         USING RBUYRDCV,R6         R6->DEMO TO BE USED                          
         OC    RBUYRDCT,RBUYRDCT   BUG FIX                                      
         BZ    DDEM05                                                           
*                                                                               
         XC    WORK(30),WORK                                                    
         MVC   WORK(L'RBUYRDCT),RBUYRDCT                                        
         DROP  R6                                                               
*                                                                               
         CLI   WORK,C'('           USER DEFINED DEMOS                           
         BE    DDEM45                                                           
         CLI   WORK+1,C'T'         FUDGE FOR DEMOCON                            
         BNE   *+8                                                              
         MVI   WORK+1,C'I'                                                      
*                                                                               
DDEM40   DS    0H                                                               
         LA    R4,IO3                                                           
         USING DBLOCKD,R4                                                       
         XC    DBLOCK,DBLOCK                                                    
         MVC   DBFILE,=C'INV'                                                   
         MVC   DBCOMFCS,ACOMFACS                                                
         MVI   DBSELMED,C'T'                                                    
*                                                                               
         GOTO1 VDEMOCON,DMCB,WORK,(2,PSDESC),(0,DBLOCKD)                        
         DROP  R4                                                               
         B     DDEM50                                                           
DDEM45   MVC   PSDESC(L'RBUYRDCT),0(R6)   USER DEFINE DEMO,MOVE TO SCR          
*                                                                               
DDEM50   DS    0H                                                               
         MVI   PSDESC+7,C':'                                                    
*                                                                               
         MVI   PSDESC+8,C'A'                 R6->DEMO TO BE USED                
         CLI   0(R3),X'0D'                                                      
         BE    DDEM60                                                           
*                                                                               
         MVI   PSDESC+8,C'R'       THIS IS A REP DEMO                           
         TM    DEMOFLGS,X'80'      AGENCY DEMO PRESENT?                         
         BZ    DDEM60                                                           
         MVI   PSDESC+9,C'*'       YES, MARK REP OVERRIDE                       
*                                                                               
DDEM60   DS    0H                                                               
         USING RBUYRDCV,R6                                                      
         CLC   RBUYRD2M,=F'-1'                                                  
         BE    DDEM65                                                           
         CLC   RBUYRD2M,RBUYRDDM                                                
         BE    DDEM65                                                           
         MVI   PSDESC+9,C'*'                                                    
DDEM65   DS    0H                                                               
*                                                                               
* DISPLAY DEMO VALUE FOR THIS PARTICULAR BUYLINE                                
*                                                                               
         CLC   RBUYRDDM,=F'-1'     -1 = NO VALUE                                
         BE    DDEM80                                                           
         ZAP   WORK(5),=P'0'                                                    
         MVO   WORK(5),RBUYRDDM                                                 
*                                                                               
         ZAP   SVRTG,WORK(5)       SAVE RATING                                  
*                                                                               
         EDIT  (P5,WORK),(8,PSDESC+10),1,ALIGN=LEFT,ZERO=NOBLANK                
*                                                                               
*        PRINT GRPS FOR BUY                                                     
*                                                                               
         ICM   RF,15,SV#SPTS       NUMBER OF SPOTS                              
         BZ    DDEM80                 NO SPOTS                                  
         CVD   RF,DUB              CVD                                          
*                                                                               
         CP    SVRTG,=P'0'         SKIP IF NO DEMO                              
         BE    DDEM80                                                           
*                                                                               
         ZAP   WORK(16),SVRTG      MOVE RATING TO WORK AREA                     
         MP    WORK(16),DUB        CALCULATE GRPS                               
         ZAP   WORK(8),WORK(16)                                                 
*                                                                               
         MVC   PSDESC+26(5),=C'GRPS:'                                           
*                                                                               
         EDIT  (P8,WORK),(8,PSDESC+32),1,ALIGN=LEFT,ZERO=NOBLANK                
*                                                                               
         DROP  R6                                                               
*                                                                               
DDEM80   DS    0H                                                               
         LR    R6,R7                                                            
         AHI   R6,1                                                             
         LR    R7,R6                                                            
         AHI   R9,L'PRTLN                                                       
         XIT1  REGS=(R6,R9)                                                     
         LTORG                                                                  
         EJECT                                                                  
************************************************************                    
* DETERMINE WHICH DEMO TO USE IN 0E AND 0D                                      
* IF THE LOGIC IS CHANGED, THEN SHOULD CHANGE THE SAME ROUNTINE                 
* IN MODULE RECNT29                                                             
************************************************************                    
PICKDEM  NTR1  BASE=*,LABEL=*                                                   
         LA    R6,RBUYREC                                                       
         MVI   ELCODE,X'0D'                                                     
         BRAS  RE,GETEL                                                         
         BNE   *+8                                                              
         OI    DEMOFLGS,X'80'      DARE AGENCY DEMO PRESENT                     
*                                                                               
         LA    R6,RBUYREC                                                       
         MVI   ELCODE,X'0E'                                                     
         BRAS  RE,GETEL                                                         
         BNE   NO0EELT                                                          
*                                                                               
HAS0EELT DS    0H                                                               
         CLI   1(R6),18            OLD DEMO LENGTH                              
         BE    PICKDNO                                                          
         CLI   1(R6),34            OLD DEMO LENGTH                              
         BE    PICKDNO                                                          
         LR    R3,R6                                                            
         LA    R7,2(R6)            R7->FIRST DEMO IN 0E ELT                     
*                                                                               
         GOTOR LOOPDEM                                                          
         BE    *+10                                                             
         LR    R6,R7               NO PRIMARY DEM, USE THE FIRST                
         B     PICKDYES                                                         
*                                                                               
         USING RBUYRDCV,R6                                                      
         LR    R5,R6               R5->PRIMARY IN 0E                            
         CLC   RBUYRDDM,=F'-1'                                                  
         BNE   PICKDYES            PRIMARY <> -1, USE IT                        
         DROP  R6                                                               
*                                                                               
*  BELOW DEALS WITH SITUATION WHEN PRIMARY DEMO VALUE IN 0E = -1                
         LA    R6,RBUYREC          PRIMARY IN 0E = -1,LOOK FOR 0D               
         MVI   ELCODE,X'0D'                                                     
         BRAS  RE,GETEL                                                         
         BE    *+10                                                             
         LR    R6,R5               NO 0D, USE THE PRIMARY IN 0E                 
         B     PICKDYES                                                         
*                                                                               
         GOTOR LOOPDEM                                                          
         BE    *+10                                                             
         LR    R6,R5               0D HAS NO PRIMARY, USE PRIMARY IN 0E         
         B     PICKDYES                                                         
*                                                                               
         USING RBUYDMCV,R6                                                      
         CLC   RBUYDMDM,=F'-1'     IF 0D'S PRIMARY HAS A VALUE OF -1            
         BNE   PICKDYES            OTHERWISE USE PRIMARY IN 0D                  
         LR    R6,R5               THEN USE THE PRIMARY IN 0E                   
         B     PICKDYES                                                         
         DROP  R6                                                               
*                                                                               
*                                                                               
NO0EELT  DS    0H                                                               
         LA    R6,RBUYREC                                                       
         MVI   ELCODE,X'0D'                                                     
         BRAS  RE,GETEL            NO DEM CAT                                   
         BNE   PICKDNO             PROTECT INPUT FIELD                          
*                                                                               
         CLI   1(R6),18            OLD DEMO LENGTH                              
         BE    PICKDNO                                                          
         CLI   1(R6),34            OLD DEMO LENGTH                              
         BE    PICKDNO                                                          
         LR    R3,R6                                                            
         LA    R8,2(R6)            R8->FIRST DEMO IN 0D ELT                     
*                                                                               
         GOTOR LOOPDEM                                                          
         BE    PICKDYES                                                         
         LR    R6,R8               USE THE FIRST DEMO IN 0D                     
PICKDYES SR    RC,RC                                                            
PICKDNO  LTR   RC,RC                                                            
PICKDEMX XIT1  REGS=(R3,R6)        R3->ELEMENT CONTAINS THE DEM CAT             
*                                                                               
***************************************                                         
* LOOP UNTIL WE FIND A PRIMARY DEMO                                             
***************************************                                         
LOOPDEM  NTR1  BASE=*,LABEL=*       R6->ELT                                     
         ZIC   R5,1(R6)                                                         
         AR    R5,R6                <BOUNDRY>                                   
         LA    R6,2(R6)                                                         
LOOPD10  DS    0H                                                               
         CLI   0(R6),C'('          USER DEFINED DEMO?                           
         BE    LOOPD30             YES, SKIP                                    
         TM    0(R6),X'40'          PRIMARY DEM?                                
         BO    LOOPDYES                                                         
LOOPD30  DS    0H                                                               
         LA    R6,L'RBUYDMCV(R6)                                                
         CR    R6,R5                                                            
         BNL   LOOPDNO                                                          
         B     LOOPD10                                                          
LOOPDYES SR    RC,RC                                                            
LOOPDNO  LTR   RC,RC                                                            
LOOPDEMX XIT1  REGS=(R6)                                                        
*                                                                               
*                                                                               
**********************************************************************          
* GENERATE PTN: DISPLAY                                                         
**********************************************************************          
DISPTN   NTR1  BASE=*,LABEL=*                                                   
         LR    R7,R6                                                            
         LR    R0,R6               SAVE R6                                      
         LA    R6,RBUYREC                                                       
         MVI   ELCODE,X'20'                                                     
         BRAS  RE,GETEL                                                         
         BE    DPTN10                                                           
         XIT1                                                                   
*                                                                               
DPTN10   CH    R7,MAX                                                           
         BNL   DPTN100                                                          
         MHI   R7,L'PRTLN                                                       
         L     R1,SAVER1                                                        
         L     R9,4(R1)            OUTPUT                                       
         AR    R9,R7               POSITION TO NEXT AVAILABLE LINE              
*                                                                               
         USING RBUYPTEL,R6                                                      
         MVC   PSDESC(4),=C'PTN='  LABEL                                        
         LA    R4,PSDESC+4                                                      
         MVC   0(L'RBUYPTPT,R4),RBUYPTPT                                        
         LA    R4,L'RBUYPTPT+2(R4)                                              
         MVC   0(9,R4),=C'NOTATION='                                            
         LA    R4,10(R4)                                                        
         MVC   0(L'RBUYPTNT,R4),RBUYPTNT                                        
         MVI   PDAY,X'FF'          MARKER                                       
         DROP  R6                                                               
         LR    R6,R0               RESTORE R6                                   
         LA    R6,1(R6)            LINE COUNT+1                                 
*                                                                               
DPTN100  DS    0H                                                               
         LR    R7,R6                                                            
         XIT1  REGS=(R6,R9)                                                     
         LTORG                                                                  
         EJECT                                                                  
**********************************************************************          
* SPORTS BUY DESCRIPTIVE LINE DISPLAY                                           
**********************************************************************          
SBDESC   NTR1  BASE=*,LABEL=*                                                   
         GOTO1 (RFGETTAB,VREPFACS),DMCB,('RTSPORTS',0)                          
         BE    *+6                                                              
         DC    H'0'                          MUST BE THERE                      
         L     R5,0(R1)                                                         
         MVC   HALF,0(R5)                    TABLE ENTRY LENGTH                 
         LA    R5,4(R5)                      START OF TABLE                     
SBD10    DS    0H                                                               
         CLI   0(R5),X'FF'                   BUY TYPE NOT MATCHED               
         BNE   *+6                                                              
         DC    H'0'                                                             
         CLC   RBUYSTYP,67(R5)                                                  
         BE    SBD20                                                            
         AH    R5,HALF                                                          
         B     SBD10                                                            
*                                                                               
SBD20    DS    0H                                                               
         LR    R7,R6                                                            
         LR    R0,R6               SAVE R6                                      
         LA    R6,RBUYREC                                                       
         MVI   ELCODE,X'81'                                                     
         BRAS  RE,GETEL                                                         
         BE    *+10                                                             
         LR    R6,R0               RESTORE R6                                   
         B     SBD50                                                            
*                                                                               
         CH    R7,MAX                                                           
         BNL   SBD100                                                           
         MHI   R7,L'PRTLN                                                       
         L     R1,SAVER1                                                        
         L     R9,4(R1)            OUTPUT                                       
         AR    R9,R7               POSITION TO NEXT AVAILABLE LINE              
*                                                                               
         MVC   PSDESC(24),4(R5)    DESCRIPTIVE LABEL                            
         LA    R4,PSDESC+23                                                     
         CLI   0(R4),C' '                                                       
         BNE   *+10                                                             
         BCTR  R4,0                                                             
         B     *-10                                                             
         MVI   1(R4),C':'                                                       
         ZIC   R1,1(R6)                                                         
         AHI   R1,-3                                                            
         EX    R1,*+4                                                           
         MVC   3(0,R4),2(R6)       DESCRIPTIVE 1                                
         MVI   PDAY,X'FF'          MARKER                                       
         LR    R6,R0               RESTORE R6                                   
         LA    R6,1(R6)            LINE COUNT+1                                 
*                                                                               
SBD50    DS    0H                                                               
         LR    R7,R6                                                            
         LR    R0,R6               SAVE R6                                      
         LA    R6,RBUYREC                                                       
         MVI   ELCODE,X'82'                                                     
         BRAS  RE,GETEL                                                         
         BE    *+10                                                             
         LR    R6,R0               RESTORE R6                                   
         B     SBD100                                                           
*                                                                               
         CH    R7,MAX                                                           
         BNL   SBD100                                                           
         MHI   R7,L'PRTLN                                                       
         L     R1,SAVER1                                                        
         L     R9,4(R1)            OUTPUT                                       
         AR    R9,R7               POSITION TO NEXT AVAILABLE LINE              
*                                                                               
         MVC   PSDESC(24),28(R5)   DESCRIPTIVE LABEL                            
         LA    R4,PSDESC+23                                                     
         CLI   0(R4),C' '                                                       
         BNE   *+10                                                             
         BCTR  R4,0                                                             
         B     *-10                                                             
         MVI   1(R4),C':'                                                       
         ZIC   R1,1(R6)                                                         
         AHI   R1,-3                                                            
         EX    R1,*+4                                                           
         MVC   3(0,R4),2(R6)       DESCRIPTIVE 2                                
         MVI   PDAY,X'FF'          MARKER                                       
         LR    R6,R0               RESTORE R6                                   
         LA    R6,1(R6)            LINE COUNT+1                                 
*                                                                               
SBD100   DS    0H                                                               
         LR    R7,R6                                                            
         XIT1  REGS=(R6,R9)                                                     
         LTORG                                                                  
         EJECT                                                                  
**********************************************************************          
* BUILD MADE-GOOD BUY LIST IN SORTED ORDER                                      
* SINCE X'56' IS IN DATE ORDER, WE NEED TO PUT THE MADEGOOD BUY                 
* NUMBERS IN SORTED ORDER FOR DISPLAY                                           
**********************************************************************          
BLDBLIST NTR1  BASE=*,LABEL=*                                                   
         NI    MYFLAGS,X'FF'-X'08'                                              
         XC    BUYLIST,BUYLIST                                                  
*                                                                               
         LA    R6,RBUYELEM         FOR EACH BUY WE COME ACROSS,                 
BL03     CLI   0(R6),X'56'                                                      
         BH    BLX                                                              
         BE    BL05                                                             
BL04     ZIC   R0,1(R6)                                                         
         AR    R6,R0                                                            
         CLI   0(R6),0                                                          
         BNE   BL03                                                             
         B     BLX                                                              
*                                                                               
BL05     DS    0H                                                               
         USING RBYMGSEL,R6         IN BUYLIST                                   
         LA    R3,BUYLIST          255 CELLS                                    
*                                                                               
         ZIC   R4,RBYMGSLI                                                      
         AR    R3,R4                                                            
         BCTR  R3,0                                                             
**********                                                                      
         TM    RBYMGSFG,X'80'      REPLACEMENT OFFER?                           
         BZ    BL20                                                             
         OI    0(R3),X'40'                                                      
         OI    MYFLAGS,X'08'       CONTAINS AT LEAST ONE REPLACMENT NA          
         B     BL04                                                             
*                                                                               
BL20     DS    0H                                                               
         OI    0(R3),X'80'                                                      
         OI    MYFLAGS,X'10'       CONTAINS AT LEAST ONE MAKEGOOD               
         B     BL04                                                             
*                                                                               
BLX      DS    0H                                                               
         XIT1                                                                   
         EJECT                                                                  
         LTORG                                                                  
         EJECT                                                                  
*   RETRIEVE THE X'22' ELEMENT, AND INSERT ITS CONTENTS TO THE                  
*        DAY FIELD ON THE SCREEN.  IF THIS ROUTINE IS REFERENCED,               
*        THE BUYLINE WILL ONLY CONTAIN A SINGLE X'02' ELEMENT,                  
*        BECAUSE IT WAS CONVERTED FROM KATZ THAT WAY.                           
*        R6 --> X'02' ELEMENT IN THE RECORD.                                    
*                                                                               
KZDAYERR NTR1  BASE=*,LABEL=*                                                   
         LA    R6,RBUYELEM                                                      
KZDA0020 EQU   *                                                                
         ZIC   RF,1(R6)                                                         
         AR    R6,RF               BUMP TO NEXT ELEMENT                         
         CLI   0(R6),0             END OF RECORD?                               
         BNE   *+6                 NO                                           
         DC    H'0'                ERROR RECORD MUST HAVE X'22'                 
         CLI   0(R6),X'22'         ERROR ELEMENT?                               
         BNE   KZDA0020            NO  - GO BACK FOR NEXT                       
         MVC   PDAY(10),2(R6)      YES - MOVE DATA TO FIELD                     
         XIT1                                                                   
         EJECT                                                                  
         LTORG                                                                  
         EJECT                                                                  
*                                                                               
*   RETRIEVE THE X'32' ELEMENT, AND INSERT ITS CONTENTS TO THE                  
*        TIME FIELD ON THE SCREEN.  IF THIS ROUTINE IS REFERENCED,              
*        THE BUYLINE WILL ONLY CONTAIN A SINGLE X'02' ELEMENT,                  
*        BECAUSE IT WAS CONVERTED FROM KATZ THAT WAY.                           
*        R6 --> X'02' ELEMENT IN THE RECORD.                                    
*                                                                               
KZTIMERR NTR1  BASE=*,LABEL=*                                                   
         LA    R6,RBUYELEM                                                      
KZTM0020 EQU   *                                                                
         ZIC   RF,1(R6)                                                         
         AR    R6,RF               BUMP TO NEXT ELEMENT                         
         CLI   0(R6),0             END OF RECORD?                               
         BNE   *+6                 NO                                           
         DC    H'0'                ERROR RECORD MUST HAVE X'32'                 
         CLI   0(R6),X'32'         ERROR ELEMENT?                               
         BNE   KZTM0020            NO  - GO BACK FOR NEXT                       
         MVC   PTIM(11),2(R6)      YES - MOVE DATA TO FIELD                     
         XIT1                                                                   
         EJECT                                                                  
         LTORG                                                                  
         EJECT                                                                  
**********************************************************************          
* DISPLAY MAKEGOOD REFERECES BASED ON EXISTENCE OF X'05' ELEMENTS               
* ALSO DISPLAY IF BUY IS A LATE RUN BONUS                                       
**********************************************************************          
DISPMKGS NTR1  BASE=*,LABEL=*                                                   
*                                                                               
         TM    RBUYRTS,X'20'+X'04'                                              
         BNO   DMKGS10                                                          
         MVC   PDAY+6(14),=C'LATE RUN BONUS'                                    
         B     DMKGS80                                                          
*                                                                               
DMKGS10  DS    0H                                                               
         CLI   0(R3),0                                                          
         BE    DMKGS20                                                          
         CLI   0(R3),X'04'                                                      
         BE    DMKGS15                                                          
         CLI   0(R3),X'05'                                                      
         BE    DMKGS30                                                          
         BH    DMKGS20                                                          
*                                                                               
DMKGS13  DS    0H                                                               
         ZIC   R0,1(R3)                                                         
         AR    R3,R0                                                            
         B     DMKGS10                                                          
*                                                                               
DMKGS15  DS    0H                  SKIP FOR CREDIT FOR SPOT                     
         CLC   =C'CR=',2(R3)                                                    
         BNE   DMKGS13                                                          
*                                                                               
DMKGS20  DS    0H                                                               
         XIT1                      NO X'05', RESTORE ALL REGISTERS              
*                                                                               
*                                                                               
DMKGS30  DS    0H                                                               
         TM    RBUYRTS,X'08'+X'04'                                              
         BZ    DMKGS40                                                          
         MVC   PDAY+6(22),=C'SPOT RAN LATE FOR LINE'                            
*                                                                               
         USING RBUYMGEL,R3                                                      
         LA    R5,PDAY+29          SET A(PRINTOUT)                              
         EDIT  RBUYMGLI,(3,0(R5)),ALIGN=LEFT                                    
         AR    R5,R0                                                            
         MVC   1(2,R5),=C'ON'                                                   
         AHI   R5,4                                                             
         EDIT  (1,RBUYMGD1+1),(2,0(R5)),ALIGN=LEFT                              
         AR    R5,R0                                                            
         MVI   0(R5),C'/'                                                       
         AHI   R5,1                                                             
         EDIT  (1,RBUYMGD1+2),(2,0(R5)),ALIGN=LEFT                              
         B     DMKGS80                                                          
*                                                                               
DMKGS40  DS    0H                                                               
         BAS   RE,CHKSIB           CHECK IF ANY SIBLING MAKEGOOD BUYS           
         BZ    DMKGSX                                                           
         MVC   PDAY+6(29),=C'MKGD FOR SPOTS MISSED ON LN #'                     
         LA    R5,PDAY+35          SET A(PRINTOUT)                              
         TM    RBUYRTS,X'02'                                                    
         BZ    DMKGS50                                                          
         MVC   PDAY+6(32),=C'REPLACEMENT FOR SPOTS NA ON LN #'                  
         LA    R5,PDAY+38          SET A(PRINTOUT)                              
*                                                                               
DMKGS50  DS    0H                                                               
         LA    R4,2                                                             
         B     DMKGS70                                                          
*                                                                               
DMKGS60  DS    0H                                                               
         MVI   0(R5),C','                                                       
         AHI   R5,1                                                             
         B     DMKGS73                                                          
*                                                                               
DMKGS70  DS    0H                                                               
         EDIT  RBUYMGLI,(3,0(R5)),ALIGN=LEFT                                    
         AR    R5,R0                                                            
         MVI   0(R5),C':'                                                       
         AHI   R5,1                                                             
*                                                                               
DMKGS73  DS    0H                                                               
         EDIT  (1,RBUYMGD1+1),(2,0(R5)),ALIGN=LEFT                              
         AR    R5,R0                                                            
         MVI   0(R5),C'/'                                                       
         AHI   R5,1                                                             
         EDIT  (1,RBUYMGD1+2),(2,0(R5)),ALIGN=LEFT                              
         AR    R5,R0                                                            
*                                                                               
         OC    RBUYMGD2,RBUYMGD2                                                
         BZ    DMKGS75                                                          
         MVI   0(R5),C'-'                                                       
         AHI   R5,1                                                             
         EDIT  (1,RBUYMGD2+1),(2,0(R5)),ALIGN=LEFT                              
         AR    R5,R0                                                            
         MVI   0(R5),C'/'                                                       
         AHI   R5,1                                                             
         EDIT  (1,RBUYMGD2+2),(2,0(R5)),ALIGN=LEFT                              
         AR    R5,R0                                                            
*                                                                               
DMKGS75  DS    0H                                                               
         MVI   0(R5),C'('                                                       
         AHI   R5,1                                                             
         EDIT  RBUYMGSP,(3,0(R5)),ALIGN=LEFT,ZERO=NOBLANK                       
         AR    R5,R0                                                            
         MVC   0(4,R5),=C'/WK)'                                                 
         AHI   R5,4                                                             
*                                                                               
         LR    RE,R3               IF SAME MISSED, KEEP ON SAME LINE            
         ZIC   R0,1(R3)            ELSE, BREAK TO NEW LINE                      
         AR    R3,R0                                                            
         CLI   0(R3),0                                                          
         BE    DMKGS80                                                          
         CLI   0(R3),X'05'                                                      
         BNE   DMKGS80                                                          
         CLC   RBUYMGLI,RBUYMGLI-RBUYMGEL(RE)                                   
         BNE   DMKGS78                                                          
         BCT   R4,DMKGS60                                                       
*                                                                               
DMKGS78  DS    0H                                                               
         AHI   R6,1                                                             
         AHI   R9,L'PRTLN                                                       
         CH    R6,MAX                                                           
         BNH   DMKGS40                                                          
         B     DMKGSX                                                           
*                                                                               
DMKGS80  DS    0H                                                               
         AHI   R6,1                                                             
         AHI   R9,L'PRTLN                                                       
*                                                                               
DMKGSX   DS    0H                                                               
         XIT1  REGS=(R6,R9)                                                     
         DROP  R3                                                               
         EJECT                                                                  
*                                                                               
* CHECK IF MAKEGOOD BUY IS A PART OF A MAKEGOOD OFFER PACKAGE                   
* IF SO, DISPLAY SIBLING INFO                                                   
*                                                                               
CHKSIB   NTR1                                                                   
*                                                                               
CHKS10   DS    0H                                                               
         CLI   0(R3),0                                                          
         BE    CHKSNO                                                           
         CLI   0(R3),X'26'                                                      
         BE    CHKS30                                                           
         BH    CHKSNO                                                           
*                                                                               
CHKS20   DS    0H                                                               
         ZIC   R0,1(R3)                                                         
         AR    R3,R0                                                            
         B     CHKS10                                                           
*                                                                               
CHKS30   DS    0H                                                               
         USING RBUYSLEL,R3                                                      
         CLC   RBUYKLIN,RBUYSLLK                                                
         BE    CHKSNO              FIRST MAKEGOOD BUY SIBLING NEEDS             
         ZIC   R2,RBUYSLLN         NUMBER OF SIBLINGS                           
         SHI   R2,2                DELETE OVERHEAD                              
         LA    R3,RBUYSLLK                                                      
*                                  TO DISPLAY ALL MISSED DATES/TARGETS          
CHKS40   DS    0H                                                               
         MVC   PDAY+6(31),=C'PART OF A MKGD MADE UP OF LINES'                   
         TM    RBUYRTS,X'02'                                                    
         BZ    *+10                                                             
         MVC   PDAY+16(4),=C'REPL'                                              
*                                                                               
         LA    R5,PDAY+38          SET A(PRINTOUT)                              
         LA    R4,8                                                             
         B     CHKS90                                                           
         DROP  R3                                                               
*                                                                               
CHKS50   DS    0H                                                               
         BCT   R2,CHKS60                                                        
         B     CHKS110                                                          
CHKS60   DS    0H                                                               
         C     R2,=F'1'            LAST TARGET INSERT 'AND'                     
         BNE   CHKS70                                                           
         MVC   0(2,R5),=X'4050'                                                 
         AHI   R5,3                                                             
         B     CHKS80                                                           
*                                                                               
CHKS70   DS    0H                                                               
         MVI   0(R5),C','                                                       
         AHI   R5,2                                                             
*                                                                               
CHKS80   DS    0H                                                               
         AHI   R3,1                                                             
*                                                                               
CHKS90   DS    0H                                                               
         EDIT  (1,0(R3)),(3,0(R5)),ALIGN=LEFT                                   
         AR    R5,R0                                                            
         BCT   R4,CHKS50                                                        
*                                                                               
CHKS100  DS    0H                                                               
         AHI   R6,1                                                             
         AHI   R9,L'PRTLN                                                       
         CH    R6,MAX                                                           
         BH    CHKSYES                                                          
         AHI   R3,1                                                             
         BCT   R2,CHKS40                                                        
         B     CHKSYES                                                          
*                                                                               
CHKS110  DS    0H                                                               
         AHI   R6,1                                                             
         AHI   R9,L'PRTLN                                                       
*                                                                               
CHKSYES  SR    RC,RC                                                            
CHKSNO   LTR   RC,RC                                                            
         XIT1  REGS=(R6,R9)                                                     
         EJECT                                                                  
         LTORG                                                                  
         EJECT                                                                  
*                                                                               
* PRINT LINE DSECT                                                              
       ++INCLUDE REGENPBYD                                                      
*                                                                               
WORKD    DSECT                                                                  
DUB      DS    D                                                                
DMCB     DS    6F                                                               
SAVEBX   DS    3F                                                               
SAVER1   DS    F                                                                
DAY1     DS    F                                                                
RELO     DS    A                                                                
VOUTDAY  DS    A                   A(OUTDAY)                                    
VUNTIME  DS    A                   A(UNTIME)                                    
VDATCON  DS    A                   A(DATCON)                                    
VADDAY   DS    A                   A(ADDAY)                                     
VREPFACS DS    A                   A(REPFACS)                                   
VDEMOCON DS    A                   A(DEMOCON)                                   
ACOMFACS DS    A                   A(COMFACS)                                   
MAX      DS    H                                                                
DATADISP DS    H                                                                
HALF     DS    H                                                                
TODAY    DS    CL3                                                              
NOSEND   DS    X                   DON'T SEND STATUS                            
MGCNTR#  DS    X                                                                
MGTARGET DS    CL3                                                              
ELCODE   DS    X                                                                
FLAGS    DS    X                   PASSED FROM PARM 1                           
WORK     DS    CL255                                                            
SVCONF   DS    CL1                 CONFIRMED STATUS FOR ACE/GRAPHNET            
PROFILES DS    XL8                 CONTRACT PROGRAM PROFILES                    
BUYLIST  DS    XL255                                                            
MYFLAGS  DS    X                   X'80' - SPORTS DESCRIPTIVES PRINTED          
*                                  X'40' - PGM= PRINTED                         
*                                  X'20' - PTN= PRINTED                         
*                                  X'10' - MAKEGOOD DETECTED                    
*                                  X'01' - REPLACEMENT NA DETECTED              
DEMOFLGS DS    XL1                                                              
*                                  X'80' - DARE AGENCY PRESENTS                 
SV#SPTS  DS    XL4                 NUMBER OF SPOTS IN BUYLINE                   
SVRTG    DS    PL5                 SAVE RATING FOR BUYLINE                      
*                                                                               
         DS    0D                  ALIGNMENT                                    
IO3      DS    XL280                                                            
ACAREA   DS    CL(60*10)           AUTOCOMMENT AREA                             
WORKX    EQU   *                                                                
BUYREC   DSECT                                                                  
       ++INCLUDE REGENBUY                                                       
DBLOCKD  DSECT                                                                  
       ++INCLUDE DEDBLOCK                                                       
CONREC   DSECT                                                                  
       ++INCLUDE REGENCON                                                       
       ++INCLUDE RECNTPROF                                                      
       ++INCLUDE REPFACSQ                                                       
       ++INCLUDE REPTABSQ                                                       
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'005REGENPBY  06/11/12'                                      
         END                                                                    
