*          DATA SET FAPQSEC    AT LEVEL 003 AS OF 02/09/09                      
*CATALP FAPQSEC                                                                 
                                                                                
***********************************************************************         
* VALIDATE USER PID AND/OR PIN FROM A SCREEN INPUT FIELD              *         
* DOES SCANNER ON THE FIELD AND CHECKS IF FIRST FIELD IS PID= OR PIN= *         
* IF NOT EXITS WITH NULLS IN RETURN AREA. ELSE VALIDATES.             *         
* INPUT PARAMETERS:                                                   *         
* P1  XL1=X'80' FIELD IS DEDICATED TO PID/PIN                         *         
*     AL3(SECURITY FIELD HEADER)                                      *         
* P2  A(SECRET SECURITY BLOCK) - OPTIONAL - NOT USED IN THIS VERSION  *         
* P3  A(COMFACS)                                                      *         
* P4  XL1 RETURN X'80'=ERROR,X'02'=PIN,X'01'=PID                      *         
*     AL3(BYTE RETURN FIELD)                                          *         
* RETURN FIELD FORMAT:                                                *         
* XL1 TYPE 00=NO PID/PIN,02=PIN,03=PID                                *         
* XL4 INPUT PIN NUMBER OR PID=CL2'SECURITY AGY'/XL2'PERSON NUMBER'    *         
* XL1 N/D                                                             *         
***********************************************************************         
                                                                                
         TITLE 'FAPQSEC - VALIDATES PQ PID/PIN'                                 
         PRINT NOGEN                                                            
FAPQSEC  CSECT                                                                  
         NMOD1 WORKL,**PQSE**,R8                                                
         USING WORKD,RC                                                         
         XC    WORKD(WORKI),WORKD  CLEAR FIRST PART OF W/S                      
         ST    R1,APARMS                                                        
         MVC   PARM(PARML),0(R1)   SAVE INPUT PARMS                             
         MVI   12(R1),0            SET OK RETURN                                
*                                                                               
         L     RF,ACOMFACS                                                      
         MVC   VDATAMGR,CDATAMGR-COMFACSD(RF)                                   
         MVC   VSECRET,CSECRET-COMFACSD(RF)                                     
         MVC   VSCANNER,CSCANNER-COMFACSD(RF)                                   
         MVC   VDATCON,CDATCON-COMFACSD(RF)                                     
         MVC   VDICTATE,CDICTATE-COMFACSD(RF)                                   
         MVC   VXTRAINF,CXTRAINF-COMFACSD(RF)                                   
         L     RF,VXTRAINF                                                      
         MVC   MYAGYPER(4),XIAGYPER-XTRAINFO(RF)                                
*                                                                               
PQSEC1   LA    R4,WORK             TEST FOR PID= OR PIN=                        
         USING SCANBLKD,R4                                                      
         XR    RF,RF                                                            
         ICM   RF,7,ASECFLDH                                                    
         CLI   5(RF),0             NORMAL RETURN IF NO INPUT                    
         BE    RETURN                                                           
         GOTO1 VSCANNER,DMCB,(RF),(8,(R4)) MAX OF 8 LINES                       
         MVC   LINES,4(R1)                                                      
         CLI   LINES,1             TEST NUMBER OF SCAN LINES                    
         BL    WHATXIT                                                          
         BH    PQSEC1A                                                          
         B     PQSEC1A             MUST HAVE KEYWORD=VALUE FOR PIN/PID          
         CLC   SC1STLEN(2),=X'0400'                                             
         BE    PQSEC2              ONLY A SINGLE 4-CHR FIELD INPUT              
         CLC   SC1STLEN(2),=X'0800'                                             
         BE    PQSEC3              ONLY A SINGLE 8-CHR FIELD INPUT              
*                                                                               
PQSEC1A  CLI   SC1STLEN,0          EXIT IF FIRST LINE NOT KEYWORD=VALUE         
         BE    WHATXIT                                                          
         CLI   SC2NDLEN,0                                                       
         BE    WHATXIT                                                          
         XR    R1,R1               GET LENGTH OF FIRST KEYWORD=VALUE            
         ICM   R1,1,SC1STLEN                                                    
         CHI   R1,1                TEST FOR SHORT PID/PIN CONVENTION            
         BH    PQSEC1B                                                          
         CLI   SC1STFLD,C'I'       I=PID                                        
         BE    PQSEC5                                                           
         CLI   SC1STFLD,C'N'       N=PIN                                        
         BE    PQSEC20                                                          
         B     PQSEC1C                                                          
PQSEC1B  CLC   SC1STFLD(L'DC@PID),DC@PID                                        
         BE    PQSEC5                                                           
         CLC   SC1STFLD(L'DC@PIN),DC@PIN                                        
         BE    PQSEC20                                                          
PQSEC1C  B     WHATXIT             EXIT IF FIRST NOT PID= OR PIN=               
*                                                                               
PQSEC2   XR    RF,RF               SINGLE 4-CHR FIELD INPUT                     
         ICM   RF,7,ASECFLDH                                                    
         TM    INFLAGS,X'80'       ONLY VALID FOR DEDICATED FIELD               
         BZ    WHATXIT                                                          
         OI    FLAGS,X'82'         SET FLAGS TO ASSUME ITS A PIN                
         MVC   RTNPIN,8(RF)                                                     
         OC    RTNPIN,SPACES                                                    
         MVI   RTNTYP,RTNTPINQ                                                  
         B     PQSEC22             VALIDATE PIN                                 
*                                                                               
PQSEC3   XR    RF,RF               SINGLE 8-CHR FIELD INPUT                     
         ICM   RF,7,ASECFLDH                                                    
         TM    INFLAGS,X'80'       ONLY VALID FOR DEDICATED FIELD               
         BZ    WHATXIT                                                          
         OI    FLAGS,X'81'         SET FLAGS TO ASSUME ITS A PID                
         MVC   PIDINPUT,8(RF)                                                   
         OC    PIDINPUT,SPACES                                                  
         B     PQSEC9              VALIDATE PID                                 
*                                                                               
PQSEC5   OI    FLAGS,X'01'         PID=XXXXXXXX OR PID=*                        
         XR    R1,R1                                                            
         ICM   R1,1,SC2NDLEN                                                    
         BZ    ERRXIT                                                           
         CHI   R1,1                                                             
         BH    PQSEC7                                                           
         CLI   SC2NDFLD,C'*'       TEST PID OF USER                             
         BNE   ERRXIT                                                           
         MVC   PIDINPUT,SPACES                                                  
         MVC   RTNPID(4),MYAGYPER                                               
         MVI   RTNTYP,RTNTPIDQ                                                  
         B     RETURN                                                           
*                                                                               
PQSEC7   CHI   R1,4                CHECK MIN LEN OF PID ALPHA                   
         BL    ERRXIT                                                           
         BCTR  R1,0                SAVE ALPHA PID INPUT                         
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   PIDINPUT(0),SC2NDFLD                                             
         OC    PIDINPUT,SPACES                                                  
*                                                                               
PQSEC9   LA    R2,IOKEY            READ PERSON RECORD                           
         USING SAPEREC,R2                                                       
         XC    SAPEKEY,SAPEKEY                                                  
         MVI   SAPETYP,SAPETYPQ                                                 
         MVI   SAPESUB,SAPESUBQ                                                 
         MVC   SAPEAGY,MYAGYPER                                                 
         MVC   SAPEPID,PIDINPUT                                                 
         GOTOR VDATCON,DMCB,(5,0),(2,SAPEDEF)                                   
         XC    SAPEDEF,EFFS        COMPLEMENT DATE                              
         GOTOR RDHISA                                                           
         BE    *+6                                                              
         DC    H'0'                                                             
         CLC   SAPEKEY(SAPEDEF-SAPEKEY),IOAREA                                  
         BNE   ERRXIT              NO RECORD FOUND FOR THIS PERSON              
         DROP  R2                                                               
*                                                                               
         LA    R3,IOAREA+(SAPEDATA-SAPEREC)                                     
PQSEC11  CLI   0(R3),0             TEST E-O-R                                   
         BE    ERRXIT                                                           
         CLI   0(R3),SAPWDELQ      TEST PERSON PASSWORD ELEMENT                 
         BE    PQSEC13                                                          
         XR    RF,RF                                                            
         IC    RF,1(R3)                                                         
         AR    R3,RF                                                            
         B     PQSEC11                                                          
*                                                                               
         USING SAPWDD,R3                                                        
PQSEC13  MVC   RTNPID(2),MYAGYPER  RETURN AGENCY AND PERSON NUMBER              
         MVC   RTNPID+2(2),SAPWDNUM                                             
         MVI   RTNTYP,RTNTPIDQ                                                  
         B     RETURN                                                           
         DROP  R3                                                               
*                                                                               
PQSEC20  OI    FLAGS,X'02'         PIN=XXXX                                     
         XR    R1,R1                                                            
         ICM   R1,1,SC2NDLEN                                                    
         CHI   R1,4                                                             
         BNE   ERRXIT              MUST BE 4 CHRS LONG                          
         BCTR  R1,0                                                             
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   RTNPIN(0),SC2NDFLD  RETURN PIN                                   
         OC    RTNPIN,SPACES                                                    
         MVI   RTNTYP,RTNTPINQ                                                  
*                                                                               
PQSEC22  LA    RE,RTNPIN           PIN CHRS MUST BE 0-9 OR A-Z                  
         LA    RF,4                                                             
PQSEC24  CLI   0(RE),C'A'                                                       
         BL    PQSEC28                                                          
         CLI   0(RE),C'I'                                                       
         BNH   PQSEC26                                                          
         CLI   0(RE),C'J'                                                       
         BL    PQSEC28                                                          
         CLI   0(RE),C'R'                                                       
         BNH   PQSEC26                                                          
         CLI   0(RE),C'S'                                                       
         BL    PQSEC28                                                          
         CLI   0(RE),C'Z'                                                       
         BNH   PQSEC26                                                          
         CLI   0(RE),C'0'                                                       
         BL    PQSEC28                                                          
         CLI   0(RE),C'9'                                                       
         BH    WHATXIT                                                          
PQSEC26  LA    RE,1(RE)                                                         
         BCT   RF,PQSEC24                                                       
         CLC   RTNPIN,=C'0000'     PIN CANT BE ALL ZEROS                        
         BNE   RETURN                                                           
PQSEC28  EQU   *                   PIN IS INVALID                               
*                                                                               
WHATXIT  TM    INFLAGS,X'80'       TEST IF FIELD MUST BE VALID                  
         BO    ERRXIT                                                           
         MVI   RTNTYP,0            SET NOT A PID OR PIN                         
         B     RETURN                                                           
*                                                                               
ERRXIT   XC    RTN(RTNL),RTN       RETURN NULLS                                 
         L     R1,APARMS                                                        
         MVC   12(1,R1),FLAGS      SET IF PIN/PID                               
         OI    12(R1),X'80'        SET ERROR FLAG                               
*                                                                               
RETURN   XR    RF,RF               RETURN TYPE AND VALUE                        
         ICM   RF,7,ARETURN                                                     
         MVC   0(RTNL,RF),RTN                                                   
         B     XIT                                                              
         DROP  R4,R8                                                            
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO READ SECURITY ACCESS RECORD                              *         
***********************************************************************         
                                                                                
READSA   NTR1                                                                   
         MVC   IOAREA(L'IOKEY),IOKEY                                            
         GOTOR VDATAMGR,DMCB,DMREAD,CTFILE,IOAREA,IOAREA                        
         B     XIT                                                              
                                                                                
***********************************************************************         
* ROUTINE TO READ HIGH FOR A SECURITY ACCESS RECORD                   *         
***********************************************************************         
                                                                                
RDHISA   NTR1                                                                   
         MVC   IOAREA(L'IOKEY),IOKEY                                            
         GOTOR VDATAMGR,DMCB,DMRDHI,CTFILE,IOAREA,IOAREA                        
         B     XIT                                                              
                                                                                
***********************************************************************         
* ROUTINE TO READ SEQUENTIAL FOR A SECURITY ACCESS RECORD             *         
***********************************************************************         
                                                                                
RDSQSA   NTR1                                                                   
         MVC   IOAREA(L'IOKEY),IOKEY                                            
         GOTOR VDATAMGR,DMCB,DMRDSQ,CTFILE,IOAREA,IOAREA                        
         B     XIT                                                              
                                                                                
XIT      XIT1                                                                   
         EJECT                                                                  
***********************************************************************         
* DATA CONSTANTS AND LITERAL POOL                                     *         
***********************************************************************         
FF       EQU   X'FF'                                                            
EFFS     DC    X'FFFFFFFFFFFFFFFF'                                              
SPACES   DC    C'        '                                                      
*                                                                               
DMREAD   DC    C'DMREAD  '         FOR DATA MANAGER CALLS                       
DMRDHI   DC    C'DMRDHI  '                                                      
DMRDSQ   DC    C'DMRSEQ  '                                                      
CTFILE   DC    C'CTFILE  '                                                      
*                                                                               
DC@PID   DC    CL8'PID'                                                         
DC@PIN   DC    CL8'PIN'                                                         
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
* LOCAL WORKING STORAGE                                               *         
***********************************************************************         
WORKD    DSECT                                                                  
APARMS   DS    A                   A(INPUT PARMS)                               
*                                                                               
PARM     DS    0F                  INPUT PARAMTERS                              
INFLAGS  DS    X                   INPUT FLAGS                                  
ASECFLDH DS    AL3                 A(FIELD HEADER)                              
ASECBLK  DS    A                   A(SECRECT BLOCK)                             
ACOMFACS DS    A                   A(COMFACS)                                   
OTFLAGS  DS    X                   OUTPUT FLAGS                                 
ARETURN  DS    AL3                 A(RETURN DATA)                               
PARML    EQU   *-PARM                                                           
*                                                                               
VDATAMGR DS    V(DATAMGR)          A(DATA MANAGER)                              
VSECRET  DS    V(SECRET)           A(SECRET)                                    
VSCANNER DS    V(SCANNER)          A(SCANNER)                                   
VDATCON  DS    V(DATCON)           A(DATCON)                                    
VDICTATE DS    V(DICTATE)          A(DICTATE)                                   
VXTRAINF DS    A                   A(EXTRA CONNECT INFO)                        
*                                                                               
DMCB     DS    6F                                                               
*                                                                               
MYAGYPER DS    CL2                 PERSON SECURITY AGENCY                       
MYPIDNUM DS    XL2                 PERSON PID NUMBER                            
*                                                                               
RTN      DS    0XL6                RETURN BLOCK                                 
RTNTYP   DS    XL1                 DATA TYPE                                    
RTNTPINQ EQU   2                   PIN                                          
RTNTPIDQ EQU   3                   PID                                          
RTNPID   DS    0XL4                PID OR PIN                                   
RTNPIN   DS    CL4                                                              
         DS    XL1                 N/D                                          
RTNL     EQU   *-RTN                                                            
*                                                                               
LINES    DS    X                   SCAN LINES                                   
FLAGS    DS    X                   SCAN FLAGS                                   
*                                                                               
PIDINPUT DS    CL8                 PID INPUT VALUE                              
*                                                                               
WORKI    EQU   *-WORKD             END OF INITIALIZED AREA                      
*                                                                               
         DS    0D                                                               
WORK     DS    CL256               USED TO CALL SCANNER                         
*                                                                               
IOKEY    DS    XL(L'SAASKEY)       IO KEY                                       
IOAREA   DS    XL2048              IO AREA                                      
*                                                                               
WORKL    EQU   *-WORKD             END OF WHOLE AREA                            
                                                                                
         EJECT                                                                  
* DDCOMFACS                                                                     
         PRINT OFF                                                              
       ++INCLUDE DDCOMFACS                                                      
         PRINT ON                                                               
                                                                                
* DDDSCANBLKD                                                                   
         PRINT OFF                                                              
       ++INCLUDE DDSCANBLKD                                                     
         PRINT ON                                                               
                                                                                
* FAXTRAINF                                                                     
         PRINT OFF                                                              
       ++INCLUDE FAXTRAINF                                                      
         PRINT ON                                                               
                                                                                
* SEACSFILE                                                                     
         PRINT OFF                                                              
       ++INCLUDE SEACSFILE                                                      
         PRINT ON                                                               
                                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'003FAPQSEC   02/09/09'                                      
         END                                                                    
