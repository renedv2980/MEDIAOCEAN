*          DATA SET ACGETSAL   AT LEVEL 003 AS OF 05/01/02                      
*CATALP ACGETSAL                                                                
         TITLE 'ACGETSAL - CALCULATE SALARY INFORMATION (NEWCOST)'              
*                          (NEWCOST VERSION)                                    
*                                                                               
*              PARM 1                   A(ACSALHSTD I/O BLOCK)                  
*              PARM 2                   A(2000 BYTE IO AREA)                    
*              PARM 3                   A(DATAMGR)                              
*                                                                               
ACGETSAL CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 ACSALQ,*ACGSAL*                                                  
         USING ACSALD,RC                                                        
         MVC   PARMS(PARMQ),0(R1)                                               
         EJECT                                                                  
***********************************************************************         
*              PARSE PARAMETER LIST                                   *         
***********************************************************************         
*                                                                               
         MVC   ABLOCK,PARM1        *** PARM #1 ***                              
         MVC   AIO,PARM2           *** PARM #2 ***                              
         MVC   DATAMGR,PARM3       *** PARM #3 ***                              
*                                                                               
         USING SALARYD,R6          R6 --> A(I/O BLOCK)                          
         L     R6,ABLOCK                                                        
         LA    RF,SAL$INFO         INITIALIZE I/O BLOCK ACCUMS                  
         LA    R0,SAL$INF#                                                      
         ZAP   0(L'SAL$INFO,RF),=P'0'                                           
         LA    RF,L'SAL$INFO(RF)                                                
         BCT   R0,*-10                                                          
*                                                                               
         MVI   SALSTAT,SALOK                                                    
         CLC   SALSTDTE,SALENDTE                                                
         BNH   *+12                                                             
         OI    SALSTAT,SALIDATE    END NOT > THAN START DATE                    
         B     EXIT                                                             
*                                                                               
         MVI   SPACES,C' '                                                      
         MVC   SPACES+1(L'SPACES-1),SPACES                                      
         EJECT                                                                  
***********************************************************************         
*              READ METHOD RECORD                                     *         
***********************************************************************         
*                                                                               
METH     DS    0H                                                               
         USING PYCODED,R5          R5 --> A(PAYROLL TYPE TABLE)                 
         LA    R5,PYTABLE                                                       
*                                                                               
         USING CAHRECD,R4          R4 --> A(COST ALLOCATION HISTORY)            
         L     R4,AIO                                                           
         MVC   CAHKEY,SPACES                                                    
         MVI   CAHKTYP,CAHKTYPQ    X'3E'                                        
         MVI   CAHKSUB,CAHKSUBQ    X'01'                                        
         MVC   CAHKCPY,SALCPY      COMPANY                                      
         MVC   CAHKMTHD,SALMETHD   METHOD CODE                                  
         XC    CAHKOFC,CAHKOFC                                                  
         GOTO1 DATAMGR,DMCB,DMREAD,ACCFIL,(R4),(R4)                             
         CLI   DMCB+8,0                                                         
         BE    *+12                                                             
         OI    SALSTAT,SALINMET    MISSING METHOD CODE RECORD                   
         B     EXIT                                                             
*                                                                               
         MVI   ELCODE,PATELQ       X'85' ELEMENT                                
         BAS   RE,GETEL                                                         
         BE    *+12                                                             
         OI    SALSTAT,SALINX85    NO PAYROLL CODE ELEMENT                      
         B     EXIT                                                             
*                                                                               
         USING PATELD,R4                                                        
         SR    R3,R3                                                            
METH100  MVC   PYNUM,PATNUM        PAYROLL CODE NUMBER                          
         MVC   PYTYPE,PATTYPE      SALARY TYPE                                  
         LA    R3,1(R3)            R3 = COUNTER FOR # TABLE ENTRIES             
         CH    R3,=Y(PYTABLEX)                                                  
         BNH   *+6                                                              
         DC    H'0'                PYTABLE IS FULL                              
         LA    R5,PYLNQ(R5)                                                     
         BAS   RE,NEXTEL                                                        
         BE    METH100                                                          
         STC   R3,PYTABLE#         SAVE # TABLE ENTRIES                         
         EJECT                                                                  
***********************************************************************         
*              PAYROLL HISTORY RECORD                                 *         
***********************************************************************         
*                                                                               
         USING PHIRECD,R4                                                       
HIST     L     R4,AIO                                                           
         MVC   PHIKEY,SPACES                                                    
         MVI   PHIKTYP,PHIKTYPQ    X'3E'                                        
         MVI   PHIKSUB,PHIKSUBQ    X'05'                                        
         MVC   PHIKCPY,SALCPY                                                   
         MVC   PHIKOFC,SALOFC                                                   
         MVC   PHIKDPT,SALDPT                                                   
         MVC   PHIKSBD,SALSBD                                                   
         MVC   PHIKPER,SALPER                                                   
         SR    R1,R1                                                            
         ICM   R1,3,SALENDTE       HIGHEST DATES COME FIRST ON FILE             
         LNR   R1,R1               DATE STORED AS X'FFFF'-YYMM                  
         STCM  R1,3,PHIKMOA                                                     
         MVI   PHIKSEQ,0                                                        
         MVC   SVKEY,PHIKEY                                                     
         MVC   COMMAND,DMRDHI                                                   
*                                                                               
HIST100  GOTO1 DATAMGR,DMCB,COMMAND,ACCFIL,(R4),(R4)                            
         CLC   PHIKEY(PHIKMOA-PHIKEY),SVKEY                                     
         BNE   HIST300                                                          
         MVC   COMMAND,DMRSEQ                                                   
         SR    R1,R1                                                            
         ICM   R1,3,PHIKMOA                                                     
         LNR   R1,R1                                                            
         MVC   HALF,SALSTDTE                                                    
         CH    R1,HALF             STOP READING WHEN < START DATE               
         BL    HIST300                                                          
*                                                                               
         USING PDEELD,R4                                                        
         MVI   ELCODE,PDEELQ       X'86' ELEMENT                                
         BAS   RE,GETEL                                                         
         BNE   HIST100                                                          
         B     *+12                                                             
*                                                                               
HIST200  BAS   RE,NEXTEL                                                        
         BNE   HIST100                                                          
         CLC   PDEDTE,SALSTDTE                                                  
         BL    HIST200                                                          
         CLC   PDEDTE,SALENDTE                                                  
         BH    HIST200                                                          
         OC    SALPCFLT,SALPCFLT   FILTER ON PAYROLL TYPE                       
         BZ    *+14                                                             
         CLC   SALPCFLT,PDENUM                                                  
         BNE   HIST200                                                          
*                                                                               
         USING PYCODED,R5          R5 --> A(PAYROLL TYPE TABLE)                 
         LA    R5,PYTABLE          PUT PAYTYPE INTO 1 OF 5 CATEGORIES           
         MVI   TYPE,PATTOTH        - 1. SALARY                                  
         ZIC   R0,PYTABLE#         - 2. BENEFIT                                 
         CLC   PYNUM,PDENUM        - 3. PENSION                                 
         BNE   *+14                - 4. INDIRECT                                
         MVC   TYPE,PYTYPE         - 5. OTHER                                   
         B     *+12                                                             
         LA    R5,PYLNQ(R5)                                                     
         BCT   R0,*-24                                                          
*                                                                               
*              ADD AMOUNT/ADJUSTMENT INTO CORRECT ACCUMULATOR                   
*                                                                               
         LA    RF,OFFSET                                                        
         CLC   TYPE,0(RF)                                                       
         BE    *+12                                                             
         LA    RF,L'OFFSET(RF)                                                  
         B     *-14                                                             
         ZIC   R1,1(RF)                                                         
         LA    RF,SAL$INFO(R1)                                                  
         AP    0(L'SAL$INFO,RF),PDEAMT                                          
         AP    0(L'SAL$INFO,RF),PDEADJ                                          
         B     HIST200                                                          
*                                                                               
HIST300  DS    0H                  ADD UP TOTAL AMOUNT                          
         AP    SALTOTAL,SALSALRY                                                
         AP    SALTOTAL,SALBENFT                                                
         AP    SALTOTAL,SALPENSN                                                
         AP    SALTOTAL,SALINDIR                                                
         AP    SALTOTAL,SALOTHER                                                
*                                                                               
EXIT     XMOD1                                                                  
         EJECT                                                                  
***********************************************************************         
*              LITERALS                                               *         
***********************************************************************         
*                                                                               
EFFS     DC    12X'FF'                                                          
DATADISP DC    H'49'                                                            
ACCFIL   DC    CL8'ACCFIL'                                                      
DMREAD   DC    CL8'DMREAD'                                                      
DMRDHI   DC    CL8'DMRDHI'                                                      
DMRSEQ   DC    CL8'DMRSEQ'                                                      
*                                                                               
         GETEL R4,DATADISP,ELCODE                                               
*                                                                               
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
*              ACCUMULATOR OFFSET TABLE                               *         
***********************************************************************         
*                                                                               
OFFSET   DS    0XL2                                                             
         DC    AL1(PATTSAL),AL1(SALSALRY-SAL$INFO)                              
         DC    AL1(PATTBEN),AL1(SALBENFT-SAL$INFO)                              
         DC    AL1(PATTPEN),AL1(SALPENSN-SAL$INFO)                              
         DC    AL1(PATTIND),AL1(SALINDIR-SAL$INFO)                              
         DC    AL1(PATTOTH),AL1(SALOTHER-SAL$INFO)                              
         EJECT                                                                  
***********************************************************************         
*              PAYROLL TYPE DSECT                                     *         
***********************************************************************         
*                                                                               
PYCODED  DSECT                                                                  
PYNUM    DS    CL1                 PAYROLL CODE NUMBER                          
PYTYPE   DS    CL1                 PAYROLL TYPE (SAL,BEN,PEN..ETC)              
PYLNQ    EQU   *-PYCODED                                                        
         EJECT                                                                  
***********************************************************************         
*              ACSALARY WORKING STORAGE                               *         
***********************************************************************         
*                                                                               
ACSALD   DSECT                                                                  
PARMS    DS    0F                  PARAMETER BLOCK                              
PARM1    DS    A                                                                
PARM2    DS    A                                                                
PARM3    DS    A                                                                
PARMQ    EQU   *-PARMS                                                          
*                                                                               
APERSON  DS    A                   A(PERSON RECORD)                             
AIO      DS    A                   A(2000 BYTE IO AREA)                         
DATAMGR  DS    A                   A(DATAMGR)                                   
ABLOCK   DS    A                   A(OUTPUT BLOCK)                              
*                                                                               
DMCB     DS    6F                                                               
HALF     DS    H                                                                
*                                                                               
SPACES   DS    CL132                                                            
ELCODE   DS    XL1                                                              
TYPE     DS    XL1                                                              
COMMAND  DS    CL8                                                              
SVKEY    DS    CL42                                                             
*                                                                               
PYTABLE# DS    XL1                 PAYROLL TYPE TABLE                           
PYTABLE  DS    (PYTABLEX*PYLNQ)C                                                
PYTABLEX EQU   100                 MAX # OF TABLE ENTRIES                       
*                                                                               
ACSALQ   EQU   *-ACSALD                                                         
         EJECT                                                                  
***********************************************************************         
*              ++INCLUDES                                             *         
***********************************************************************         
*                                                                               
       ++INCLUDE ACGETSALD                                                      
         PRINT OFF                                                              
       ++INCLUDE ACGENFILE                                                      
         PRINT ON                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'003ACGETSAL  05/01/02'                                      
         END                                                                    
