*          DATA SET DDPANACHEO AT LEVEL 024 AS OF 05/01/02                      
*CATALP PANACHEO                                                                
         TITLE 'PANACHEO - PAN#1 COBOL USER EXIT - MVS'                         
PANEXIT  CSECT                                                                  
*                                                                               
* AFTER A PAN#1 ADD/LEVEL/UPDATE SEQ THE FOLLOWING CONTROL CARDS WILL           
* CAUSE THE APPROPRIATE DATA TO BE CONDITIONALLY WRITTEN TO THE PAN             
* WORK AND PUNCH FILES.                                                         
*                                                                               
* &&COMPILE    COMPILE                                                          
* &&CATALP X.. COMPILE  & CATALOGUE OBJECT DECK TO PAN LIB (RM...)              
* &&CATALR X.. COMPILE  & CATALOGUE OBJECT DECK TO LOAD LIBRARY                 
* &&PHASE X... COMPILE  & LINK EDIT WITH &&LINKEDT STATEMENTS                   
*                                                                               
         REQUS                                                                  
         USING *,RB                                                             
         STM   RE,RC,12(RD)        SAVE PANS REGISTERS                          
         LR    RB,RF                                                            
         LA    R9,2048(RB)                                                      
         LA    R9,2048(R9)                                                      
         USING PANEXIT+4096,R9                                                  
         LR    RC,RD                                                            
         LA    RD,SAVEAREA                                                      
         ST    RC,4(RD)                                                         
         ST    RD,8(RC)                                                         
         SPACE 2                                                                
         ST    R1,APARM            R2=A(PAN RECORD)                             
         LM    R2,R3,0(R1)         R3=A(PAN IOCODES)                            
         CLI   FRSTTIME,C'Y'                                                    
         BNE   PANNEXT                                                          
         EJECT                                                                  
PANFRST  MVI   FRSTTIME,C'N'       RELOCATE ADCONS ON FIRST CALL                
         LA    R6,RELO                                                          
         S     R6,RELO                                                          
         ST    R6,RELO                                                          
         L     R5,=A(SETAREA)                                                   
         AR    R5,R6                                                            
         ST    R5,ASETAREA                                                      
         L     R5,=A(WRKAREA)                                                   
         AR    R5,R6                                                            
         ST    R5,AWRKAREA                                                      
         L     R5,=A(GENAREA)                                                   
         AR    R5,R6                                                            
         ST    R5,AGENAREA                                                      
         L     R5,=A(LNKAREA)                                                   
         AR    R5,R6                                                            
         ST    R5,ALNKAREA                                                      
*                                                                               
PANFRS1  MVC   PRCNAME,SPACES      GET PROCEDURE NAME FROM DDS DD STAT          
         LA    R5,DUB                                                           
         EXTRACT (5),FIELDS=TIOT                                                
         L     R5,DUB                                                           
         LA    R5,24(R5)                                                        
         SR    R6,R6                                                            
PANFRS1A CLI   0(R5),0             TEST FOR END OF TIOT TABLE                   
         BE    PANFRS2                                                          
         CLC   4(3,R5),=C'DDS'     TEST DDNAME=DDS.....                         
         BE    PANFRS1B                                                         
         IC    R6,0(R5)            GET LENGTH OF THIS ENTRY                     
         AR    R5,R6                                                            
         B     PANFRS1A                                                         
PANFRS1B MVC   PRCNAME,4(R5)       SAVE PROCEDURE NAME                          
         MVC   PRCNAME(3),=C'PAN'                                               
*                                                                               
PANFRS2  LA    R5,PRCTBL           SEARCH FOR PROCEDURE NAME IN TABLE           
         CLC   PRCNAME,2(R5)                                                    
         BE    PANFRS3                                                          
         LA    R5,L'PRCTBL(R5)                                                  
         CLI   0(R5),0                                                          
         BNE   PANFRS2+4                                                        
         LA    R5,ACTTBL           SET ALL ACTIONS VALID FOR UNKNOWN            
         OI    1(R5),X'80'                                                      
         LA    R5,L'ACTTBL(R5)                                                  
         CLI   0(R5),0                                                          
         BNE   *-12                                                             
         MVC   ACTDFLT,ACTTBL+3    SET DEFAULT ACTION TO FIRST                  
         B     PANFRS4                                                          
*                                                                               
PANFRS3  MVC   PRCNUM,0(R5)        R5=A(PROC DATA FOR THIS PROC)                
         MVC   PRCFLAG,1(R5)                                                    
         LA    R6,L'PRCTBL(R5)                                                  
         LA    R5,10(R5)           POINT TO FIRST ACTION NUM FOR PROC           
         SR    R6,R5                                                            
         LA    R0,L'ACTTBL                                                      
         STH   R0,DUB                                                           
         SR    RF,RF                                                            
PANFRS3A SR    RE,RE               GET NEXT ACTION NUMBER                       
         ICM   RE,1,0(R5)                                                       
         BZ    PANFRS4                                                          
         BCTR  RE,0                                                             
         MH    RE,DUB                                                           
         LA    RE,ACTTBL(RE)                                                    
         OI    1(RE),X'80'         SET ACTION VALID FOR THIS PROC               
         LTR   RF,RF                                                            
         BNZ   *+14                                                             
         LA    RF,1                                                             
         MVC   ACTDFLT,3(RE)       SET DEFAULT ACTION TO FIRST FOR PROC         
         LA    R5,1(R5)                                                         
         BCT   R6,PANFRS3A                                                      
*                                                                               
PANFRS4  DS    0H                                                               
         SPACE 2                                                                
PANNEXT  CLI   1(R3),C'1'                                                       
         BNE   RETURN              EXIT IF NOT PAN#1                            
         CLI   0(R3),C'1'                                                       
         BE    BRC                 PAN#1 BEFORE READ CARD                       
         CLI   0(R3),C'2'                                                       
         BE    ARC                 PAN#1 AFTER READ CARD                        
         CLI   0(R3),C'5'                                                       
         BE    BWW                 PAN#1 BEFORE WRITE WORK                      
         CLI   0(R3),C'6'                                                       
         BE    URR                 USER REQUESTED RETURN                        
         CLI   0(R3),C'7'                                                       
         BE    EOF                 PAN#1 EOF                                    
         SPACE 2                                                                
RETURN   EQU   *                                                                
         L     RD,4(RD)                                                         
         LM    RE,RC,12(RD)                                                     
         BR    RE                                                               
         EJECT                                                                  
*                                                                               
* PAN#1 BEFORE A WRITE TO WORK FILE                                             
*                                                                               
BWW      TM    WFLAG,X'01'         EXIT IF NOT WRITING SOURCE BOOK              
         BZ    RETURN                                                           
         SPACE 2                                                                
         CLI   6(R2),C'O'           O/S STMT                                    
         BNE   BWW0                NO                                           
         MVI   6(R2),C' '          YES, USE IT                                  
         B     BWW0X                                                            
BWW0     CLI   6(R2),C'D'          DOS                                          
         BNE   BWW0X                NO                                          
         MVI   6(R2),C'*'          YES, COMMENT IT OUT                          
         B     BWW0X                                                            
         CLC   6(3,R2),=C'*&&&&'   PROCESS *&& CARDS                            
         BNE   BWW0X                                                            
         CLI   14(R2),C' '                                                      
         BNE   BWW0B                                                            
         CLC   11(3,R2),=C'*&&&&'                                               
         BE    BWW0I                                                            
         CLI   20(R2),C' '                                                      
         BNE   BWW0B                                                            
*                                                                               
BWW0A    CLC   9(5,R2),SPACES      UNNAMED *&& CARD                             
         BNE   BWW0E                                                            
         CLC   15(5,R2),=C'SET  '   CHECK FOR SET CARD                          
         BE    BWW0C                                                            
         CLC   15(5,R2),SPACES      CHECK FOR END DELETE MODE                   
         BE    *+14                                                             
         CLC   15(5,R2),=C'END  '                                               
         BNE   BWW0B                                                            
*                                                                               
BWW0A1   LH    RE,INCLV            DECR LEVEL AT END OF INCLUDE                 
         SH    RE,=H'1'                                                         
         BM    BWW0B               ERROR IF TOO LOW                             
         STH   RE,INCLV                                                         
BWW0A2   MVI   INCSW,C'Y'          RESET INCLUDE SWITCH                         
         EX    RE,*+8                                                           
         B     *+10                                                             
         CLC   INCTBL(0),=C'YYYYY' ALL LEVELS MUST BE Y IF INCSW=Y              
         BE    *+8                                                              
         MVI   INCSW,C'N'                                                       
         B     RETURN                                                           
*                                                                               
BWW0B    XC    INCLV,INCLV         RESET LEVEL IF INVALID *&& CARD              
         MVI   INCSW,C'Y'                                                       
BWW0B1   MVI   6(R2),C' '          FORCE ASSEMBLY SYNTAX ERROR                  
         B     RETURN                                                           
*                                                                               
BWW0C    CLI   INCSW,C'Y'          DONT PROCESS SET IF IN DELETE MODE           
         BNE   RETURN                                                           
         MVC   SETDATAC,SPACES                                                  
         MVC   SETDATAC(56),15(R2)                                              
BWW0D    BAS   RA,SETDATA                                                       
         CLI   SETDATAC,0          CHECK FOR ERRORS                             
         BE    BWW0B1                                                           
         B     RETURN                                                           
*                                                                               
BWW0E    CLC   15(5,R2),SPACES      NAMED *&&CARD                               
         BE    BWW0F                                                            
         CLC   15(5,R2),=C'START'   ALLOW NAMED START                           
         BE    BWW0F                                                            
         CLC   15(5,R2),=C'END  '   ALLOW NAMED END                             
         BE    BWW0A1                                                           
         CLC   15(5,R2),=C'SET  '                                               
         BNE   BWW0B                                                            
*                                                                               
         CLI   21(R2),C'N'         ALLOW NAMED SET                              
         BE    *+12                                                             
         CLI   21(R2),C'Y'                                                      
         BNE   BWW0B1                                                           
         CLI   32(R2),C' '                                                      
         BNE   BWW0B1                                                           
         CLI   INCSW,C'Y'          DONT PROCESS SET IF IN DELETE MODE           
         BNE   RETURN                                                           
         MVC   SETDATAC,SPACES     CONVERT SHORT TO FULL SYNTAX                 
         MVC   SETDATAC(5),3(R2)                                                
         LA    R7,SETDATAC                                                      
         CLI   0(R7),C' '                                                       
         BE    *+12                                                             
         LA    R7,1(R7)                                                         
         B     *-12                                                             
         MVI   0(R7),C'='                                                       
         MVC   1(1,R7),15(R2)                                                   
         B     BWW0D                                                            
*                                                                               
BWW0F    L     R7,ASETAREA         START OF NEW INCLUDE LEVEL                   
         CLI   0(R7),C' '                                                       
         BE    BWW0G                                                            
         CLI   0(R7),0                                                          
         BE    BWW0G                                                            
         CLC   0(5,R7),3(R2)                                                    
         BE    BWW0G+4             USE TABLE VALUE IF FOUND                     
         LA    R7,6(R7)                                                         
         B     BWW0F+4                                                          
BWW0G    L     R7,ASETAREA         USE ALL VALUE IF NOT FOUND                   
         LH    RE,INCLV                                                         
         LA    RE,1(RE)                                                         
         STH   RE,INCLV            BUMP INCLUDE LEVEL                           
         LA    RE,INCTBL(RE)                                                    
         CLI   0(RE),X'FF'                                                      
         BE    BWW0B               ERROR IF TOO HIGH                            
         MVC   0(1,RE),5(R7)                                                    
         LH    RE,INCLV                                                         
         B     BWW0A2              GO SET INCSW FROM INCTBL VALUES              
*                                                                               
BWW0I    CLI   INCSW,C'Y'          PROCESS SINGLE *&&XX*&& CARD                 
         BNE   RETURN                                                           
         CLI   3(R2),C' '                                                       
         BE    BWW0B1                                                           
         L     R7,ASETAREA                                                      
BWW0J    CLI   0(R7),C' '          SEARCH SET TABLE FOR TWO CHR ID              
         BE    BWW0L                                                            
         CLI   0(R7),0                                                          
         BE    BWW0L                                                            
         CLC   0(2,R7),3(R2)                                                    
         BE    *+12                                                             
         LA    R7,6(R7)                                                         
         B     BWW0J                                                            
         CLC   2(3,R7),SPACES                                                   
         BNE   *-14                                                             
BWW0K    CLI   5(R7),C'Y'          IS IT TO BE INCLUDED                         
         BNE   RETURN              NO- LEAVE AS COMMENT                         
         MVC   DUB,0(R2)           YES-REMOVE *&&XX*&&                          
         MVC   0(8,R2),SPACES      AND PUT XX IN COLS 70-71 IF AVAIL            
         CLC   68(4,R2),SPACES                                                  
         BNE   *+10                                                             
         MVC   69(2,R2),DUB+3                                                   
         B     RETURN                                                           
BWW0L    L     R7,ASETAREA         IF NOT FOUND LOOK AT ALL VALUE               
         B     BWW0K                                                            
*                                                                               
BWW0X    CLI   INCSW,C'Y'          ARE WE INCLUDING CARDS                       
         BE    BWW1                YES                                          
         MVI   6(R2),C'*'          NO- SET TO COMMENT CARD                      
         MVI   71(R2),C' '                                                      
         SPACE 2                                                                
BWW1     TM    WFLAG,X'02'         TEST IF ASSEMBLER END REQUIRED               
         BZ    BWW6                                                             
         CLI   6(R2),C'*'                                                       
         BE    BWW6                                                             
         B     BWW6                                                             
         SPACE 2                                                                
BWW4     BAS   RA,CLEARWRK         REPLACE END BY CSECT/DC/END                  
         MVC   9(11,R5),=C'DC    X''00'''                                       
         LA    R5,80(R5)                                                        
         MVC   0(8,R5),SAVCSECT                                                 
         MVC   8(7,R5),=C' CSECT '                                              
         LA    R5,80(R5)                                                        
         MVC   9(32,R5),=C'DC    C''LEVEL=NNN DATE=NN/NN/NN'''                  
         MVC   23(3,R5),SAVLEVEL                                                
         MVC   32(8,R5),SAVDATE                                                 
         LA    R5,80(R5)                                                        
         MVC   0(80,R5),0(R2)                                                   
         LA    R5,80(R5)                                                        
         MVI   0(R5),0                                                          
         B     URR                                                              
         SPACE 2                                                                
BWW6     TM    WFLAG,X'04'         PHASE SOURCE WRITE                           
         BZ    BWWE                                                             
         TM    WFLAG,X'40'         HAS FIRST *PHASE CARD BEEN FOUND             
         BO    BWWA                YES                                          
         TM    WFLAG,X'80'         TEST INITITIALIZE SEARCH                     
         BO    BWW7                                                             
         OI    WFLAG,X'80'                                                      
         BAS   RA,CLEARWRK                                                      
         XC    WCOUNT,WCOUNT                                                    
         XC    SAVLEVEL,SAVLEVEL                                                
         SPACE 2                                                                
BWW7     L     R5,WRKADDR          LOOK FOR EMBEDDED PHASE CARD IN BOOK         
         CLI   0(R2),C'*'                                                       
         BNE   BWW70                                                            
         CLI   INCSW,C'Y'          IGNORE CARDS DELETED BY *&& ACTION           
         BNE   BWW8                                                             
BWW70    OC    SAVLEVEL,SAVLEVEL   SAVE FIRST LEVEL NUMBER                      
         BNZ   BWW7A                                                            
         CLC   75(2,R2),=C'LV'                                                  
         BNE   BWW7A                                                            
         MVC   SAVLEVEL,77(R2)                                                  
         CLI   0(R2),C'*'                                                       
         BNE   BWW8                                                             
BWW7A    CLC   1(6,R2),=CL8'PHASE'                                              
         BE    *+14                                                             
         CLC   1(5,R2),=CL8'NAME'                                               
         BNE   BWW8                                                             
         LA    R7,LNKCARD          IF FOUND POINT TO INPUT PHASE CARD           
         CLC   6(66,R7),SPACES     DOES INPUT PHASE HAVE A NAME                 
         BNE   *+10                YES USE IT                                   
         MVC   1(71,R7),1(R2)      NO USE *PHASE CARD                           
         LA    R7,1000                                                          
         STH   R7,WCOUNT           FORCE END OF SEARCH                          
         SPACE 2                                                                
BWW8     LH    R7,WCOUNT           BUMP WRITE TO WORK COUNT                     
         LA    R7,1(R7)                                                         
         STH   R7,WCOUNT                                                        
         CLC   BOOKNAME(2),=C'LK'  DONT OUTPUT ANY LK.. BOOK CARDS              
         BE    *+14                                                             
         CLC   BOOKNAME(2),=C'LM'                                               
         BNE   *+8                                                              
BWW8A    MVI   0(R3),C'F'          DELETE WORK CARD                             
         CH    R7,=H'100'          HAVE MAX WORK CARDS BEEN EXAMINED            
         BL    BWWX                NO CONTINUE                                  
         SPACE 2                                                                
BWW9     MVI   0(R5),0             SET END OF *PHASE SEARCH                     
         OI    WFLAG,X'40'                                                      
BWW9A    LA    R7,LNKCARD+5        FIND START OF PHASE CARD                     
         CLI   0(R7),C' '                                                       
         BE    *+8                                                              
         LA    R7,1(R7)                                                         
         LA    R0,20                                                            
         CLI   0(R7),C' '                                                       
         BNE   *+16                                                             
         LA    R7,1(R7)                                                         
         BCT   R0,*-12                                                          
         B     BWW9C                                                            
         LR    RE,R7               R7=A(START OF PHASE NAME)                    
         LA    R0,9                                                             
BWW9B    CLI   0(RE),C','                                                       
         BE    BWW9D                                                            
         CLI   0(RE),C' '                                                       
         BE    BWW9D                                                            
         CLI   0(RE),C'('                                                       
         BE    BWW9D                                                            
         LA    RE,1(RE)                                                         
         BCT   R0,BWW9B                                                         
BWW9C    MVC   LNKCARD,SPACES                                                   
         MVC   LNKCARD+1(13),=C'PHASE DUMMY,*'                                  
         B     BWW9A                                                            
BWW9D    LR    RF,RE                                                            
         SR    RF,R7               RF=L'PHASE NAME                              
         CLI   AMPLVL,C' '                                                      
         BE    BWW9E                                                            
         CH    RF,=H'8'                                                         
         BNL   BWW9C               WILL MAKE PHASE NAME INVALID                 
         MVC   WORK(60),0(RE)                                                   
         MVC   0(1,RE),AMPLVL      APPEND PHASE LEVEL CHARACTER                 
         MVC   1(60,RE),WORK                                                    
         LA    RF,1(RF)                                                         
BWW9E    STH   RF,AMPLEN                                                        
BWW9F    CLI   AMPLIB,C'C'         SET PHASE CARD                               
         BNE   BWW9X                                                            
         L     R7,AWRKAREA                                                      
         LA    R7,80(R7)                                                        
         MVC   0(80,R7),LNKCARD                                                 
BWW9X    CLC   BOOKNAME(2),=C'LK'  DONT OUTPUT ANY LK.. BOOK CARDS              
         BE    *+14                                                             
         CLC   BOOKNAME(2),=C'LM'                                               
         BNE   *+8                                                              
         MVI   0(R3),C'F'                                                       
         B     BWWX                                                             
         SPACE 2                                                                
BWWA     CLI   0(R2),C'*'          LOOK FOR EMBEDDED LNKEDT CARDS               
         BNE   BWWX                                                             
         CLI   INCSW,C'Y'          IGNORE CARDS DELETED BY *&& ACTION           
         BNE   BWWX                                                             
         LA    R7,1(R2)                                                         
         CLC   0(6,R7),=CL8'PHASE'                                              
         BE    BWWC                                                             
         CLC   0(5,R7),=CL8'NAME'                                               
         BE    BWWC                                                             
         CLC   0(8,R7),=CL8'INCLUDR'                                            
         BE    BWWC                                                             
         CLC   0(8,R7),=CL8'INCLUDP'                                            
         BE    BWWC                                                             
         CLC   0(8,R7),=CL8'INCLUDL'                                            
         BE    BWWC                                                             
         SR    RE,RE                                                            
         LA    RF,LNKTBL                                                        
BWWB     IC    RE,0(RF)                                                         
         LTR   RE,RE                                                            
         BZ    BWWD                                                             
         EX    RE,*+8                                                           
         B     *+10                                                             
         CLC   0(0,R7),2(RF)                                                    
         BE    BWWC                                                             
         LA    RF,10(RF)                                                        
         B     BWWB                                                             
         SPACE 2                                                                
BWWC     L     R6,LNKADDR          SAVE EMBEDDED LINK STATEMENT                 
         MVI   0(R6),C'*'          SET EMBEDDED CODE                            
         MVC   1(71,R6),0(R7)                                                   
         LA    R6,80(R6)                                                        
         ST    R6,LNKADDR                                                       
         SPACE 2                                                                
BWWD     CLC   BOOKNAME(2),=C'LK'  DONT WRITE ANY LK.. RECS TO WORK             
         BE    *+14                                                             
         CLC   BOOKNAME(2),=C'LM'                                               
         BNE   *+8                                                              
         MVI   0(R3),C'F'                                                       
         B     BWWX                                                             
         SPACE 2                                                                
BWWE     TM    WFLAG,X'08'         CATAL SOURCE WRITE                           
         BZ    BWWX                                                             
         TM    WFLAG,X'40'                                                      
         BO    BWWX                EXIT IF HAS BEEN COMPLETED                   
         TM    WFLAG,X'80'                                                      
         BO    BWWF                                                             
         OI    WFLAG,X'80'         SET INITIALISED FLAG                         
         BAS   RA,CLEARWRK                                                      
         XC    WCOUNT,WCOUNT                                                    
         XC    SAVLEVEL,SAVLEVEL                                                
         SPACE 2                                                                
BWWF     CLI   0(R2),C'*'          LOOK FOR EMBEDDED CATAL CARD IN BOOK         
         BNE   BWWF1                                                            
         CLI   INCSW,C'Y'          IGNORE CARDS DELETED BY *&& ACTION           
         BNE   BWWI                                                             
BWWF1    OC    SAVLEVEL,SAVLEVEL                                                
         BNZ   BWWG                                                             
         CLC   75(2,R2),=C'LV'                                                  
         BNE   BWWG                                                             
         MVC   SAVLEVEL,77(R2)     SAVE FIRST LEVEL NUMBER                      
         CLI   0(R2),C'*'                                                       
         BNE   BWWH                                                             
         SPACE 2                                                                
BWWG     CLC   1(5,R2),=CL8'CATALR'                                             
         BNE   BWWI                                                             
         LA    R7,6(R2)                                                         
         CLI   0(R7),C' '                                                       
         BE    *+8                                                              
         LA    R7,1(R7)                                                         
         CLC   1(5,R7),=C'MACRO'                                                
         BNE   *+12                                                             
         OI    WFLAG,X'20'         SET CATAL MACRO FOUND                        
         B     BWWI                                                             
         LA    R7,LNKCARD          POINT TO INPUT CATAL CARD                    
         CLC   7(65,R7),SPACES     DOES INPUT CATAL HAVE A NAME                 
         BNE   *+10                YES USE IT                                   
         MVC   1(71,R7),1(R2)      NO USE *CATAL CARD                           
         LA    R7,1000                                                          
         STH   R7,WCOUNT           FORCE END OF SEARCH                          
         B     BWWI                                                             
         SPACE 2                                                                
BWWH     TM    WFLAG,X'20'         LOOK FOR PUNCH STATEMENT                     
         BZ    BWWI                                                             
         CLC   0(9,R2),SPACES                                                   
         BNE   BWWI                                                             
         CLC   9(6,R2),=CL8'PUNCH'                                              
         BNE   BWWI                                                             
         MVI   0(R2),C'*'          CONVERT PUNCH TO COMMENT                     
         LA    R7,LNKCARD                                                       
         CLC   7(65,R7),SPACES     DOES INPUT CATAL HAVE A NAME                 
         BNE   BWWH1               YES USE IT                                   
         MVC   LNKCARD,SPACES                                                   
         MVC   LNKCARD+1(5),=CL8'PUNCH'                                         
         LA    RE,16(R2)                                                        
         LA    R0,54                                                            
         CLI   0(RE),C''''                                                      
         BE    *+12                                                             
         LA    RE,1(RE)                                                         
         BCT   R0,*-12                                                          
         LA    RF,16(R2)                                                        
         SR    RE,RF                                                            
         BZ    BWWH1                                                            
         BCTR  RE,0                                                             
         EX    RE,*+8                                                           
         B     *+10                                                             
         MVC   LNKCARD+7(0),16(R2) MOVE PUNCH CARD TEXT TO LNKCARD              
BWWH1    LA    R7,1000                                                          
         STH   R7,WCOUNT           FORCE END OF SEARCH                          
         B     BWWI                                                             
         SPACE 2                                                                
BWWI     LH    R7,WCOUNT           BUMP WRITE TO WORK COUNT                     
         LA    R7,1(R7)                                                         
         STH   R7,WCOUNT                                                        
         CH    R7,=H'100'                                                       
         BL    BWWX                                                             
         SPACE 2                                                                
BWWJ     OI    WFLAG,X'40'         SET END OF *CATAL SEARCH                     
         B     BWWX                                                             
         SPACE 2                                                                
BWWX     B     RETURN                                                           
         EJECT                                                                  
*                                                                               
* PAN#1 USER REQUESTED RETURN                                                   
*                                                                               
URR      L     R5,WRKADDR                                                       
         MVC   0(80,R2),0(R5)      SET WORK FILE DATA                           
         MVI   0(R3),C'E'          SET WRITE TO WORK IOCODE                     
         LA    R5,80(R5)                                                        
         ST    R5,WRKADDR                                                       
         CLI   0(R5),0             LAST WORK FILE DATA                          
         BE    *+8                 YES                                          
         MVI   2(R3),C'Y'          NO REQUEST USER RETURN                       
         B     RETURN                                                           
         EJECT                                                                  
*                                                                               
* PAN#1 BEFORE A READ OF NEXT INPUT CARD                                        
*                                                                               
BRC      CLI   EOFFLAG,0           TEST IF PAN#1 AT END OF FILE                 
         BNE   *+12                                                             
         CLI   WHATIDID,0                                                       
         BE    RETURN              PAN#1 PROVIDED LAST INPUT CARD               
         L     R5,GENADDR                                                       
         CLI   0(R5),2                                                          
         BH    BRC2                                                             
         BE    BRC1                                                             
         CLI   EOFFLAG,0                                                        
         BNE   EOF3                                                             
         MVI   WHATIDID,0          END OF NORMAL GENERATION                     
         B     RETURN                                                           
BRC1     MVI   0(R3),C'G'          TELL PAN#1 EOF                               
         B     RETURN                                                           
BRC2     MVC   0(80,R2),0(R5)      GIVE PAN#1 NEXT INPUT CARD                   
         MVI   0(R3),C'A'                                                       
         LA    R5,80(R5)           BUMP TO NEXT GENERATED CARD                  
         ST    R5,GENADDR                                                       
         CLI   0(R5),1             WAS PREVIOUS GENERATED CARD GENUINE          
         BNE   *+12                                                             
         MVI   WHATIDID,0          NO SET PAN#1 PROVIDED LAST CARD              
         B     ARC                                                              
         MVI   WHATIDID,2          YES SET I PROVIDED LAST CARD                 
         CLC   72(8,R2),SPACES                                                  
         BNE   RETURN                                                           
         MVC   72(8,R2),AMACTN     SET ACTION IN COLS 73-80                     
         B     RETURN                                                           
         EJECT                                                                  
*                                                                               
* PAN#1 AFTER A READ OF INPUT CARD                                              
*                                                                               
ARC      EQU   *                                                                
         CLI   WHATIDID,2          DID I GIVE PAN THIS CARD                     
         BE    RETURN              YES NO NEED TO PROCESS IT                    
         CLC   0(2,R2),PANAM                                                    
         BNE   ARC0                                                             
         MVC   AC,0(R2)            SAVE &&CARD IN AC                            
         OI    ACFLAG,X'02'        SET INPUT FLAG                               
         B     ARCAM                                                            
ARC0     CLC   LASTCARD(2),PANAM                                                
         BE    EOF+4               LAST CARD WAS LAST && IN SEQUENCE            
         SPACE 2                                                                
ARC1     CLC   0(2,R2),=C'++'                                                   
         BE    *+14                                                             
         CLC   0(2,R2),=C'--'                                                   
         BNE   ARCX                                                             
         CLC   2(L'SOAM,R2),SOAM                                                
         BNE   *+18                                                             
         MVC   AC,0(R2)            SPECIAL STAND ALONE CARD                     
         OI    ACFLAG,X'02'                                                     
         B     ARCAM5                                                           
         LA    R5,PANTBL           THIS CARD IS PAN#1 ACTION                    
         SR    R6,R6                                                            
         SPACE 2                                                                
ARC2     IC    R6,0(R5)            SEARCH PAN ACTION TABLE                      
         SH    R6,=H'1'                                                         
         BM    ARCX                                                             
         EX    R6,ARC3                                                          
         BE    ARC4                IF PRESENT EXTRACT DATA SET NAME             
         LA    R5,10(R5)                                                        
         B     ARC2                                                             
ARC3     CLC   2(0,R2),1(R5)                                                    
         SPACE 2                                                                
ARC4     MVC   BOOKNAME,SPACES     INITIALISE FOR NEW &&SEQUENCE                
         MVC   PANACTN,SPACES                                                   
         MVC   AMACTN,SPACES                                                    
         MVC   SAVCSECT,SPACES                                                  
         MVI   AMERR,C'N'                                                       
         MVI   WFLAG,0                                                          
         MVI   INCSW,C'Y'                                                       
         XC    INCLV,INCLV                                                      
         MVI   SETSW,C'N'                                                       
         MVC   AA,SPACES                                                        
         MVC   AC,SPACES                                                        
         MVI   ACFLAG,0                                                         
         CLI   9(R5),C'Y'          CAN &&S FOLLOW THIS ACTION                   
         BNE   ARCX                NO EXIT                                      
         MVC   PANACTN,2(R2)       YES SAVE ACTION & BOOK NAME                  
         LA    R5,3(R6,R2)                                                      
         LA    R0,11                                                            
         CLI   0(R5),C' '                                                       
         BNE   *+16                                                             
         LA    R5,1(R5)                                                         
         BCT   R0,*-12                                                          
         B     ARCX                                                             
         LR    R6,R5                                                            
         LA    R0,11                                                            
         CLI   0(R5),C','                                                       
         BE    *+16                                                             
         LA    R5,1(R5)                                                         
         BCT   R0,*-12                                                          
         B     ARCX                                                             
         SR    R5,R6                                                            
         BZ    ARCX                                                             
         BCTR  R5,R0                                                            
         EX    R5,*+8                                                           
         B     ARC5                                                             
         MVC   BOOKNAME(0),0(R6)                                                
         SPACE 2                                                                
ARC5     LA    R5,1(R5,R6)         SEARCH FOR COMMENT ON PAN ACTION             
         LA    R0,72(R2)                                                        
         SR    R0,R5                                                            
         CLI   0(R5),C' '                                                       
         BE    *+16                                                             
         LA    R5,1(R5)                                                         
         BCT   R0,*-12                                                          
         B     ARCX                                                             
         CLI   0(R5),C' '                                                       
         BNE   *+16                                                             
         LA    R5,1(R5)                                                         
         BCT   R0,*-12                                                          
         B     ARCX                                                             
         CLC   0(2,R5),PANAM       CHECK IF COMMENT IS &&DATA                   
         BNE   ARCX                                                             
         LR    R6,R5                                                            
         LR    R5,R0                                                            
         CH    R5,=H'6'                                                         
         BL    ARCX                                                             
         BCTR  R5,0                                                             
         EX    R5,*+8                                                           
         B     ARCX                                                             
         MVC   AA(0),0(R6)         SAVE ACTION &&DATA IN AA                     
         SPACE 2                                                                
ARCX     MVC   LASTCARD,0(R2)      SAVE LAST PAN#1 PROVIDED INPUT CARD          
         B     RETURN                                                           
         EJECT                                                                  
*                                                                               
* PAN#1 AFTER A READ OF AN && CARD                                              
*                                                                               
ARCAM    EQU   *                   INPUT IS AN &&CARD                           
         CLI   AMACTN,C' '                                                      
         BE    *+16                                                             
         CLI   AMERR,C'Y'          NOT FIRST OF SEQUENCE                        
         BE    ARCAMERR            DELETE IF PREVIOUS && CARD BAD               
         B     ARCAM8                                                           
         CLI   BOOKNAME,C' '       FIRST OF SEQUENCE                            
         BE    ARCAMERR            ERROR IF NO BOOKNAME                         
         MVC   AMACTN,SPACES                                                    
         SPACE 2                                                                
         CLC   AC+2(4),=C'SET  '   CHECK FOR &&SET CARD BEFORE ACTION           
         BNE   ARCAM0                                                           
         CLI   AC+6,C' '                                                        
         BE    ARCAMERR                                                         
         MVC   SETDATAC,SPACES                                                  
         MVC   SETDATAC(74),AC+6                                                
         BAS   RA,SETDATA                                                       
         CLI   SETDATAC,0                                                       
         BE    ARCAMERR            IGNORE AND DELETE IF ERROR                   
         MVI   WHATIDID,0          DELETE &&SET CARD                            
         MVI   0(R3),C'B'                                                       
         B     ARCX                                                             
         SPACE 2                                                                
ARCAM0   LA    R5,ACTTBL                                                        
         SR    R6,R6                                                            
ARCAM1   CLI   0(R5),0             SEARCH &&ACTION TABLE                        
         BE    ARCAMERR            ERROR IF NOT PRESENT                         
         TM    1(R5),X'80'                                                      
         BZ    ARCAM1A             IGNORE IF NOT AVAIL ACTION                   
         IC    R6,2(R5)                                                         
         LA    RE,AC+2(R6)                                                      
         CLI   0(RE),C' '          MUST BE SPACE AFTER ACTION                   
         BNE   ARCAM1A                                                          
         BCTR  R6,0                                                             
         EX    R6,ARCAM1B                                                       
         BE    ARCAM1C                                                          
ARCAM1A  LA    R5,L'ACTTBL(R5)                                                  
         B     ARCAM1                                                           
ARCAM1B  CLC   AC+2(0),3(R5)                                                    
ARCAM1C  MVC   ACTNUM,0(R5)        SAVE ACTION DATA                             
         MVC   ACTFLAG,1(R5)                                                    
         TM    PRCFLAG,X'80'       TEST IF LK.... ONLY PROC                     
         BZ    ARCAM3                                                           
         CLC   BOOKNAME(2),=C'LK'                                               
         BE    *+14                                                             
         CLC   BOOKNAME(2),=C'LM'                                               
         BNE   ARCAMERR                                                         
         SPACE 2                                                                
ARCAM3   LH    R5,AMPCOUNT         BUMP COUNT OF && CARDS                       
         LA    R5,1(R5)                                                         
         STH   R5,AMPCOUNT                                                      
         CH    R5,=H'1'                                                         
         BH    ARCAMERR            MAX OF ONE PER EXEC                          
         EX    R6,*+8                                                           
         B     *+10                                                             
         MVC   AMACTN(0),AC+2      SAVE ACTION ON 1ST && CARD                   
         SPACE 2                                                                
         MVC   SETDATAC,SPACES     SEARCH FOR SET DATA ON 1ST &&CARD            
         LA    R5,AC                                                            
         LA    R6,70                                                            
         CLC   0(7,R5),=C' &&&&SET '                                            
         BE    *+16                                                             
         LA    R5,1(R5)                                                         
         BCT   R6,*-14                                                          
         B     ARCAM4                                                           
         LA    R5,7(R5)                                                         
         CLI   0(R5),C' '                                                       
         BE    ARCAMERR                                                         
         LA    R6,AC+79                                                         
         SR    R6,R5                                                            
         EX    R6,*+8                                                           
         B     *+10                                                             
         MVC   SETDATAC(0),0(R5)   MOVE TO SET DATA CARD                        
         EX    R6,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R5),SPACES      CLEAR SET DATA ON 1ST &&CARD                 
         SH    R5,=H'7'                                                         
         MVC   0(7,R5),SPACES                                                   
         SPACE 2                                                                
ARCAM4   BAS   RA,SETDATA          INITIALISE SET DATA AREA                     
         CLI   SETDATAC,0                                                       
         BE    ARCAMERR                                                         
         BAS   RA,CLEARGEN         INITIALISE GENERATE AREA                     
         MVC   MSG3+13(10),BOOKNAME                                             
         MVC   MSG7+14(10),BOOKNAME                                             
         CLC   AMACTN,=CL8'COMPILE'                                             
         BE    ARCAMAS                                                          
         CLC   AMACTN,=CL8'ASM'                                                 
         BE    ARCAMAS                                                          
         CLC   AMACTN(5),=CL8'CATALR'                                           
         BE    ARCAMCA                                                          
         CLC   AMACTN,=CL8'PHASE'                                               
         BE    ARCAMPH                                                          
         CLC   AMACTN(4),=C'LINK'                                               
         BE    ARCAMPH                                                          
         DC    H'0'                                                             
         SPACE 2                                                                
ARCAM5   MVC   BOOKNAME,SPACES     INITIALISE FOR STAND ALONE                   
         MVC   PANACTN,SOAM                                                     
         MVC   AMACTN,SPACES                                                    
         MVI   AMERR,C'N'                                                       
         MVI   WFLAG,0                                                          
         MVI   INCSW,C'Y'                                                       
         XC    INCLV,INCLV                                                      
         MVI   SETSW,C'N'                                                       
         MVC   AA,SPACES                                                        
*                                                                               
         LA    R5,AC+2+L'SOAM      SCAN FOR BOOKNAME                            
         CLI   0(R5),C' '                                                       
         BNE   ARCAMERR            MUST BE ADD LEAST ONE BLANK                  
         LA    R5,1(R5)                                                         
         LA    R0,20                                                            
         CLI   0(R5),C' '                                                       
         BNE   *+16                                                             
         LA    R5,1(R5)                                                         
         BCT   R0,*-12                                                          
         B     ARCAMERR                                                         
*                                                                               
         LR    R6,R5               R5=A(START OF BOOKNAME)                      
         LA    R0,11                                                            
         SR    RE,RE               SET BOOKNAME DELIMITED BY SPACE              
ARCAM5A  CLI   0(R6),C','                                                       
         BE    ARCAM5B                                                          
         CLI   0(R6),C' '                                                       
         BE    ARCAM5B+4                                                        
         LA    R6,1(R6)                                                         
         BCT   R0,ARCAM5A                                                       
         B     ARCAMERR            ERROR BOOKNAME TOO LONG                      
*                                                                               
ARCAM5B  LA    RE,1                SET BOOKNAME DELIMITED BY COMMA              
         SR    R6,R5               R6=L'BOOKNAME                                
         BZ    ARCAMERR                                                         
         BCTR  R6,0                                                             
         EX    R6,*+8                                                           
         B     *+10                                                             
         MVC   BOOKNAME(0),0(R5)   SAVE BOOKNAME                                
*                                                                               
         LA    R6,2(R5,R6)         R6=A(FIRST ACTION CHR)                       
         MVC   WORK,0(R6)          SAVE ACTION DATA                             
         MVC   AC(2),PANAM                                                      
         MVC   AC+2(78),SPACES                                                  
         LA    RF,AC+2                                                          
         LTR   RE,RE               IF COMMA THEN ACTION FOLLOWS IT              
         BNZ   ARCAM5C                                                          
         MVC   AC+2(8),ACTDFLT     IF SPACE THEN SUPPLY DEFAULT ACTION          
         LA    RF,AC+10                                                         
         CLI   0(RF),C' '          SET RF TO POINT TO END OF ACTION             
         BNE   *+10                                                             
         BCTR  RF,0                                                             
         B     *-10                                                             
         LA    RF,2(RF)                                                         
ARCAM5C  LA    R5,AC+79                                                         
         SR    R5,R6                                                            
         EX    R5,*+8                                                           
         B     *+10                                                             
         MVC   0(0,RF),WORK                                                     
ARCAM5D  MVC   0(80,R2),AC                                                      
         B     ARCAM               REPROCESS AS &&CARD                          
         SPACE 2                                                                
ARCAM8   LA    R5,LNKTBL           SEARCH &&LNKEDT TABLE                        
         SR    R6,R6                                                            
ARCAM9   IC    R6,0(R5)                                                         
         SH    R6,=H'1'                                                         
         BM    ARCAMERR            SET ERROR & DELETE IF NOT THERE              
         EX    R6,*+16                                                          
         BE    ARCAMA                                                           
         LA    R5,10(R5)                                                        
         B     ARCAM9                                                           
         CLC   2(0,R2),2(R5)                                                    
         SPACE 2                                                                
ARCAMA   CLC   AMACTN,=CL8'PHASE'                                               
         BE    *+14                                                             
         CLC   AMACTN(4),=C'LINK'                                               
         BNE   ARCAMERR                                                         
         CLC   AC+2(7),=CL8'INCLUDE'                                            
         BNE   ARCAMA4                                                          
         LA    R6,AC+10                                                         
         LA    R7,9                                                             
ARCAMA0  CLI   0(R6),C' '          FIND END OF OBJECT NAME                      
         BE    ARCAMA1                                                          
         CLI   0(R6),C','                                                       
         BE    ARCAMA1                                                          
         LA    R6,1(R6)                                                         
         BCT   R7,ARCAMA0                                                       
         B     ARCAMERR            ERROR LONGER THAN 8 CHRS                     
ARCAMA1  LR    R7,R6                                                            
         LA    R0,AC+10                                                         
         SR    R7,R0                                                            
         BZ    ARCAMERR                                                         
         BCTR  R7,0                R7=L'OBJECT NAME1                            
         L     R6,ALNKAREA                                                      
         B     *+8                                                              
ARCAMA2  LA    R6,80(R6)           SEARCH FOR EMBEDDED OBJECT NAME              
         CLI   0(R6),C' '                                                       
         BNE   *+12                                                             
         MVI   AC+8,C'R'                                                        
         B     ARCAMA4             END OF TABLE                                 
         CLI   0(R6),C'*'                                                       
         BNE   ARCAMA2                                                          
         CLC   1(7,R6),=CL8'INCLUDE'                                            
         BNE   ARCAMA2                                                          
         EX    R7,*+8                                                           
         B     *+10                                                             
         CLC   AC+10(0),9(R6)                                                   
         BNE   ARCAMA2                                                          
ARCAMA3  MVI   7(R6),C'R'          IF FOUND CHANGE INCLUDE TO INCLUDR           
         B     ARCAMA5                                                          
ARCAMA4  L     R6,LNKADDR          SAVE INPUT LINK STATEMENT                    
         MVC   0(71,R6),AC+1       SET INPUT CODE AND SAVE INPUT CARD           
         LA    R6,80(R6)                                                        
         ST    R6,LNKADDR                                                       
ARCAMA5  MVI   WHATIDID,0          DELETE LNKEDT STATEMENT                      
         MVI   0(R3),C'B'                                                       
         B     ARCX                                                             
         SPACE 2                                                                
ARCAMERR MVI   AMERR,C'Y'          PROCESS INVALID && CARD                      
         BAS   RA,CLEARGEN                                                      
         MVI   0(R5),0                                                          
         MVC   WORK,SPACES         CONVERT TO INVALID PAN#1 CARD                
         MVC   WORK(9),=C'++COMMENT'                                            
         MVC   WORK+10(70),AC                                                   
         TM    ACFLAG,X'01'        TEST IF &&CARD GIVEN BY EOF                  
         BO    EOF1                                                             
         MVC   0(80,R2),WORK                                                    
         MVC   LASTCARD,WORK                                                    
         B     RETURN                                                           
         SPACE 2                                                                
ARCAMGX  MVI   WHATIDID,1          DELETE && CARD AND SET END OF GEN            
         MVI   0(R5),0                                                          
         TM    ACFLAG,X'01'        TEST IF &&CARD GIVEN BY EOF                  
         BO    EOF1                                                             
         MVI   0(R3),C'B'                                                       
         B     ARCX                                                             
         EJECT                                                                  
*                                                                               
* &&COMPILE CARD GENERATION                                                     
*                                                                               
ARCAMAS  MVC   0(L'MSG3,R5),MSG3   --WRITE WORK,BOOKNAME,/*                     
         CLC   PANACTN,SOAM                                                     
         BNE   *+10                                                             
         MVC   0(2,R5),=C'++'                                                   
         LA    RE,13(R5)                                                        
         CLI   0(RE),C' '                                                       
         BE    *+12                                                             
         LA    RE,1(RE)                                                         
         B     *-12                                                             
         MVC   0(3,RE),=C',/*'                                                  
         LA    R5,80(R5)                                                        
         MVI   WFLAG,X'01'         WRITE SOURCE                                 
         B     ARCAMGX                                                          
         EJECT                                                                  
*                                                                               
* &&CATAL CARD GENERATION                                                       
*                                                                               
ARCAMCA  MVC   LNKCARD,SPACES      SAVE AND PROCESS INPUT CATAL CARD            
         MVC   LNKCARD+1(78),AC+2                                               
         MVC   0(L'MSG3,R5),MSG3   --WRITE WORK,BOOKNAME,/*                     
         CLC   PANACTN,SOAM                                                     
         BNE   *+10                                                             
         MVC   0(2,R5),=C'++'                                                   
         LA    RE,13(R5)                                                        
         CLI   0(RE),C' '                                                       
         BE    *+12                                                             
         LA    RE,1(RE)                                                         
         B     *-12                                                             
         MVC   0(3,RE),=C',/*'                                                  
         LA    R5,80(R5)                                                        
         MVI   WFLAG,X'0B'         WRITE SOURCE/SEARCH END/CATAL                
         B     ARCAMGX                                                          
         EJECT                                                                  
*                                                                               
* &&PHASE AND &&LINK CARD GENERATION                                            
*                                                                               
ARCAMPH  MVC   LNKCARD,SPACES      SAVE AND PROCESS INPUT PHASE CARD            
         MVC   LNKCARD+1(78),AC+2                                               
         MVI   AMPLVL,C' '                                                      
ARCAMPH0 CLI   AMACTN,C'P'         SET DEFAULT CIL FOR &&PHASE                  
         BNE   ARCAMPH1                                                         
         MVI   AMPLIB,C'C'                                                      
         B     ARCAMPH3                                                         
ARCAMPH1 MVC   AMPLIB,AMACTN+4     SET LIBRARY FROM LINKX CARD                  
         MVC   AMACTN,=CL8'PHASE'                                               
         CLI   AMPLIB,C' '                                                      
         BNE   ARCAMPH2                                                         
         MVC   WORK(74),LNKCARD+6  CONVERT &&LINK X.. TO &&PHASE X..            
         MVI   LNKCARD+6,C' '                                                   
         MVC   LNKCARD+7(73),WORK                                               
         MVC   LNKCARD+1(5),=CL8'PHASE'                                         
         B     ARCAMPH0                                                         
ARCAMPH2 MVC   LNKCARD+1(5),=CL8'PHASE'                                         
ARCAMPH3 CLC   LNKCARD+7(5),=C'TEST='                                           
         BNE   ARCAMPH4                                                         
         MVC   AMPLVL,LNKCARD+12   SAVE TEST LEVEL = A/B/C                      
         CLI   AMPLVL,C'A'                                                      
         BL    ARCAMERR                                                         
         CLI   AMPLVL,C'C'                                                      
         BH    ARCAMERR                                                         
         CLC   LNKCARD+13(20),SPACES                                            
         BNE   ARCAMERR                                                         
         MVC   LNKCARD+7(6),SPACES                                              
ARCAMPH4 BAS   RA,CLEARLNK         CLEAR LINK AREA                              
         CLC   BOOKNAME(2),=C'LK'                                               
         BE    *+14                                                             
         CLC   BOOKNAME(2),=C'LM'                                               
         BNE   ARCAMPH5                                                         
         MVC   0(L'MSG7,R5),MSG7   --WRITE PRINT,BOOKNAME                       
         CLC   PANACTN,SOAM                                                     
         BNE   *+10                                                             
         MVC   0(2,R5),=C'++'                                                   
         LA    R5,80(R5)                                                        
ARCAMPH5 MVC   0(L'MSG3,R5),MSG3   --WRITE WORK,BOOKNAME                        
         CLC   PANACTN,SOAM                                                     
         BNE   *+10                                                             
         MVC   0(2,R5),=C'++'                                                   
         CLC   BOOKNAME(2),=C'LK'                                               
         BE    ARCAMPH6                                                         
         CLC   BOOKNAME(2),=C'LM'                                               
         BE    ARCAMPH6                                                         
         LA    RE,13(R5)                                                        
         CLI   0(RE),C' '                                                       
         BE    *+12                                                             
         LA    RE,1(RE)                                                         
         B     *-12                                                             
         MVC   0(3,RE),=C',/*'                                                  
ARCAMPH6 LA    R5,80(R5)                                                        
         MVI   WFLAG,X'07'         WRITE SOURCE/SEARCH END/PHASE                
         B     ARCAMGX                                                          
         EJECT                                                                  
SETDATA  CLI   SETSW,C'N'          CLEAR/SET DATA AREA                          
         BNE   SETDATA0                                                         
         MVI   SETSW,C'Y'          INITIALISE AND SET FLAG                      
         L     R5,ASETAREA                                                      
         MVI   0(R5),C' '                                                       
         MVC   1(251,R5),0(R5)                                                  
         MVC   0(3,R5),=C'ALL'     SET ALL STANDARD NAME                        
         MVI   5(R5),C'Y'                                                       
         LA    R5,6(R5)                                                         
         MVC   0(2,R5),=C'UK'      SET UK & US STANDARD NAMES                   
         MVC   6(2,R5),=C'US'                                                   
         MVI   05(R5),C'Y'                                                      
         MVI   11(R5),C'N'                                                      
         DATE  DUB,DATE=NO                                                      
         CLC   DUB+6(2),=C'UK'                                                  
         BE    *+12                                                             
         MVI   05(R5),C'N'                                                      
         MVI   11(R5),C'Y'                                                      
         LA    R5,12(R5)                                                        
         MVC   0(2,R5),=C'OS'      SET DOS & MVS STANDARD NAMES                 
         MVC   6(2,R5),=C'DO'                                                   
         MVI   05(R5),C'Y'                                                      
         MVI   11(R5),C'N'                                                      
         LA    R5,12(R5)                                                        
         ST    R5,SETADDR                                                       
         SPACE 2                                                                
SETDATA0 LA    RE,SETDATAC         POINT TO SET DATA CARD                       
         CLI   0(RE),C' '                                                       
         BE    SETDATAX            EXIT IF NO SET DATA                          
         MVI   80(RE),C' '                                                      
         SPACE 2                                                                
SETDATA2 LA    R0,6                SCAN FOR SET DATA NAME (MAX=5)               
         LR    RF,RE               SAVE A(START OF NAME)                        
         CLI   0(RE),C'='                                                       
         BE    SETDATA3                                                         
         LA    RE,1(RE)                                                         
         BCT   R0,*-12                                                          
         B     SETDATAE            ERROR TOO LONG                               
         SPACE 2                                                                
SETDATA3 LR    R1,RE               SAVE A(END OF NAME)                          
         CLI   1(RE),C'Y'                                                       
         BE    SETDATA4                                                         
         CLI   1(RE),C'N'                                                       
         BE    SETDATA4                                                         
         B     SETDATAE            ERROR VALUE NOT Y OR N                       
SETDATA4 SR    RE,RF                                                            
         BZ    SETDATAE                                                         
         BCTR  RE,0                RE=L'NAME-1                                  
         SPACE 2                                                                
         L     R5,ASETAREA         SEARCH FOR NAME IN SET AREA                  
SETDATA5 CLI   0(R5),0                                                          
         BE    SETDATAE            ERROR END OF TABLE                           
         CLI   0(R5),C' '                                                       
         BE    SETDATA6            NAME NOT FOUND                               
         EX    RE,*+8                                                           
         B     *+10                                                             
         CLC   0(0,RF),0(R5)                                                    
         BE    *+12                                                             
         LA    R5,6(R5)                                                         
         B     SETDATA5                                                         
         MVC   5(1,R5),1(R1)       SET VALUE                                    
         B     SETDATA7                                                         
         SPACE 2                                                                
SETDATA6 EX    RE,*+8              INSERT NEW NAME/VALUE                        
         B     *+10                                                             
         MVC   0(0,R5),0(RF)                                                    
         MVC   5(1,R5),1(R1)                                                    
         LA    R5,6(R5)                                                         
         ST    R5,SETADDR                                                       
         SPACE 2                                                                
SETDATA7 LA    RE,2(R1)            RESTORE CARD POINTER                         
         CLI   0(RE),C' '                                                       
         BE    SETDATAX            END OF CARD                                  
         CLI   0(RE),C','                                                       
         BNE   SETDATAE            ERROR INVALID DELIMITER                      
         LA    RE,1(RE)                                                         
         B     SETDATA2            BACK FOR NEXT NAME                           
         SPACE 2                                                                
SETDATAE MVI   SETDATAC,0          RETURN ERROR VALUE                           
SETDATAX BR    RA                                                               
         EJECT                                                                  
CLEARGEN L     R5,AGENAREA         CLEAR GENERATION AREA                        
         ST    R5,GENADDR                                                       
         LR    R7,R5                                                            
CLEARG1  SH    R7,=H'2'                                                         
         LH    R0,0(R7)                                                         
         LA    R7,2(R7)                                                         
         MVC   0(80,R7),SPACES                                                  
         LA    R7,80(R7)                                                        
         BCT   R0,*-10                                                          
         BR    RA                                                               
         SPACE 2                                                                
CLEARLNK L     R6,ALNKAREA         CLEAR LINK STATEMENT AREA                    
         ST    R6,LNKADDR                                                       
         LR    R7,R6                                                            
         B     CLEARG1                                                          
         SPACE 2                                                                
CLEARWRK L     R5,AWRKAREA         CLEAR WORK STATEMENT AREA                    
         ST    R5,WRKADDR                                                       
         LR    R7,R5                                                            
         B     CLEARG1                                                          
         EJECT                                                                  
*                                                                               
* GENERATE CATAL CONTROL STATEMENTS                                             
*                                                                               
GENCAT   EQU   *                                                                
         MVI   FLAG,X'01'                                                       
         MVC   0(L'MSG4,R5),MSG4   --INSERT PUNCH                               
         LA    R5,80(R5)                                                        
         SPACE 2                                                                
GENC1    CLC   LNKCARD+1(5),=CL8'PUNCH'                                         
         BNE   GENC2                                                            
         MVC   0(54,R5),LNKCARD+7  TEXT FROM ASSEMBLER PUNCH STATEMENT          
         CLC   0(2,R5),=C'++'                                                   
         BNE   *+8                                                              
         MVI   0(R5),C'$'          PAN CONVENTION FOR COL-1                     
         CLC   0(2,R5),=C'--'                                                   
         BNE   *+8                                                              
         MVI   0(R5),C'$'                                                       
         LA    R5,80(R5)                                                        
         B     GENCX                                                            
         SPACE 2                                                                
GENC2    LA    R7,LNKCARD+6        SEARCH FOR OBJECT NAME                       
         CLI   0(R7),C' '                                                       
         BE    *+8                                                              
         LA    R7,1(R7)                                                         
         LA    R0,20                                                            
         CLI   0(R7),C' '                                                       
         BNE   *+16                                                             
         LA    R7,1(R7)                                                         
         BCT   R0,*-12                                                          
         B     GENC2C                                                           
         LR    RE,R7               R7=A(START OF OBJECT NAME)                   
         LA    R0,11                                                            
GENC2A   CLI   0(RE),C' '                                                       
         BE    GENC2B                                                           
         CLI   0(RE),C','                                                       
         BE    GENC2B                                                           
         LA    RE,1(RE)                                                         
         BCT   R0,GENC2A                                                        
         B     GENC2C                                                           
GENC2B   LR    RF,RE                                                            
         SR    RF,R7               RF=L'OBJECT NAME                             
         BZ    GENC2C                                                           
         CH    RF,=H'8'                                                         
         BNH   GENC3                                                            
         TM    PRCFLAG,X'40'       ALLOW RLXXXXXXXX AND RMXXXXXXXX              
         BZ    GENC2C              FOR CATAL TO PAN LIBRARY ONLY                
         CLC   0(2,R7),=C'RL'                                                   
         BE    GENC3                                                            
         CLC   0(2,R7),=C'RM'                                                   
         BNE   GENC3                                                            
GENC2C   LA    R7,LNKCARD+6        SET CATAL NAME FOR ERROR                     
         CLI   LNKCARD+6,C' '                                                   
         BE    *+8                                                              
         LA    R7,1(R7)                                                         
         MVC   0(20,R7),SPACES                                                  
         LA    R7,1(R7)                                                         
         LA    RF,5                                                             
         MVC   0(5,R7),=C'DUMMY'                                                
         SPACE 2                                                                
GENC3    TM    PRCFLAG,X'40'       CATALOGUE OBJECT MODULE TO PAN               
         BZ    GENC4                                                            
         LA    RE,0(R7,RF)         RE=A(CHR AFTER OBJECT NAME)                  
         CLI   0(RE),C','                                                       
         BNE   GENC3A                                                           
         CLC   1(3,RE),=C'ADD'     SET ACTION TO ADD IF SPECIFIED               
         BNE   *+12                                                             
         MVI   DUB,C'A'                                                         
         B     GENC3B                                                           
         CLC   1(3,RE),=C'UPD'     SET ACTION TO UPDATE IF SPECIFIED            
         BNE   *+12                                                             
         MVI   DUB,C'U'                                                         
         B     GENC3B                                                           
GENC3A   MVI   DUB,C'U'            DEFAULT TO ++UPDATE                          
         CLC   SAVLEVEL,=C'001'                                                 
         BNE   *+8                                                              
         MVI   DUB,C'A'            DEFAULT TO ++ADD IF LEVEL IS 001             
GENC3B   MVC   0(5,R5),=C'$+ADD'                                                
         LA    RE,6(R5)                                                         
         CLI   DUB,C'A'                                                         
         BE    *+14                                                             
         MVC   0(8,R5),=C'$+UPDATE'                                             
         LA    RE,9(R5)                                                         
         CLC   0(2,R7),=C'RL'      TEST IF RLXXXXXXXX                           
         BE    GENC3C                                                           
         CLC   0(2,R7),=C'RM'      TEST IF RMXXXXXXXX                           
         BE    GENC3C                                                           
         MVC   0(2,RE),=C'RM'                                                   
         LA    RE,2(RE)                                                         
GENC3C   BCTR  RF,0                                                             
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   0(0,RE),0(R7)       MOVE OBJECT NAME                             
         LA    RE,1(RE,RF)                                                      
         MVI   0(RE),C','                                                       
         LA    RE,1(RE)                                                         
         CLI   DUB,C'A'                                                         
         BNE   *+14                                                             
         MVC   0(6,RE),=C'OBJECT'  ++ADD RMXXXXXXXX,OBJECT                      
         B     *+10                                                             
         MVC   0(5,RE),=C'0,ALL'   ++UPDATE RMXXXXXXXX,0,ALL                    
         LA    R5,80(R5)                                                        
         B     GENCX                                                            
         SPACE 2                                                                
GENC4    MVC   1(4,R5),=CL8'NAME'  CATALOGUE OBJECT MODULE TO IBM               
         LA    RE,6(R5)                                                         
         BCTR  RF,0                                                             
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   0(0,RE),0(R7)       NAME XXXXXXXX(R)                             
         LA    RE,1(RE,RF)                                                      
         MVC   0(3,RE),=C'(R)'                                                  
         LA    R5,80(R5)                                                        
         B     GENCX                                                            
         SPACE 2                                                                
GENCX    LH    RF,GENCOUNT         BUMP GENERATED COUNT                         
         LA    RF,1(RF)                                                         
         STH   RF,GENCOUNT                                                      
         BR    RA                                                               
         EJECT                                                                  
*                                                                               
* GENERATE TRAILING LINK EDIT JCL FROM LNKAREA                                  
*                                                                               
GENLNK   EQU   *                   GENERATE LINK EDIT JCL                       
         L     R6,LNKADDR                                                       
         MVI   0(R6),0             SET END OF LINK AREA                         
         L     R6,ALNKAREA                                                      
         ST    R6,LNKADDR                                                       
         MVI   FLAG,0                                                           
         MVC   OBJSAVE,SPACES                                                   
         B     GENL1+4                                                          
         SPACE 2                                                                
GENL1    LA    R6,80(R6)           BUMP TO NEXT LINK AREA CARD                  
         CLI   0(R6),0                                                          
         BE    GENL6               END OF TABLE                                 
         CLC   1(5,R6),=CL8'PHASE'                                              
         BE    GENL6               NEW PHASE                                    
         CLC   1(4,R6),=CL8'NAME'                                               
         BE    GENL6               NEW PHASE                                    
         CLC   1(6,R6),=CL8'INCLUDR'                                            
         BNE   GENL2                                                            
         TM    FLAG,X'40'          PROCESS INCLUDE CARDS ON 1ST PASS            
         BO    GENL1                                                            
         B     GENL4                                                            
         SPACE 2                                                                
GENL2    LA    RE,LNKTBL           SEARCH LINK EDIT COMMANDS TABLE              
         SR    RF,RF                                                            
         ICM   RF,1,0(RE)                                                       
         BZ    GENL1               IGNORE IF NOT IN TABLE                       
         BCTR  RF,0                                                             
         EX    RF,*+8                                                           
         B     *+10                                                             
         CLC   2(0,RE),1(R6)                                                    
         BE    GENL3                                                            
         LA    RE,10(RE)                                                        
         B     GENL2+4                                                          
         SPACE 2                                                                
GENL3    LA    RF,2(RF,R6)         IGNORE IF BAD SYNTAX                         
         CLI   0(RF),C' '                                                       
         BNE   GENL1                                                            
         TM    1(RE),X'80'         TEST IF TO BE INSERTED AT END                
         BZ    GENL3A              NO                                           
         TM    FLAG,X'40'          YES PROCESS IF 2ND PASS                      
         BO    GENL3B                                                           
         OI    FLAG,X'80'          SET 2ND PASS REQUIRED                        
         B     GENL1                                                            
GENL3A   TM    FLAG,X'40'          IGNORE IF 2ND PASS                           
         BO    GENL1                                                            
GENL3B   TM    FLAG,X'01'                                                       
         BO    *+18                                                             
         OI    FLAG,X'01'                                                       
         MVC   0(L'MSG4,R5),MSG4   --INSERT PUNCH                               
         LA    R5,80(R5)                                                        
         MVC   1(79,R5),1(R6)      LNKEDT CONTROL CARD                          
         LA    R5,80(R5)                                                        
         B     GENL1                                                            
         SPACE 2                                                                
GENL4    CLI   7(R6),C'E'          INCLUDE OBJECT MODULE FROM PAN               
         BE    *+12                                                             
         CLI   7(R6),C'P'          INCLUDE/INCLUDP                              
         BNE   GENL5                                                            
         CLI   8(R6),C' '                                                       
         BNE   GENL1               IGNORE IF BAD SYNTAX                         
         LA    R7,9(R6)                                                         
         LA    R0,20                                                            
         CLI   0(R7),C' '          R7=A(START OF OBJECT NAME)                   
         BNE   *+16                                                             
         LA    R7,1(R7)                                                         
         BCT   R0,*-12                                                          
         B     GENL1                                                            
         LR    RE,R7                                                            
         LA    R0,11                                                            
GENL4A   CLI   0(RE),C' '                                                       
         BE    GENL4B                                                           
         CLI   0(RE),C','                                                       
         BE    GENL4B                                                           
         LA    RE,1(RE)                                                         
         BCT   R0,GENL4A                                                        
         B     GENL1                                                            
GENL4B   LR    RF,RE                                                            
         SR    RF,R7               RF=L'OBJECT NAME                             
         BZ    GENL1                                                            
         CH    RF,=H'8'                                                         
         BNH   GENL4C                                                           
         CLC   0(2,R7),=C'RL'                                                   
         BE    GENL4C                                                           
         CLC   0(2,R7),=C'RM'                                                   
         BNE   GENL1                                                            
GENL4C   MVC   OBJNAME,SPACES      MOVE OUT OBJECT NAME                         
         BCTR  RF,0                                                             
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   OBJNAME(0),0(R7)                                                 
         CLC   OBJNAME(2),=C'IJ'   IGNORE DOS NAMES                             
         BE    GENL1                                                            
         CLC   OBJNAME,OBJSAVE                                                  
         BE    GENL1               IGNORE DUPLICATES                            
         MVC   OBJSAVE,OBJNAME                                                  
         NI    FLAG,X'FE'                                                       
         MVC   0(L'MSG6,R5),MSG6   --WRITE PUNCH,RMXXXXXXXX                     
         CLC   OBJNAME(2),=C'RL'                                                
         BE    *+14                                                             
         CLC   OBJNAME(2),=C'RM'                                                
         BNE   GENL4D                                                           
         MVC   14(10,R5),OBJNAME                                                
         B     GENL4E                                                           
GENL4D   MVC   14(2,R5),=C'RM'                                                  
         MVC   16(8,R5),OBJNAME                                                 
GENL4E   LA    R5,80(R5)                                                        
         B     GENL1                                                            
         SPACE 2                                                                
GENL5    CLI   7(R6),C'R'          INCLUDE OBJECT MODULE FROM IBM               
         BE    *+12                                                             
         CLI   7(R6),C'L'          INCLUDR/INCLUDL                              
         BNE   GENL1                                                            
         CLI   8(R6),C' '                                                       
         BNE   GENL1               IGNORE IF BAD SYNTAX                         
         LA    R0,20                                                            
         LA    R7,9(R6)                                                         
         CLI   0(R7),C' '          R7=A(START OF OBJECT NAME)                   
         BNE   *+16                                                             
         LA    R7,1(R7)                                                         
         BCT   R0,*-12                                                          
         B     GENL1                                                            
         LR    RE,R7                                                            
         LA    R0,9                                                             
GENL5A   CLI   0(RE),C' '                                                       
         BE    GENL5B                                                           
         CLI   0(RE),C'('                                                       
         BE    GENL5B                                                           
         LA    RE,1(RE)                                                         
         BCT   R0,GENL5A                                                        
         B     GENL1                                                            
GENL5B   LR    RF,RE                                                            
         SR    RF,R7               RF=L'OBJECT NAME                             
         CLI   0(RE),C' '          TEST IF OS SYNTAX                            
         BE    GENL5C              NO                                           
         TM    FLAG,X'01'                                                       
         BO    *+18                                                             
         OI    FLAG,X'01'                                                       
         MVC   0(L'MSG4,R5),MSG4   --INSERT PUNCH                               
         LA    R5,80(R5)                                                        
         MVC   1(79,R5),1(R6)      INCLUDE DDNAME(OBJNAME)                      
         MVC   0(9,R5),=C' INCLUDE '                                            
         LA    R5,80(R5)                                                        
         B     GENL1                                                            
GENL5C   CLC   0(2,R7),=C'IJ'      IGNORE DOS NAMES                             
         BE    GENL1                                                            
         TM    FLAG,X'01'                                                       
         BO    *+18                                                             
         OI    FLAG,X'01'                                                       
         MVC   0(L'MSG4,R5),MSG4   --INSERT PUNCH                               
         LA    R5,80(R5)                                                        
         MVC   0(9,R5),=C' INCLUDE '                                            
         MVC   9(L'OBJLIB,R5),OBJLIB                                            
         LA    RE,9+L'OBJLIB(R5)                                                
         MVI   0(RE),C'('          INCLUDE OBJLIB(OBJNAME)                      
         BCTR  RF,0                                                             
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   1(0,RE),0(R7)                                                    
         LA    RE,2(RE,RF)                                                      
         MVI   0(RE),C')'                                                       
         LA    R5,80(R5)                                                        
         B     GENL1                                                            
         SPACE 2                                                                
GENL6    TM    FLAG,X'40'          TEST IF END OF 2ND PASS                      
         BO    GENL7               YES                                          
         TM    FLAG,X'80'          TEST IF 2ND PASS REQUIRED                    
         BZ    GENL7               NO                                           
         NI    FLAG,X'7F'                                                       
         OI    FLAG,X'40'          SET 2ND PASS                                 
         L     R6,LNKADDR                                                       
         B     GENL1+4                                                          
         SPACE 2                                                                
GENL7    ST    R6,LNKADDR          SAVE A(NEXT LNKEDT SET)                      
         MVC   WORK,SPACES                                                      
         MVC   WORK+1(4),=CL8'NAME'                                             
         CLC   LNKCARD+1(5),=CL8'PHASE'                                         
         BE    GENL8                                                            
         CLC   LNKCARD+1(4),=CL8'NAME'                                          
         BE    GENL8                                                            
         B     GENL8D              ERROR IF NO VALID DATA IN LNKCARD            
         SPACE 2                                                                
GENL8    CLC   1(4,R6),=CL8'NAME'                                               
         BNE   *+14                                                             
         MVC   WORK+1(79),1(R6)                                                 
         B     GENL9                                                            
         CLI   LNKCARD+6,C' '                                                   
         BNE   GENL8D                                                           
         LA    RE,LNKCARD+7                                                     
         LA    R0,20                                                            
         CLI   0(RE),C' '                                                       
         BNE   *+16                                                             
         LA    RE,1(RE)                                                         
         BCT   R0,*-12                                                          
         B     GENL8D                                                           
         LR    RF,RE                                                            
         LA    R0,9                                                             
GENL8A   CLI   0(RE),C' '                                                       
         BE    GENL8B                                                           
         CLI   0(RE),C','                                                       
         BE    GENL8B                                                           
         LA    RE,1(RE)                                                         
         BCT   R0,GENL8A                                                        
         B     GENL8D                                                           
GENL8B   SR    RE,RF                                                            
         BZ    GENL8D                                                           
GENL8C   BCTR  RE,0                RF=A(NAME) AND RE=L'NAME                     
         EX    RE,*+8                                                           
         B     *+10                                                             
         MVC   WORK+6(0),0(RF)                                                  
         LA    RE,WORK+7(RE)                                                    
         MVC   0(3,RE),=C'(R)'                                                  
         B     GENL9                                                            
GENL8D   LA    RF,=C'DUMMY'        SET PHASE NAME FOR ERROR                     
         LA    RE,5                                                             
         B     GENL8C                                                           
         SPACE 2                                                                
GENL9    TM    FLAG,X'01'                                                       
         BO    *+18                                                             
         OI    FLAG,X'01'                                                       
         MVC   0(L'MSG4,R5),MSG4   --INSERT PUNCH                               
         LA    R5,80(R5)                                                        
         MVC   0(80,R5),WORK       NAME XXXXXXXX(R)                             
         LA    R5,80(R5)                                                        
         SPACE 2                                                                
GENLA    CLI   0(R6),0             TEST IF END OF LNKAREA                       
         BE    GENLX                                                            
         MVI   LNKCARD,C' '        SAVE FIRST CARD OF SET IN LNKCARD            
         MVC   LNKCARD+1(79),1(R6)                                              
         LA    R6,80(R6)                                                        
         ST    R6,LNKADDR          SAVE A(FIRST CARD IN SET)                    
         NI    FLAG,X'01'                                                       
         MVC   OBJSAVE,SPACES                                                   
         B     GENL1+4             BACK TO PROCESS NEXT LNKAREA SET             
         SPACE 2                                                                
GENLX    LH    RF,GENCOUNT         BUMP GENERATED COUNT                         
         LA    RF,1(RF)                                                         
         STH   RF,GENCOUNT                                                      
         BR    RA                                                               
         EJECT                                                                  
*                                                                               
* PAN#1 AT EOF OR END OF &&CARD SEQUENCE                                        
*                                                                               
EOF      OI    EOFFLAG,X'01'       SET PAN#1 END OF FILE                        
         BAS   RA,CLEARGEN                                                      
         CLC   LASTCARD(2),PANAM   WAS LAST CARD &&CARD                         
         BE    EOF3                YES                                          
         OC    AMPCOUNT,AMPCOUNT                                                
         BNZ   EOF6                                                             
         MVC   AC,SPACES           SET DEFAULT &&CARD                           
         MVC   AC(2),PANAM                                                      
         MVC   AC+2(8),ACTDFLT                                                  
         CLI   AA,C' '             CAN BE OVER WRITTEN BY ACTION &&DATA         
         BE    *+10                                                             
         MVC   AC,AA                                                            
         OI    ACFLAG,X'01'        SET &&CARD CREATED BY EOF                    
         B     ARCAM                                                            
         SPACE 2                                                                
EOF1     MVC   LASTCARD,AC         RETURN FROM ARCAM                            
         CLI   AMERR,C'Y'                                                       
         BE    *+12                                                             
         MVI   0(R3),C'B'                                                       
         B     RETURN                                                           
         MVC   0(80,R5),WORK                                                    
         LA    R5,80(R5)                                                        
         MVI   0(R5),0                                                          
         MVC   LASTCARD,WORK                                                    
         MVI   WHATIDID,1                                                       
         MVI   0(R3),C'B'                                                       
         B     RETURN                                                           
         SPACE 2                                                                
EOF3     MVI   WFLAG,0                                                          
         BAS   RA,CLEARGEN                                                      
         CLC   AMACTN,=CL8'COMPILE'                                             
         BE    EOF4                                                             
         CLC   AMACTN,=CL8'ASM'                                                 
         BE    EOF4                                                             
         CLC   AMACTN(5),=CL8'CATALR'                                           
         BNE   *+12                                                             
         BAS   RA,GENCAT                                                        
         B     EOF4                                                             
         CLC   AMACTN,=CL8'PHASE'                                               
         BNE   *+12                                                             
         BAS   RA,GENLNK                                                        
         B     EOF4                                                             
         CLC   LASTCARD(9),=C'++COMMENT'                                        
         BNE   *+6                                                              
         DC    H'0'                                                             
         OC    AMPCOUNT,AMPCOUNT                                                
         BNZ   EOF6                                                             
         B     EOF4                                                             
         SPACE 2                                                                
EOF4     TM    EOFFLAG,X'01'       PREVIOUS CARD WAS &&CARD                     
         BO    EOF5                                                             
         MVC   0(80,R5),0(R2)      SAVE THIS CARD                               
         LA    R5,80(R5)                                                        
         MVI   0(R5),1             SET TEMPORARY DELETE                         
         MVI   WHATIDID,1                                                       
         MVI   0(R3),C'B'                                                       
         B     ARCX                                                             
         SPACE 2                                                                
EOF5     MVI   0(R5),2             SET END OF GEN                               
         MVI   WHATIDID,1                                                       
         MVI   0(R3),C'B'                                                       
         B     ARCX                                                             
         SPACE 2                                                                
EOF6     MVI   0(R3),C'G'          TELL PAN#1 EOF                               
         B     RETURN                                                           
         EJECT                                                                  
* PANTBL DEFINES PAN ACTIONS                                                    
* AL1    LENGTH OF PAN ACTION                                                   
* CL8    PAN ACTION NAME                                                        
* CL1    PAN ACTION FLAG - C'Y'=&& CAN FOLLOW - C'N'=NO && ALLOWED              
*                                                                               
PANTBL   DS    0CL10                                                            
         DC    AL1(6),CL8'UPDATE  ',C'Y'                                        
         DC    AL1(3),CL8'ADD     ',C'Y'                                        
         DC    AL1(5),CL8'LEVEL   ',C'Y'                                        
         DC    AL1(4),CL8'COPY    ',C'N'                                        
         DC    AL1(6),CL8'RENAME  ',C'N'                                        
         DC    AL1(6),CL8'STATUS  ',C'N'                                        
         DC    AL1(6),CL8'INSERT  ',C'N'                                        
         DC    AL1(5),CL8'WRITE   ',C'N'                                        
         DC    AL1(6),CL8'SELECT  ',C'N'                                        
PANTBLX  DC    AL1(0)                                                           
         SPACE 2                                                                
* ACTTBL DEFINES VALID NAMES FOR THE FIRST && CARD ACTION                       
* AL1    ACTION NUMBER                                                          
* XL1    ACTION FLAGS - X'80'=ACTION AVAIL FOR THIS PROC                        
* AL1    ACTION LENGTH                                                          
* CL8    ACTION NAME                                                            
*                                                                               
ACTTBL   DS    0CL11                                                            
         DC    AL1(01),X'00',AL1(3),CL8'ASM'                                    
         DC    AL1(02),X'00',AL1(8),CL8'COMPILE'                                
         DC    AL1(03),X'00',AL1(5),CL8'CATAL'                                  
         DC    AL1(04),X'00',AL1(6),CL8'CATALP'                                 
         DC    AL1(05),X'00',AL1(6),CL8'CATALR'                                 
         DC    AL1(06),X'00',AL1(5),CL8'PHASE'                                  
         DC    AL1(07),X'00',AL1(4),CL8'LINK'                                   
         DC    AL1(08),X'00',AL1(5),CL8'LINKC'                                  
         DC    AL1(09),X'00',AL1(5),CL8'LINKP'                                  
ACTTBLX  DC    AL1(00)                                                          
         SPACE 2                                                                
* PRCTBL DEFINES VALID PROCEDURES AND THE ACTIONS AVAILABLE                     
* AL1    PROC NUMBER                                                            
* XL1    PROC FLAGS - X'80'=LK...,X'40'=CATALP,X'20'=CATALR                     
* CL8    PROC NAME                                                              
* AL1    VALID ACTION NUMBERS FOR THIS PROC (MAX OF FOUR)                       
*                                                                               
PRCTBL   DS    0CL14                                                            
         DC    AL1(01),X'00',CL8'PANA    ',AL1(01,02,00,00)                     
         DC    AL1(02),X'40',CL8'PANACAT ',AL1(03,04,00,00)                     
         DC    AL1(03),X'40',CL8'PANACATP',AL1(03,04,00,00)                     
         DC    AL1(04),X'20',CL8'PANACATR',AL1(03,05,00,00)                     
         DC    AL1(05),X'00',CL8'PANALNK ',AL1(06,07,08,00)                     
         DC    AL1(06),X'80',CL8'PANLNK  ',AL1(06,07,08,00)                     
PRCTBLX  DC    AL1(00)                                                          
         EJECT                                                                  
* LNKTBL DEFINES &&CARDS THAT CAN FOLLOW &&PHASE                                
* AL1    LENGTH OF LINK ACTION NAME                                             
* XL1    LINK ACTION FLAGS - X'80'=INSERT AT END OF LINK CONTROL                
* CL8    LINK ACTION NAME                                                       
*                                                                               
LNKTBL   DS    0CL10                                                            
         DC    AL1(05),X'80',CL8'ALIAS'                                         
         DC    AL1(06),X'00',CL8'CHANGE'                                        
         DC    AL1(05),X'80',CL8'ENTRY'                                         
         DC    AL1(06),X'00',CL8'EXPAND'                                        
         DC    AL1(08),X'00',CL8'IDENTIFY'                                      
         DC    AL1(07),X'00',CL8'INCLUDE'                                       
         DC    AL1(07),X'00',CL8'LIBRARY'                                       
         DC    AL1(05),X'80',CL8'ORDER'                                         
         DC    AL1(07),X'00',CL8'REPLACE'                                       
         DC    AL1(07),X'80',CL8'SETCODE'                                       
         DC    AL1(06),X'80',CL8'SETSSI'                                        
LNKTBLX  DC    AL1(00)                                                          
         SPACE 2                                                                
SOAM     DC    C'DUMMY'            STAND ALONE DUMMY ++CARD                     
OBJLIB   DC    C'ASMLIB'           DEFAULT OBJECT LIBRARY                       
PRCNUM   DC    X'00'                                                            
PRCFLAG  DC    X'00'                                                            
PRCNAME  DC    CL8' '                                                           
ACTNUM   DC    X'00'                                                            
ACTFLAG  DC    X'00'                                                            
ACTDFLT  DC    CL8' '                                                           
         SPACE 2                                                                
MSG1     DC    C'--INSERT WORK'                                                 
MSG2     DC    C'--INSERT WORK,/*'                                              
MSG3     DC    C'--WRITE WORK,XXXXXXXXXX'                                       
MSG4     DC    C'--INSERT PUNCH'                                                
MSG5     DC    C'--INSERT PUNCH,/*'                                             
MSG6     DC    C'--WRITE PUNCH,XXXXXXXXXX'                                      
MSG7     DC    C'--WRITE PRINT,XXXXXXXXXX'                                      
         SPACE 2                                                                
RELO     DC    A(*)                                                             
APARM    DS    A                                                                
ASETAREA DS    A                                                                
AGENAREA DS    A                                                                
ALNKAREA DS    A                                                                
AWRKAREA DS    A                                                                
SETADDR  DS    A                                                                
GENADDR  DS    A                                                                
LNKADDR  DS    A                                                                
WRKADDR  DS    A                                                                
         SPACE 2                                                                
DUB      DS    D                                                                
SAVEAREA DS    4F                                                               
SAVADDR  DS    A                                                                
WCOUNT   DC    H'0'                                                             
AMPCOUNT DC    H'0'                                                             
GENCOUNT DC    H'0'                                                             
SAVLEVEL DS    CL3                                                              
SAVDATE  DS    CL8                                                              
SAVCSECT DS    CL8                                                              
OBJNAME  DS    CL10                                                             
OBJSAVE  DS    CL10                                                             
PANAM    DC    C'&&&&'                                                          
FRSTTIME DC    C'Y'                                                             
ACFLAG   DC    X'00'                                                            
AMERR    DC    C'N'                                                             
PCHFLAG  DC    C'N'                                                             
INCSW    DC    C'Y'                                                             
INCLV    DC    H'0'                                                             
INCTBL   DC    C'Y    ',X'FF'                                                   
SETSW    DC    C'N'                                                             
EOFFLAG  DC    X'00'                                                            
WFLAG    DC    X'00'                                                            
FLAG     DC    X'00'                                                            
WHATIDID DC    X'00'                                                            
PANACTN  DC    C' '                                                             
BOOKNAME DC    CL10' '                                                          
AMACTN   DC    CL8' '                                                           
LASTCARD DC    CL10' '                                                          
SPACES   DC    CL80' '                                                          
WORK     DS    CL80                                                             
SETDATAC DS    CL80                                                             
AMPLEN   DS    H                                                                
AMPLVL   DS    CL1                                                              
AMPLIB   DS    CL1                                                              
LNKCARD  DS    CL80                                                             
AA       DS    CL80                                                             
AC       DS    CL80                                                             
         SPACE 2                                                                
         LTORG                                                                  
         EJECT                                                                  
         DC    H'42'               NUM ENTRYS IN SET AREA                       
SETAREA  DS    42CL6                                                            
         DC    X'0000'                                                          
         SPACE 2                                                                
         DC    H'08'               NUM ENTRYS IN WORK RECORD AREA               
WRKAREA  DS    08CL80                                                           
         SPACE 2                                                                
         DC    H'120'              NUM ENTRYS IN LNKEDT STATEMENT AREA          
LNKAREA  DS    120CL80                                                          
         SPACE 2                                                                
         DC    H'372'              NUM ENTRYS IN GEN AREA = 3*LNK+12            
GENAREA  DS    372CL80                                                          
         SPACE 2                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'024DDPANACHEO05/01/02'                                      
         END                                                                    
