*          DATA SET DELMPAIR   AT LEVEL 004 AS OF 10/26/11                      
*PROCESS USING(WARN(15))                                                        
*CATALP LMPAIR                                                                  
         TITLE 'STORE PROGRAM AIRED DAYS TO DDS INTERNAL FORMAT'                
         TITLE 'TRANSLATE DDS INTERNAL PROGRAM AIRED DAYS TO STRING'            
***********************************************************************         
LMPAIR   CSECT                                                                  
*----------------------------------------------------------------------         
* THIS MODULE WILL BE INCLUDED IN THE CONVERSIONS AS WELL AS                    
* MODULES THAT WILL READ INTAB RECORDS.                                         
*----------------------------------------------------------------------         
* STORE MODE                                                                    
*        PARAMETERS 1.   BYTE  0   ON INPUT  = 0: STORE MAP                     
*                        BYTES 1-3 A(28 BYTE STRING)                            
*                                                                               
*                   2.   BYTE  0   ON OUTPUT = # OF DAYS AIRED                  
*                        BYTES 1-3 A(FULL WORD FOR OUTPUT)                      
*----------------------------------------------------------------------         
* TRANSLATE MODE                                                                
*        PARAMETERS 1.   BYTE  0   ON INPUT  = 1: TRANSLATE MAP                 
*                        BYTES 1-3 A(28 BYTE STRING OUTPUT)                     
*                                                                               
*                   2.   BYTES 1-3 ON INPUT  = A(FULL WORD FOR INPUT)           
*                        BYTES 0   ON OUTPUT = # OF DAYS AIRED                  
*                   3.   BYTES 0   ON OUTPUT = MULTIDAY? Y/N                    
*                   4.   BYTES 0   ON OUTPUT = NUMBER OF WEEKS                  
*----------------------------------------------------------------------         
*                                                                               
         NMOD1 LMPAIRWL,LMPAIR                                                  
         LR    R9,RC                                                            
         USING LMPAIRWD,R9                                                      
         L     R2,0(R1)            SET MAP STRING ADDRESS                       
         LA    R2,0(R2)            CLEAR HIGH ORDER BYTE                        
         L     R3,4(R1)            SET INTERNAL FULL WORD MAP ADDRESS           
         LA    R3,0(R3)            CLEAR HIGH ORDER BYTE                        
         CLI   0(R1),1             0: STORE  1: TRANSLATE                       
         BE    TRN                                                              
         XC    0(4,R3),0(R3)                                                    
         LA    R4,0                TO KEEP TRACK OF # OF DAYS AIRED             
         LA    R0,4                4 WEEKS                                      
ST10     LA    R6,7                7 DAYS                                       
         LA    R5,1                START TO TEST MONDAY                         
         LA    RE,0                                                             
ST20     CLI   0(R2),X'40'                                                      
         BE    *+10                                                             
         OR    RE,R5               TURN ON MAP FOR THIS DAY                     
         AHI   R4,1                ADD A DAY TO TALLY                           
         MHI   R5,2                BUMP TO TEST NEXT DAY                        
         LA    R2,1(R2)                                                         
         BCT   R6,ST20             NEXT DAY                                     
         STC   RE,0(R3)                                                         
         LA    R3,1(R3)                                                         
         BCT   R0,ST10             NEXT WEEK                                    
         STC   R4,4(R1)                                                         
         XIT1                                                                   
*                                                                               
TRN      XC    DAY,DAY                                                          
         XC    WK,WK                                                            
         XC    0(28,R2),0(R2)                                                   
         LA    R4,0                TO KEEP TRACK OF # OF DAYS AIRED             
         LA    R7,1                R7 KEEP SEQUENTIAL DAY#                      
         LA    R0,4                4 WEEKS                                      
TR30     LA    R6,7                7 DAYS                                       
         LA    R5,1                                                             
TR40     EX    R5,*+8                                                           
         B     *+8                                                              
         TM    0(R3),0                                                          
         BNO   TR50                                                             
         STC   R7,0(R2)            STORE DAY NUMBER                             
         AHI   R4,1                                                             
         LA    R2,1(R2)                                                         
*                                                                               
         MVI   8(R1),C'N'          MULTI-DAY?                                   
         ZICM  RE,DAY,1                                                         
         BZ    TR45                                                             
         CR    R6,RE                                                            
         BE    *+12                                                             
         MVI   8(R1),C'Y'          MULTI-DAY                                    
TR45     STC   R6,DAY                                                           
*                                                                               
         ZICM  RE,WK,1                                                          
         CR    R0,RE                                                            
         BE    TR50                                                             
         ZIC   RE,12(R1)                                                        
         AHI   RE,1                                                             
         STC   RE,12(R1)           NUMBER OF WEEKS                              
TR47     STC   R0,WK                                                            
*                                                                               
TR50     MHI   R5,2                                                             
         AHI   R7,1                                                             
         BCT   R6,TR40                                                          
         LA    R3,1(R3)                                                         
         BCT   R0,TR30                                                          
         STC   R4,4(R1)                                                         
         XIT1                                                                   
         LTORG                                                                  
***********************************************************************         
*                                                                               
LMPAIRWD DSECT                                                                  
DAY      DS    X                                                                
WK       DS    X                                                                
LMPAIRWL EQU   *-LMPAIRWD                                                       
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'004DELMPAIR  10/26/11'                                      
         END                                                                    
