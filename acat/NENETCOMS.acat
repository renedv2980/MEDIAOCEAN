*          DATA SET NENETCOMS  AT LEVEL 025 AS OF 05/01/02                      
*CATALP NETCOM                                                                  
         TITLE 'NETCOM - NETWORK COMMENT READING MODULE'                        
NETCOM   CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 40,**NCOM**                                                      
         USING MYD,RC                                                           
         MVC   USERRD,4(RD)                                                     
         L     R9,0(R1)                                                         
         USING NCOMBLKD,R9                                                      
         L     RA,NCBNETB                                                       
         USING NETBLOCK,RA                                                      
*                                                                               
         OC    NBDM,NBDM           IS NETBLOCK DATAMGR PRESENT                  
         BNZ   GC1                                                              
         MVC   NBDM,NCBDMGR        NO/USER MUST PASS DATAMGR                    
         OC    NBDM,NBDM                                                        
         BNZ   *+6                                                              
         DC    H'0'                                                             
         SPACE 2                                                                
* FILL KEY FROM NCOMBLKD                                                        
* REQKEYA GETS REQUESTED KEY WITH * FOR 'ALL' FIELDS (USED AS FILTER)*          
* REQKEY  WILL HAVE  0 FOR * (ALL) FIELDS                            *          
* BOTH REQKEYA AND REQKEY WILL HAVE FF IN FIELDS NOT SET             *          
*                                                                               
GC1      LA    R4,REQKEY                                                        
         USING NCOMKEY,R4                                                       
         XC    REQKEY,REQKEY                                                    
         MVI   REQKEY,X'FF'                                                     
         MVC   REQKEY+1(L'REQKEY-1),REQKEY                                      
         MVI   NCOMKTYP,X'0C'                                                   
         MVC   NCOMKAM,NCBAM                                                    
         MVC   NCOMKID(2),NCBID                                                 
         MVC   NCOMKIDB,NCBID+2                                                 
         CLC   NCBCLT,=5X'0'                                                    
         BE    *+10                                                             
         MVC   NCOMKCLT,NCBCLT                                                  
         CLC   NCBPRD,=5X'0'                                                    
         BE    *+10                                                             
         MVC   NCOMKPRD,NCBPRD                                                  
         CLC   NCBEST,=5X'0'                                                    
         BE    *+10                                                             
         MVC   NCOMKEST,NCBEST                                                  
         CLC   NCBNTWK,=5X'0'                                                   
         BE    *+10                                                             
         MVC   NCOMKNET,NCBNTWK                                                 
         CLC   NCBDPT,=5X'0'                                                    
         BE    *+10                                                             
         MVC   NCOMKDPT,NCBDPT                                                  
         CLC   NCBPKG,=5X'0'                                                    
         BE    *+10                                                             
         MVC   NCOMKPKG,NCBPKG                                                  
         MVC   REQKEYA,REQKEY                SET REQKEYA                        
         EJECT                                                                  
* SET 0 FOR *(ALL) FIELDS IN REQKEY                                             
         MVI   FILTER,0                                                         
         CLI   NCOMKID,C'*'        ID GROUP                                     
         BNE   *+6                                                              
         DC    H'0'                                                             
         CLI   NCOMKIDA,C'*'       ID SUB-GROUP                                 
         BNE   *+8                                                              
         MVI   NCOMKIDA,0                                                       
         SPACE                                                                  
         LA    R3,NCOMKEND-NCOMKCLT    TEST IF ANY ALL FIELDS                   
         LA    R2,NCOMKCLT                                                      
GC2      CLI   0(R2),C'*'                                                       
         BNE   GC2A                                                             
         MVI   0(R2),0             YES/SET * TO ZERO                            
         MVI   FILTER,C'Y'             SET FILTER FLAG                          
GC2A     LA    R2,1(R2)                                                         
         BCT   R3,GC2                                                           
         DROP  R4                                                               
         MVC   KEY(20),REQKEY      ** SET KEY **                                
         B     GC3                                                              
         EJECT                                                                  
         SPACE                                                                  
*******************************************                                     
*     COMMENT PRINT CODES                 *                                     
* NN1=TOP OF PAGE COMMENT                 *                                     
* NN2=BOTTOM OF PAGE COMMENT              *                                     
* NN3=FIRST PAGE ONLY-TOP COMMENT         *                                     
* NN4=FIRST PAGE ONLY-BOTTOM COMMENT      *                                     
* NN5=LAST PAGE ONLY COMMENT (BOTTOM)     *                                     
*                                         *                                     
*******************************************                                     
         SPACE 2                                                                
*                                                                               
GC3      DS    0H                                                               
         LA    R4,KEY                                                           
         USING NCOMKEY,R4                                                       
         CLI   NCOMKIDB,0                                                       
         BNE   GC10                                                             
         DC    H'0'                                                             
GC10     MVC   REQKEY+5(1),NCOMKIDB   SET COM PRINT ID                          
         B     GC15                                                             
         SPACE                                                                  
*                                                                               
EXIT     XIT1                                                                   
         EJECT                                                                  
************************************                                            
* DECISION ON DEFAULT              *                                            
***********************************                                             
         SPACE                                                                  
GC15     DS    0H                                                               
         CLI   NCBKDFL,C'Y'                                                     
         BE    DEC30               DEF=Y                                        
         B     DEC20               DEF=0                                        
**************************************************                              
         SPACE                                                                  
***************************************                                         
* GETS REQUESTED REQ ONLY                                                       
* NO DEFAULT                                                                    
***************************************                                         
DEC20    DS    0H                 DEF=0                                         
         BAS   RE,HIGH                                                          
         CLC   KEY(2),KEYSAVE                                                   
         BNE   DEC25X                                                           
         CLC   KEY(20),KEYSAVE                                                  
         BNE   DEC24                                                            
DEC22    BAS   RE,GETREC                                                        
         BE    *+6                                                              
         DC    H'0'                                                             
         BAS   RE,GOHOOK                                                        
DEC24    CLI   FILTER,C'Y'         IF FILTER=Y/COULD BE                         
         BNE   DEC25X              MULTIPLE RECS                                
         BAS   RE,DFLRTNB                                                       
         BNZ   DEC22                                                            
DEC25X   B     EXIT                                                             
         EJECT                                                                  
**************************************************                              
*  THIS ROUTINE WILL GET DEFAULT REC IF NO MATCH *                              
*                                                *                              
*  IF ID=NNN IT WILL GIVE CLOSEST REC            *                              
*  IT ID=N*N IT WILL GIVE CLOSEST REC IN EACH * (SUB-GROUP)                     
*  IF ONE OF THE KEY FIELDS=* IT WILL READ HI    *                              
*     AND THEN GO TO SEQ                         *                              
*                                                                               
**********************************************                                  
         SPACE                                                                  
DEC30    DS    0H                                                               
         LA    R4,KEY                                                           
         USING NCOMKEY,R4                                                       
         BAS   RE,HIGH                                                          
         CLC   KEY(20),KEYSAVE                                                  
         BE    DEC39                                                            
         B     DEC35                                                            
DEC32    BAS   RE,SEQ                                                           
DEC35    CLC   KEY(2),REQKEY       AGY/MED                                      
         BNE   DEC3X                                                            
         SPACE                                                                  
         CLI   REQKEYA+2,C'*'      ID COMMENT GROUP                             
         BE    *+14                                                             
         CLC   KEY+2(1),REQKEY+2                                                
         BNE   DEC3X                                                            
         SPACE                                                                  
         CLI   REQKEYA+3,C'*'       ID COMMENT SUB GROUP                        
         BE    DEC36                                                            
         CLC   KEY+3(1),REQKEY+3                                                
         BNE   DEC40                                                            
         SPACE                                                                  
DEC36    CLC   KEY+5(1),REQKEY+5      ID COMMENT TYPE (TOP/BOT)                 
         BE    DEC37                                                            
         BH    DEC40                                                            
         MVC   KEY+5(NCOMKEND-NCOMKIDB),REQKEY+5                                
         B     DEC30                                                            
         SPACE                                                                  
DEC37    BAS   RE,DFLRTNC     CHK REST OF KEY=REQKEY                            
*                             CC=1 IF KEY OK/ELSE RETURNS NXT KEY               
         BNZ   DEC39                                                            
         CLI   SEQFLG,C'Y'                                                      
         BE    DEC32                                                            
         B     DEC30                                                            
         SPACE                                                                  
DEC39    MVC   KEYSV,KEY           SAVE KEY FOR SUBSEQUENT READ                 
         BAS   RE,GETREC                                                        
         BE    *+6                                                              
         DC    H'0'                                                             
         BAS   RE,GOHOOK           PASS REC TO CALLING MODULE                   
         MVC   KEY,KEYSV           RESTORE KEY AFTER HOOK                       
         CLI   FILTER,C'Y'         IF ONE OF KEY FIELDS=*(ALL)                  
         BNE   DEC40               THEN SET SEQFLG                              
         MVI   SEQFLG,C'Y'                                                      
         BAS   RE,HIGH                                                          
         B     DEC32                                                            
DEC40    CLI   KEY+3,X'FF'         TEST END OF SUB-GROUPS                       
         BE    DEC3X                                                            
         ZIC   R1,KEY+3            BUMP TO NEXT SUB-GROUP                       
         LA    R1,1(R1)                                                         
         STC   R1,KEY+3                                                         
         MVC   KEY+5(NCOMKEND-NCOMKIDB),REQKEY+5                                
         B     DEC30                                                            
         SPACE                                                                  
DEC3X    B     EXIT                                                             
         EJECT                                                                  
         EJECT                                                                  
***********************************************                                 
* THIS ROUTINE DOES NOT GIVE DEFAULT KEY      *                                 
*                                             *                                 
* IT IS FOR GETTING A SPECIFIC REC            *                                 
* OR FOR A FILTER.                            *                                 
*                                             *                                 
* INPUT  REQKEY                               *                                 
* OUTPUT KEY                                  *                                 
*                                             *                                 
*  CONDITION CODE=0 IF NO KEY FOUND           *                                 
***********************************************                                 
         SPACE                                                                  
DFLRTNB  NTR1                                                                   
         LA    R4,KEY                                                           
         USING NCOMKEY,R4                                                       
         SPACE                                                                  
DB10     CLC   KEY(2),REQKEY       AGY/MED                                      
         BNE   DBX                                                              
         SPACE                                                                  
         CLI   REQKEYA+2,C'*'      IS IT ALL FILTER                             
         BE    DB10A                                                            
         CLC   NCOMKID,REQKEY+2     ID COM GROUP                                
         BNE   DBX                                                              
         SPACE                                                                  
DB10A    CLI   NCOMKIDA,C'*'       SUB-GROUP ID                                 
         BE    DB10B                                                            
         CLC   NCOMKIDA,KEY+3                                                   
         BE    DB10B                                                            
         CLI   REQKEYA+2,C'*'      TEST COM GROUP=*                             
         BNE   DBX                 IF NOT/EXIT                                  
         SPACE                                                                  
DB10B    CLC   NCOMKIDB,REQKEYA+5   ID COM PRINT(TOP/BOT)                       
         BE    DB10C                                                            
         CLI   REQKEYA+3,C'*'       IF NOT EQUAL                                
         BE    DB10C                TEST ID COM/SUB COM FOR *(ALL)              
         CLI   REQKEYA+2,C'*'       IF NOT/ EXIT                                
         BNE   DBX                                                              
         SPACE                                                                  
DB10C    DS    0H                                                               
         CLI   REQKEYA+6,C'*'       IS IT ALL FILTER                            
         BE    DB13                YES/SKIP CHK                                 
         CLC   NCOMKCLT,REQKEY+6   CLIENT                                       
         BNE   DBREC                                                            
         SPACE                                                                  
DB13     CLI   REQKEYA+8,C'*'       IS IT ALL FILTER                            
         BE    DB16                YES/SKIP CHK                                 
         CLC   NCOMKPRD,REQKEY+8   PRODUCT                                      
         BNE   DBREC                                                            
         SPACE                                                                  
DB16     CLI   REQKEYA+9,C'*'       IS IT ALL FILTER                            
         BE    DB22                YES/SKIP CHK                                 
         CLC   NCOMKEST,REQKEY+9   ESTIMATE                                     
         BNE   DBREC                                                            
         SPACE                                                                  
DB22     CLI   REQKEYA+10,C'*'       IS IT ALL FILTER                           
         BE    DB25                YES/SKIP CHK                                 
         CLC   NCOMKNET,REQKEY+10   NETWORK                                     
         BNE   DBREC                                                            
         SPACE                                                                  
DB25     CLI   REQKEYA+14,C'*'       IS IT ALL FILTER                           
         BE    DB28                 YES/SKIP CHK                                
         CLC   NCOMKDPT,REQKEY+14   DAYPART                                     
         BNE   DBREC                                                            
         SPACE                                                                  
DB28     CLI   REQKEYA+15,C'*'       IS IT ALL FILTER                           
         BE    GOTITB               YES/SKIP CHK                                
         CLC   NCOMKPKG,REQKEY+15   PACKAGE                                     
         BE    GOTITB                                                           
         CLI   NCOMKPKG,X'FF'                                                   
         BE    GOTITB                                                           
         SPACE                                                                  
*                                                                               
DBREC    DS    0H                                                               
         BAS   RE,SEQ                                                           
         BZ    DBX                                                              
         CLC   KEY,KEYSAVE                                                      
         BE    GOTITB                                                           
         B     DB10                                                             
         SPACE                                                                  
*                                                                               
GOTITB   DS    0H                                                               
         LA    R1,1                                                             
         B     *+6                                                              
DBX      SR    R1,R1               CC=ZERO/MEANS NO KEY FOUND                   
         LTR   R1,R1                                                            
         B     EXIT                                                             
         DROP  R4                                                               
         EJECT                                                                  
********************************************                                    
* THIS ROUTINE DOES NOT READ ANY KEYS/RECS *                                    
* IT MERELY CHK KEY AGAINST REQUESTED KEY  *                                    
* AND RETURNS THE CLOSEST KEY              *                                    
* I.E. GETS THE DEFAULT KEY                *                                    
*                                          *                                    
********************************************                                    
DFLRTNC  NTR1                                                                   
         SPACE                                                                  
         LA    R4,KEY                                                           
         USING NCOMKEY,R4                                                       
DC10     DS    0H                                                               
         CLI   REQKEYA+6,C'*'       IS IT ALL FILTER                            
         BE    DC13                YES/SKIP CHK                                 
         CLC   NCOMKCLT,REQKEY+6   CLIENT                                       
         BE    DC13                                                             
         CLC   NCOMKCLT,=20X'FF'                                                
         BE    DC13                                                             
*        CLI   FILTER,C'Y'                                                      
*        BE    DCREC                                                            
         CLC   NCOMKCLT,REQKEY+6                                                
         BNL   DC12                                                             
         MVC   NCOMKCLT,REQKEY+6                                                
         B     *+10                                                             
DC12     MVC   NCOMKCLT,=20X'FF'                                                
         MVC   NCOMKPRD(NCOMKEND-NCOMKPRD),REQKEY+8                             
         B     DCREC                                                            
         SPACE                                                                  
DC13     CLI   REQKEYA+8,C'*'       IS IT ALL FILTER                            
         BE    DC16                YES/SKIP CHK                                 
         CLC   NCOMKPRD,REQKEY+8   PRODUCT                                      
         BE    DC16                                                             
         CLC   NCOMKPRD,=20X'FF'                                                
         BE    DC16                                                             
*        CLI   FILTER,C'Y'                                                      
*        BE    DCREC                                                            
         CLC   NCOMKPRD,REQKEY+8                                                
         BNL   DC14                                                             
         MVC   NCOMKPRD,REQKEY+8                                                
         B     *+10                                                             
DC14     MVC   NCOMKPRD,=20X'FF'                                                
         MVC   NCOMKEST(NCOMKEND-NCOMKEST),REQKEY+9                             
         B     DCREC                                                            
         SPACE                                                                  
DC16     CLI   REQKEYA+9,C'*'       IS IT ALL FILTER                            
         BE    DC22                YES/SKIP CHK                                 
         CLC   NCOMKEST,REQKEY+9   ESTIMATE                                     
         BE    DC22                                                             
         CLC   NCOMKEST,=20X'FF'                                                
         BE    DC22                                                             
*        CLI   FILTER,C'Y'                                                      
*        BE    DCREC                                                            
         CLC   NCOMKEST,REQKEY+9                                                
         BNL   DC18                                                             
         MVC   NCOMKEST,REQKEY+9                                                
         B     *+10                                                             
DC18     MVC   NCOMKEST,=20X'FF'                                                
         MVC   NCOMKNET(NCOMKEND-NCOMKNET),REQKEY+10                            
         B     DCREC                                                            
         SPACE                                                                  
DC22     CLI   REQKEYA+10,C'*'       IS IT ALL FILTER                           
         BE    DC25                YES/SKIP CHK                                 
         CLC   NCOMKNET,REQKEY+10   NETWORK                                     
         BE    DC25                                                             
         CLC   NCOMKNET,=20X'FF'                                                
         BE    DC25                                                             
*        CLI   FILTER,C'Y'                                                      
*        BE    DCREC                                                            
         CLC   NCOMKNET,REQKEY+10                                               
         BNL   DC24                                                             
         MVC   NCOMKNET,REQKEY+10                                               
         B     *+10                                                             
DC24     MVC   NCOMKNET,=20X'FF'                                                
         MVC   NCOMKDPT(NCOMKEND-NCOMKDPT),REQKEY+14                            
         B     DCREC                                                            
         SPACE                                                                  
DC25     CLI   REQKEYA+14,C'*'       IS IT ALL FILTER                           
         BE    DC28                 YES/SKIP CHK                                
         CLC   NCOMKDPT,REQKEY+14   DAYPART                                     
         BE    DC28                                                             
         CLC   NCOMKDPT,=20X'FF'                                                
         BE    DC28                                                             
*        CLI   FILTER,C'Y'                                                      
*        BE    DCREC                                                            
         CLC   NCOMKDPT,REQKEY+14                                               
         BNL   DC27                                                             
         MVC   NCOMKDPT,REQKEY+14                                               
         B     *+10                                                             
DC27     MVC   NCOMKDPT,=20X'FF'                                                
         MVC   NCOMKPKG(NCOMKEND-NCOMKPKG),REQKEY+15                            
         B     DCREC                                                            
         SPACE                                                                  
DC28     CLI   REQKEYA+15,C'*'       IS IT ALL FILTER                           
         BE    DC30                 YES/SKIP CHK                                
         CLC   NCOMKPKG,REQKEY+15   PACKAGE                                     
         BE    DC30                                                             
         CLC   NCOMKPKG,=20X'FF'                                                
         BE    DC30                                                             
         CLC   NCOMKPKG,REQKEY+15                                               
         BNL   DC29                                                             
         MVC   NCOMKPKG,REQKEY+15                                               
         B     DCREC                                                            
DC29     MVI   NCOMKPKG,X'FF'                                                   
         B     DCREC                                                            
DC30     LA    R1,1                GOTIT SET CC=1                               
         B     *+6                                                              
         SPACE                                                                  
DCREC    SR    R1,R1               NEXT KEY SET CC=0                            
         LTR   R1,R1                                                            
         B     EXIT                                                             
         DROP  R4                                                               
         EJECT                                                                  
*              DATAMGR INTERFACE                                                
         SPACE 3                                                                
HIGH     NTR1                                                                   
         MVC   KEYSAVE,KEY                                                      
         MVC   COMMAND,=CL8'DMRDHI'          HANDLE COMMANDS                    
         B     DIRALL                                                           
         SPACE 1                                                                
SEQ      NTR1                                                                   
         MVC   KEYSAVE,KEY                                                      
         MVC   COMMAND,=CL8'DMRSEQ'                                             
         B     DIRALL                                                           
         SPACE 1                                                                
READ     NTR1                                                                   
         MVC   COMMAND,=CL8'DMREAD'                                             
         SPACE 1                                                                
DIRALL   MVC   FILE(8),=C'UNTDIR  '         DIRECTORIES                         
         LA    R2,WORK                                                          
         GOTO1 NBDM,DMCB,COMMAND,FILE,KEY,(R2),0                                
         BAS   RE,DMCHECK                                                       
         MVC   KEY,0(R2)                                                        
         B     YES                                                              
         SPACE 1                                                                
GETREC   NTR1                                                                   
         LA    R3,KEY+21                                                        
         MVC   FILE(8),=C'UNTFILE '     FILE                                    
         MVC   NBDTADSP,=H'27'                                                  
         SPACE 1                                                                
GETREC2  L     R2,NCBAIO                                                        
         GOTO1 NBDM,DMCB,(X'00',=C'GETREC'),FILE,(R3),(R2),DMWORK               
         BAS   RE,DMCHECK                                                       
         B     XIT                                                              
         SPACE 1                                                                
DMCHECK  CLI   8(R1),0                                                          
         BER   RE                                                               
         TM    8(R1),X'90'                                                      
         BM    NO                                                               
         DC    H'0'                                                             
         SPACE 1                                                                
         GETEL (R6),NBDTADSP,ELCODE                                             
         SPACE 1                                                                
NO       SR    R1,R1                                                            
         B     *+8                                                              
         SPACE 1                                                                
YES      LA    R1,1                                                             
         LTR   R1,R1                                                            
         SPACE 1                                                                
XIT      XIT1                                                                   
         EJECT                                                                  
         SPACE 1                                                                
GOHOOK   NTR1                                                                   
         L     RF,NCBAHOOK                                                      
         L     RE,USERRD           USERS RD                                     
         LM    R0,RC,20(RE)        RESTORE USERS R0-RC                          
         BASR  RE,RF               RF CONTAINED A(HOOK ROUTINE)                 
         XIT1                                                                   
         SPACE 1                                                                
RELO     DS    A                                                                
         EJECT                                                                  
*              DSECTS ETC                                                       
         SPACE 3                                                                
*                                                                               
MYD      DSECT                                                                  
DUB      DS    D                                                                
WORK     DS    CL64                                                             
PARAS    DS    0F                                                               
DMCB     DS    6F                                                               
USERRD   DS    F                                                                
KEY      DS    CL27                                                             
KEYSAVE  DS    CL27                                                             
KEYSV    DS    CL27                                                             
REQKEY   DS    CL27                                                             
REQKEYA  DS    CL27                                                             
ELCODE   DS    CL1                                                              
FILTER   DS    CL1                                                              
SEQFLG   DS    CL1                                                              
COMMAND  DS    CL8                                                              
FILE     DS    CL8                                                              
DMWORK   DS    CL96                                                             
         SPACE 3                                                                
         EJECT                                                                  
       ++INCLUDE NETBLOCKD                                                      
         EJECT                                                                  
       ++INCLUDE NECOMBLOK                                                      
         EJECT                                                                  
       ++INCLUDE NEGENCOM                                                       
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'025NENETCOMS 05/01/02'                                      
         END                                                                    
