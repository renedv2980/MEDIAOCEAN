*          DATA SET DDNUMTOLET AT LEVEL 005 AS OF 05/01/02                      
*CATALP NUMTOLET                                                                
         SPACE 1                                                                
*PARM1   XL1   LANGUAGE CODE (LOW ORDER FOUR BITS)                              
*              X'80' INPUT IS NUMBER (ELSE ASSUMED MONEY)                       
*              X'40' USE EURO INSTEAD OF LOCAL CURRENCY                         
*        AL3   A(INPUT FULL WORD)                                               
*                                                                               
*PARM2   XL1   C'P'=INPUT IS PL8                                                
*        AL3   A(OUTPUT)                                                        
*                                                                               
*RETURN LENGTH OF OUTPUT STRING IN PARM1                                        
*                                                                               
         TITLE 'DDNUMTOLET - CONVERT NUMBERS TO LETTERS STRING'                 
         PRINT NOGEN                                                            
NUMTOLET CSECT                                                                  
         NMOD1 NMDEND-NMDSTT,**NMLT**                                           
         USING NMLTD,RC                                                         
*                                                                               
         MVI   MONEY,C'Y'          SET IF MONEY OR NUMBER                       
         TM    0(R1),X'80'                                                      
         BZ    *+8                                                              
         MVI   MONEY,C'N'                                                       
         MVI   EURO,C'N'           SET TO USE EURO CURRENCY                     
         TM    0(R1),X'40'                                                      
         BZ    *+8                                                              
         MVI   EURO,C'Y'                                                        
*                                                                               
         MVC   LANG,0(R1)          SAVE PASSED LANGUAGE BYTE                    
         NI    LANG,X'0F'                                                       
         CLI   LANG,0              TEST IF VALID LANGUAGE BYTE                  
         BE    *+12                                                             
         CLI   LANG,7                                                           
         BNH   NL00                                                             
         MVC   LANG(1),DEFLANG     SET DEFAULT LANGUAGE                         
*                                                                               
NL00     XC    LANGMOD(2),LANGMOD  CALC MOD FOR TABLE OFFSETS                   
         CLI   LANG,USA            IF UK/US MOD=0                               
         BNH   NL0                                                              
         SR    R2,R2                                                            
         IC    R2,LANG                                                          
         BCTR  R2,0                                                             
         BCTR  R2,0                ELSE MOD=(LANG-2)                            
         STC   R2,LANGMOD+1                                                     
         EJECT                                                                  
*              SPLIT INPUT INTO CHUNKS                                          
         SPACE 3                                                                
NL0      EQU   *                                                                
         ZAP   PCURFRAC,=P'0'                                                   
         LM    R2,R3,0(R1)                                                      
         CLI   4(R1),C'P'          IS INPUT PACKED?                             
         BE    NL1                 YES                                          
         L     R4,0(R2)            NO, SO PACK IT                               
         CVD   R4,DUB                                                           
         B     *+10                                                             
NL1      ZAP   DUB,0(8,R2)                                                      
         LR    R2,R3               USE R2 AS OUTPUT POINTER                     
         CP    DUB,=P'0'           INPUT = 0 ?                                  
         BNH   EXIT                YES, EXIT                                    
         ZAP   PCURUNIT,DUB        NO, STORE INPUT                              
         CLI   MONEY,C'Y'          TEST IF MONEY                                
         BNE   NL2                 NO                                           
         DP    DUB,=P'100'         YES SPLIT INTO UNITS                         
         ZAP   PCURUNIT,DUB(6)     POSITION DECIMAL POINT                       
         ZAP   PCURFRAC,DUB+6(2)   SPLIT INPUT                                  
         SPACE 2                                                                
NL2      SR    R4,R4                                                            
         CVB   R5,PCURFRAC         CONVERT INPUT TO BIN                         
         ST    R5,CURFRAC                                                       
         CVB   R5,PCURUNIT         DITTO                                        
         ST    R5,CURUNIT                                                       
         D     R4,=F'1000'         DIVIDE                                       
         ST    R4,UNITS            REMAINDER = UNITS                            
         SR    R4,R4                                                            
         D     R4,=F'1000'         DIVIDE                                       
         ST    R4,THOUSAND         REMAINDER = THOUSANDS                        
         ST    R5,MILLIONS         QUOTIENT = MILLIONS                          
         EJECT                                                                  
*              ROUTINE FOR LESS THAN 1 UNIT                                     
         SPACE 3                                                                
NL5      EQU   *                                                                
         OC    CURUNIT,CURUNIT                                                  
         BNZ   NL10                                                             
         MVC   INTER,CURFRAC                                                    
         BAS   RE,GETUNITS                                                      
         L     R2,INTER                                                         
         LA    R5,1                1 = PENCE, ETC                               
         BAS   RE,GETCURR                                                       
         B     EXIT                                                             
         EJECT                                                                  
*              ROUTINE FOR OVER A MILLION                                       
         SPACE 3                                                                
NL10     OC    MILLIONS,MILLIONS                                                
         BZ    NL20                                                             
         MVC   INTER,MILLIONS                                                   
         BAS   RE,GETUNITS                                                      
         L     R2,INTER                                                         
         CLI   LANG,GERMAN                                                      
         BE    NL15                                                             
         SR    R5,R5               GET 'MILLIONS' WORD                          
         BAS   RE,GETMAGN                                                       
         CLI   LANG,FRENCH                                                      
         BE    NL20                                                             
         OC    THOUSAND,THOUSAND   CHECK WHETHER AN 'AND' IS NEEDED             
         BNZ   NL20                                                             
         CLC   UNITS,=F'100'                                                    
         BNL   NL20                                                             
         OC    UNITS,UNITS                                                      
         BZ    NL20                                                             
         MVC   0(4,R2),=C'AND '                                                 
         LA    R2,4(R2)                                                         
         B     NL20                                                             
         SPACE 2                                                                
NL15     EQU   *                   GERMAN ONLY                                  
         L     R5,MILLIONS         SET UP EINMOD PARAMS                         
         MVI   EINCHAR,C'E'                                                     
         BAS   RE,EINMOD           MODIFY NO OF MILLIONS IF IT ENDS 01          
NL16     EQU   *                                                                
         SR    R5,R5                                                            
         BAS   RE,GETMAGN                                                       
         CLC   MILLIONS,=F'1'      > 1 MILLION ?                                
         BNH   NL20                BRANCH IF NOT                                
         BCTR  R2,0                                                             
         MVC   0(3,R2),=C'EN '     CHANGE 'MILLION' TO 'MILLIONEN'              
         LA    R2,3(R2)                                                         
         EJECT                                                                  
*              ROUTINE TO HANDLE THE THOUSANDS                                  
         SPACE 3                                                                
NL20     OC    THOUSAND,THOUSAND                                                
         BZ    NL30                                                             
         CLI   LANG,FRENCH                                                      
         BNE   NL22                                                             
         CLC   THOUSAND,=F'1'      IF ONE THOUSAND,                             
         BE    NL23                DON'T BOTHER WITH 'UN'                       
NL22     EQU   *                                                                
         MVC   INTER,THOUSAND                                                   
         BAS   RE,GETUNITS                                                      
         L     R2,INTER                                                         
         CLI   LANG,FRENCH         IF FRENCH                                    
         BNE   NL23                                                             
         BCTR  R2,0                                                             
         BCTR  R2,0                                                             
         CLC   0(2,R2),=C'TS'      PLURAL HUNDREDS ?                            
         BNE   NL22A                                                            
         MVI   1(R2),C' '          REPLACE 'S' WITH A SPACE                     
         LA    R2,2(R2)                                                         
         B     NL23                                                             
NL22A    EQU   *                                                                
         LA    R2,3(R2)            INSERT A SPACE BEFORE 'MILLE'                
NL23     EQU   *                                                                
         LA    R5,1                GET 'THOUSAND' WORD                          
         BAS   RE,GETMAGN                                                       
         CLC   UNITS,=F'100'                                                    
         BNL   NL30                                                             
         OC    UNITS,UNITS                                                      
         BZ    NL30                                                             
         CLI   LANG,GERMAN                                                      
         BE    NL25                                                             
         CLI   LANG,FRENCH                                                      
         BE    NL30                                                             
         MVC   0(4,R2),=C'AND '                                                 
         LA    R2,4(R2)                                                         
         B     NL30                                                             
NL25     EQU   *                                                                
         MVC   0(3,R2),=C'UND'                                                  
         LA    R2,3(R2)                                                         
         EJECT                                                                  
*              HANDLE UNITS AND FRACTIONS                                       
         SPACE 3                                                                
NL30     OC    UNITS,UNITS                                                      
         BZ    NL32                                                             
         MVC   INTER,UNITS                                                      
         BAS   RE,GETUNITS                                                      
         L     R2,INTER                                                         
         SPACE 2                                                                
NL32     EQU   *                                                                
         CLI   LANG,GERMAN         IF GERMAN...                                 
         BNE   NL33                                                             
         L     R5,UNITS            SET UP EINMOD PARAMS                         
         MVI   EINCHAR,C'E'                                                     
         CLI   MONEY,C'Y'          TEST IF MONEY                                
         BE    *+8                 YES                                          
         MVI   EINCHAR,C'S'                                                     
         BAS   RE,EINMOD           MODIFY NUM IF IT ENDS '01'                   
         SPACE 2                                                                
NL33     EQU   *                   REST OF THE WORLD                            
         CLI   MONEY,C'Y'          EXIT IF NOT MONEY                            
         BNE   EXIT                                                             
         SR    R5,R5                                                            
         BAS   RE,GETCURR                                                       
         SPACE 2                                                                
NL34     EQU   *                                                                
         CLI   LANG,GERMAN                                                      
         BE    NL36                PLURAL IF NOT GERMAN                         
         CLC   CURUNIT,=F'1'                                                    
         BE    NL36                                                             
         MVI   0(R2),C'S'          ADD THE PLURAL IF CURUNITS                   
         LA    R2,1(R2)            ARE GREATER THAN ONE                         
         SPACE 2                                                                
NL36     OC    CURFRAC,CURFRAC     ANY LOOSE CHANGE ?                           
         BZ    EXIT                NO, FINISH                                   
         L     R4,CURFRAC                                                       
         CVD   R4,DUB                                                           
         MVI   0(R2),C' '                                                       
         UNPK  1(2,R2),DUB         UNPACK IT INTO OUTPUT LINE                   
         OI    2(R2),X'F0'                                                      
         LA    R2,3(R2)                                                         
         SPACE 2                                                                
EXIT     SR    R2,R3                                                            
         ST    R2,0(R1)                                                         
         XMOD1 1                                                                
         EJECT                                                                  
*              CONTROL NUMBERS UNDER 1000                                       
         SPACE 3                                                                
GETUNITS NTR                                                                    
         XC    R5SAVE,R5SAVE       CLEAR SAVE WORD                              
         L     R4,INTER                                                         
         SRDA  R4,32                                                            
         D     R4,=F'100'                                                       
         LTR   R5,R5                                                            
         BZ    NL40                                                             
         CLI   LANG,FRENCH                                                      
         BE    NL37                                                             
         BAS   RE,GETDIGIT                                                      
         B     NL38                                                             
NL37     EQU   *                                                                
         ST    R5,R5SAVE           SAVE 00'S VALUE                              
         CH    R5,=H'1'            IF ONE HUNDRED                               
         BE    NL38                DON'T BOTHER WITH 'UN'                       
         BAS   RE,GETDIGIT                                                      
         LA    R2,1(R2)            INSERT A SPACE                               
NL38     EQU   *                                                                
         LA    R5,2                GET 'HUNDRED' WORD                           
         BAS   RE,GETMAGN                                                       
         LTR   R4,R4               ANYTHING LEFT ?                              
         BZ    NL52                NO                                           
         XC    R5SAVE,R5SAVE       CLEAR 00'S SAVE WORD                         
         CLI   LANG,USA                                                         
         BH    NL40                                                             
         MVC   0(5,R2),=C' AND '   ENGLISH ONLY                                 
         LA    R2,5(R2)                                                         
         SPACE 2                                                                
NL40     EQU   *                                                                
         ST    R4,R4SAVE           KEEP UNSPLIT VERSION                         
         SRDA  R4,32               TWENTY TO NINETY                             
         D     R4,=F'10'                                                        
         CH    R5,=H'1'            TENS FIGURE = 1 ?                            
         BNE   *+8                                                              
         LA    R4,10(R4)           ADJUST FOR 10-19                             
         CH    R5,=H'2'            TENS FIGURE > 1 ?                            
         BL    NL50                NO,ZERO                                      
         CLI   LANG,GERMAN                                                      
         BE    NL41                                                             
         ST    R5,R5SAVE                                                        
         BAS   RE,GETTENS                                                       
         CLI   LANG,FRENCH                                                      
         BE    NL43                                                             
         LTR   R4,R4               ANY DIGITS?                                  
         BZ    GETUNEND            NO                                           
         MVI   0(R2),C'-'          ADD A HYPHEN IF DIGIT FOLLOWS                
         LA    R2,1(R2)                                                         
         B     NL50                                                             
         SPACE 2                                                                
NL41     EQU   *                   *GERMAN ONLY*                                
         LTR   R4,R4                                                            
         BZ    NL42                NO DIGITS                                    
         ST    R5,R5SAVE           SAVE TENS FIGURE                             
         LR    R5,R4                                                            
         BAS   RE,GETDIGIT         GET DIGIT FIRST                              
         MVC   0(3,R2),=C'UND'                                                  
         LA    R2,3(R2)                                                         
         L     R5,R5SAVE           RESTORE TENS FIGURE                          
NL42     EQU   *                                                                
         BAS   RE,GETTENS          NOW GET TENS                                 
         B     GETUNEND                                                         
         SPACE 2                                                                
NL43     EQU   *                   *FRENCH ONLY*                                
         CLC   R4SAVE,=F'80'       80 - 99 ?                                    
         BL    NL45                NO                                           
         MVC   0(5,R2),=C'VINGT'                                                
         LA    R2,5(R2)                                                         
         L     R5,R4SAVE           GET 'UNSPLIT' NO.                            
         SH    R5,=H'80'           REDUCE TO 0 - 19                             
         BNZ   NL44                IF > 0, GET THE DIGIT                        
         MVI   0(R2),C'S'          NO DIGIT                                     
         LA    R2,1(R2)                                                         
         B     GETUNEND                                                         
NL44     EQU   *                                                                
         MVI   0(R2),C'-'          BEFORE DIGIT                                 
         LA    R2,1(R2)                                                         
         BAS   RE,GETDIGIT                                                      
         B     GETUNEND                                                         
NL45     EQU   *                                                                
         CLC   R4SAVE,=F'70'       IF 70, STILL NEEDS 'DIGIT'                   
         BE    NL46                                                             
         LTR   R4,R4                                                            
         BZ    GETUNEND            NO DIGIT                                     
         C     R4,=F'1'            DIGIT = 1 ?                                  
         BNE   NL46                                                             
         MVC   0(4,R2),=C' ET '    SEPARATOR                                    
         LA    R2,4(R2)                                                         
         B     NL47                                                             
NL46     EQU   *                                                                
         MVI   0(R2),C'-'          SEPARATOR                                    
         LA    R2,1(R2)                                                         
NL47     EQU   *                                                                
         CLC   R4SAVE,=F'70'                                                    
         BL    NL50                                                             
         LA    R4,10(R4)           FORCE 10 -19                                 
         SPACE 2                                                                
NL50     LR    R5,R4               DOWN TO THE DIGIT                            
         BAS   RE,GETDIGIT                                                      
         B     GETUNEND                                                         
NL52     EQU   *                   ADD FRENCH PLURAL                            
         CLI   LANG,FRENCH                                                      
         BNE   GETUNEND                                                         
         BCTR  R2,0                LOSE A TRAILING SPACE                        
         CLI   MONEY,C'Y'          TEST IF MONEY                                
         BE    GETUNEND            YES, NO PLURAL                               
         CLC   R5SAVE,=F'1'        HUNDREDS = 1 ?                               
         BNH   GETUNEND            YES, NO PLURAL                               
         MVI   0(R2),C'S'                                                       
         LA    R2,1(R2)                                                         
         SPACE 2                                                                
GETUNEND ST    R2,INTER                                                         
         XIT                                                                    
         EJECT                                                                  
*              ROUTINE CONVERTS R5 TO ONE TO NINETEEN                           
         SPACE 3                                                                
GETDIGIT BCTR  R5,R0                                                            
         LA    R6,209              LENGTH OF TABLE GROUP (19 * 11)              
         MH    R6,LANGMOD          CALC OFFSET FROM START                       
         MH    R5,=H'11'                                                        
         AR    R5,R6               ADD OFFSET IN                                
         LA    R5,DIGITAB(R5)      POINT TO ENTRY REQUIRED                      
         B     MOVEIT                                                           
         SPACE 2                                                                
DIGITAB  DC    AL1(3),CL10'ONE'                                                 
         DC    AL1(3),CL10'TWO'                                                 
         DC    AL1(5),CL10'THREE'                                               
         DC    AL1(4),CL10'FOUR'                                                
         DC    AL1(4),CL10'FIVE'                                                
         DC    AL1(3),CL10'SIX'                                                 
         DC    AL1(5),CL10'SEVEN'                                               
         DC    AL1(5),CL10'EIGHT'                                               
         DC    AL1(4),CL10'NINE'                                                
         DC    AL1(3),CL10'TEN'                                                 
         DC    AL1(6),CL10'ELEVEN'                                              
         DC    AL1(6),CL10'TWELVE'                                              
         DC    AL1(8),CL10'THIRTEEN'                                            
         DC    AL1(8),CL10'FOURTEEN'                                            
         DC    AL1(7),CL10'FIFTEEN'                                             
         DC    AL1(7),CL10'SIXTEEN'                                             
         DC    AL1(9),CL10'SEVENTEEN'                                           
         DC    AL1(8),CL10'EIGHTEEN'                                            
         DC    AL1(8),CL10'NINETEEN'                                            
GERDTAB  DC    AL1(3),CL10'EIN'                                                 
         DC    AL1(4),CL10'ZWEI'                                                
         DC    AL1(4),CL10'DREI'                                                
         DC    AL1(4),CL10'VIER'                                                
         DC    AL1(5),CL10'FUENF'                                               
         DC    AL1(5),CL10'SECHS'                                               
         DC    AL1(6),CL10'SIEBEN'                                              
         DC    AL1(4),CL10'ACHT'                                                
         DC    AL1(4),CL10'NEUN'                                                
         DC    AL1(4),CL10'ZEHN'                                                
         DC    AL1(3),CL10'ELF'                                                 
         DC    AL1(6),CL10'ZWOELF'                                              
         DC    AL1(8),CL10'DREIZEHN'                                            
         DC    AL1(8),CL10'VIERZEHN'                                            
         DC    AL1(9),CL10'FUENFZEHN'                                           
         DC    AL1(8),CL10'SECHZEHN'                                            
         DC    AL1(8),CL10'SIEBZEHN'                                            
         DC    AL1(8),CL10'ACHTZEHN'                                            
         DC    AL1(8),CL10'NEUNZEHN'                                            
FREDTAB  DC    AL1(2),CL10'UN'                                                  
         DC    AL1(4),CL10'DEUX'                                                
         DC    AL1(5),CL10'TROIS'                                               
         DC    AL1(6),CL10'QUATRE'                                              
         DC    AL1(4),CL10'CINQ'                                                
         DC    AL1(3),CL10'SIX'                                                 
         DC    AL1(4),CL10'SEPT'                                                
         DC    AL1(4),CL10'HUIT'                                                
         DC    AL1(4),CL10'NEUF'                                                
         DC    AL1(3),CL10'DIX'                                                 
         DC    AL1(4),CL10'ONZE'                                                
         DC    AL1(5),CL10'DOUZE'                                               
         DC    AL1(6),CL10'TREIZE'                                              
         DC    AL1(8),CL10'QUATORZE'                                            
         DC    AL1(6),CL10'QUINZE'                                              
         DC    AL1(5),CL10'SEIZE'                                               
         DC    AL1(8),CL10'DIX-SEPT'                                            
         DC    AL1(8),CL10'DIX-HUIT'                                            
         DC    AL1(8),CL10'DIX-NEUF'                                            
         DS    0F                  FORCE ALIGNMENT                              
         EJECT                                                                  
*              ROUTINE CONVERTS R5 TO TWENTY - NINETY                           
         SPACE 3                                                                
GETTENS  EQU   *                                                                
         LA    R6,80               LENGTH OF TABLE GROUP (8 * 10)               
         MH    R6,LANGMOD          CALC OFFSET                                  
         SH    R5,=H'2'                                                         
         MH    R5,=H'10'                                                        
         AR    R5,R6                                                            
         LA    R5,TENTAB(R5)                                                    
         B     MOVEIT                                                           
         SPACE 2                                                                
TENTAB   DC    AL1(6),CL9'TWENTY'                                               
         DC    AL1(6),CL9'THIRTY'                                               
         DC    AL1(5),CL9'FORTY'                                                
         DC    AL1(5),CL9'FIFTY'                                                
         DC    AL1(5),CL9'SIXTY'                                                
         DC    AL1(7),CL9'SEVENTY'                                              
         DC    AL1(6),CL9'EIGHTY'                                               
         DC    AL1(6),CL9'NINETY'                                               
*                                                                               
GERTTAB  DC    AL1(7),CL9'ZWANZIG'                                              
         DC    AL1(8),CL9'DREISSIG'                                             
         DC    AL1(7),CL9'VIERZIG'                                              
         DC    AL1(8),CL9'FUENFZIG'                                             
         DC    AL1(7),CL9'SECHZIG'                                              
         DC    AL1(7),CL9'SIEBZIG'                                              
         DC    AL1(7),CL9'ACHTZIG'                                              
         DC    AL1(7),CL9'NEUNZIG'                                              
*                                                                               
FRETTAB  DC    AL1(5),CL9'VINGT'                                                
         DC    AL1(6),CL9'TRENTE'                                               
         DC    AL1(8),CL9'QUARANTE'                                             
         DC    AL1(9),CL9'CINQUANTE'                                            
         DC    AL1(8),CL9'SOIXANTE'                                             
         DC    AL1(8),CL9'SOIXANTE'                                             
         DC    AL1(7),CL9'QUATRE-'                                              
         DC    AL1(7),CL9'QUATRE-'                                              
         SPACE 2                                                                
GETMAGN  EQU   *                                                                
         LA    R6,33               LENGTH OF TABLE GROUP                        
         MH    R6,LANGMOD                                                       
         MH    R5,=H'11'                                                        
         AR    R5,R6                                                            
         LA    R5,MAGTAB(R5)                                                    
         B     MOVEIT                                                           
         SPACE 2                                                                
MAGTAB   DC    AL1(9),CL10' MILLION '                                           
         DC    AL1(10),CL10' THOUSAND '                                         
         DC    AL1(8),CL10' HUNDRED'                                            
GERMTAB  DC    AL1(9),CL10' MILLION '                                           
         DC    AL1(7),CL10'TAUSEND'                                             
         DC    AL1(7),CL10'HUNDERT'                                             
FREMTAB  DC    AL1(9),CL10' MILLION '                                           
         DC    AL1(6),CL10'MILLE '                                              
         DC    AL1(5),CL10'CENT '                                               
         DS    0F                  FORCE ALIGNMENT                              
         SPACE 2                                                                
*              ROUTINE CONVERTS R5 TO CURRENCY NAME                             
         SPACE 2                                                                
GETCURR  EQU   *                                                                
         SR    R6,R6                                                            
         IC    R6,LANG             CALC CURRENCY REQUIRED                       
         BCTR  R6,0                                                             
         MH    R6,=H'20'           TABLE GROUP LENGTH                           
         MH    R5,=H'10'           ELEMENT LENGTH                               
         AR    R5,R6                                                            
         LA    R5,CURRTAB(R5)      POINT TO ELEMENT                             
         CLI   EURO,C'Y'                                                        
         BNE   MOVEIT                                                           
         LA    R5,EUROTAB                                                       
         B     MOVEIT                                                           
         SPACE 2                                                                
EUROTAB  DC    AL1(5),CL9' EURO'                                                
         DC    AL1(6),CL9' CENTS'                                               
CURRTAB  DC    AL1(6),CL9' POUND'                                               
         DC    AL1(6),CL9' PENCE'                                               
         DC    AL1(7),CL9' DOLLAR'                                              
         DC    AL1(6),CL9' CENTS'                                               
         DC    AL1(5),CL9' MARK'                                                
         DC    AL1(8),CL9' PFENNIG'                                             
*&&UK*&& DC    AL1(6),CL9' FRANC'                                               
*&&UK*&& DC    AL1(9),CL9' CENTIMES'                                            
*&&US*&& DC    AL1(7),CL9' DOLLAR'                                              
*&&US*&& DC    AL1(6),CL9' CENTS'                                               
         SPACE 2                                                                
MOVEIT   EQU   *                                                                
         SR    R7,R7                                                            
         IC    R7,0(R5)            GET TABLE ELEMENT LENGTH                     
         BCTR  R7,0                                                             
         EX    R7,MOVEX            COPY IT TO OUTPUT                            
         LA    R2,1(R7,R2)         POINT BEYOND IT                              
         BR    RE                  RETURN                                       
         SPACE 2                                                                
MOVEX    MVC   0(0,R2),1(R5)                                                    
         SPACE 2                                                                
EINMOD   EQU   *                   SORT OUT ******* GERMAN GENDERS              
         CVD   R5,DUB              CONVERT SUPPLIED NUM                         
         NI    DUB+6,X'0F'         SWITCH OFF HUNDREDS                          
         CP    DUB+6(2),=P'01'     NUM ENDS 01 ?                                
         BNER  RE                  NO                                           
         MVC   0(1,R2),EINCHAR     APPEND SUPPLIED CHAR                         
         LA    R2,1(R2)                                                         
         BR    RE                                                               
         EJECT                                                                  
DEFLANG  DS    0C                                                               
*&&UK*&& DC    AL1(1)                                                           
*&&US*&& DC    AL1(2)                                                           
*                                                                               
         LTORG                                                                  
         EJECT                                                                  
NMLTD    DSECT                                                                  
NMDSTT   DS    0H                                                               
DUB      DS    D                                                                
PCURUNIT DS    D                                                                
PCURFRAC DS    D                                                                
MILLIONS DS    F                                                                
THOUSAND DS    F                                                                
UNITS    DS    F                                                                
CURUNIT  DS    F                                                                
CURFRAC  DS    F                                                                
INTER    DS    F                                                                
R4SAVE   DS    F                                                                
R5SAVE   DS    F                                                                
LANGMOD  DS    H                                                                
*                                                                               
LANG     DS    XL1                                                              
UK       EQU   X'01'                                                            
USA      EQU   X'02'                                                            
GERMAN   EQU   X'03'                                                            
FRENCH   EQU   X'04'                                                            
*                                                                               
MONEY    DS    CL1                                                              
EINCHAR  DS    CL1                                                              
EURO     DS    CL1                                                              
NMDEND   DS    0H                                                               
         SPACE 2                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'005DDNUMTOLET05/01/02'                                      
         END                                                                    
