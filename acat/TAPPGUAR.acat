*          DATA SET TAPPGUAR   AT LEVEL 065 AS OF 01/25/17                      
*CATALP TAPPGUAR                                                                
*                                                                               
*        PARAMETERS                                                             
*        ----------                                                             
*                                                                               
*        P1 = GENCON W/S                                                        
*                                                                               
*        P2 BYTE  0   X'80' = DRAFT - DON'T WRITE - MAINTAIN BUFFER             
*                     X'40' = TRACE - PRINT TRACE OF WRITTEN RECORDS            
*                     X'20' = UPDATE INVOICE RECORD                             
*                     X'10' = CLEAR INVOICE TOTALS FROM BUFFER                  
*                     X'08' = ADD INVOICE TOTALS BACK TO BUFFER BAL.            
*                     X'18' = INITIALIZE (CLEAR BUFFER)                         
*                     X'04' = INVOICE COMMENT IN CHECK RECORD                   
*                     X'02' = DON'T WRITE GUARANTEE RECORDS                     
*                     X'01' = WRITE ALL GUARANTEE RECORDS IN BUFFER             
*                                                                               
*        P2 BYTES 1-3 A(CHECK RECORD)                                           
*                                                                               
*        P3 BYTE  0   X'80' = IGNORE GUARANTEE CONTRACTS                        
*                     X'40' = READ FOR UPDATE                                   
*                                                                               
*        P3 BYTES 1-3 A(INVOICE RECORD)                                         
*                                                                               
*        P4 = A(SYSTEM COMMON ROUTINES)                                         
*                                                                               
*                                                                               
*        RETURNS                                                                
*        -------                                                                
*                                                                               
*        P1 BYTE 0 = X'FE' - SOME RECORDS WERE UPDATED  (RETURNS CC EQ)         
*                    X'FF' - NO CHANGES MADE            (RETURNS CC EQ)         
*               ERROR CODE - ERROR FOUND                (RETURNS CC NE)         
*                                                                               
         TITLE 'TAPPGUAR - APPLY GUARANTEE CREDITS'                             
TAPPGUAR CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 WORKLNQ,TAPPGUAR,R6,R5,CLEAR=YES                                 
         LR    RA,RC               RA=LOCAL WORKING STORAGE                     
         USING WORKD,RA                                                         
         L     RC,0(R1)            RC=CONTROLLER STORAGE AREA                   
         USING GEND,RC                                                          
         L     R9,ASTARTSV         R9=GLOBAL SAVED VALUES                       
         USING SYSWORKD,R9                                                      
         L     R8,ASPOOLD          R8=SPOOL STORAGE AREA                        
         USING SPOOLD,R8                                                        
         L     R7,12(R1)           R7=COMMON FACILITIES FOR SYSTEM              
         USING SYSCOMMD,R7                                                      
         SPACE 1                                                                
         ST    R1,USERPARM         SAVE A(PARMS)                                
         MVC   USERP1(12),0(R1)                                                 
         MVC   USERST,4(R1)             STATUS                                  
         MVC   USERST2,8(R1)            STATUS 2                                
         MVC   USERIO,AIO               USER'S I/O AREA                         
         MVC   USERKEY,KEY                     AND KEY                          
         SPACE 1                                                                
         LA    R1,GUARIO           INITIALIZE                                   
         ST    R1,AGUARIO                                                       
         MVI   MYSTAT,0                                                         
         LA    R1,GUARIO                                                        
         AHI   R1,L'GUARIO                                                      
         ST    R1,AGCONIO                                                       
         XC    AGCONENT,AGCONENT                                                
         SPACE 1                                                                
         L     R1,ATWA             R1=A(TWA)                                    
         USING TWAD,R1                                                          
         MVC   MYWRITE,TWAWRITE    SAVE WRITE= VALUE                            
         EJECT                                                                  
*              PROCESSING CONTROL                                               
         SPACE 2                                                                
         MVI   MYERROR,X'FF'       INIT RETURN CODE TO NO CHANGES MADE          
         SPACE 1                                                                
         TM    USERST,X'18'        TEST FOR BUFFER UPDATE CALL                  
         BZ    *+12                                                             
         BAS   RE,BUFFUP           GO DO IT                                     
         B     APPGZ               ALL DONE                                     
         SPACE 1                                                                
         TM    USERST,X'01'        TEST WRITE ALL GUARANTEES IN BUFFER          
         BZ    *+16                                                             
         BAS   RE,FLUSH            GO DO IT                                     
         BAS   RE,FLUSHGC          ALSO WRITE ALL GUARANTEE CONTRACTS           
         B     APPGZ               ALL DONE                                     
         SPACE 1                                                                
         MVC   AIO,USERP2          AIO=A(CHECK RECORD)                          
         GOTO1 EXTRACT             EXTRACT GLOBAL VALUES FROM CHECK REC         
         SPACE 1                                                                
         BAS   RE,CHECK            EXTRACT DATA FROM CHECK RECORD               
         BNE   APPGX                                                            
         SPACE 1                                                                
         BAS   RE,GUAR             EXTRACT DATA FROM GUARANTEE RECORD           
         BNE   APPGX                                                            
         SPACE 1                                                                
         BAS   RE,GCON             EXTRACT DATA FROM GRT CONTRACT REC           
         BNE   APPGX                                                            
         SPACE 1                                                                
         BAS   RE,APPLY            APPLY GUARANTEE AGAINST CHECK                
         SPACE 1                                                                
         BAS   RE,APPLYGC          APPLY GUARANTEE CONTRACT AGAINST CHK         
         SPACE 1                                                                
         CLI   MYERROR,X'FE'       IF NO ERRORS HAVE BEEN ENCOUNTERED           
         BL    *+8                                                              
         BAS   RE,UPDATE           UPDATE GUARANTEE RECORD                      
         SPACE 1                                                                
         BAS   RE,UPDGCON          UPDATE GUARANTEE CONTRACT RECORD             
         SPACE 1                                                                
         CLI   MYERROR,X'FE'       IF NO ERRORS HAVE BEEN ENCOUNTERED           
         BL    *+8                                                              
         BAS   RE,TRACK            BUILD/ADD GUARANTEE TRACKING RECORD          
         SPACE 1                                                                
         BAS   RE,TRKGCON          BUILD/ADD GCON TRACKING RECORD               
         SPACE 1                                                                
         TM    USERST,X'20'        IF EXPLICITLY REQUESTED                      
         BZ    APPG10                                                           
         BAS   RE,INVOICE          UPDATE INVOICE RECORD                        
         BNE   APPGX                                                            
         SPACE 1                                                                
APPG10   CLI   MYERROR,TAINWGDU                                                 
         BE    APPG11                                                           
         CLI   MYERROR,TAINWGCN                                                 
         BE    APPG11                                                           
         MVI   MYERROR,X'FE'       SET SOME RECORDS WERE UPDATED                
         SPACE 1                                                                
APPG11   TM    MYSTAT,MYSDUPCY     IF DUPLICATE CYCLE FOUND                     
         BZ    *+8                                                              
         MVI   MYERROR,TAINWGDU    RETURN SPECIAL WARNING                       
         SPACE 1                                                                
APPGX    XC    TGINV,HEXFFS        ASSUME INV S/B COMPLEMENTED IN GLB           
         SPACE 1                                                                
APPGZ    MVC   AIO,USERIO          RESTORE USER'S I/O AREA                      
         MVC   KEY,USERKEY                        AND KEY                       
         L     R1,USERPARM                                                      
         MVC   0(1,R1),MYERROR     RETURN ERROR CODE                            
         SPACE 1                                                                
         CLI   MYERROR,X'FE'       RETURN CC                                    
         BNL   YES                                                              
         B     NO                                                               
         EJECT                                                                  
*              ROUTINE TO UPDATE BUFFER FOR THIS INVOICE                        
         SPACE 1                                                                
BUFFUP   NTR1                                                                   
         TM    USERST,X'18'        BOTH BITS SET MEAN CLEAR BUFFER              
         BNO   BUFF1                                                            
         L     RF,=AL4(NGUARS*GUARLNQ)                                          
         XCEFL GUARTAB                                                          
         XC    GUARCNT,GUARCNT     ALSO CLEAR COUNT                             
         B     XIT                 THAT'S IT                                    
         SPACE 1                                                                
BUFF1    LA    RF,GUARTAB          RF=A(TABLE)                                  
         USING GUARD,RF                                                         
         LA    R0,NGUARS           R0=MAX N'ENTRIES IN TABLE                    
         SPACE 1                                                                
BUFF2    OC    GUARSSN,GUARSSN     TEST END OF TABLE                            
         BZ    XIT                                                              
         TM    USERST,X'08'        TEST ADD BACK TO BALANCE                     
         BZ    BUFF4                                                            
         L     R1,GUARBAL          R1=OVERALL BALANCE                           
         L     R0,GUARINV          R0=TOTAL THIS INVOICE                        
         SPACE 1                                                                
         TM    GUARGUA,GUARDBAL    IF BALANCE DESCENDING                        
         BZ    *+6                                                              
         LCR   R0,R0               COMPLEMENT INVOICE TOTAL                     
         SPACE 1                                                                
         AR    R1,R0               ADD BACK TO BALANCE                          
         ST    R1,GUARBAL                                                       
         SPACE 1                                                                
BUFF4    XC    GUARINV,GUARINV     CLEAR THIS INVOICE TOTAL                     
         SPACE 1                                                                
         LA    RF,GUARNEXT         TRY NEXT ENTRY                               
         BCT   R0,BUFF2                                                         
         SPACE 1                                                                
         B     XIT                                                              
         DROP  RF                                                               
         EJECT                                                                  
*              ROUTINE TO UPDATE ALL GUARANTEES IN BUFFER                       
         SPACE 1                                                                
FLUSH    NTR1                                                                   
         MVC   AIO,AGUARIO         SET I/O AREA TO USE                          
         SPACE 1                                                                
         LA    R4,GUARTAB          R4=A(TABLE)                                  
         USING GUARD,R4                                                         
         LH    R0,GUARCNT          R0=N'ENTRIES IN TABLE                        
         SPACE 1                                                                
         CH    R0,=H'2'            TEST SOMETHING WORTH SORTING                 
         BNH   FLU10                                                            
         GOTO1 XSORT,DMCB,(R4),(R0),GUARLNQ,GUARSLNQ,GUARSKEY-GUARD             
         SPACE 1                                                                
FLU10    OC    GUARSSN,GUARSSN     TEST END OF TABLE                            
         BZ    FLUX                                                             
         L     R1,GUARSSN          SET GLOBAL STORAGE FOR RECVAL CALL           
         CVD   R1,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  TGSSN,DUB           SOCIAL SECURITY NUMBER                       
         SPACE 1                                                                
         MVC   DUB(2),GUARGUA                                                   
         NI    DUB,ALL-GUARDBAL                                                 
         LH    R1,DUB                                                           
         CVD   R1,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  TGGUA,DUB           GUARANTEE CODE                               
         XC    TGGUA,HEXFFS        (COMPLEMENTED)                               
         SPACE 1                                                                
***      TM    USERST2,X'40'       IF READING FOR UPDATE                        
***      BZ    *+8                                                              
***      MVI   RDUPDATE,C'Y'                                                    
         GOTO1 RECVAL,DMCB,TLGUCDQ,(X'30',0)  GET GUARANTEE RECORD              
         BE    *+6                                                              
         DC    H'0'                                                             
         SPACE 1                                                                
         L     R3,AIO                                                           
         MVI   ELCODE,TAGUELQ      LOOK FOR GUARANTEE DETAILS EL.               
         BAS   RE,GETEL                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
         USING TAGUD,R3                                                         
         NI    TAGUSTAT,ALL-TAGUSNO  INSURE DISALLOW CREDITTING IS OFF          
         SPACE 1                                                                
         OC    TAGUCOM,TAGUCOM     IF PRIMARY COMMERCIAL ISN'T DEFINED          
         BNZ   FLU20                                                            
         MVC   TAGUBAL,GUARBAL     SET NEW GUARANTEE BALANCE                    
******** CLI   TGUSEQU,UGRT        AND IF GUARANTEE PAYMENT                     
******** BNE   FLU50                                                            
         MVC   TAGUAMT,GUARAMT     SET NEW GUARANTEE AMOUNT                     
         B     FLU50                                                            
         SPACE 1                                                                
FLU20    MVI   ELCODE,TAGCELQ      ELSE LOOK FOR CYCLE EL. ON REC.              
         GOTO1 GETL,DMCB,(6,GUARPD)                                             
         BNE   FLU30                                                            
         L     R2,TGELEM           IF FOUND,                                    
         USING TAGCD,R2                                                         
         MVC   TAGCBAL,GUARBAL     UPDATE BALANCE THERE                         
         B     FLU40                                                            
         SPACE 1                                                                
FLU30    XC    ELEMENT,ELEMENT     ELSE THIS MUST BE A NEW GUARANTEE            
         LA    R2,ELEMENT          SO BUILD NEW ELEMENT                         
         MVI   TAGCEL,TAGCELQ      ELEMENT CODE                                 
         MVI   TAGCLEN,TAGCLNQ     ELEMENT LENGTH                               
         MVC   TAGCPD,GUARPD       PERIOD                                       
         MVC   TAGCAMT,GUARAMT     INITIAL AMOUNT                               
         MVC   TAGCBAL,GUARBAL     REMAINING BALANCE                            
         GOTO1 ADDELEM             ADD THE ELEMENT                              
         SPACE 1                                                                
FLU40    CLC   GUARNKEY,GUARSKEY   IF KEY OF NEXT TABLE ENTRY THE SAME          
         BNE   FLU50                                                            
         LA    R4,GUARNEXT         THEN BUMP TO IT                              
         BCT   R0,FLU20            AND PROCESS NOW                              
         DC    H'0'                IMPOSSIBLE IF MATCHES PREVIOUS               
         SPACE 1                                                                
FLU50    GOTO1 PUTREC              WRITE BACK THE RECORD                        
         SPACE 1                                                                
         GOTO1 MYTRACE,DMCB,=C'UPDATED GUARANTEE',AIO                           
         SPACE 1                                                                
         LA    R4,GUARNEXT         LOOK FOR ANOTHER GUARANTEE IN TABLE          
         BCT   R0,FLU10                                                         
         SPACE 1                                                                
FLUX     B     XIT                                                              
         DROP  R2,R4                                                            
         EJECT                                                                  
*              ROUTINE TO UPDATE ALL GUARANTEES CONTRACTS IN BUFFER             
         SPACE 1                                                                
FLUSHGC  NTR1                                                                   
         USING GCOND,R4                                                         
         LA    R4,GCONTAB          R4=A(TABLE)                                  
         LH    R0,GCONCNT          R0=N'ENTRIES IN TABLE                        
         ZICM  R0,GCONCNT,2                                                     
         BZ    XIT                                                              
                                                                                
         MVC   AIO,AGCONIO         SET I/O AREA TO USE                          
                                                                                
         USING TLGCD,R3                                                         
FGC10    OC    GCONSSN,GCONSSN     TEST END OF TABLE                            
         BZ    FGCX                                                             
         LA    R3,KEY              READ FOR GUARANTEE CONTRACT KEY              
         XC    KEY,KEY                                                          
         MVI   TLGCCD,TLGCCDQ                                                   
         MVC   TLGCSSN,GCONSSN                                                  
         MVC   TLGCGCNT,GCONGCO                                                 
         GOTO1 HIGH                                                             
         CLC   TLGCKEY,KEYSAVE                                                  
         BNE   FGC20                                                            
         DROP  R3                                                               
                                                                                
         TM    USERST2,X'40'       IF READING FOR UPDATE                        
         BZ    *+8                                                              
         MVI   RDUPDATE,C'Y'       READ FOR GUARANTEE CONTRACT RECORD           
         GOTO1 GETREC                                                           
                                                                                
         GOTO1 MYTRACE,DMCB,=C'READ GCON',AIO                                   
                                                                                
         USING TAGCD,R3                                                         
         L     R3,AIO                                                           
         MVI   ELCODE,TAGCELQ      LOOK FOR GUARANTEE CONTRACT YEAR             
         BAS   RE,GETEL            ELEMENT                                      
         BNE   FGC20                                                            
         OC    TAGCBAL,TAGCBAL                                                  
         BZ    FGC20                                                            
         MVC   TAGCBAL,GCONBAL                                                  
         DROP  R3                                                               
                                                                                
         GOTO1 PUTREC              WRITE BACK THE RECORD                        
         GOTO1 MYTRACE,DMCB,=C'UPDATE GCON',AIO                                 
                                                                                
FGC20    LA    R4,GCONNEXT         LOOK FOR ANOTHER GUARANTEE IN TABLE          
         BCT   R0,FGC10                                                         
         DROP  R4                                                               
                                                                                
FGCX     MVC   AIO,AGUARIO         SET I/O AREA TO USE                          
         B     XIT                                                              
         EJECT                                                                  
*              ROUTINE TO EXTRACT CHECK RECORD DETAILS                          
         SPACE 1                                                                
CHECK    NTR1                                                                   
         USING TLCKD,R3                                                         
         L     R3,AIO                                                           
         MVC   SVCKAGY,TLCKAGY     SAVE AGENCY                                  
         MVC   SVCKINV,TLCKINV     AND INVOICE NUMBER                           
         DROP  R3                                                               
         SPACE 1                                                                
         MVI   ELCODE,TACAELQ      LOOK FOR CAST DETAILS EL.                    
         BAS   RE,GETEL                                                         
         BNE   CHKERR                                                           
         SPACE 1                                                                
         ST    R3,ATACAEL          SAVE ITS ADDRESS                             
         USING TACAD,R3                                                         
         OC    TACAGUA,TACAGUA     TEST FOR GUARANTEE CODE                      
         BZ    CHKNO                                                            
         MVC   TGGUA,TACAGUA       SAVE IT IN GLOBAL STORAGE                    
         XC    TGGUA,HEXFFS        (IT'S COMPLEMENTED IN KEY)                   
         MVC   GUACODE,TACAGUA     SAVE UN-COMPLEMENTED CODE AS WELL            
         SPACE 1                                                                
         L     R3,AIO                                                           
         MVI   ELCODE,TAPDELQ      LOOK FOR PAYMENT DETAILS EL.                 
         BAS   RE,GETEL                                                         
         BNE   CHKERR                                                           
         ST    R3,ATAPDEL          SAVE A(ELEMENT)                              
         USING TAPDD,R3                                                         
         SPACE 1                                                                
         OC    TAPDGUAR,TAPDGUAR   IF NO ORIGINAL CRED. TAKEN FROM CHK          
         BNZ   CHECK10                                                          
         L     R3,USERP3           AND THIS INVOICE IS THE CANCELL'ER'          
         LTR   R3,R3                                                            
         BZ    CHECK10                                                          
         CLI   0(R3),TLINCDQ                                                    
         BNE   CHECK10                                                          
         MVI   ELCODE,TAINELQ                                                   
         BAS   RE,GETEL                                                         
         BNE   CHECK10                                                          
         USING TAIND,R3                                                         
         TM    TAINSTAT,TAINSCIN   NO GUARANTEE CREDITS TO REVERSE              
         BO    CHKNO                                                            
         SPACE 1                                                                
CHECK10  L     R3,ATAPDEL          RESTORE R3=A(PAYMENT DETAILS EL.)            
         USING TAPDD,R3                                                         
         GOTO1 USEVAL,DMCB,TAPDUSE,TAPDTYPE  LOOK UP USE TYPE                   
         BNE   CHKNO                                                            
         CLI   TGUSEQU,UGRT        NEED TO TRACK GUARANTEE PAYMENTS             
         BE    CHKX                                                             
         TM    TGUSSTAT,NOGUARCR   USE TYPE DOESN'T TAKE GUAR. CREDITS          
         BO    CHKNO                                                            
         TM    TAPDSTAT,TAPDSGUP   TEST GUAR. RECORD ALREADY UPDATED            
         BO    CHKNO                                                            
         TM    TAPDOPT1,TAPDONGC   TEST DON'T TAKE CREDITS THIS PAYMENT         
         BO    CHKNO                                                            
CHKX     B     YES                 RETURN CC EQ                                 
         SPACE 2                                                                
CHKERR   MVI   MYERROR,TAINEGCK    SET BAD CHECK RECORD                         
CHKNO    B     NO                  RETURN CC NE                                 
         EJECT                                                                  
*              ROUTINE TO EXTRACT GUARANTEE RECORD DETAILS                      
         SPACE 1                                                                
GUAR     NTR1                                                                   
         L     R3,ATACAEL          R3=A(CAST DETAILS EL.)                       
         USING TACAD,R3                                                         
         L     R4,ATAPDEL          R4=A(PAYMENT DETAILS EL.)                    
         USING TAPDD,R4                                                         
         MVC   AIO,AGUARIO         SET I/O AREA FOR GUARANTEE RECORD            
         SPACE 1                                                                
         MVC   TGDATE,TAPDCYCS                                                  
         TM    TAPDOPT4,TAPDGRTE                                                
         JZ    *+10                                                             
         MVC   TGDATE,TAPDCYCE                                                  
         DROP  R4                                                               
         SPACE 1                                                                
***      TM    USERST2,X'40'       IF READING FOR UPDATE                        
***      BZ    *+8                                                              
***      MVI   RDUPDATE,C'Y'                                                    
         GOTO1 GUARVAL,DMCB,(X'80',TACACORP),TGDATE  READ/VALIDATE GRT          
         BNE   GUAERR                                                           
         L     R3,0(R1)            R3=A(GUARANTEE DETAILS EL.)                  
         ST    R3,ATAGUEL                                                       
         USING TAGUD,R3                                                         
         SPACE 1                                                                
         CLI   TGUSEQU,UGRT        IF WE'RE PROCESSING GUARANTEE PMT            
         BNE   *+8                                                              
         NI    TAGUSTAT,ALL-TAGUSNO  SET TO ALLOW CREDITTING                    
         SPACE 1                                                                
         OC    TAGUCOM,TAGUCOM     IF PRIMARY COMMERCIAL DEFINED                
         BZ    *+12                                                             
         BAS   RE,CYCLE            LOOK UP APPROPRIATE GUAR CYCLE EL.           
         BNE   GUAERR                                                           
         SPACE 1                                                                
         TM    USERST,X'82'        IF THIS IS DRAFT MODE OR SUPPRESSING         
         BZ    GUAX                GUARANTEE WRITES                             
         BAS   RE,GETGUAR          LOOK UP GUARANTEE IN TABLE                   
         BL    GUAERR                                                           
         BH    TBLERR                                                           
         L     RF,AGUARENT         RF=A(ENTRY FOR THIS GUARANTEE)               
         USING GUARD,RF                                                         
         MVC   TAGUBAL,GUARBAL     USE UPDATED GUARANTEE BAL FROM TABLE         
         SPACE 1                                                                
GUAX     B     YES                 RETURN CC EQ                                 
         SPACE 2                                                                
GUAERR   MVI   MYERROR,TAINEGGU    SET BAD GUARANTEE RECORD                     
         B     *+8                                                              
TBLERR   MVI   MYERROR,TAINEGTB    SET GUARANTEE TABLE FULL                     
         B     NO                  RETURN CC NE                                 
         EJECT                                                                  
*              ROUTINE TO EXTRACT GUARANTEE CONTRACT RECORD DETAILS             
         SPACE 1                                                                
GCON     NTR1                                                                   
         TM    USERST2,X'80'       IF NOT IGNORING GUARANTEE CONTRACTS          
         BO    YES                                                              
                                                                                
         USING TACAD,R4                                                         
         L     R4,ATACAEL                                                       
         CLC   TACAUN,=C'SAG'      IF PERFORMER'S UNION IS SAG                  
         BE    GCON10                                                           
         CLC   TACAUN,=C'AFT'      OR AFTRA                                     
         BNE   YES                                                              
         DROP  R4                                                               
                                                                                
         USING TAPDD,R4                                                         
GCON10   L     R4,ATAPDEL                                                       
         CLI   TGUSEQU,UGRT        AND THIS IS NOT A GRT                        
         BE    YES                                                              
         CLI   TGUSEQU,UPNH        OR PNH PAYMENT                               
         BE    YES                                                              
                                                                                
         USING TAGUD,R3                                                         
         L     R3,AGUARIO                                                       
         MVI   ELCODE,TAGUELQ                                                   
         BAS   RE,GETEL                                                         
         BNE   YES                                                              
         OC    TAGUGCNT,TAGUGCNT   AND GUARANTEE IS ATTACHED TO                 
         BZ    YES                 A GUARANTEE CONTRACT                         
                                                                                
         MVC   TGDATE,TAPDCYCS                                                  
         TM    TAPDOPT4,TAPDGRTE                                                
         BZ    *+10                                                             
         MVC   TGDATE,TAPDCYCE                                                  
         DROP  R4                                                               
                                                                                
         USING GCOND,R2                                                         
         LA    R2,GCONTAB          RF=A(TABLE)                                  
         LA    R0,NGCONS           R0=MAX N'ENTRIES IN TABLE                    
                                                                                
GCON20   OC    GCONSSN,GCONSSN     TEST END OF TABLE                            
         BZ    GCON40                                                           
                                                                                
         CLC   TGSSN,GCONSSN       IF GUARANTEE CONTRACT YEAR                   
         BNE   GCON30              ALREADY ENCOUNTERED                          
         CLC   TAGUGCNT,GCONGCO                                                 
         BNE   GCON30                                                           
         CLC   TGDATE,GCONYRS                                                   
         BL    GCON30                                                           
         CLC   TGDATE,GCONYRE                                                   
         BH    GCON30                                                           
         ST    R2,AGCONENT         SAVE A(GRT CONTRACT ENTRY IN TABLE)          
                                                                                
         TM    USERST,X'80'        IF NOT IN DRAFT MODE                         
         BO    YES                 RESTORE ORIGINAL BALANCE TO FILE             
         MVC   GCONOBAL,GCONFBAL   VALUE                                        
         B     YES                                                              
                                                                                
GCON30   LA    R2,GCONNEXT         TRY NEXT ENTRY                               
         BCT   R0,GCON20                                                        
         B     GCOERR                                                           
                                                                                
GCON40   LH    R1,GCONCNT          BUMP N'ENTRIES IN TABLE                      
         AHI   R1,1                                                             
         STH   R1,GCONCNT                                                       
                                                                                
         USING TLGCD,R4                                                         
         LA    R4,KEY                                                           
         XC    KEY,KEY             READ FOR GUARANTEE CONTRACT KEY              
         MVI   TLGCCD,TLGCCDQ                                                   
         MVC   TLGCSSN,TGSSN                                                    
         MVC   TLGCGCNT,TAGUGCNT                                                
         GOTO1 HIGH                                                             
         CLC   TLGCKEY,KEYSAVE                                                  
         BNE   GCOERR                                                           
         DROP  R3                                                               
                                                                                
         MVC   AIO,AGCONIO                                                      
         TM    USERST2,X'40'       IF READING FOR UPDATE                        
         BZ    *+8                                                              
         MVI   RDUPDATE,C'Y'                                                    
         GOTO1 GETREC              READ GUARANTEE CONTRACT RECORD               
         MVC   AIO,AGUARIO                                                      
                                                                                
         USING TAGCD,R3                                                         
         L     R3,AGCONIO                                                       
         MVI   ELCODE,TAGCELQ      FIND CONTRACT YEAR P&H LIMIT AND             
         BRAS  RE,GETEL            BALANCE                                      
         B     *+8                                                              
GCON50   BRAS  RE,NEXTEL                                                        
         BNE   GCOERR                                                           
         CLC   TGDATE,TAGCSTRT                                                  
         BL    GCON50                                                           
         CLC   TGDATE,TAGCEND                                                   
         BH    GCON50                                                           
                                                                                
         MVC   GCONSSN,TGSSN       SAVE SOCIAL SECURITY NUMBER                  
         MVC   GCONGCO,TLGCGCNT    GUARANTEE CONTRACT COEE                      
         MVC   GCONYRS,TAGCSTRT    CONTRACT YEAR START DATE                     
         MVC   GCONYRE,TAGCEND     END DATE                                     
         MVC   GCONFBAL,TAGCBAL    FILE BALANCE                                 
         MVC   GCONOBAL,TAGCBAL    ORIGINAL BALANCE                             
         MVC   GCONBAL,TAGCBAL     BALANCE                                      
         ST    R2,AGCONENT         SAVE A(GRT CONTRACT ENTRY IN TABLE)          
         DROP  R3,R4                                                            
                                                                                
         USING TLOTD,R4                                                         
         MVC   GCONSEQ,=X'FFFF'                                                 
         LA    R4,KEY             SAVE NEXT TRACKING SEQUENCE NUMBER            
         XC    KEY,KEY                                                          
         MVI   TLOTCD,TLOTCDQ                                                   
         MVC   TLOTSSN,GCONSSN                                                  
         MVC   TLOTGCNT,GCONGCO                                                 
         MVC   TLOTSTRT,GCONYRS                                                 
         OI    DMINBTS,X'08'                                                    
         GOTO1 HIGH                                                             
         NI    DMINBTS,X'FF'-X'08'                                              
         CLC   KEY(TLOTSEQ-TLOTD),KEYSAVE                                       
         BNE   YES                                                              
         ZICM  R1,TLOTSEQ,2                                                     
         SHI   R1,1                                                             
         STCM  R1,3,GCONSEQ                                                     
         B     YES                                                              
         DROP  R2,R4                                                            
                                                                                
GCOERR   MVI   MYERROR,TAINEGGC    SET BAD GUARANTEE CONTRACT RECORD            
         B     NO                  RETURN CC NE                                 
         EJECT                                                                  
*              ROUTINE LOOKS UP APPROPRIATE GUAR. CYCLE ELEMENT                 
         SPACE 1                                                                
         USING TAGUD,R3            R3=A(GUARANTEE DETAILS ELEMENT)              
         USING TAPDD,R4            R4=A(PAYMENT DETAILS ELEMENT)                
CYCLE    NTR1                                                                   
         XC    APRGCEL,APRGCEL     CLEAR A(PREV VALID GC ELEMENT)               
         LA    R2,TAPDCYCS         USE CYCLE START DATE                         
         MVC   TGDATE,TAPDCYCS                                                  
         TM    TAPDOPT4,TAPDGRTE   IF USING G=E OPTION                          
         BZ    *+10                                                             
         MVC   TGDATE,TAPDCYCE     USE CYCLE END DATE                           
         OC    0(3,R2),0(R2)                                                    
         BNZ   CYC2                                                             
         LA    R2,TGTODAY1         USE TODAY'S DATE IF NO CYCLE DATES           
         MVC   TGDATE,TGTODAY1                                                  
         SPACE 1                                                                
CYC2     L     R3,AIO                                                           
         MVI   ELCODE,TAGCELQ      LOOK UP APPROPRIATE GUAR CYCLE EL.           
         BAS   RE,GETEL                                                         
         B     *+8                                                              
CYC4     BAS   RE,NEXTEL                                                        
         BE    CYC8                                                             
         ICM   R3,15,APRGCEL       IF AN ELEMENT WAS FOUND                      
         BZ    CYC5                                                             
         TM    TAPDSTA2,TAPDSPRI   THEN IF THIS IS FIX CYC PMT ON PRI.          
         BZ    CYC4D                                                            
         TM    MYSTAT,MYSADDGC     AND JUST ADDED IT MYSELF                     
         BZ    *+12                                                             
         NI    MYSTAT,X'FF'-MYSADDGC  TURN OFF LOCAL STATUS BIT                 
         B     *+8                                                              
         OI    MYSTAT,MYSDUPCY     ELSE SET DUPLICATE CYCLE FOUND               
CYC4D    B     CYC16               OK TO CONTINUE                               
         SPACE 1                                                                
CYC5     TM    TAPDSTA2,TAPDSPRI   EL NOT FOUND - IF THIS IS PRI COMML          
         BO    CYC6                                                             
         TM    USERST,X'82'        OR IF DRAFT MODE OR NOT WRITING GUAR         
         BNZ   CYC6                                                             
         CLI   MYWRITE,C'N'        OR WRITE=NO, OK-GETGUAR WILL HANDLE          
         BNE   NO                  ELSE ERROR - RETURN CC NE                    
         SPACE 1                                                                
CYC6     XC    ELEMENT,ELEMENT     THIS IS A NEW GUARANTEE                      
         LA    R3,ELEMENT          BUILD NEW ELEMENT                            
         USING TAGCD,R3                                                         
         MVI   TAGCEL,TAGCELQ      ELEMENT CODE                                 
         MVI   TAGCLEN,TAGCLNQ     ELEMENT LENGTH                               
         MVC   TAGCPD,TAPDCYCS     PERIOD                                       
         OC    TAPDCYCS,TAPDCYCS   IF THERE ARE NO CYCLE DATES                  
         BNZ   *+16                                                             
         MVC   TAPDCYCS,0(R2)      SET START AND END DATES TO TODAY             
         MVC   TAPDCYCE,0(R2)                                                   
         L     R1,TAPDPAYI         PAYMENT AMOUNT IS GUARANTEE                  
         A     R1,TAPDPAYC                                                      
         ST    R1,TAGCAMT          INITIALIZE AMOUNT                            
         ST    R1,TAGCBAL                     BALANCE                           
         GOTO1 ADDELEM             ADD THE ELEMENT                              
         SPACE 1                                                                
         TM    USERST,X'20'        IF UPDATING INVOICE RECORDS                  
         BZ    CYC7                                                             
         GOTO1 PUTREC              PUT GUARANTEE NOW                            
         SPACE 1                                                                
CYC7     OI    MYSTAT,MYSADDGC     SET ADDED GC EL                              
         B     CYC2                NOW TRY AGAIN                                
         SPACE 1                                                                
CYC8     CLC   TGDATE,TAGCEND      MUST BEGIN ON OR BEFORE GUAR END             
         BH    CYC4                                                             
         CLC   TGDATE,TAGCSTRT     AND ON OR AFTER GUAR START                   
         BL    CYC4                                                             
         TM    TAPDSTA2,TAPDSPRI   IF PROCESSING FIXED CYCLE PMT ON             
         BZ    *+14                PRIMARY COMML                                
         CLC   0(6,R2),TAGCSTRT    SKIP IF DATES AREN'T EXACT (DON'T            
         BNE   CYC4                WANT TO CREDIT AGAINST OVERLAP CYC)          
         ST    R3,APRGCEL          SAVE A(VALID ELEMENT)                        
         B     CYC4                & TRY TO FIND THE NEXT VALID ONE             
         SPACE 1                                                                
CYC16    ST    R3,ATAGCEL          ELSE SAVE A(GUAR CYCLE ELEMENT)              
         SPACE 1                                                                
         L     RF,ATAGUEL                   RF=A(GUAR. DETAILS EL.)             
         MVC   TAGUBAL-TAGUD(4,RF),TAGCBAL  SET BAL. HERE FOR CALC.             
         B     YES                          RETURN CC EQ                        
         DROP  R3                                                               
         EJECT                                                                  
*              ROUTINE LOOKS UP GUARANTEES IN TABLE                             
         SPACE 1                                                                
         USING TAGUD,R3            R3=A(GUARANTEE DETAILS ELEMENT)              
GETGUAR  NTR1                                                                   
         L     R2,ATAGCEL          R2=A(GUAR CYCLE EL.)                         
         USING TAGCD,R2                                                         
         LA    RF,GUARTAB          RF=A(TABLE)                                  
         USING GUARD,RF                                                         
         PACK  DUB,TGSSN                                                        
         CVB   R1,DUB              R1=BINARY S/S NUMBER                         
         PACK  DUB,GUACODE                                                      
         CVB   RE,DUB              RE=BINARY GUARANTEE CODE                     
         LA    R0,GUARDBAL                                                      
         SLL   R0,8                                                             
         TM    TAGUSTAT,TAGUSDES   IF DESCENDING BALANCE                        
         BZ    *+6                                                              
         OR    RE,R0               'OR' IN INDICATOR IN CODE FOR TABLE          
         SPACE 1                                                                
         LA    R0,NGUARS           R0=MAX N'ENTRIES IN TABLE                    
         SPACE 1                                                                
GETG2    OC    GUARSSN,GUARSSN     TEST END OF TABLE                            
         BZ    GETG6                                                            
         CL    R1,GUARSSN          MATCH ON S/S NUMBER                          
         BNE   GETG4                                                            
         CLM   RE,3,GUARGUA        AND ON GUARANTEE CODE                        
         BNE   GETG4                                                            
         OC    TAGUCOM,TAGUCOM     IF PRIMARY COMMERCIAL DEFINED                
         BZ    GETGX                                                            
         LTR   R2,R2               REQUIRE A(GUAR. CYCLE ELEMENT)               
         BZ    GETGLOW                                                          
         CLC   GUARPD,TAGCPD       MATCH ON PERIOD AS WELL                      
         BE    GETGX                                                            
GETG4    LA    RF,GUARNEXT         TRY NEXT ENTRY                               
         BCT   R0,GETG2                                                         
         CR    RB,R0               RETURN CC HIGH                               
         B     XIT                 ERROR - TABLE FULL                           
         SPACE 1                                                                
GETG6    TM    TAGUSTAT,TAGUSNO    TEST WHETHER CREDITTING DISALLOWED           
         BO    GETGLOW                                                          
         SPACE 1                                                                
         OC    TAGUCOM,TAGUCOM     IF INTERNAL COMML NUMBER DEFINED             
         BZ    GETG8                                                            
         LTR   R2,R2               AND WE DON'T HAVE A(GUAR CYCLE EL.)          
         BZ    GETGLOW             ERROR - HAVEN'T SEEN PRI PAYMENT             
         SPACE 1                                                                
         TM    MYSTAT,MYSADDGC     IF ADDED GC EL FOR NON-PRI COM               
         BO    GETGLOW             THEN IF NOT IN TABLE THEN ERROR              
         SPACE 1                                                                
         MVC   GUARBAL,TAGCBAL     ELSE USE BALANCE FROM IT                     
         MVC   GUARPD,TAGCPD            SAVE PERIOD AS WELL                     
         TM    USERST,X'02'        IF NOT UPDATING GUARANTEE RECS.              
         BZ    *+10                                                             
         MVC   GUARAMT,TAGCBAL     SAVE ORIGINAL AMOUNT IN TABLE                
         B     *+10                                                             
GETG8    MVC   GUARBAL,TAGUBAL     BALANCE                                      
         ST    R1,GUARSSN          SET S/S NUMBER                               
         STH   RE,GUARGUA          GUARANTEE CODE                               
         DROP  R2                                                               
         SPACE 1                                                                
         LH    R1,GUARCNT          BUMP N'ENTRIES IN TABLE                      
         AHI   R1,1                                                             
         STH   R1,GUARCNT                                                       
         SPACE 1                                                                
GETGX    ST    RF,AGUARENT         SAVE A(GUARANTEE ENTRY IN TABLE)             
         B     YES                                                              
         SPACE 1                                                                
GETGLOW  CR    R0,RB               CREDITTING DISALLOWED                        
         B     XIT                 RETURN CC MINUS                              
         EJECT                                                                  
*              ROUTINE TO APPLY GUARANTEE AGAINST CHECK RECORD                  
         SPACE 1                                                                
APPLY    NTR1                                                                   
         L     R3,ATAGUEL          R3=A(GUARANTEE DETAILS ELEMENT)              
         USING TAGUD,R3                                                         
         L     R4,ATAPDEL          R4=A(PAYMENT DETAILS ELEMENT)                
         USING TAPDD,R4                                                         
         CLI   TGUSEQU,UGRT        GET OUT IF THIS IS GRT PAYMENT               
         BE    APPX                                                             
         TM    TAPDPST2,TAPDPCYC   OR PAYMENT TO PER CYCLE COMM'L               
         BO    APPX                                                             
         CLI   TAPDACDE,APPLGUAR   IF AT THIS POINT WE HAVE CREDIT AMT          
         BE    APPX                THEN DON'T NEED TO UPDATE PAY DTLS           
         SPACE 1                                                                
         L     R0,TAGUBAL          R0=REMAINING BALANCE                         
         TM    TAGUSTAT,TAGUSDES   IF BALANCE NOT DESCENDING                    
         BO    *+8                                                              
         S     R0,TAGUBAL          REM. BALANCE = BALANCE - INITIAL AMT         
         LR    R2,R0               SAVE IT                                      
         SPACE 1                                                                
         L     R1,TAPDPAYI                                                      
         A     R1,TAPDPAYC         R1=PAYMENT AMOUNT                            
         SPACE 1                                                                
         LTR   R1,R1               IF PAYMENT AMOUNT IS NEGATIVE                
         BM    APP6                   MAKE CREDIT AMOUNT = PAYMENT              
         TM    TAGUSTAT,TAGUSOVR   IF NOT PAYING OVERAGE                        
         BZ    APP6                   MAKE CREDIT AMOUNT = PAYMENT              
         LTR   R0,R0               IF BALANCE IS NOT POSITIVE                   
         BNP   APPX                   NOTHING TO APPLY                          
         CR    R0,R1               IF BALANCE LESS THAN PAYMENT                 
         BNH   *+6                    MAKE CREDIT AMOUNT = BALANCE              
APP6     LR    R0,R1                  ELSE MAKE CREDIT AMOUNT = PAYMENT         
         SPACE 1                                                                
         LCR   R0,R0               R0=GUARANTEE CREDITS                         
         LR    R1,R2               R1=BALANCE                                   
         BAS   RE,APPLCHK          TEST IF OK (WON'T RETURN IF NOT)             
         SPACE 1                                                                
         ST    R0,TAPDGUAR         SAVE GUARANTEE CREDITS                       
         OI    TAPDSTAT,TAPDSGOF   SET APPLIED OFFLINE                          
         SPACE 1                                                                
         LTR   R0,R0               IF THERE'S ACTUALLY A CREDIT AMOUNT          
         BZ    APP8                                                             
         CLI   TAPDACDE,APPLWSPU   IF APPLIED CODE WAS WSP UPGRADE              
         BE    APP7                                                             
         CLI   TAPDACDE,APPLCAB    OR CABLE                                     
         BNE   *+10                                                             
APP7     XC    TAPDAPPL,TAPDAPPL   CLEAR ORIG. WSP AMOUNT                       
         MVI   TAPDACDE,APPLGUAR   SET GUARANTEE CREDITS APPLIED                
         SPACE 1                                                                
APP8     TM    TAPDSTA2,TAPDSPRI   IF PROC FIXED CYCLE PMT ON PRI COM           
         BZ    APP10                                                            
         LCR   R0,R0               REVERSE SIGN                                 
         A     R0,TAPDGRS          AND ADD BACK TO GROSS                        
         ST    R0,TAPDGRS                                                       
         B     APP12               DON'T DEDUCT FROM PAYMENT AMOUNT             
         SPACE 1                                                                
APP10    LA    R1,TAPDPAYI         DETERMINE WHICH PAY AMT TO DECREASE          
         CLI   TAPDW4TY,TAW4TYIN                                                
         BE    *+16                                                             
         CLI   TAPDW4TY,TAW4TYES                                                
         BE    *+8                                                              
         LA    R1,TAPDPAYC                                                      
         A     R0,0(R1)            ADD BACK TO PAYMENT                          
         ST    R0,0(R1)            AND SAVE NEW PAYMENT AMOUNT                  
         SPACE 1                                                                
         C     R0,TAPDMDED         IF NEW PAYMENT AMOUNT < MISC DED             
         BL    APPERR              SET ERROR                                    
         SPACE 1                                                                
         CLI   TGUSEQU,UPNH        SKIP IF P&H ONLY USE                         
         BE    APP12                                                            
         TM    TAGUSTAT,TAGUSPNH   IF NOT PAYING P&H ON USE                     
         BO    *+8                                                              
         BAS   RE,PNHADJ           ADJUST P&H AMOUNT                            
         SPACE 1                                                                
APP12    GOTO1 MYTRACE,DMCB,=C'GUAR CHECK',USERP2                               
         SPACE 1                                                                
APPX     B     YES                                                              
         SPACE 2                                                                
APPERR   MVI   MYERROR,TAINEGAP    SET APPLY ERROR                              
         B     NO                  RETURN CC NE                                 
         SPACE 3                                                                
*              ROUTINE PREVENTS RESTORING TO ZERO BALANCE                       
         SPACE 1                                                                
*                                  R0=APPLIED AMOUNT, R1=BALANCE                
APPLCHK  DS    0H                                                               
         TM    TAGUSTAT,TAGUSOVR   IF PAYING OVERAGE                            
         BZR   RE                                                               
         LTR   R1,R1               AND BALANCE IS ZERO OR NEGATIVE              
         BPR   RE                                                               
         LTR   R0,R0               AND APPLIED AMOUNT IS POSITIVE               
         BNPR  RE                                                               
         MVI   MYERROR,TAINWGCN    SET WARNING - CANT RESTORE TO 0 BAL          
         B     NO                  RETURN CC NE                                 
         EJECT                                                                  
*              ROUTINE TO ADJUST P&H BASED ON NEW PAYMENT AMOUNT                
         SPACE 1                                                                
*                                  R0=NEW PAYMENT AMOUNT                        
         USING TAPDD,R4            R4=A(PAYMENT DETAILS ELEMENT)                
PNHADJ   NTR1                                                                   
         MVC   CHKSPNH,TAPDSPNH    SAVE CURRENT SUBJ. TO P&H                    
         MVC   CHKPNH,TAPDPNH      AND CURRENT P&H                              
         SPACE 1                                                                
         OC    CHKSPNH,CHKSPNH     IF NOTHING WAS SUBJ. TO P&H                  
         BZ    XIT                 THEN DONE                                    
         SPACE 1                                                                
         TM    TAPDPST1,TAPDPCRD   IF NON-CREDIT PAYMENT                        
         BO    *+8                 SET NEW PAYMENT AMOUNT IS SUBJ.              
         ST    R0,TAPDSPNH         TO P&H NOW                                   
         SPACE 1                                                                
         BAS   RE,GCONADJ          ADJUST P&H BASED ON GRT CONTRACT             
         SPACE 1                                                                
         TM    TAPDPST1,TAPDPCRD   IF CREDIT PAYMENT                            
         BZ    *+8                 SET NEW PAYMENT AMOUNT IS SUBJ.              
         ST    R0,TAPDSPNH         TO P&H NOW                                   
         SPACE 1                                                                
         BAS   RE,ADJADAMS                                                      
         B     XIT                                                              
         DROP  R4                                                               
         EJECT                                                                  
*              ROUTINE TO APPLY GUARANTEE CONTRACT AGAINST CHK RECORD           
         SPACE 1                                                                
APPLYGC  NTR1                                                                   
         USING TAPDD,R4                                                         
         L     R4,ATAPDEL                                                       
                                                                                
         TM    USERST,X'80'        IF DRAFT MODE                                
         BZ    AGC10                                                            
         TM    MYSTAT,MYSGCUPA     AND GUARATEE CONTRACT UPDATE WAS             
         BO    XIT                 NOT ALREADY ATTEMPTED                        
                                                                                
         OC    TAPDSPNH,TAPDSPNH   AND GUARANTEE HAS A SUBJECT TO               
         BZ    XIT                 P&H AMOUNT                                   
                                                                                
         MVC   CHKSPNH,TAPDSPNH    SAVE CURRENT SUBJ. TO P&H                    
         MVC   CHKPNH,TAPDPNH      AND CURRENT P&H                              
                                                                                
         BAS   RE,GCONADJ          ADJUST P&H AMOUNT                            
                                                                                
         BAS   RE,ADJADAMS         AND ADJUST AMOUNTS                           
                                                                                
         GOTO1 MYTRACE,DMCB,=C'GUAR CHECK',USERP2                               
         B     XIT                                                              
                                                                                
AGC10    TM    TAPDSTA3,TAPDSGCU   IF NOT DRAFT MODE AND WE'VE APPLIED          
         BZ    XIT                 TO GUARANTEE CONTRACT ...                    
         DROP  R4                                                               
                                                                                
         USING GCOND,R3                                                         
         ICM   R3,15,AGCONENT      R3=A(GUARANTEE CONTRACT YEAR ENTRY)          
         BZ    XIT                                                              
                                                                                
         L     R2,AIO                                                           
                                                                                
         USING TAFND,R4                                                         
         LA    R4,ELEMENT                                                       
         XC    ELEMENT,ELEMENT     ADD GUARANTEE CONTRACT CODE                  
         MVI   TAFNEL,TAFNELQ      TO THE CHECK                                 
         MVI   TAFNLEN,TAFNLNQ+6                                                
         MVI   TAFNTYPE,TAFNTGCO                                                
         MVC   FULL,GCONGCO                                                     
         XC    FULL,=4X'FF'                                                     
         L     R1,FULL                                                          
         AHI   R1,1                                                             
         EDIT  (R1),(6,TAFNNAME),0,ALIGN=RIGHT                                  
         OC    TAFNNAME(6),=C'000000'                                           
         MVC   TAFNNAME(2),=C'GC'                                               
         DROP  R3,R4                                                            
                                                                                
         MVC   AIO,USERP2                                                       
         GOTO1 ADDELEM                                                          
                                                                                
         L     R3,AIO                                                           
         MVI   ELCODE,TAPDELQ                                                   
         BAS   RE,GETEL                                                         
         BNE   *+8                                                              
         ST    R3,ATAPDEL          UPDATE A(PAYMENT DETAILS ELEMENT)            
                                                                                
         L     R3,AIO                                                           
         MVI   ELCODE,TACAELQ                                                   
         BAS   RE,GETEL                                                         
         BNE   *+8                                                              
         ST    R3,ATACAEL          UPDATE A(CAST DETAILS ELEMENT)               
                                                                                
         ST    R2,AIO                                                           
         B     XIT                                                              
         EJECT                                                                  
*              ROUTINE TO ADJUST P&H BASED ON GUARANTEE CONTRACT YEAR           
         SPACE 1                                                                
         USING TAPDD,R4            R4=A(PAYMENT DETAILS ELEMENT)                
GCONADJ  NTR1                                                                   
         TM    USERST,X'80'        IF DRAFT MODE                                
         BZ    XIT                                                              
         OI    MYSTAT,MYSGCUPA     SET GRT CONTRACT UPDATE ATTEMPTED            
                                                                                
         USING GCOND,R3                                                         
         OC    TAPDSPNH,TAPDSPNH   IF WE STILL HAVE SUBJ. TO P&H                
         BZ    NO                                                               
         ICM   R3,15,AGCONENT      AND GUARANTEE CONTRACT YEAR IS               
         BZ    NO                  DEFINED                                      
                                                                                
         TM    TAPDPST1,TAPDPCRD   IF NON-CREDIT INVOICE ...                    
         BO    GCA30                                                            
                                                                                
         OC    GCONOBAL,GCONOBAL   IF GCON CONTRACT YEAR BALANCE                
         BNZ   GCA10               WAS ORIGINALLY ZERO                          
         XC    TAPDGCON,TAPDGCON   APPLIED AMOUNT                               
         XC    TAPDSPNH,TAPDSPNH   AND SUBJECT TO P&H WILL BE ZERO              
         B     GCA50                                                            
                                                                                
GCA10    ICM   R1,15,GCONOBAL                                                   
         ICM   RE,15,TAPDSPNH      IF GCON CONTRACT YEAR BALANCE                
         CR    R1,RE               IS GREATER THAN OR EQUAL TO THE              
         BL    GCA20               SUBJECT TO P&H AMOUNT                        
         MVC   TAPDGCON,TAPDSPNH   SET APPLIED AMOUNT                           
         S     R1,TAPDSPNH         KNOCK DOWN GCON CONTRACT YEAR                
         STCM  R1,15,GCONBAL       BALANCE                                      
         B     GCA50                                                            
                                                                                
GCA20    MVC   TAPDGCON,GCONOBAL   ELSE SET APPLIED AMOUT                       
         MVC   TAPDSPNH,GCONOBAL   AND SUBJECT TO P&H EQUALS                    
         XC    GCONBAL,GCONBAL     GCON CONTRACT YEAR BALANCE                   
         B     GCA50                                                            
                                                                                
*                                  IF CREDIT INVOICE ...                        
                                                                                
GCA30    OC    GCONOBAL,GCONOBAL   IF GCON CONTRACT YEAR BALANCE                
         BNZ   GCA40               WAS ORIGINALLY ZERO                          
         XC    TAPDGCON,TAPDGCON   APPLIED AMOUNT WILL BE ZERO                  
         B     GCA50                                                            
                                                                                
GCA40    MVC   TAPDGCON,TAPDSPNH   IF BALANCE WAS NOT ORIGINALLY ZERO           
         ICM   R1,15,GCONOBAL      SET APPLIED AMOUNT                           
         S     R1,TAPDSPNH         AND BUMP UP GCON CONTRACT YEAR               
         STCM  R1,15,GCONBAL       BALANCE                                      
                                                                                
GCA50    OI    TAPDSTA3,TAPDSGCU   SET APPLIED TO GCON                          
         B     YES                                                              
         DROP  R3,R4                                                            
         EJECT                                                                  
*              ROUTINE TO ADJUST SUBJECT TO P&H AND P&H                         
         SPACE 1                                                                
         USING TAPDD,R4            R4=A(PAYMENT DETAILS ELEMENT)                
ADJADAMS NTR1                                                                   
         L     R0,TAPDSPNH                                                      
         CVD   R0,DUB                                                           
         ZAP   PL16,DUB                                                         
         MP    PL16,=P'10'         * 10 FOR ROUNDING                            
         L     R0,TAPDPNH                                                       
         CVD   R0,DUB                                                           
         MP    PL16,DUB            * OLD P&H                                    
         L     R0,CHKSPNH                                                       
         CVD   R0,DUB                                                           
         DP    PL16,DUB            / OLD SUBJ. TO P&H                           
         SRP   PL16(8),63,5        ROUNDED                                      
         CVB   R1,PL16                                                          
         ST    R1,TAPDPNH          = NEW P&H AMOUNT                             
         SPACE 1                                                                
         L     R0,CHKPNH           OLD P&H                                      
         SR    R0,R1               - NEW P&H                                    
         ST    R0,CHKPNH           = CHANGE IN P&H                              
         SPACE 1                                                                
         L     R0,CHKSPNH          OLD SUBJ. TO P&H                             
         S     R0,TAPDSPNH         - NEW SUBJ. TO P&H                           
         ST    R0,CHKSPNH          = CHANGE IN SUBJ. TO P&H                     
         B     XIT                                                              
         DROP  R4                                                               
         EJECT                                                                  
*              ROUTINE UPDATES GUARANTEE RECORD                                 
         SPACE 1                                                                
UPDATE   NTR1                                                                   
         L     R3,ATAGUEL          R3=A(GUARANTEE DETAILS ELEMENT)              
         USING TAGUD,R3                                                         
         L     R4,ATAPDEL          R4=A(PAYMENT DETAILS ELEMENT)                
         USING TAPDD,R4                                                         
         L     R0,TAPDGUAR         R0=GUARANTEE CREDITS                         
         L     R1,TAGUBAL          R1=BALANCE                                   
         BAS   RE,APPLCHK          TEST IF OK (WON'T RETURN IF NOT)             
         SPACE 1                                                                
         MVC   GTRACRD,TAPDGUAR    SAVE GUARANTEE CREDITS                       
         MVC   GTRABAL,TAGUBAL     AND BALANCE FOR TRACKING                     
         SPACE 1                                                                
         CLI   TGUSEQU,UGRT        IF THIS IS A GUARANTEE PAYMENT               
         BNE   UPD2                                                             
         TM    TAPDPST2,TAPDPITC   AND NO LONGER UPDATING GUARANTEE             
         BZ    UPD5                AMOUNT AT INSTALLMENT PAY TIME               
         CLC   TAGUINV,SVCKINV     AND THIS IS NOT THE INVOICE                  
         BNE   UPD1                THAT CREATED THE GUARANTEE                   
         OC    TAGUIAY,TAGUIAY     ORIGINALLY                                   
         BZ    UPD5                                                             
         CLC   TAGUIAY,SVCKAGY                                                  
         BE    UPD5                                                             
UPD1     L     R0,TAGUAMT                                                       
         A     R0,TAPDPAYI                                                      
         A     R0,TAPDPAYC                                                      
         ST    R0,TAGUAMT          ADJUST GUARANTEE AMOUNT                      
         L     R0,TAGUBAL                                                       
         B     UPD3                AND BALANCE                                  
         DROP  R1                                                               
         SPACE 1                                                                
UPD2     L     R0,TAGUBAL          NEW BALANCE = CURRENT BALANCE                
         TM    TAPDPST2,TAPDPCYC                                                
         BO    UPD4                                                             
         A     R0,TAPDGUAR                     - APPLIED AMOUNT                 
         OC    TAGUCOM,TAGUCOM     (IF PRIMARY COMML DEFINED DON'T              
         BNZ   *+12                 ALLOW BALANCE TO BECOME NEG.)               
         S     R0,TAPDPAYI                     - REMAINING PAY AMOUNT           
         S     R0,TAPDPAYC                                                      
         SPACE 1                                                                
         TM    TAGUSTAT,TAGUSDES   IF BALANCE NOT DESCENDING                    
         BO    UPD4                                                             
         L     R0,TAPDGUAR         NEW BALANCE = CREDIT AMOUNT                  
         LCR   R0,R0                             (COMPLEMENTED)                 
         A     R0,TAGUBAL                      + CURRENT BALANCE                
         OC    TAGUCOM,TAGUCOM     (IF PRIMARY COMML DEFINED DON'T              
         BNZ   *+12                 ALLOW BALANCE TO BECOME NEG.)               
UPD3     A     R0,TAPDPAYI                     + REMAINING PAY AMOUNT           
         A     R0,TAPDPAYC                                                      
         SPACE 1                                                                
UPD4     ST    R0,TAGUBAL          SAVE NEW BALANCE                             
         ST    R0,GTRABAL          (SAVE FOR TRACKING)                          
         SPACE 1                                                                
UPD5     TM    USERST,X'82'        IF THIS IS DRAFT MODE OR SUPPRESSING         
         BZ    UPD8                GUARANTEE WRITES                             
         L     RF,AGUARENT         POINT TO TABLE ENTRY FOR GUAR.               
         USING GUARD,RF                                                         
         MVC   GUARBAL,TAGUBAL     UPDATE BALANCE THERE                         
******** CLI   TGUSEQU,UGRT        AND IF GUARANTEE PAYMENT                     
******** BNE   *+10                                                             
         MVC   GUARAMT,TAGUAMT     UPDATE AMOUNT THERE                          
         SPACE 1                                                                
         TM    USERST,X'02'        IF SUPPRESSING GUAR. WRITES                  
         BO    UPD6                DON'T TOUCH GUARINV (IT HAS PAY AMT)         
         L     R0,GUARINV                                                       
         A     R0,TAPDGUAR         ADD TO CREDITS APPLIED TO THIS INV.          
         ST    R0,GUARINV                                                       
         SPACE 1                                                                
UPD6     TM    USERST,X'40'        TEST TRACE REQUESTED                         
         BZ    UPD7                                                             
         GOTO1 TRACE,DMCB,AGUARENT,GUARLNQ,=C'GUAR TABLE',10                    
         SPACE 1                                                                
UPD7     TM    USERST,X'80'        IF THIS ISN'T DRAFT MODE                     
         BZ    UPD10               NEED TO UPDATE PAYMENT DETAILS               
         B     UPDX                ELSE DONE                                    
         SPACE 1                                                                
UPD8     ICM   R2,15,ATAGCEL       IF WE HAVE A(GUAR CYCLE EL.)                 
         BZ    *+16                                                             
         USING TAGCD,R2                                                         
         MVC   TAGCBAL,TAGUBAL     UPDATE BALANCE THERE                         
         XC    TAGUBAL,TAGUBAL     AND CLEAR FROM GUAR. ELEMENT                 
         DROP  R2                                                               
         SPACE 1                                                                
         GOTO1 MYTRACE,DMCB,=C'GUARANTEE',AIO                                   
         SPACE 1                                                                
         GOTO1 PUTREC              WRITE BACK GUARANTEE RECORD                  
         SPACE 1                                                                
UPD10    OI    TAPDSTAT,TAPDSGUP   SET GUARANTEE RECORD UPDATED                 
         DROP  R4                                                               
         SPACE 1                                                                
UPDX     B     YES                                                              
         EJECT                                                                  
*              ROUTINE UPDATES GUARANTEE CONTRACT RECORD                        
         SPACE 1                                                                
UPDGCON  NTR1                                                                   
         USING TAPDD,R2                                                         
         L     R2,ATAPDEL                                                       
         OC    TAPDGCON,TAPDGCON                                                
         JZ    XIT                                                              
                                                                                
         USING GCOND,R4                                                         
         ICM   R4,15,AGCONENT                                                   
         BZ    XIT                                                              
         OC    GCONOBAL,GCONOBAL                                                
         JZ    XIT                                                              
                                                                                
         TM    USERST,X'80'        IF THIS IS DRAFT MODE                        
         BZ    UGC10               ORIGINAL BALANCE IS NOW THE                  
         MVC   GCONOBAL,GCONBAL    UPDATED BALANCE                              
         B     XIT                                                              
                                                                                
         USING TLGCD,R3                                                         
UGC10    LA    R3,KEY              IF THIS IS NOT DRAFT MODE                    
         XC    KEY,KEY             READ FOR GUARANTEE CONTRACT KEY              
         MVI   TLGCCD,TLGCCDQ                                                   
         MVC   TLGCSSN,GCONSSN                                                  
         MVC   TLGCGCNT,GCONGCO                                                 
         GOTO1 HIGH                                                             
         CLC   TLGCKEY,KEYSAVE                                                  
         BNE   XIT                                                              
         DROP  R3                                                               
                                                                                
         MVC   AIO,AGCONIO                                                      
         TM    USERST2,X'40'       IF READING FOR UPDATE                        
         BZ    *+8                                                              
         MVI   RDUPDATE,C'Y'       READ GUARANTEE CONTRACT RECORD               
         GOTO1 GETREC              FOR UPDATE                                   
                                                                                
         TM    USERST,X'02'        IF NOT UPDATING GUARANTEE RECS.              
         BO    UGC20                                                            
         GOTO1 MYTRACE,DMCB,=C'READ GCON',AIO                                   
                                                                                
         USING TAGCD,R3                                                         
UGC20    L     R3,AIO                                                           
         MVI   ELCODE,TAGCELQ      FIND CONTRACT YEAR ELEMENT                   
         BRAS  RE,GETEL                                                         
         B     *+8                                                              
UGC30    BRAS  RE,NEXTEL                                                        
         BNE   UGCX                                                             
         CLC   GCONYRS,TAGCSTRT                                                 
         BNE   UGC30                                                            
                                                                                
         ICM   R1,15,GCONOBAL                                                   
         S     R1,TAPDGCON         UPDATE GUARANTEE CONTRACT YEAR               
         STCM  R1,15,GCONFBAL      BALANCE                                      
         STCM  R1,15,GCONBAL                                                    
         DROP  R2,R4                                                            
                                                                                
         TM    USERST,X'02'        IF UPDATING GUARANTEE RECS.                  
         BO    UGCX                                                             
         STCM  R1,15,TAGCBAL       UPDATE BALANCE IN RECORD                     
         DROP  R3                                                               
                                                                                
         GOTO1 PUTREC              WRITE BACK GCON RECORD                       
         GOTO1 MYTRACE,DMCB,=C'UPDATE GCON',AGCONIO                             
                                                                                
UGCX     MVC   AIO,AGUARIO                                                      
         B     XIT                                                              
         EJECT                                                                  
*              ROUTINE ADDS TRACKING RECORD                                     
         SPACE 1                                                                
TRACK    NTR1                                                                   
         MVC   AIO,AGUARIO         SET A(LOCAL I/O AREA)                        
         XC    KEY,KEY             BUILD INITIAL KEY                            
         SPACE 1                                                                
         LA    R4,KEY              R4=A(KEY)                                    
         USING TLGTD,R4                                                         
         MVI   TLGTCD,TLGTCDQ      GTRACK RECORD CODE                           
         MVC   TLGTSSN,TGSSN       SOCIAL SECURITY NUMBER                       
         MVC   TLGTGUA,GUACODE     GUARANTEE CODE                               
         SPACE 1                                                                
         ICM   R2,15,ATAGCEL       IF WE HAVE A(GUAR CYCLE EL.)                 
         BZ    TRA4                                                             
         USING TAGCD,R2                                                         
         MVC   TLGTSTRT(6),TAGCSTRT  PERIOD                                     
         XC    TLGTSTRT(6),HEXFFS    (COMPLEMENTED)                             
         DROP  R2                                                               
         SPACE 1                                                                
TRA4     MVC   TGDUB(4),GTRACRD    SET GUARANTEE CREDITS                        
         MVC   TGDUB+4(4),GTRABAL      GUARANTEE BALANCE                        
         SPACE 1                                                                
         GOTO1 BLDTRK,DMCB,TLGTTRK-TLGTD,USERP2  BUILD TRACKING RECORD          
         SPACE 1                                                                
         ICM   RF,15,AGUARENT      POINT TO TABLE ENTRY FOR GUAR.               
         BZ    TRA7                (MUST BE BILLING AND WRITE=NO)               
         USING GUARD,RF                                                         
         OC    GUARTRK,GUARTRK     IF WE HAVE SAVED TRACKING NUMBER             
         BZ    TRA6                                                             
         LH    R1,GUARTRK          USE IT TO GET NEXT                           
         SH    R1,=H'1'                                                         
         STH   R1,TLGTTRK          SAVE IT IN KEY                               
         SPACE 1                                                                
TRA6     MVC   GUARTRK,TLGTTRK     SAVE LAST TRACKING NUMBER USED               
         SPACE 1                                                                
TRA7     L     R4,AIO              R4=A(GUARANTEE TRACKING RECORD)              
         MVC   TLGTKEY,KEY         MOVE KEY TO RECORD                           
         SPACE 1                                                                
         MVI   ELCODE,TACMELQ      LOOK FOR INVOICE COMMENT ON INV REC          
         MVC   AIO,USERP3                                                       
         TM    USERST,X'04'        TEST IT'S IN CHECK RECORD                    
         BZ    *+10                                                             
         MVC   AIO,USERP2                                                       
         GOTO1 GETL,DMCB,(1,=AL1(TACMTYPG))                                     
         MVC   AIO,AGUARIO         RESTORE A(TRACKING RECORD)                   
         BNE   TRA8                                                             
         L     R1,TGELEM           R1=A(ELEMENT)                                
         MVC   ELEMENT,0(R1)       MOVE TO W/S                                  
         GOTO1 ADDELEM             ADD ELEMENT TO RECORD                        
         SPACE 1                                                                
TRA8     TM    USERST,X'80'        IF NOT DRAFT MODE                            
         BO    TRAX                                                             
         GOTO1 MYTRACE,DMCB,=C'GUAR TRACKING',AIO                               
         GOTO1 ADDREC              ADD RECORD TO FILE                           
         SPACE 1                                                                
TRAX     B     XIT                                                              
         EJECT                                                                  
*              ROUTINE ADDS GRT CONTRACT TRACKING RECORD                        
         SPACE 1                                                                
TRKGCON  NTR1                                                                   
         USING TAPDD,R2                                                         
         L     R2,ATAPDEL                                                       
         TM    TAPDSTA3,TAPDSGCU   IF GUARANTEE CONTRACT HAS BEEN               
         BZ    XIT                 UPDATED                                      
                                                                                
         USING GCOND,R3                                                         
         ICM   R3,15,AGCONENT      AND GUARANTEE CONTRACT YEAR IS               
         BZ    NO                  DEFINED                                      
                                                                                
         TM    USERST,X'80'        IF THIS ISN'T DRAFT MODE                     
         BO    TGC40                                                            
                                                                                
         MVC   AIO,AGCONIO                                                      
                                                                                
         USING TLOTD,R4                                                         
         L     R4,AGCONIO          BUILD KEY IN RECORD                          
         XC    0(250,R4),0(R4)                                                  
         MVI   TLOTCD,TLOTCDQ                                                   
         MVC   TLOTSSN,GCONSSN                                                  
         MVC   TLOTGCNT,GCONGCO                                                 
         MVC   TLOTSTRT,GCONYRS                                                 
         MVC   TLOTSEQ,GCONSEQ                                                  
                                                                                
         ZICM  R1,GCONSEQ,2                                                     
         SHI   R1,1                                                             
         STCM  R1,3,GCONSEQ                                                     
                                                                                
         OC    GCONOBAL,GCONOBAL                                                
         BNZ   TGC10                                                            
         OI    TLOTSTA,TLOTSBA0                                                 
                                                                                
         USING TAGUD,R1                                                         
TGC10    TM    TAPDPST1,TAPDPCRD                                                
         BO    TGC30                                                            
         L     RE,TAPDPAYI                                                      
         A     RE,TAPDPAYC                                                      
         L     R1,ATAGUEL                                                       
         TM    TAGUSTAT,TAGUSPNH                                                
         BZ    TGC20                                                            
         S     RE,TAPDGUAR                                                      
TGC20    ICM   RF,15,TAPDGCON                                                   
         CR    RE,RF                                                            
         BE    TGC30                                                            
         OI    TLOTSTA,TLOTSPHA                                                 
         DROP  R1                                                               
                                                                                
TGC30    MVC   TLOTAGY,TGAGY                                                    
         MVC   TLOTINV,TGINV                                                    
         DROP  R4                                                               
                                                                                
         USING TAGCD,R4                                                         
         LA    R4,ELEMENT                                                       
         XC    ELEMENT,ELEMENT                                                  
         MVI   TAGCEL,TAGCELQ                                                   
         MVI   TAGCLEN,TAGCLNQ                                                  
                                                                                
         MVC   TAGCPD,TAPDCYCS                                                  
         MVC   TAGCUSE,TAPDUSE                                                  
         MVC   TAGCCOM,TAPDCOM                                                  
                                                                                
         MVC   TAGCAMT,TAPDGCON                                                 
         MVC   TAGCBAL,GCONBAL                                                  
         DROP  R2                                                               
                                                                                
         USING TACAD,R1                                                         
         L     R1,ATACAEL                                                       
         MVC   TAGCUNI,TACAUN                                                   
         MVC   TAGCGRT,TACAGUA                                                  
         XC    TAGCGRT,=4X'FF'                                                  
         DROP  R1                                                               
                                                                                
         GOTO1 ADDELEM                                                          
         DROP  R4                                                               
                                                                                
         GOTO1 ADDREC                                                           
         MVC   AIO,AGUARIO                                                      
                                                                                
         GOTO1 MYTRACE,DMCB,=C'ADD GCONTRK',AGCONIO                             
                                                                                
TGC40    MVC   GCONOBAL,GCONBAL                                                 
         B     XIT                                                              
         EJECT                                                                  
         DROP  R3                                                               
*              ROUTINE UPDATES INVOICE RECORD                                   
         SPACE 1                                                                
INVOICE  NTR1                                                                   
         CLI   MYERROR,X'FE'       EXIT IF ANY ERRORS HAVE BEEN                 
         BNL   INV1                ENCOUNTERED                                  
         CLI   MYERROR,TAINWGCN    UNLESS THAT ERROR IS CANCEL                  
         BNE   NO                  WARNING                                      
         SPACE 1                                                                
INV1     L     R3,USERP3           R3=A(INVOICE RECORD)                         
         MVI   ELCODE,TAPDELQ      LOOK FOR PAYMENT DETAILS EL.                 
         BAS   RE,GETEL                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
         LR    R4,R3                                                            
         USING TAPDD,R4            R4=A(INVOICE PAYMENT DETAILS EL.)            
         SPACE 1                                                                
         L     R2,ATAPDEL          R2=A(CHECKS PAYMENT DETAILS EL.)             
         TM    TAPDSTAT-TAPDD(R2),TAPDSGOF                                      
         BO    INV2                                                             
         TM    TAPDSTA3-TAPDD(R2),TAPDSGCU                                      
         BZ    YES                 DON'T BOTHER IF NOT APPLIED BY US            
         SPACE 1                                                                
INV2     L     R0,TAPDGCON         UPDATE GUARANTEE CONTRACT CREDITS            
         A     R0,TAPDGCON-TAPDD(R2)                                            
         ST    R0,TAPDGCON                                                      
         SPACE 1                                                                
         CLI   MYERROR,TAINWGCN                                                 
         BE    INV10                                                            
         SPACE 1                                                                
         L     R0,TAPDGUAR         UPDATE GUARANTEE CREDITS                     
         A     R0,TAPDGUAR-TAPDD(R2)                                            
         ST    R0,TAPDGUAR                                                      
         SPACE 1                                                                
         CLI   TAPDACDE,C' '       IF NO APPLIED CODE SET                       
         BH    *+8                                                              
         MVI   TAPDACDE,APPLGUAR   SET GUARANTEE CREDITS APPLIED                
         SPACE 1                                                                
*                                  IF PROC FIXED CYCLE PMT ON PRI COMML         
         TM    TAPDSTA2-TAPDD(R2),TAPDSPRI                                      
         BZ    INV6                                                             
         L     R0,TAPDGRS          ADD GUAR. CREDITS BACK TO GROSS              
         S     R0,TAPDGUAR-TAPDD(R2)                                            
         ST    R0,TAPDGRS                                                       
         B     INV10               DIDN'T DEDUCT FROM PAYMENT AMOUNT            
         SPACE 1                                                                
INV6     LA    R1,TAPDPAYI         DETERMINE WHICH PAY AMT TO DECREASE          
         CLI   TAPDW4TY-TAPDD(R2),TAW4TYIN                                      
         BE    *+16                                                             
         CLI   TAPDW4TY-TAPDD(R2),TAW4TYES                                      
         BE    *+8                                                              
         LA    R1,TAPDPAYC                                                      
         SPACE 1                                                                
         L     R0,0(R1)            UPDATE PAYMENT AMOUNT                        
         A     R0,TAPDGUAR-TAPDD(R2)                                            
         ST    R0,0(R1)                                                         
         SPACE 1                                                                
         L     R0,TAPDSPNH         UPDATE SUBJECT TO P&H                        
         S     R0,CHKSPNH                                                       
         ST    R0,TAPDSPNH                                                      
         SPACE 1                                                                
         L     R0,TAPDPNH          UPDATE P&H                                   
         S     R0,CHKPNH                                                        
         ST    R0,TAPDPNH                                                       
         SPACE 1                                                                
INV10    GOTO1 MYTRACE,DMCB,=C'GUAR INVOICE',USERP3                             
**NO-OP  B     XIT                                                              
         B     YES                                                              
         EJECT                                                                  
*              ROUTINE HANDLES RECORD TRACES                                    
         SPACE 1                                                                
MYTRACE  NTR1                                                                   
         TM    USERST,X'40'        TEST TRACE REQUESTED                         
         BZ    XIT                                                              
         LM    R2,R3,0(R1)         R2=A(LITERAL), R3=A(I/O AREA)                
         ZIC   R4,0(R1)            R4=L'LITERAL                                 
         GOTO1 TRACE,DMCB,(R3),0,(R2),(R4)                                      
         B     XIT                                                              
         SPACE 3                                                                
         GETEL (R3),DATADISP,ELCODE                                             
         SPACE 3                                                                
YES      XR    RC,RC                                                            
NO       LTR   RC,RC                                                            
XIT      XIT1                                                                   
         EJECT                                                                  
*              CONSTANTS, ETC.                                                  
         SPACE 2                                                                
HEXFFS   DC    6X'FF'                                                           
         SPACE 1                                                                
         LTORG                                                                  
         SPACE 2                                                                
         DS    0D                                                               
         DC    C'**GCONS*'                                                      
GCONCNT  DC    H'0'                N'GUARANTEE CONTRACTS IN BUFFER              
GCONTAB  DC    (NGCONS*GCONLNQ)X'00'                                            
NGCONS   EQU   200                                                              
         SPACE 2                                                                
         DS    0D                                                               
         DC    C'**GUARS*'                                                      
GUARCNT  DC    H'0'                N'GUARANTEES IN BUFFER                       
GUARTAB  DC    (NGUARS*GUARLNQ)X'00'                                            
NGUARS   EQU   2000                                                             
         EJECT                                                                  
*              DSECT TO COVER LOCAL WORKING STORAGE                             
         SPACE 1                                                                
WORKD    DSECT                                                                  
PL16     DS    PL16                WORK AREA                                    
USERPARM DS    A                   A(USER PARMS)                                
USERP1   DS    A                   USER PARMS                                   
USERP2   DS    A                                                                
USERP3   DS    A                                                                
USERIO   DS    A                   USER I/O AREA                                
USERST   DS    XL1                 USER STATUS BYTE                             
USERST2  DS    XL1                 USER STATUS BYTE 2                           
USERKEY  DS    CL(L'KEY)           USER KEY                                     
*                                                                               
MYWRITE  DS    CL1                 WRITE= VALUE                                 
MYERROR  DS    XL1                 RETURN ERROR CODE                            
MYSTAT   DS    XL1                 LOCAL STATUS                                 
MYSADDGC EQU   X'80'               ADDED TAGC EL FOR NON-PRI COMML              
MYSDUPCY EQU   X'40'               DUPLICATE CYCLE FOUND ON PRI COMML           
MYSGCUPA EQU   X'20'               GUARANTEE CONTRACT UPDATE ATTEMPTED          
*                                                                               
GUACODE  DS    CL(L'TLGUGUA)       UN-COMPLEMENTED GUARANTEE CODE               
GTRACRD  DS    F                   GUARANTEE CREDITS TAKEN                      
GTRABAL  DS    F                   NEW GUARANTEE BALANCE                        
*                                                                               
ATACAEL  DS    A                   A(CAST DETAILS ELEMENT)                      
ATAPDEL  DS    A                   A(PAYMENT DETAILS ELEMENT)                   
ATAGUEL  DS    A                   A(GUARANTEE DETAILS ELEMENT)                 
ATAGCEL  DS    A                   A(GUARANTEE CYCLE ELEMENT)                   
AGUARENT DS    A                   A(GUARANTEE TABLE ENTRY)                     
AGCONENT DS    A                   A(GUARANTEE CONTRACT TABLE ENTRY)            
*                                                                               
APRGCEL  DS    A                   A(PREV VALID GUAR CYCLE ELEMENT)             
*                                                                               
CHKSPNH  DS    F                   CHG TO SUBJECT TO P&H ON CHECK REC.          
CHKPNH   DS    F                   CHG TO P&H ON CHECK REC.                     
*                                                                               
SVCKAGY  DS    XL(L'TLCKAGY)       SAVED AGENCY                                 
SVCKINV  DS    XL(L'TLCKINV)       SAVED INVOICE NUMBER                         
*                                                                               
AGUARIO  DS    A                   A(GUARANTEE RECORD)                          
AGCONIO  DS    A                   A(GUARANTEE CONTRACT RECORD)                 
GUARIO   DS    CL4000              LOCAL I/O AREA FOR GUARS/TRACKING            
GCONIO   DS    CL4000              LOCAL I/O AREA FOR GRT CONTRACT              
*                                                                               
WORKLNQ  EQU   *-WORKD                                                          
         EJECT                                                                  
*              DSECT TO COVER GUARANTEE TABLE                                   
         SPACE 1                                                                
GUARD    DSECT                                                                  
GUARSKEY EQU   *                   START OF SORT KEY                            
GUARSSN  DS    F                   S/S NUMBER                                   
GUARGUA  DS    H                   GUARANTEE CODE                               
GUARDBAL EQU   X'80'               SET IN HOB OF CODE FOR DESC. BALANCE         
GUARSLNQ EQU   *-GUARSKEY          L'SORT KEY                                   
         SPACE 1                                                                
GUARTRK  DS    H                   LAST TRACKING NUMBER                         
GUARINV  DS    F                   CURRENT INVOICE TOTAL                        
         ORG   *-4                                                              
GUARAMT  DS    F                   PAYMENT AMOUNT IF NOT UPDATING GUAR.         
GUARBAL  DS    F                   BALANCE                                      
         SPACE 1                                                                
GUARPD   DS    0PL6                                                             
GUARSTRT DS    PL3                 PERIOD START DATE                            
GUAREND  DS    PL3                        END DATE                              
         SPACE 1                                                                
GUARLNQ  EQU   *-GUARD             L'TABLE ENTRY                                
GUARNEXT EQU   *                   A(NEXT ENTRY IN TABLE)                       
         SPACE 1                                                                
         ORG   *+GUARSKEY-GUARD                                                 
GUARNKEY DS    CL(GUARSLNQ)        KEY OF NEXT GUARANTEE REC IN TABLE           
         EJECT                                                                  
*              DSECT TO COVER GUARANTEE CONTRACT TABLE                          
         SPACE 1                                                                
GCOND    DSECT                                                                  
GCONSSN  DS    CL9                 S/S NUMBER                                   
GCONGCO  DS    XL4                 GUARANTEE CONTRACT CODE                      
GCONYRS  DS    XL3                 CONTRACT YEAR START DATE                     
GCONYRE  DS    XL3                 CONTRACT YEAR END DATE                       
GCONSEQ  DS    XL2                 LAST TRACKING NUMBER                         
GCONFBAL DS    XL4                 BALANCE ON FILE                              
GCONOBAL DS    XL4                 ORIGINAL BALANCE                             
GCONBAL  DS    XL4                 UPDATED BALANCE                              
GCONLNQ  EQU   *-GCOND                                                          
GCONNEXT EQU   *                   A(NEXT ENTRY IN TABLE)                       
         EJECT                                                                  
* TASYSVALD                                                                     
* TASYSWORKD                                                                    
* TASYSDSECT                                                                    
* TASYSEQUS                                                                     
* DDSPLWORKD                                                                    
* DDSPOOLD                                                                      
* TAGENFILE                                                                     
* DDGENTWA (TWAD)                                                               
         PRINT OFF                                                              
SYSCOMMD DSECT                                                                  
       ++INCLUDE TASYSVALD                                                      
SYSWORKD DSECT                                                                  
       ++INCLUDE TASYSWORKD                                                     
       ++INCLUDE TASYSDSECT                                                     
       ++INCLUDE TASYSEQUS                                                      
       ++INCLUDE DDSPLWORKD                                                     
       ++INCLUDE DDSPOOLD                                                       
       ++INCLUDE TAGENFILE                                                      
TWAD     DSECT                                                                  
         DS    CL64                                                             
CONHEADH EQU   *                                                                
       ++INCLUDE DDGENTWA                                                       
         PRINT ON                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'065TAPPGUAR  01/25/17'                                      
         END                                                                    
