*          DATA SET EZMMPCDSB  AT LEVEL 022 AS OF 06/30/99                      
*CATALP EZMMPCDA                                                                
*        TITLE 'EZMMPCDS - FIND DDS CLT/PRD/EST CODES'                          
         TITLE 'EZMMPCDS - FIND DDS CLT/PRD/EST CODES'                          
***********************************************************************         
*                                                                     *         
*        FIND DDS CLT/PRD/EST CODES FROM MMPLUS DATA                  *         
*                                                                     *         
*NTRY    PARM 1   A(MMPLUS ESTIMATE NAME FIELD)                       *         
*        PARM 2   A(OUTPUT AREA)                                      *         
*        PARM 3   A(DATAMGR)                                          *         
*                                                                     *         
*EXIT    OUTAREA FILLED IN                                            *         
*              CL3 - CLIENT CODE                                      *         
*              CL3 - PRODUCT CODE                                     *         
*              ZL3 - ESTIMATE NUMBER                                  *         
*                                                                     *         
***********************************************************************         
         SPACE 2                                                                
         TITLE 'EZMMPCDS - FIND DDS CLT/PRD/EST CODES - INIT'                   
***********************************************************************         
*                                                                     *         
*        INITIALIZATION                                               *         
*                                                                     *         
***********************************************************************         
         SPACE 2                                                                
EZMMPCDS CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 WORKL,EZMMPCDS,RR=RE                                             
*                                                                               
         USING WORKD,RC            ESTABLISH WORKING STORAGE                    
*                                                                               
         ST    RE,RELO             SAVE RELOCATION FACTOR                       
*                                                                               
         ST    R1,APARMS           SAVE A(PARM LIST)                            
*                                                                               
         L     R2,0(R1)            POINT TO INPUT AREA                          
         MVC   INPUT(INPUTL),0(R2) COPY INPUT                                   
*                                                                               
         MVC   VDATAMGR,8(R1)      SAVE V(DATAMGR)                              
*                                                                               
         XC    OUTPUT(OUTPUTL),OUTPUT  INIT RETURNED DATA                       
*                                                                               
         MVC   OUTCLT,=CL3'CC '    RETURN CLIENT CODE                           
*                                                                               
         GOTO1 VCLPACK,DMCB,OUTCLT,CLTPACK PACK CLIENT CODE                     
*                                                                               
         TITLE 'EZMMPCDS - FIND DDS CLT/PRD/EST CODES - PRD'                    
***********************************************************************         
*                                                                     *         
*        FIND PRODUCT CODE FROM TABLE                                 *         
*                                                                     *         
***********************************************************************         
         SPACE 2                                                                
PRD      DS    0H                                                               
*                                                                               
         LM    R3,R5,PRTBBXLE      LOAD BXLE REGS FOR PRODUCT TABLE             
         A     R3,RELO             RE-LOCATE ADDRESSES                          
         A     R5,RELO             RE-LOCATE ADDRESSES                          
*                                                                               
PRDLOOP  DS    0H                                                               
*                                                                               
         USING PRTBD,R3            ESTABLISH PRODUCT TABLE ENTRY                
*                                                                               
         CLC   INPCDM,PRTBPCDM     MATCH TO MMPLUS PRODUCT CODE                 
         BE    PRDFD                                                            
*                                                                               
PRDCONT  DS    0H                                                               
*                                                                               
         BXLE  R3,R4,PRDLOOP                                                    
         B     PRDX                NO MATCH                                     
*                                                                               
PRDFD    DS    0H                                                               
*                                                                               
         MVC   OUTPRD,PRTBPRDD     RETURN DDS PRODUCT CODE                      
*                                                                               
PRDX     DS    0H                                                               
*                                                                               
         TITLE 'EZMMPCDS - FIND DDS CLT/PRD/EST CODES - EST'                    
***********************************************************************         
*                                                                     *         
*        FIND ESTIMATE CODE                                           *         
*                                                                     *         
***********************************************************************         
         SPACE 2                                                                
EST      DS    0H                                                               
*                                                                               
         TITLE 'EZMMPCDS - FIND DDS CLT/PRD/EST CODES - ESTINIT'                
***********************************************************************         
*                                                                     *         
*        BUILD ESTIMATE TRANLATION TABLE ON FIRST TIME                *         
*                                                                     *         
***********************************************************************         
         SPACE 2                                                                
ESTINIT  DS    0H                                                               
*                                                                               
         OC    ESTBBFA,ESTBBFA     SKIP IF NOT FIRST TIME                       
         BNZ   ESTINITX                                                         
*                                                                               
         ICM   R3,15,*+8           GET SIZE OF GETMAIN AREA                     
         B     *+8                                                              
         DC    AL4(100000)               100K BUFFER                            
*                                                                               
         LA    R4,ESTBBFA          BUFFER ADDRESS SAVEAREA                      
*                                                                               
         GETMAIN EC,LV=(R3),A=(R4)  GET CORE                                    
*                                                                               
         ST    R3,ESTBBFL          SAVE RETURNED BUFFER LENGTH                  
*                                                                               
         LTR   RF,RF                                                            
         BZ    *+6                 CHECK FOR ERRORS                             
         DC    H'0'                                                             
*                                                                               
         XC    BSPPRMS(BSPPRML),BSPPRMS INIT BINSRCH PARAMETERS                 
*                                                                               
         MVC   BSPATAB,ESTBBFA     A(START OF TABLE)                            
*                                                                               
         LA    RF,ESTBENTL         SET ENTRY LENGTH                             
         ST    RF,BSPLENR                                                       
*                                                                               
         LA    RF,ESTBKEYL         SET KEY LENGTH                               
         ST    RF,BSPLENK                                                       
*                                                                               
         ICM   RF,15,=AL4(ESTBMAXQ)   SET # OF AVAILABLE ENTRIES TO MAX         
         ST    RF,BSPMAX                                                        
*                                                                               
*        LOAD TABLE                                                             
*                                                                               
         XC    KEY,KEY                                                          
         LA    R2,KEY                                                           
         USING ESTHDRD,R2          ESTABLISH ESTIMATE KEY                       
*                                                                               
         MVI   EKEYAM,X'B1'        SET AGENCY/MEDIA                             
         MVC   EKEYCLT,CLTPACK     SET CLIENT                                   
         MVC   EKEYPRD,=C'POL'     SET PRODUCT CODE                             
*                                                                               
         MVC   KEYSAVE,KEY         SAVE STARTING KEY                            
*                                                                               
         GOTO1 VDATAMGR,DMCB,=C'DMRDHI',=C'SPTDIR',KEY,KEY READ 1ST             
*                                                                               
ESTINILP DS    0H                                                               
*                                                                               
         LA    R2,KEY              POINT TO KEY                                 
*                                                                               
         CLC   KEY(EKEYEST-EKEY),KEYSAVE DONE ON CHANGE IN PRODUCT              
         BNE   ESTINIDN                                                         
*                                                                               
         CLI   EKEYEST,0           SKIP IF NOT AN ESTIMATE RECORD               
         BE    ESTINICN                                                         
*                                                                               
         CLI   EKEYEST+1,0         SKIP IF NOT AN ESTIMATE HEADER               
         BNE   ESTINICN                                                         
*                                                                               
         GOTO1 VDATAMGR,DMCB,=C'GETREC',=C'SPTFILE',EKEY+14,IOA1,DMWORK         
*                                                                               
         CLI   DMCB+8,0            NO ERRORS TOLERATED                          
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         LA    R2,IOA1             RESET POINTER TO ESTIMATE RECORD             
*                                                                               
         LA    R3,ESTBENTC         POINT TO TABLE ENTRY BUILD AREA              
         USING ESTBENTD,R3         ESTABLISH TABLE ENTRY                        
*                                                                               
         MVC   ESTBCAT,EUSER1      SET ESTIMATE CATEGORY                        
*                                                                               
         CLI   ESTBCAT+1,C' '      IF SINGLE DIGIT                              
         BH    *+14                                                             
         MVC   ESTBCAT+1(1),ESTBCAT    ADD LEADING ZERO                         
         MVI   ESTBCAT,C'0'                                                     
*                                                                               
         GOTO1 VGETBRD,DMCB,ESTART,DTWORK GET BROADCAST MONTH                   
*                                                                               
*        END DATE AT DTWORK+6 IS IN CALENDAR MONTH                              
*                                                                               
         MVC   ESTBYRST,DTWORK+7   LAST DIGIT OF YEAR                           
*                                                                               
         CLC   =C'03',DTWORK+8     FIND QUARTER OF START DATE                   
         BL    *+12                                                             
         MVI   ESTBQSTR,C'1'       FIRST QUARTER                                
         B     ESTINQSX                                                         
*                                                                               
         CLC   =C'06',DTWORK+8                                                  
         BL    *+12                                                             
         MVI   ESTBQSTR,C'2'       SECOND QUARTER                               
         B     ESTINQSX                                                         
*                                                                               
         CLC   =C'09',DTWORK+8                                                  
         BL    *+12                                                             
         MVI   ESTBQSTR,C'3'       THIRD QUARTER                                
         B     ESTINQSX                                                         
*                                                                               
         CLC   =C'12',DTWORK+8                                                  
         BL    *+12                                                             
         MVI   ESTBQSTR,C'4'       FOURTH QUARTER                               
         B     ESTINQSX                                                         
*                                                                               
ESTINQSX DS    0H                                                               
*                                                                               
         GOTO1 VGETBRD,DMCB,EEND,DTWORK GET BROADCAST MONTH                     
*                                                                               
*        END DATE AT DTWORK+6 IS IN CALENDAR MONTH                              
*                                                                               
         MVC   ESTBYREN,DTWORK+7   LAST DIGIT OF YEAR                           
*                                                                               
         CLC   =C'03',DTWORK+8     FIND QUARTER OF START DATE                   
         BL    *+12                                                             
         MVI   ESTBQEND,C'1'       FIRST QUARTER                                
         B     ESTINQEX                                                         
*                                                                               
         CLC   =C'06',DTWORK+8                                                  
         BL    *+12                                                             
         MVI   ESTBQEND,C'2'       SECOND QUARTER                               
         B     ESTINQEX                                                         
*                                                                               
         CLC   =C'09',DTWORK+8                                                  
         BL    *+12                                                             
         MVI   ESTBQEND,C'3'       THIRD QUARTER                                
         B     ESTINQEX                                                         
*                                                                               
         CLC   =C'12',DTWORK+8                                                  
         BL    *+12                                                             
         MVI   ESTBQEND,C'4'       FOURTH QUARTER                               
         B     ESTINQEX                                                         
*                                                                               
ESTINQEX DS    0H                                                               
*                                                                               
         MVC   ESTBEST,EKEYEST     SET ESTIMATE NUMBER                          
*                                                                               
ESTADDLP DS    0H                                                               
*                                                                               
         GOTO1 VBINSRCH,BSPPRMS,('BSPADD',ESTBENTD)   ADD TO TABLE              
*                                                                               
         OC    BSPAREC,BSPAREC     DIE IF NO ROOM                               
         BNZ   *+6                                                              
         DC    H'0'                                                             
*                                                                               
         CLI   BSPCNTL,BSPNF       DONE IF ENTRY NOT FOUND                      
         B     *+6                 NO DUPLICATES ALLOWED                        
         DC    H'0'                                                             
*                                                                               
ESTADDCN DS    0H                                                               
*                                                                               
         CLC   ESTBYRST,ESTBYREN   DONE IF ESTIMATE FOR ONLY 1 QTR              
         BNE   *+10                                                             
         CLC   ESTBQSTR,ESTBQEND   DONE IF ESTIMATE FOR ONLY ONE QTR            
         BE    ESTADDDN                                                         
*                                                                               
         PACK  DUB,ESTBQSTR        BUMP START QUARTER                           
         CVB   RF,DUB                                                           
         AHI   RF,1                                                             
*                                                                               
         CHI   RF,4                MAX 4 QUARTERS                               
         BNH   ESTADDC1                                                         
*                                                                               
         LA    RF,1                RESET TO FIRST QUARTER                       
         PACK  DUB,ESTBYRST        BUMP LAST DIGIT OF START YEAR                
         CVB   RE,DUB                                                           
         AHI   RE,1                                                             
*                                                                               
         CHI   RE,9                MAX DIGIT IS 9                               
         BNH   *+8                                                              
         LA    RE,0                RESET TO 0                                   
*                                                                               
         CVD   RE,DUB                                                           
         OI    DUB+7,X'0F'         FORCE SIGN                                   
         UNPK  ESTBYRST,DUB                                                     
*                                                                               
ESTADDC1 DS    0H                                                               
*                                                                               
         CVD   RF,DUB                                                           
         OI    DUB+7,X'0F'         FORCE SIGN                                   
         UNPK  ESTBQSTR,DUB                                                     
*                                                                               
         B     ESTADDLP                                                         
*                                                                               
ESTADDDN DS    0H                                                               
*                                                                               
ESTINICN DS    0H                                                               
*                                                                               
         GOTO1 VDATAMGR,DMCB,=C'DMRSEQ',=C'SPTDIR',KEY,KEY READ NEXT            
         B     ESTINILP                                                         
*                                                                               
ESTINIDN DS    0H                                                               
*                                                                               
ESTINITX DS    0H                                                               
*                                                                               
         TITLE 'EZMMPCDS - FIND DDS CLT/PRD/EST CODES - ESTFIND'                
***********************************************************************         
*                                                                     *         
*        FIND TABLE ENTRY FOR INPUT                                   *         
*                                                                     *         
***********************************************************************         
         SPACE 2                                                                
ESTFIND  DS    0H                                                               
*                                                                               
*                                                                               
         LA    R3,ESTBENTC         POINT TO TABLE ENTRY BUILD AREA              
         USING ESTBENTD,R3         ESTABLISH TABLE ENTRY                        
*                                                                               
         MVC   ESTBYRST,INEYRM     SET LAST DIGIT OF YEAR                       
         MVC   ESTBCAT,INECTM      SET ESTIMATE CATEGORY                        
         MVC   ESTBQSTR,INEQTM     SET QUARTER                                  
*                                                                               
         GOTO1 VBINSRCH,BSPPRMS,('BSPFIND',ESTBENTD)   FIND IN TABLE            
*                                                                               
         CLI   BSPCNTL,BSPNF       DONE IF ENTRY NOT FOUND                      
         BE    ESTFINDX                                                         
*                                                                               
         L     R3,BSPAREC          POINT TO FOUND TABLE ENTRY                   
*                                                                               
         MVC   FULL,=X'40202120'   EDIT PATTERN                                 
         SR    RF,RF                                                            
         IC    RF,ESTBEST          ESTIMATE NUMBER                              
         CVD   RF,DUB              CVD                                          
         ED    FULL,DUB+6          PRINT ESITMATE NUMBER                        
*                                                                               
         MVC   OUTEST,FULL+1       MOVE TO OUTPUT AREA                          
*                                                                               
ESTFINDX DS    0H                                                               
         B     MMPCDSX                                                          
*                                                                               
                                                                                
         TITLE 'EZMMPCDS - FIND DDS CLT/PRD/EST CODES-XIT'                      
***********************************************************************         
*                                                                     *         
*        EXIT ROUTINES                                                *         
*                                                                     *         
***********************************************************************         
         SPACE 2                                                                
MMPCDSX  DS    0H                                                               
*                                                                               
         L     R3,APARMS           POINT TO INPUT PARAMETERS                    
         L     R3,4(R3)            POINT TO OUTPUT AREA                         
         MVC   0(OUTPUTL,R3),OUTPUT   RETURN OUTPUT                             
*                                                                               
         XIT1                                                                   
*                                                                               
         TITLE 'EZMMPCDS - FIND DDS CLT/PRD/EST CODES-LTORG'                    
***********************************************************************         
*                                                                     *         
*        CONSTANTS                                                    *         
*                                                                     *         
***********************************************************************         
         SPACE 2                                                                
         LTORG                                                                  
ZEROS    DC    16C'0'                                                           
SPACES   DC    CL256' '                                                         
REGSAVE  DS    18F                 REGISTER SAVEAREA                            
*                                                                               
ESTBBFA  DC    A(0)                A(ESTIMATE BUFFER)                           
ESTBBFL  DC    F'0'                ESTIMATE BUFFER LENGTH                       
*                                                                               
*        EXTERNAL ADDRESSES                                                     
*                                                                               
VDATAMGR DS    V                   V(DATAMGR)                                   
VGETBRD  DC    V(GETBROAD)         V(GETBROAD)                                  
VGETDAY  DC    V(GETDAY)           V(GETDAY)                                    
VADDAY   DC    V(ADDAY)            V(ADDAY)                                     
VBINSRCH DC    V(BINSRCH)          V(BINSRCH2)                                  
VCLPACK  DC    V(CLPACK)           V(CLPACK)                                    
*                                                                               
       ++INCLUDE DDBSRPRMD                                                      
*                                                                               
         TITLE 'EZMMPCDS - FIND DDS CLT/PRD/EST CODES-PRTB'                     
***********************************************************************         
*                                                                     *         
*        PRODUCT TRANSLATION TABLE                                    *         
*                                                                     *         
***********************************************************************         
         SPACE 2                                                                
PRTBBXLE DS    0A                  BXLE REGISTERS FOR TABLE                     
PRTBSTRT DC    A(PRTB)             A(START OF TABLE)                            
PRTBLEN  DC    AL4(PRTBL)          TABLE ENTRY  LENGTH                          
PRTBEND  DC    A(PRTBX)            A(END OF TABLE)                              
*                                                                               
PRTB     DS    0D                  PRODUCT TRANSLATION TABLE                    
         DC    C'01',C'CL '        COCA-COLA CLASSIC                            
         DC    C'02',C'FR '        FRESCA                                       
         DC    C'04',C'SP '        SPRITE                                       
         DC    C'05',C'PB '        MR. PIBB                                     
         DC    C'06',C'MY '        MELLO YELLO                                  
         DC    C'07',C'FN '        FANTA                                        
         DC    C'11',C'MM '        MINUTE MAID                                  
         DC    C'12',C'DC '        DIET COKE                                    
         DC    C'13',C'CY '        CHERRY COKE                                  
         DC    C'14',C'DS '        DIET SPRITE                                  
         DC    C'15',C'PA '        POWERADE                                     
         DC    C'18',C'CD '                                                     
         DC    C'20',C'BQ '                                                     
         DC    C'22',C'SG '        PROJECT VANILLA                              
         DC    C'23',C'CT '        PROJECT KINGFISHER                           
         DC    C'34',C'PR '        PRESENCE MARKETING                           
         DC    C'58',C'MJ '        MINUTE MAID JUICE TO GO                      
         DC    C'59',C'FT '        FRIUTOPIA                                    
         DC    C'70',C'NT '        NESTEA ICED TEA                              
         DC    C'71',C'NC '        NESTEA COOL                                  
         DC    C'96',C'CO '        OLYMPIC CITY                                 
*                                                                               
PRTBX    EQU   *-1                 END OF TABLE                                 
*                                                                               
         TITLE 'EZMMPCDS - FIND DDS CLT/PRD/EST CODES-PRTBD'                    
***********************************************************************         
*                                                                     *         
*        PRODUCT TRANSLATION TABLE DSECT                              *         
*                                                                     *         
***********************************************************************         
         SPACE 2                                                                
PRTBD    DSECT                     PRODUCT TRANSLATION TABLE                    
PRTBPCDM DS    ZL2                 MMPLUS PRODUCT CODE                          
PRTBPRDD DS    CL3                 DDS PRODUCT CODE                             
PRTBL    EQU   *-PRTBD             TABLE ENTRY LENGTH                           
*                                                                               
         TITLE 'EZMMPCDS - FIND DDS CLT/PRD/EST CODES-ESTBD'                    
***********************************************************************         
*                                                                     *         
*        ESTIMATE TRANSLATION TABLE ENTRY DSECT                       *         
*                                                                     *         
***********************************************************************         
         SPACE 2                                                                
ESTBENTD DSECT                     ESTIMATE TRANSLATION TABLE                   
*                                                                               
ESTBKEY  DS    0C                  START OF ENTRY KEY                           
ESTBYRST DS    CL1                 LAST DIGIT OF START YEAR                     
ESTBCAT  DS    CL2                 CATEGORY                                     
ESTBQSTR DS    CL1                 START QUARTER                                
ESTBKEYL EQU   *-ESTBKEY           KEY LENGTH                                   
*                                                                               
ESTBYREN DS    CL1                 LAST DIGIT OF END   YEAR                     
ESTBQEND DS    CL1                 END   QUARTER                                
*                                                                               
ESTBEST  DS    AL1                 ESTIMATE NUMBER                              
*                                                                               
ESTBENTL EQU   *-ESTBENTD          TABLE ENTRY LENGTH                           
*                                                                               
ESTBMAXQ EQU   1000                MAX ENTRIES IN TABLE                         
*                                                                               
         TITLE 'EZMMPCDS - FIND DDS CLT/PRD/EST CODES-WORKD'                    
***********************************************************************         
*                                                                     *         
*        WORKING STORAGE                                              *         
*                                                                     *         
***********************************************************************         
         SPACE 2                                                                
WORKD    DSECT                     WORKING STORAGE                              
DMCB     DS    6F                  PARAMETERS AREA                              
DUB      DS    D                                                                
DUB2     DS    D                                                                
FULL     DS    F                                                                
HALF     DS    H                                                                
HALF2    DS    H                                                                
RELO     DS    A                   RELOCATION FACTOR                            
*                                                                               
WORK     DS    CL32                WORKAREA                                     
*                                                                               
APARMS   DS    A                   A(PARAMETER LIST)                            
SAVESTR  DS    H                   STARTING ESTIMATE NUMBER                     
*                                                                               
CLT      DS    CL3                 CLIENT CODE                                  
CLTPACK  DS    XL2                 CLIENT CODE PACKED                           
*                                                                               
         DS    0D                  ALIGNMENT                                    
KEY      DS    XL32                KEYAREA                                      
KEYSAVE  DS    XL32                STARTIN KEY SAVEAREA                         
*                                                                               
DTWORK   DS    CL24                DATE WORK AREA                               
DMWORK   DS    32D                 DATAMGR WORKAREA                             
*                                                                               
ESTBENTC DS    XL(ESTBENTL)        ESTIMATE TABLE BUILD AREA                    
*                                                                               
ESTBPRMS DS    6F                  BINSRCH PARAMETERS                           
*                                                                               
BAGYMD   DS    XL1                 AGENCY/MEDIA PASSED IN PARAMETERS            
*                                                                               
INPUT    DS    0X                  INPUT AREA                                   
INPCDM   DS    CL2                 PRODUCT CODE                                 
         DS    C'/'                                                             
INEYRM   DS    ZL1                 LAST DIGIT OF YEAR                           
INECTM   DS    ZL2                 ESTIMATE CATEGORY                            
INEQTM   DS    ZL1                 QUARTER                                      
         DS    C'/'                                                             
         DS    CL4                 SPARE                                        
INPUTL   EQU   *-INPUT             INPUT AREA LENGTH                            
*                                                                               
OUTPUT   DS    0X                  OUTPUT                                       
OUTCLT   DS    CL3                 DDS CLIENT  CODE                             
OUTPRD   DS    CL3                 DDS PRODUCT CODE                             
OUTEST   DS    ZL3                 DDS ESTIMATE CODE                            
OUTPUTL  EQU   *-OUTPUT            OUTPUT AREA LENGTH                           
*                                                                               
         DS    0D                  ALIGNMENT                                    
IOA1     DS    XL4096              IOAREA                                       
*                                                                               
WORKL    EQU   *-WORKD             WORKING STORAGE LENGTH                       
*                                                                               
         TITLE 'EZMMPCDS - FIND DDS CLT/PRD/EST CODES-ESTHDRD'                  
***********************************************************************         
*                                                                     *         
*        ESTIMATE HEADER RECORD                                       *         
*                                                                     *         
***********************************************************************         
         SPACE 2                                                                
ESTHDRD  DSECT                     WORKING STORAGE                              
       ++INCLUDE SPGENEST                                                       
*                                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'022EZMMPCDSB 06/30/99'                                      
         END                                                                    
